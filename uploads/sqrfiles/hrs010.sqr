!***********************************************************************
!  HRS010: Vacant Budgeted Position Report                           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/08/17:19:51:13                                 !
!       $Release:  HR9                                                 !
!      $Revision:  104                                                 !
!                                                                      *
!***********************************************************************
! HRS010 - Vacant budgeted Position Report                          *
!***********************************************************************
! 6/14/01
!  > Changed JOB_REQ_STATUS field to STATUS_CODE and created another
!     procudure to look up descr from PS_ER_STS_REQ_VW
!  > Changed a few prints statements to make more room for longer status_code descrs
!************************************************************************

#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

begin-Report
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Init-Report
   !do Process-Main
   do Reset
   do Stdapi-Term
end-Report

!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from HRS010 program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation ('HR')

  do Get_Field_Information ('HRS010', 'REPORT_TITLE',   $HRS010-REPORTTITLE,    #CW)
  do Get_Field_Information ('HRS010', 'FPC',            $HRS010-FPC,            #CW)
  do Get_Field_Information ('HR',      'POSITION_STAT1', $POSITION_STAT1,         #CW)
  do Get_Field_Information ('HRS010', 'RTUAF',          $HRS010-RTUAF,          #CW)
  do Get_Field_Information ('HRS010', 'REQN',           $HRS010-REQN,           #CW)
  do Get_Field_Information ('HRS010', 'DEPT',           $HRS010-DEPT,           #CW)
  do Get_Field_Information ('HR',      'JOBCODE1',       $JOBCODE1,               #CW)
  do Get_Field_Information ('HRS010', 'EMLRD',          $HRS010-EMLRD,          #CW)
  do Get_Field_Information ('HR',      'STATUS',         $STATUS,                 #CW)
  do Get_Field_Information ('HRS010', 'VACANCY',        $HRS010-VACANCY,        #CW)
  do Get_Field_Information ('HR',      'RECRUIT',        $RECRUIT,                #CW)
  do Get_Field_Information ('HRS010', 'ID',             $HRS010-ID,             #CW)
  do Get_Field_Information ('HR',      'JOBCODE2',       $JOBCODE2,               #CW)
  do Get_Field_Information ('HR',      'POSITION_NBR2',  $POSITION_NBR2,          #CW)
  do Get_Field_Information ('HR',      'POSITION_TITLE', $POSITION_TITLE,         #CW)
  do Get_Field_Information ('HRS010', 'GPLTL',          $HRS010-GPLTL,          #CW)
  do Get_Field_Information ('HR',      'EFFDT2',         $EFFDT2,                 #CW)
  do Get_Field_Information ('HRS010', 'ANN',            $HRS010-ANN,            #CW)
  do Get_Field_Information ('HR',      'RECRUIT2',       $RECRUIT2,               #CW)
  do Get_Field_Information ('HRS010', 'CLOSES',         $HRS010-CLOSES,         #CW)

End-Procedure


begin-heading 8
#Include 'stdhdgtr.sqc'
   print $HRS010-FPC      (+1,61)
   print $HRS010-RTUAF    (+1,61)
   print $POSITION_STAT1   (0,81)
   print $HRS010-REQN     (0,100)

   print $HRS010-DEPT     (+1,1)
   print $JOBCODE1         (0,13)
   print $POSITION_STAT1   (0,21)
   print $HRS010-EMLRD    (0,61)

   print $POSITION_STAT1   (0,72)
   print $POSITION_STAT2   (0,81)
   print $HRS010_VACANCY  (0,92)
   print $POSITION_STAT2   (0,100)
   print $HRS010-VACANCY  (0,111)
   print $RECRUIT1         (0,120)
   print $RECRUIT1         (0,135)

   print $HRS010-ID       (+1,1)
   print $JOBCODE2         (0,13)
   print $POSITION_NBR2    (0,21)
   print $POSITION_TITLE   (0,30)
   print $HRS010-GPLTL    (0,61)
   print $STATUS           (0,72)
   print $EFFDT2           (0,81)
   print $HRS010-ANN      (0,93)
   print $EFFDT2           (0,100)
   print $STATUS           (0,112)
   print $RECRUIT2         (0,124)
   print $HRS010-CLOSES   (0,135)

end-heading

!********************************************
begin-procedure Init-Report

  do Stdapi-Init

  let $ReportID = 'HRS010'
  do Get-Report-Language
  let $ReportTitle = $HRS010-REPORTTITLE
  display $ReportTitle

  if $prcs_process_instance = ''
     do Ask-Vacant-Pos-Request
  else
     do Select-Parameters
     do process-main
  end-if
end-procedure



!***************************************************
!   PROC Process-Main                              *
!               Main Procedure                     *
!***************************************************
begin-procedure Process-Main
begin-SELECT
A.DEPTID                 (+1,1)  on-break level=1 skiplines=1
A.JOBCODE                (0,13)
A.POSITION_NBR           (0,21)
  move &A.Position_Nbr to $Position_Nbr
  do Get-Position-Title
  print $PosName         (0,30)

A.REG_TEMP               (0,61)
A.FULL_PART_TIME         (0,65)
A.DESCRLONG
A.CONFIDENTIAL_POSN
   if &A.Confidential_Posn = 'Y'
     print 'Y'           (0,69)
   end-if
A.POSN_STATUS
  move 'POSN_STATUS' to $FieldName
  move &A.Posn_Status  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName   (0,72)
A.STATUS_DT
   if RTRIM(&A.Status_Dt, ' ') <> ''
     do Format-DateTime(&A.Status_Dt, $out, {DEFDATE}, '', '')
     print $out          (0,81)
   end-if

  do Get-Requisition

FROM  PS_POSN_VACANT A
ORDER BY A.DEPTID,
         A.JOBCODE,
         A.POSITION_NBR
end-SELECT
end-procedure

begin-procedure Get-Requisition
begin-SELECT

J.HRS_JOB_OPENING_ID     (0,92)   on-break skiplines=1
J2.HRS_JO_PST_SEQ
J.STATUS_CODE
   DO GET-STATUS-CODE-DESCR
   if $Curr_Language_Cd <> $PSOptions_Language_Cd
     do GetJOSTSLng
   end-if
   print $status-code-defn (0,112)

J.STATUS_DT
   do Format-DateTime(&J.Status_Dt, $out, {DEFDATE}, '', '')
   print $out          (0,100)

  do Get-OpenDt
J2.GVT_DT_LAST_SUBMIT



  if RTRIM(&J2.GVT_DT_LAST_SUBMIT, ' ') <> ''
      do Format-DateTime(&J2.GVT_DT_LAST_SUBMIT, $out, {DEFDATE}, '', '')
      print $out  (0,135)
  end-if

FROM PS_HRS_JO_I J,
     PS_HRS_JO_POSN_I J1,
     PS_HRS_JO_POSTING J2
WHERE J1.POSITION_NBR = &A.Position_Nbr
  AND J1.HRS_JOB_OPENING_ID = J.HRS_JOB_OPENING_ID
  AND J2.HRS_JOB_OPENING_ID = J1.HRS_JOB_OPENING_ID
end-SELECT

end-procedure

BEGIN-PROCEDURE Get-OpenDt

BEGIN-SELECT
MIN(DST.HRS_JO_PST_OPN_DT)     &DST.HRS_JO_PST_OPN_DT
  if RTRIM(&DST.HRS_JO_PST_OPN_DT, ' ') <> ''
      do Format-DateTime(&DST.HRS_JO_PST_OPN_DT, $out, {DEFDATE}, '', '')
      print $out  (0,124)
  end-if

FROM PS_HRS_JO_PST_DST  DST
WHERE DST.HRS_JOB_OPENING_ID = &J.HRS_JOB_OPENING_ID
AND DST.HRS_JO_PST_SEQ = &J2.HRS_JO_PST_SEQ
END-SELECT

END-PROCEDURE


BEGIN-PROCEDURE GET-STATUS-CODE-DESCR

LET $status-code-defn = ''

BEGIN-SELECT
REQ_VW.DESCRSHORT
   let $status-code-defn = &REQ_VW.DESCRSHORT
FROM  PS_HRS_STS_JO_I REQ_VW
WHERE STATUS_AREA = '1'
AND STATUS_CODE = &J.STATUS_CODE
END-SELECT


END-PROCEDURE

!***********************************************************************
!  PROC GetJOSTSLng
!***********************************************************************

BEGIN-PROCEDURE GetJOSTSLng

BEGIN-SELECT

C.DESCRSHORT   
  move &C.DESCRSHORT to $status-code-defn

FROM PS_HRS_STS_JO_LVW C
WHERE C.STATUS_CODE = &J.STATUS_CODE

END-SELECT

END-PROCEDURE



!**********************************************************************

#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines

#Include 'getposnm.sqc'  !Get-Department-Name procedure
#Include 'askvacnt.sqc'  !Get Vacant Report Request
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Format date and time
#Include 'number.sqc'    !Routines to format numbers
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Routines to Update Run Status

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
