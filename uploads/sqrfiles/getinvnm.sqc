!************************************************************
!  GetInvNm:  Get Name of Savings Investment Option
!************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:09:08:09                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!***********************************
begin-procedure Get-Svgs-Invt-Name
!***********************************

  !Keys passed in:  $Plan_Type, $Benefit_Plan, $Option_ID, $AsOfDate
  !Values returned: $SvgsInvtDescr, $SvgsInvtDescrAbbrv

  move '' to  $SvgsInvtDescr
  move '' to  $SvgsInvtDescrAbbrv

  if Rtrim($Plan_Type,' ') = '' or
     Rtrim($Benefit_Plan,' ') = '' or
     Rtrim($Option_ID,' ') = ''
    goto GetInvNmExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
SVG.DESCR
SVG.DESCRSHORT

  let $SvgsInvtDescr      = Rtrim(&SVG.DESCR,' ')
  let $SvgsInvtDescrAbbrv = Rtrim(&SVG.DESCRSHORT,' ')

FROM  PS_SAVINGS_INV_TBL SVG
WHERE SVG.PLAN_TYPE      = $Plan_Type
  AND SVG.BENEFIT_PLAN   = $Benefit_Plan
  AND SVG.INVESTMENT_OPT = $Option_ID
  AND SVG.EFFDT         <= $AsOfDate
  AND SVG.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_SAVINGS_INV_TBL
         WHERE PLAN_TYPE      = SVG.PLAN_TYPE
           AND BENEFIT_PLAN   = SVG.BENEFIT_PLAN
           AND INVESTMENT_OPT = SVG.INVESTMENT_OPT
           AND EFFDT         <= $AsOfDate)
end-SELECT

  do Get-Svgs-Invt-Name-Lang

GetInvNmExit:

end-procedure

!****************************************
begin-procedure Get-Svgs-Invt-Name-Lang
!****************************************

  !Keys passed in:  $Plan_Type, $Benefit_Plan, $Option_ID, $AsOfDate
  !Values returned: $SvgsInvtDescr, $SvgsInvtDescrAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetInvNmLngExit
  end-if

  if Rtrim($Plan_Type,' ') = '' or
     Rtrim($Benefit_Plan,' ') = '' or
     Rtrim($Option_ID,' ') = ''
    goto GetInvNmLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
SVGL.DESCR
SVGL.DESCRSHORT

  if Rtrim(&SVGL.DESCR,' ') <> ''
    let $SvgsInvtDescr = Rtrim(&SVGL.DESCR,' ')
  end-if
  if Rtrim(&SVGL.DESCRSHORT,' ') <> ''
    let $SvgsInvtDescrAbbrv = Rtrim(&SVGL.DESCRSHORT,' ')
  end-if

FROM  PS_SAVNGS_INV_LANG SVGL
WHERE SVGL.PLAN_TYPE      = $Plan_Type
  AND SVGL.BENEFIT_PLAN   = $Benefit_Plan
  AND SVGL.INVESTMENT_OPT = $Option_ID
  AND SVGL.LANGUAGE_CD    = $Curr_Language_Cd
  AND SVGL.EFFDT         <= $AsOfDate
  AND SVGL.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_SAVNGS_INV_LANG
         WHERE PLAN_TYPE      = SVGL.PLAN_TYPE
           AND BENEFIT_PLAN   = SVGL.BENEFIT_PLAN
           AND INVESTMENT_OPT = SVGL.INVESTMENT_OPT
           AND LANGUAGE_CD    = SVGL.LANGUAGE_CD
           AND EFFDT         <= $AsOfDate)
end-SELECT

GetInvNmLngExit:

end-procedure

!***************************** End of Include File **************************
