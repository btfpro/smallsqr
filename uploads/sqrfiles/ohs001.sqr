!********************************************************
!  OHS001:  Occupational Injury/Illness Log - OSHA 200  *
!********************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:07:10                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-heading 23
  move #current-line to #LastLine
  if $EstabID <> $priorEstabID
    do Get-Header-Data
    move $EstabID to $priorEstabID
  end-if
  #Include 'stdhdg02.sqc'
  move 6 to #i
  move 0 to #start_print1
  move 0 to #start_print2
  move 0 to #start_print3

  while #i <= #LastLine - 7
    print '|' (#i,1)
    print '|' (#i,7)
    print '|' (#i,18)
    print '|' (#i,31)
    print '|' (#i,43)
    print '|' (#i,73)
    print '|' (#i,109)
    print '|' (#i,176)

    if #i = 8 or #start_print1 = 1
       print '|' (#i,84)
       print '|' (#i,139)
       print '|' (#i,150)
       move 1 to #start_print1
    end-if

    if #i = 11 or #start_print2 = 1
       print '|' (#i,103)
       print '|' (#i,114)
       print '|' (#i,118)
       print '|' (#i,122)
       print '|' (#i,126)
       print '|' (#i,130)
       print '|' (#i,134)
       print '|' (#i,169)

       move 1 to #start_print2
    end-if

    if #i = 14 or #start_print3 = 1
       print '|' (#i,88)
       print '|' (#i,92)
       print '|' (#i,97)

       print '|' (#i,154)
       print '|' (#i,158)
       print '|' (#i,163)

       move 1 to #start_print3
    end-if

    add 1 to #i
  end-while
  print 'For Calendar Year '                              (2,124)
  print $Calendar_Year                                    ()
  print 'Establishment:'                                  (+2,1)
  !print $EstabNameAddr                                    (0,+2)

  if RTRIM($addline2,' ') != ''
    let $addline0 = $addline1||','||$addline2
  end-if
  if RTRIM($addline3,' ') != ''
    let $addline0 = $addline0||','||$addline3
  end-if
  if RTRIM($addline4,' ') != ''
   let $addline0 = $addline0||','||$addline4
  end-if
  if RTRIM($addline5,' ') != ''
    let $addline0 = $addline0||','||$addline5
  end-if
  if RTRIM($addline6,' ') != ''
    let $addline0 = $addline0||','||$addline6
  end-if
  Print $addline0                                    (0,+2)
  move '' to $addline0

  print '_'                                               (+1, 1, 175) fill
  print 'Extent/Outcome of Injury'                        (+1,74)
  print 'Extent/Outcome of Illness'                       (0,110)
  print '_'                                               (+1, 74, 102) fill

  print '   Nonfatal Injuries'                            (+1, 85)
  print '   Type of illness'                              (0, 110)
  print '   Nonfatal Illnesses'                           (0, 151)

  print 'Fatalities'                                      (+1, 74)
  print '(see full titles below)'                         (0, 85)
  print '(see full titles below)'                         (0, 110)
  print 'Fatalities'                                      (0, 140)
  print '(see full titles below)'                         (0, 151)
  print '_'                                               (+1, 74, 102) fill

  print 'Injury'                                          (+1, 74)
  print 'Injuries With Lost'                              (0, 85)

  print 'Illness'                                         (0, 140)
  print 'Illness With Lost'                               (0, 151)

  print 'Related'                                         (+1, 74)
  print 'Workdays'                                        (0, 85)
  print 'Related'                                         (0, 140)
  print 'Workdays'                                        (0, 151)

  print '_'                                               (+1, 85, 18) fill
  print '_'                                               (0, 151, 18) fill

  print 'Employee'                                        (+5,19)
  print 'Name  C '                                        (+1,19)
  print 'Case'                                            (+1,2)
  print 'Incident'                                        (0,8)
  print 'Employee ID'                                     (0,19)
  print 'Date of'                                         (0,74)
  print 'Awy'                                             (0,85)
  print 'Awy'                                             (0,89)
  print 'Awy'                                             (0,93)
  print 'Res'                                             (0,98)
  print 'No'                                              (0,104)

  print 'Date of'                                         (0,140)
  print 'Awy'                                             (0,151)
  print 'Awy'                                             (0,155)
  print 'Awy'                                             (0,159)
  print 'Res'                                             (0,164)
  print 'No'                                              (0,170)

  print ' No.'                                            (+1,2)
  print '   Date'                                         (0,8)
  print ' Job Title'                                      (0,19)
  print 'Department'                                      (0,32)
  print 'Injury/Illness Description'                      (0, 44)

  print ' Death'                                          (0,74)
  print 'Res'                                             (0,85)
  print 'Dys'                                             (0,93)
  print 'Dys'                                             (0,98)
  print 'Dys'                                             (0,104)

  print ' Death'                                          (0,140)
  print 'Res'                                             (0,151)
  print 'Dys'                                             (0,159)
  print 'Dys'                                             (0,164)
  print 'Dys'                                             (0,170)

  print '  A '                                            (+1,2)
  print '    B '                                          (0,8)
  print '    D '                                          (0,19)
  print '    E '                                          (0,32)
  print '         F '                                     (0,44)

  print '   1 '                                           (0,74)
  print ' 2'                                              (0,85)
  print ' 3'                                              (0,89)
  print ' 4'                                              (0,93)
  print ' 5'                                              (0,97)
  print ' 6 '                                             (0,104)

  print '   8 '                                           (0,140)
  print ' 9'                                              (0,151)
  print '10'                                              (0,155)
  print '11'                                              (0,159)
  print ' 12'                                             (0,163)
  print '13 '                                             (0,170)

  print 'R'                                               (-11,119)

  print 'e'                                               (+1,119)
  print 'P'                                               (0,127)
  print 'R'                                               (0,131)

  print 's'                                               (+1,119)
  print 'P'                                               (0,123)
  print 'h'                                               (0,127)
  print 'e'                                               (0,131)

  print 'p'                                               (+1,119)
  print 'o'                                               (0,123)
  print 'y'                                               (0,127)
  print 'p'                                               (0,131)

  print 'i'                                               (+1,119)
  print 'i'                                               (0,123)
  print 's'                                               (0,127)

  print 'r'                                               (+1,119)
  print 's'                                               (0,123)
  print 'T'                                               (0,131)

  print 'L'                                               (+1,115)
  print 'a'                                               (0,119)
  print 'o'                                               (0,123)
  print 'A'                                               (0,127)
  print 'r'                                               (0,131)
  print 'O'                                               (0,135)

  print 'S'                                               (+1,111)
  print 'u'                                               (0,115)
  print 't'                                               (0,119)
  print 'n'                                               (0,123)
  print 'g'                                               (0,127)
  print 'a'                                               (0,131)
  print 't'                                               (0,135)

  print 'k'                                               (+1,111)
  print 'n'                                               (0,115)
  print 'o'                                               (0,119)
  print 'i'                                               (0,123)
  print 'e'                                               (0,127)
  print 'u'                                               (0,131)
  print 'h'                                               (0,135)

  print 'i'                                               (+1,111)
  print 'g'                                               (0,115)
  print 'r'                                               (0,119)
  print 'n'                                               (0,123)
  print 'n'                                               (0,127)
  print 'm'                                               (0,131)
  print 'e'                                               (0,135)

  print 'n'                                               (+1,111)
  print 's'                                               (0,115)
  print 'y'                                               (0,119)
  print 'g'                                               (0,123)
  print 't'                                               (0,127)
  print 'a'                                               (0,131)
  print 'r'                                               (0,135)

  print '7a'                                              (+1,111)
  print '7b'                                              (0,115)
  print '7c'                                              (0,119)
  print '7d'                                              (0,123)
  print '7e'                                              (0,127)
  print '7f'                                              (0,131)
  print '7g'                                              (0,135)

  print '_'                                               (+1, 2, 174) fill

end-heading

begin-procedure Init-Report

  let #Page_Maximum  = 50         !Maximum physical rows before bottom line

  move 'OHS001' to $ReportID
  move 'Occupational Injury/Illness Log - OSHA 200' to $ReportTitle
  display $ReportTitle

  do Create-Array

  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-Calendar-Year
  else
    do Select-Parameters
  end-if

end-procedure

begin-procedure Get-Values

   do Get-Calendar-Year

end-procedure

begin-procedure Create-Array

  create-array name=Totals size=3 field=TotalAmt:number:8
    !Row 1 = Injury Totals
    !Row 2 = Illness Totals
    !Row 0 = Illness Type Totals

end-procedure

begin-procedure Get-Header-Data
begin-SELECT
G.COMPANY,G.DESCR
  move &G.Company      to $Company
  move &G.Descr        to $CompanyName

F.DESCR &F_Descr,F.ADDRESS1 &F_Address1,F.CITY &F_City,F.STATE &F_State
 ! let $EstabNameAddr=&F_Descr||',  '||&F_Address1||', '||&F_City||', '||RTRIM(&F_State,' ')||'  '


F.POSTAL
  move &F.Postal to $Postal
  !concat $Postal with $EstabNameAddr
F.ADDRESS2  &F_ADDRESS2
F.ADDRESS3  &F_ADDRESS3
F.ADDRESS4   &F_ADDRESS4
F.NUM1       &F_NUM1
F.NUM2       &F_NUM2
F.HOUSE_TYPE  &F_HOUSE_TYPE
F.ADDR_FIELD1  &F_ADDR_FIELD1
F.ADDR_FIELD2  &F_ADDR_FIELD2
F.ADDR_FIELD3  &F_ADDR_FIELD3
F.COUNTY      &F_COUNTY
F.COUNTRY     &F_COUNTRY
F.GEO_CODE    &F_GEO_CODE
F.IN_CITY_LIMIT   &F_IN_CITY_LIMIT
  LET $name = RTRIM(&F_DESCR,' ')
  Let $address1 = RTRIM(&F_ADDRESS1,' ')
  Let $address2 = RTRIM(&F_ADDRESS2,' ')
  LET $address3 = RTRIM(&F_ADDRESS3,' ')
  LET $address4 = RTRIM(&F_ADDRESS4,' ')
  LET $num1 = RTRIM(&F_NUM1,' ')
  LET $num2 = RTRIM(&F_NUM2,' ')
  LET $house_type = RTRIM(&F_HOUSE_TYPE,' ')
  Let $addr_field1 = RTRIM(&F_ADDR_FIELD1,' ')
  Let $addr_field2 = RTRIM(&F_ADDR_FIELD2,' ')
  LET $addr_field3 = RTRIM(&F_ADDR_FIELD3,' ')
  LET $city = RTRIM(&F_CITY,' ')
  LET $state = RTRIM(&F_STATE,' ')
  LET $county = RTRIM(&F_COUNTY,' ')
  LET $country = RTRIM(&F_COUNTRY,' ')
  LET $geo_code = RTRIM(&F_GEO_CODE,' ')
  LET $in_city_limit = RTRIM(&F_IN_CITY_LIMIT,' ')

  DO adformat

FROM  PS_ESTAB_TBL F, PS_COMPANY_TBL G
WHERE F.ESTABID = $EstabID
  AND F.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_ESTAB_TBL
       WHERE  ESTABID = F.ESTABID
         AND EFFDT <= $End_Date)
  AND G.COMPANY = F.COMPANY
  AND G.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_COMPANY_TBL
       WHERE  COMPANY = G.COMPANY
         AND EFFDT <= $End_Date)
end-SELECT
end-procedure

begin-procedure Process-Main

  do Build-Time-Period
  do OSHA-200

end-procedure

begin-procedure Build-Time-Period
  move $Calendar_Year to $End_CMP
  let $End_CMP = $End_CMP || '1231'
  do Format-DateTime($End_CMP, $End_Date, {DEFCMP}, '', 'NATIVE')

  move $Calendar_Year to $Begin_CMP
  let $Begin_CMP = $Begin_CMP || '0101'
  do Format-DateTime($Begin_CMP, $Begin_Date, {DEFCMP}, '', 'NATIVE')
end-procedure

begin-procedure OSHA-200
begin-SELECT
C.ESTABID () on-break print=never procedure=Print-Totals
  move &C.EstabID to $EstabID

  Let #Start_Line = 1

  if #current-line >= #Page_Maximum - 6

     print '_'                                       (0, 2, 175) fill

     print 'Total:'                                   (+1, 65)

     Do Print-Totals-Array
     Do Print-Page-Foot

     print 'Pre Page Total:'                          (1, 55)

     Do Print-Totals-Array

     Let #Start_Line = 3

  end-if

  Let #Second_Line  = #Start_Line + 1
  Let #Third_Line  = #Second_Line + 1

A.CASE_NBR_USA              (#Start_Line,2)
I.INCIDENT_DT &I.Incident_Dt
  do Format-DateTime(&I.Incident_Dt, $out, {DEFDATE}, '', '')
  print $out             (#Start_Line,7)
F.NAME
  move &F.Name to $Name
  do Rotate-Name
  print $FirstInitial  (#Start_Line,19,21)
  do Incident-Analysis
A.OSHA_200_DESCR       (#Start_Line,44,30) WRAP 30 3
A.EMPLID               (#Second_Line,19)
D.DESCRSHORT           (#Third_Line,19)
E.DESCRSHORT           (#Third_Line,32)
  NEXT-LISTING SKIPLINES=1 NEED=3
A.INJURY_ILLNESS,A.AWAY_FROM_WORKDAYS,A.RESTRICTD_WORKDAYS
A.ILLNESS_TYPE,A.ILLNESS_RESULT

B.DT_OF_DEATH           &B.Dt_Of_Death

FROM  PS_INJURY_ILL A, PS_JOB J, PS_INCIDENT_DATA I ,PS_PERSON B,
   PS_PERSON_NAME F,
   !*****BEGIN RESOLUTION 591599********** 
   
   !PS_LOCATION_TBL C,
   PS_ESTAB_LOC_US C,
   
   !*****END RESOLUTION 591599************ 
   
   PS_JOBCODE_TBL D, PS_DEPT_TBL E
WHERE A.REPORTABLE_CASE = 'Y'
  AND I.INCIDENT_NBR = A.INCIDENT_NBR
  AND I.INCIDENT_DT >= $Begin_Date
  AND I.INCIDENT_DT <= $End_Date
  AND B.EMPLID = A.EMPLID
  AND J.PER_ORG = 'EMP'
  AND F.EMPLID = B.EMPLID
  AND J.EMPLID = A.EMPLID
  AND J.EMPL_RCD = A.EMPL_RCD
  AND J.EFFDT =
      (SELECT MAX(EFFDT)
       FROM  PS_JOB
       WHERE EMPLID = J.EMPLID
       AND   EMPL_RCD = J.EMPL_RCD
       AND   EFFDT <= I.INCIDENT_DT)
  AND J.EFFSEQ =
      (SELECT MAX(EFFSEQ)
      FROM PS_JOB
      WHERE EMPLID = J.EMPLID
      AND EMPL_RCD = J.EMPL_RCD
      AND EFFDT = J.EFFDT)
  AND C.LOCATION = J.LOCATION
  AND C.SETID = J.SETID_LOCATION
  AND C.EFFDT =
      (SELECT MAX(EFFDT)
      
       !*****BEGIN RESOLUTION 591599**********
       
       !FROM   PS_LOCATION_TBL
       FROM PS_ESTAB_LOC_US
       
       !*****END RESOLUTION 591599************
       
       
         WHERE  LOCATION = C.LOCATION
         AND SETID = C.SETID
         AND  EFFDT   <= I.INCIDENT_DT)
  AND D.JOBCODE = J.JOBCODE
  AND D.SETID = J.SETID_JOBCODE
  AND D.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_JOBCODE_TBL
       WHERE  JOBCODE = D.JOBCODE
         AND SETID = D.SETID
         AND  EFFDT  <= I.INCIDENT_DT)
  AND E.DEPTID = J.DEPTID
  AND E.SETID = J.SETID_DEPT
  AND E.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_DEPT_TBL
       WHERE  DEPTID = E.DEPTID
         AND  SETID = E.SETID
         AND  EFFDT <= I.INCIDENT_DT)

ORDER BY C.ESTABID,A.CASE_NBR_USA
end-SELECT

do Print-Totals
end-procedure

begin-procedure Incident-Analysis
  if &A.Injury_Illness = 'N'  !Injury
    move  1 to #TotalGroup
    move 74 to #startCol
  else                     !Illness
    move &A.Illness_Type to #Illness
    array-add 1 to Totals(0) TotalAmt(#Illness)
    multiply 4 times #Illness
    add 107 to #Illness
    print 'X' (0,#Illness)
    if &A.Illness_Result <> 'N'  !then Termination or Transfer
      print '*' ()
    end-if
    move 2 to #TotalGroup
    move 140 to #startCol
  end-if
  position (0,#startCol)
  if not IsNULL(&B.Dt_Of_Death)
    do Format-DateTime(&B.Dt_Of_Death, $out, {DEFDATE}, '', '')
    print $out ()
    array-add 1 to Totals(#TotalGroup) TotalAmt(1)
  else
    if &A.Away_from_Workdays > 0
      print 'X   X' (0,+12)
      do Format-Number(&A.Away_from_Workdays, $out, '9999')
      print $out        (0,+2)
      array-add 1 1 to Totals(#TotalGroup) TotalAmt(2) TotalAmt(3)
      array-add &A.Away_from_Workdays to Totals(#TotalGroup) TotalAmt(4)
      position (0,#startCol)
    end-if
    if &A.Restrictd_Workdays > 0
      print 'X' (0,+12)
      do Format-Number(&A.Restrictd_Workdays, $out, '9999')
      print $out        (0,+11)
      array-add &A.Restrictd_Workdays to Totals(#TotalGroup) TotalAmt(5)
      if &A.Away_from_Workdays = 0
        array-add 1 to Totals(#TotalGroup) TotalAmt(2)
      end-if
    end-if
    if &A.Away_from_Workdays = 0 and &A.Restrictd_Workdays = 0
      print 'X' (0,+31)
      array-add 1 to Totals(#TotalGroup) TotalAmt(6)
    end-if
  end-if
end-procedure


begin-procedure Print-Totals


  if #current-line < #Page_Maximum - 6
     Let #Incease_num = #Page_Maximum - #current-line - 6
  else
     Let #Incease_num = 1
  end-if

  print '_'                                               (+#Incease_num, 2, 175) fill

  print 'Total:'                                          (+1, 65)

  Do Print-Totals-Array

  Do Init-Totals-Array

  Do Print-Page-Foot


end-procedure

begin-procedure Print-Totals-Array

 position (0,78)

  move 1 to #TotalGroup
  while #TotalGroup <= 2
    get #Total from Totals(#TotalGroup) TotalAmt(1)
    do Format-Number(#Total, $out, '9,999')
    print $out ()
    get #Total from Totals(#TotalGroup) TotalAmt(2)
    do Format-Number(#Total, $out, '9999')
    print $out (0,+2)
    get #Total from Totals(#TotalGroup) TotalAmt(3)
    do Format-Number(#Total, $out, '9999')
    print $out (0,+1)
    get #Total from Totals(#TotalGroup) TotalAmt(4)
    do Format-Number(#Total, $out, '9999')
    print $out (0, +1)
    get #Total from Totals(#TotalGroup) TotalAmt(5)
    do Format-Number(#Total, $out, '9999')
    print $out (0, +1)
    get #Total from Totals(#TotalGroup) TotalAmt(6)
    do Format-Number(#Total, $out, '9,999')
    print $out (0,+1)
    position (0,144)
    add 1 to #TotalGroup
  end-while
  position (0,110)
  move 1 to #adj
  move 1 to #i
  while #i <= 7
    get #Total from Totals(0) TotalAmt(#i)
    do Format-Number(#Total, $out, '999')
    print $out (0,+1)
    add 1 to #i
  end-while

  print '_'                                               (+1, 1, 175) fill

end-procedure



begin-procedure Init-Totals-Array

  move 0 to #TotalGroup  !clear totals for the next establishment

  while #TotalGroup <= 2
    move 1 to #i
    while #i <= 7
      put 0 into Totals(#TotalGroup) TotalAmt(#i)
      add 1 to #i
    end-while
    add 1 to #TotalGroup
  end-while

 end-procedure



begin-procedure Print-Page-Foot

  print '7a - Occupational skin diseases or disorders'       (+1, 1)
  print '2&9  - Any Days Away or Rest Duty'                  (0, 60)

  print '7b - Dust diseases of lungs'                        (+1, 1)
  print '3&10 - Any Days Away from Work'                     (0, 60)

  print 'Certification of Annual Summary Totals by'          (0,115)
  print '_'                                                  (0,156,20) FILL


  print '7c - Respiratory conditions due to toxic agents'    (+1, 1)
  print '4&11 - # Days Away from Work'                       (0, 60)

  print '7d - Poisoning(systemic effects of toxic materials)'(+1, 1)
  print '5&12 - # Days Rest Duty'                            (0, 60)

  print 'Title'                                              (0,150)
  print '_'                                                  (0,156,20) FILL

  print '7e - Disorders due to physical agents'              (+1, 1)
  print '6    - Injuries Without Days Away or Rest Duty'     (0, 60)

  print '7f - Disorders associated with repeated truma'      (+1, 1)
  print '13   - Illnesses Without Days Away or Rest Duty'    (0, 60)

  print 'Date'                                               (0,150)
  print '_'                                                  (0,156,20) FILL

  print '7g - All other occupational illnesses'              (+1, 1)

  new-page

end-procedure


#include 'stdapi.sqc'    !Routine to update run status
#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askcalyr.sqc'  !Ask Calendar Year input
#include 'adformat.sqc'  !Global address formatting

#Include 'getcodta.sqc'  !Get-Company-Data procedure (used in StdHdg02)
#Include 'rotname2.sqc'  !Rotate-Name procedure
#Include 'reset1.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
