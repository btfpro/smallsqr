!***********************************************************************
! PUHRY62: DUMP OF DESIRED RATING DISTRIBUTION HISTORY
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/03/24:10:11:19                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************

#define PTDateDelim    -

#Include 'setenv.sqc'   !Set environment

Begin-Setup
  #Include 'ptpsp125.sqc'
End-Setup

!***********************************************************************
Begin-Program
!***********************************************************************

  do Init-Report

  do Print-Review-Distrib-History

  do StdAPI-Term

End-Program

!***********************************************************************
Begin-Procedure Init-Report
!***********************************************************************

  do Init-DateTime
  do Get-Current-DateTime
  do StdAPI-Init

  let  $ReportID = 'PUHRY62'
  do Get-Report-Language

  let $ReportTitle  = $PUHRY62-TITLE
  display  'Report ID: ' noline
  display  $ReportTitle

End-Procedure

!***********************************************************************
Begin-Procedure Report-Translation
!***********************************************************************

  do Init_Report_Translation($ReportID, $Language_Cd)

  do Get_Field_Information ('PUHRY62','TITLE',            $PUHRY62-TITLE ,  #COL-TITLE)   !43
  do Get_Field_Information ('PUHRY62','GROUP_ID',         $PUHRY62-GROUP,   #COL-GROUP)   !15
  do Get_Field_Information ('PUHRY62','EFFDT',            $PUHRY62-EFFDT ,  #COL-EFFDT)   !12
  do Get_Field_Information ('PUHRY62','EFF_STATUS',       $PUHRY62-EFFSTS , #COL-EFFSTS)  !6
  do Get_Field_Information ('PUHRY62','DESCR',            $PUHRY62-DESCR ,  #COL-DESCR)   !30
  do Get_Field_Information ('PUHRY62','REVIEW_TYPE',      $PUHRY62-RTYPE ,  #COL-RTYPE)   !11
  do Get_Field_Information ('PUHRY62','RATING_MODEL',     $PUHRY62-MODEL ,  #COL-MODEL)   !12
  do Get_Field_Information ('PUHRY62','CALC_METHOD',      $PUHRY62-CALC,    #COL-CALC)    !11
  do Get_Field_Information ('PUHRY62','REVIEW_BAND_CD',   $PUHRY62-RBAND,   #COL-RBAND)   !11
  do Get_Field_Information ('PUHRY62','REVIEW_RATING',    $PUHRY62-RRATE,  #COL-RRATE)    !13
  do Get_Field_Information ('PUHRY62','FROM_POINTS',      $PUHRY62-FPOINTS, #COL-FPOINTS) !11
  do Get_Field_Information ('PUHRY62','TO_POINTS',        $PUHRY62-TPOINTS, #COL-TPOINTS) !9
  do Get_Field_Information ('PUHRY62','DESIRED_DIST_PCT', $PUHRY62-DPCT,    #COL-DPCT)    !20
  do Get_Field_Information ('PUHRY62','MESSAGE',          $PUHRY62-MSG,     #COL-MSG)     !40


  !----- This section is for testing purposes -----
   !let $PUHRY62-TITLE    = 'Dump of Desired Rating Distribution History'
   !let $PUHRY62-GROUP    = 'Group Id'
   !let $PUHRY62-MODEL    = 'Rating Model'
   !let $PUHRY62-EFFDT    = 'Eff Date'
   !let $PUHRY62-EFFSTS   = 'Status'
   !let $PUHRY62-DESCR    = 'Description'
   !let $PUHRY62-RTYPE    = 'Review Type'
   !let $PUHRY62-CALC     = 'Calc Method'
   !let $PUHRY62-RBAND    = 'Review Band'
   !let $PUHRY62-RRATE    = 'Review Rating'
   !let $PUHRY62-FPOINTS  = 'From Points'
   !let $PUHRY62-TPOINTS  = 'To Points'
   !let $PUHRY62-DPCT     = 'Desired Distribution'
   !let $PUHRY62-MSG      = 'No distribution data was selected'
   !let #COL-GROUP = 15
   !let #COL-MODEL = 12
   !let #COL-EFFDT = 12
   !let #COL-EFFSTS = 6
   !let #COL-DESCR = 30
   !let #COL-RTYPE = 11
   !let #COL-CALC = 11
   !let #COL-RBAND = 11
   !let #COL-RRATE = 13
   !let #COL-FPOINTS = 11
   !let #COL-TPOINTS = 9
   !let #COL-DPCT = 20
   !let #COL-MSG = 40
  !----- End of testing section -----


End-Procedure

!***********************************************************************
Begin-Heading 6
!***********************************************************************

#Include 'stdhdgtr.sqc'

  print $PUHRY62-GROUP        (+2,1,#COL-GROUP)
  print $PUHRY62-EFFDT        (0,+1,#COL-EFFDT)
  print $PUHRY62-EFFSTS       (0,+1,#COL-EFFSTS)
  print $PUHRY62-DESCR        (0,+1,#COL-DESCR)
  print $PUHRY62-RTYPE        (0,+2,#COL-RTYPE)
  print $PUHRY62-MODEL        (0,+1,#COL-MODEL)
  print $PUHRY62-CALC         (0,+1,#COL-CALC)

  print '='      (+1,1,#COL-GROUP)   Fill
  print '='      (0,+1,#COL-EFFDT)   Fill
  print '='      (0,+1,#COL-EFFSTS)  Fill
  print '='      (0,+1,#COL-DESCR)   Fill
  print '='      (0,+2,#COL-RTYPE)   Fill
  print '='      (0,+1,#COL-MODEL)   Fill
  print '='      (0,+1,#COL-CALC)    Fill


End-Heading


!***********************************************************************
Begin-Procedure Print-Review-Distrib-History
!***********************************************************************

Begin-SELECT

RD.GB_GROUP_ID       (+2,1,#COL-GROUP)
RD.EFFDT
  do Format-DateTime(&RD.EFFDT, $FmtDt, {DEFYMD}, '','')
  print $FmtDt       (0,+1,#COL-EFFDT)
RD.EFF_STATUS        (0,+1,#COL-EFFSTS)
RD.DESCR50
  move &RD.DESCR50 to $RD_Descr
  do Get-Des-Distrib-Lang
  print $RD_Descr    (0,+1,#COL-DESCR)
RD.REVIEW_TYPE       (0,+2,#COL-RTYPE)
RS.RATING_MODEL      (0,+1,#COL-MODEL)
RS.REVW_SCALE_TYPE
  move 'REVW_SCALE_TYPE'  to $FieldName
  move &RS.REVW_SCALE_TYPE to $FieldValue
  do Read-Translate-Table
  let $CalcMeth = RTRIM($XlatShortName, ' ')
  print $CalcMeth    (0,+1,#COL-CALC)
RS.MERIT_SCALE

  if &RS.REVW_SCALE_TYPE = 'R'
    do Print-ReviewBand-Distrib-Pct
  else
    if &RS.REVW_SCALE_TYPE = 'A'
      do Print-Average-Distrib-Pct
    else
      if &RS.REVW_SCALE_TYPE = 'S'
        do Print-Summation-Distrib-Pct
      end-if
    end-if
  end-if

from PS_REVIEW_DISTRIB RD,
     PS_REVW_SCALE_TBL RS

where RS.RATING_SCALE = RD.RATING_SCALE
AND RS.EFFDT = (SELECT MAX(EFFDT)
               FROM PS_REVW_SCALE_TBL
               WHERE RATING_SCALE = RS.RATING_SCALE
               AND EFFDT <= RD.EFFDT)
order by RD.GB_GROUP_ID ASC,
         RD.EFFDT ASC

End-SELECT

IF IsBlank(&RD.GB_GROUP_ID)
   print $PUHRY62-MSG (+2,1,#COL-MSG)
end-if

End-Procedure

!***********************************************************************
Begin-Procedure Print-ReviewBand-Distrib-Pct
!***********************************************************************

print $PUHRY62-RBAND           (+1,5,#COL-RBAND)
print $PUHRY62-DESCR           (0,+2,#COL-DESCR)
print $PUHRY62-DPCT            (0,+2,#COL-DPCT)
print '='                       (+1,5,#COL-RBAND)   Fill
print '='                       (0,+2,#COL-DESCR)   Fill
print '='                       (0,+2,#COL-DPCT)    Fill

Begin-SELECT

RDD1.SEQ_NUM
RDD1.REVIEW_BAND_CD
RB.EFFDT
RB.DESCR50
  move &RB.DESCR50 to $RB_Descr
  do Get-ReviewBand-Lang
RDD1.DESIRED_DIST_PCT

  print &RDD1.REVIEW_BAND_CD       (+1,5, #COL-RBAND)
  print $RB_Descr                  (0,+2, #COL-DESCR)
  print &RDD1.DESIRED_DIST_PCT     (0,+2, #COL-DPCT)

from PS_REVW_DESIR_DIST RDD1,
     PS_REVIEW_BAND_TBL RB
where RDD1.GB_GROUP_ID = &RD.GB_GROUP_ID
and   RDD1.EFFDT = &RD.EFFDT
and   RB.REVIEW_BAND_CD = RDD1.REVIEW_BAND_CD
and   RB.EFFDT = (SELECT MAX(EFFDT)
                  FROM PS_REVIEW_BAND_TBL
                  WHERE REVIEW_BAND_CD = RB.REVIEW_BAND_CD
                  AND EFFDT <= &RD.EFFDT)
ORDER BY RDD1.SEQ_NUM ASC

End-SELECT
End-Procedure

!***********************************************************************
Begin-Procedure Print-Average-Distrib-Pct
!***********************************************************************

print $PUHRY62-RRATE           (+1,5,#COL-RRATE)
print $PUHRY62-DESCR           (0,+2,#COL-DESCR)
print $PUHRY62-DPCT            (0,+2,#COL-DPCT)
print '='                       (+1,5,#COL-RRATE)   Fill
print '='                       (0,+2,#COL-DESCR)   Fill
print '='                       (0,+2,#COL-DPCT)    Fill

Begin-SELECT

RDD2.SEQ_NUM
RDD2.REVIEW_RATING
RR.EFFDT
RR.DESCR
  move &RR.DESCR to $RR_Descr
  do Get-ReviewRating-Lang
RDD2.DESIRED_DIST_PCT

  print &RDD2.REVIEW_RATING        (+1,5, #COL-RRATE)
  print $RR_Descr                  (0,+2, #COL-DESCR)
  print &RDD2.DESIRED_DIST_PCT     (0,+2, #COL-DPCT)

from PS_REVW_DESIR_DIST RDD2,
     PS_REVW_RATING_TBL RR

where RDD2.GB_GROUP_ID = &RD.GB_GROUP_ID
and   RDD2.EFFDT = &RD.EFFDT
AND   RR.RATING_MODEL = &RS.RATING_MODEL
AND   RR.REVIEW_RATING = RDD2.REVIEW_RATING
AND   RR.EFFDT = (SELECT MAX(EFFDT)
                  FROM PS_REVW_RATING_TBL
                  WHERE RATING_MODEL = RR.RATING_MODEL
                  AND REVIEW_RATING = RR.REVIEW_RATING
                  AND EFFDT <= &RD.EFFDT)
ORDER BY RDD2.SEQ_NUM ASC

End-SELECT

End-Procedure

!***********************************************************************
Begin-Procedure Print-Summation-Distrib-Pct
!***********************************************************************

print $PUHRY62-FPOINTS         (+1,5,#COL-FPOINTS)
print $PUHRY62-TPOINTS         (0,+2,#COL-TPOINTS)
print $PUHRY62-DPCT            (0,+2,#COL-DPCT)
print '='                       (+1,5,#COL-FPOINTS)   Fill
print '='                       (0,+2,#COL-TPOINTS)   Fill
print '='                       (0,+2,#COL-DPCT)      Fill

Begin-SELECT

RDD3.SEQ_NUM
MG.FROM_POINTS
MG.TO_POINTS
RDD3.DESIRED_DIST_PCT

  print &MG.FROM_POINTS            (+1,5, #COL-FPOINTS)
  print &MG.TO_POINTS              (0,+2, #COL-TPOINTS)
  print &RDD3.DESIRED_DIST_PCT     (0,+2, #COL-DPCT)

from PS_REVW_DESIR_DIST RDD3,
     PS_MERIT_GROUP_TBL MG

where RDD3.GB_GROUP_ID = &RD.GB_GROUP_ID
and   RDD3.EFFDT = &RD.EFFDT
and   MG.MERIT_SCALE = &RS.MERIT_SCALE
AND   MG.MERIT_GROUP = RDD3.MERIT_GROUP
AND   MG.EFFDT = (SELECT MAX(EFFDT)
               FROM PS_MERIT_GROUP_TBL
               WHERE MERIT_SCALE = MG.MERIT_SCALE
               AND   MERIT_GROUP = MG.MERIT_GROUP
               AND   EFFDT <= &RD.EFFDT)
ORDER BY RDD3.SEQ_NUM ASC

End-SELECT

End-Procedure


!***********************************************************************
Begin-Procedure Get-Des-Distrib-Lang
!***********************************************************************

Begin-SELECT
RDL.DESCR50

  if Not IsBlank(&RDL.DESCR50)
    move &RDL.DESCR50 to $RD_Descr
  end-if

FROM  PS_REVW_DIST_LNG RDL
WHERE RDL.GB_GROUP_ID = &RD.GB_GROUP_ID
AND RDL.EFFDT = &RD.EFFDT
AND RDL.LANGUAGE_CD = $curr_language_cd

End-SELECT
End-Procedure

!***********************************************************************
Begin-Procedure Get-ReviewBand-Lang
!***********************************************************************

Begin-SELECT
RBL.DESCR50

  if Not IsBlank(&RBL.DESCR50)
    move &RBL.DESCR50 to $RB_Descr
  end-if

FROM  PS_RVW_BAND_TB_LNG RBL
WHERE RBL.REVIEW_BAND_CD = &RDD1.REVIEW_BAND_CD
AND RBL.EFFDT = &RB.EFFDT
AND RBL.LANGUAGE_CD = $curr_language_cd

End-SELECT
End-Procedure

!***********************************************************************
Begin-Procedure Get-ReviewRating-Lang
!***********************************************************************

Begin-SELECT
RRL.DESCR

  if Not IsBlank(&RRL.DESCR)
    move &RRL.DESCR to $RR_Descr
  end-if

FROM  PS_REVW_RATING_LNG RRL
WHERE RRL.RATING_MODEL = &RS.RATING_MODEL
AND RRL.REVIEW_RATING = &RDD2.REVIEW_RATING
AND RRL.EFFDT = &RR.EFFDT
AND RRL.LANGUAGE_CD = $curr_language_cd

End-SELECT
End-Procedure



!***********************************************************************
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Format-DateTime procedures
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
#Include 'readxlat.sqc'  !get translates for fieldname, value
!***********************************************************************

