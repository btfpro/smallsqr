!*************************************************************
!  GetGBJpn:  Get GB Group Description
!*************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!          $Date:  2006/07/19:13:05:29                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!*********************************
begin-procedure Get-GB-Group-Descr
!*********************************

  !Keys passed in:  $GB_Group_ID, $AsOfDate
  !Values returned: $Group_Descr, $Group_Descrshort

  move '' to $Group_Descr
  move '' to $Group_Descrshort

  if Rtrim($GB_Group_ID,' ') = ''
    goto GetGBGrpExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

LET $sql-statement = 'An error occurred in reading GB Group Table(GB_GROUP_TBL).'

begin-SELECT on-error=SQL-Error
GB.EFFDT
GB.DESCR
GB.DESCRSHORT

  let $Group_Effdt = &GB.EFFDT
  let $Group_Descr = Rtrim(&GB.DESCR,' ')
  let $Group_Descrshort = Rtrim(&GB.DESCRSHORT,' ')

FROM  PS_GB_GROUP_TBL GB
WHERE GB.GB_GROUP_ID = $GB_Group_ID
  AND GB.EFFDT       =
       (SELECT MAX(EFFDT)
        FROM   PS_GB_GROUP_TBL
        WHERE  GB_GROUP_ID = GB.GB_GROUP_ID
          AND  EFFDT <= $AsOfDate)
  AND GB.EFF_STATUS  = 'A'
end-SELECT

  do Get-GB-Group-Lang

GetGBGrpExit:

end-procedure

!********************************
begin-procedure Get-GB-Group-Lang
!********************************

  !Keys passed in:  $GB_Group_ID, $AsOfDate
  !Values returned: $Group_Descr, $Group_Descrshort

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetGBGrpLngExit
  end-if

  if Rtrim($GB_Group_ID,' ') = ''
    goto GetGBGrpLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

LET $sql-statement = 'An error occurred in reading GB Group Language Table(GB_GROUPTBL_LNG).'

begin-SELECT on-error=SQL-Error
GBL.DESCR
GBL.DESCRSHORT

  if Rtrim(&GBL.DESCR,' ') <> ''
    let $Group_Descr = Rtrim(&GBL.DESCR,' ')
  end-if
  if Rtrim(&GBL.DESCRSHORT,' ') <> ''
    let $Group_Descrshort = Rtrim(&GBL.DESCRSHORT,' ')
  end-if

FROM  PS_GB_GROUPTBL_LNG GBL
WHERE GBL.GB_GROUP_ID     = $GB_Group_ID
  AND GBL.LANGUAGE_CD     = $Curr_Language_Cd
  AND GBL.EFFDT           = $Group_Effdt

end-SELECT

GetGBGrpLngExit:

end-procedure
