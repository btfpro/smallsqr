!********************************
!  TAX100CN:  Exemption Report  *
!********************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/07/06:12:55:33                           !
!             $Release:  HR9                                           !
!            $Revision:  102                                           !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup31.sqc'   !Printer and page-size initialization
#Include 'canrpt.sqc'    !Get CAN variables

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  do Stdapi-Init
  move 'TAX100CN' to $ReportID
  do Get-Report-Language          ! Procedure that decides the report language
                                  ! and then calls 'Report-Translation' procedure
                                  ! that reads the language dep. text.

  move $Tax100cn_Title_exemp_rpt to $ReportTitle
  display $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  if $Prcs_Process_Instance = ''
    do Get-As-Of-Date
  else
    do Select-Parameters
    move $RC_PAY.AsOfDate to $AsOfDate
    do Update-As-Of-Date
  end-if

end-procedure

!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
!Read language-dependent text from the database.

begin-procedure Report-Translation        ! Read from TAX100CN  program ids

   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('TAX100CN', 'TITLE_EXEMP_RPT' , $Tax100cn_Title_exemp_rpt , #dummy_width)
   do Get_Field_Information ('TAX100CN', 'HEADING1'        , $Tax100cn_Heading1        , #dummy_width)
   do Get_Field_Information ('TAX100CN', 'HEADING2'        , $Tax100cn_Heading2        , #dummy_width)
   do Get_Field_Information ('TAX100CN', 'HEADING3'        , $Tax100cn_Heading3        , #dummy_width)
   do Get_Field_Information ('TAX100CN', 'YES'             , $Tax100cn_Yes             , #dummy_width)


end-procedure

begin-heading 8
  #Include 'stdhtr09.sqc'
  print $Tax100cn_Heading1               (+3,81)
  print $Tax100cn_Heading2               (+1,1)
  print $Tax100cn_Heading3               ()
end-heading

begin-procedure Update-As-Of-Date
begin-SQL on-error=Bad-Date
UPDATE PSASOFDATE SET ASOFDATE = $AsOfDate
end-SQL
end-procedure

begin-procedure Process-Main
begin-SELECT
A.COMPANY () on-break print=never procedure=Company-Change
A.EMPLID                  (+1,1)
PN.NAME                     (0,14,30)
A.DEPTID                    (0,46)
A.DEPTNAME_ABBRV            (0,58)
P1.NATIONAL_ID
  move &A.Company to $Company  !for use in Heading procedure
  print &P1.National_id    (0,70) edit xxx-xxx-xxx
B.CPP_SUBJECT_MONTHS        (0,85) EDIT B9
B.SPECIAL_CIT_STATUS
  if &B.Special_CIT_Status = 'E'
    print $Tax100cn_Yes   (0,92)
  end-if
B.SPECIAL_QIT_STATUS
  if &B.Special_QIT_Status = 'E'
    print $Tax100cn_Yes   (0,99)
  end-if
B.UI_EXEMPT
  if &B.UI_Exempt = 'Y'
    print $Tax100cn_Yes   (0,106)
  end-if
B.PAYROLL_TAX_EXEMPT
  if &B.Payroll_Tax_Exempt = 'Y'
    print $Tax100cn_Yes   (0,114)
  end-if
B.QPIP_EXEMPT
  if &B.QPIP_EXEMPT = 'Y'
    print $Tax100cn_Yes   (0,121)
  end-if

FROM  PS_PERSONNEL A, PS_CAN_TAX_DATA B , PS_PERS_NID P1, PS_NAMES PN
WHERE B.COMPANY              = A.COMPANY
  AND B.EMPLID               = A.EMPLID
  AND B.EMPLID               = P1.EMPLID
  AND B.EMPLID               = PN.EMPLID
  AND PN.NAME_TYPE = 'PRI'
  AND PN.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PN.EMPLID
         AND  PX.NAME_TYPE = PN.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfDate)
  AND P1.COUNTRY             = {NID_COUNTRY}
  AND P1.NATIONAL_ID_TYPE    = $PAYROLL_NID_TYPE
  AND (B.SPECIAL_CIT_STATUS  = 'E' OR
       B.SPECIAL_QIT_STATUS  = 'E' OR
       B.UI_EXEMPT           = 'Y' OR
       B.PAYROLL_TAX_EXEMPT  = 'Y' OR
       B.QPIP_EXEMPT         = 'Y' OR
       B.CPP_SUBJECT_MONTHS  < 12)
  AND B.EFFDT                =
      (SELECT MAX(B1.EFFDT)
         FROM PS_CAN_TAX_DATA B1
        WHERE B1.COMPANY    = B.COMPANY
          AND B1.EMPLID     = B.EMPLID
          AND B1.EFFDT     <= $AsOfDate)
ORDER BY A.COMPANY,PN.NAME
end-SELECT
end-procedure

begin-procedure Company-Change
  new-page
end-procedure

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'getasodt.sqc'  !Get-As-Of-Date procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrctl2.sqc'  !Get-Run-Control procedure
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
