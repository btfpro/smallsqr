!*****************************************************************
!  PER033:  Citizenship Status/Country of Citizenship/Visa Audit *
!*****************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/04/05:02:55:53                                 
!       $Release:  HR9                                                 
!    $Resolution:  689716                                              
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-heading 6
do Get_Field_Information ('PER033',     'REPORT_TITLE', $REPORTTITLE,   #DW)
  #Include 'stdhdgtr.sqc'
do Get_Field_Information ('HR',         'EMPLID',       $EMPLID,        #DW)
do Get_Field_Information ('HR',         'EMPL_NAME',    $NAME,          #DW)

  print $Heading                    (+1,1)
  print $EMPLID                     (+1,1,13)
  print $NAME                       (0,15)
end-heading

begin-procedure Init-Report

  move 'PER033' to $ReportID
  do Stdapi-Init
  do Security-Param

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if
do Init_Printer
do Init_Report_Translation ($ReportID, $language_cd)
do Append_Report_Translation ('HR')

end-procedure

begin-procedure Get-Values
let $language_cd = $PRCS_LANGUAGE_CD
end-procedure

begin-procedure Process-Main

  do Get-Installation-Country
  do Not-Native-No-Visa
  do Native-Local-Visa
  do Local-Ctz-Local-Visa
  do Local-Ctz-Not-Native
!
! Uncomment do statements if you track Country of Citizenship for all employees
! The following audit will provide you with list of employees without
! Country of Citizenship and has discrepancy with visas/citizenship status
!
  do No-Local-Ctz-No-Visa
  do Native-No-Local-Ctz

end-procedure

begin-procedure Get-Installation-Country

begin-SELECT
IC.COUNTRY
FROM PS_INSTALLATION IC
end-SELECT

end-procedure

begin-procedure Not-Native-No-Visa

! 1 - Citizenship status not Native/Naturalized and has no local visa
! (Original PeopleCode message 1000,47)
do Get_Field_Information ('PER033',    'HEADING1',     $HEAD1,         #DW)
  let $heading = $HEAD1

begin-SELECT DISTINCT

A.EMPLID    (+1,1,13)

!**********Begin SQR NAME DISPLAY CHANGE ****************
!A.NAME      (0,15)
A.NAME_DISPLAY (0,15)
!**********End SQR NAME DISPLAY CHANGE ****************
FROM PS_PERSON_NAME A, PS_CITIZENSHIP A1, PS_FAST_SQR_SEC_VW SCRTY
WHERE (A1.CITIZENSHIP_STATUS = '3'
       OR A1.CITIZENSHIP_STATUS = '4')
  AND A.EMPLID = A1.EMPLID                              ! 09/00
  AND NOT EXISTS
    (SELECT 'x'
    FROM PS_VISA_PMT_DATA B
    WHERE A.EMPLID = B.EMPLID
      AND B.DEPENDENT_ID = ' '
      AND B.COUNTRY = &IC.Country)
AND A.EMPLID = SCRTY.EMPLID
[$SecurityClause]
ORDER BY A.EMPLID

end-SELECT

end-procedure

begin-procedure Native-Local-Visa

! 2 - Citizenship status is Native/Naturalized and has local visa
! (Original PeopleCode message 1000,49)

  new-page
do Get_Field_Information ('PER033',    'HEADING2',     $HEAD2,         #DW)
  let $heading = $HEAD2

begin-SELECT DISTINCT

C.EMPLID    (+1,1,13)


!**********Begin SQR NAME DISPLAY CHANGE ****************
!C.NAME      (0,15)
C.NAME_DISPLAY (0,15)
!**********End SQR NAME DISPLAY CHANGE ****************


FROM PS_PERSON_NAME C, PS_CITIZENSHIP C1, PS_FAST_SQR_SEC_VW SCRTY
WHERE (C1.CITIZENSHIP_STATUS = '1'
       OR C1.CITIZENSHIP_STATUS = '2')
  AND C.EMPLID = C1.EMPLID                              ! 09/00
  AND EXISTS
    (SELECT 'x'
    FROM PS_VISA_PMT_DATA D
    WHERE C.EMPLID=D.EMPLID
      AND D.COUNTRY = &IC.Country
      AND D.DEPENDENT_ID = ' ')
AND C.EMPLID = SCRTY.EMPLID
[$SecurityClause]
ORDER BY C.EMPLID

end-SELECT

end-procedure

begin-procedure Local-Ctz-Local-Visa

! 3 - Country of citizenship is local and has local visa
! (Original PeopleCode message 1000,50)

  new-page
do Get_Field_Information ('PER033',    'HEADING3',     $HEAD3,         #DW)
  let $heading = $HEAD3

begin-SELECT DISTINCT

E.EMPLID    (+1,1,13)


!**********Begin SQR NAME DISPLAY CHANGE ****************
!E.NAME      (0,15)
E.NAME_DISPLAY (0,15)
!**********End SQR NAME DISPLAY CHANGE ****************


FROM PS_PERSON_NAME E, PS_CITIZENSHIP F, PS_FAST_SQR_SEC_VW SCRTY
WHERE E.EMPLID = F.EMPLID
  AND F.DEPENDENT_ID = ' '
  AND F.COUNTRY = &IC.Country
  AND EXISTS
    (SELECT 'x'
    FROM PS_VISA_PMT_DATA G
    WHERE F.EMPLID = G.EMPLID
      AND F.DEPENDENT_ID = G.DEPENDENT_ID
      AND F.COUNTRY = G.COUNTRY)
AND E.EMPLID = SCRTY.EMPLID
[$SecurityClause]
ORDER BY E.EMPLID

end-SELECT

end-procedure

begin-procedure Local-Ctz-Not-Native

! 4 - Country of citizenship is Local and citizenship status is not Native/Naturalized
! (Original PeopleCode message 1000,112)

  new-page
do Get_Field_Information ('PER033',    'HEADING4',     $HEAD4,         #DW)
  let $heading = $HEAD4

begin-SELECT DISTINCT

H.EMPLID    (+1,1,13)


!**********Begin SQR NAME DISPLAY CHANGE ****************
!H.NAME      (0,15)
H.NAME_DISPLAY (0,15)
!**********End SQR NAME DISPLAY CHANGE ****************

FROM PS_PERSON_NAME H, PS_CITIZENSHIP I, PS_FAST_SQR_SEC_VW SCRTY
WHERE I.COUNTRY = &IC.Country
  AND I.EMPLID = H.EMPLID
  AND I.DEPENDENT_ID = ' '
  AND I.CITIZENSHIP_STATUS <> '1'
  AND I.CITIZENSHIP_STATUS <> '2'
  AND I.EMPLID = SCRTY.EMPLID
[$SecurityClause]
ORDER BY H.EMPLID

end-SELECT

end-procedure

begin-procedure No-Local-Ctz-No-Visa

! 5 - Country of citizenship is not local and has no local visa
! (Original PeopleCode message 1000,48)

  new-page
do Get_Field_Information ('PER033',    'HEADING5',     $HEAD5,         #DW)
  let $heading = $HEAD5

! 12/27/99 - I modified this select statement so that it compares the person's
!            country with the country of the location they work at. Otherwise
!            all employees working outside of the country where the company
!            is headquartered will show up as not having citizenship or visa

begin-SELECT DISTINCT

J.EMPLID    (+1,1,13)


!**********Begin SQR NAME DISPLAY CHANGE ****************
!J.NAME      (0,15)
J.NAME_DISPLAY (0,15)
!**********End SQR NAME DISPLAY CHANGE ****************

L.LOCATION

FROM    PS_PERSON_NAME J,
        PS_LOCATION_TBL L,
        PS_JOB JOB,
        PS_FAST_SQR_SEC_VW SCRTY

WHERE   J.EMPLID = JOB.EMPLID
AND     L.LOCATION = JOB.LOCATION
AND     L.SETID = JOB.SETID_LOCATION                ! 09/00
AND     L.COUNTRY <> &IC.Country                    ! 09/00

AND     JOB.EMPL_RCD = (SELECT MIN(JOB1.EMPL_RCD)
                        FROM PS_JOB JOB1
                        WHERE JOB1.EMPLID = JOB.EMPLID)

AND     JOB.EFFDT = (SELECT MAX(JOB2.EFFDT)
                        FROM PS_JOB JOB2
                        WHERE JOB2.EMPLID = JOB.EMPLID
                        AND JOB2.EMPL_RCD = JOB.EMPL_RCD
                        AND JOB2.EFFDT <= $AsOfToday)


AND L.EFFDT =   (SELECT MAX(L1.EFFDT)
                        FROM PS_LOCATION_TBL L1
                        WHERE L1.LOCATION = L.LOCATION
                        AND L1.SETID = JOB.SETID_LOCATION
                        AND L1.EFFDT <= $AsOfToday)


AND NOT EXISTS  (SELECT 'x'
                        FROM PS_CITIZENSHIP K
                        WHERE K.COUNTRY = L.COUNTRY
                        AND K.EMPLID = J.EMPLID
                        AND K.DEPENDENT_ID = ' ')

AND NOT EXISTS  (SELECT 'x'
                         FROM PS_VISA_PMT_DATA M
                        WHERE M.COUNTRY = L.COUNTRY
                        AND M.EMPLID = J.EMPLID
                        AND M.DEPENDENT_ID = ' ')
AND J.EMPLID = SCRTY.EMPLID
[$SecurityClause]

ORDER BY J.EMPLID

end-SELECT

end-procedure

begin-procedure Native-No-Local-Ctz

! 6 - Citizenship status is Native/Naturalized & country of citizenship is not Local

  new-page
do Get_Field_Information ('PER033',    'HEADING6',     $HEAD6,         #DW)
  let $heading = $HEAD6

! 12/27/99 - I modified this select statement so that it compares the person's
!            country with the country of the location they work at. Otherwise
!            all employees working outside of the country where the company
!            is headquartered will show up as not having citizenship or visa

begin-SELECT DISTINCT

MB.EMPLID    (+1,1,13)


!**********Begin SQR NAME DISPLAY CHANGE ****************
!MB.NAME      (0,15)
MB.NAME_DISPLAY (0,15)
!**********End SQR NAME DISPLAY CHANGE ****************

LB.LOCATION

FROM    PS_PERSON_NAME MB, PS_CITIZENSHIP M1,
        PS_LOCATION_TBL LB,
        PS_JOB JOBB,
        PS_FAST_SQR_SEC_VW SCRTY

WHERE MB.EMPLID = M1.EMPLID                       ! 09/00
AND   (M1.CITIZENSHIP_STATUS = '1'
       OR M1.CITIZENSHIP_STATUS = '2')

AND     MB.EMPLID = JOBB.EMPLID
AND     LB.LOCATION = JOBB.LOCATION
AND     LB.SETID = JOBB.SETID_LOCATION            ! 09/00

AND     JOBB.EMPL_RCD = (SELECT MIN(JOBB1.EMPL_RCD)
                        FROM PS_JOB JOBB1
                        WHERE JOBB1.EMPLID = JOBB.EMPLID)

AND     JOBB.EFFDT = (SELECT MAX(JOBB2.EFFDT)
                        FROM PS_JOB JOBB2
                        WHERE JOBB2.EMPLID = JOBB.EMPLID
                        AND JOBB2.EMPL_RCD = JOBB.EMPL_RCD
                        AND JOBB2.EFFDT <= $AsOfToday)


AND LB.EFFDT =  (SELECT MAX(LB1.EFFDT)
                        FROM PS_LOCATION_TBL LB1
                        WHERE LB1.LOCATION = LB.LOCATION
                        AND LB1.SETID = JOBB.SETID_LOCATION
                        AND LB1.EFFDT <= $AsOfToday)

AND NOT EXISTS  (SELECT 'x'
                        FROM PS_CITIZENSHIP N
                        WHERE N.EMPLID =  MB.EMPLID
                        AND N.COUNTRY = LB.COUNTRY
                        AND N.DEPENDENT_ID = ' ')
AND MB.EMPLID = SCRTY.EMPLID
[$SecurityClause]
ORDER BY MB.EMPLID

end-SELECT

end-procedure

#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'stdapi.sqc'    !Routines to update run status

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
#include 'hrsecty.sqc'   !Get SQR Security parameters
