!***********************************************************************
!  PAY711: Print contents of Paygroup / Paygrp Empltype Tables         *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:00:05                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY711' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language
  move $Pay711_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY705A program ids
 do Init_Report_Translation($ReportID,$language_cd)

 do Get_Field_Information ('PAY711', 'REPORT_TITLE',  $Pay711_ReportTitle,   #dummy_width)
 do Get_Field_Information ('PAY711', 'PAYGROUP1',     $Pay711_Paygroup1,     #dummy_width)
 do Get_Field_Information ('PAY711', 'EFFDT1',        $Pay711_EffDt1,        #dummy_width)
 do Get_Field_Information ('PAY711', 'COUNTRY1',      $Pay711_Country1,      #dummy_width)
 do Get_Field_Information ('PAY711', 'DESCRSHORT1',   $Pay711_Descrshort1,   #dummy_width)
 do Get_Field_Information ('PAY711', 'PAY_FREQ1',     $Pay711_Pay_Freq1,     #dummy_width)
 do Get_Field_Information ('PAY711', 'RET_PAYGRP1',   $Pay711_Ret_Paygrp1,   #dummy_width)
 do Get_Field_Information ('PAY711', 'TRANSIT1',      $Pay711_Transit1,      #dummy_width)
 do Get_Field_Information ('PAY711', 'EMPL_TYPE1',    $Pay711_Empl_Type1,    #dummy_width)
 do Get_Field_Information ('PAY711', 'NET_PAY1',      $Pay711_Net_Pay1,      #dummy_width)
 do Get_Field_Information ('PAY711', 'AUTO_OT1',      $Pay711_Auto_OT1,      #dummy_width)
 do Get_Field_Information ('PAY711', 'PAY_RPT1',      $Pay711_Pay_Rpt1,      #dummy_width)
 do Get_Field_Information ('PAY711', 'DFLT_BEN_PGM1', $Pay711_Dflt_Ben_Pgm1, #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_PRGM1',     $Pay711_Ern_Prgm1,     #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_REG_HRS1',  $Pay711_Ern_Reg_Hrs1,  #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_OT_HRS1',   $Pay711_Ern_OT_Hrs1,   #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_REG_ERN1',  $Pay711_Ern_Reg_Ern1,  #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_HOLIDAY1',  $Pay711_Ern_Holiday1,  #dummy_width)
 do Get_Field_Information ('PAY711', 'HOL_NONSTAT1',  $Pay711_Hol_Nonstat1,  #dummy_width)
 do Get_Field_Information ('PAY711', 'HOL_SCHD1',     $Pay711_Hol_Schd1,     #dummy_width)
 do Get_Field_Information ('PAY711', 'EMP_TYPE1',     $Pay711_Emp_Type1,     #dummy_width)
 do Get_Field_Information ('PAY711', 'PRT_PAY_SHT1',  $Pay711_Prt_Pay_Sht1,  #dummy_width)
 do Get_Field_Information ('PAY711', 'CF_REQD_PRTL1', $Pay711_Cf_Reqd_Prtl1, #dummy_width)

 do Get_Field_Information ('PAY711', 'COMPANY2',      $Pay711_Company2,      #dummy_width)
 do Get_Field_Information ('PAY711', 'PAYGROUP2',     $Pay711_Paygroup2,     #dummy_width)
 do Get_Field_Information ('PAY711', 'EFFDT2',        $Pay711_EffDt2,        #dummy_width)
 do Get_Field_Information ('PAY711', 'COUNTRY2',      $Pay711_Country2,      #dummy_width)
 do Get_Field_Information ('PAY711', 'DESCRLONG2',    $Pay711_Descrlong2,    #dummy_width)
 do Get_Field_Information ('PAY711', 'DESCRSHORT2',   $Pay711_Descrshort2,   #dummy_width)
 do Get_Field_Information ('PAY711', 'PAY_FREQ2',     $Pay711_Pay_Freq2,     #dummy_width)
 do Get_Field_Information ('PAY711', 'RET_PAYGRP2',   $Pay711_Ret_Paygrp2,   #dummy_width)
 do Get_Field_Information ('PAY711', 'TRANSIT2',      $Pay711_Transit2,      #dummy_width)
 do Get_Field_Information ('PAY711', 'BANK_CD',       $Pay711_DD_Transit#,   #dummy_width)
 do Get_Field_Information ('PAY711', 'EMPL_TYPE2',    $Pay711_Empl_Type2,    #dummy_width)
 do Get_Field_Information ('PAY711', 'NET_PAY_MIN2',  $Pay711_Net_Pay_Min2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'NET_PAY_MAX2',  $Pay711_Net_Pay_Max2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'AUTO_OT2',      $Pay711_Auto_OT2,      #dummy_width)
 do Get_Field_Information ('PAY711', 'PAY_RPT_SEQ2',  $Pay711_Pay_Rpt_Seq2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'PAY_RPT_SUBT2', $Pay711_Pay_Rpt_SubT2, #dummy_width)
 do Get_Field_Information ('PAY711', 'PAY_EMP_SEQ2',  $Pay711_Pay_Emp_Seq2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'DFLT_BEN_PGM2', $Pay711_Dflt_Ben_Pgm2, #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_PRGM2',     $Pay711_Ern_Prgm2,     #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_REG_HRS2',  $Pay711_Ern_Reg_Hrs2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_OT_HRS2',   $Pay711_Ern_OT_Hrs2,   #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_REG_ERN2',  $Pay711_Ern_Reg_Ern2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'ERN_HOLIDAY2',  $Pay711_Ern_Holiday2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'HOL_NONSTAT2',  $Pay711_Hol_Nonstat2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'HOL_SCHD2',     $Pay711_Hol_Schd2,     #dummy_width)
 do Get_Field_Information ('PAY711', 'EMP_TYPE2',     $Pay711_Emp_Type2,     #dummy_width)
 do Get_Field_Information ('PAY711', 'PRT_PAY_SHT2',  $Pay711_Prt_Pay_Sht2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'CF_REQD_JOB2',  $Pay711_Cf_Reqd_Job2,  #dummy_width)
 do Get_Field_Information ('PAY711', 'CF_REQD_PRTL2', $Pay711_Cf_Reqd_Prtl2, #dummy_width)

 do Get_Field_Information ('PAY711', 'DELAY_WHTLD1',   $Pay711_Delay_Wthld1,  #Del_Wth_width)
 do Get_Field_Information ('PAY711', 'TIPS_ADJUST1',   $Pay711_Tip_Adjust1,   #Tip_Adj_width)
 do Get_Field_Information ('PAY711', 'TIPS_ADJ_ERN1',  $Pay711_Tip_Adj_Ern1,  #Tip_AdjErn_width)
 do Get_Field_Information ('PAY711', 'TIPS_CRD_ERN1',  $Pay711_Tip_Crd_Ern1,  #Tip_CrdErn_width)
 do Get_Field_Information ('PAY711', 'BANK_BRANCH',    $Pay711_BankBranch,   #dummy_width)


end-procedure


begin-HEADING 7

  #Include 'stdhdgtr.sqc'

  print $Pay711_Paygroup1               (+2,5)
  print $Pay711_EffDt1                  (0,9)
  print $Pay711_Country1                (0,21)
  print $Pay711_Descrshort1             (0,55)
  print $Pay711_Pay_Freq1               (0,66)
  print $Pay711_Ret_Paygrp1             (0,74)
  print $Pay711_Empl_Type1              (0,89)
  print $Pay711_Net_Pay1                (0,94)
  print $Pay711_Auto_OT1                (0,110)
  print $Pay711_Pay_Rpt1                (0,117)
  print $Pay711_Dflt_Ben_Pgm1           (0,129)
  print $Pay711_Ern_Prgm1               (0,133)
  print $Pay711_Ern_Reg_Hrs1            (0,137)
  print $Pay711_Ern_OT_Hrs1             (0,141)
  print $Pay711_Ern_Reg_Ern1            (0,145)
  print $Pay711_Ern_Holiday1            (0,149)
  print $Pay711_Hol_Nonstat1            (0,153)
  print $Pay711_Hol_Schd1               (0,157)
  print $Pay711_Emp_Type1               (0,162)
  print $Pay711_Prt_Pay_Sht1            (0,166)
  print $Pay711_Cf_Reqd_Prtl1           (0,174)

  print $Pay711_Company2                (+1,1)
  print $Pay711_Paygroup2               (0,5)
  print $Pay711_EffDt2                  (0,9)
  print $Pay711_Country2                (0,21)
  print $Pay711_Descrlong2              (0,25)
  print $Pay711_Descrshort2             (0,55)
  print $Pay711_Pay_Freq2               (0,66)
  print $Pay711_Ret_Paygrp2             (0,74)
  print $Pay711_Empl_Type2              (0,89)
  print $Pay711_Net_Pay_Min2            (0,95)
  print $Pay711_Net_Pay_Max2            (0,105)
  print $Pay711_Auto_OT2                (0,110)
  print $Pay711_Pay_Rpt_Seq2            (0,115)
  print $Pay711_Pay_Rpt_SubT2           (0,119)
  print $Pay711_Pay_Emp_Seq2            (0,124)
  print $Pay711_Dflt_Ben_Pgm2           (0,129)
  print $Pay711_Ern_Prgm2               (0,133)
  print $Pay711_Ern_Reg_Hrs2            (0,137)
  print $Pay711_Ern_OT_Hrs2             (0,141)
  print $Pay711_Ern_Reg_Ern2            (0,145)
  print $Pay711_Ern_Holiday2            (0,149)
  print $Pay711_Hol_Nonstat2            (0,153)
  print $Pay711_Hol_Schd2               (0,157)
  print $Pay711_Emp_Type2               (0,162)
  print $Pay711_Prt_Pay_Sht2            (0,166)
  print $Pay711_Cf_Reqd_Job2            (0,170)
  print $Pay711_Cf_Reqd_Prtl2           (0,174)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
if $Curr_language_cd = $Psoptions_Language_Cd
  do Process-Main-table
else
  do Process-Main-lang-table
end-if
END-PROCEDURE


BEGIN-PROCEDURE Process-Main-table
BEGIN-SELECT

A.COMPANY        &A.COMPANY    (+1,1,3)
A.PAYGROUP       &A.PAYGROUP   (0,+1,3)

A.EFFDT  &A.EffDt
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                  (0,+1,10)

                    move &A.COMPANY    to $COMPANY
                    move &A.PAYGROUP   to $PAYGROUP
                    move &A.EFFDT      to $EFFDT

A.EFF_STATUS
  if &A.eff_status = 'I'
    print 'I'                  (0,0,1)
  else
    print ' '                  (0,0,1)
  end-if
A.COUNTRY                      (0,+1,3)
A.DESCR                        (0,+1,29)
A.DESCRSHORT                   (0,+1,10)
F.FREQUENCY_TYPE               (0,+2,2)
A.RETIREE_PAYGROUP             (0,+6,3)
A.EMPL_TYPE                    (0,+12,3)
A.NET_PAY_MINIMUM              (0,+1,4)      EDIT B999
A.NET_PAY_MAXIMUM              (0,+3,8)      EDIT B9999999
A.AUTO_OVERTIME                (0,+1,5)      EDIT B9.99
A.PAY_RPT_SEQ                  (0,+2,2)
A.PAY_RPT_SUBTOTALS            (0,+1,2)
A.PAY_RPT_EMPL_SEQ             (0,+3,2)
A.DFLT_BEN_PROGRAM             (0,+2,3)
A.ERN_PROGRAM                  (0,+1,3)
A.ERNCD_REG_HRS                (0,+1,3)
A.ERNCD_OT_HRS                 (0,+1,3)
A.ERNCD_REG_EARNS              (0,+1,3)
A.ERNCD_HOLIDAY                (0,+1,3)
A.ERNCD_HOL_NONSTAT            (0,+1,3)
A.HOLIDAY_SCHEDULE             (0,+1,4)
A.DD_SRC_BANK_ID
A.SRC_BANK_ID


  do Get-EmplTypes

  if &A.DD_Src_bank_id <> ' '
     do Print-Direct-Deposit-Transit#
  end-if

  if &A.Src_bank_id <> ' '
     do Print-Bank-Transit#
  end-if

  PRINT $Pay711_Delay_Wthld1                  (+1,66,#Del_Wth_width)
  PRINT ':'                                   (,94,)
A.TIPS_DELAY_WTHLD                            (,+2,)

  PRINT $Pay711_Tip_Adjust1                   (+1,66,#Tip_Adj_width)
  PRINT ':'                                   (,94,)
A.TIPS_ADJUST                                 (,+2,)

  PRINT $Pay711_Tip_Adj_Ern1                  (+1,66,#Tip_AdjErn_width)
  PRINT ':'                                   (,94,)
A.ERNCD_TIPS_ADJUST                           (,+2,)

  PRINT $Pay711_Tip_Crd_Ern1                  (+1,66,#Tip_CrdErn_width)
  PRINT ':'                                   (,94,)
A.ERNCD_TIPS_CRED                             (,+2,)




FROM PS_PAYGROUP_TBL A, PS_FREQUENCY_TBL F
  WHERE A.PAY_FREQUENCY = F.FREQUENCY_ID
    AND F.EFF_STATUS    = 'A'
    AND F.EFFDT         =
        (SELECT MAX(F1.EFFDT)
         FROM  PS_FREQUENCY_TBL F1
         WHERE F1.FREQUENCY_ID  = F.FREQUENCY_ID
           AND F1.EFF_STATUS    = F.EFF_STATUS
           AND F1.EFFDT        <= A.EFFDT)
ORDER BY A.COMPANY, A.PAYGROUP, A.EFFDT DESC

END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Process-Main-lang-table
BEGIN-SELECT

LA.COMPANY        &LA.COMPANY    (+1,1,3)
LA.PAYGROUP       &LA.PAYGROUP   (0,+1,3)

LA.EFFDT  &LA.EffDt
  do Format-DateTime(&LA.EffDt, $out, {DEFDATE}, '', '')
  print $out                  (0,+1,10)

LA.EFF_STATUS
  if &LA.eff_status = 'I'
    print 'I'                  (0,0,1)
  else
    print ' '                  (0,0,1)
  end-if
LA.COUNTRY                      (0,+1,3)
LA.DESCR          &LA.DESCR      ! (0,+1,29)
LA.DESCRSHORT     &LA.DESCRSHORT ! (0,+1,10)

                    move &LA.COMPANY    to $COMPANY
                    move &LA.PAYGROUP   to $PAYGROUP
                    move &LA.EFFDT      to $EFFDT
                move &LA.DESCR      to $PRNT_DESCR
                move &LA.DESCRSHORT to $PRNT_DESCRSHORT
        do Lang-Record-Details
                    print $PRNT_DESCR      (0,+1,29)
                    print $PRNT_DESCRSHORT (0,+1,10)

FT.FREQUENCY_TYPE               (0,+2,2)
LA.RETIREE_PAYGROUP             (0,+6,3)
LA.EMPL_TYPE                    (0,+12,3)
LA.NET_PAY_MINIMUM              (0,+1,4)      EDIT B999
LA.NET_PAY_MAXIMUM              (0,+3,8)      EDIT B9999999
LA.AUTO_OVERTIME                (0,+1,5)      EDIT B9.99
LA.PAY_RPT_SEQ                  (0,+2,2)
LA.PAY_RPT_SUBTOTALS            (0,+1,2)
LA.PAY_RPT_EMPL_SEQ             (0,+3,2)
LA.DFLT_BEN_PROGRAM             (0,+2,3)
LA.ERN_PROGRAM                  (0,+1,3)
LA.ERNCD_REG_HRS                (0,+1,3)
LA.ERNCD_OT_HRS                 (0,+1,3)
LA.ERNCD_REG_EARNS              (0,+1,3)
LA.ERNCD_HOLIDAY                (0,+1,3)
LA.ERNCD_HOL_NONSTAT            (0,+1,3)
LA.HOLIDAY_SCHEDULE             (0,+1,4)
LA.DD_SRC_BANK_ID
LA.SRC_BANK_ID

  do Get-EmplTypes

  if &LA.DD_Src_bank_id <> ' '
     do Print-Direct-Deposit-Transit#
  end-if

  if &LA.Src_bank_id <> ' '
     do Print-Bank-Transit#
  end-if


  PRINT $Pay711_Delay_Wthld1                  (+1,66,#Del_Wth_width)
  PRINT ':'                                   (,94,)
LA.TIPS_DELAY_WTHLD                           (,+2,)

  PRINT $Pay711_Tip_Adjust1                   (+1,66,#Tip_Adj_width)
  PRINT ':'                                   (,94,)
LA.TIPS_ADJUST                                (,+2,)

  PRINT $Pay711_Tip_Adj_Ern1                  (+1,66,#Tip_AdjErn_width)
  PRINT ':'                                   (,94,)
LA.ERNCD_TIPS_ADJUST                          (,+2,)

  PRINT $Pay711_Tip_Crd_Ern1                  (+1,66,#Tip_CrdErn_width)
  PRINT ':'                                   (,94,)
LA.ERNCD_TIPS_CRED                            (,+2,)



FROM PS_PAYGROUP_TBL LA, PS_FREQUENCY_TBL FT
    WHERE LA.PAY_FREQUENCY = FT.FREQUENCY_ID
      AND FT.EFF_STATUS    = 'A'
      AND FT.EFFDT         =
        (SELECT MAX(FT1.EFFDT)
         FROM  PS_FREQUENCY_TBL FT1
         WHERE FT1.FREQUENCY_ID  = FT.FREQUENCY_ID
           AND FT1.EFF_STATUS    = FT.EFF_STATUS
           AND FT1.EFFDT        <= LA.EFFDT)
ORDER BY LA.COMPANY, LA.PAYGROUP, LA.EFFDT DESC

END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Lang-Record-Details
BEGIN-SELECT
DESCR   &LA1.DESCR
        let $PRNT_DESCR = Cond(Rtrim(&LA1.DESCR,' ')='', &LA.DESCR, &LA1.DESCR)
DESCRSHORT  &LA1.DESCRSHORT
        let $PRNT_DESCRSHORT = Cond(Rtrim(&LA1.DESCRSHORT,' ')='', &LA.DESCRSHORT, &LA1.DESCRSHORT)

FROM PS_PAYGROUP_LANG LA1

WHERE LA1.COMPANY    = &LA.COMPANY
  AND LA1.PAYGROUP   = &LA.PAYGROUP
  AND LA1.EFFDT      = (SELECT MAX(EFFDT) FROM PS_PAYGROUP_LANG
                                 WHERE COMPANY  = &LA.COMPANY
                                   AND PAYGROUP = &LA.PAYGROUP
                                   AND LANGUAGE_CD = $Curr_language_cd
                                   AND EFFDT   <= &LA.EFFDT)
  AND LA1.LANGUAGE_CD = $Curr_language_cd

END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC GET-EMPLTYPES
!         Get data for each employee type within paygroup
!***********************************************************************

BEGIN-PROCEDURE Get-EmplTypes

  move 0 to #count

BEGIN-SELECT

B.COMPANY
B.PAYGROUP
B.EFFDT
B.EMPL_TYPE
  if #count = 0
    print &B.empl_type           (0,+2,1)
    move 999 to #count
  else
    print &B.empl_type           (+1,-13,1)
  end-if

B.PRINT_PAY_SHEET                (0,+3,1)
B.CONF_REQD_JOB                  (0,+3,1)
B.CONF_REQD_PARTIAL              (0,+3,1)

FROM PS_PAYGRP_EMPLTYPE B

WHERE B.COMPANY          = $company
  AND B.PAYGROUP         = $paygroup
  AND B.EFFDT            =
      ( SELECT MAX(EFFDT)
        FROM   PS_PAYGRP_EMPLTYPE
        WHERE  EFFDT = $effdt )

END-SELECT
END-PROCEDURE


!***********************************************************************
!   If there is a direct deposit override bank ID on the Paygroup record
!   Get it's associated bank transit number and print it
!***********************************************************************

BEGIN-PROCEDURE Print-Direct-Deposit-Transit#

BEGIN-SELECT

S1.SRC_BANK_ID
   PRINT $Pay711_DD_Transit#                   (+1,66,#Del_Wth_width)
   PRINT ':'                                   (,94,)
S1.BANK_CD                                     (,+2,)
S1.FORM_ID_ADVICE

FROM PS_PAYGROUP_TBL B1,
      PS_SRC_BANK    S1
WHERE B1.COMPANY         = $COMPANY
  AND B1.PAYGROUP        = $PAYGROUP
  AND B1.EFFDT           = $EFFDT
  AND B1.DD_SRC_BANK_ID  = S1.SRC_BANK_ID

END-SELECT
END-PROCEDURE



!***********************************************************************
!   If there is a source bank ID on the Paygroup record
!   Get it's associated bank transit number and print it
!***********************************************************************

BEGIN-PROCEDURE Print-Bank-Transit#

BEGIN-SELECT

   print $Pay711_Transit1                      (+1,66,)
   print $Pay711_Transit2                      (,+1,)
   print ':'                                   (,94,)
SB.BANK_CD                                     (,+2,9)
   print $Pay711_BankBranch                    (+1,66,)
   print ':'                                   (,94,)
SB.BRANCH_EC_CD                                (,+2,)

FROM PS_PAYGROUP_TBL PG,
      PS_SRC_BANK    SB
WHERE PG.COMPANY         = $COMPANY
  AND PG.PAYGROUP        = $PAYGROUP
  AND PG.EFFDT           = $EFFDT
  AND PG.SRC_BANK_ID     = SB.SRC_BANK_ID

END-SELECT
END-PROCEDURE


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
