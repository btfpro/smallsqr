! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
! XRFMENU: Cross-Reference -- Menu Listing                             *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

#define C_MenuName          1
#define C_BarName           32
#define C_ItemName          43
#define C_ItemType          74
#define C_PnlGrpName        89
#define C_SearchRecName     108
#define C_AddSrchRecName    124
#define C_SearchPnlName     140

begin-setup
#include 'ptset02.sqc'         ! Init printer and page size
end-setup

begin-report
    do Initialize
    do Report
    do Terminate
    do Commit-Transaction
end-report

begin-procedure Initialize            ! Get Source Database Information
    do Init-DateTime
    do Init-Number
    do Define-Prcs-Vars
    do Get-Run-Control-Parms
    move 'XRFMENU'                  to $ReportID
    move 'Menu Listing'             to $ReportTitle
#ifdef debugx
    SHOW 'Report Title: ' $ReportTitle
#endif
    do Create_Code_Arrays
    do Get-Current-DateTime
end-procedure

begin-procedure Terminate
    if #prcs_process_instance > 0
    do Update-Prcs-Run-Status
    end-if
    do Reset
end-procedure

begin-heading 8
   #include 'pthdg01.sqc'
   print 'Window Name'                   (7,{C_MenuName})
   print 'Bar Name'                      (7,{C_BarName})
   print 'Item Name'                     (7,{C_ItemName})
   print 'Item Type'                     (7,{C_ItemType})
   print 'Panel Group Name'              (7,{C_PnlGrpName})
   print 'Search Record'                 (6,{C_SearchRecName})
   print 'Name'                          (7,{C_SearchRecName})
   print 'Add Search'                    (6,{C_AddSrchRecName})
   print 'Record Name'                   (7,{C_AddSrchRecName})
   print 'Detail Panel'                  (6,{C_SearchPnlName})
   print 'Name'                          (7,{C_SearchPnlName})
   print '------------------------------' (8,{C_MenuName})
   print '----------'                    (8,{C_BarName})
   print '------------------------------' (8,{C_ItemName})
   print '--------------'                (8,{C_ItemType})
   print '------------------'            (8,{C_PnlGrpName})
   print '---------------'               (8,{C_SearchRecName})
   print '---------------'               (8,{C_AddSrchRecName})
   print '------------------'            (8,{C_SearchPnlName})
end-heading

begin-procedure Create_Code_Arrays
   create-array  NAME=ItemTypes  SIZE=10  FIELD=ItemType:char
       put 'Add'               into ItemTypes(0)
       put 'Update/Display'    into ItemTypes(1)
       put 'Update All'        into ItemTypes(2)
       put 'Correction'        into ItemTypes(3)
       put 'Delete'            into ItemTypes(4)
       put 'Panel Group'       into ItemTypes(5)
       put 'Process'           into ItemTypes(6)
       put 'Data Entry'        into ItemTypes(7)
       put 'Separator'         into ItemTypes(8)
       put 'PeopleCode'        into ItemTypes(9)
end-procedure

begin-procedure Report          ! Main report processing
begin-SELECT
A.MENUNAME      (+1,{C_MENUNAME}) on-break PRINT=CHANGE/TOP-PAGE LEVEL=1 PROCEDURE=PAGE_BREAK
B.BARNAME                (0,{C_BARNAME})   on-break PRINT=CHANGE/TOP-PAGE LEVEL=2 SKIPLINES=1
B.ITEMNAME               (0,{C_ITEMNAME})
B.ITEMNUM
B.ITEMTYPE
   move &B.ItemType to #ItemType
   GET $ItemType from ITEMTYPES (#ITEMTYPE) ITEMTYPE
   print $ItemType       (0,{C_ItemType})
B.PNLGRPNAME             (0,{C_PNLGRPNAME})
C.SEARCHRECNAME            (0,{C_SEARCHRECNAME})
C.ADDSRCHRECNAME           (0,{C_ADDSRCHRECNAME})
C.SEARCHPNLNAME            (0,{C_SEARCHPNLNAME})

FROM PSMENUDEFN A, PSMENUITEM B, PSPNLGRPDEFN C
WHERE A.MENUNAME= B.MENUNAME
  AND B.PNLGRPNAME = C.PNLGRPNAME
  AND B.MARKET = C.MARKET
ORDER BY A.MENUNAME,B.ITEMNUM
end-SELECT
end-procedure

begin-procedure page_break
  if $End = 'Y'
    do Reset
  end-if
  new-page
end-procedure

#include 'reset.sqc'            ! Reset printer
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#include 'prcsapi.sqc'   !Update Process Request API
