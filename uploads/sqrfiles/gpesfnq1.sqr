!***********************************************************************
!  GPESFNQ1.SQR  Company Certificate                                   *
!***********************************************************************
!----------------------------------------------------------------------
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!-----------------------------------------------------------------------
!                                                                      *
!          $Date:  2006/09/13:01:54:56                                 *
!       $Release:  HR9                                                 *
!      $Revision:  103                                                 *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

!************************************************************************
!  Report Setup
!************************************************************************
begin-setup
!#include 'ptset01.sqc'
!***********************************************************************
!  PTSET01:  Printer and Page-Size Initialization (Portrait)           *
!      Orientation: Portrait                                           *
!      Max Columns: 125                                                *
!      Max Lines:   79                                                 *
!***********************************************************************
! SourceSafe Information:                                              *
!                                                                      *
! $Header:: /PT81/SQR/ptset01.sqc 4     7/26/00 5:10p Jmacnaug        $*
!                                                                      *
!***********************************************************************
#include 'setupdb.sqc'               ! Database specific setup

#define PTSETxx-Included

declare-printer DEFAULT-HP
!#ifdef PRINT_JAPANESE
!      init-string=<27>&t31P
!      font=28825
!      symbol-set=19K
!#endif

    point-size=7.2           !Original setting at ptset01.sqc 7.2
    pitch=17                 !Original setting at ptset01.sqc 17
end-declare

declare-printer DEFAULT-PD
    point-size=7.2
    pitch=17
end-declare

#ifndef EBCDIC
declare-printer DEFAULT-LP
!#ifdef PRINT_JAPANESE
!   init-string=<27>E<27>(19K<27>&l0O<27>&l6C<27>&l0E<27>&I95F<27>(s16.66H<27>&k2G<27>&t31P<27>(s0p7.25v0s0b28825T
!!
!!     <27>(19K : Command to print out single-byte Katakana
!!     <27>&t31P : 2byte mode
!!     <27>(s0p7.25v0s0b28825T : Kanji font
!!
!#else
    init-string=<27>E<27>(0N<27>&l0O<27>&l6C<27>&l0E<27>&l95F<27>(s16.66H<27>&k2G
!               |    |      |       |       |       |        |           |
!               |    |      |       |       |       |        |            --> CR
!               |    |      |       |       |       |         --> Line Prntr font
!               |    |      |       |       |        --> Number of Lines
!               |    |      |       |        --> Top Margin
!               |    |      |        --> Vertical Motion Index (1/48" increments)
!               |    |       --> Portrait orientation
!               |     --> ISO 8859-1 symbol set
!                --> Reset
!#endif
    before-bold=<27>(s3B
    after-bold=<27>(s0B
end-declare

#else

declare-printer DEFAULT-LP
end-declare
#endif

declare-printer DEFAULT-PS
    point-size=7.2
!
! add STARTUP-FILE=dir\filename to change the symbol set
!
end-declare


#define PAGE_ORIENTATION PORTRAIT


#Define ColR 126  !Column # referenced by Standard Headings. Original value: 108

! Define Parameters
#if {PAPER_SIZE} = 'LETTER'
    #define PAGE_PAPER_SIZE (LETTER)
    #define LINE_HEIGHT 7
    #define CHAR_WIDTH  4.32

#End-if

#if {PAPER_SIZE} = 'A4'

    #define PAGE_PAPER_SIZE (A4)
    #define LINE_HEIGHT 8          !Original setting at ptset01.sqc: 9
    #define CHAR_WIDTH   4.15      !Original setting at ptset01.sqc: 4.32

#endif

declare-layout DEFAULT
    paper-size={PAGE_PAPER_SIZE}
    orientation={PAGE_ORIENTATION}
    line-height={LINE_HEIGHT}              ! 72/printer_point-size
    char-width={CHAR_WIDTH}                ! points, to handle max cols
    left-margin=.01
    top-margin=.05
    bottom-margin=.01
end-declare

declare-report DEFAULT
    layout=DEFAULT
    printer-type=HP
end-declare

!** Allow for 23.3 big decimals
!** To turn on: add "#define DECIMAL26" to your code before calling this sqc.
#ifdef DECIMAL26
    declare-variable
        Default-Numeric=DECIMAL(26)
    end-declare
#end-if


DECLARE-IMAGE WorkMinistery
  type         = BMP-FILE
  image-size   = (32,7)   !10,5
  source       = 'mntrabjo.bmp'
end-declare

DECLARE-IMAGE INEM
  type         = BMP-FILE
  image-size   = (13,5)
  source       = 'inem.bmp'
end-declare

end-setup


#include 'sqrtrans.sqc'


#define ACTN_REASON_TBL
#define EMPL_CTG_L1
!#define STATE_NAMES_TBL
#define JOBCODE_TBL
#define COMPANY_TBL
#define RGM_BSE_ESP_TBL
#Include 'rellang.sqc'   ! Translations File 

!************************************************************************
! Report Section
!************************************************************************
begin-report
#debug do Fin-Debug-Msg('begin-report')

  do Init-Report
  do Process-Main
  do Stdapi-Term

end-report


!************************************************************************
! Procedure Init-Report
!************************************************************************
begin-procedure Init-Report
#debug do Fin-Debug-Msg('init-report')
  move 'GPESFNQ1' to $ReportID
  move 'Certificado de Empresa' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do Stdapi-Init

end-procedure


!************************************************************************
! Procedure Main
!************************************************************************
begin-procedure Process-Main
#debug do Fin-Debug-Msg('Process-Main')

  #debugd show 'Operator Id:     ' $prcs_oprid
  #debugd show 'Run Control Id:  ' $prcs_run_cntl_id
  
  let $Responsible-Name = ''
  let $Responsible-DNI = ''
  let $Reponsible-JobCode = ''

let $sql-statement = 'GPESFNQ1.SQR,Process-Main,Select,PS_GPES_RC_FNQ1'
Begin-SELECT on-error=SQL-Error
A.RESPONSIBLE_ID  &RESPONSIBLE_ID
A.EMPL_RCD        &RESPONSIBLE_RCD
A.ASOFDATE        &ASOFDATE
A.LANGUAGE_CD
  let $Curr_Language_Cd = &A.LANGUAGE_CD
  
B.EMPLID
B.EMPL_RCD
  
  let $Select-ASOFDATE = &ASOFDATE

  if $Responsible-Name = ''
     let $Select-EMPLID = &RESPONSIBLE_ID
     Do RESPONSIBLE-JOB
     Do PERSONAL-DATA
     let $Responsible-Name = $Empl-Name
     let $Responsible-DNI = $Empl-DNI
     let $Reponsible-JobCode = $Empl-JobCode-Desc
  else
     new-page
  end-if

  let $Select-EMPLID = &B.EMPLID
  let #Select-EMPL-RCD = &B.EMPL_RCD
  Do Check-Empl-Termtn-Dt
  if $Empl-Terminated = 'Y'
    Do JOB
    Do PERSONAL-DATA
    Do COMPANY-TBL
    Do TERMINATION-DATA
    Do Print-Certificate
    Do Print-Contribution-Bases
  end-if


FROM PS_GPES_RC_FNQ1 A
    ,PS_GPES_RC_FNQ1_EE B

WHERE A.OPRID = $prcs_oprid
 AND  A.RUN_CNTL_ID = $prcs_run_cntl_id
 AND  B.OPRID = A.OPRID
 AND  B.RUN_CNTL_ID = A.RUN_CNTL_ID
End-SELECT

  Do STDAPI-TERM
  
end-procedure

!************************************************************************
! Procedure Responsible-TERMINATION-DATA
!************************************************************************
begin-procedure TERMINATION-DATA
#debug do Fin-Debug-Msg('TERMINATION-DATA')

let #days_lit = 0
let $days_lit = ''

let $sql-statement = 'GPESFNQ1.SQR,TERMINATION-DATA,Select,PS_GPES_TERM_MNG'
Begin-SELECT on-error=SQL-Error
TERMINATION_DT 
GPES_FINAL_DT

  do Convert-To-DTU-Date(&TERMINATION_DT ,$TERMINATION_DT)        
  do Convert-To-DTU-Date(&GPES_FINAL_DT ,$GPES_FINAL_DT )        
  do dtu-add-days($TERMINATION_DT, 1, $TERMINATION_DT)
  do dtu-diff-days($TERMINATION_DT, $GPES_FINAL_DT,  #days_lit)  
  do Format-DateTime(&TERMINATION_DT, $TERMINATION_DT-PRINT, {DEFDMY}, '', '')
  do Format-DateTime(&GPES_FINAL_DT, $GPES_FINAL_DT-PRINT, {DEFDMY}, '', '')
  let $days_lit = to_char(#days_lit)


FROM PS_GPES_TERM_MNG
WHERE EMPLID = $Select-EMPLID
 AND EMPL_RCD = #Select-EMPL-RCD
 AND TERMINATION_DT < $Select-ASOFDATE
 AND GPES_FINAL_DT IS NOT NULL

end-SELECT


End-Procedure !TERMINATION-DATA

!************************************************************************
! Procedure Responsible-JOB
!************************************************************************
begin-procedure Responsible-JOB
#debug do Fin-Debug-Msg('Responsible-JOB')

let $sql-statement = 'GPESFNQ1.SQR,Responsible-JOB,Select,PS_JOB'
begin-SELECT on-error=SQL-Error
J.EMPLID
J.EMPL_RCD
J.EFFDT
J.EFFSEQ
J.SETID_JOBCODE
J.JOBCODE
  let $JC-EffDt = &J.EFFDT
  let $JC-SetID-JobCode = &J.SETID_JOBCODE
  let $JC-JobCode = &J.JOBCODE

  Do Get-JobCode-Descr
  
  EXIT-SELECT

FROM PS_JOB J

WHERE J.EMPLID = &RESPONSIBLE_ID
  AND J.EMPL_RCD = &RESPONSIBLE_RCD
  AND J.EFFDT <= $Select-ASOFDATE

ORDER BY J.EMPLID, J.EMPL_RCD, J.EFFDT DESC, J.EFFSEQ DESC
end-SELECT

  #debugd show 'Employee:                ' &RESPONSIBLE_ID
  #debugd show 'Employee record:         ' &RESPONSIBLE_RCD
  #debugd show 'Effective Date:          ' &ASOFDATE

end-procedure


!************************************************************************
! Procedure Check-Empl-Termtn-Dt
!************************************************************************
begin-procedure Check-Empl-Termtn-Dt
#debug do Fin-Debug-Msg('Check-Empl-Termtn-Dt')

  let $Empl-Terminated = 'N'
  Let $Job-EffDt-Select = ''
  Let #Job-EffSeq-Select = 0
  
  #debugd show 'Employee:                ' $Select-EMPLID
  #debugd show 'Employee record:         ' #Select-EMPL-RCD

let $sql-statement = 'GPESFNQ1.SQR,Check-Empl-Termtn-Dt,Select,PS_EMPLOYMENT'
begin-SELECT on-error=SQL-Error
JTERM.EMPLID
JTERM.EMPL_RCD
JTERM.EFFDT
JTERM.EFFSEQ
   !{DateTimeOut-Prefix}JTERM.EFFDT{DateTimeOut-Suffix}       &Hire-Dt-Wk
   !{DateTimeOut-Prefix}JTERM.EFFDT - 1{DateTimeOut-Suffix}   &Termination-Dt-Wk
JTERM.ACTION

  #debugd show 'Effective Date: ' &JTERM.EFFDT
  #debugd show 'Action:         ' &JTERM.ACTION

  If &JTERM.ACTION = 'TER'
    Let $Empl-Terminated   =  'Y'
    do Convert-To-DTU-Date(&JTERM.EFFDT, $Termination-Dt-Wk)
    do dtu-subtract-days( $Termination-Dt-Wk , 1 , $Termination-Dt)
    do Convert-From-DTU-Date($Termination-Dt, $Termination-Dt)
    #debugd show 'Termination Dt: ' $Termination-Dt
    Let $Job-EffDt-Select  =  &JTERM.EFFDT
    Let #Job-EffSeq-Select =  &JTERM.EFFSEQ
  else
    If &JTERM.ACTION = 'HIR' Or &JTERM.ACTION = 'REH'
      Let $Hire-Dt         =  &JTERM.EFFDT
      If $Empl-Terminated = 'Y'
        EXIT-SELECT
      end-if
    end-if
  end-if

FROM PS_JOB JTERM

WHERE JTERM.EMPLID = $Select-EMPLID
  AND JTERM.EMPL_RCD = #Select-EMPL-RCD
  AND JTERM.EFFDT <= $Select-ASOFDATE
  AND JTERM.ACTION IN ('HIR','REH','TER')

ORDER BY JTERM.EMPLID, JTERM.EMPL_RCD, JTERM.EFFDT DESC, JTERM.EFFSEQ DESC
end-SELECT

  #debugd show 'Employee:                ' $Select-EMPLID
  #debugd show 'Employee record:         ' #Select-EMPL-RCD
  #debugd show 'Employee hire/rehire dt: ' $Hire-Dt
  #debugd show 'Employee Termination dt: ' $Termination-Dt

end-procedure


!************************************************************************
! Procedure JOB
!************************************************************************
begin-procedure JOB
#debug do Fin-Debug-Msg('JOB')

  let $Action_Reason_Desc = ''
  let $Empl-Ctg-Desc = ''
  let $Empl-SS-Grp-Desc = ''
  let $Empl-JobCode-Desc = ''
  let $Cmpny-Ssn = ''

let $sql-statement = 'GPESFNQ1.SQR,JOB,Select,PS_JOB'
begin-SELECT on-error=SQL-Error
JOB.EMPLID
JOB.EMPL_RCD
JOB.EFFDT
JOB.COMPANY
JOB.SETID_LOCATION
JOB.LOCATION
JOB.REG_REGION
JOB.FULL_PART_TIME
JOB.GP_PAYGROUP
  Let $Full-Part-Time = &JOB.FULL_PART_TIME
  Let $Pay_Group = &JOB.GP_PAYGROUP
  If $Full-Part-Time = 'P'
     Do Get-Schedule-Days
  End-If

JOB.SETID_LBR_AGRMNT
  let $SetId_Lbr_Agrmnt = &JOB.SETID_LBR_AGRMNT

JOB.LABOR_AGREEMENT
JOB.EMPL_CTG
JOB.CONTRACT_NUM

JOB.ESTABID
  let $EstabID = rtrim(&JOB.ESTABID, ' ')
  
JOB.ACTION
  let $FieldName = 'ACTION'
  let $FieldValue = &JOB.ACTION
  let $AsOfDate = $Select-ASOFDATE
  Do Read-Translate-Table
  let $Action_Reason_Desc = $XLatLongName

JOB.ACTION_REASON
JOBJR.SSN_EMPLOYER
  let $Cmpny-Ssn = &JOBJR.SSN_EMPLOYER

CNTRCT.SCHEME_ID_ESP
CNTRCT.CONTRIB_ID_ESP
  let $SchemeId = &CNTRCT.SCHEME_ID_ESP
  let $ContribId = &CNTRCT.CONTRIB_ID_ESP

JOB.SETID_JOBCODE
JOB.JOBCODE
  let $JC-EffDt = &JOB.EFFDT
  let $JC-SetID-JobCode = &JOB.SETID_JOBCODE
  let $JC-JobCode = &JOB.JOBCODE
  Do Get-JobCode-Descr

  Do Get-Action-Reason-Descr
  Do Get-Empl-Ctg-Descr


FROM PS_JOB JOB
    ,PS_JOB_JR JOBJR
    ,PS_CONTRACT_DATA CNTRCT
    

WHERE JOB.EMPLID = $Select-EMPLID
  AND JOB.EMPL_RCD = #Select-EMPL-RCD
  AND JOB.EFFDT = $Job-EffDt-Select
  AND JOB.EFFSEQ = #Job-EffSeq-Select
  AND JOBJR.EMPLID = JOB.EMPLID
  AND JOBJR.EMPL_RCD = JOB.EMPL_RCD
  AND JOBJR.EFFDT = JOB.EFFDT
  AND JOBJR.EFFSEQ = JOB.EFFSEQ
  AND CNTRCT.EMPLID = JOB.EMPLID
  AND CNTRCT.CONTRACT_NUM = JOB.CONTRACT_NUM
end-SELECT

  #debugd show 'Employee:        ' $Select-EMPLID
  #debugd show 'Employee record: ' #Select-EMPL-RCD
  #debugd show 'As Of:           ' $Select-ASOFDATE
  #debugd show 'Establishment:   ' $EstabID

end-procedure


!************************************************************************
! Procedure Get-Schedule-Days
!************************************************************************
begin-procedure Get-Schedule-Days
#debug do Fin-Debug-Msg('Get-Schedule-Days')

let $Schedule_SetID = ''
let $Schedule_ID = ''
let $Schedule_Days = 0

let $sql-statement = 'GPESFNQ1.SQR,Get-Schedule-Days,Select,PS_GP_PYGRP_DTL'
begin-SELECT on-error=SQL-Error
PGRP.GP_PAYGROUP
PGRP.EFFDT
PGRP.SETID
PGRP.SCHEDULE_ID

  let $Schedule_SetID = &PGRP.SETID
  let $Schedule_ID = &PGRP.SCHEDULE_ID

FROM PS_GP_PYGRP_DTL  PGRP
WHERE PGRP.GP_PAYGROUP = $Pay_Group
 AND PGRP.EFFDT = (SELECT MAX(EFFDT) FROM PS_GP_PYGRP_DTL 
                    WHERE GP_PAYGROUP = PGRP.GP_PAYGROUP
                      AND EFFDT <= $Select-ASOFDATE)
end-SELECT
  
  #debugd show 'Paygroup:       ' $Pay_Group
  #debugd show 'Paygroup Effdt: ' &PGRP.EFFDT
  #debugd show 'Schedule Setid: ' $Schedule_SetID
  #debugd show 'Schedule ID:    ' $Schedule_ID


let $sql-statement = 'GPESFNQ1.SQR,Get-Schedule-Days,Select,PS_GP_PYGRP_DTL'
begin-SELECT on-error=SQL-Error
COUNT(*)     &SCHEDULE_DAYS

  let $Schedule_Days = &SCHEDULE_DAYS


FROM PS_SCH_DEFN_DTL  SCHD
WHERE SCHD.SETID = $Schedule_SetID
 AND SCHD.SCH_ADHOC_IND = '1'
 AND SCHD.SCHEDULE_ID = $Schedule_ID
 AND SCHD.EFFDT = (SELECT MAX(EFFDT) FROM PS_SCH_DEFN_DTL
                    WHERE SETID = SCHD.SETID
                      AND SCH_ADHOC_IND = SCHD.SCH_ADHOC_IND
                      AND SCHEDULE_ID = SCHD.SCHEDULE_ID
                      AND EFFDT <= $Select-ASOFDATE)
 AND OFFDAY_IND = 'N'
end-SELECT

  #debugd show 'Schedule Days:    ' $Schedule_Days  

end-procedure


!************************************************************************
! Procedure Get-Action-Reason-Descr
!************************************************************************
begin-procedure Get-Action-Reason-Descr
#debug do Fin-Debug-Msg('Get-Action-Reason-Descr')

let $sql-statement = 'GPESFNQ1.SQR,Get-Action-Reason-Descr,Select,PS_ACTN_REASON_TBL'
begin-SELECT on-error=SQL-Error
ACTNRSN.ACTION
ACTNRSN.ACTION_REASON
ACTNRSN.EFFDT
ACTNRSN.DESCR

  let $Actn_Reason = rtrim(&ACTNRSN.ACTION_REASON, ' ')
  let $ACTN_REASON_TBL-DESCR = rtrim(&ACTNRSN.DESCR, ' ')
  Do Get_Related_ACTN_REASON_TBL(&ACTNRSN.ACTION,&ACTNRSN.ACTION_REASON,&ACTNRSN.EFFDT)
  let $Action_Reason_Desc = $ACTN_REASON_TBL-DESCR
  let $Action_Reason_Desc = $Actn_Reason || ' - ' || $Action_Reason_Desc


FROM PS_ACTN_REASON_TBL ACTNRSN

WHERE ACTNRSN.ACTION = &JOB.ACTION
 AND  ACTNRSN.ACTION_REASON = &JOB.ACTION_REASON
 AND  ACTNRSN.EFFDT = (SELECT MAX(EFFDT)
                        FROM PS_ACTN_REASON_TBL ACTNRSN2
                        WHERE ACTNRSN2.ACTION = ACTNRSN.ACTION
                         AND  ACTNRSN2.ACTION_REASON = ACTNRSN.ACTION_REASON
                         AND  ACTNRSN2.EFFDT <= $Select-ASOFDATE)
 AND  ACTNRSN.EFF_STATUS <> 'I'

end-SELECT
  
  #debugd show 'Action:         ' &JOB.ACTION
  #debugd show 'Action reason:  ' &JOB.ACTION_REASON
  #debugd show 'Effective date: ' &ACTNRSN.EFFDT
  #debugd show 'Descr:          ' $Action_Reason_Desc
  #debugd show 'Process Lang:   ' $Curr_Language_Cd
  #debugd show 'Base Lang:      ' $PSoptions_language_cd

end-procedure


!************************************************************************
! Procedure Get-Empl-Ctg-Descr
!************************************************************************
BEGIN-PROCEDURE Get-Empl-Ctg-Descr
#debug do Fin-Debug-Msg('Get-Empl-Ctg-Descr')

 ! Do Get-setid(&JOB.REG_REGION, 'EMPL_CTG_L1', $SetId_Lbr_Agrmnt)

let $Empl-SS-Grp-Cd = ''

let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Ctg-Descr,Select,PS_EMPL_CTG_L1'
BEGIN-SELECT on-error=SQL-Error
CTG.SETID
CTG.LABOR_AGREEMENT
CTG.EMPL_CTG
CTG.EFFDT
CTG.DESCR
CTG.SOC_SEC_WRK_GRP_CD

  let $EMPL_CTG_L1-DESCR = rtrim(&CTG.DESCR, ' ')
  Do Get_Related_EMPL_CTG_L1(&CTG.EFFDT,&CTG.EMPL_CTG,&CTG.LABOR_AGREEMENT,&CTG.SETID)
  let $Empl-Ctg-Desc = $EMPL_CTG_L1-DESCR
  
  LET $Empl-SS-Grp-Cd = &CTG.SOC_SEC_WRK_GRP_CD


FROM PS_EMPL_CTG_L1 CTG

  WHERE CTG.SETID = $SetId_Lbr_Agrmnt
   AND  CTG.LABOR_AGREEMENT = &JOB.LABOR_AGREEMENT
   AND  CTG.EMPL_CTG = &JOB.EMPL_CTG
   AND  CTG.EFFDT = (SELECT MAX(EFFDT)
                        FROM PS_EMPL_CTG_L1
                        WHERE SETID = CTG.SETID
                         AND  LABOR_AGREEMENT = CTG.LABOR_AGREEMENT
                         AND  EFFDT <= $Select-ASOFDATE)

END-SELECT


let $Soc-Sec-Cntb-Freq = ''

let $sql-statement = 'GPESFNQ1.SQR,Get-SSec-Wrk-Grp,Select,PS_RGM_BSE_ESP_TBL'
BEGIN-SELECT on-error=SQL-Error
SSWGRP.CURRENCY_CD
SSWGRP.EFFDT
SSWGRP.DESCR50
SSWGRP.WRK_GRP_FREQ_ESP

  
  let $RGM_BSE_ESP_TBL-DESCR50 = rtrim(&SSWGRP.DESCR50, ' ')
  Do Get_Related_RGM_BSE_ESP_TBL(&SSWGRP.CURRENCY_CD,&SSWGRP.EFFDT,$SchemeID,$Empl-SS-Grp-Cd)
  let $Empl-SS-Grp-Desc = $Empl-SS-Grp-Cd || ' - ' || $RGM_BSE_ESP_TBL-DESCR50
  LET $Soc-Sec-Cntb-Freq = &SSWGRP.WRK_GRP_FREQ_ESP


FROM PS_RGM_BSE_ESP_TBL SSWGRP

  WHERE SSWGRP.SCHEME_ID_ESP = $SchemeID
   AND  SSWGRP.SOC_SEC_WRK_GRP_CD = $Empl-SS-Grp-Cd
   AND  SSWGRP.EFFDT = (SELECT MAX(EFFDT)
                        FROM PS_RGM_BSE_ESP_TBL
                        WHERE SCHEME_ID_ESP = SSWGRP.SCHEME_ID_ESP
                         AND  SOC_SEC_WRK_GRP_CD = SSWGRP.SOC_SEC_WRK_GRP_CD
                         AND  EFFDT <= $Select-ASOFDATE)

END-SELECT

  #debugd show 'Labor agreement setid: ' $SetId_Lbr_Agrmnt
  #debugd show 'Labor agreement:       ' &JOB.LABOR_AGREEMENT
  #debugd show 'EE Categorization Cd:  ' &JOB.EMPL_CTG
  #debugd show 'Descr:                 ' $Empl-Ctg-Desc
  #debugd show 'Scheme ID:             ' $SchemeID
  #debugd show 'Social Security Group: ' $Empl-SS-Grp-Cd
  #debugd show 'Soc Sec Grp Descr:     ' $Empl-SS-Grp-Desc
  #debugd show 'Effective date:        ' &SSWGRP.EFFDT
  #debugd show 'Process Lang:          ' $Curr_Language_Cd
  #debugd show 'Base Lang:             ' $PSoptions_language_cd
  #debugd show 'Contribution Freq:     ' $Soc-Sec-Cntb-Freq

END-PROCEDURE


!************************************************************************
! Procedure Get-Jobcode-Descr
!************************************************************************
begin-procedure Get-JobCode-Descr
#debug do Fin-Debug-Msg('Get-JobCode-Descr')

let $sql-statement = 'GPESFNQ1.SQR,Get-JobCode-Descr,Select,PS_JOBCODE_TBL'
begin-SELECT on-error=SQL-Error
JOBCODE.SETID
JOBCODE.JOBCODE
JOBCODE.EFFDT
JOBCODE.DESCR

  let $JOBCODE_TBL-DESCR = rtrim(&JOBCODE.DESCR, ' ')
  Do Get_Related_JOBCODE_TBL(&JOBCODE.EFFDT,&JOBCODE.JOBCODE,&JOBCODE.SETID)
  let $Empl-JobCode-Desc = $JOBCODE_TBL-DESCR


FROM PS_JOBCODE_TBL JOBCODE

WHERE JOBCODE.SETID = $JC-SetID-JobCode
  AND JOBCODE.JOBCODE = $JC-JobCode
  AND JOBCODE.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL JOBCODE2
                        WHERE JOBCODE2.SETID = JOBCODE.SETID
                         AND  JOBCODE2.JOBCODE = JOBCODE.JOBCODE
                         AND  JOBCODE2.EFFDT <= $Select-ASOFDATE)
  AND JOBCODE.EFF_STATUS <> 'I'
end-SELECT

  #debugd show 'Job Code Setid: ' $JC-SetID-JobCode
  #debugd show 'Job Code:       ' $JC-JobCode
  #debugd show 'Effective date: ' $JC-EffDt
  #debugd show 'Description:    ' $Empl-JobCode-Desc
  #debugd show 'Process Lang:   ' $Curr_Language_Cd
  #debugd show 'Base Lang:      ' $PSoptions_language_cd
  
end-procedure

       
!************************************************************************
! Procedure PERSONAL-DATA
!************************************************************************
begin-procedure PERSONAL-DATA
#debug do Fin-Debug-Msg('PERSONAL-DATA')

  let $Empl-Name = ''
  let $Empl-Address = ''
  let $Empl-City = ''
  let $Empl-State-Desc = ''
  let $Empl-Postal = ''
  let $Empl-Mar-Status-Desc = ''
  let $Empl-DNI = ''
  let $Empl-SSN = ''

let $sql-statement = 'GPESFNQ1.SQR,PERSONAL-DATA,Select,PS_PERSONAL_DT_FST'
begin-SELECT on-error=SQL-Error
PDTA.NAME
  let $Empl-Name = &PDTA.NAME
 
PDTA.MAR_STATUS
  let $FieldName = 'MAR_STATUS'
  let $FieldValue = &PDTA.MAR_STATUS
  let $AsOfDate = $Select-ASOFDATE
  Do Read-Translate-Table
  let $Empl-Mar-Status-Desc = $XLatLongName

  Do Get-Empl-Address
  Do Get-Empl-Nid


FROM PS_PERSONAL_DT_FST PDTA

WHERE PDTA.EMPLID = $Select-EMPLID

end-SELECT

  #debugd show 'Employee ID: ' $Select-EMPLID
  #debugd show 'Employee name: ' $Empl-Name
  #debugd show 'Employee address: ' $Empl-address
  #debugd show 'Employee city: ' $Empl-City
  #debugd show 'Employee state: ' $Empl-State-Desc
  #debugd show 'Employee postal: ' $Empl-Postal
  #debugd show 'Marital Status: ' $Empl-Mar-Status-Desc

end-procedure


!************************************************************************
! Procedure Get-Empl-Address
!************************************************************************
begin-procedure Get-Empl-Address
#debug do Fin-Debug-Msg('Get-Empl-Address')

let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Address,Select,PS_PERSON_ADDRESS'
begin-SELECT on-error=SQL-Error
ADDRT.ADDRESS_TYPE
ADDRT.ORDER_BY_SEQ

PADDR.COUNTRY
PADDR.ADDRESS1
PADDR.ADDRESS2
PADDR.ADDRESS3
PADDR.ADDRESS4
PADDR.CITY
PADDR.NUM1
PADDR.NUM2
PADDR.HOUSE_TYPE
PADDR.ADDR_FIELD1
PADDR.ADDR_FIELD2
PADDR.ADDR_FIELD3
PADDR.COUNTY
PADDR.STATE
PADDR.POSTAL
PADDR.GEO_CODE
PADDR.IN_CITY_LIMIT
  let $Empl-ADDLINE2=LTRIM(RTRIM(&PADDR.ADDR_FIELD1,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.ADDRESS1,' ')||' ',' ')
  let $Empl-ADDLINE3=LTRIM(RTRIM(&PADDR.ADDRESS2,' ')||' ',' ')
  let $Empl-ADDLINE31=LTRIM(RTRIM(&PADDR.ADDR_FIELD2,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.ADDR_FIELD3,' ')||' ',' ')
  let $Empl-ADDLINE4=LTRIM(RTRIM(&PADDR.ADDRESS4,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.NUM1,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.NUM2,' ')||' ',' ')||LTRIM(RTRIM(&PADDR.HOUSE_TYPE,' ')||' ',' ')

  let $Empl-address = $Empl-ADDLINE2||$Empl-ADDLINE3||$Empl-ADDLINE31||$Empl-ADDLINE4
  let $Empl-City = &PADDR.CITY
  let $Country = &PADDR.COUNTRY
  let $State = &PADDR.STATE
  Do State-Names-Tbl
  let $Empl-State-Desc = $State-Desc
  let $Empl-Postal = &PADDR.POSTAL
  
  #debugd show 'Address Seq#: ' &ADDRT.ORDER_BY_SEQ
  #debugd show 'Address Type: ' &ADDRT.ADDRESS_TYPE
  
  EXIT-SELECT

FROM PS_PERSON_ADDRESS PADDR
   , PS_ADDRESS_TYP_TBL ADDRT

WHERE PADDR.EMPLID = $Select-EMPLID
  AND PADDR.ADDRESS_TYPE = ADDRT.ADDRESS_TYPE

ORDER BY ADDRT.ORDER_BY_SEQ

end-SELECT

end-procedure

!**********************************************************************!
! PROCEDURE:     GET-EMPL_NID                                          !
! DESCRIPTION:   GET EMPLOYEE NATIONAL ID                              !
!**********************************************************************!

BEGIN-PROCEDURE  Get-Empl-Nid
#debug do Fin-Debug-Msg('Get-Empl-Nid')

let $National_type = 'DNI'
let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Nid,Select,PS_PERS_NID (DNI)'
BEGIN-SELECT on-error=SQL-Error
NID1.NATIONAL_ID

  let $Empl-DNI = &NID1.NATIONAL_ID


FROM PS_PERS_NID NID1

WHERE NID1.EMPLID = $Select-EMPLID
 AND  NID1.COUNTRY = &PADDR.COUNTRY
 AND  NID1.NATIONAL_ID_TYPE = $National_type

END-SELECT


let $National_type = 'SSN'
let $sql-statement = 'GPESFNQ1.SQR,Get-Empl-Nid,Select,PS_PERS_NID (SSN)'
BEGIN-SELECT on-error=SQL-Error
NID2.NATIONAL_ID

  let $Empl-SSN = &NID2.NATIONAL_ID


FROM PS_PERS_NID NID2

WHERE NID2.EMPLID = $Select-EMPLID
 AND  NID2.COUNTRY = &PADDR.COUNTRY
 AND  NID2.NATIONAL_ID_TYPE = $National_type

END-SELECT

  #debugd show 'Employee id: ' $Select-EMPLID
  #debugd show 'Country: ' &PADDR.COUNTRY
  #debugd show 'DNI: ' $Empl-DNI
  #debugd show 'SSN: ' $Empl-SSN

END-PROCEDURE


!************************************************************************
! Procedure COMPANY-TBL
!************************************************************************
begin-procedure COMPANY-TBL
#debug do Fin-Debug-Msg('COMPANY-TBL')

  let $Cmpny-Descr = ''
  let $Cmpny-Address = ''
  let $Cmpny-City = ''
  let $Cmpny-Postal = ''
  let $Cmpny-State-Desc = ''
  let $Cmpny-Indstry-Desc = ''

  let $Cntrct-Type-Desc = ''
  let $Wrk-Center-Addr = ''
  
  let $SSN_Empl_Type = ''

let $sql-statement = 'GPESFNQ1.SQR,COMPANY-TBL,Select,PS_COMPANY_TBL'
begin-SELECT on-error=SQL-Error
CMPNY.COMPANY
CMPNY.EFFDT
CMPNY.DESCR
CMPNY.COUNTRY
CMPNY.ADDRESS1
CMPNY.ADDRESS2
CMPNY.ADDRESS3
CMPNY.ADDRESS4
CMPNY.CITY
CMPNY.NUM1
CMPNY.NUM2
CMPNY.HOUSE_TYPE
CMPNY.ADDR_FIELD1
CMPNY.ADDR_FIELD2
CMPNY.ADDR_FIELD3
CMPNY.COUNTY
CMPNY.STATE
CMPNY.POSTAL
CMPNY.GEO_CODE
CMPNY.IN_CITY_LIMIT
  let $Cmpny-ADDLINE2=LTRIM(RTRIM(&CMPNY.ADDR_FIELD1,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.ADDRESS1,' ')||' ',' ')
  let $Cmpny-ADDLINE3=LTRIM(RTRIM(&CMPNY.ADDRESS2,' ')||' ',' ')
  let $Cmpny-ADDLINE31=LTRIM(RTRIM(&CMPNY.ADDR_FIELD2,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.ADDR_FIELD3,' ')||' ',' ')
  let $Cmpny-ADDLINE4=LTRIM(RTRIM(&CMPNY.ADDRESS4,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.NUM1,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.NUM2,' ')||' ',' ')||LTRIM(RTRIM(&CMPNY.HOUSE_TYPE,' ')||' ',' ')

  let $COMPANY_TBL-DESCR = rtrim(&CMPNY.DESCR, ' ')
  Do Get_Related_COMPANY_TBL(&CMPNY.COMPANY,&CMPNY.EFFDT)
  let $Cmpny-Descr = $COMPANY_TBL-DESCR
  
  let $Cmpny-address = $Cmpny-ADDLINE2||$Cmpny-ADDLINE3||$Cmpny-ADDLINE31||$Cmpny-ADDLINE4
  let $Cmpny-City = &CMPNY.CITY
  let $Cmpny-Postal = RTRIM(&CMPNY.POSTAL, ' ')
  let $Country = &CMPNY.COUNTRY
  let $State = &CMPNY.STATE
  Do State-Names-Tbl
  let $Cmpny-State-Desc = $State-Desc

  let $Cmpny-address-Cmp = $Cmpny-address||'  '||RTRIM($Cmpny-City, ' ')||'  ('||RTRIM(&CMPNY.POSTAL, ' ')||')'
  

FROM PS_COMPANY_TBL CMPNY

WHERE CMPNY.COMPANY = &JOB.COMPANY
  AND CMPNY.EFFDT = (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL CMPNY2
                     WHERE CMPNY2.COMPANY = CMPNY.COMPANY
                      AND  CMPNY2.EFFDT <= $Select-ASOFDATE)
  AND CMPNY.EFF_STATUS <> 'I'

end-SELECT

#debugd show 'Company Addr for compare w/establishment: ' $Cmpny-address-Cmp


if $EstabID = ''
  let $sql-statement = 'GPESFNQ1.SQR,ESTABLISHMENT LOCATION,Select,LOCATION_TBL'
begin-SELECT on-error=SQL-Error
LOC.ESTABID
LOC.EFFDT
  let $EstabID = &LOC.ESTABID

  FROM PS_LOCATION_TBL LOC
  WHERE SETID = &JOB.SETID_LOCATION
    AND LOCATION = &JOB.LOCATION
    AND EFFDT = (SELECT MAX(EFFDT) FROM PS_LOCATION_TBL
                  WHERE SETID = LOC.SETID
                   AND LOCATION = LOC.LOCATION
                   AND EFFDT <= $Select-ASOFDATE)
end-SELECT

  #debugd show 'Location: ' &JOB.SETID_LOCATION '/' &JOB.LOCATION
  #debugd show 'EffDT: ' &LOC.EFFDT 
  #debugd show 'Establishment ID: ' $EstabID

end-if


let $sql-statement = 'GPESFNQ1.SQR,ESTABLISHMENT ADDRESS,Select,ESTAB_TBL'
begin-SELECT on-error=SQL-Error
L.COUNTRY
L.ADDRESS1
L.ADDRESS2
L.ADDRESS3
L.ADDRESS4
L.CITY
L.NUM1
L.NUM2
L.HOUSE_TYPE
L.ADDR_FIELD1
L.ADDR_FIELD2
L.ADDR_FIELD3
L.COUNTY
L.STATE
L.POSTAL
L.GEO_CODE
L.IN_CITY_LIMIT
  let $Wrk-Center-ADDLINE2=LTRIM(RTRIM(&L.ADDR_FIELD1,' ')||' ',' ')||LTRIM(RTRIM(&L.ADDRESS1,' ')||' ',' ')
  let $Wrk-Center-ADDLINE3=LTRIM(RTRIM(&L.ADDRESS2,' ')||' ',' ')
  let $Wrk-Center-ADDLINE31=LTRIM(RTRIM(&L.ADDR_FIELD2,' ')||' ',' ')||LTRIM(RTRIM(&L.ADDR_FIELD3,' ')||' ',' ')
  let $Wrk-Center-ADDLINE4=LTRIM(RTRIM(&L.ADDRESS4,' ')||' ',' ')||LTRIM(RTRIM(&L.NUM1,' ')||' ',' ')||LTRIM(RTRIM(&L.NUM2,' ')||' ',' ')||LTRIM(RTRIM(&L.HOUSE_TYPE,' ')||' ',' ')

  let $Wrk-Center-Addr = $Wrk-Center-ADDLINE2||$Wrk-Center-ADDLINE3||$Wrk-Center-ADDLINE31||$Wrk-Center-ADDLINE4
  let $Wrk-Center-City = RTRIM(&L.CITY, ' ')
  let $Wrk-Center-Addr = $Wrk-Center-Addr||'  '||$Wrk-Center-City||'  ('||RTRIM(&L.POSTAL, ' ')||')'


FROM PS_ESTAB_TBL L

WHERE L.ESTABID = $EstabID
  AND L.EFFDT = (SELECT MAX(EFFDT) FROM PS_ESTAB_TBL
                  WHERE ESTABID = L.ESTABID
                    AND EFFDT <= $Select-ASOFDATE)

end-SELECT

#Debugd show 'Establishment address:                    ' $Wrk-Center-Addr

let $sql-statement = 'GPESFNQ1.SQR,CONTRIBUTION GROUP,Select,RGM_CTZ_ESP_TBL'
begin-SELECT on-error=SQL-Error
C.SSN_EMPL_TYPE
  let $SSN_Empl_Type = &C.SSN_EMPL_TYPE
  let $FieldName = 'SSN_EMPL_TYPE'
  let $FieldValue = &C.SSN_EMPL_TYPE
  let $AsOfDate = $Select-ASOFDATE
  Do Read-Translate-Table
  let $Cntrct-Type-Desc = $XLatLongName


FROM PS_RGM_CTZ_ESP_TBL C 

WHERE C.SCHEME_ID_ESP = $SchemeId
  AND C.CONTRIB_ID_ESP = $ContribId
  AND C.EFFDT = (SELECT MAX(EFFDT) FROM PS_RGM_CTZ_ESP_TBL
                   WHERE SCHEME_ID_ESP = C.SCHEME_ID_ESP
                    AND CONTRIB_ID_ESP = C.CONTRIB_ID_ESP
                    AND EFFDT <= $Select-ASOFDATE)

end-SELECT


let $sql-statement = 'GPESFNQ1.SQR,ESTAB_SSEC_ESP,Select,'
begin-SELECT on-error=SQL-Error
ESTAB.INDSTRY_CD_ESP
ESTAB.EFFDT
  Do Indstry-Act-Esp

FROM PS_ESTAB_SSEC_ESP ESTAB
WHERE ESTAB.ESTABID = $EstabID
  AND ESTAB.SSN_EMPL_TYPE = $SSN_Empl_Type
  AND ESTAB.SSN_EMPLOYER = &JOBJR.SSN_EMPLOYER
  AND ESTAB.EFFDT = (SELECT MAX(EFFDT) FROM PS_ESTAB_SSEC_ESP
                     WHERE ESTABID = ESTAB.ESTABID
                       AND SSN_EMPL_TYPE = ESTAB.SSN_EMPL_TYPE
                       AND SSN_EMPLOYER = ESTAB.SSN_EMPLOYER
                       AND EFFDT <= $Select-ASOFDATE)
end-SELECT


  #debugd show 'Company: ' &JOB.COMPANY
  #debugd show 'As Of: ' $Select-ASOFDATE
  #debugd show 'EE Contract num: ' &JOB.CONTRACT_NUM 
  #debugd show 'Scheme ID: ' $SchemeID
  #debugd show 'SSN EMPL TYPE: ' $SSN_Empl_Type
  #debugd show 'Wrk Center Addr: ' $Wrk-Center-Addr
  #debugd show 'Company Desc: ' $Cmpny-Descr
  #debugd show 'Company Addr: ' $Cmpny-Address
  #debugd show 'Company City: ' $Cmpny-City
  #debugd show 'Company Postal: ' $Cmpny-Postal
  #debugd show 'Company State: ' $Cmpny-State-Desc
  #debugd show 'Establishment: ' $EstabID
  #debugd show 'SSN Employer:  ' &JOBJR.SSN_EMPLOYER
  #debugd show 'Company Industry Desc: ' $Cmpny-Indstry-Desc
  #debugd show 'Company SSN: ' $Cmpny-Ssn

end-procedure


!************************************************************************
! Procedure STATE-NAMES-TBL
!************************************************************************
begin-procedure STATE-NAMES-TBL
#debug do Fin-Debug-Msg('STATE-NAMES-TBL')

  let $State-Desc = ''

let $sql-statement = 'GPESFNQ1.SQR,STATE-NAMES-TBL,Select,PS_STATE_NAMES_TBL'
begin-SELECT on-error=SQL-Error
STE.DESCR
 
  if $Curr_Language_Cd <> $PSoptions_language_cd
    do Get-State-Lang
  end-if
  if $State-Desc = ''
    let $State-Desc = &STE.DESCR
  end-if
  

FROM PS_STATE_NAMES_TBL STE

WHERE STE.COUNTRY = $Country
  AND STE.STATE = $State

end-SELECT

  #debugd show 'Country: ' $Country
  #debugd show 'State cd: ' $State
  #debugd show 'Description: ' $State-Desc

end-procedure


!************************************************************************
! Procedure Get-State-Lang
!************************************************************************
BEGIN-PROCEDURE Get-State-Lang
#debug do Fin-Debug-Msg('Get-State-Lang')

let $sql-statement = 'GPESFNQ1.SQR,Get-State-Lang,Select,PS_STATE_NAMES_LNG'
BEGIN-SELECT on-error=SQL-Error
STELNG.DESCR

  LET $State-Desc = &STELNG.DESCR


FROM PS_STATE_NAMES_LNG STELNG

WHERE STELNG.COUNTRY = $Country
  AND STELNG.STATE = $State
  AND STELNG.LANGUAGE_CD = $Curr_Language_Cd

END-SELECT

END-PROCEDURE


!************************************************************************
! Procedure INDSTRY-ACT-ESP
!************************************************************************
begin-procedure INDSTRY-ACT-ESP
#debug do Fin-Debug-Msg('INDSTRY-ACT-ESP')


let $sql-statement = 'GPESFNQ1.SQR,INDSTRY-ACT-ESP,Select,PS_INDSTRY_ACT_ESP'
begin-SELECT on-error=SQL-Error
IND.DESCR50

  let $Cmpny-Indstry-Desc = &IND.DESCR50
  

FROM PS_INDSTRY_ACT_ESP IND

WHERE IND.INDSTRY_CD_ESP = &ESTAB.INDSTRY_CD_ESP
  AND IND.EFFDT = (SELECT MAX(EFFDT) FROM PS_INDSTRY_ACT_ESP
                     WHERE INDSTRY_CD_ESP = IND.INDSTRY_CD_ESP
                      AND  EFFDT <= &ESTAB.EFFDT)
  AND IND.EFF_STATUS <> 'I'

end-SELECT

  #debugd show 'Industry cd: ' &ESTAB.INDSTRY_CD_ESP
  #debugd show 'Description: ' $Cmpny-Indstry-Desc
  
end-procedure


!************************************************************************
! Procedure Print-Certificate
!************************************************************************
begin-procedure Print-Certificate
#debug do Fin-Debug-Msg('Print-Certificate')

  #define DFLT_COL1                08
  #define DFLT_COL2                16
  #define CMPNY_NM_BOX             {DFLT_COL1}
  #define CMPNY_NM_FLD             03
  #define CMPNY_CCC_BOX            98
  #define CMPNY_CCC_FLD            99
  #define CMPNY_ADDR_BOX           {DFLT_COL1}
  #define CMPNY_ADDR_FLD           03
  #define CMPNY_CITY_BOX           {DFLT_COL1}
  #define CMPNY_CITY_FLD           03
  #define CMPNY_POSTAL_BOX         32
  #define CMPNY_POSTAL_FLD         109
  #define CMPNY_STATE_BOX          49
  #define CMPNY_STATE_FLD          50
  #define CMPNY_INDSTRY_BOX        79
  #define CMPNY_INDSTRY_FLD        80
  #define EMPL_NM_BOX              {DFLT_COL2}
  #define EMPL_NM_FLD              17
  #define EMPL_DNI_BOX             97
  #define EMPL_DNI_FLD             98
  #define EMPL_ADDR_BOX            {DFLT_COL2}
  #define EMPL_ADDR_FLD            17
  #define EMPL_MARIT_BOX           97
  #define EMPL_MARIT_FLD           98
  #define EMPL_SSN_BOX             {DFLT_COL2}
  #define EMPL_SSN_FLD             17
  #define EMPL_GCTZ_BOX            72
  #define EMPL_GCTZ_FLD            73
  #define WCNTR_ADDR_BOX           {DFLT_COL2}
  #define WCNTR_ADDR_FLD           68
  #define CNTRCT_TYPE_BOX          96
  #define CNTRCT_TYPE_FLD          97
  #define EMPL_JOBCODE_BOX         {DFLT_COL2}
  #define EMPL_JOBCODE_FLD         17
  #define EMPL_CTG_BOX             55
  #define EMPL_CTG_FLD             56
  #define EMPL_HIREDT_BOX          83
  #define EMPL_HIREDT_FLD          84
  #define EMPL_SS_TERMDT_BOX      107
  #define EMPL_SS_TERMDT_FLD      108
  #define EMPL_TERMDT_BOX          {DFLT_COL2}
  #define EMPL_TERMDT_FLD          17
  #define EMPL_TERMRSN_BOX         74
  #define EMPL_TERMRSN_FLD         48
  #define EMPL_VACN_DAYS_BOX       {DFLT_COL2}
  #define EMPL_VACN_DAYS_FLD       66

  #define MAIN_BOX_WIDTH    {ColR}
  
  #define ARIAL               4
  #define ARIAL_BOLD        400
  #define COURIER_NEW         3
  #define HELVETICA_NARROW   38
  #define TIMES_ITALIC       32
  
    !---- HEADER PART----!
    print-image WorkMinistery                           (  1,  {DFLT_COL1})
    print-image INEM                                    (  1,  116)
 
    alter-printer point-size=19 font={ARIAL_BOLD}
    set-color print-text-foreground = ('gray')
    print 'Certificado de empresa'                      (+10, {DFLT_COL1})
       
    alter-printer point-size=9 font={ARIAL}
    set-color print-text-foreground = ('black')
    print 'D/Dña.'                                      ( +3, {DFLT_COL1})
    print $Responsible-Name                             (   , 18)
    graphic (,16,118) horz-line 
    set-color print-text-foreground = ('black')
    print 'con D.N.I o N.I.E.'                          ( +2, {DFLT_COL1})
    print $Responsible-DNI                              (   , 28)
    graphic (,26,108) horz-line 
    set-color print-text-foreground = ('black')
    print 'que desempeña en la Empresa el cargo de'     ( +2, {DFLT_COL1})
    print $Reponsible-JobCode                           (   , 52)
    graphic (,50,84) horz-line 
    alter-printer point-size=9 font={ARIAL_BOLD}
    set-color print-text-foreground = ('green')
    print 'CERTIFICA:'                                  (+3, {DFLT_COL1})
    alter-printer point-size=8 font={ARIAL}
    set-color print-text-foreground = ('black')
    print 'A efectos de solicitud de Prestación por Desempleo, que son ciertos los datos relativos a la Empresa, así como los personales,'   (   , 20)
    print 'profesionales y de cotización del/de la trabajador/a que a continuación se consignan.'                                            ( +1, {DFLT_COL1})
    
    
    !------------------------PART 1------------------------------------------------------
    graphic (+4,{DFLT_COL1},{MAIN_BOX_WIDTH}) box 2.5 3 10
    alter-printer point-size=10 font={ARIAL}
    print '   1. Datos de la empresa'                      ( +1, {DFLT_COL1})  Bold

    alter-printer point-size=7 font={ARIAL}
    print 'Nombre o razón social'                          ( +2, {DFLT_COL1})
    graphic (,26,108) horz-line 
    print $Cmpny-Descr                                     (   , 28)
    print 'Código cuenta cotización'                       ( +2, {DFLT_COL1})
    graphic (,28,106) horz-line 
    print $Cmpny-Ssn                                       (   , 30)  EDIT 'XX/XXXXXXX/XX'
    print 'Domicilio Social.'                              ( +2, {DFLT_COL1})
    set-color print-text-foreground = ('green')
    print 'Calle o Plaza y Número'                         (   , 21)
    set-color print-text-foreground = ('black')
    graphic (,40,94) horz-line 
    print $Cmpny-Address                                   (   , 42) 
    set-color print-text-foreground = ('black')
    print 'Localidad'                                      ( +2, {DFLT_COL1})
    graphic (,17,87) horz-line 
    print $Cmpny-City                                      (   , 19)
    print 'Código Postal'                                  (   , {CMPNY_POSTAL_FLD})
    graphic (,121,13) horz-line 
    print $Cmpny-Postal                                    (   , 123)
    print 'Provincia'                                      ( +2, {DFLT_COL1})
    graphic (,17,117) horz-line 
    print $Cmpny-State-Desc                                (   , 19)
    print 'Actividad Económica'                            ( +2, {DFLT_COL1})
    graphic (,25,109) horz-line 
    print $Cmpny-Indstry-Desc                              (   , 27)
    print 'Domicilio centro de trabajo(sólo cumplimentar si es distinto del domicilio social)'  (+2, {DFLT_COL1})
    If $Cmpny-address-Cmp <> $Wrk-Center-Addr
       print $Wrk-Center-Addr                              (  , {WCNTR_ADDR_FLD})
    end-if
    graphic (,66,68)   horz-line 
    graphic (+2,{DFLT_COL1},126)   horz-line 
    
    
    !---------------------------PART 2------------------------------------------------------
    graphic (+3,{DFLT_COL1},{MAIN_BOX_WIDTH}) box 2.5 5 10
    alter-printer point-size=10 font={ARIAL}
    print '   2. Datos del trabajador'                    ( +1, {DFLT_COL1}) Bold 
    
    alter-printer point-size=7 font={ARIAL}
    print 'Apellidos y nombre'                             ( +2, {DFLT_COL1})
    graphic (,24,110) horz-line 
    print $Empl-Name                                       (   , 26)
    print 'Nº. D.N.I. o N.I.E'                             ( +2, {DFLT_COL1})
    graphic (,22,43) horz-line 
    print $Empl-DNI                                        (   , 24)
    print 'Nº. Afiliación S.S.'                            (   , 70)
    graphic (,84,50) horz-line 
    print $Empl-SSN                                        (   , 86) EDIT 'XX/XXXXXXXX/XX'
    print 'Grupo de cotización'                            (+2 , {DFLT_COL1})
    graphic (,24,52) horz-line 
    print $Empl-SS-Grp-Desc                                (   , 26)
    print 'Tipo de contrato'                               (   , 81)
    set-color print-text-foreground = ('green')
    print '(1)'                                            (   , 93)
    set-color print-text-foreground = ('black')
    graphic (,97,37) horz-line 
    print $Cntrct-Type-Desc                                (   , 99)
    print 'Nº de dias trabajados a la semana, en caso de trabajo a tiempo parcial'  ( +2, {DFLT_COL1})
    set-color print-text-foreground = ('green')
    print '(2)'                                            ( ,60)
    set-color print-text-foreground = ('black')
    graphic (,64,70) horz-line 
    print $Schedule_Days                                   ( ,66)    EDIT '9B'
    print 'Profesión'                                      ( +2, {DFLT_COL1})
    graphic (,17,42) horz-line 
    print $Empl-JobCode-Desc                               (   , 19)
    print 'Categoría'                                      (   , 64)
    graphic (,73,61) horz-line 
    print $Empl-Ctg-Desc                                   (   , 75)
    print 'Fecha de alta en la empresa'                    ( +2, {DFLT_COL1})
    graphic (,30,41) horz-line 
    do Format-DateTime($Hire-Dt, $Hire-Dt-Print, {DEFDMY}, '', '')
    print $Hire-Dt-Print                                   (   , 32)    
    print 'Fecha de baja en la S.S'                        (   , 76)
    graphic (,95,39) horz-line 
    do Format-DateTime($Termination-Dt, $Termination-Dt-Print, {DEFDMY}, '', '')
    print $Termination-Dt-Print                            (   , 96)
    print 'Situación legal de desempleo: Causa y código'   ( +2, {DFLT_COL1})
    set-color print-text-foreground = ('green')
    print '(3)'                                            (   , 42)
    set-color print-text-foreground = ('black')
    graphic (,46,88) horz-line 
    print $Action_Reason_Desc                             (    , {EMPL_TERMRSN_FLD})    
    print 'Fecha de efectos'                              ( +2 , {DFLT_COL1})
    set-color print-text-foreground = ('green')
    print '(4)'                                           (    , 21)
    set-color print-text-foreground = ('black')
    print ': Inicio'                                      (    , 23)
    graphic (,30,31) horz-line 
    print $Termination-Dt-Print                           (    , 32)
    print 'Fin'                                           (    , 66)
    graphic (,71,24) horz-line 
    print 'Nº E.R.E'                                      ( ,100)
    graphic (,108,26) horz-line 
    print 'Nº de dias de vacaciones anuales retribuidas y no disfrutadas antes del cese:'  ( +2, {DFLT_COL1})
    graphic (,65,4) horz-line 
    let #Vactn-days-ln = #current-line
    let #Vactn-days-col = {EMPL_VACN_DAYS_FLD}

    print 'Periodo de salarios de tramitación: de'        (    , 72)
    print $TERMINATION_DT-PRINT                           (    , 101)
    graphic (,100,10) horz-line 
    print 'a'                                             (    , 111)
    print $GPES_FINAL_DT-PRINT                            (    , 114)
    graphic (,113,10) horz-line 
    print 'Nº dias'                                       (    , 124)
    print $days_lit                                       (    , 131)
    graphic (,130,4) horz-line 
    
    
    !---------------------------PART 3------------------------------------------------------
    graphic (+3,{DFLT_COL1},{MAIN_BOX_WIDTH}) box 2.5 5 10
    alter-printer point-size=10 font={ARIAL}
    print '   3. Cotizaciones por contingencias comunes y de desempleo' ( +1, {DFLT_COL1}) Bold 
    alter-printer point-size=8 font={ARIAL}
    set-color print-text-foreground = ('green')
    print 'Según TC-2, durante los últimos 180 días precedentes a la fecha del cese' (+2, {DFLT_COL1}) Bold
    alter-printer point-size=7 font={ARIAL}
    set-color print-text-foreground = ('black')
    print '(Se incluyen también las cotizaciones al cese, correspondientes a las vacaciones anuales, retribuidas y no disfrutadas antes de la fecha de cese).'  (+2,{DFLT_COL1})
    
    
        
        graphic (+2, {DFLT_COL1},8) box 1.5 1
        graphic (, {DFLT_COL1},16) box 1.5 1
        graphic (, {DFLT_COL1},38) box 1.5 1
        graphic (, {DFLT_COL1},70) box 1.5 1
        graphic (, {DFLT_COL1},100) box 1.5 1
        graphic (, 108,26) box 1.5 1
        Let #Line-Prev = #current-line
        

        
    #define TABLE-ROWS         9
    #define DFLT_HDR_HEIGHT     2
    #define DFLT_LINE_HEIGHT    2
    
    alter-printer point-size=6 font={ARIAL}
    graphic (+1, {DFLT_COL1}, {MAIN_BOX_WIDTH}) box {DFLT_HDR_HEIGHT} 1
        !Let #Line-Prev = #current-line
    Let #Height = {DFLT_HDR_HEIGHT}
    Let #I = 1
        While #I <= {TABLE-ROWS}
          If #I = {TABLE-ROWS} - 1 
             Let #Vactn-Line = #current-line + 1
             Let #vert-line = #current-line - 1
             let #Shadow = 10
             
           !  let #Height = 2     ! Vacations specific line always has 2 lines height
           !  alter-printer point-size=4 font={ARIAL}
           !  set-color print-text-foreground = ('black')
           !  print 'Por vacaciones anuales,retribuidas y no disfrutadas '  (,109) !wrap 30 2
           !  print 'antes de la fecha del cese en la empresa'              (+0,109)
           !  set-color print-text-foreground = ('black')
           !  alter-printer point-size=7 font={ARIAL}
           !  position (-1, )
           else
             let #Shadow = 0
           end-if
           If #I <> {TABLE-ROWS} 
             graphic (  , {DFLT_COL1},  8) box #Height 1 #Shadow   !Column AÑO
           else
             print 'Totales' ( ,9)
             let #Totals-Line = #current-line + 1
           end-if
    
          graphic (  , {DFLT_COL1}, 16) box #Height 1 #Shadow      !Column MES
          graphic (  , {DFLT_COL1}, 38) box #Height 1 0            !Column NUMEROS DIAS COTIZADOS (4)
          graphic (  , {DFLT_COL1}, 70) box #Height 1 0            !Column BASE COTIZACIÓN CONTINGENCIAS COMUNES (6)
          graphic (  , {DFLT_COL1}, 100) box #Height 1 0            !Column BASE CONTIZACIÓN DESEMPLEO (6)
          graphic (  , {DFLT_COL1}, {MAIN_BOX_WIDTH}) box #Height 1 0
          position (+#Height, )
       
           Let #Height = {DFLT_LINE_HEIGHT}
           Let #I = #I + 1
    End-While
 

    !----new fields-----!
    do Format-DateTime($AsOfToday, $out, {DEFCMP}, '', '')
    let $Day = substr($out, 7, 2)
    let $Month = substr($out, 5, 2)
    let $Year = substr($out, 1, 4)

    let $FieldName = 'GPES_MONTHCD'
    let $FieldValue = $Month
    let $AsOfDate = $Select-ASOFDATE
    Do Read-Translate-Table
    let $Month_Cd_Descr = RTrim($XLatshortName, ' ')

   ! alter-printer point-size=7 font={ARIAL}
   print 'En'                      (+2, 48)
   print $Wrk-Center-City     (  , 52)
   graphic                    (  , 51, 30) horz-line  
   print 'a,'                 (  , 82)
   print $Day                 (  , 86)
   graphic                    (  , 85, 9) horz-line 
   print 'de'                 (  , 95)
   print $Month_Cd_Descr      (  , 100)
   graphic                    (  , 98, 22) horz-line 
   print 'de'                 (  ,121)
   print $Year                (  ,124)
   graphic                    (  ,123, 11) horz-line 


   print 'Firma:'             (+6, 78)
   graphic (,{DFLT_COL1},15) box 1.5 5 9
   print 'Sello de la empresa'    (  , 9)

   print 'www.inem.es'        (+4, 9)
   graphic (,{DFLT_COL1},10) box 1.5 5 

 
   alter-printer point-size=4 font={ARIAL}
   set-color print-text-foreground = ('green')
   print 'antes de la fecha del cese en la empresa'   (#vert-line,109)
   print 'Por vacaciones anuales,retribuidas y no disfrutadas '  (-2,109)
   set-color print-text-foreground = ('black')
        
           
   alter-printer point-size=6 font={ARIAL}
   graphic (#vert-line, 16,2) vert-line  
   print 'Año'                                            (#Line-Prev, 10)
   print 'Mes(3)'                                         (  ,18)
   print 'Número de dias cotizados (5)'                   (  ,26)
   print 'Base contización contingencias comunes (6)'     (  ,48)
   print 'Base cotización desempleo (7)'                  (  ,83)
    print 'Observaciones (8)'                              (  ,114)
end-procedure


!************************************************************************
! Procedure Print-Contribution-bases
!************************************************************************
begin-procedure Print-Contribution-bases
#debug do Fin-Debug-Msg('Print-Contribution-bases')

  do Format-DateTime($Termination-Dt, $out, {DEFCMP}, '', '')
  let #Prev-Year = substr($out, 1, 4)
  let #Prev-Month = substr($out, 5, 2)
  let #From-Year = substr($out, 1, 4)
  let #From-Month = substr($out, 5, 2)
  let #Total-CC = 0
  let #Total-Dsmpl = 0
  let #Ctz-Days = 0
  let #Empl-Total-CC = 0
  let #Empl-Total-Dsmpl = 0
  let #Empl-Ctz-Days = 0
  let #Vactn-Total-CC = 0
  let #Vactn-Total-Dsmpl = 0
  let #Vactn-Ctz-Days = 0
  let $Prev-sstc-Paymnt = ' '
  let #Natural-Days = 0
  !let #Cntb-Base-Fct = 1
  !let #Day-Ovrrd-Fct = 1
  !let #Day-Ovrrd-Value = 0
  let $Forced-Exit-Flag = 'N'

  !Natural days count initialization for the from date
  let $TC-Period-Begin = to_char(#From-Year) || '-' || to_char(#From-Month) || '-01'
  do Convert-To-DTU-Date($Termination-Dt, $TC-Period-End)
  do dtu-diff-days($TC-Period-Begin, $TC-Period-End, #Natural-Days)
  let #Natural-Days = #Natural-Days + 1
  if $Soc-Sec-Cntb-Freq = 'M' and #Natural-Days > 30
    let #Natural-Days = 30
  end-if

  let #Table-Row = {TABLE-ROWS} - 8

  alter-printer point-size=8 font={COURIER_NEW}

let $sql-statement = 'GPESFNQ1.SQR,Print-Contribution-bases,Select,PS_GPES_SSTC_RSLT'
Begin-SELECT on-error=SQL-Error
SSTC.EMPLID
SSTC.GPES_YEAR_RECALC
SSTC.GPES_MONTH_RECALC
SSTC.GPES_SSTC_ELEMENT
SSTC.GPES_SSTC_PAYMNT
  ! In case of litigation salaries we find L00 & L02 records in the table that make to print
  ! two lines in the report for the same month. We just want to print one line with the sum of
  ! L00 & L02 records for a month.
  let $sstc-Paymnt = &SSTC.GPES_SSTC_PAYMNT
  if &SSTC.GPES_SSTC_PAYMNT <> 'L13'
    let $sstc-Paymnt = 'L00'
  end-if

SUM(SSTC.GPES_SSTC_DAYS)    &SSTC_DAYS
SUM(SSTC.GPES_SSTC_AMOUNT)  &SSTC_AMOUNT

  if #Prev-Month <> &SSTC.GPES_MONTH_RECALC
      or $Prev-sstc-Paymnt <> $sstc-Paymnt
    if $Prev-sstc-Paymnt = ' '
      !Initialization for the first loop
      let $Prev-sstc-Paymnt =  $sstc-Paymnt
      let #Prev-Year        =  &SSTC.GPES_YEAR_RECALC
      let #Prev-Month       =  &SSTC.GPES_MONTH_RECALC
    else
      if #Table-Row = {TABLE-ROWS} - 2
        EXIT-SELECT
      end-if
  
      if $Prev-sstc-Paymnt <> 'L13'
        Do Print-Contribution-Ln
        if #Natural-Days >= 180
          let $Forced-Exit-Flag = 'Y'
          EXIT-SELECT
        end-if
        if (&SSTC.GPES_YEAR_RECALC = #From-Year and &SSTC.GPES_MONTH_RECALC < #From-Month)
            or &SSTC.GPES_YEAR_RECALC < #From-Year
          if $Soc-Sec-Cntb-Freq = 'D'
            let $TC-Period-Begin = to_char(&SSTC.GPES_YEAR_RECALC) || '-' || to_char(&SSTC.GPES_MONTH_RECALC) || '-01'
            do DTU-Month-End($TC-Period-Begin, $TC-Period-End)
            do dtu-diff-days($TC-Period-Begin, $TC-Period-End, #Natural-Days-Wk)
          else
            let #Natural-Days-Wk = 30
          end-if
          Let #Partial-Days = 180 - #Natural-Days
          If #Natural-Days + #Natural-Days-Wk <= 180
            let #Natural-Days = #Natural-Days + #Natural-Days-Wk
          else
            !let #Day-Ovrrd-Fct = 0
            !let #Day-Ovrrd-Value = 180 - #Natural-Days
            !let #Cntb-Base-Fct = (180 - #Natural-Days) / #Natural-Days-Wk
            let #Natural-Days = #Natural-Days + (180 - #Natural-Days)
          end-if
        end-if
      end-if
      let #Prev-Year    = &SSTC.GPES_YEAR_RECALC
      let #Prev-Month   = &SSTC.GPES_MONTH_RECALC
      let #Total-CC     =  0
      let #Total-Dsmpl  =  0
      let #Ctz-Days     =  0
      let $Prev-sstc-Paymnt =  $sstc-Paymnt
    end-if
  end-if

  #debugd show 'Diff Days:          ' #Natural-Days-Wk
  #debugd show 'Natural Days Count: ' #Natural-Days
  #debugd show 'Contributing Days:  ' #Contributed-Days
  #debugd show 'Base reduction Fct: ' #Cntb-Base-Fct
  #debugd show 'TC Payment code:    ' &SSTC.GPES_SSTC_PAYMNT
  #debugd show 'TC Element code:    ' &SSTC.GPES_SSTC_ELEMENT

  if &SSTC.GPES_SSTC_ELEMENT = 'BA01' or &SSTC.GPES_SSTC_ELEMENT = 'BA21'
    if &SSTC.GPES_SSTC_PAYMNT = 'L13'
       let #Vactn-Total-CC =  #Vactn-Total-CC +  &SSTC_AMOUNT
    else
       let #Total-CC       =  #Total-CC + &SSTC_AMOUNT
    end-if
  else
    if &SSTC.GPES_SSTC_PAYMNT = 'L13'
       let #Vactn-Total-Dsmpl  =  #Vactn-Total-Dsmpl +  &SSTC_AMOUNT
       let #Vactn-Ctz-Days     =  #Vactn-Ctz-Days +  &SSTC_DAYS
    else
       let #Total-Dsmpl      =  #Total-Dsmpl +  &SSTC_AMOUNT
       if $sstc-Paymnt = $Prev-sstc-Paymnt
          let #Ctz-Days         =  #Ctz-Days +  &SSTC_DAYS
          let $Prev-sstc-Paymnt =  $sstc-Paymnt
       end-if
    end-if
  end-if

 FROM PS_GPES_SSTC_RSLT  SSTC

WHERE SSTC.EMPLID = $Select-EMPLID
 AND  SSTC.EMPL_RCD = #Select-EMPL-RCD
 AND  SSTC.GPES_SSTC_ELEMENT IN ('BA01','BA02','BA21','BA22')
 AND  SSTC.GPES_SSTC_LGL_CUST <> 'T'
 AND  (((SSTC.GPES_YEAR_RECALC = #From-Year AND  SSTC.GPES_MONTH_RECALC <= #From-Month)
        OR (SSTC.GPES_YEAR_RECALC < #From-Year))
       OR
       ((SSTC.GPES_YEAR = #From-Year AND SSTC.GPES_MONTH = #From-Month)
         AND GPES_SSTC_PAYMNT = 'L13'))

 AND  SSTC.CONTRACT_NUM = &JOB.CONTRACT_NUM

 AND  SSTC.RSLT_VER_NUM = (SELECT MAX(RSLT_VER_NUM) FROM PS_GPES_SSTC_RSLT
                            WHERE EMPLID = SSTC.EMPLID
                              AND EMPL_RCD = SSTC.EMPL_RCD
                              AND CAL_ID = SSTC.CAL_ID)
 AND  SSTC.RSLT_REV_NUM = (SELECT MAX(RSLT_REV_NUM) FROM PS_GPES_SSTC_RSLT
                            WHERE EMPLID = SSTC.EMPLID
                              AND EMPL_RCD = SSTC.EMPL_RCD
                              AND CAL_ID = SSTC.CAL_ID)

GROUP BY SSTC.EMPLID, SSTC.GPES_YEAR_RECALC, SSTC.GPES_MONTH_RECALC, SSTC.GPES_SSTC_PAYMNT, SSTC.GPES_SSTC_ELEMENT

ORDER BY SSTC.EMPLID, SSTC.GPES_YEAR_RECALC DESC, SSTC.GPES_MONTH_RECALC DESC, SSTC.GPES_SSTC_PAYMNT DESC, SSTC.GPES_SSTC_ELEMENT
End-SELECT


  #debugd show 'Employee ID: ' $Select-EMPLID
  #debugd show 'Empl Rcd#: ' #Select-EMPL-RCD
  #debugd show 'Termination Dt: ' $Termination-Dt

  If $Forced-Exit-Flag = 'N'
    Do Print-Contribution-Ln
  end-if


 ! Print Vacations related data total
  Do Format-Number(#Vactn-Ctz-Days, $Vactn-Ctz-Days-Print, '999')
  let $Out = lpad($Vactn-Ctz-Days-Print, 3, ' ')
  print $Vactn-Ctz-Days-Print                  ( #Vactn-Line, 30)
  Do Format-Number(#Vactn-Total-CC, $out, '999,999.99')
  let $Out = lpad($out, 12, ' ')
  print $Out                                   ( #Vactn-Line, 50)
  Do Format-Number(#Vactn-Total-Dsmpl, $out, '999,999.99')
  let $Out = lpad($out, 12, ' ')
  print $Out                                   ( #Vactn-Line, 85)

 ! Print employee totals
  Do Format-Number(#Empl-Ctz-Days, $out, '999')
  let $Out = lpad($out, 3, ' ')
  print $out                                   ( #Totals-Line, 30)
  Do Format-Number(#Empl-Total-CC, $out, '999,999.99')
  let $Out = lpad($out, 12, ' ')
  print $Out                                   ( #Totals-Line, 50)
  Do Format-Number(#Empl-Total-Dsmpl, $out, '999,999.99')
  let $Out = lpad($out, 12, ' ')
  print $Out                                   ( #Totals-Line, 85)

  
  let $Vactn-Ctz-Days-Print = ltrim($Vactn-Ctz-Days-Print, ' ')
  print $Vactn-Ctz-Days-Print                    (#Vactn-days-ln, #Vactn-days-col)

end-procedure


!************************************************************************
! Procedure Print-Contribution-Ln
!************************************************************************
begin-procedure Print-Contribution-Ln
#debug do Fin-Debug-Msg('Print-Contribution-Ln')

  If #Natural-Days >= 180
    if #Partial-Days < #Ctz-Days
      let #Ctz-Days = #Partial-Days
    End-if
    Let #Total-CC = (#Total-CC / #Ctz-Days) * #Ctz-Days
    let #Total-Dsmpl = (#Total-Dsmpl / #Ctz-Days) * #Ctz-Days
  End-if

  let #Empl-Ctz-Days    =  #Empl-Ctz-Days +  #Ctz-Days
  let #Empl-Total-CC    =  #Empl-Total-CC + #Total-CC
  let #Empl-Total-Dsmpl =  #Empl-Total-Dsmpl +  #Total-Dsmpl
  
  print #Prev-Year                             (+{DFLT_LINE_HEIGHT}, 10)    EDIT '9999'
  print #Prev-Month                            (  , 19)   EDIT '99'
  Do Format-Number(#Ctz-Days, $out, '999')
  let $Out = lpad($out, 3, ' ')
  print $out                                   (  , 30)
  Do Format-Number(#Total-CC, $out, '999,999.99')
  let $Out = lpad($out, 11, ' ')
  print $Out                                   (  , 50)
  Do Format-Number(#Total-Dsmpl, $out, '999,999.99')
  let $Out = lpad($out, 11, ' ')
  print $Out                                   (  , 85)

  let #Table-Row = #Table-Row + 1

end-procedure


!--------------------------------------------------------------------!
! Debug Msg                                                          !
!--------------------------------------------------------------------!
begin-procedure Fin-Debug-Msg($procedure_name)
   display ' '
   display '----------------------------------'
   display $procedure_name
   #debugt date-time () {Native-DateTime} &SysDateTime
   #debugt move &SysDateTime to $SysDateTime
   #debugt show 'TIMING, ' $procedure_name ', ' $SysDateTime
   display ' '
end-procedure ! Fin-Debug-Msg


!***************************************************************
!Include SQCs
!***************************************************************
#Include 'getsetid.sqc'
#Include 'readxlat.sqc'
#Include 'sqlerr.sqc'
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#include 'datemath.sqc'  !Routines for date calculation
