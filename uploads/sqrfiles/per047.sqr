!***********************************************************************
!  PER047.SQR:  TENURE REPORT - EMPLOYEES BY TENURE STATUS             *
!               HRMS for Education & Government Reports                *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:44:31                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
! This program is specific to HRMS for Education & Government          *
!***********************************************************************


#include 'setenv.sqc'         !Set Environment
#include 'setup32.sqc'        !Printer and Page-Size Initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-heading 6
#include 'stdhdgtr.sqc'                                               !F-LPARKER-125 (CAMI)

Print ' '                    (+2,1,2)                                 !F-LPARKER-125 (CAMI)
Print $PER047_TENURE_GRP     (,+1,30)                                 !F-LPARKER-125 (CAMI)
Print $PER047_TEN_STATUS     (,+2,20)                                 !F-LPARKER-125 (CAMI)
Print $PER047_FAC_MEMBER     (,+2,30)                                 !F-LPARKER-125 (CAMI)
Print $PER047_RANK           (,+2,20)                                 !F-LPARKER-125 (CAMI)
Print $PER047_HOME_DEPT      (,+2,25)                                 !F-LPARKER-125 (CAMI)
Print $PER047_TEN_START_DT   (,+2,18)                                !F-LPARKER-125 (CAMI)
Print $PER047_MANDT_REVW_DT  (,+2,17)                                 !F-LPARKER-125 (CAMI)

Print ' '                                  (+1,1,2)
Print '------------------------------'     (,+1,30)
Print '--------------------'               (,+2,20)
Print '------------------------------'     (,+2,30)
Print '--------------------'               (,+2,20)
Print '-------------------------'          (,+2,25)
Print '------------------'                 (,+2,18)
Print '-----------------'                  (,+2,17)
Print ' '                                  (+2,1,2)


end-heading


!***********************************************************************
! F-LPARKER-125 (CAMI)                                                 *
! Read language-dependent text from the database using PER047 program  *
! ids.                                                                 *
!***********************************************************************
begin-procedure REPORT-TRANSLATION

  let $language_cd = $PRCS_LANGUAGE_CD
  do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PER047', 'EG_ACADEMIC_RANK', $PER047_RANK,         #DW)
  do Get_Field_Information ('PER047', 'EG_MAND_REVW_DT',  $PER047_MANDT_REVW_DT,#DW)
  do Get_Field_Information ('PER047', 'FAC_MEMBER',       $PER047_FAC_MEMBER,   #DW)
  do Get_Field_Information ('PER047', 'HOME_DEPT',        $PER047_HOME_DEPT,    #DW)
  do Get_Field_Information ('PER047', 'REPORT_TITLE',     $REPORTTITLE,         #DW)
  do Get_Field_Information ('PER047', 'TEN_START_DT',     $PER047_TEN_START_DT, #DW)
  do Get_Field_Information ('PER047', 'TENURE_GROUP',     $PER047_TENURE_GRP,   #DW)
  do Get_Field_Information ('PER047', 'TENURE_STATUS',    $PER047_TEN_STATUS,   #DW)

end-procedure


begin-procedure Init-Report

  move 'PER047'             to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
  do REPORT-TRANSLATION                                               !F-LPARKER-125 (CAMI)

  if $prcs_process_instance = ''
        !No Prompt
  else
    do Select-Parameters

  end-if

  do Set-Tenure-Selection

end-procedure


begin-procedure Get-Values

 let $Tenure_Group    = RTRIM(&RUN_CNTL_EG.EG_GROUP, ' ')
 let $Tenure_Status   = RTRIM(&RUN_CNTL_EG.TENURE_STATUS, ' ')


end-procedure


Begin-Procedure Set-Tenure-Selection

#DEBUG show 'Entering Set-Tenure-Selection module'

   if $Tenure_Group = ''
      move '1=1' to $WhereTenureGroup
   else
      let $WhereTenureGroup = 'TN.EG_GROUP = '''||$Tenure_Group ||''''
   end-if

   if $Tenure_Status = ''
      move '1=1'  to $WhereTenureStatus
   else
      let $WhereTenureStatus='TN.TENURE_STATUS='''||$Tenure_Status||''''
   end-if



#DEBUG show '$WhereTenureGroup   = <' $WhereTenureGroup  '>'
#DEBUG show '$WhereTenureStatus  = <' $WhereTenureStatus '>'


End-Procedure


begin-procedure Process-Main

Begin-SELECT

TN.EG_GROUP
TN.EG_TENURE_HOME
TN.TENURE_STATUS
TN.EG_ACADEMIC_RANK
TN.EG_TRACK_HIRE_DT
TN.EMPLID
TN.SETID
TN.EG_MAND_REVW_DT
PS.NAME


  let $Academic_Rank = RTRIM(&TN.EG_ACADEMIC_RANK, ' ')
  let $Home_Dept     = RTRIM(&TN.EG_TENURE_HOME, ' ')
  let $Calc_Group    = RTRIM(&TN.EG_GROUP, ' ')
  let $Ten_Status    = RTRIM(&TN.TENURE_STATUS, ' ')
  let $EmplID        = &TN.EMPLID
  let $SetID         = &TN.SETID

  let $Name          = &PS.NAME

  move 'TENURE_STATUS' to $FieldName
  move $Ten_Status  to $FieldValue
  do Read-Translate-Table
  move $XlatLongName to $Tenure_Sts_Descr

  do Get-Rank-Description

  do Get-Calc-Group-Description

  let $DeptID = &TN.EG_TENURE_HOME
  do Get-Department-Name-With-SetId
  let $Home_Dept_Name = $DeptName

  Print $Calc_Group_Descr        (+1,4,30)     ON-BREAK PRINT=Change/Top-Page
                                               SKIPLINES=3
  Print $Tenure_Sts_Descr        (,+2,20)      ON-BREAK PRINT=Change/Top-Page
                                               SKIPLINES=3
  Print $Name                    (,+2,30)
  Print $Rank_Descr              (,+2,20)
  Print $Home_Dept_Name          (,+2,25)
  Print &TN.EG_TRACK_HIRE_DT     (,+6,11)
  Print &TN.EG_MAND_REVW_DT      (,+8,11)


FROM PS_EG_TENURE_DATA TN,
     PS_PERSON_NAME PS
WHERE
TN.EMPLID = PS.EMPLID  AND
[$WhereTenureGroup]    AND
[$WhereTenureStatus]
ORDER BY TN.EG_GROUP, TN.TENURE_STATUS, PS.NAME

end-SELECT

End-procedure



begin-procedure Get-Rank-Description
begin-SELECT
R.DESCR

  let $Academic_Rank_Descr = RTRIM(&R.DESCR, ' ')

FROM PS_EG_RANK_TBL R
WHERE R.EG_ACADEMIC_RANK = $Academic_Rank
end-SELECT
  let $Rank_Descr = RTRIM(&R.DESCR, ' ')
end-procedure


begin-procedure Get-Calc-Group-Description
begin-SELECT

C.DESCR
   let $Calc_Group_Descr = RTRIM(&C.DESCR, ' ')
FROM PS_EG_CALC_GRP_TBL C
WHERE C.EG_GROUP = $Calc_Group
end-SELECT
end-procedure

!***********************************************************************
#include 'egrnctl1.sqc'  !Get run control parameter values             *
#include 'stdapi.sqc'    !Routines to update run status                *
#Include 'reset.sqc'     !Reset printer procedure                      *
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure               *
#Include 'datetime.sqc'  !Routines for date and time formatting        *
#Include 'number.sqc'    !Routines to format numbers                   *
#include 'readxlat.sqc'  !Read Translate Table                         *
#include 'getempnm.sqc'  !Get Employee Name                            *
#include 'rotname1.sqc'  !Rotate Name                                  *
#include 'getdptnm.sqc'  !Get Department Name                          *
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language      * !F-LPARKER-125 (CAMI)
!***********************************************************************
