!***********************************************************************
!  TAX860VA:  Create Wage Listing for UI - Nevada - MMREF              *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
! Copyright (C) 1988, 2010, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2010/04/16:09:36:23                        
!       $Release:  HR9                                        
!    $Resolution:  804998                                     
!                                                                      *
!***********************************************************************
!***********************************************************************
! Rimini Street Modification Log                                       *
!                                                                      *
!  10/19/2015 RSI-HCM105268  v1.0                                      *
!  Descr: Send middle initial and not middle name on RS record.        *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup31.sqc' !Printer and page-size initialization
#include 'usarpt.sqc'  ! NID_COUNTRY defined

begin-report

#Define Prgm_Ver         '1.0'   !Corresponds to Modification History above.   ! RSI-HCM105268
#Define ReportID         TAX860NV                                              ! RSI-HCM105268
                                                                                                                    ! RSI-HCM105268 Begin
  let $Program_Info = '{ReportID}.SQR - Version ' || {Prgm_Ver} || ' on {sqr-platform} on {sqr-database} database'
  Show $Program_Info
  Show '**************************************************'
  Show 'Creating Wage Listing for UI - Nevada - MMREF'
  Show ''
                                                                                                                    ! RSI-HCM105268 End

  do Init-Report

! These variables are used by rptsmmry.sqc
  Let $Prnt_CoTxGrs = 'N'
  Let $Prnt_CoExGrs = 'N'
  Let $Prnt_CoTips  = 'Y'

  do Process-Main

  close 10

  if $RecordWritten = 'Y'
    display ''
    display 'Upon successful conclusion of this program, the following '
    display 'file will have been created:'
    display ''
    display '  TAX860NV  contains 512-character records in the MMREF-1'
    display '            format required for electronic submission to the'
    display '            Nevada Employment Security Department.'
    display ''
    display 'Follow the instructions from the commission for preparing'
    display 'and submitting the file electronically.'
    display ''
  else
    do Print-Summary-Data('NoData')
  end-if

  do Stdapi-Term

end-report

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number

  do Get-Current-DateTime
  do Get-Calendar-Year-Id
  do Stdapi-Init

  do Initialization
  move 'TAX860NV' to $ReportID

  move 'Nevada Quarterly UI Wage Report ' to $ReportTitle
  display ''
  display 'Creating Electronic File for Nevada UI Wage Reporting'
end-procedure

begin-procedure Initialization
#ifdef TAXTEST
  move 'CA'              to $State
#else
  move 'NV'              to $State
#endif
  move '32'              to $StateNumCd

  do Get-Tax-Reporting-Run-Controls
  move &TX.Balance_Year to $RptYear 9999
  let $ReportingPeriod = $QtrEndMonth || $RptYear
  move $PeriodEndDate    to $AsOfDate

  display ' '
  display 'Processing balances for '   noline
  display $RptQtrYr

  move 512 to #RecordLength
  add 2 to #RecordLength

  if $PRCS_Process_Instance = ''
    Display 'Report cannot be run outside of PeopleSoft, please use process scheduler.'
    Stop
  else
     do Select-Parameters
     do Convert-Parameters
  end-if
  do Calculate-Diskette-Capacity
  move 0 to #RecordCount
  do Open-File
end-procedure

begin-procedure Open-File

if $FileType = 'D'
   if #FileExtension <> 0
      close 10
   end-if
   move 0 to #RecordCount
   add 1 to #FileExtension
   move #FileExtension to $FileExtension 099
#ifdef OS400
   let $FileExtension = '(D' || $FileExtension || ')'
   let $FileID = '{IMPORTPREFIX}' || 'TAX860NV' || $FileExtension
#else
   let $FileID = '{IMPORTPREFIX}' || 'TAX860NV.' || $FileExtension
#endif

   open $FileID as 10 for-writing record=512:fixed
else
  open '{IMPORTPREFIX}TAX860NV{IMPORTSUFFIX}' as 10 for-writing record=512:fixed
end-if
end-procedure

begin-procedure Process-Main

  move &TX.Company       to $Company  !TX.Company is the Transmitter
  if RTRIM($Company,' ') = ''
    display '*** Company field blank on Tax Run Control Record ***'
    display '***************** Processing stopped ****************'
    stop
  end-if
  do Get-Company-Data                 !Transmitter identifying data

  do Write-Transmitter-Record

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
A.COMPANY
A.DESCR
A.FEDERAL_EIN
A.ADDRESS1
A.ADDRESS2
A.CITY
A.STATE
A.POSTAL
A.COUNTRY

  move &A.Company to $Company

  move '' to $priorEmplID
  do Read-Employee-Data

FROM  PS_COMPANY_TBL A
WHERE A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
  (SELECT MAX(EFFDT)
     FROM PS_COMPANY_TBL
     WHERE  COMPANY = A.COMPANY
       AND  EFFDT  <= $AsOfDate)
ORDER BY COMPANY
end-SELECT
  if #Count_F > 0
    do Write-F-Record
    do Print-Summary-Data('FileTotal')
  end-if
end-procedure

begin-procedure Read-Employee-Data
begin-SELECT
C.COMPANY    () on-break print=never before=Before-Company
                                     AFTER=AFTER-COMPANY
C.EMPLID
C.STATE
C.TXGRS_QTD
C.NLGRS_QTD
D1.NATIONAL_ID
D.LAST_NAME
D.FIRST_NAME
D.MIDDLE_NAME
D.NAME_SUFFIX

  move &C.EMPLID to $EmplID

  if &C.NlGrs_QTD > 0
    do Process-Employee
  else
    do Print-Summary-Data('NegWage')
  end-if

FROM  PS_TAX_BALANCE C, PS_PERSON_NAME D, PS_PERS_NID D1
WHERE C.COMPANY       = &A.Company
  AND D.EMPLID        = C.EMPLID
  AND C.TAX_CLASS     = 'U'
  AND C.BALANCE_ID    = $Calendar_Year_Id
  AND C.BALANCE_YEAR  = &TX.Balance_Year
  AND C.BALANCE_QTR   = &TX.Balance_Qtr
#ifdef TAXTEST
  AND C.STATE         = 'CA'
#else
  AND C.STATE         = 'NV'
#endif
  AND C.LOCALITY      = ' '
  AND C.BALANCE_PERIOD  =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE
       WHERE  EMPLID        = C.EMPLID
         AND  COMPANY       = C.COMPANY
         AND  BALANCE_ID    = C.BALANCE_ID
         AND  BALANCE_YEAR  = C.BALANCE_YEAR
         AND  BALANCE_QTR   = C.BALANCE_QTR
         AND  STATE         = C.STATE
         AND  LOCALITY      = C.LOCALITY
         AND  TAX_CLASS     = C.TAX_CLASS)
  AND C.NLGRS_QTD <> 0
  AND D.EMPLID            = D1.EMPLID
  AND D1.COUNTRY          = {NID_Country}
  AND D1.NATIONAL_ID_TYPE = $Payroll_NID_Type
ORDER BY D1.NATIONAL_ID
end-SELECT
end-procedure

begin-procedure Process-Employee
  move &D1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits
  if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
    move '000000000'   to $NATIONAL_ID
  else
    move &D1.NATIONAL_ID        to $NATIONAL_ID
  end-if
  move &D.LAST_NAME    to $LastName
  move &D.FIRST_NAME   to $FirstName
  move &D.MIDDLE_NAME  to $MiddleName
  move &D.NAME_SUFFIX  to $Suffix

  uppercase $LastName
  uppercase $FirstName
  uppercase $MiddleName
  uppercase $Suffix

  do Process-Tips

  move &C.NlGrs_QTD to #NlGrs_QTD
  add #NlGrs_QTD to #Co_NlGrs_QTD
  multiply 100 times #NlGrs_QTD
  move &C.TxGrs_QTD to #TxGrs_QTD
  add #TxGrs_QTD to #Co_TxGrs_QTD
  add #Tips_QTD to #Co_Tips_QTD
  multiply 100 times #Tips_QTD

  do write-s-record
  add 1 to #Count_S

end-procedure

begin-procedure Process-Tips    ! use GETEARNS.SQC

  move 'PS_RC_TAX810NV D'  to $TAX810X1
  move 'PS_RC_TAX810NV D1' to $TAX810X2
  move 'PS_RC_TAX810NV D2' to $TAX810X3
  move 'PS_RC_TAX810NV D3' to $TAX810X4
  do Get-Earns

  move #Earns to #Tips_QTD

end-procedure

begin-procedure Before-Company
  do Get-Company-Data

  do Get-State-Tax-Data             !Employer ID
  if RTRIM(&Employer_ID_SUT,' ') = ''
    display '*** State Unemployment ID missing in Company State Tax Table ***'
    display '*********************** Processing stopped *********************'
    display 'Company'
    display $Company
    stop
  end-if

  let $StateEIN = RTRIM(&Employer_ID_SUT,' ')
  while INSTR($StateEIN,'-',1) <> 0
    let #Hyphen = INSTR($StateEIN,'-',1)
    if #Hyphen > 0
      let $StateEIN = SUBSTR($StateEIN,1,(#Hyphen - 1)) ||
                      SUBSTR($StateEIN,(#Hyphen + 1),9)
    end-if
  end-while
  while INSTR($StateEIN,'.',1) <> 0
    let #Marks = INSTR($StateEIN,'.',1)
    if #Marks > 0
      let $StateEIN = SUBSTR($StateEIN,1,(#Marks - 1)) ||
                      SUBSTR($StateEIN,(#Marks + 1),9)
    end-if
  end-while
  while INSTR($StateEIN,' ',1) <> 0
    let #Spaces = INSTR($StateEIN,' ',1)
    if #Spaces > 0
      let $StateEIN = SUBSTR($StateEIN,1,(#Spaces - 1)) ||
                      SUBSTR($StateEIN,(#Spaces + 1),9)
    end-if
  end-while
  move $StateEIN to $StateEIN 099999999

  do Company-Run-Control

  do Write-E-Record
end-procedure

begin-procedure After-Company

  add #Co_NlGrs_QTD to #Tot_NlGrs_QTD
 ! add #Co_TxGrs_QTD to #Tot_TxGrs_QTD
  add #Count_S to #Count_F

  do Print-Summary-Data('CoTotal')
  do Write-T-Record

  move 0 to #Co_NlGrs_QTD
  move 0 to #Co_TxGrs_QTD
  move 0 to #Co_Tips_QTD
  move 0 to #Count_S
  new-page
end-procedure


Begin-Procedure Write-E-Record

  if $FileType = 'D'
    if #RecordCount >= #MaxRecordsPerDiskette
      do Open-File
    end-if
  end-if

  move &A.Federal_EIN  to $FedEIN   099999999
  move &A.Descr        to $ErName
  move &A.Address1     to $ErAddress1
  move &A.Address2     to $ErAddress2
  move &A.City         to $City
  move &A.State        to $StateCd
  move &A.Postal       to $Zip
  move &A.Country      to $Country

  do Foreign-Address-Test('Format 3',25)

  If $ForeignAddrInd = 'X'
     Let $US_Zip      = ' '
     Let $US_ZipXtnsn = ' '
     Let $US_State    = ' '
     Let $Frgn_Zip    = $Zip
     Let $Frgn_State  = $StateName
  Else
     Let $US_Zip      = $Zip
     Let $US_ZipXtnsn = $ZipExtsn
     Let $US_State    = $StateCd
     Let $Frgn_Zip    = ' '
     Let $Frgn_State  = ' '
     Let $Country-2Char   = ' '
  End-if

  uppercase $ErName
  uppercase $ErAddress1
  uppercase $ErAddress2
  uppercase $City
  uppercase $StateCd

  write 10 from   'RE':2   ! 2
                   $Sp:23  ! 3 - 25
                   '0':1   ! 26
                   $Sp:13  ! 27 - 39
               $ErName:57  ! 40 - 96
           $ErAddress1:22  ! 97 - 118
           $ErAddress2:22  ! 119 - 140
                 $City:22  ! 141 - 162
             $US_State:2   ! 163 - 164
               $US_Zip:5   ! 165 - 169
          $US_ZipXtnsn:4   ! 170 - 173
                   $Sp:5   ! 174 - 178
           $Frgn_State:23  ! 179 - 201
             $Frgn_Zip:15  ! 202 - 216
        $Country-2Char:2   ! 217 - 218
                   $Sp:294 ! 219 - 512

  add 1 to #RecordCount
  add 1 to #Count_E
  move 'Y' to $RecordWritten

End-Procedure


begin-procedure Write-S-Record

  if $FileType = 'D'
    if #RecordCount >= #MaxRecordsPerDiskette
      do Open-File
    end-if
  end-if

  do Chk-Empl-Name

  move #NlGrs_QTD        to $NlGrs_QTD   09999999999
  move #Tips_QTD         to $Tips_QTD    099999999

  write 10 from 'RS':2    ! 1 - 2
         $StateNumCd:2    ! 3 - 4
              'WAGE':4    ! 5 - 8
                 $Sp:1    ! 9 - 9
        $NATIONAL_ID:9    ! 10 - 18
          $FirstName:15   ! 19 - 33
         $MiddleName:15   ! 34 - 48
           $LastName:20   ! 49 - 68
             $Suffix:4    ! 69 - 72
                 $Sp:124  ! 73 - 196
    $ReportingPeriod:6    ! 197 - 202
          $NlGrs_QTD:11   ! 203 - 213
                 $Sp:11   ! 214 - 224
                 $Sp:2    ! 225 - 226
                 $Sp:21   ! 227 - 247
           $StateEIN:9    ! 248 - 256
                 $Sp:81   ! 257 - 337
           $Tips_QTD:9    ! 338 - 346
                 $Sp:4    ! 347 - 350
                 $Sp:9    ! 351 - 359
                 $Sp:153  ! 360 - 512

  add 1 to #RecordCount
  move 'Y' to $RecordWritten

  move '' to $NlGrs_QTD
  move 0  to #NlGrs_QTD
  move '' to $Tips_QTD
  move 0  to #Tips_QTD

end-procedure


begin-procedure Chk-Empl-Name

  let $ChkName   = rtrim($FirstName,' ')
  do Chk-Name-Err($ChkName,'Employee',$FirstName)

  let $ChkName  = rtrim($LastName,' ')
  do Chk-Name-Err($ChkName,'EmplLastName',$LastName)

                                                                                           ! RSI-HCM105268 Begin
  If length($MiddleName) > 1
     let $MiddleName = substr($MiddleName,1,1)
  End-If
                                                                                           ! RSI-HCM105268 End

  let $ChkName    = rtrim($MiddleName,' ')
  do Chk-Name-Err($ChkName,'Employee',$MiddleName)

  let $ChkName    = rtrim($Suffix,' ')
  do Chk-Name-Err($ChkName,'Employee',$Suffix)

end-procedure


begin-procedure Chk-Name-Err($ChkName,$NameType,:$OutputName)

  let #ChkName_Lgth = length($ChkName)
  let #ChkName_Pos  = 1

  while #ChkName_Pos <= #ChkName_Lgth
    let $ChkName_Sub  = substr($ChkName, #ChkName_Pos, 1)
    let $ChkName_Sub  = upper($ChkName_Sub)
    let #ChkName_Rem_Lgth = #ChkName_Lgth - #ChkName_Pos

    if $ChkName_Sub = ',' or $ChkName_Sub = '-' or $ChkName_Sub = '''' or $ChkName_Sub = '.'
      ! Nevada allows hyphens in the last name, eg, Sarah Leigh-Jones,
      ! therefore only substitute with space if not lastname and hyphen,
      if $NameType = 'EmplLastName' and $ChkName_Sub = '-'
       !do nothing
      else
       let $OutputName = substr($OutputName,1,#ChkName_Pos -1)||' '||substr($OutputName,#ChkName_Pos+1,#ChkName_Rem_Lgth)
      end-if
    else
      let #ChkName_Inst = instr('ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789 ',$ChkName_Sub, 1)
        if #ChkName_Inst  = 0
            let $Name_Displ   = rtrim($_FirstName,' ')||' '||rtrim($_MiddleName,' ')||' '||rtrim($_LastName,' ')||
                rtrim($_Suffix,' ')
            display '*** RS record Employee name should be alpha-numeric characters only (A-Z, 0-9)***'
            display '*** except for hyphens within the last name                                   ***'
            display '****************************** Processing stopped *******************************'
            display 'Employee Name:   ' noline
            display $Name_Displ
            stop
        end-if
    end-if
    add 1 to #ChkName_Pos
  end-while

end-procedure

Begin-Procedure Write-T-Record

  if $FileType = 'D'
    if #RecordCount >= #MaxRecordsPerDiskette
      do Open-File
    end-if
  end-if

  multiply 100 times #Co_NlGrs_QTD
  move #Co_NlGrs_QTD to $Co_NlGrs_QTD  09999999999999
  multiply 100 times #Co_TxGrs_QTD
  move #Co_TxGrs_QTD to $Co_TxGrs_QTD  09999999999999
  let #Co_ExGrs_QTD = #Co_NlGrs_QTD - #Co_TxGrs_QTD
  move #Co_ExGrs_QTD to $Co_ExGrs_QTD  09999999999999

  move #Count_S to $Count_S  0999999

  if #Count_S = 0
      move 'Y' to $NoWWInd
  else
      move ' ' to $NoWWInd
  end-if

  write 10 from 'RS':2    ! 1 - 2
         $StateNumCd:2    ! 3 - 4
              'SUMM':4    ! 5 - 8
                 $Sp:2    ! 9 - 10
             $FedEIN:9    ! 11 - 19
                 $Sp:1    ! 20
           $StateEIN:9    ! 21 - 29
                 $Sp:1    ! 30
    $ReportingPeriod:6    ! 31 - 36
                 $Sp:1    ! 37
            $Count_S:7    ! 38 - 44
                 $Sp:1    ! 45
       $Co_NlGrs_QTD:14   ! 46 - 59
                 $Sp:1    ! 60
       $Co_ExGrs_QTD:14   ! 61 - 74
                 $Sp:1    ! 75
       $Co_TxGrs_QTD:14   ! 76 - 89
                 $Sp:1    ! 90
                 $Sp:13   ! 91 - 103
        $Month1Count:7    ! 104 - 110
                 $Sp:1    ! 111
        $Month2Count:7    ! 112 - 118
                 $Sp:1    ! 119
        $Month3Count:7    ! 120 - 126
                 $Sp:1    ! 127
            $NoWWInd:1    ! 128
                 $Sp:384  ! 129 - 512

  add 1 to #RecordCount
  move 'Y' to $RecordWritten

End-Procedure


begin-procedure Company-Run-Control
begin-select
NV.MONTH1_COUNT7
NV.MONTH2_COUNT7
NV.MONTH3_COUNT7
FROM PS_RC_TAX860NV NV
WHERE NV.OPRID = $Prcs_OprID
  AND NV.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
  AND NV.COMPANY = &C.Company
end-SELECT

  move &NV.Month1_Count7    to #Month1Count
  move &NV.Month2_Count7    to #Month2Count
  move &NV.Month3_Count7    to #Month3Count

  move #Month1Count     to $Month1Count     0999999
  move #Month2Count     to $Month2Count     0999999
  move #Month3Count     to $Month3Count     0999999
end-procedure


Begin-Procedure Write-F-Record

  write 10 from 'RF':2    ! 1 - 2
                 $Sp:510  ! 3 - 512

  add 1 to #RecordCount

End-Procedure


begin-procedure Convert-Parameters

  move 'X' to $FileType
  move '0' to $DisketteType
end-procedure


begin-procedure Foreign-Address-Test($Format,#MaxLength)
  Let $_StateName = ''
  If $_Country = 'USA'
    move ' ' to $_ForeignAddrInd
    move $_Zip to $_ZipExtsn ~~~~~~xxxx
    If instr($_Zip,'-',6) = 0
    and $_ZipExtsn <> '    '
      let $_Zip = substr($_Zip,1,5) || '-' || substr($_Zip,6,4)
    End-if
    move $_Country to $priorCountry
  else
    move 'X' to $_ForeignAddrInd

    If $_Country <> $priorCountry
      do Get-Country-Code
      move $_Country to $priorCountry
      move &_Descr   to $CountryName
      move &_COUNTRY_CD_1042 to $_Country-2Char
    End-if

    if $_StateCd = ''
      Show '      Note: No state code for this record.  Country - ' $_Country
    else
      do Get-State-Code
      move &_STATE.DESCR to $_StateName
      uppercase $_StateName
    end-if

    Evaluate $Format
      when = 'Format 1'
        let #Length = LENGTH(RTRIM($CountryName,' '))
        let $_City = SUBSTR(RTRIM($_City,' '),1,(#MaxLength - #Length - 4) )
        let $_City = $_City || ' ' || $_StateCd || ' ' || $CountryName
        move ' ' to $_StateCd
      when = 'Format 2'
        let $_StateCd = $_StateCd || ' ' || $CountryName
        uppercase $_StateCd
      when = 'Format 3'
        let $_StateCd = $_StateCd
        uppercase $_StateCd
    End-evaluate
  End-if
  uppercase $_City
  let $EditZip = RTRIM($_Zip, ' ')
  let #DashPosition = INSTR($EditZip,'-',1)
  if #DashPosition <> 6
    move $_Zip to $_ZipExtsn ~~~~~xxxx
  else
    move $_Zip to $_ZipExtsn ~~~~~~xxxx
  end-if
End-Procedure


Begin-Procedure Get-Country-Code
  Let $Record_found = 'N'
Begin-Select
COUNTRY_2CHAR
DESCR
  Let $Record_found = 'Y'
FROM  PS_COUNTRY_TBL
WHERE COUNTRY = $Country
End-Select

 If $Record_found = 'N'
   Show ' Two Character Country code for ' $Country ' doesn''t exist in Country table.'
   Stop
 End-if

  Let $Record_found = 'N'
Begin-Select
COUNTRY_CD_1042
  Let $Record_found = 'Y'
FROM  PS_CNTRY_1042_TBL
WHERE COUNTRY = $Country
End-Select

 If $Record_found = 'N'
   Show ' Two Character Country code for ' $Country ' doesn''t exist in Country 1042 table.'
   Stop
 End-if
End-Procedure


Begin-Procedure Get-State-Code
  Let $Record_found = 'N'
Begin-Select
STATE.DESCR
  Let $Record_found = 'Y'
FROM  PS_STATE_NAMES_TBL  STATE
WHERE STATE.COUNTRY = $Country
  AND STATE.STATE   = $StateCd
End-Select

 If $Record_found = 'N'
   Show ' State code State : ' $StateCd ', Country : ' $Country ' doesn''t exist in State table.'
   Stop
 End-if
End-Procedure


Begin-Procedure Write-Transmitter-Record

Begin-Select
RC960.EMPLR_PIN
RC960.RESUB_INDICATOR
RC960.RESUB_TLCN
RC960.CONTACT_NAME_SSA
RC960.TELEPHONE_NBR
RC960.PHONE_EXTN
RC960.EMAIL_ADDR_MMREF
RC960.CONTACT_FAX
RC960.NOTIFY_METHOD

FROM PS_TAX960_PARM RC960
End-Select

  if rtrim(&RC960.CONTACT_NAME_SSA, ' ') = ''
    Show 'MMREF Parameters must be set up on SETUP Menu.'
    Show 'Re-execute after establishing MMREF Parameters.'
    Stop
  End-if

  Let $RC960.CONTACT_NAME_SSA    = translate(&RC960.CONTACT_NAME_SSA,',','')
  move &CT.Federal_EIN          to $FedEIN  099999999
  move &RC960.EMAIL_ADDR_MMREF  to $RC960.Email_addr

  If &RC960.PHONE_EXTN <> 0
    move &RC960.PHONE_EXTN to $Phone_Extn
  Else
    move '    ' to $Phone_Extn
  End-if

  move &CT.Descr        to $Xmitr_Name
  move &CT.Address1     to $X.Address1
  move &CT.Address2     to $X.Address2
  move &CT.City         to $City
  let  $StateCd = rtrim(&CT.State, ' ')
  move &CT.Postal       to $Zip
  move &CT.Country      to $Country
  do Foreign-Address-Test('Format 3',25)

  uppercase $Xmitr_Name
  uppercase $X.Address1
  uppercase $X.Address2
  uppercase $City
  uppercase $StateCd
  uppercase $RC960.Email_addr

  If $ForeignAddrInd = 'X'
     Let $US_Zip      = ' '
     Let $US_ZipXtnsn = ' '
     Let $US_State    = ' '
     Let $Frgn_Zip    = $Zip
     Let $Frgn_State  = $StateName
  Else
     Let $US_Zip      = $Zip
     Let $US_ZipXtnsn = $ZipExtsn
     Let $US_State    = $StateCd
     Let $Frgn_Zip    = ' '
     Let $Frgn_State  = ' '
     Let $Country-2Char   = ' '
  End-if

          write 10 from 'RA':2      ! 1 - 2
                     $FedEIN:9      ! 3 - 11
                         $Sp:205    ! 12 - 216
                 $Xmitr_Name:57     ! 217 - 273
                 $X.Address1:22     ! 274 - 295
                 $X.Address2:22     ! 296 - 317
                       $City:22     ! 318 - 339
                   $US_State:2      ! 340 - 341
                     $US_Zip:5      ! 342 - 346
                $US_ZipXtnsn:4      ! 347 - 350
                         $Sp:5      ! 351 - 355
                 $Frgn_State:23     ! 356 - 378
                   $Frgn_Zip:15     ! 379 - 393
              $Country-2Char:2      ! 394 - 395
     $RC960.Contact_name_ssa:27     ! 396 - 422
        &RC960.Telephone_Nbr:15     ! 423 - 437
                 $Phone_Extn:5      ! 438 - 442
                         $Sp:3      ! 443 - 445
           $RC960.Email_addr:40     ! 446 - 485
                         $Sp:3      ! 486 - 488
          &RC960.Contact_Fax:10     ! 489 - 498
                         $Sp:1      ! 499
                         'L':1      ! 500          Preparer Code - Self Prepared
                         $Sp:12     ! 501 - 512

  add 1 to #RecordCount

End-Procedure


#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getfrmat.sqc'  !Get-Diskette-Format procedure
#Include 'rptsmmry.sqc'  !Print-Summary-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls
#Include 'getearns.sqc'
