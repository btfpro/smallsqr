!***********************************************************************
!  PAY709: Print contents of WageLs Table - General Ledger Data
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
! Copyright (C) 1988, 2010, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2010/09/14:10:46:38                                 
!       $Release:  HR9                                                 
!    $Resolution:  813279                                              
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do StdAPI-Term
end-report

begin-procedure Init-Report
  move 'PAY709' to $ReportID
  move 'Wage Loss Plan Table'    to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do StdAPI-Init
end-procedure

#Include 'setup31.sqc'  !printer and page-size initialization

begin-HEADING 5
  #Include 'stdhdg01.sqc'

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

 print '***'                   (+1,2,3)
WL.COMPANY                     (0,+2,3)
! descr                          (0,+3,30)
  print 'Effective Date: '     (0,+3)

WL.EFFDT  &WL.EffDt
  do Format-DateTime(&WL.EffDt, $out, {DEFDATE}, '', '')
  print $out                   (0,+1,10)

WL.EFF_STATUS
  if &WL.eff_status = 'I'
    print 'I'          (0,0,1)
  else
    print ' '          (0,0,1)
  end-if
  print 'RCT NO: '             (0,+4)
WL.CANADA_RCT_NO               (0,+1)
  print 'Payroll Account No: '        (0,+4)
WL.CANADA_BN                   (0,+1)
  print 'Wage Loss Plan:   '   (+2,5)
WL.WAGE_LOSS_PLAN              (0,+1)
  print 'EFF Tax Year  :   '   (0,+3)
WL.EFF_TAX_YR                  (0,+1)
  print 'UI Rates    ER:   '   (0,+3)
WL.UI_ER_RT
  print &WL.UI_ER_Rt           (0,+1)  edit 9.9999
  print 'QC ER:   '   (0,+3)
WL.QEI_ER_RT
  print &WL.QEI_ER_Rt          (0,+1)  edit 9.9999

  move 'Y' to $New-Plan
  do Liability-Accounts
  do Expense-Accounts

  POSITION (+2)

FROM PS_WAGELS_PLAN_TBL WL

ORDER BY WL.COMPANY ASC, WL.WAGE_LOSS_PLAN ASC, WL.EFFDT DESC

END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Liability-Accounts
BEGIN-SELECT

WLL.TAX_CLASS_CAN
WLL.GL_NBR

  move 'TAX_CLASS_CAN'     to $FieldName
  move &WLL.TAX_CLASS_CAN  to $FieldValue
  move &WL.EFFDT           to $AsOfDate
  move 'ENG'               to $Curr_Language_Cd
  do Read-Translate-Table

  if  rtrim(&WLL.GL_NBR,' ') <> ''
    if $New-Plan = 'Y'
      move 'N' to $New-Plan
      POSITION (+1)
    end-if
    print 'Liability Account#: ' (+1,10)
    print $XlatLongName          (0,31)
    print &WLL.GL_NBR               (0,71)

  end-if


FROM PS_WGLS_LIA_GL_ACC WLL
WHERE WLL.COMPANY        = &WL.COMPANY
  AND WLL.WAGE_LOSS_PLAN = &WL.WAGE_LOSS_PLAN
  AND WLL.EFFDT          = &WL.EFFDT

ORDER BY WLL.TAX_CLASS_CAN

END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Expense-Accounts
BEGIN-SELECT

WLE.TAX_CLASS_CAN
WLE.GL_NBR

  move 'TAX_CLASS_CAN'     to $FieldName
  move &WLE.TAX_CLASS_CAN  to $FieldValue
  move &WL.EFFDT           to $AsOfDate
  move 'ENG'               to $Curr_Language_Cd
  do Read-Translate-Table

  if  rtrim(&WLE.GL_NBR,' ') <> ''
    if $New-Plan = 'Y'
      move 'N' to $New-Plan
      POSITION (+1)
    end-if
    print 'Expense Account#:   ' (+1,10)
    print $XlatLongName          (0,31)
    print &WLE.GL_NBR               (0,71)

  end-if


FROM PS_WGLS_EXP_GL_ACC WLE
WHERE WLE.COMPANY        = &WL.COMPANY
  AND WLE.WAGE_LOSS_PLAN = &WL.WAGE_LOSS_PLAN
  AND WLE.EFFDT          = &WL.EFFDT

ORDER BY WLE.TAX_CLASS_CAN

END-SELECT
END-PROCEDURE

#Include 'reset.sqc'     !Reset printer procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
