!***********************************************************************
! CBRADDR:  COBRA Address
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/11:17:59:09                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************

!*****************************************************
Begin-Procedure Get-Empl-Name-Address
!*****************************************************

Begin-Select
PD.NAME
PER.BIRTHDATE

  let $emplid_name = rtrim(&PD.name,' ')
  let $name = rtrim(&PD.name,' ')

  if ($get_name_only = 'N')

    do Get-Other-Addr ($emplid, $Addroth1, $Addroth2, $Cityoth, $Stateoth, $Postaloth)
    if rtrim( $Addroth1,' ') = ''

        do Get-Empl-Address ($emplid,$AddrType, $AddressFound)

        move $GETADDR_address1 to $address1
        move $GETADDR_address2 to $address2
        let $city_state = rtrim($GETADDR_city,' ') ||', '||$GETADDR_state ||'  '|| $GETADDR_postal
    else
        move  $Addroth1 to $address1
        move $Addroth2 to $address2
        let $city_state = rtrim($Cityoth,' ') ||', '||$Stateoth ||'  '|| $Postaloth
    end-if

    move &PER.BIRTHDATE to $birthdt
  end-if

  do Get-Employee-NID

FROM PS_PERSON_NAME PD, PS_PERSON PER
WHERE PD.EMPLID = $emplid
AND   PER.EMPLID = PD.EMPLID
End-Select


End-Procedure

!**********************************
Begin-Procedure Get-Employee-NID
!**********************************

Begin-SELECT
PN.NATIONAL_ID

FROM PS_PERS_NID PN
WHERE PN.EMPLID      = $emplid
  AND PN.COUNTRY     = {NID_COUNTRY}
  AND PN.PRIMARY_NID = 'Y'
End-SELECT

   move &PN.NATIONAL_ID to $ssn

End-Procedure

!*****************************************************
Begin-Procedure Get-Dep-Name-Address
!*****************************************************

Begin-Select
DBK.NAME
  move &DBK.name to $name
DBK.SAME_ADDRESS_EMPL
DBK.ADDRESS1
DBK.ADDRESS2
DBK.CITY
DBK.STATE
DBK.POSTAL
DBK.RELATIONSHIP
DBK.BIRTHDATE

  if &DBK.SAME_ADDRESS_EMPL = 'Y'
     do Get-Empl-Address ($emplid,$AddrType, $AddressFound)
     move $GETADDR_address1 to $address1
     move $GETADDR_address2 to $address2
     let $city_state = rtrim($GETADDR_city,' ') ||', '||$GETADDR_state ||'  '|| $GETADDR_postal
  else
     move &DBK.address1 to $address1
     move &DBK.address2 to $address2
     let $city_state = rtrim(&DBK.city,' ') ||', '||&DBK.state ||'  '|| &DBK.postal
  end-if

  move &DN.NATIONAL_ID to $ssn
  do Rotate-Name
  let $emplid_name = $name
  let $relation = &DBK.RELATIONSHIP
  let $birthdt = &DBK.BIRTHDATE
  do Get-Dependent-NID

FROM PS_DEPENDENT_BENEF DBK
WHERE DBK.EMPLID = $emplid
  AND DBK.DEPENDENT_BENEF = $ltr_getter
End-Select

End-Procedure

!*****************************************************
Begin-Procedure Get-Dependent-NID
!*****************************************************

Begin-SELECT
DN.NATIONAL_ID

FROM PS_DEP_BENEF_NID DN
WHERE DN.EMPLID          = $emplid
  AND DN.DEPENDENT_BENEF = $ltr_getter
  AND DN.COUNTRY         = {NID_COUNTRY}
  AND DN.PRIMARY_NID     = 'Y'
End-SELECT

  move &DN.NATIONAL_ID to $ssn

End-Procedure

!*****************************************************
Begin-Procedure Get-COBRA-Administrator
!*****************************************************

Begin-Select
CA.CONTACT_NAME
CA.BUSINESS_TITLE
CA.ADDRESS1
CA.ADDRESS2
CA.CITY
CA.STATE
CA.POSTAL
CA.PHONE

  move &CA.Contact_Name to $admin_name
  move &CA.business_title to $admin_title
  move &CA.address1 to $admin_address1
  let $CAcity_state = rtrim(&CA.city, ' ') ||', '||&CA.state ||'  '|| &CA.postal
  if rtrim(&CA.address2,' ') = ''
    let $admin_address2 = $CAcity_state
    let $admin_address3 = &CA.phone
    let $admin_address4 = ' '
  else
    let $admin_address2 = &CA.address2
    let $admin_address3 = $CAcity_state
    let $admin_address4 = &CA.phone
  end-if
FROM PS_BEN_DEFN_PGM PG,
     PS_BEN_CONTACT_TBL CA
WHERE PG.BENEFIT_PROGRAM = $benefit_program
AND   PG.EFFDT = (SELECT MAX (X.EFFDT)
                  FROM PS_BEN_DEFN_PGM X
                  WHERE X.BENEFIT_PROGRAM = PG.BENEFIT_PROGRAM
                  AND   X.EFFDT <= $event_dt)
AND   CA.BENEF_CONTACT_ID = PG.COBRA_CONTACT_ID
AND   CA.EFFDT = (SELECT MAX (X.EFFDT)
                  FROM PS_BEN_CONTACT_TBL X
                  WHERE X.BENEF_CONTACT_ID = CA.BENEF_CONTACT_ID
                  AND   X.EFFDT <= $event_dt)
End-Select

End-Procedure

!*****************************************************
Begin-Procedure Get-Cobra-Emplid-Address
!*****************************************************


  do Get-Other-Addr ($cobra_emplid, $Addroth1, $Addroth2, $Cityoth, $Stateoth, $Postaloth)

  if rtrim($Addroth1,' ') = ''
      do Get-Empl-Address ($cobra_emplid,$AddrType, $AddressFound)
      move $GETADDR_address1 to $address1
      move $GETADDR_address2 to $address2
      let $city_state = rtrim($GETADDR_city,' ') ||', '||$GETADDR_state ||'  '|| $GETADDR_postal
  else
      move $Addroth1 to $address1
      move $Addroth2 to $address2
      let $city_state = rtrim($Cityoth ,' ') ||', '|| $Stateoth ||'  '||$Postaloth
  end-if


End-Procedure


!********************************************************************************************************************!
begin-procedure Get-Other-Addr ($Emplid, :$Addroth1, :$Addroth2, :$Cityoth, :$Stateoth, :$Postaloth)
!********************************************************************************************************************!

begin-SELECT
ADDRESS1_OTHER
ADDRESS2_OTHER
ADDRESS3_OTHER
CITY_OTHER
STATE_OTHER
POSTAL_OTHER
COUNTRY_OTHER

FROM  PS_PERSON_ADDROTH
WHERE EMPLID = $Emplid
end-SELECT

let $Addroth1      = rtrim(&ADDRESS1_OTHER,' ')
let $Addroth2      = rtrim(&ADDRESS2_OTHER,' ')
let $Addroth3      = rtrim(&ADDRESS3_OTHER,' ')
let $Cityoth       = rtrim(&CITY_OTHER,' ')
let $Stateoth      = rtrim(&STATE_OTHER,' ')
let $Postaloth     = rtrim(&POSTAL_OTHER,' ')
let $Countryoth    = rtrim(&COUNTRY_OTHER,' ')


end-procedure

!*********************************************************************
#Include 'getaddr.sqc' !Get current address for specified address type
!*********************************************************************
