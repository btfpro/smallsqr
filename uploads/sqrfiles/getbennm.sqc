!***********************************************
!  GetBenNm:  Get the Benefit Name
!***********************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:09:05:52                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!*******************************
begin-procedure Get-Benefit-Name
!*******************************

  !Keys passed in:  $Plan_Type, $Benefit_Plan, $AsOfDate
  !Values returned: $BenefitName, $BenefitNameAbbrv

  move '' to $BenefitName
  move '' to $BenefitNameAbbrv

  if Rtrim($Benefit_Plan,' ') = '' or Rtrim($Plan_Type,' ') = ''
    goto GetBenNmExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
BP.DESCR
BP.DESCRSHORT

 let $BenefitName      = Rtrim(&BP.DESCR,' ')
 let $BenefitNameAbbrv = Rtrim(&BP.DESCRSHORT,' ')

FROM PS_BENEF_PLAN_TBL BP
WHERE BP.BENEFIT_PLAN = $Benefit_Plan
  AND BP.PLAN_TYPE    = $Plan_Type
  AND BP.EFFDT       <= $AsOfDate
  AND BP.EFFDT =
        (SELECT MAX(EFFDT)
         FROM   PS_BENEF_PLAN_TBL
         WHERE  BENEFIT_PLAN = BP.BENEFIT_PLAN
           AND  PLAN_TYPE    = BP.PLAN_TYPE
           AND  EFFDT       <= $AsOfDate)
end-SELECT

  do Get-Benefit-Name-Lang

GetBenNmExit:

end-procedure

!************************************
begin-procedure Get-Benefit-Name-Lang
!************************************

  !Keys passed in:  $Plan_Type, $Benefit_Plan, $AsOfDate
  !Values returned: $BenefitName, $BenefitNameAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetBenNmLngExit
  end-if

  if Rtrim($Benefit_Plan,' ') = '' or Rtrim($Plan_Type,' ') = ''
    goto GetBenNmLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
BPL.DESCR
BPL.DESCRSHORT

  if Rtrim(&BPL.DESCR,' ') <> ''
    let $BenefitName = Rtrim(&BPL.DESCR,' ')
  end-if
  if Rtrim(&BPL.DESCRSHORT,' ') <> ''
    let $BenefitNameAbbrv = Rtrim(&BPL.DESCRSHORT,' ')
  end-if

FROM PS_BENEF_PLAN_LANG BPL
WHERE BPL.BENEFIT_PLAN = $Benefit_Plan
  AND BPL.PLAN_TYPE    = $Plan_Type
  AND BPL.LANGUAGE_CD  = $Curr_Language_Cd
  AND BPL.EFFDT       <= $AsOfDate
  AND BPL.EFFDT =
         (SELECT MAX(EFFDT)
          FROM  PS_BENEF_PLAN_LANG
          WHERE BENEFIT_PLAN = BPL.BENEFIT_PLAN
            AND PLAN_TYPE    = BPL.PLAN_TYPE
            AND LANGUAGE_CD  = BPL.LANGUAGE_CD
            AND EFFDT       <= $AsOfDate)
end-SELECT

GetBenNmLngExit:

end-procedure

!***************************** End of Include File **************************
