!***********************************************************************
!FG1514D: This is the OPM Form 1514-Military Service Deposit Worksheet *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:11:44:45                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
! >  5/10/01 -- Added new field to PS_GVT_SVCTRN_TBL.INTEREST_RATE select.  We will
!    also print this field in the report. 
!***********************************************************************
!****************************************************
!           *** PROCEDURE GET-1514-DATA ***
!****************************************************

Begin-Procedure Get-1514-Data

!-------------------------------------------
!         *** GET-MILITARY-DATA ***
!-------------------------------------------
Begin-Select
SD.EMPLID
SD.EMPL_RCD
SD.SEQ_NO
SD.GVT_STATUS_ACCT
SD.STATUS_DT
SD.GVT_SRV_AGENCY
SD.FROM_DATE
SD.THRU_DATE
SD.AMT_OF_EARNS
SD.WITHHOLD_PCT
SD.GVT_DESCR8
SD.GVT_MILDEP_IAD

   Let $EMPLID   = &SD.EMPLID
   Let #EMPL_RCD = &SD.EMPL_RCD
   Let #SEQ_NO   = &SD.SEQ_NO
   Let #Current_Row = 1
   Let #Start_line  = 23

   if $first_page = 'N'
      new-page
   else
      let $first_page = 'N'
   end-if

   Do Print-Report
   Do Get-Transaction-Data

FROM PS_GVT_SVCDEP_TBL SD
WHERE [$SVCDEP_WHERE]
End-SELECT

End-Procedure

Begin-Procedure Get-Transaction-Data
!---------------------------------------------
!        *** GET-TRANSACTION-DATA ***
!---------------------------------------------

Begin-Select

TR.EMPLID
TR.EMPL_RCD
TR.SEQ_NO
TR.TRANS_DT
TR.GVT_TRANS_TYPE
TR.TRANS_AMT
TR.BEGIN_BAL
TR.END_BAL
TR.INTEREST_RATE
TR.DESCR50

   let $trans_type = rtrim(&TR.GVT_TRANS_TYPE,' ')
   DO PRINT-TRANSACTION-DATA

FROM PS_GVT_SVCTRN_TBL TR
WHERE TR.EMPLID = $EMPLID
AND   TR.EMPL_RCD = #EMPL_RCD
AND   TR.SEQ_NO = #SEQ_NO
AND   TR.TRANS_AMT <> 0
ORDER BY TR.TRANS_DT

End-Select

End-Procedure

!----------------------------------------------
!       *** GET-HEADER-DATA ***
!----------------------------------------------

Begin-Procedure Get-Header-Data

Begin-Select
PN.NAME
PD.BIRTHDATE
E.HIRE_DT
J.COMPANY
J.GVT_RETIRE_PLAN
NID.NATIONAL_ID

  MOVE 'GVT_RETIRE_PLAN' to $FieldName
  MOVE &J.GVT_RETIRE_PLAN to $FieldValue
  Do Read-Translate-Table
  MOVE $XlatLongName to $GVT_RETIRE_PLAN

FROM PS_PERSON PD,
     PS_PERSON_NAME PN,
     PS_EMPLOYMENT E,
     PS_GVT_JOB J,
     PS_GVT_PERS_NID NID

WHERE PD.EMPLID  = $EMPLID
AND   E.EMPLID   = $EMPLID
AND   J.EMPLID   = $EMPLID
AND   NID.EMPLID = $EMPLID
AND   PN.EMPLID  = $EMPLID


End-Select

End-procedure


!----------------------------------------------
!         *** PRINT-TRANSACTION-DATA ***
!----------------------------------------------

Begin-Procedure Print-Transaction-Data


  If #Current_Row > #Max_rows

     New-Page
     Let #Current_Row = 1
     Let #Start_line  = 23
     Do Print-Report

  End-if

  If $trans_type = 'I'

    Do Format-DateTime (&TR.TRANS_DT,$TRDATE, {DEFMDY}, '','')

    Position (#Start_line)
    graphic () font 5 10
    Print $TRDATE                        (0,2,0)
    Print &TR.TRANS_AMT                  (0,14,0)  Edit 99999.99
    Print &TR.INTEREST_RATE            (0,26,0)  Edit 99999.99
    Print &TR.END_BAL                    (0,38,0)  Edit 99999.99

  Else

    Do Format-DateTime (&TR.TRANS_DT,$TRDATE, {DEFMDY}, '','')

    Position (#Start_line)
    graphic () font 5 10

    Print $TRDATE                        (0,52,0)
    Print &TR.TRANS_AMT                  (0,68,0)  Edit 99999.99
    Print &TR.END_BAL                    (0,85,0)  Edit 99999.99

  end-if

  Let #Start_line  = #Start_line + 2
  Let #Current_Row = #Current_Row + 1

End-Procedure


!-------------------------------------------------
!  *** CONVERT DAYS LOST TO YY/MM/DD FORMAT ***
!-------------------------------------------------
begin-procedure DateDiffYMD($Date1,$Date2,:#wkYears,:#wkMonths,:#wkDays)
   declare-variable
        date    $wkDate1
        date    $wkDate2
        date    $wkDate3  !Used as a working Date
        integer #wkDays
        integer #wkMonths
        integer #wkYears
    end-declare
#ifdef debugx
    show 'Entering DATEMATH.SQC: DateDiffYMD'
    show ' Input $Date1: ' $Date1
    show ' Input $Date2: ' $Date2
#end-if

! Convert Input Date Strings into Date Variables
    let $wkDate1 = strtodate($Date1,'YYYY-MM-DD')
    let $wkDate2 = strtodate($Date2,'YYYY-MM-DD')

! Calculate Years
    let  #wkYears  = datediff($wkDate2, $wkDate1,'year')

! Subtract Years from $Date2 to Build $Date3
    let  $wkDate3  = dateadd($wkDate2,'year',(#wkYears * -1))

! Calculate Months
    let  #wkMonths = datediff($wkDate3, $wkDate1,'Month')

! Subtract Months from $Date3 to build new $Date3
    let  $wkDate3  = dateadd($wkDate3,'month',(#wkMonths * -1))

! Calculate Days
    let  #wkDays   = datediff($wkDate3, $wkDate1,'Day')

#ifdef debugx
    show 'Exiting DATEMATH.SQC: DateDiffYMD'
    show '#wkYears : ' #wkYears
    show '#wkMonths: ' #wkMonths
    show '#wkDays  : ' #wkDays
#end-if

end-procedure


!-----------------------------------------------
!         *** INCLUDE STATEMENTS ***
!-----------------------------------------------
#include 'readxlat.sqc'  ! Read-Translate-Table Procedure

!***************************************************************************
!                        >>>>  END OF FILE  <<<<                           !
!***************************************************************************
