!***********************************************************************
!  GetCvgCd:  Retrieves coverage code info from COVRG_CD_TBL
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:09:07:01                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!***********************************
begin-procedure Get-Covrg-Code-Descr
!***********************************

  !Keys passed in:  $CovrgCd, $CovrgEffdt
  !Values returned: $CovrgDescr, $CovrgDescrShort

  move '' to $CovrgDescr
  move '' to $CovrgDescrShort

  if Rtrim($CovrgCd,' ') = ''
    goto GetCvgCdExit
  end-if

  if $CovrgEffdt = ''
    move $AsOfToday to $CovrgEffdt
  end-if

begin-SELECT
CC.DESCR
CC.DESCRSHORT

   let $CovrgDescr      = Rtrim(&CC.DESCR,' ')
   let $CovrgDescrShort = Rtrim(&CC.DESCRSHORT,' ')

FROM PS_COVRG_CD_TBL CC
WHERE CC.COVRG_CD = $CovrgCd
  AND CC.EFFDT   <= $CovrgEffdt
  AND CC.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_COVRG_CD_TBL
         WHERE COVRG_CD = CC.COVRG_CD
           AND EFFDT   <= $CovrgEffdt)
end-SELECT

  do Get-Covrg-Code-Descr-Lang

GetCvgCdExit:

end-procedure

!****************************************
begin-procedure Get-Covrg-Code-Descr-Lang
!****************************************

  !Keys passed in:  $CovrgCd, $CovrgEffdt
  !Values returned: $CovrgDescr, $CovrgDescrShort

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetCvgCdLngExit
  end-if

  if Rtrim($CovrgCd,' ') = ''
    goto GetCvgCdLngExit
  end-if

  if $CovrgEffdt = ''
    move $AsOfToday to $CovrgEffdt
  end-if

begin-SELECT
CCL.DESCR
CCL.DESCRSHORT

   if Rtrim(&CCL.DESCR,' ') <> ''
     let $CovrgDescr = Rtrim(&CCL.DESCR,' ')
   end-if
   if Rtrim(&CCL.DESCRSHORT,' ') <> ''
     let $CovrgDescrShort = Rtrim(&CCL.DESCRSHORT,' ')
   end-if

FROM PS_COVRG_CD_LANG CCL
WHERE CCL.COVRG_CD    = $CovrgCd
  AND CCL.LANGUAGE_CD = $Curr_Language_Cd
  AND CCL.EFFDT      <= $CovrgEffdt
  AND CCL.EFFDT =
         (SELECT MAX(EFFDT)
          FROM  PS_COVRG_CD_LANG
          WHERE COVRG_CD    = CCL.COVRG_CD
            AND LANGUAGE_CD = CCL.LANGUAGE_CD
            AND EFFDT      <= $CovrgEffdt)
end-SELECT

GetCvgCdLngExit:

end-procedure

!***************************** End of Include File **************************

