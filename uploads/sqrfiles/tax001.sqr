!***********************************************************************
!  TAX001:  Tax Deposit Summary                                        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2012, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2012/11/08:11:50:50                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  872695                                              !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#Include 'setup32.sqc'  !Printer and page-size initialization
#Include 'usarpt.sqc'   !Get USA variables


begin-report
  do Init-Report
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'TAX001' to $ReportID
  move 'Tax Deposit Summary' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Array-Initialization
  do Payroll-Report-Initialization
end-procedure

begin-procedure Array-Initialization
  move '**' to $priorState
  move ' '  to $USFederal_data_exists
  move  0   to #OtherTaxCount
  move  0   to #TaxType_Location


  create-array name=OtherTaxes size=8  -
    field=Jurisdiction:char            -
    field=TaxType:char:24              -
    field=TxGrs_Cur:number:24          -
    field=Tax_Cur:number:24            -
    field=ER_Match:number:24           -
    field=WithholdingGross:number      -
    field=WithholdingTax:number        -
    field=EIC:number                   -
    field=Unempl_ER_Gross:number       -
    field=Unempl_ER_Tax:number         -
    field=Unempl_SP_Gross:number       -
    field=Unempl_SP_Tax:number
    !   0 = Individual States
    !   1 = US excluding Puerto Rico and  Territories
    !   2 = Puerto Rico
    !   3 = American Samoa
    !   4 = Guam
    !   5 = Virgin Islands
    !   6 = Territories (AS + GU + VI)
    !   7 = Company Totals ($U)

  create-array name=Totals size=5 -
    field=TotalName:char          -
    field=total:number:5          -
    field=startCol:number:5

  put 'Grand Total'            into Totals(1) TotalName
  put 'Company Total'          into Totals(2) TotalName
  put 'Pay Period Total'       into Totals(3) TotalName
  put 'Check Date Total'       into Totals(4) TotalName
  put  52                      into Totals(0) startCol(0)
  put  92                      into Totals(0) startCol(1)
  put 106                      into Totals(0) startCol(2)
  put 121                      into Totals(0) startCol(3)
  put 151                      into Totals(0) startCol(4)
end-procedure

begin-heading 9
  #Include 'stdhdgpi.sqc'
  #Include 'stdhdg05.sqc'
  print '** NOTE: Totals on this report do not include any online balance adjustment panel activity.**'     (+1,27)
  print ' State/       Locality /Abbrv        <------ WITHHOLDING -----' (+2,1)
  print '-> <-------------------  OTHER  ----------------------->                 ' ()
  print ' <--------------  UI  ------------->'                    ()
  print 'SWT EIN       County/Loc EIN          Taxable Gross          Tax ' (+1,1)
  print ' Type       Taxable Gross           Tax      Employer  Total Deposit' ()
  print '   Taxable Gross           Tax   Rate'                       ()
end-heading

begin-procedure Report
begin-SELECT
A.COMPANY    () on-break print=never level=1 procedure=Company-Change
A.PAY_END_DT () on-break print=never level=2 procedure=PayEndDate-Change
A.CHECK_DT   () on-break print=never level=3 procedure=CheckDate-Change
B.STATE      () on-break print=never level=4 procedure=State-Local-Change
B.LOCALITY   () on-break print=never level=4 procedure=State-Local-Change
B.TAX_CLASS,B.TXGRS_CUR,B.TAX_CUR

  do Process-Record

FROM  PS_PAY_CALENDAR A,
      PS_PAY_TAX      B
WHERE [$SlctCalendar]
  AND B.OFF_CYCLE IN ($SlctOffCycleA, $SlctOffCycleB)
  AND B.PAGE_NUM BETWEEN #SlctPageFrom AND #SlctPageThru
  AND A.COMPANY    = B.COMPANY
  AND B.PAYGROUP   = A.PAYGROUP
  AND B.PAY_END_DT = A.PAY_END_DT
ORDER BY A.COMPANY,
         A.PAY_END_DT,
         A.CHECK_DT,
         B.STATE,
         B.LOCALITY
end-SELECT

  move 1 to #changeLevel
  move 'Y' to $End
  do Print-Totals
end-procedure

begin-procedure Process-Record
  if &A.Company <> $priorCompany
    move &A.Company to $Company
    move &A.Company to $priorCompany
    do Get-Company-Data  !for Federal_EIN, SUT Rate
    move &CT.Federal_EIN to $FedEIN 099999999
  end-if

  move &A.Pay_End_Dt to $PayEndDate
  move &A.Check_Dt   to $AsOfDate
  let $State = rtrim(&B.State, ' ')
  move &B.Locality   to $Locality

  move 0 to #Jurisdiction_Index  ! Individual States
  evaluate $State
    when = '$U'                  ! Company Totals
      move 7 to #Jurisdiction_Index
      put '$U' into OtherTaxes(#Jurisdiction_Index)  Jurisdiction
      if #TaxType_Location = 0
        move 7 to #TaxType_Location
      end-if
    break
    when = '$UPR'                ! Puerto Rico
      move 2 to #Jurisdiction_Index
      put '$UPR' into OtherTaxes(#Jurisdiction_Index) Jurisdiction
      if #TaxType_Location = 0
        move 2 to #TaxType_Location
      end-if
    break
    when = '$UAS'                ! American Samoa
      move 3 to #Jurisdiction_Index
      put '$UAS' into OtherTaxes(#Jurisdiction_Index) Jurisdiction
      if #TaxType_Location = 0
        move 3 to #TaxType_Location
      end-if
    break
    when = '$UGU'                ! Guam
      move 4 to #Jurisdiction_Index
      put '$UGU' into OtherTaxes(#Jurisdiction_Index) Jurisdiction
      if #TaxType_Location = 0
        move 4 to #TaxType_Location
      end-if
    break
    when = '$UVI'                ! Virgin Islands
      move 5 to #Jurisdiction_Index
      put '$UVI' into OtherTaxes(#Jurisdiction_Index) Jurisdiction
      if #TaxType_Location = 0
        move 5 to #TaxType_Location
      end-if
    break
  end-evaluate

  let $Tax_Class = rtrim(&B.Tax_Class, ' ')
  evaluate &B.Tax_Class
    when = 'A'  !Non-resident alien (1042)                                !HP99999
      if #A_Index = 0                                                     !HP99999
        add 1 to #OtherTaxCount                                           !HP99999
        move #OtherTaxCount to #A_Index                                   !HP99999
      end-if                                                              !HP99999
        put '1042' into OtherTaxes(#Jurisdiction_Index) TaxType(#A_Index) !HP99999
        array-add &B.TxGrs_Cur &B.Tax_Cur 0                               !HP99999
          to OtherTaxes(#Jurisdiction_Index)                              !HP99999
             TxGrs_Cur(#A_Index) Tax_Cur(#A_Index) ER_Match(#A_Index)     !HP99999
      break                                                               !HP99999

    when = 'B'  !ER/EE
      if #B_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #B_Index
      end-if
        put 'ER/EE' into OtherTaxes(#Jurisdiction_Index) TaxType(#B_Index)
        array-add &B.TxGrs_Cur &B.Tax_Cur &B.Tax_Cur
          to OtherTaxes(#Jurisdiction_Index)
             TxGrs_Cur(#B_Index) Tax_Cur(#B_Index) ER_Match(#B_Index)
      break

    when = 'C'  !Earned Income Credit
      array-add &B.Tax_Cur to OtherTaxes(#Jurisdiction_Index)
         EIC
      break

    when = 'D'  !FICA or SDI EE
      if #D_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #D_Index
      end-if
      if #Jurisdiction_Index > 0
        put 'OASEE' into OtherTaxes(#Jurisdiction_Index) TaxType(#D_Index)
      else
        put 'SDI'   into OtherTaxes(#Jurisdiction_Index) TaxType(#D_Index)
      end-if
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
            to OtherTaxes(#Jurisdiction_Index)
                    TxGrs_Cur(#D_Index) Tax_Cur(#D_Index)
                    ER_Match(#D_Index)
      break

    when = 'G'  !FICA Tips EE
      if #G_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #G_Index
      end-if
      if #Jurisdiction_Index > 0
        put 'OASEE/Tips' into OtherTaxes(#Jurisdiction_Index) TaxType(#G_Index)
      else
        put 'SDI/Tips'   into OtherTaxes(#Jurisdiction_Index) TaxType(#G_Index)
      end-if
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
            to OtherTaxes(#Jurisdiction_Index)
                    TxGrs_Cur(#G_Index) Tax_Cur(#G_Index)
                    ER_Match(#G_Index)
      break

    when = 'E'  !FICA or SDI ER
      if #E_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #E_Index
      end-if
      put 'OASER' into OtherTaxes(#Jurisdiction_Index) TaxType(#E_Index)
      array-add &B.TxGrs_Cur      0 &B.Tax_Cur
        to OtherTaxes(#Jurisdiction_Index)
                TxGrs_Cur(#E_Index) Tax_Cur(#E_Index)
                ER_Match(#E_Index)
      break

    when = 'J'  !FICA or SDI ER
      if #J_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #J_Index
      end-if
      put 'OASER/Tips' into OtherTaxes(#Jurisdiction_Index) TaxType(#J_Index)
      array-add &B.TxGrs_Cur    0   &B.Tax_Cur
         to OtherTaxes(#Jurisdiction_Index)
                 TxGrs_Cur(#J_Index) Tax_Cur(#J_Index)
                 ER_Match(#J_Index)
      break

    when = 'F'  !FICA Medicare/EE
      if #F_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #F_Index
      end-if
      put 'MEDEE' into OtherTaxes(#Jurisdiction_Index) TaxType(#F_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
        to OtherTaxes(#Jurisdiction_Index)
                TxGrs_Cur(#F_Index) Tax_Cur(#F_Index)
                ER_Match(#F_Index)
      break

    when = 'T'  !FICA Medicare Tips/EE
      if #T_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #T_Index
      end-if
      put 'MEDEE/Tips' into OtherTaxes(#Jurisdiction_Index) TaxType(#T_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
        to OtherTaxes(#Jurisdiction_Index)
                TxGrs_Cur(#T_Index) Tax_Cur(#T_Index)
                ER_Match(#T_Index)
      break

    when = 'Q'  !FICA Medicare/ER
      if #Q_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #Q_Index
      end-if
      put 'MEDER' into OtherTaxes(#Jurisdiction_Index) TaxType(#Q_Index)
      array-add &B.TxGrs_Cur 0 &B.Tax_Cur
          to OtherTaxes(#Jurisdiction_Index)
                  TxGrs_Cur(#Q_Index) Tax_Cur(#Q_Index)
                  ER_Match(#Q_Index)
      break

    when = 'Z'  !FICA Medicare Tips/ER
      if #Z_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #Z_Index
      end-if
      put 'MEDER/Tips' into OtherTaxes(#Jurisdiction_Index) TaxType(#Z_Index)
      array-add &B.TxGrs_Cur 0 &B.Tax_Cur
          to OtherTaxes(#Jurisdiction_Index)
                  TxGrs_Cur(#Z_Index) Tax_Cur(#Z_Index)
                  ER_Match(#Z_Index)
      break

    when = 'H'  !Withholding Employee Income Tax
    when = 'P'  !Withholding Employee Occ Priv
      array-add &B.TxGrs_Cur to OtherTaxes(#Jurisdiction_Index)
                                WithholdingGross
      array-add &B.Tax_Cur   to OtherTaxes(#Jurisdiction_Index)
                                WithholdingTax
      break

    when = 'R'  !Employer Only Local
      if #R_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #R_Index
      end-if
      put 'LocER' into OtherTaxes(#Jurisdiction_Index) TaxType(#R_Index)
      array-add &B.TxGrs_Cur 0 &B.Tax_Cur
          to OtherTaxes(#Jurisdiction_Index)
                  TxGrs_Cur(#R_Index) Tax_Cur(#R_Index)
                  ER_Match(#R_Index)
      break

    when = 'U'  !Unemployment - ER
      array-add &B.TxGrs_Cur to OtherTaxes(#Jurisdiction_Index)
                                Unempl_ER_Gross
      array-add &B.Tax_Cur   to OtherTaxes(#Jurisdiction_Index)
                                Unempl_ER_Tax
      break

    when = 'S'  !Unemployment - Special
      array-add &B.TxGrs_Cur to OtherTaxes(#Jurisdiction_Index)
                                Unempl_SP_Gross
      array-add &B.Tax_Cur   to OtherTaxes(#Jurisdiction_Index)
                                Unempl_SP_Tax
      break

    when = 'W'  !VDI EE
      if #W_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #W_Index
      end-if
        put 'VPDIE' into OtherTaxes(#Jurisdiction_Index) TaxType(#W_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
            to OtherTaxes(#Jurisdiction_Index)
                    TxGrs_Cur(#W_Index) Tax_Cur(#W_Index)
                    ER_Match(#W_Index)
      break

    when = 'I'  !FLI EE
      if #I_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #I_Index
      end-if
        put 'FLIEE' into OtherTaxes(#Jurisdiction_Index) TaxType(#I_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
            to OtherTaxes(#Jurisdiction_Index)
                    TxGrs_Cur(#I_Index) Tax_Cur(#I_Index)
                    ER_Match(#I_Index)
      break

    when = 'O'  !FLI EE
      if #O_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #O_Index
      end-if
        put 'VOLFLI' into OtherTaxes(#Jurisdiction_Index) TaxType(#O_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
            to OtherTaxes(#Jurisdiction_Index)
                    TxGrs_Cur(#O_Index) Tax_Cur(#O_Index)
                    ER_Match(#O_Index)
      break

    when = 'X'  !VDI ER
      if #X_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #X_Index
      end-if
      put 'VPDIR' into OtherTaxes(#Jurisdiction_Index) TaxType(#X_Index)
      array-add &B.TxGrs_Cur 0 &B.Tax_Cur
            to OtherTaxes(#Jurisdiction_Index)
                    TxGrs_Cur(#X_Index) Tax_Cur(#X_Index)
                    ER_Match(#X_Index)
      break

    when = 'Y'  !FLI ER
      if #Y_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #Y_Index
      end-if
      put 'VFLIR' into OtherTaxes(#Jurisdiction_Index) TaxType(#Y_Index)
      array-add &B.TxGrs_Cur 0 &B.Tax_Cur
            to OtherTaxes(#Jurisdiction_Index)
                    TxGrs_Cur(#Y_Index) Tax_Cur(#Y_Index)
                    ER_Match(#Y_Index)
      break

    when = 'L'  !New Jersey SWAF
      if #L_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #L_Index
      end-if
        put 'SWAF ' into OtherTaxes(#Jurisdiction_Index) TaxType(#L_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
          to OtherTaxes(#Jurisdiction_Index)
                  TxGrs_Cur(#L_Index) Tax_Cur(#L_Index)
                  ER_Match(#L_Index)
      break

    when = 'M'  !New Jersey WFDP
      if #M_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #M_Index
      end-if
        put 'WFDP ' into OtherTaxes(#Jurisdiction_Index) TaxType(#M_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
          to OtherTaxes(#Jurisdiction_Index)
                  TxGrs_Cur(#M_Index) Tax_Cur(#M_Index)
                  ER_Match(#M_Index)
      break

    when = 'N'  !New Jersey HCSF
      if #N_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #N_Index
      end-if
      put 'HCSF ' into OtherTaxes(#Jurisdiction_Index) TaxType(#N_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
          to OtherTaxes(#Jurisdiction_Index)
                  TxGrs_Cur(#N_Index) Tax_Cur(#N_Index)
                  ER_Match(#N_Index)
      break

    when = 'V'  !Unemployment - EE
      if #V_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #V_Index
      end-if
      put 'UI-EE' into OtherTaxes(#Jurisdiction_Index) TaxType(#V_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
          to OtherTaxes(#Jurisdiction_Index)
                  TxGrs_Cur(#V_Index) Tax_Cur(#V_Index)
                  ER_Match(#V_Index)
      break

    when = '7'  !Additional Medicare
      if #7_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #7_Index
      end-if
      put 'ADDL MED' into OtherTaxes(#Jurisdiction_Index) TaxType(#7_Index)
      array-add &B.TxGrs_Cur &B.Tax_Cur 0
         to OtherTaxes(#Jurisdiction_Index)
                 TxGrs_Cur(#7_Index) Tax_Cur(#7_Index)
                 ER_Match(#7_Index)
      break

    when = '8'  !FICA Exempt
      if #8_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #8_Index
      end-if
      put 'FICA/ER X' into OtherTaxes(#Jurisdiction_Index) TaxType(#8_Index)
      array-add &B.TxGrs_Cur      0 &B.Tax_Cur
        to OtherTaxes(#Jurisdiction_Index)
                TxGrs_Cur(#8_Index) Tax_Cur(#8_Index)
                ER_Match(#8_Index)
      break

    when = '9'  !FICA Tips Exempt
      if #9_Index = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #9_Index
      end-if
      put 'FICATipERX' into OtherTaxes(#Jurisdiction_Index) TaxType(#9_Index)
      array-add &B.TxGrs_Cur    0   &B.Tax_Cur
         to OtherTaxes(#Jurisdiction_Index)
                 TxGrs_Cur(#9_Index) Tax_Cur(#9_Index)
                 ER_Match(#9_Index)
      break
  end-evaluate
end-procedure

begin-procedure Company-Change
  move 2 to #changeLevel
  do Print-Totals
end-procedure

begin-procedure PayEndDate-Change
  move 3 to #changeLevel
  do Print-Totals
end-procedure

begin-procedure CheckDate-Change
  move 4 to #changeLevel
  do Print-Totals
end-procedure

begin-procedure State-Local-Change
  if #current-line > 56  !ensure enough lines for the tax authority
    new-page
  end-if

  if $State <> $priorState
    move $State to $priorState
    do Get-State-Tax-Data
  end-if

  if $State = '$U'   or
     $State = '$UAS' or
     $State = '$UGU' or
     $State = '$UPR' or
     $State = '$UVI'
     move 'Y' to $USFederal_data_exists !'Y'=yes, ' '=no, 'P'=already processed
  else
    if $USFederal_data_exists = 'Y'
      do Process-Federal-Detail
    end-if
    move 0 to #Jurisdiction_Index
    print $StateName         (1,1)

    if RTRIM($Locality, ' ') = ''
      do Format-Number(&SUT_Experience_Rt, $out, '99.999')
      print $out               (0,165)
      print &Employer_ID_SWT   (2,1)
    end-if

    if RTRIM($Locality, ' ') <> ''
      do Get-Local-Tax-Data
      move &Employer_ID_LWT to $Employer_ID_LWT
      print $Locality            (1,15,10)
      print $Localabbr           (1,25,12)
      print $Localcnty           (2,15,12)    ! change to 30 chars, if needed

      if rtrim($Employer_ID_LWT, ' ') <> ''
        print $Employer_ID_LWT     (,+1)
      end-if

      move '' to $Employer_ID_LWT
    end-if
    do Print-detail
    do accum-totals
    do initialize-jurisdiction
  end-if
end-procedure

begin-procedure Process-Federal-Detail
   MOVE 0 TO #TERRITORY_COUNT
  get $Jurisdiction from OtherTaxes(2)
       Jurisdiction

  if $Jurisdiction = '$UPR'
    ADD 1 TO #TERRITORY_COUNT

    move 2 to #Source_Index
    move 1 to #Target_Index
    put 'NET' into OtherTaxes(#Target_Index) Jurisdiction
    do Subtract-Territory
  end-if

  get $Jurisdiction from OtherTaxes(3)
       Jurisdiction
  if $Jurisdiction = '$UAS'
    ADD 1 TO #TERRITORY_COUNT
    move 3 to #Source_Index
    move 1 to #Target_Index
    put 'NET' into OtherTaxes(#Target_Index) Jurisdiction
    do Subtract-Territory
    move 6 to #Target_Index
    put 'TERR' into OtherTaxes(#Target_Index) Jurisdiction
    do Add-Territory
  end-if

  get $Jurisdiction from OtherTaxes(4)
       Jurisdiction
  if  $Jurisdiction = '$UGU'
    ADD 1 TO #TERRITORY_COUNT
    move 4 to #Source_Index
    move 1 to #Target_Index
    put 'NET' into OtherTaxes(#Target_Index) Jurisdiction
    do Subtract-Territory
    move 6 to #Target_Index
    put 'TERR' into OtherTaxes(#Target_Index) Jurisdiction
    do Add-Territory
  end-if

  get $Jurisdiction from OtherTaxes(5)
       Jurisdiction
  if $Jurisdiction = '$UVI'
    ADD 1 TO #TERRITORY_COUNT
    move 5 to #Source_Index
    move 1 to #Target_Index
    put 'NET' into OtherTaxes(#Target_Index) Jurisdiction
    do Subtract-Territory
    move 6 to #Target_Index
    put 'TERR' into OtherTaxes(#Target_Index) Jurisdiction
    do Add-Territory
  end-if

   IF #TERRITORY_COUNT > 1
    move 7 to #Source_Index
    move 1 to #Target_Index
    put 'NET' into OtherTaxes(#Target_Index) Jurisdiction
    do Add-Territory
  end-if

   IF #TERRITORY_COUNT > 1    !$U net (1) must be printed
    move 1 to #Jurisdiction_Index
    get $Jurisdiction from OtherTaxes(#Jurisdiction_Index)
         Jurisdiction
    if #TaxType_Location <> 1
      do Load-TaxType-Values
    end-if
    print 'U.S. Federal'     (1,1)
    let #FUTARate_edit = #FUTARate * 100
    do Format-Number(#FUTARate_edit, $out, 'B9.999')
    print $out               (1,165)
    print $FedEIN            (2,1)  edit xx-xxxxxxx
    do Print-detail
  end-if

  move 2 to #Jurisdiction_Index
  get $Jurisdiction from OtherTaxes(#Jurisdiction_Index)
       Jurisdiction
  if $Jurisdiction = '$UPR'
    if #TaxType_Location <> 2
      do Load-TaxType-Values
    end-if
    print 'Puerto Rico'     (1,1)
    let #FUTARate_edit = #FUTARate * 100
    do Format-Number(#FUTARate_edit, $out, 'B9.999')
    print $out               (1,165)
    print $FedEIN            (2,1)  edit xx-xxxxxxx
    do Print-detail
  end-if

  move 3 to #Jurisdiction_Index
  get $Jurisdiction from OtherTaxes(#Jurisdiction_Index)
       Jurisdiction
  if $Jurisdiction = '$UAS'
    if #TaxType_Location <> 3
      do Load-TaxType-Values
    end-if
    print 'American Samoa'     (1,1)
    let #FUTARate_edit = #FUTARate * 100
    do Format-Number(#FUTARate_edit, $out, 'B9.999')
    print $out               (1,165)
    print $FedEIN            (2,1)  edit xx-xxxxxxx
    do Print-detail
  end-if

  move 4 to #Jurisdiction_Index
  get $Jurisdiction from OtherTaxes(#Jurisdiction_Index)
       Jurisdiction
  if $Jurisdiction = '$UGU'
    if #TaxType_Location <> 4
      do Load-TaxType-Values
    end-if
    print 'Guam'     (1,1)
    let #FUTARate_edit = #FUTARate * 100
    do Format-Number(#FUTARate_edit, $out, 'B9.999')
    print $out               (1,165)
    print $FedEIN            (2,1)  edit xx-xxxxxxx
    do Print-detail
  end-if

  move 5 to #Jurisdiction_Index
  get $Jurisdiction from OtherTaxes(#Jurisdiction_Index)
       Jurisdiction
  if $Jurisdiction = '$UVI'
    if #TaxType_Location <> 5
      do Load-TaxType-Values
    end-if
    print 'Virgin Islands'     (1,1)
    let #FUTARate_edit = #FUTARate * 100
    do Format-Number(#FUTARate_edit, $out, 'B9.999')
    print $out               (1,165)
    print $FedEIN            (2,1)  edit xx-xxxxxxx
    do Print-detail
  end-if

  if #Territory_Count > 1
    move 6 to #Jurisdiction_Index
    get $Jurisdiction from OtherTaxes(#Jurisdiction_Index)
         Jurisdiction
    if #TaxType_Location <> 6
      do Load-TaxType-Values
    end-if
    print '941-SS Totals (AS + GU + VI)' (1,1)
    let #FUTARate_edit = #FUTARate * 100
    do Format-Number(#FUTARate_edit, $out, 'B9.999')
    print $out               (1,165)
    print $FedEIN            (2,1)  edit xx-xxxxxxx
    do Print-detail
  end-if

  move 7 to #Jurisdiction_Index
  get $Jurisdiction from OtherTaxes(#Jurisdiction_Index)
       Jurisdiction
  if $Jurisdiction = '$U'
    if #TaxType_Location <> 7
      do Load-TaxType-Values
    end-if
    print 'Federal Totals' (1,1)
    let #FUTARate_edit = #FUTARate * 100
    do Format-Number(#FUTARate_edit, $out, 'B9.999')
    print $out               (1,165)
    print $FedEIN            (2,1)  edit xx-xxxxxxx
    do Print-detail
    do Accum-totals
  end-if

  move 'P' to $USFederal_data_exists  ! P = Exists and has been printed
end-procedure

begin-procedure Add-Territory

  move 0 to #TaxType_Index
  while #TaxType_Index <= 17
     get #TxGrs_Cur #Tax_Cur #ER_Match from
         OtherTaxes(#Source_Index)
          TxGrs_Cur(#TaxType_Index)
           Tax_Cur(#TaxType_Index)   ER_Match(#TaxType_Index)
     array-add  #TxGrs_Cur #Tax_Cur #ER_Match to
         OtherTaxes(#Target_Index)
                 TxGrs_Cur(#TaxType_Index)   Tax_Cur(#TaxType_Index)
                 ER_Match(#TaxType_Index)
     add 1 to #TaxType_Index
  end-while

     get #WithholdingGross #WithholdingTax #EIC
         #Unempl_ER_Gross #Unempl_ER_Tax #Unempl_SP_Gross #Unempl_SP_Tax
         from OtherTaxes(#Source_Index)
          WithholdingGross  WithholdingTax  EIC
          Unempl_ER_Gross  Unempl_ER_Tax  Unempl_SP_Gross  Unempl_SP_Tax
     array-add #WithholdingGross #WithholdingTax #EIC
               #Unempl_ER_Gross #Unempl_ER_Tax #Unempl_SP_Gross #Unempl_SP_Tax
           to OtherTaxes(#Target_Index)
                WithholdingGross  WithholdingTax  EIC
                Unempl_ER_Gross  Unempl_ER_Tax  Unempl_SP_Gross  Unempl_SP_Tax
end-procedure

begin-procedure Subtract-Territory
  move 1 to #TaxType_Index
  while #TaxType_Index <= 17
     get #TxGrs_Cur #Tax_Cur #ER_Match from OtherTaxes(#Source_Index)
          TxGrs_Cur(#TaxType_Index) Tax_Cur(#TaxType_Index)
          ER_Match(#TaxType_Index)
     array-subtract  #TxGrs_Cur #Tax_Cur #ER_Match from
         OtherTaxes(#Target_Index)
                      TxGrs_Cur(#TaxType_Index)   Tax_Cur(#TaxType_Index)
                      ER_Match(#TaxType_Index)
     add 1 to #TaxType_Index
  end-while

     get #WithholdingGross #WithholdingTax #EIC
         #Unempl_ER_Gross #Unempl_ER_Tax #Unempl_SP_Gross #Unempl_SP_Tax
         from OtherTaxes(#Source_Index)
          WithholdingGross  WithholdingTax  EIC
          Unempl_ER_Gross  Unempl_ER_Tax  Unempl_SP_Gross  Unempl_SP_Tax
     array-subtract #WithholdingGross #WithholdingTax #EIC
               #Unempl_ER_Gross #Unempl_ER_Tax #Unempl_SP_Gross #Unempl_SP_Tax
           from OtherTaxes(#Target_Index)
                     WithholdingGross  WithholdingTax  EIC
                Unempl_ER_Gross  Unempl_ER_Tax  Unempl_SP_Gross  Unempl_SP_Tax
end-procedure

begin-procedure Load-TaxType-Values
  move 0 to #TaxType_Index
  while #TaxType_Index <= 17
    get $TaxType from OtherTaxes(#TaxType_Location)   TaxType(#TaxType_Index)
    put $TaxType into OtherTaxes(#Jurisdiction_Index) TaxType(#TaxType_Index)
    add 1 to #TaxType_Index
  end-while
end-procedure

begin-procedure Print-detail
  get #WithholdingGross #WithholdingTax #EIC
      #Unempl_ER_Gross  #Unempl_ER_Tax
      #Unempl_SP_Gross  #Unempl_SP_Tax    from OtherTaxes(#Jurisdiction_Index)
       WithholdingGross  WithholdingTax EIC
       Unempl_ER_Gross   Unempl_ER_Tax
       Unempl_SP_Gross   Unempl_SP_Tax

  if abs(#WithholdingGross) > 999999999.99
    move #WithholdingGross to $out  99999999999.99mi
  else
    do Format-Number(#WithholdingGross, $out, 'B99,999,999.99mi')
  end-if
  print $out        (1,37)
  if abs(#WithholdingTax) > 99999999.99
    move #WithholdingTax to $out  9999999999.99mi
  else
    do Format-Number(#WithholdingTax, $out, 'B9,999,999.99mi')
  end-if
  print $out        (0,52)
  move #WithholdingTax to #TotalDeposit

  if #EIC <> 0
    print '(EIC)'   (2,40)
    if abs(#EIC) > 99999999.99
      move #EIC to $out  9999999999.99mi
    else
      do Format-Number(#EIC, $out, 'B9,999,999.99mi')
    end-if
    print $out      (0,52)
    add #EIC to #TotalDeposit
  end-if

  move 0 to #Tax_Cur_Total
  move 0 to #ER_Match_Total
  move 0 to #TaxType_Index
  move 1 to #Detail_Lineno
  while #TaxType_Index <= 17
    get $TaxType #TxGrs_Cur #Tax_Cur #ER_Match
      from OtherTaxes(#Jurisdiction_Index)
          TaxType(#TaxType_Index)  TxGrs_Cur(#TaxType_Index)
          Tax_Cur(#TaxType_Index)  ER_Match(#TaxType_Index)

    if #TxGrs_Cur <> 0 or #Tax_Cur <> 0 or #ER_Match <> 0
      print $TaxType     (#Detail_Lineno,67)
      add 1 to #Detail_Lineno
      if abs(#TxGrs_Cur) > 999999999.99
        move #TxGrs_Cur to $out  99999999999.99mi
      else
        do Format-Number(#TxGrs_Cur, $out, 'B99,999,999.99mi')
      end-if
      print $out         (0,77)
      if abs(#Tax_Cur) > 99999999.99
        move #Tax_Cur to $out  9999999999.99mi
      else
        do Format-Number(#Tax_Cur, $out, 'B9,999,999.99mi')
      end-if
      print $out         (0,92)
      if abs(#ER_Match) > 99999999.99
        move #ER_Match to $out  9999999999.99mi
      else
        do Format-Number(#ER_Match, $out, 'B9,999,999.99mi')
      end-if
      print $out         (0,106)
      add #Tax_Cur to #Tax_Cur_Total
      add #ER_Match to #ER_Match_Total
      add #Tax_Cur to #TotalDeposit
      add #ER_Match to #TotalDeposit
    end-if

    add 1 to #TaxType_Index
  end-while

  if abs(#TotalDeposit) > 99999999.99
    move #TotalDeposit to $out  9999999999.99mi
  else
    do Format-Number(#TotalDeposit, $out, 'B9,999,999.99mi')
  end-if
  print $out           (1,121)
  if abs(#Unempl_ER_Gross) > 999999999.99
    move #Unempl_ER_Gross to $out  99999999999.99mi
  else
    do Format-Number(#Unempl_ER_Gross, $out, 'B99,999,999.99mi')
  end-if
  print $out           (1,136)
  if abs(#Unempl_ER_Tax) > 99999999.99
    move #Unempl_ER_Tax to $out  9999999999.99mi
  else
    do Format-Number(#Unempl_ER_Tax, $out, 'B9,999,999.99mi')
  end-if
  print $out           (1,151)

  if #Unempl_SP_Gross <> 0 or #Unempl_SP_Tax <> 0
    if abs(#Unempl_SP_Gross) > 999999999.99
      move #Unempl_SP_Gross to $out  99999999999.99mi
    else
      do Format-Number(#Unempl_SP_Gross, $out, 'B99,999,999.99mi')
    end-if
    print $out           (2,136)
    if abs(#Unempl_SP_Tax) > 99999999.99
      move #Unempl_SP_Tax to $out  9999999999.99mi
    else
      do Format-Number(#Unempl_SP_Tax, $out, 'B9,999,999.99mi')
    end-if
    print $out           (2,151)

    let #SpecialUIRate = #SpecialUIRate * 100
    do Format-Number(#SpecialUIRate, $out, 'B9.999')
    print $out           (2,165)
    print 'Spcl'         (,+1)
  end-if

  next-listing need=3 skiplines=1
end-procedure

begin-procedure accum-totals
  if #Jurisdiction_Index = 0 or
     #Jurisdiction_Index = 7
    array-add #Tax_Cur_Total #ER_Match_Total
        to Totals(4) total(1) total(2)
  end-if
  array-add #WithholdingTax #EIC #TotalDeposit
            #Unempl_ER_Tax #Unempl_SP_Tax #Dis_ER_Tax
            to Totals(4) total(0) total(0) total(3)
                         total(4) total(4) total(4)
end-procedure

begin-procedure initialize-jurisdiction
  move 0 to #TotalDeposit
  move 0 to #OtherTaxCount
  move 0 to #Territory_Count
  move 0 to #TaxType_Location
  move 0 to #A_Index
  move 0 to #B_Index
  move 0 to #D_Index
  move 0 to #E_Index
  move 0 to #F_Index
  move 0 to #G_Index
  move 0 to #I_Index
  move 0 to #J_Index
  move 0 to #Q_Index
  move 0 to #O_Index
  move 0 to #R_Index
  move 0 to #L_Index
  move 0 to #M_Index
  move 0 to #N_Index
  move 0 to #T_Index
  move 0 to #V_Index
  move 0 to #W_Index
  move 0 to #X_Index
  move 0 to #Y_Index
  move 0 to #Z_Index
  move 0 to #7_Index
  move 0 to #8_Index
  move 0 to #9_Index
  Clear-Array name=OtherTaxes
end-procedure


begin-procedure Print-Totals
  do State-Local-Change
  if $USFederal_data_exists = 'Y'
    do Process-Federal-Detail
    do initialize-jurisdiction
  end-if
  move 4 to #totalRow
  while #totalRow >= #changeLevel
    move #totalRow to #rollRow
    subtract 1 from #rollRow
    get $TotalName from Totals(#totalRow) TotalName
    print $TotalName (+2,21)
    move 0 to #i
    while #i <= 4
      get #total from Totals(#totalRow) total(#i)
      get #startCol from Totals(0) startCol(#i)
      if abs(#total) > 99999999.99
        move #total to $out  9999999999.99mi
      else
        do Format-Number(#total, $out, 'B9,999,999.99mi')
      end-if
      print $out (0,#startCol)
      array-add #total to Totals(#rollRow) total(#i)
      put 0 into Totals(#totalRow) total(#i)
      add 1 to #i
    end-while
    subtract 1 from #totalRow
  end-while
  move ' ' to $USFederal_data_exists
  if $End = 'Y'
    do Reset
  end-if
  new-page
end-procedure

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'getlcdta.sqc'  !Get-Local-Tax-Data procedure
#Include 'payinit.sqc'   !Report Initialization and Timing
#Include 'reset1.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
