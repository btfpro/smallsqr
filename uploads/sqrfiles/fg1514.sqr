!***********************************************************************
!  FG1514:  Print the Military Service Deposit Report                  *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:11:44:11                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   ! Set Environment Procedure
! #include 'setup32.sqc'  ! Landscape printer/page initialization

#define PageWidth  110
#define PageLength 60

begin-setup

#include 'setupdb.sqc'

declare Printer           !Declare printer characteristics.
    type=HPLASERJET       !Types are POSTSCRIPT, HPLASERJET.
    orientation=Landscape !Orientations are PORTRAIT or LANDSCAPE.
    top-margin  = .25
    left-margin = .25
    font        =  5
    point-size  =  12
    char-size   =  7.2
    line-size   =  12

page-size {PageLength} {PageWidth}
end-setup

begin-Report
  DO Init-DateTime
  DO Init-Report
  DO Process-Main
  DO Delete-Run-Cntl
  DO Stdapi-Term
end-Report

!-------------------------------------------
!          *** INIT-REPORT ***
!-------------------------------------------

begin-procedure Init-Report

  move 'FG1514' to $ReportID
  move 'Military Service Deposit Reports' to $ReportTitle
  display $ReportTitle

  Do Stdapi-Init
  Let $first_page      = 'Y'
  Let #Max_Rows        = 11
  Let #Current_Row     = 1
  Let #Start_line      = 23

end-procedure

!-----------------------------------------------
!            *** PROCESS MAIN ***
!-----------------------------------------------

Begin-procedure Process-Main


  if $prcs_process_instance = ''
        input $EMPLID MAXLEN=11 'Enter Employee ID' TYPE=CHAR
        move 'Y' to $DateOK
        input $REQDT 'Enter Date For Request ({Prompt-Date})'
        if $REQDT = ''
           do Get-Current-DateTime
           move $AsOfToday to $REQDT
        else
           move {PDelimiter} to $DDelimiter
           do Format-DateTime($REQDT,$REQUESTDT,{Prompt-Mask},'','native')
        end-if

        do Validate-Native-Date($REQUESTDT,$DateOK)

        if $DateOK = 'N'
           display '**** This is an invalid date, please reenter ****'
        end-if
  else
    Do 1514-Run-Control-Data
  end-if
end-procedure


!-------------------------------------------------
!        *** 1514-RUN-CONTROL ***
!-------------------------------------------------

Begin-procedure 1514-Run-Control-Data

display 'Begin Report'

Begin-select

C.OPRID
C.RUN_CNTL_ID
C.GVT_STATUS_ACCT
C.GVT_PRTALL_1514

  Let $STATUS_ACCT = rtrim(&C.GVT_STATUS_ACCT,' ')
  Let $PRTALL_1514 = &C.GVT_PRTALL_1514
  Let $prcs_oprid  = &C.OPRID
  Let $prcs_run_cntl_id = &C.RUN_CNTL_ID

  If $PRTALL_1514 = 'N'
     Let $svcdep_stat = 'SD.GVT_STATUS_ACCT = ''' || $STATUS_ACCT || ''''
     Do Multiple-Run-Control-Data
  Else
     Let $svcdep_where = 'SD.GVT_STATUS_ACCT = ''' || $STATUS_ACCT || ''''
     Do Get-1514-Data
     display $svcdep_where
  End-if

FROM PS_GVT_RUN_CNTL C
WHERE C.OPRID = $prcs_oprid
AND   C.RUN_CNTL_ID = $prcs_run_cntl_id

End-select

End-procedure

!-----------------------------------------------
!      *** MULTIPLE-RUN-CONTROL-DATA ***
!-----------------------------------------------

Begin-procedure Multiple-Run-Control-Data

Begin-select

R.EMPLID

   Let $EMPLID = rtrim(&R.EMPLID,' ')
   Let $svcdep_where =  'SD.EMPLID = ''' || $EMPLID || ''''
   Do Get-1514-Data

FROM PS_GVT_RUNCTL_1514 R
WHERE   R.OPRID = $prcs_oprid
AND     R.RUN_CNTL_ID = $prcs_run_cntl_id

End-select

End-procedure

!-------------------------------------------
!            *** PRINT REPORT ***
!-------------------------------------------

Begin-procedure Print-Report

display 'Printing Report'

Do Get-Header-Data
Do Print-1514-Form
Do Print-1514-Header

End-procedure


!--------------------------------------------------------
!           *** PRINT WORKSHEET HEADER ***
!--------------------------------------------------------

Begin-procedure Print-1514-Header
MOVE &PN.NAME to $Name
DO Rotate-Name
LET $TMPNID = edit(RTRIM(&NID.NATIONAL_ID, ' '), 'xxx-xx-xxxx')
LET #AMT_DEP_DUE = (&SD.AMT_OF_EARNS * (&SD.WITHHOLD_PCT)/100)

DO Format-DateTime (&PD.BIRTHDATE,$BDATE, {DEFMDY}, '','')
DO Format-DateTime (&SD.FROM_DATE,$FDATE, {DEFMDY}, '','')
DO Format-DateTime (&SD.THRU_DATE,$TDATE, {DEFMDY}, '','')
DO Format-DateTime (&SD.STATUS_DT,$STDATE, {DEFMDY}, '','')
DO Format-DateTime (&SD.GVT_MILDEP_IAD,$IADATE, {DEFMDY}, '','')
DO Format-Number(&SD.AMT_OF_EARNS,$AMT_EARNS, '$999,999.99')

DO Convert-To-DTU-Date(&SD.FROM_DATE,$Date1)
DO Convert-To-DTU-Date(&SD.THRU_DATE,$Date2)
DO DateDiffYMD ($Date1,$Date2,#Years,#Months,#Days)
Show '$Date1: ' $Date1 ' $Date2: ' $Date2  ' #Years: ' #Years ' #Months: ' #Months ' #Days: ' #Days

MOVE '99' to $mask
MOVE #Years to $Years :$mask
MOVE #Months to $Months :$mask
MOVE #Days to $Days :$mask

LET $TOTDAYS = $Years ||'/'||$Months ||'/'||$Days

GRAPHIC () font 5 10
PRINT $Name                                (9,7)
PRINT $BDATE                               (9,38)
PRINT $TMPNID                              (9,58)
PRINT $STDATE                              (9,82)
PRINT $FDATE                               (13,8)
PRINT $TDATE                               (13,24)
PRINT $TOTDAYS                             (13,38)
PRINT &SD.AMT_OF_EARNS                     (13,56) edit 999,999.99
PRINT &SD.WITHHOLD_PCT                     (13,72)
PRINT #AMT_DEP_DUE                         (13,85)
PRINT &J.COMPANY                           (16,8)
PRINT $IADATE                              (16,60)
PRINT $GVT_RETIRE_PLAN                     (16,82)

end-procedure

!----------------------------------------------------------
!         *** DELETE RUN-CNTL ***
!----------------------------------------------------------

Begin-procedure Delete-Run-Cntl

Begin-sql

     DELETE FROM PS_GVT_RUN_CNTL
     WHERE OPRID = $prcs_oprid
     AND RUN_CNTL_ID = $prcs_run_cntl_id

End-sql

Begin-sql

     DELETE FROM PS_GVT_RUNCTL_1514
     WHERE OPRID = $prcs_oprid
     AND RUN_CNTL_ID = $prcs_run_cntl_id

End-sql

End-procedure


!-----------------------------------------------------------
!             *** INCLUDE STATEMENTS ***
!-----------------------------------------------------------
#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines
#Include 'reset.sqc'     !Reset printer procedure
#include 'fg1514d.sqc'   !OPM 1514 Data
#include 'fg1514f.sqc'   !OPM 1514 Form
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines to format datetimes
#Include 'datemath.sqc'  !Routine to format date to YYYY-MM-DD.
#Include 'number.sqc'    !Routine to format numbers
#include 'validdt.sqc'   !Validate date routine
#Include 'stdapi.sqc'    !Routines to Update Run Status
#include 'rotname1.sqc'  !Converts name to different format
!#include 'datediffenh.sqc'   !

!-----------------------------------------------------------
!              >>> END OF FILE <<<
!-----------------------------------------------------------
