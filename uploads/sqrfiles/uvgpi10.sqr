!***********************************************************************
!  UVGPI10: List of Post Process Formula that uses the  *
!            System Element SET CURR AMT VAL.                          *
!                                                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/08/17:13:22:42                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    ! Set environment
#Include 'setup32.sqc'   ! Printer and page-size initialization

!#Include 'setup01.sqc'   ! Printer and page-size initialization


!***********************************************************************
begin-program
!***********************************************************************

        do Init-Report
        do Process-Main
        do Reset
        do Stdapi-Term
end-program


!***********************************************************************
begin-heading 6
!***********************************************************************

  #include 'stdhdg01.sqc'

  print 'COUNTRY'                       (+2,1,8)   Underline
  print 'PIN OWNER'                     (0,+2,33)  Underline
  print 'POST FORMULA'                  (0,+2,25)  Underline
  print 'DESCR'                         (0,+2,34)  Underline
  print 'PIN TYPE'                      (0,+2,10)  Underline
  print 'EARNING/DEDUCTION'             (0,+2,32)  Underline
  print 'CALC RULE'                     (0,+2,10)  Underline

end-heading


!***********************************************************************
!  Function:     Init Report
!  Description:  Prompt for run-time parameters
!***********************************************************************

begin-procedure Init-Report

  do Init-DateTime
  do Get-Current-DateTime

  let $ReportID    = 'UVGPV10'
  let $ReportTitle = 'Post Process Formula that uses "SET CURR AMT VAL"'
  show ' '
  show $ReportID  $ReportTitle
  show ' '

  do Stdapi-Init

end-procedure Init-Report



!***********************************************************************
!  Function:     Process-Main
!  Description:  This is the main report procedure.  Calls select
!                routines for data in the PS_JOBCODE_TBL.
!***********************************************************************

begin-procedure Process-Main

let #COUNTER = 0

begin-SELECT DISTINCT 
C2.COUNTRY, 
D.XLATLONGNAME,
A.PIN_NM,
A.DESCR,
C2.PIN_TYPE,
C2.PIN_NM, 
C.CALC_RULE

        let #COUNTER = #COUNTER + 1

        print &C2.COUNTRY        (+1,1)
        print &D.XLATLONGNAME    (0,11)
        print &A.PIN_NM          (0,46)
        print &A.DESCR           (0,73)
        print &C2.PIN_TYPE       (0,112)
        print &C2.PIN_NM         (0,121)
        print &C.CALC_RULE       (0,158)

FROM PS_GP_PIN A,
     PS_GP_PIN A1,
     PS_GP_FORMULA_DTL B,
     PS_GP_ERN_DED C,
     PS_GP_PIN C2,
     PSXLATITEM D

WHERE A.PIN_NUM = B.PIN_NUM
  AND A1.PIN_CODE = 'SET CURR AMT VAL ALL' 
  AND A1.PIN_NUM = B.PIN_FRML_FLD3_NUM
  AND B.PIN_NUM = C.PIN_POST_FRML_NUM
  AND C.PIN_NUM = C2.PIN_NUM
  AND C2.COUNTRY = 'AUS'                      ! -- remove this -- 
  AND D.FIELDVALUE = A.PIN_OWNER
  AND D.FIELDNAME = 'PIN_OWNER'
  AND NOT EXISTS (SELECT 'X' 
                  FROM PS_GP_FORMULA_DTL D, 
                       PS_GP_PIN D1
                  WHERE B.PIN_NUM = D.PIN_NUM 
                    AND D1.PIN_CODE = 'OVRD CURR AMT VAL ALL'
                    AND D.PIN_FRML_FLD3_NUM = D1.PIN_NUM)

ORDER BY C2.COUNTRY, D.XLATLONGNAME, A.PIN_NM, C2.PIN_TYPE, C2.PIN_NM

end-SELECT

print 'Number of rows selected:  '         (+3,1,26)
print #COUNTER                             (,+1,)

end-procedure Process-Main



!***********************************************************************
! SQC Files for called procedures
!***********************************************************************
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Routines to update run status
#Include 'reset.sqc'     !Reset printer procedure

