!***********************************************************************
!  TAX703: Print contents of Local Tax Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2006/07/20:06:25:26                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!                                                                      *
!  $Date    :: 9/23/99 2:48p    $                                      *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Get-Current-DateTime
  display 'Report Ended: ' noline
  display $AsOfNow
  do Stdapi-Term
end-report

!***********************************************************************
!   PROCEDURE:  Init-Report                                            *
!***********************************************************************
begin-procedure Init-Report
  move 'TAX703' to $ReportID
  move 'Local Tax Table' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  display 'Report Began: ' noline
  display $AsOfNow
  do Stdapi-Init
end-procedure

!***********************************************************************
!   HEADING 8                                                          *
!***********************************************************************
begin-HEADING 8
  #Include 'stdhdg01.sqc'

  print 'St-'                           (+2,1,3)
  print 'Locality'                      (0,+1,10)
  print 'Eff'                           (0,+1)
  print 'Locality Name'                 (0,27,20)
  print 'Loca. Abbr'                    (0,+1,10)
  print 'County'                        (0,+1,21)

  print 'Maint'                         (0,+1,5)
  print 'Tax'                           (0,0,5)
  print 'Rpt'                           (0,0,3)
  print 'Dtl'                           (0,0,4)
  print 'Coll.'                         (0,0,4)
  print '   Resident'                   (0,+1,11)
  print 'NonResident'                   (0,+1,11)
  print ' '                             (0,+1,10)
  print 'Minimum'                       (0,+1,7)
  print 'Maximum'                       (0,+2,8)
  print 'Exemptn'                       (0,+1,7)

  print 'ate'                           (+1,1,3)
  print ' '                             (0,+1,10)
  print 'Date'                          (0,+1)
  print ' '                             (0,27,20)
  print ' '                             (0,+1,10)
  print ' '                             (0,+1,21)
  print 'Resp'                          (0,+1,5)
  print 'Class'                         (0,0,5)
  print 'Int'                           (0,0,3)
  print 'Req?'                          (0,0,4)
  print 'Code'                          (0,0,4)
  print '   Tax Rate'                   (0,+1,11)
  print '   Tax Rate'                   (0,+1,11)
  print 'Low Gross'                     (0,+2,10)
  print '  Tax  '                       (0,0,7)
  print '   Tax '                       (0,+1,8)
  print ' Amount'                       (0,+2,7)

  print 'Oth. Loca.'                    (+1,5,10)
  print 'Other Locality Name          ' (0,27,20)
  print 'Other Abbr'                    (0,+1,10)
  print 'Tax Type'                      (0,81,10)
  print 'P. Ind'                        (0,98,6)
  print 'Tbl Code'                      (0,+2,8)

  print ' '                             (+1,1)
end-HEADING


!***********************************************************************
!  PROCEDURE:   Process-Main (Main Procedure)                          *
!***********************************************************************
BEGIN-PROCEDURE Process-Main

BEGIN-SELECT
A.STATE                       (+1,1,3)  on-break print=change/top-page -
                                      PROCEDURE=STATE-CHANGE
A.LOCALITY                    (0,+1,10)  on-break

A.EFFDT  &A.EffDt
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                  (0,+1)

A.EFF_STATUS

  if &A.Eff_Status = 'I'
     print 'I'                (0,0,1)
  else
     print ' '                (0,0,1)
  end-if

A.LOCALITY_NAME               (0,27,20)
A.COUNTY                      (0,59,21)      ! should be changed to 30 chars, if needed
A.MAINT_RESPONSBLTY

 if &A.Maint_Responsblty = 'P'
    print 'PSft'              (0,+1,4)
 else
    print 'Cust'              (0,+1,4)
 end-if

A.TAX_CLASS

 evaluate &A.Tax_Class
   when = 'B'
     print 'EE/ER'            (0,+1,5)
   when = 'H'
     print 'W/H'              (0,+1,5)
   when = 'P'
     print 'OP'               (0,+1,5)
   when = 'R'
     print 'ER'               (0,+1,5)
 end-evaluate

   do Local_Tax_Tbl2

A.RESIDENT_TAX_RT
  do Format-Number(&A.Resident_Tax_Rt, $out, 'b9999.99999')
  print $out                  (0,103,11)
A.NONRESIDENT_TAX_RT
  do Format-Number(&A.NonResident_Tax_Rt, $out, 'b9999.99999')
  print $out                  (0,+1,11)
A.LOW_GROSS
  do Format-Number(&A.Low_Gross, $out, 'b999999.99')
  print $out                  (0,+1,10)
A.MINIMUM_TAX
  do Format-Number(&A.Minimum_Tax, $out, 'b999.99')
  print $out                  (0,+1,7)
A.MAXIMUM_TAX
  do Format-Number(&A.Maximum_Tax, $out, 'b9999.99')
  print $out                  (0,+1,8)
A.EXEMPTION_AMT
  do Format-Number(&A.Exemption_Amt, $out, 'b999999')
  print $out                  (0,+1,7)

A.OTH_LOCALITY                (+1,5,10)
A.OTH_LOCALITY_NAME           (0,27,20)
A.OTH_LOCALITY_ABBRV          (0,+1,10)
A.LOC_TAX_TYPE

  move 'LOC_TAX_TYPE' to $FieldName
  move &A.Loc_Tax_Type to $FieldValue
  do Read-Translate-Table
  print $XlatShortName        (0,81,10)

A.PARTIAL_IND                 (0,100,4)
A.GRADUATED_TBL_CD            (0,+5,4)

FROM PS_LOCAL_TAX_TBL A
ORDER BY A.STATE, A.LOCALITY_NAME, A.COUNTY, A.TAX_CLASS, A.EFFDT DESC

END-SELECT

END-PROCEDURE


!***********************************************************************
!   PROCEDURE:  State-Change                                           *
!***********************************************************************
BEGIN-PROCEDURE State-Change

  move 2 to #changeLevel

END-PROCEDURE


!***********************************************************************
!   PROCEDURE:  Local_Tax_Tbl2                                         *
!***********************************************************************
BEGIN-PROCEDURE Local_Tax_Tbl2

BEGIN-SELECT

B.LOCALITY_ABBRV              (0,48,10)
B.TAX_RPT_INTERVAL            (0,92,3)
B.EE_DTL_REQUIRED             (0,0,3)
C.VENDOR_ID                   (0,0,6)

FROM  PS_LOCAL_TAX_TBL2 B, PS_LOCL_TX_TYP_TBL C, PS_LOCL_TX_CLS_TBL D
WHERE B.STATE    = &A.State
AND   B.LOCALITY = &A.Locality
AND   B.EFFDT =
        (SELECT MAX(EFFDT)
           FROM PS_LOCAL_TAX_TBL2 BB
         WHERE  BB.STATE    = B.STATE
           AND  BB.LOCALITY = B.LOCALITY
           AND  BB.EFFDT  <= &A.EffDt)
AND   B.STATE    = C.STATE
AND   B.LOCALITY = C.LOCALITY
AND   C.EFFDT    = (SELECT MAX(C1.EFFDT)
                    FROM   PS_LOCL_TX_TYP_TBL C1
                    WHERE  C1.STATE = C.STATE
                    AND    C1.LOCALITY = C.LOCALITY
                    AND    C1.EFFDT <= &A.Effdt)
AND C.STATE = D.STATE
AND C.LOCALITY = D.LOCALITY
AND C.EFFDT = D.EFFDT
AND C.TAX_GRP_TYPE = D.TAX_GRP_TYPE
AND D.TAX_CLASS = &A.Tax_Class

END-SELECT

END-PROCEDURE

!***********************************************************************
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
