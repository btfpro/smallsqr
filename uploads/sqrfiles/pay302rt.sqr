!**********************************************************************
!  PAY302RT:  Retro Pay Summary Report                                *
!**********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:50:15                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!  History: 05/22/01 RLW -- Add Retropay Interest Column               *
!***********************************************************************

#include 'setenv.sqc' !Set environment

begin-report

   do Init-DateTime
   do Init-Number
   do Get-Current-Datetime
   do Init-Report
   do Process-Main
   do Reset
   do Stdapi-Term

end-report

!***********************************************************************



!******************** H E A D I N G      S E C T I O N  *****************

#include 'setup32.sqc'  !Printer and page-size initialization

begin-heading 10

#include 'stdhdg01.sqc'

    print $Pay302rt_Rqst_Type       (+4,1)
    print $Retropay-Rqst-Type       (0,15)
    print $Rqst-Descr               (0,17)
    print $Pay302rt_Mass_ID         (0,28)
    print $Mass-Retro-Rqst-ID       (0,40)
    print $Pay302rt_Old_Ern_Amt      (+3,27)
    print $Pay302rt_New_Ern_Amt      (0,50)
    print $Pay302rt_Retropay         (0,73)
    print $Pay302rt_Retropay_Int     (0,96)

end-heading

!******************  I N I T I A L I Z A T I O N   *********************

begin-procedure Init-Report

    move 'PAY302RT' to $ReportID

    do Stdapi-Init
    do Get-Report-Language
    move $Pay302rt_ReportTitle to $ReportTitle
    display $ReportTitle

    do Select-Parameters
    do Array-Initialization

end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY302RT program ids
  do Init_Report_Translation($ReportID,$language_cd)

  do Get_Field_Information ('PAY302RT', 'REPORTTITLE',  $Pay302rt_ReportTitle, #dummy_width)
  do Get_Field_Information ('PAY302RT', 'RQST_TYPE',    $Pay302rt_Rqst_Type,   #dummy_width)
  do Get_Field_Information ('PAY302RT', 'MASS_ID',      $Pay302rt_Mass_ID,     #dummy_width)
  do Get_Field_Information ('PAY302RT', 'OLD_ERN_AMT',  $Pay302rt_Old_Ern_Amt, #dummy_width)
  do Get_Field_Information ('PAY302RT', 'NEW_ERN_AMT',  $Pay302rt_New_Ern_Amt, #dummy_width)
  do Get_Field_Information ('PAY302RT', 'RETROPAY',     $Pay302rt_Retropay,    #dummy_width)
  do Get_Field_Information ('PAY302RT', 'GRAND_TOT',    $Pay302rt_Grand_Tot,     #dummy_width)
  do Get_Field_Information ('PAY302RT', 'RQST_TOT',     $Pay302rt_Rqst_Tot,    #dummy_width)
  do Get_Field_Information ('PAY302RT', 'RETROPAY_INT', $Pay302rt_Retropay_Int, #dummy_width)

end-procedure

!**************************************************************
!                                                             *
!  Function:     Array-Initialization                         *
!                                                             *
!  Description:  Loads Amounts into the array to accumulate   *
!                  totals.                                    *
!                                                             *
!**************************************************************

begin-procedure Array-Initialization

    CREATE-ARRAY NAME=Totals                -
                 SIZE=3                     -
                 FIELD=TotalName:char       -
                 FIELD=Total:number:4       -
                 FIELD=startCol:number:4

    put $Pay302rt_Grand_Tot          into Totals(1) TotalName
    put $Pay302rt_Rqst_Tot           into Totals(2) TotalName

    put  28  into Totals(0) startCol(0)
    put  51  into Totals(0) startCol(1)
    put  73  into Totals(0) startCol(2)
    put  99  into Totals(0) startCol(3)

 end-procedure

!**************************  M A I N   P R O C E S S   ******************

begin-procedure Process-Main

begin-SELECT

A.RETROPAY_RQST_TYPE  () on-break print=never level=2 procedure=Rqst-Type-Change

    if &A.RETROPAY_RQST_TYPE = 'J'
        move 'J' to $Retropay-Rqst-Type
        move '- Job' to $Rqst-Descr
    else
        move 'A' to $Retropay-Rqst-Type
        move '- Add' to $Rqst-Descr
    end-if

A.RETROPAY_PRCS_FLAG
A.EMPLID
A.EMPL_RCD
A.RETROPAY_EFFDT
A.MASS_RETRO_RQST_ID

    if &A.RETROPAY_RQST_TYPE = 'M'
        move 'M' to $Retropay-Rqst-Type
        move $Mass_ID to $Mass-Retro-Rqst-ID
        move '- Mass' to $Rqst-Descr
    end-if

    do Get-RetroPay-Earns

FROM PS_RETROPAY_RQST A

WHERE (A.RETROPAY_PRCS_FLAG = 'C') AND
      (A.RETROPAY_RQST_TYPE = $Rqst-TypeA  OR
       A.RETROPAY_RQST_TYPE = $Rqst-TypeJ  OR
      (A.RETROPAY_RQST_TYPE = $Rqst-TypeM AND
       A.MASS_RETRO_RQST_ID = $Mass_ID))

ORDER BY A.RETROPAY_RQST_TYPE,
         A.EMPLID,
         A.EMPL_RCD,
         A.RETROPAY_EFFDT

end-SELECT

move 1 to #changeLevel
move 'Y' to $End
do Print-Totals

end-procedure

!**********************  R E T R O P A Y    E A R N S   ******************

begin-procedure Get-RetroPay-Earns

begin-SELECT

B.EMPLID
B.EMPL_RCD
B.RETROPAY_EFFDT
B.EARNS_AMT
B.NEW_EARNS_AMT
B.EARNS_AMT_OVRD
B.AMOUNT_DIFF
B.RETROPAY_ERNCD

    do Store-Totals

FROM PS_RETROPAY_EARNS B

WHERE B.EMPLID           = &A.EMPLID AND
      B.EMPL_RCD        = &A.EMPL_RCD AND
      B.RETROPAY_EFFDT   = &A.RETROPAY_EFFDT AND
      B.RETROPAY_LOAD_SW = 'Y'

end-SELECT

end-procedure


!**************************  S T O R E  T O T A L S    ******************

begin-procedure Store-Totals

begin-SELECT DISTINCT

C.GVT_ERNCD_INTEREST

    move &C.GVT_ERNCD_INTEREST to $INT_ERNCD

FROM PS_PAYGROUP_TBL C

WHERE C.GVT_ERNCD_INTEREST= &B.RETROPAY_ERNCD

end-SELECT

    move &B.EARNS_AMT      to #Earns
    move &B.NEW_EARNS_AMT  to #Newerns
    move &B.AMOUNT_DIFF    to #Diff

    if &B.EARNS_AMT_OVRD = 0
        move &B.AMOUNT_DIFF to #Diff
    else
        move &B.EARNS_AMT_OVRD to #Diff
    end-if

    array-add #Earns   to Totals(2) Total(0)
    array-add #Newerns to Totals(2) Total(1)

    if $INT_ERNCD > ' '
       array-add #Diff    to Totals(2) Total(3)
    else
       array-add #Diff    to Totals(2) Total(2)
    end-if

end-procedure

!**************************************************************************

begin-procedure Rqst-Type-Change

    move 2 to #changeLevel
    do Print-Totals

end-procedure

!*****************    P R I N T    T O T A L S  ***************************

begin-procedure Print-Totals

    move 2 to #totalRow

    while #totalRow >= #changeLevel

        move #totalRow to #rollRow
        subtract 1 from #rollRow
        get $TotalName from Totals(#totalRow) TotalName
        print $TotalName (+5,2)
        move 0 to #i

        while #i <= 3
            get #total from Totals(#totalRow) total(#i)
            get #startCol from Totals(0) startCol(#i)
            do Format-Number(#total, $out, '999,999,999.99mi')
            print $out (0,#startCol)
            array-add #total to Totals(#rollRow) total(#i)
            put 0 into Totals(#totalRow) total(#i)
            add 1 to #i
        end-while

        subtract 1 from #totalRow

    end-while

    if #changeLevel = 2     !skip to new page on change in Request Type
           new-page
    end-if

    if $End = 'Y'
        do Reset
    end-if

end-procedure

!**************************************************************************
#Include 'pay302rt.sqc'  !Custom Run Control Parameters
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
