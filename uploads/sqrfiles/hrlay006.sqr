!***********************************************************************
!  HRLAY006:  LayoffReport(Post Processing)                            *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      
!          $Date:  2006/07/19:13:31:52                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!***********************************************************************
! Description:
!***********************************************************************

#include 'setenv.sqc'   !Set environment


!***********************************************************************
!-----------------Seup Section-----------------------------------------*
!Description: This section sets the Page Size and Font Size            *
!***********************************************************************


Begin-Setup
#include 'setupdb.sqc'

          page-size  70 225
          DECLARE PRINTER
          type          = HPLASERJET
          orientation   = LANDSCAPE
          lines-inch    = 8
          chars-inch    = 18.6
          top-margin    = 0.30
          left-margin   = 0.20
          font          = 5
          point-size    = 7
          lines-inch    = 8
 
End-Setup


!***********************************************************************
!-----------------Program Section--------------------------------------*
!Description: 
!***********************************************************************

BEGIN-PROGRAM

 DO STDAPI-INIT
 do Init-DateTime
  do Get-Current-DateTime
     
   DO Get-RP-Lbl
   do Find-Date-Format
        evaluate $DT_FMT
          when = 'M'
               do Format-DateTime($AsOfToday,$currentdate,{DEFMDY},'','')
               break
          when = 'D'
               do Format-DateTime($AsOfToday,$$currentdate,{DEFDMY},'','')
               break
          when-Other
               do Format-DateTime($AsOfToday,$currentdate,{DEFYMD},'','')
               break
          end-evaluate
  
  Do Proinst
  Do Rost_distinct 
  DO Report
  Do TEMPTABLE-DETAILS
  IF rtrim($AUTOFLAG,' ') = 'Y'
  Do Tempdelete
  End-if
  
 DO STDAPI-TERM
 
END-PROGRAM




!***********************************************************************
!-----------------Heading Section--------------------------------------*
!Description: This section Prints the Header of this Report            *
!***********************************************************************

BEGIN-HEADING 6
print $Reportname  (1,1)
let $Name = $HR_LAY_LST
print $Name              (1,15)
print $Peoplesoft       (1,110)
Print $Pageno (1,195) 
PAGE-NUMBER (1,205)' '             
let $ReportTitle = $HR_LAY_RPT
print $ReportTitle       (2,101) 
print $Rundate         (2,195) 
print $currentdate ()  !print $current-date () edit 'dd/mm/yyyy'
print $Runtime         (3,195)
print $current-date () edit 'HH:MI:SS'
print $PROCESS_INSTANCE (4,1)
print #proc_inst () edit 9999999999

Print $ROSTER_ID (+2,1)
print &LBR_ROSTER_ID (,25)
print $ROSTERDESC (+1,1)
print $ROST_DESCR (,25)
END-HEADING



!***********************************************************************
!-----------------Find-Date-Format-------------------------------------*
!Description: 
!***********************************************************************
 begin-procedure Find-Date-Format

let $REC_FOUND = 'N'
begin-SELECT
A.USER_OPTION_VALUE
  let $REC_FOUND = 'Y'
FROM PSUSERPRSNLOPTN A

WHERE A.OPRID = $prcs_oprid
  AND A.USEROPTN = 'DFRMT'
end-SELECT

IF $REC_FOUND = 'N'
  let $DT_FMT = 'M'
ELSE
  LET $DT_FMT = &A.USER_OPTION_VALUE
  LET $DT_FMT = RTRIM($DT_FMT,' ')
END-If

end-procedure !Find-Date-Format


!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the Process Instance                *
!***********************************************************************
Begin-procedure Proinst
begin-select
PROCESS_INSTANCE
LBR_PROP_TERM_DT
ACTION
ACTION_REASON
 LET #proc_inst = &PROCESS_INSTANCE
 let $LBR_PROP_TERM_DT = &LBR_PROP_TERM_DT
 let $RUNACTION = &ACTION
 let $RUNACTION_REASON = &ACTION_REASON
FROM PS_HR_RUN_CNTL_LAY
WHERE OPRID = $prcs_oprid
AND RUN_CNTL_ID = $prcs_run_cntl_id
end-select


begin-select
LY2.LBR_AUTO_REIN_FLG
LY2.ACTION
LY2.ACTION_REASON

FROM PS_HR_RUN_CNTL_LY2 LY2
WHERE LY2.OPRID = $prcs_oprid
AND LY2.RUN_CNTL_ID = $prcs_run_cntl_id
end-select

IF &LY2.LBR_AUTO_REIN_FLG = 'Y'
 let $RUNACTION = &LY2.ACTION
 let $RUNACTION_REASON = &LY2.ACTION_REASON
END-IF
End-procedure



!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the Roster Status Description       *
!***********************************************************************

BEGIN-PROCEDURE EMPLNAME

BEGIN-SELECT
   
NAME
FROM PS_NAMES_VW
   WHERE EMPLID = &A.EMPLID
   And NAME_TYPE = 'PRI'
   
END-SELECT
   Let $Name = &NAME
   print $Name ( ,14) 
END-PROCEDURE


!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the Action Reason Description       *
!***********************************************************************

Begin-procedure Actionreason-Desc 
!-----SELECT which fetches the Action Reason description for a Action-Action Reason--------!
!-------Description is 1st fetched from related language table. If the Login language = Base Language then
!-------the description would not be present in the Related Lang table, in which case it should be 
!-------fetched from the Base table
#define ACTN_REASON_VW
Begin-select
D.DESCR
       Let $Desc = &D.DESCR
FROM PS_ACTN_REASON_VW D
WHERE ACTION = &A.ACTION
AND ACTION_REASON = &A.ACTION_REASON
AND D.EFF_STATUS = 'A'
End-select

Do Get_Related_ACTN_REASON_VW(&A.ACTION,&A.ACTION_REASON)

if $ACTN_REASON_VW-DESCR <> ''
 let $Desc = $ACTN_REASON_VW-DESCR
end-if

End-procedure



!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the Jobcode Description             *
!***********************************************************************
Begin-procedure JOBCODE
LET #JOBDATA = 0
BEGIN-SELECT
X.JOBCODE (,76)
 !This is to check if there is Layoffed Job data for the emplid , emplrcd.
 LET #JOBDATA = 1
FROM PS_JOB X
WHERE EMPLID = &A.EMPLID
AND EMPL_RCD = &A.EMPL_RCD
AND EFFDT = (SELECT MAX(EFFDT)
             FROM PS_JOB
             WHERE EMPLID = &A.EMPLID
             AND EMPL_RCD = &A.EMPL_RCD)
 AND EFFSEQ = (SELECT MAX(EFFSEQ)
              FROM PS_JOB
             WHERE EMPLID = &A.EMPLID
             AND EMPL_RCD = &A.EMPL_RCD
             AND EFFDT = X.EFFDT)
 AND ACTION = $RUNACTION
 AND ACTION_REASON = $RUNACTION_REASON
END-SELECT

End-Procedure



!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the column heading                  *
!***********************************************************************

Begin-procedure Report
print $EmployeeID (+2,1)
print $EMPLOYEENAME (,14)
print $lbl_Empl ( ,70)
print $Jobcode_Lbl ( ,76)
print $lbl_EffDate ( ,88)
print $ACTION ( ,100)
print $ACTION ( ,107)
print $AUTO_REINST ( ,149)
print $REINST ( ,163)
print $REINST_ACTION ( ,170)
print $REINSTDT ( ,208)
print $lbl_Emplrec (+1,70)
print $REASON ( ,107)
print $FLAG ( ,149)
print $ACTION ( ,163)
print $REASON ( ,170)

End-Procedure

!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the Column Data                     *
!***********************************************************************
Begin-procedure Rost_distinct

Begin-select distinct
LBR_ROSTER_ID !(,25) on-break level=1 PRINT=CHANGE/TOP-PAGE BEFORE=ROSTERTITLE
FROM PS_HR_ROSTEXEC_TMP 
   where PROCESS_INSTANCE = #proc_inst
End-select  

!-----SELECT which fetches the Action Reason description for a Action-Action Reason--------!
!-------Description is 1st fetched from related language table. If the Login language = Base Language then
!-------the description would not be present in the Related Lang table, in which case it should be 
!-------fetched from the Base table
BEGIN-SELECT
ROSTL.DESCR50
 LET $ROST_DESCR = &ROSTL.DESCR50
FROM PS_HR_LBR_ROST_LNG ROSTL
WHERE LBR_ROSTER_ID = &LBR_ROSTER_ID
AND LANGUAGE_CD = $curr_language_cd
AND EFFDT = (SELECT MAX(EFFDT)
             FROM PS_HR_LBR_ROST_LNG
             WHERE LBR_ROSTER_ID = &LBR_ROSTER_ID
             AND LANGUAGE_CD = $curr_language_cd
             AND EFFDT <= $AsOfToday)
END-SELECT

IF $ROST_DESCR =''
BEGIN-SELECT
ROST.DESCR50
  LET $ROST_DESCR = &ROST.DESCR50
FROM PS_HR_LBR_ROSTDATA ROST
WHERE LBR_ROSTER_ID = &LBR_ROSTER_ID
AND EFFDT = (SELECT MAX(EFFDT)
             FROM PS_HR_LBR_ROSTDATA
             WHERE LBR_ROSTER_ID = &LBR_ROSTER_ID
             AND EFFDT <= $AsOfToday)
END-SELECT
END-IF

End-procedure




!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the Reinst Action Reason Description*
!***********************************************************************
Begin-procedure ReinstActDes  

!-----SELECT which fetches the Action Reason description for a Action-Action Reason--------!
!-------Description is 1st fetched from related language table. If the Login language = Base Language then
!-------the description would not be present in the Related Lang table, in which case it should be 
!-------fetched from the Base table
#define ACTN_REASON_VW
Begin-select
H.DESCR 
  Let $Reinst_Actde = &H.DESCR
FROM PS_ACTN_REASON_VW H
WHERE H.ACTION = &A.LBR_REINST_ACTION
AND H.ACTION_REASON = &A.LBR_REINST_ACT_RSN
AND H.EFF_STATUS = 'A'
End-select

Do Get_Related_ACTN_REASON_VW(&A.LBR_REINST_ACTION,&A.LBR_REINST_ACT_RSN)

if $ACTN_REASON_VW-DESCR <> ''
 let $Reinst_Actde = $ACTN_REASON_VW-DESCR
end-if

End-procedure

!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section selects the Column Data                     *
!***********************************************************************
BEGIN-PROCEDURE TEMPTABLE-DETAILS
         
BEGIN-SELECT
A.LBR_ROSTER_ID
A.EMPLID (+2,1) 
 DO EMPLNAME 
A.EMPL_RCD ( ,70)
  Do JOBCODE
A.TERMINATION_DT 
 
 !Check if Termination Date in the Run control page is not null i.e Multiple Term flag is unchecked
 if rtrim($LBR_PROP_TERM_DT, ' ') <> ''
  let $termination_date = $LBR_PROP_TERM_DT
 else  !i.e Multiple Term flag is checked
  let $termination_date = &A.TERMINATION_DT
 end-if
 
     evaluate $DT_FMT
       when = 'M'
     do Format-DateTime($termination_date,$TERMIN,{DEFMDY},'','')
       break
       when = 'D'
     do Format-DateTime($termination_date,$TERMIN,{DEFDMY},'','')
       break
       when-Other
     do Format-DateTime($termination_date,$TERMIN,{DEFYMD},'','')
       break
      end-evaluate
      
      !Only when Jobdata is present for the emplid,job information like termination date, Job code
      !Action, Action reason would be displyed.
      if #JOBDATA = 1
      print $TERMIN ( ,88)
      end-if
      
A.ACTION
 let $printaction = &A.ACTION

 if #JOBDATA = 1
 print $printaction (,100)
 end-if 
 
A.ACTION_REASON
   Do Actionreason-Desc
    if #JOBDATA = 1
    print $Desc (,107)
    end-if
    
A.LBR_AUTO_REIN_FLG ( ,149)
       Let $AUTOFLAG = &A.LBR_AUTO_REIN_FLG
A.LBR_REINST_ACTION 
A.LBR_REINST_ACT_RSN
   Do ReinstActDes
A.LBR_REINST_DT
   
     Let $Rein = &A.LBR_REINST_ACTION
       IF rtrim($AUTOFLAG,' ') = 'Y' AND #JOBDATA = 1
       print $Rein ( ,163)  
       print $Reinst_Actde ( ,170)
      
      evaluate $DT_FMT
        when = 'M'
      do Format-DateTime(&A.LBR_REINST_DT,$REIN_DAT,{DEFMDY},'','')
       break
        when = 'D'
      do Format-DateTime(&A.LBR_REINST_DT,$REIN_DAT,{DEFDMY},'','')
       break
        when-Other
      do Format-DateTime(&A.LBR_REINST_DT,$REIN_DAT,{DEFYMD},'','')
       break
      end-evaluate
      
       print $REIN_DAT ( ,208)
       END-IF
       
       Let #no = #current-line
       
         
         if #no >= 50
         New-page
         Do Report
         end-if
         
FROM PS_HR_ROSTEXEC_TMP A
where A.LBR_ROSTER_ID = &LBR_ROSTER_ID
  and A.PROCESS_INSTANCE = #proc_inst
      
ORDER BY A.LBR_ROSTER_ID
END-SELECT
 
END-PROCEDURE

!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: This section Deletes the selected Temp Table             *
!***********************************************************************


Begin-Procedure Tempdelete

BEGIN-SQL
  
DELETE FROM PS_HR_ROSTEXEC_TMP
   WHERE PROCESS_INSTANCE = #proc_inst
   
END-SQL

End-Procedure 


!***********************************************************************
!-----------------Procedure Layoff Roster------------------------------*
!Description: String Table Selection                                   *
!***********************************************************************

Begin-Procedure Get-RP-Lbl

let $ReportID = 'HR_UHRLY'
do Init_Report_Translation($ReportID,$curr_language_cd) 
do Append_Report_Translation ($ReportID)

do Get_Field_Information ('HR_UHRLY','JOBCODE1',$Jobcode_Lbl,#DW1)
do Get_Field_Information ('HR_UHRLY','HR_LAYOFF_RPT',$LAY_RPT_LBL,#DW1)
do Get_Field_Information ('HR_UHRLY','HR_LAYOFF_LST',$REPORT_NAME,#DW1)
do Get_Field_Information ('HR_UHRLY','ROSTER_ID',$ROSTER_ID,#DW1)
do Get_Field_Information ('HR_UHRLY','PAGE_NO',$Pageno,#DW1)
do Get_Field_Information ('HR_UHRLY','PEOPLESOFT',$Peoplesoft,#DW1)
do Get_Field_Information ('HR_UHRLY','REPORTNAME',$Reportname,#DW1)
do Get_Field_Information ('HR_UHRLY','RUN DATE',$Rundate,#DW1)
do Get_Field_Information ('HR_UHRLY','RUN_TIME',$Runtime,#DW1)
do Get_Field_Information ('HR_UHRLY','EMP',$EMP,#DW1)
do Get_Field_Information ('HR_UHRLY','ID',$ID,#DW1)

do Get_Field_Information ('HR_UHRLY','PROCESS_INSTANCE',$PROCESS_INSTANCE,#DW1)
do Get_Field_Information ('HR_UHRLY','EFFECTIVEDATE',$EFFECTIVEDATE,#DW1)
do Get_Field_Information ('HR_UHRLY','EMPLOYEERECNUM',$EMPLOYEERECNUM,#DW1)
do Get_Field_Information ('HR_UHRLY','EMPLOYEE NAME',$EMPLOYEENAME,#DW1)
do Get_Field_Information ('HR_UHRLY','ACTION1',$ACTION,#DW1)
do Get_Field_Information ('HR_UHRLY','ACTION REASON',$ACTIONREASON,#DW1)
do Get_Field_Information ('HR_UHRLY','AUTO REINS_FLAG',$AUTOREINS_FLAG,#DW1)
do Get_Field_Information ('HR_UHRLY','REINST_ACT',$REINST_ACT,#DW1)
do Get_Field_Information ('HR_UHRLY','REINST_ACT_REAS',$REINST_ACT_REAS,#DW1)
do Get_Field_Information ('HR_UHRLY','ENR',$ENR,#DW1)
do Get_Field_Information ('HR_UHRLY','FLAG',$FLAG,#DW1)
do Get_Field_Information ('HR_UHRLY','REASON',$REASON,#DW1)
do Get_Field_Information ('HR_UHRLY','HR_LAY_LST',$HR_LAY_LST,#DW1)
do Get_Field_Information ('HR_UHRLY','HR_LAY_RPT',$HR_LAY_RPT,#DW1)
do Get_Field_Information ('HR_UHRLY','ROST_DESC',$ROST_DESC,#DW1)
do Get_Field_Information ('HR_UHRLY','EMPL',$lbl_Empl,#DW1)
do Get_Field_Information ('HR_UHRLY','REC',$lbl_Emplrec,#DW1)
do Get_Field_Information ('HR_UHRLY','EFF_DATE',$lbl_EffDate,#DW1)
do Get_Field_Information ('HR_UHRLY','AUTO_REINST',$AUTO_REINST,#DW1)
do Get_Field_Information ('HR_UHRLY','REINST_ACTION',$REINST_ACTION,#DW1)
do Get_Field_Information ('HR_UHRLY','ACTION8',$ACTION,#DW1)
do Get_Field_Information ('HR_UHRLY','REINST_ACT8',$REINST_ACT8,#DW1)
do Get_Field_Information ('HR_UHRLY','ROSTERDESC',$ROSTERDESC,#DW1)
do Get_Field_Information ('HR_UHRLY','EMPLOYEE_ID',$EmployeeID,#DW1)
do Get_Field_Information ('HR_UHRLY','REINST',$REINST,#DW1)
do Get_Field_Information ('HR_UHRLY','REINSTDT',$REINSTDT,#DW1)
End-Procedure !Get-RP-Lbl

!************************************************************************
!-----------------------------SQCs------------------------------------- *
!************************************************************************

#include 'stdapi.sqc'   !Routine to update run status
#include 'prcsapi.sqc'
#include 'prcsdef.sqc'
#include 'datetime.sqc'    !Routines for date and time formatting
#include 'curdttim.sqc'    !Get-Current-DateTime procedure
#include 'number.sqc'  
#Include 'sqrtrans.sqc'

#include 'prcslng.sqc'   !Gives Language Code
#include 'rellang.sqc'   !Gives Description from Related Lang tables