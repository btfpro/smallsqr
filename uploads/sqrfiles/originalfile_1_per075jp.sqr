!***********************************************************************
! PER075JP : Tamatsuki Placement Checklist                             *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:59:52                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!                                                                      *
!***********************************************************************
!    *
!                                                 *
!***********************************************************************

Begin-setup

#define  REPORTID  'PER075JP'
#include 'setenv.sqc'      !Set environment
#include 'ptset02.sqc'     !Init printer and page sizeLandscape

end-setup


begin-program
    do Init-DateTime
    do Init-Number
    do Get-Current-DateTime
    do Define-Prcs-Vars
    do Init-Report
    do Process-Main
    do Update-Prcs-Run-Status
end-program

!*************************************************************************!
! Init-Report
!*************************************************************************!
begin-procedure Init-Report

!***********************************************************************
! Translations procedures & Strings Table retrievals.                  *
!***********************************************************************

  move {REPORTID} to $ReportID
  let #Error_Total = 0
  do Get-Run-Control-Parms
  do Get-Run-Cntl-Data  

  do Init_Report_Translation ('PER075JP', $curr_language_cd)
  do Append_Report_Translation ('PER075JP')

  do Get_Field_Information ('PER075JP' ,'PER075JP_TITLE'    ,$PER075JP_Title          ,#DW)
  do Get_Field_Information ('PER075JP' ,'ORG_PLAN'          ,$org_plan_lbl            ,#DW)
  do Get_Field_Information ('PER075JP' ,'ORG_DATE'          ,$org_date_lbl            ,#DW)
  do Get_Field_Information ('PER075JP' ,'BUNIT'             ,$BUnit_lbl               ,#DW)
  do Get_Field_Information ('PER075JP' ,'DEPT_NAME'         ,$DEPT_NAME_lbl           ,#DW)
  do Get_Field_Information ('PER075JP' ,'EMPLID'            ,$EMPLID_lbl              ,#DW)
  do Get_Field_Information ('PER075JP' ,'EMPL_RCD'          ,$EMPL_RCD_lbl            ,#DW)
  do Get_Field_Information ('PER075JP' ,'NAME'              ,$NAME_lbl                ,#DW)
  do Get_Field_Information ('PER075JP' ,'APPT_TYPE'         ,$APPT_TYPE_lbl           ,#DW)  
  do Get_Field_Information ('PER075JP' ,'SUPV_LVL_NAME'     ,$SUPV_LVL_NAME_lbl       ,#DW)
  do Get_Field_Information ('PER075JP' ,'PLAN'              ,$PLAN_lbl                ,#DW)
  do Get_Field_Information ('PER075JP' ,'GRADE'             ,$GRADE_lbl               ,#DW)
  do Get_Field_Information ('PER075JP' ,'UNIT_JPN'          ,$UNIT_JPN_lbl            ,#DW)
  do Get_Field_Information ('PER075JP' ,'NOTES'             ,$NOTES_lbl               ,#DW)
 
  move $PER075JP_Title to $ReportTitle
  let $ReportDate            = $AsOfToday          ! Printed in header
  let $ReportTime            = $AsOfNow            ! Printed in header
  let $PNLGRPNAME = 'RC_IDO_TAM_CL_JPN'
  do Security-Param
  do Security-Param-JPN2

end-procedure !Init-Report

!*************************************************************************!
! Get-Run-Cntl-Data
!*************************************************************************!
begin-procedure Get-Run-Cntl-Data
  
begin-SELECT

RC.ORG_PLAN_JPN
RC.BUSINESS_UNIT
RC.SETID
RC.TREE_NAME
RC.TREE_NODE
RC.EFFDT_JPN

  do get-org-plan-date

FROM
PS_RC_IDO_JPN RC

WHERE
RC.OPRID = $prcs_oprid
AND RC.RUN_CNTL_ID = $prcs_run_cntl_id

end-SELECT

end-procedure !Get-Run-Cntl-Data

!*************************************************************************!
! get-org-plan-date
!*************************************************************************!

begin-procedure get-org-plan-date

begin-select

X.ORG_DATE_JPN

 let $AsOfDate      = &X.ORG_DATE_JPN

FROM
PS_IDO_ORGPLAN_JPN X

WHERE
X.ORG_PLAN_JPN = &RC.ORG_PLAN_JPN

end-select

end-procedure !get-org-plan-date

!*************************************************************************!
! Process-Main
!*************************************************************************!

begin-procedure Process-Main

  Let $Select_Records = 'PS_IDO_TAMATSU_JPN A, PS_JOB B, ' || $Security_Record

begin-select DISTINCT

A.UNIT_JPN (+1,9,7) on-break level = 1
A.SORT_JPN
A.BUSINESS_UNIT
A.DEPTID
A.SUPV_LVL_ID
B.APPT_TYPE
A.EMPLID
A.EMPL_RCD
B.SAL_ADMIN_PLAN
B.GRADE
A.IDO_ACTION_JPN
B.SETID_DEPT

 ! Keys to be passed for Procedure Get-Department-Name 
 let $BU             = &A.BUSINESS_UNIT
 let $DEPTID         = &A.DEPTID
 
 ! Keys to be passed for Procedure Get-Employee-Name
 let $EMPLID         = &A.EMPLID
 
 let #EMPL_RCD       = &A.EMPL_RCD
 
 ! Keys to be passed for Procedure Get-Supvlvl-Name
 let $SUPV_LVL_ID    = &A.SUPV_LVL_ID
 let $SETID_DEPT     = &B.SETID_DEPT
 
 ! Keys to be passed for Procedure Read-Translate-Table for Field APPT_TYPE
 let $FieldName   = 'APPT_TYPE'
 let $FieldValue  = &B.APPT_TYPE
 
 do Read-Translate-Table
 
 let $APPT_TYPE = $XlatShortName
 
 ! Keys to be passed for Procedure Read-Translate-Table for Field IDO_ACTION_JPN
 let $FieldName   = 'IDO_ACTION_JPN'
 let $FieldValue  = &A.IDO_ACTION_JPN
 
 do Read-Translate-Table
 
 let $IDO_ACTION_JPN = $XlatShortName

 do Get-Department-Name
 do Get-Supvlvl-Name
 do Get-Employee-Name
 do Print_Line

FROM  [PS_IDO_TAMATSU_JPN A, PS_JOB B, PS_FAST_SQR_SEC_VW SCRTY: $Select_Records]

WHERE A.EMPLID = B.EMPLID
AND A.EMPL_RCD = B.EMPL_RCD
AND B.EFFDT = (SELECT MAX(EFFDT)
FROM PS_JOB
WHERE EMPLID = B.EMPLID
AND EMPL_RCD = B.EMPL_RCD
AND EFFDT <= &X.ORG_DATE_JPN)
AND B.EFFSEQ = (SELECT MAX(EFFSEQ)
FROM PS_JOB
WHERE EMPLID = B.EMPLID
AND EMPL_RCD = B.EMPL_RCD
AND EFFDT = B.EFFDT)
ORDER BY A.UNIT_JPN, A.SORT_JPN

end-select

end-procedure !Process-Main

!*************************************************************************!
! Print Heading
!*************************************************************************!

begin-heading 10
#include 'stdhdgtr.sqc'

   print $org_plan_lbl (+2,9,19)
   print &RC.ORG_PLAN_JPN (0,28,4)
   print $org_date_lbl (+1,9,11)
     do Convert-To-DTU-Date (&X.ORG_DATE_JPN, $ORG_DATE_JPN_out)
   print $ORG_DATE_JPN_out (0,28,10)
   
   print $UNIT_JPN_lbl (+2,9,7)
   print $BUnit_lbl (0,20,10)
   print $DEPT_NAME_lbl (0,34,11)
   print $SUPV_LVL_NAME_lbl (0,49,11)
   print $APPT_TYPE_lbl (0,64,11)
   print $EMPLID_lbl (0,79,12)
   print $EMPL_RCD_lbl (0,95,5)
   print $NAME_lbl (0,104,31)
   print $PLAN_lbl (0,139,5)
   print $GRADE_lbl (0,148,6)
   print $NOTES_lbl (0,158,10)
   print '-' (+1,1,177) fill
    

end-heading
!*************************************************************************!
! Print_Line
!*************************************************************************!
begin-procedure Print_Line

   print $BU (0,20,10)
   print $DeptNameAbbrv (0,34,11)
   print $SupvlvlNameAbbrv (0,49,11)
   print $APPT_TYPE (0,64,11)
   print $EMPLID (0,79,12)
   print #EMPL_RCD (0,95,5) edit 99
   print $Name (0,104,31)
   print &B.SAL_ADMIN_PLAN (0,139,5)
   print &B.GRADE (0,148,6)
   print $IDO_ACTION_JPN (0,158,10)
   
end-procedure !Print_Line

!*************************************************************************!
! SQC Files for called procedures
!*************************************************************************!
#include 'prcsdef.sqc'       !Update Process Request variable declare
#include 'prcsapi.sqc'       !Update Process Request API
#include 'hrsecty.sqc'       !Get SQR Security parameters
#include 'hrsectjp.sqc'      !Get JPN SQR Security parameters
#include 'curdttim.sqc'      !Get-Current-DateTime procedure
#include 'datetime.sqc'      !Routines for date and time formatting
#include 'number.sqc'        !Routines to format numbers
#include 'sqrtrans.sqc'      !Translate SQR strings to given language
#include 'datemath.sqc'      !Date conversion procedures
#include 'getsupjp.sqc'      !Get Supervisor Level Name
#Include 'getempnm.sqc'      !Get Employee Name
#include 'getdptnm.sqc'      !Get Department Name from the Department Table
#include 'getjobtl.sqc'      !Get Job Title from the Job Table
#include 'readxlat.sqc'      !Get XLAT Value
