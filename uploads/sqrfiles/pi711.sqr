!***********************************************************************
!  PI711.SQR:INTERFACE  DEFINITION TABLE  (FIELD)                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:02:32:30                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-procedure Init-Report
  move 'PI711' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi711_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation

! Read from PI711 program ids
do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PI711', 'REPORT_TITLE', $Pi711_ReportTitle, #dummy_width)
  do Get_Field_Information ('PI711', 'FILEID', $Pi711_FileId, #dummy_width)
  do Get_Field_Information ('PI711', 'FILEDESCR', $Pi711_FileDescr, #dummy_width)
  do Get_Field_Information ('PI711', 'EFFDT', $Pi711_EffDt, #dummy_width)
  do Get_Field_Information ('PI711', 'REC_SEQ1', $Pi711_RecSeq1, #dummy_width)
  do Get_Field_Information ('PI711', 'REC_SEQ2', $Pi711_RecSeq2, #dummy_width)
  do Get_Field_Information ('PI711', 'RECORD_DESCR', $Pi711_RecordDescr, #dummy_width)
  do Get_Field_Information ('PI711', 'FLD_SEQ1', $Pi711_FldSeq1, #dummy_width)
  do Get_Field_Information ('PI711', 'FLD_SEQ2', $Pi711_FldSeq2, #dummy_width)
  do Get_Field_Information ('PI711', 'FLDID', $Pi711_FldId, #dummy_width)
  do Get_Field_Information ('PI711', 'KEY', $Pi711_Key, #dummy_width)
  do Get_Field_Information ('PI711', 'GRPID', $Pi711_GrpId, #dummy_width)
  do Get_Field_Information ('PI711', 'GRPDESCR', $Pi711_GrpDescr, #dummy_width)
  do Get_Field_Information ('PI711', 'REV_SIGN1', $Pi711_RevSign1, #dummy_width)
  do Get_Field_Information ('PI711', 'REV_SIGN2', $Pi711_RevSign2, #dummy_width)
  do Get_Field_Information ('PI711', 'CONV_CASE1', $Pi711_ConvCase1, #dummy_width)
  do Get_Field_Information ('PI711', 'CONV_CASE2', $Pi711_ConvCase2, #dummy_width)
  do Get_Field_Information ('PI711', 'REDEF_IND1', $Pi711_RedefInd1, #dummy_width)
  do Get_Field_Information ('PI711', 'REDEF_IND2', $Pi711_RedefInd2, #dummy_width)

end-procedure


begin-heading 7
#Include 'stdhdgtr.sqc'
  print $Pi711_RecSeq1       (6,37,38)
  print $Pi711_FldSeq1       (,77,59)
  print $Pi711_RevSign1      (,138,8)
  print $Pi711_ConvCase1     (,148,8)
  print $Pi711_RedefInd1     (,158,8)

  print $Pi711_FileId        (7,1,9)
  print $Pi711_FileDescr     (,11,12)
  print $Pi711_EffDt         (,25,10)
  print $Pi711_RecSeq2       (,37,6)
  print $Pi711_RecordDescr   (,45,30)
  print $Pi711_FldSeq2       (,77,30)
  print $Pi711_FldId         (,89,10)
  print $Pi711_Key           (,101,9)
  print $Pi711_GrpId         (,109,6)
  print $Pi711_GrpDescr      (,121,10)
  print $Pi711_RevSign2      (,138,15)
  print $Pi711_ConvCase2     (,148,8)
  print $Pi711_RedefInd2     (,158,8)
end-HEADING

begin-procedure Process-Main


begin-SELECT

A.PI_FILE_ID                 (+1,1,9) on-break level=1 skiplines=1
B.DESCRSHORT                 !(,11) on-break level=1

      Let $Prnt_Pifil_Descrshort = &B.DESCRSHORT
      if $Curr_language_cd <> $Psoptions_Language_Cd
        do Lang-Record-Details
      end-if
      Print  $Prnt_Pifil_Descrshort (,11,12) on-break level=1

A.EFFDT
    do Format-DateTime(&A.EFFDT, $out, {DEFDATE}, '', '')
    print $out               (,25,10) on-break level=2
A.PI_RECORD_SEQ              (,37,6)
C.DESCR                      !(,45) on-break level=3

      Let $Prnt_Pirec_Descr = &C.DESCR
      if $Curr_language_cd <> $Psoptions_Language_Cd
        do Lang-Record-Details1
      end-if
      Print  $Prnt_Pirec_Descr  (,45,30) on-break level=3

A.PI_FIELD_SEQ               (,77,10)
A.PI_FIELD_ID                (,89,10)
A.PI_KEY_FIELD               (,101,9)
A.PI_GROUP_ID                (,109,6)


 do  Get-Group-Name

 if $Curr_language_cd <> $Psoptions_Language_Cd
   do Lang-Record-Get-Group-Name
 end-if

 print $Group_Name_Descr     (,121,15)

A.REVERSE_SIGN               (,138,8)
A.CONVERT_CASE               (,148,8)
A.REDEFINE_IND               (,158,8)
A.PI_SYSTEM_ID

FROM PS_PI_DEFN_FIELD A, PS_PI_DEFN_FILE B, PS_PI_DEFN_RECORD C
WHERE A.PI_FILE_ID = B.PI_FILE_ID AND A.EFFDT = B.EFFDT
      AND A.PI_FILE_ID = C.PI_FILE_ID AND A.PI_RECORD_SEQ = C.PI_RECORD_SEQ
      AND A.EFFDT = C.EFFDT
ORDER BY A.PI_FILE_ID, A.EFFDT DESC, A.PI_RECORD_SEQ, A.PI_FIELD_SEQ

end-SELECT

end-procedure


begin-procedure Lang-Record-Details
begin-select

LB.DESCRSHORT

   Let $Prnt_Pifil_Descrshort = Cond(Rtrim(&LB.DESCRSHORT,' ')='', $Prnt_Pifil_Descrshort, &LB.DESCRSHORT)

FROM PS_PI_DEFN_F_LANG LB
WHERE PI_FILE_ID = &A.PI_FILE_ID
  AND LANGUAGE_CD  = $Curr_language_cd
  AND EFFDT        = (SELECT MAX(EFFDT) FROM PS_PI_DEFN_F_LANG
                       WHERE PI_FILE_ID = &A.PI_FILE_ID
                         AND LANGUAGE_CD  = $Curr_language_cd
                         AND EFFDT <= &A.EFFDT )

end-select
end-procedure


begin-procedure Lang-Record-Details1
begin-select

LB.DESCR

   Let $Prnt_Pirec_Descr = Cond(Rtrim(&LB.DESCR,' ')='', $Prnt_Pirec_Descr, &LB.DESCR)

FROM PS_PI_DEFN_R_LANG LB
WHERE PI_FILE_ID = &A.PI_FILE_ID
  AND LANGUAGE_CD  = $Curr_language_cd
  AND EFFDT        = (SELECT MAX(EFFDT) FROM PS_PI_DEFN_R_LANG
                       WHERE PI_FILE_ID = &A.PI_FILE_ID
                         AND LANGUAGE_CD  = $Curr_language_cd
                         AND EFFDT <= &A.EFFDT )
  AND PI_RECORD_SEQ   = &A.PI_RECORD_SEQ

end-select
end-procedure


begin-procedure Get-Group-Name

let $Group_Name_Descr = ' '

begin-SELECT

D.DESCRSHORT

 let $Group_Name_Descr =  &D.DESCRSHORT

FROM   PS_PI_GROUP_TBL D
WHERE  D.PI_SYSTEM_ID = &A.PI_SYSTEM_ID
  AND  D.PI_GROUP_ID  = &A.PI_GROUP_ID

end-SELECT

end-procedure Get-Group-Name



begin-procedure Lang-Record-Get-Group-Name


begin-SELECT

GL.DESCRSHORT

 let $Group_Name_Descr =  &GL.DESCRSHORT

FROM   PS_PI_GROUP_LANG GL
WHERE  GL.PI_SYSTEM_ID = &A.PI_SYSTEM_ID
  AND  GL.PI_GROUP_ID  = &A.PI_GROUP_ID
  AND  GL.LANGUAGE_CD  = $Curr_language_cd

end-SELECT

end-procedure Lang-Record-Get-Group-Name


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

