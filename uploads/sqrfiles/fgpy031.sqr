
!***********************************************************************
!  Leave Denial Notification                                           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:03:56:12                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and page-size initialization

!***********************************************************************
!  FGPY031:  Begin Report                                              *
!***********************************************************************
begin-report

  do Init-Report
  do Get-Parameters
  do Process-Main
  do Stdapi-Term

end-report

!**************************
begin-procedure Init-Report
!**************************

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  let $ReportID = 'FGPY031'
  let $ReportTitle = 'Leave Denial Notification'
  display  'Report ID: ' noline
  display $ReportTitle

end-procedure

!***************
begin-heading 10
!***************

  print '            '    (1,1)

end-heading

!*****************************
begin-procedure Get-Parameters
!*****************************

  if $Prcs_Process_Instance = ''
    stop
  else
    do Select-Parameters
    let $Agency = RTRIM(&RUN_CNTL_FG.Company, ' ')
    let $FromDate = RTRIM(&RUN_CNTL_FG.fromdate, ' ')
    let $ThruDate = RTRIM(&RUN_CNTL_FG.thrudate, ' ')
  end-if

end-procedure

!***************************
begin-procedure Process-Main
!***************************

  do Process-Denied-Recipients

end-procedure

!******************************************
begin-procedure Process-Denied-Recipients
!******************************************

BEGIN-SELECT

E.NAME
A.GVT_LEAVE_XFR_TYPE
A.GVT_DENIAL_REASON2
A.EFFDT
B.GVT_LV_DENL_DESCR
D.GVT_LEAVE_XFR_PRGM
G.COMPANY

  do Get-Denial-Two
  do print-letter
  new-page

FROM PS_GVT_LV_REC_APP A, 
     PS_PERSON_NAME E, 
     PS_GVT_DENL_CD_TBL B, 
     PS_GVT_XFR_TYP_TBL D,
     PS_JOB F,
     PS_COMPANY_TBL G
WHERE A.EFFDT BETWEEN $FromDate AND $ThruDate
  AND A.GVT_LV_APP_STATUS = 'D'
  AND A.EMPLID = E.EMPLID
  AND B.EFFDT =
     (SELECT MAX(B_ED.EFFDT) FROM PS_GVT_DENL_CD_TBL B_ED
      WHERE B.GVT_DENIAL_CD = B_ED.GVT_DENIAL_CD
        AND B_ED.EFFDT <= A.EFFDT)
  AND B.GVT_DENIAL_CD = A.GVT_DENIAL_REASON1
  AND D.EFFDT =
      (SELECT MAX(D_ED.EFFDT) FROM PS_GVT_XFR_TYP_TBL D_ED
       WHERE D.GVT_LEAVE_XFR_TYPE = D_ED.GVT_LEAVE_XFR_TYPE
         AND D_ED.EFFDT <= A.EFFDT)
  AND D.GVT_LEAVE_XFR_TYPE = A.GVT_LEAVE_XFR_TYPE
  AND G.COMPANY = F.COMPANY
  AND A.EMPLID = F.EMPLID
     AND F.EMPL_RCD = A.EMPL_RCD
     AND F.COMPANY = $Agency
     AND F.EFFDT =
        (SELECT MAX(F_ED.EFFDT) FROM PS_JOB F_ED
        WHERE F.EMPLID = F_ED.EMPLID
          AND F.EMPL_RCD = F_ED.EMPL_RCD
          AND F_ED.EFFDT <= A.EFFDT)
    AND F.EFFSEQ =
        (SELECT MAX(F_ES.EFFSEQ) FROM PS_JOB F_ES
        WHERE F.EMPLID = F_ES.EMPLID
          AND F.EMPL_RCD = F_ES.EMPL_RCD
          AND F.EFFDT = F_ES.EFFDT)
     AND G.EFFDT =
        (SELECT MAX(G_ED.EFFDT) FROM PS_COMPANY_TBL G_ED
        WHERE G.COMPANY = G_ED.COMPANY
          AND G_ED.EFFDT <= F.EFFDT)

END-SELECT

end-procedure

!******************************************
begin-procedure Get-Denial-Two
!******************************************

BEGIN-SELECT

C.GVT_LV_DENL_DESCR

FROM      PS_GVT_DENL_CD_TBL C

WHERE C.GVT_DENIAL_CD = &A.GVT_DENIAL_REASON2
AND C.EFFDT = (SELECT MAX(C_ED.EFFDT) FROM PS_GVT_DENL_CD_TBL C_ED
       WHERE C.GVT_DENIAL_CD = C_ED.GVT_DENIAL_CD
         AND C_ED.EFFDT <= &A.EFFDT)

END-SELECT

end-procedure

!***************************
begin-procedure print-letter
!***************************

print 'To: '                                                  (+2,)
print &E.NAME                                                 (,)  bold
print 'Date: '                                                (+1,1)
 do Format-DateTime($ThruDate, $ThruDtOut, {DEFDATE},'','')
print $ThruDtOut                                              (,)
print 'Subject: '                                             (+2,1)  
print 'Leave Recipient Application'                           (,) bold
print '_'  (+2,1,90) fill
print 'This notification is to inform you that your request to become a '                  (+4,1)
  let $Fieldname = 'GVT_LEAVE_XFR_PRGM'
  let $Fieldvalue = &D.GVT_LEAVE_XFR_PRGM
  Do Read-Translate-Table
let $LeavePrgm =  RTRIM($Xlatlongname, ' ')
print $LeavePrgm                                               (,)
print &A.GVT_LEAVE_XFR_TYPE                                    (0,+1)
print 'Leave Program '                                         (0,+1)
print 'Recipient has been denied for the following reason(s):' (+1,1)
print &B.GVT_LV_DENL_DESCR                                     (+2,10) wrap 70 4
  if &C.GVT_LV_DENL_DESCR <> ''
    print &C.GVT_LV_DENL_DESCR                                 (+1,10) wrap 70 4
  end-if
print 'Sincerely, '                                            (+4,1)
print 'Leave Transfer Approval Board'                          (+6,1)

end-procedure

!***********************************************************************
!  The following are include sqc files                                 *
!***********************************************************************
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'fgrunctl.sqc'  !Select Parameters
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'datemath.sqc'  !Do date manipulation
!*---------------------------------------------------------------------*

