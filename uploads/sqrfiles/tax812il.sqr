!***********************************************************************
!  TAX812IL:  Mthly Wage List - ILLINOIS                               *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C)  2013, Oracle and/or its affiliates.                   *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2013/03/01:09:38:48                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  885420                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#include 'setup31.sqc' !Printer and page-size initialization
#include 'usarpt.sqc'  !Get USA variables

begin-report

  do Init-Report
  do Process-Main
   
  close 10
   
  if $RecordWritten = 'Y'
    display ''
    display 'Upon successful conclusion of this program, the following'
    display 'file will have been created in CSV format for the reporting'
    display 'of monthly wage data to the State of Illinois:'
    display ''
    display '{IMPORTPREFIX}TAX812IL'
    display ''
    display 'Follow instructions from the department for preparing and'
    display 'submitting the files.'
    display ''
  else
    display ''
    display '******  No Data To Process  ******'
    display ''
    print '******  No Data Selected  ******'  (+2,) center
  end-if

  do Stdapi-Term
  
end-report

begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do Stdapi-Init
  do Get-Calendar-Year-Id

  do Initialization
  move 'TAX812IL' to $ReportID

  move 'Illinois Monthly UI Wage Report' to $ReportTitle
  display ''
  display 'Creating File for Illinois UI Wage Reporting'
  
  move 'E' to $Rec_E
  move 'S' to $Rec_S
  move  0  to #Wages_NotAlloc 
  
  move #Wages_NotAlloc to $Wages_NotAlloc 9
  
end-procedure

begin-procedure Initialization

  #ifdef TAXTEST
     move 'CA'              to $State
  #else
     move 'IL'              to $State
  #endif

  if $Prcs_Process_Instance  =  ''
     do Employer-Prompts
  else
     do Get-IL-Reporting-Run-Controls              
     move $PeriodEndDate      to $AsOfDate
  end-if
  
  display ' '
  display 'Processing balances for '   noline
  display $PeriodEndDate

  do Open-Files

  move 0   to #Count_E
  move 0   to #Count_F
  move 'N' to $RecordWritten
  
end-procedure

begin-procedure Open-Files

  move 'CSV' to $FileExtension

#ifdef OS400
  let $FileID = 'TAX812IL' || '(D' || $FileExtension || ')'
#else
  #ifdef MVS
    let $FileID = 'TAX812IL'
  #else
    let $FileID = 'TAX812IL.' || $FileExtension
  #endif
#endif

  let $FileID = '{IMPORTPREFIX}' || $FileID

#ifdef MVS
  open $FileID as 10 for-writing record=62:fixed_nolf
#else
  open $FileID as 10 for-writing record=62:fixed
#endif

end-procedure

begin-procedure Get-IL-Reporting-Run-Controls

begin-SELECT
IL.BALANCE_YEAR
IL.BALANCE_PERIOD
 
 move &IL.BALANCE_YEAR   to #PrcsYear
 move &IL.BALANCE_PERIOD to #PrcsMth
 
FROM PS_RC_TAX812IL IL
WHERE IL.OPRID       = $Prcs_OprID
  AND IL.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-SELECT
move #PrcsYear to $PeriodEndDate xxxx
move #PrcsMth  to $BalMth 09

if &IL.BALANCE_PERIOD = 2
  concat $BalMth with $PeriodEndDate -xx-2
  let #Remainder = mod(#PrcsYear, 4)
  
  if #Remainder = 0
    concat '9' with $PeriodEndDate
  else 
    concat '8' with $PeriodEndDate
  end-if  
else
  concat $BalMth with $PeriodEndDate -xx-3
  if &IL.BALANCE_PERIOD = 4 or &IL.BALANCE_PERIOD = 6 or 
     &IL.BALANCE_PERIOD = 9 or &IL.BALANCE_PERIOD = 11
     concat '0' with $PeriodEndDate
  else !Mths 1,3,5,7,8,10,12
     concat '1' with $PeriodEndDate
  end-if
end-if

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($PeriodEndDate, $PeriodEndDate, {DEFYMD}, '', 'native')
  
  move &IL.BALANCE_PERIOD to $RptQtrYr
  evaluate $RptQtrYr
    when = '1'
      move 'January '   to $RptQtrYr
    when = '2'
      move 'February '  to $RptQtrYr
    when = '3'
      move 'March '     to $RptQtrYr
    when = '4'
      move 'April '     to $RptQtrYr
    when = '5'
      move 'May '       to $RptQtrYr
    when = '6'
      move 'June '      to $RptQtrYr
    when = '7'
      move 'July '      to $RptQtrYr
    when = '8'
      move 'August '    to $RptQtrYr
    when = '9'
      move 'September ' to $RptQtrYr
    when = '10'
      move 'October '   to $RptQtrYr
    when = '11'
      move 'November '  to $RptQtrYr
    when = '12'
      move 'December '  to $RptQtrYr
  end-evaluate
  concat &IL.BALANCE_YEAR with $RptQtrYr

end-procedure

begin-procedure Process-Main

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
A.COMPANY
A.DESCR
A.FEDERAL_EIN
A.ADDRESS1
A.CITY
A.STATE
A.POSTAL

      move &A.Company to $Company
      move &A.Descr   to $CompanyName

      move '' to $priorEmplID
      do Read-Employee-Data

FROM  PS_COMPANY_TBL A
WHERE A.EFF_STATUS      = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
  (SELECT MAX(EFFDT)
     FROM PS_COMPANY_TBL
    WHERE  COMPANY = A.COMPANY
      AND  EFFDT  <= $AsOfDate)
ORDER BY COMPANY
end-SELECT

   if #Count_F > 0
      do Print-File-Tot
   end-if

end-procedure

begin-procedure Read-Employee-Data

move 0 to #Count_S

begin-SELECT
C.COMPANY    () on-break print=never before=Before-Company
                                     after=After-Company
C.EMPLID
C.NLGRS_MTD
C.TXGRS_MTD
C.NLGRS_QTD
C.TXGRS_QTD
D.LAST_NAME
D.FIRST_NAME
D.MIDDLE_NAME
D1.NATIONAL_ID

        if &C.NlGrs_MTD < 0 or &C.TxGrs_MTD < 0
           do Print-Negative
        else
           do Process-Employee
        end-if

FROM  PS_TAX_BALANCE C, PS_PERSON_NAME D, PS_PERS_NID D1
WHERE C.COMPANY           = &A.Company
  AND D.EMPLID            = C.EMPLID
  AND D.EMPLID            = D1.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND C.TAX_CLASS         = 'U'
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = #PrcsYear
  AND C.BALANCE_PERIOD    = #PrcsMth
#ifdef TAXTEST
   AND C.STATE            = 'CA'
#else
   AND C.STATE            = 'IL'
#endif
  AND C.LOCALITY          = ' '
  AND C.NLGRS_MTD <> 0
ORDER BY D1.NATIONAL_ID
end-SELECT

end-procedure

begin-procedure Before-Company
  do Get-Company-Data
  do Get-State-Tax-Data             !Employer ID
  if RTRIM(&Employer_ID_SUT,' ') = ''
    display '*** State Unemployment ID missing in Company State Tax Table ***'
    display '*********************** Processing stopped *********************'
    display 'Company ' noline
    display $Company
    stop
  end-if
  let $StateEIN = SUBSTR(&Employer_ID_SUT,1,7)
  move $StateEIN to $StateEIN 0999999
  
  move &A.Federal_EIN  to $FedEIN 099999999

  do Get-Company-Rec-Data
  
  move &CompanyNLGross to #CompanyNLGross
  
  let $Co_Wages = LTRIM(EDIT(#CompanyNLGross,'99999999999.99'),' ')       
  do Write-E-Record
  
end-procedure

begin-procedure After-Company

  move &EmplCount      to #Count_S        ! reformat to numeric for accumulating
  add  #Count_S        to #Count_F

  move #CompanyNLGross to #Co_NlGrs_MTD   ! No Limit Gross QTD
  add  #CompanyNLGross to #Tot_NlGrs_MTD

  do  Print-Company-Tot                   ! Print Summary Report
  new-page
  
end-procedure

begin-procedure Employer-Prompts

  display ' '
  display 'Enter processing data for ' noline
  display $Company noline
  display ':'
  input #PrcsYear   '   Year              '
  input #PrcsMth    '   Month             '

end-procedure

begin-procedure Get-Company-Rec-Data

  move 0 to #CompanyNLGross

begin-SELECT

COUNT(*)  &EmplCount

#ifdef DB2ALL
DECIMAL(SUM(H1.NLGRS_MTD),15,3)  &CompanyNLGross
#else
SUM(H1.NLGRS_MTD)  &CompanyNLGross
#endif

FROM  PS_TAX_BALANCE H1
WHERE H1.COMPANY           = &A.Company
  AND H1.TAX_CLASS         = 'U'
  AND H1.BALANCE_ID        = $Calendar_Year_Id
  AND H1.BALANCE_YEAR      = #PrcsYear
  AND H1.BALANCE_PERIOD    = #PrcsMth

#ifdef TAXTEST
   AND H1.STATE            = 'CA'
#else
   AND H1.STATE            = 'IL'
#endif

  AND H1.LOCALITY          = ' '
  AND H1.NLGRS_MTD         >= 0
GROUP BY H1.COMPANY, H1.STATE

end-SELECT

end-procedure

begin-procedure Process-Employee

  move &D1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
    move '000-00-0000'       to $SSN
  else
    move &D1.NATIONAL_ID     to $SSN xxx-xx-xxxx
  end-if
  let $SSN = {ps-substr}($SSN,1,11)

  let $LastName = rtrim(&D.LAST_NAME, ' ')
  if length($LastName) > 20
     let $LastName = {ps-substr}($LastName,1,20)
  end-if

  let $FirstName = rtrim(&D.FIRST_NAME, ' ')
  if length($FirstName) > 12
     let $FirstName = {ps-substr}($FirstName,1,12)
  end-if

  uppercase $FirstName
  uppercase $LastName
 
  do Replace-Special-Chars($LastName, $LastNameFMT)
  do Replace-Special-Chars($FirstName, $FirstNameFMT)
  
  move &C.NlGrs_MTD to #NLGrs_MTD
  let $NLGrs_MTD = LTRIM(EDIT(#NLGrs_MTD,'99999999999.99'),' ')   
  
  do Write-S-Record
  
end-procedure

begin-procedure Write-E-Record
    
  let $OutputF = $Rec_E || ',' || $FedEIN || ',' || $StateEIN ||
                 ',' || $Co_Wages || ',' || $Wages_NotAlloc 
                
  write 10 from $OutputF

add 1    to #Count_E
move 'Y' to $RecordWritten

end-procedure

begin-procedure Write-S-Record
    
  let $OutputF = $Rec_S || ',' || $FirstNameFMT || ',' || $LastNameFMT ||
                 ',' || $SSN || ',' || $NLGrs_MTD
                
  write 10 from $OutputF

end-procedure

begin-procedure Print-Company-Tot

  if $Company <> $LastCompany
    move $Company     to $LastCompany
    move $CompanyName to $LastCompanyName
  end-if

  print 'FEIN:'                                (+3,1,20)
  print $FedEIN                                 (,22)   

  print 'State UI Acct:'                       (+2,1,20)
  print $StateEIN                               (,22)   

  print '**** Company Total ****'              (+3,1)
  print 'No Limit Gross MTD '                  (+2,1,26)
  print #Co_NlGrs_MTD                           (,28)   edit 999,999,999.99mi

  print 'Employees Reported'                   (+2,1,29)
  print #Count_S                                (,31)   edit 999,999,999

end-procedure

begin-procedure Print-File-Tot

  move '' to $LastCompany
  move '' to $LastCompanyName


  print '**** File Total ****'     (+3,1)
  print 'No Limit Gross MTD '      (+2,1,26)
  print #Tot_NlGrs_MTD              (,28)   edit 999,999,999.99mi

  print 'Employees Reported'       (+2,1,29)
  print #Count_F                    (,31)   edit 999,999,999

  if #Count_E <> 0
    print 'Employers Reported'     (+2,1,29)
    print #Count_E                  (,31)   edit 999,999,999
  end-if

  print 'Output File:  '        (+2,1)

  let $FileID = '{IMPORTPREFIX}' || 'TAX812IL'
  print $FileID                    (+1,10)

end-procedure

begin-procedure Print-Negative

  print ' '                           (+1,1)
  print '**** Negative Wages ******'  (+1,1)
  print ' '                           (+1,1,11)
  print '     MTD     '               (0,+4,13)
  print 'EmplID'                      (+1,1,11)
  print 'No Limit Gross'              (0,+4,14)
  position (+1)

  print &C.EmplID        (+1,1,14)
  print &C.NlGrs_MTD     (0,16) edit 99,999,999.99mi
  
end-procedure

begin-procedure  Replace-Special-Chars($In_String, :$Out_String)
  if isnull($Alphabet)
     let $Alphabet =  'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789 ''-'
     let $AccentY  =  '‚‰‡Â·„Êƒ≈¿¡¬√ƒ≈∆Á«–ÈÍÎË…» ÀÔÓÏÌÃÕŒœÒ—ÙˆÚ÷Ûı“”‘’¸˚˘‹˙Ÿ⁄€ˇ˝›'
     let $AccentN  =  'AAAAAAAAAAAAAAAACCDEEEEEEEEIIIIIIIINNOOOOOOOOOOUUUUUUUUYYY'
  end-if
   
  let #I = 1
  let $Out_String = ''
  let #In_Len = Length(Rtrim($In_String, ' '))
  while #I <= #In_Len
    let $Char = Substr($In_String, #I, 1)
    let $Upper_Char = Upper($Char)
    let #Loc = Instr($Alphabet,$Upper_Char,1)
    if #Loc > 0
      let $Out_String = $Out_String || $Upper_Char
    else
      let #Loc = Instr($AccentY,$Char,1)
      if #Loc > 0
        let $Out_String = $Out_String || Substr($AccentN,#Loc,1)
      end-if
    end-if
    add 1 To #I
  end-while
end-procedure

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'rptsmmry.sqc'  !Print-Summary-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls