!***********************************************************************
!  TAX014:  Federal Liability by State                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2012, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2012/11/08:11:50:44                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  872695                                              !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
! HP00001       Add PublicSector indicator                             *
! HP00006       E&G 7.5  System test fixes                             *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#Include 'setup31.sqc'  !Printer and page-size initialization
#include 'usarpt.sqc'   !Get USA variables

begin-report
  do Init-Report
  do Stdapi-Term
end-report

begin-procedure Init-Report

  move 'TAX014' to $ReportID
  move 'Federal Liability by State' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Array-Initialization
  do Payroll-Report-Initialization
end-procedure

begin-procedure Array-Initialization
  create-array name=OtherTaxes size=10 -                                !HP00006
    field=TaxType:char                -
    field=TxGrs_Cur:number            -
    field=Tax_Cur:number              -
    field=ER_Match:number
  create-array name=Totals size=5 -
    field=TotalName:char field=total:number:4 field=startCol:number:4
  put 'Grand Total'            into Totals(1) TotalName
  put 'Company Total'          into Totals(2) TotalName
  put 'Pay Period Total'       into Totals(3) TotalName
  put 'Check Date Total'       into Totals(4) TotalName
  put  36 into Totals(0) startCol(0)       !federal withholding
  put  71 into Totals(0) startCol(1)       !OASDI/Medicare - EE
  put  85 into Totals(0) startCol(2)       !OASDI/Medicare - ER
  put 105 into Totals(0) startCol(3)       !total 'federal liability'
end-procedure

begin-heading 9
  #Include 'stdhdgpi.sqc'
  #Include 'stdhdg05.sqc'
  print '  State              <------ WITHHOLDING ------> '  (+2,1)
  print '<------------  OASDI/MEDICARE  ----------------> '  ()
  print 'State EIN             Taxable Gross       Tax    '  (+1,1)
  print ' Type   Taxable Gross       Tax         Employer '  ()
  print '    Total Liability '                               ()
end-heading

begin-procedure Report
begin-SELECT
A.COMPANY    () on-break print=never level=1 procedure=Company-Change
A.PAY_END_DT () on-break print=never level=2 procedure=PayEndDate-Change
A.CHECK_DT   () on-break print=never level=3 procedure=CheckDate-Change
ST.STATE     () on-break print=never level=4 procedure=State-Change
FD.TAX_CLASS
#ifdef DB2ALL
DECIMAL(SUM(FD.TXGRS_CUR),15,3) &FD.TxGrs_Cur
DECIMAL(SUM(FD.TAX_CUR),15,3)   &FD.Tax_Cur
#else
SUM(FD.TXGRS_CUR) &FD.TxGrs_Cur
SUM(FD.TAX_CUR)   &FD.Tax_Cur
#endif

  do Process-Record

FROM  PS_PAY_CALENDAR A, PS_PAY_TAX FD, PS_PAY_TAX ST,                  !HP00001
      PS_INSTALLATION IN1                                               !HP00001
WHERE [$SlctCalendar]
  AND FD.OFF_CYCLE IN ($SlctOffCycleA, $SlctOffCycleB)
  AND FD.PAGE_NUM BETWEEN #SlctPageFrom AND #SlctPageThru
  AND FD.COMPANY    = A.COMPANY
  AND FD.PAYGROUP   = A.PAYGROUP
  AND FD.PAY_END_DT = A.PAY_END_DT
  AND (FD.TAX_CLASS  IN ('H','D','F','C','G','T','E','J','Q','Z','7','8','9') !HP99999
       OR (IN1.PUBLIC_SECTOR = 'Y'                                      !HP00001
           AND FD.TAX_CLASS = 'A'))                                     !HP99999
  AND FD.STATE      = '$U'
  AND ST.COMPANY    = FD.COMPANY
  AND ST.PAYGROUP   = FD.PAYGROUP
  AND ST.PAY_END_DT = FD.PAY_END_DT
  AND ST.PAGE_NUM      = FD.PAGE_NUM
  AND ST.LINE_NUM      = FD.LINE_NUM
  AND ST.SEPCHK     = FD.SEPCHK
  AND ST.OFF_CYCLE  = FD.OFF_CYCLE
  AND ST.STATE     <> '$U'
  AND ST.LOCALITY   = FD.LOCALITY
  AND ST.TAX_CLASS  = (SELECT MAX(ST1.TAX_CLASS)                        !HP00006
                        FROM PS_PAY_TAX ST1                             !HP00006
                       WHERE ST1.COMPANY    = FD.COMPANY                !HP00006
                         AND ST1.PAYGROUP   = FD.PAYGROUP               !HP00006
                         AND ST1.PAY_END_DT = FD.PAY_END_DT             !HP00006
                         AND ST1.PAGE_NUM      = FD.PAGE_NUM                  !HP00006
                         AND ST1.LINE_NUM      = FD.LINE_NUM                  !HP00006
                         AND ST1.SEPCHK     = FD.SEPCHK                 !HP00006
                         AND ST1.OFF_CYCLE  = FD.OFF_CYCLE              !HP00006
                         AND ST1.STATE     <> '$U'                      !HP00006
                         AND ST1.LOCALITY   = FD.LOCALITY               !HP00006
                         AND ST1.TAX_CLASS IN ('H','A'))                !HP00006
GROUP BY A.COMPANY,A.PAY_END_DT,A.CHECK_DT,ST.STATE,FD.TAX_CLASS
ORDER BY A.COMPANY,A.PAY_END_DT,A.CHECK_DT,ST.STATE,FD.TAX_CLASS
end-SELECT
  move 1 to #changeLevel
  move 'Y' to $End
  do Print-Totals
end-procedure

begin-procedure Process-Record
  if &A.Company <> $priorCompany
    move &A.Company to $Company
    move &A.Company to $priorCompany
    do Get-Company-Data  !for Federal_EIN, SUT Rate
  end-if

  move &A.Pay_End_Dt to $PayEndDate
  move &A.Check_Dt   to $AsOfDate
  let $State = rtrim(&ST.State, ' ')

  evaluate &FD.Tax_Class

    when = 'H'  !Withholding Employee Income Tax
      add &FD.TxGrs_Cur to #WithholdingGross
      add &FD.Tax_Cur   to #WithholdingTax
    when = 'C'  !Earned Income Credit
      add &FD.Tax_Cur to #EIC
    when = 'F'  !FICA Medicare / EE
    when = 'T'  !FICA Medicare Tips / EE
      if #MediEIndex = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #MediEIndex
        put 'MEDIE' &FD.TxGrs_Cur &FD.Tax_Cur 0
          into OtherTaxes(#MediEIndex) TaxType TxGrs_Cur Tax_Cur ER_Match
      else
        array-add &FD.TxGrs_Cur &FD.Tax_Cur 0
          to OtherTaxes(#MediEIndex) TxGrs_Cur Tax_Cur ER_Match
      end-if
    when = '7'  !Addl Medicare/EE
      if #AddlMediEIndex = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #AddlMediEIndex
        put 'ADMED' &FD.TxGrs_Cur &FD.Tax_Cur 0
          into OtherTaxes(#AddlMediEIndex) TaxType TxGrs_Cur Tax_Cur ER_Match
      else
        array-add &FD.TxGrs_Cur &FD.Tax_Cur 0
          to OtherTaxes(#AddlMediEIndex) TxGrs_Cur Tax_Cur ER_Match
      end-if
    when = 'Q'  !FICA Medicare /ER
    when = 'Z'  !FICA Medicare Tips / ER
      if #MediRIndex = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #MediRIndex
        put 'MEDIR' &FD.TxGrs_Cur 0 &FD.Tax_Cur
          into OtherTaxes(#MediRIndex) TaxType TxGrs_Cur Tax_Cur ER_Match
      else
        array-add &FD.TxGrs_Cur 0 &FD.Tax_Cur
          to OtherTaxes(#MediRIndex) TxGrs_Cur Tax_Cur ER_Match
      end-if
    when = 'D'  !OASDI / EE
    when = 'G'  !OASDI Tips / EE
    when = 'W'  !VDI / EE
      if #DisEIndex = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #DisEIndex
        put 'OASDE' &FD.TxGrs_Cur &FD.Tax_Cur 0
           into OtherTaxes(#DisEIndex)
                TaxType TxGrs_Cur Tax_Cur ER_Match
      else
          array-add &FD.TxGrs_Cur &FD.Tax_Cur 0
            to OtherTaxes(#DisEIndex)
                TxGrs_Cur Tax_Cur ER_Match
      end-if
    when = 'E'  !OASDI / ER
    when = 'J'  !OASDI Tips / ER
    when = 'X'  !VDI / ER
    when = '8'  !OASDI / ER Exempt
    when = '9'  !OASDI / ER Tips Exempt
      if #DisRIndex = 0
        add 1 to #OtherTaxCount
        move #OtherTaxCount to #DisRIndex
        put 'OASDR' &FD.TxGrs_Cur 0 &FD.Tax_Cur
           into OtherTaxes(#DisRIndex)
                TaxType TxGrs_Cur Tax_Cur ER_Match
      else
          array-add &FD.TxGrs_Cur 0 &FD.Tax_Cur
            to OtherTaxes(#DisRIndex)
                TxGrs_Cur Tax_Cur ER_Match
      end-if
  end-evaluate
end-procedure

begin-procedure Company-Change
  move 2 to #changeLevel
  do Print-Totals
end-procedure

begin-procedure PayEndDate-Change
  move 3 to #changeLevel
  do Print-Totals
end-procedure

begin-procedure CheckDate-Change
  move 4 to #changeLevel
  do Print-Totals
end-procedure

begin-procedure State-Change
  if #current-line > 56  !ensure enough lines for the tax authority
    new-page
  end-if
  if $State <> $priorState
    move $State to $priorState
    do Get-State-Tax-Data
  end-if
  if $State = '$U'  !if U.S. Federal
    print 'U.S. Federal'     (1,1)
    print &CT.Federal_EIN    (2,1)  edit xx-xxxxxxx
  else
    print $StateName         (1,1)
    print &Employer_ID_SWT   (2,1)
  end-if
  do Format-Number(#WithholdingGross, $out, 'B99,999,999.99mi')
  print $out        (1,21)
  do Format-Number(#WithholdingTax, $out, 'B9,999,999.99mi')
  print $out        (0,36)
  add #WithholdingTax to #TotalLiability
  if #EIC <> 0
    print '(EIC)'   (2,27)
    do Format-Number(#EIC, $out, 'B9,999,999.99mi')
    print $out      (0,36)
    add #EIC to #TotalLiability
  end-if
  move 1 to #i
  while #i <= #OtherTaxCount
    get $TaxType #TxGrs_Cur #Tax_Cur #ER_Match
      from OtherTaxes(#i)
          TaxType TxGrs_Cur Tax_Cur ER_Match
    if #TxGrs_Cur <> 0 or #Tax_Cur <> 0
      print $TaxType     (#i,51)
      do Format-Number(#TxGrs_Cur, $out, 'B99,999,999.99mi')
      print $out         (0,56)
      do Format-Number(#Tax_Cur, $out, 'B9,999,999.99mi')
      print $out         (0,71)
      do Format-Number(#ER_Match, $out, 'B9,999,999.99mi')
      print $out         (0,85)
      array-add #Tax_Cur #ER_Match to Totals(4) total(1) total(2)
      add #Tax_Cur to #TotalLiability
      add #ER_Match to #TotalLiability
    end-if
    add 1 to #i
  end-while
  do Format-Number(#TotalLiability, $out, 'B9,999,999.99mi')
  print $out           (1,105)
  array-add #WithholdingTax #TotalLiability
            to Totals(4) total(0) total(3)
  move 0 to #TotalLiability
  move 0 to #WithholdingGross
  move 0 to #WithholdingTax
  move 0 to #EIC
  move 0 to #OtherTaxCount
  move 0 to #MediEIndex
  move 0 to #MediRIndex
  move 0 to #DisEIndex
  move 0 to #DisRIndex
  move 0 to #OtherTaxIndex
  move 0 to #AddlMediEIndex
  next-listing need=3 skiplines=1
end-procedure

begin-procedure Print-Totals
  do State-Change
  move 4 to #totalRow
  while #totalRow >= #changeLevel
    move #totalRow to #rollRow
    subtract 1 from #rollRow
    get $TotalName from Totals(#totalRow) TotalName
    print $TotalName (+2,11)
    move 0 to #i
    while #i <= 3
      get #total from Totals(#totalRow) total(#i)
      get #startCol from Totals(0) startCol(#i)
      do Format-Number(#total, $out, 'B9,999,999.99mi')
      print $out (0,#startCol)
      array-add #total to Totals(#rollRow) total(#i)
      put 0 into Totals(#totalRow) total(#i)
      add 1 to #i
    end-while
    subtract 1 from #totalRow
  end-while
  if $End = 'Y'
    do Reset
  end-if
  new-page
end-procedure

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'payinit.sqc'   !Report Initialization and Timing
#Include 'reset1.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Process Scheduler Run Control
