! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
!  CurDtTim:  Get current system date and time                         *
!             Returns the current "native" format date-time in         *
!                $_SysDateTime and &_SysDateTime                         *
!             Returns the current "native" format date in $_AsOfToday   *
!             Returns the current "native" format time in $_AsOfNow     *
!             Returns the current report format date in $_ReportDate    *
!             Returns the current report format time in $_ReportTime    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************

!This guards against the SQC being #included twice...
#ifndef CURDTTIM-INCLUDED

#define CURDTTIM-INCLUDED

begin-procedure Get-Current-DateTime LOCAL

    declare-variable
        date    $tempDate
    end-declare

#ifdef debugx
    show 'Entering CURDTTIM.SQC: Get-Current-DateTime'
#endif

#ifdef INFORMIX
begin-SELECT
CURRENT YEAR TO FRACTION(3) &_SysDateTime
FROM PSCLOCK
end-SELECT
    let $tempDate = &_SysDateTime

#else

    #ifdef SYBASE
begin-SELECT
{DateTimeOut-Prefix}GETDATE(){DateTimeOut-Suffix} &_SysDateTime
end-SELECT
        let $tempDate = &_SysDateTime

    #else

        #ifdef MICROSOFT
begin-SELECT
{DateTimeOut-Prefix}GETDATE(){DateTimeOut-Suffix} &_SysDateTime
FROM PSCLOCK
end-SELECT
            let $tempDate = &_SysDateTime

        #else

            #ifdef DB2ALL
begin-SELECT
CURRENT TIMESTAMP &_SysDateTime
FROM PSCLOCK
end-SELECT
                let $tempDate = &_SysDateTime

            #else

                #ifdef ORACLE
begin-SELECT
{DateTimeOut-Prefix}SYSDATE{DateTimeOut-Suffix} &_SysDateTime
FROM PSCLOCK
end-SELECT
                    let $tempDate = strtodate(&_SysDateTime,{Native-DateTimeMask})

                #else                           !All Others

                    date-time () {Native-DateTime} &_SysDateTime
                    let $tempDate = strtodate(&_SysDateTime,{Native-DateTimeMask})

                #endif                          ! ORACLE
            #endif                              ! DB2ALL
        #endif                                  ! MICROSOFT
    #endif                                      ! SYBASE
#endif                                          ! INFORMIX



! Format the Global Variables
let $_SysDateTime    = datetostr($tempDate,{Native-DateTimeMask})
let $_AsOfToday      = datetostr($tempDate,{Native-DateMask})
let $_AsOfNow        = datetostr($tempDate,{Native-TimeMask})
let $_CurrentCentury = datetostr($tempDate,'CC')

! Set ReportDate/Time
let $_ReportDate     = datetostr($tempDate,{PTRPTDATE})
let $_ReportTime     = datetostr($tempDate,{PTRPTTIME})

#ifdef debugx
  show 'Exiting CURDTTIM.SQC: Get-Current-DateTime'
  show ' Output $AsOfToday  : ' $_AsOfToday
  show ' Output $AsOfNow    : ' $_AsOfNow
  show ' Output $ReportDate : ' $_ReportDate
  show ' Output $ReportTime : ' $_ReportTime
  show ' Output $SysDateTime: ' $_SysDateTime
#endif
end-procedure

#endif                                  ! CURDTTIM-INCLUDED
