!***********************************************************************
!  RotName8:  Convert from "First MidIn Last Suffix" to                *
!             "First MidIn Last" Remove Title and Suffix from Name     *
!                                                                      *
!             Note: This procedure will only find a member of the      *
!             comma-delimited suffixes defined below in the variable   *
!             $SuffixString.  Additional suffixes may be added to the  *
!             variable as necessary.  If two or more suffixes, e.g.    *
!             'JR.' and 'JR', in which the second is a substring of    *
!             the first are included, the longest suffix must be the   *
!             first in the list.                                       *
!                                                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:28:45                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Rotate-Name8
  move '' to $Suffix
  move 'N' to $SuffixFound
  uppercase $Name

  let $SuffixString =
    'JR.,JR,SR.,SR,III,II,I,IV,MD,M.D.,CPA,C.P.A.,ESQ.,ESQ,' ||
    '2ND.,2ND,SECOND,3RD.,3RD,THIRD,4TH.,4TH,FOURTH,PHD,PHD.,P.H.D.'

  let $Name = rtrim($Name,' ')
  let $Sufsw = 'N'
  Unstring $Name by ' ' into $FirstN $MidI $LastN $Suf1 $Suf2

  if $Suf2 <> ''
     let $Suftest = $Suf2
     let $Sufsw = 'Y'
     do Test-Suffix
     let $Suf2 = $Suftest
  end-if

  if $Suf1 <> '' and $Sufsw = 'N'
     let $Suftest = $Suf1
     let $Sufsw = 'Y'
     do Test-Suffix
     let $Suf1 = $Suftest
  end-if

  if $LastN <> '' and $Sufsw = 'N'
     let $Suftest = $LastN
     let $Sufsw = 'Y'
     do Test-Suffix
     let $LastN = $Suftest
  end-if

  String $FirstN $MidI $LastN $Suf1 $Suf2 by ' ' into $Name
end-procedure

begin-procedure Test-Suffix
  let #i = 1
  while INSTR($SuffixString,',',#i) > 0
    let #j = INSTR($SuffixString,',',#i)
    let $Suffix = {ps-substr}($SuffixString,#i,#j - #i)
    let #locnsuffix = INSTR($SufTest, $Suffix, 1)

    if #locnsuffix > 0
      if $SufTest = $Suffix
         move ' ' to $SufTest
         move 'Y' to $SuffixFound
      end-if
    end-if

    let #i = #j + 1
    if $SuffixFound = 'Y'
       break
    end-if
  end-while
end-procedure
