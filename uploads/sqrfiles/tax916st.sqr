!***********************************************************************
!  TAX916ST:  State W-2 Tax Totals Report                              *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2010, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2010/08/23:11:46:43                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  813279                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup31.sqc' !Printer and page-size initialization
#include 'usarpt.sqc'  !Get USA variables

begin-report
  do Init-Report
  do Process-Main
  do Stdapi-Term
end-report

begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Initialization
  !do Create-Suffix-Array

  move 'TAX916ST' to $ReportID
  move 'State W-2 Tax Totals Report' to $ReportTitle
  display 'State W-2 Tax Totals Report'

  if $EEDetailFlag = 'Y'
    display ''
    move 'State W-2 Tax Totals Detail Report' to $ReportTitle
    display 'State W-2 EE detail records will be printed'
    display 'instead of the State Summary record'
  else
    move 'State W-2 Tax Totals Summary Report' to $ReportTitle
    display ''
    display 'State W-2 summary records will be printed'
    display 'instead of EE detail records'
  end-if


end-procedure

begin-procedure Initialization
  do Get-Current-DateTime
  move '1' to $MMLZero
  move '1' to $Year4

  move 0 to #Count-company
  move 0 to #17-company
  move 0 to #18-company
  move 0 to #19-company
  move 0 to #Count-co-grand
  move 0 to #17-co-grand
  move 0 to #18-co-grand
  move 0 to #19-co-grand

  do Format-DateTime($AsOfToday, $910Date, {DEFYMD},'','')

  move $910Date to $AsOfYear xxxx   !extract the current year
  move $AsOfYear  to #AsOfYear
  move #AsOfYear  to #saveAsOfYear
  subtract 1    from #AsOfYear
  do Get-Tax-Reporting-Run-Controls
  do Select-US-Year-End-Parameters
  do assign-year-end-variables
  do Convert-Parameters


  move &TX.Balance_Year to $CalYear 9999
  let $AsOfDate = $CalYear || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP}, '', 'native')

  if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''
    display ''
    display 'Current Year is not one greater than Tax Reporting Year.'
    display 'Current Year is    ' noline
    display  #saveAsOfYear  9999
    display 'Tax Reporting Year ' noline
    display &TX.Balance_Year
    input $Answer maxlen=1 'Do you want to continue? (Y/N)'
    uppercase $Answer
    if $Answer <> 'Y'
      stop
    end-if
  end-if


begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

end-procedure

begin-heading 7
  #Include 'stdhdg01.sqc'

  if $EEDetailFlag = 'Y'
    print 'W2 Rpt Company '        (3,1)
    print $A.W2_Reporting_Co       (0,+2)
    print $A.Descr                 (0,+2)
    let $TaxYear = 'Tax Year ' || $CalYear
    print $TaxYear                 (3,)    center
    print 'Federal EIN '           (4,1)
    print $A.Federal_EIN           (,+2)   edit xx-xxxxxxx
    print 'State:'                 (5,1)
    print $State                   (,+1)
    print ' '                      (+2,31)
    print 'SSN'                    (+1,27)
    print 'Name'                   (,45)
    print 'Total State Wages'      (,67)
    print 'Total State Income Tax' (,87)
    print 'Total State EIC'        (,112)
  else
    print 'W2 Rpt Company '        (3,1)
    print $A.W2_Reporting_Co       (0,+2)
    print $A.Descr                 (0,+2)
    let $TaxYear = 'Tax Year ' || $CalYear
    print $TaxYear                 (3,)    center
    print 'Federal EIN '           (4,1)
    print $A.Federal_EIN           (,+2)   edit xx-xxxxxxx
    print 'Number of'              (+2,27)
    print 'W-2 Statements'         (+1,23)
    print 'Total State Wages'      (,42)
    print 'Total State Income Tax' (,62)
    print 'Total State EIC'        (,87)
  end-if
end-heading

begin-procedure Process-Main
begin-SELECT
A.COMPANY
A.DESCR
A.FEDERAL_EIN
A.W2_REPORTING_CO

  move &A.Federal_EIN     to $A.Federal_EIN  099999999
  move &A.Descr           to $A.Descr
  move &A.W2_Reporting_Co to $A.W2_Reporting_Co

  if $EEDetailFlag = 'Y'
    do Get-EE-Details
  else
    do Get-State-Count
  end-if

FROM  PS_W2_COMPANY A
WHERE A.CALENDAR_YEAR    = &TX.Balance_Year
  AND A.W2_REPORTING_CO  = A.COMPANY
  AND A.W2_REPORTING_CO <> ' '
  AND A.TAXFORM_ID       = 'W'
ORDER BY A.W2_REPORTING_CO
end-SELECT

  if $EEDetailFlag = 'Y'

    print 'Number of'              (+3,47)

    print 'W-2 Statements'         (+1,42)
    print 'Total State Wages'      (,67)
    print 'Total State Income Tax' (,87)
    print 'Total State EIC'        (,112)

    print 'Grand Totals:'   (+2,1)


    do Format-Number(#Count-co-grand, $out, 'B99,999')               !Count
    print $out  (,49)

    do Format-Number(#17-co-grand, $out, 'B9999999999.99')        !State wages
    print $out  (,70)

    do Format-Number(#18-co-grand, $out, 'B9999999999.99')        !State tax
    print $out  (,95)

    do Format-Number(#19-co-grand, $out, 'B9999999999.99')        !State EIC
    print $out  (,113)

  else
     print 'Grand Totals:'   (+2,1)

     do Format-Number(#Count-co-grand, $out, 'B99,999')       !Total Count
     print $out  (,30)

     do Format-Number(#17-co-grand, $out, 'B9999999999.99')           !State wages
     print $out  (,45)

     do Format-Number(#18-co-grand, $out, 'B9999999999.99')           !State tax
     print $out  (,70)

     do Format-Number(#19-co-grand, $out, 'B9999999999.99')           !State EIC
     print $out  (,88)
  end-if
end-procedure

begin-procedure Get-State-Count
begin-SELECT DISTINCT
C1.W2_REPORTING_CO   () on-break print=never after=Print-Co-totals
F1.W2_STATE          () on-break print=never after=Print-Amounts
F1.EMPLID
F1.TAXFORM_ID

   add 1 to #Count
   move &F1.TaxForm_ID to $TAXFORM_ID
   move &F1.W2_State to $State

FROM  PS_YE_AMOUNTS D1, PS_YE_EE E1, PS_YE_DATA F1, PS_W2_COMPANY C1
WHERE C1.W2_REPORTING_CO = &A.W2_Reporting_Co
  AND D1.COMPANY         = C1.COMPANY
  AND D1.CALENDAR_YEAR   = C1.CALENDAR_YEAR
  !AND D1.TAXFORM_ID      = C1.TAXFORM_ID
  AND D1.COMPANY         = E1.COMPANY
  AND D1.EMPLID          = E1.EMPLID
  AND D1.TAXFORM_ID      = E1.TAXFORM_ID
  AND D1.CALENDAR_YEAR   = E1.CALENDAR_YEAR
  AND D1.COMPANY         = F1.COMPANY
  AND D1.EMPLID          = F1.EMPLID
  AND D1.TAXFORM_ID      = F1.TAXFORM_ID
  AND D1.CALENDAR_YEAR   = F1.CALENDAR_YEAR
  AND D1.SEQUENCE_NUMBER = F1.SEQUENCE_NUMBER
  AND D1.CALENDAR_YEAR   = &TX.Balance_Year
  AND D1.STATE           <> ' '
  AND (   (D1.BOX            IN ('08','14')
           AND D1.TAXFORM_ID      = 'P')
       OR (D1.BOX            IN ($YE_Fed_Wages, $YE_Fed_Tax)
           AND D1.TAXFORM_ID     IN ('I','M','S'))
       OR (D1.BOX            IN ($YE_State_Wages,$YE_State_Tax, '14I')
           AND D1.TAXFORM_ID      = 'W')
       OR (D1.BOX            = $YE_Local_Tax
           AND D1.TAXFORM_ID = 'W'
           AND D1.LOCALITY   = '99990'))
  AND E1.PROCESS_FLAG   <> 'V'
ORDER BY C1.W2_REPORTING_CO,F1.W2_STATE,F1.TAXFORM_ID,F1.EMPLID
end-SELECT
end-procedure

begin-procedure Print-Amounts
  do Get-State-Totals
  move &A.W2_Reporting_Co to $Company
  do Get-State-Tax-Data

  print $State     (+1,1)
  print $StateEIN  (,6)

  do Format-Number(#Count, $out, 'B99,999')               !Count
  print $out  (,30)

  do Format-Number(#17, $out, 'B9999999999.99')           !State wages
  print $out  (,45)

  do Format-Number(#18, $out, 'B9999999999.99')           !State tax
  print $out  (,70)

  do Format-Number(#19, $out, 'B9999999999.99')           !State EIC
  print $out  (,88)

  add #Count to #Count-company
  add #17 to #17-company
  add #18 to #18-company
  add #19 to #19-company

  move 0 to #17
  move 0 to #18
  move 0 to #19
  move 0 to #Count
  move '' to $StateEIN
end-procedure

begin-procedure Get-State-Totals
begin-SELECT
C.W2_REPORTING_CO
D.STATE
D.EMPLID
D.BOX
D.TAXFORM_ID

#ifdef DB2ALL
DECIMAL(SUM(D.W2_AMOUNT),15,3) &D.W2_Amount
#else
SUM(D.W2_AMOUNT) &D.W2_Amount
#endif
   move &D.W2_Amount  to #Amount
   move &D.TaxForm_ID to $TAXFORM_ID
   let $D.Box = RTRIM(&D.Box,' ')

   do Compute-Territory-State-Wages

FROM  PS_YE_AMOUNTS D, PS_YE_EE E, PS_W2_COMPANY C
WHERE C.W2_REPORTING_CO = &A.W2_Reporting_Co
  AND D.COMPANY         = C.COMPANY
  AND D.CALENDAR_YEAR   = C.CALENDAR_YEAR
  AND D.CALENDAR_YEAR   = &TX.Balance_Year
  AND D.STATE           = $State
  AND D.COMPANY         = E.COMPANY
  AND D.EMPLID          = E.EMPLID
  AND D.TAXFORM_ID      = E.TAXFORM_ID
  AND (   (D.BOX            IN ('08','14')
           AND D.TAXFORM_ID  = 'P')
       OR (D.BOX            IN ($YE_Fed_Wages, $YE_Fed_Tax)
           AND D.TAXFORM_ID IN ('I','M','S'))
       OR (D.BOX            IN ($YE_State_Wages, $YE_State_Tax, '14I')
           AND D.TAXFORM_ID  = 'W')
       OR (D.BOX            = $YE_Local_Tax
           AND D.TAXFORM_ID = 'W'
           AND D.LOCALITY   = '99990'))
  AND D.CALENDAR_YEAR   = E.CALENDAR_YEAR
  AND E.PROCESS_FLAG   <> 'V'
GROUP BY C.W2_REPORTING_CO,D.STATE,D.TAXFORM_ID,D.EMPLID,D.BOX
ORDER BY C.W2_REPORTING_CO,D.STATE,D.TAXFORM_ID,D.EMPLID,D.BOX
end-SELECT
end-procedure

begin-procedure Compute-Territory-State-Wages

 do assign-year-end-variables
     if $TaxForm_ID = 'W'

         evaluate $D.Box
           when = $YE_State_Wages                             !State wages
             add #Amount to #17
             break
           when = $YE_State_Tax                               !State tax
             add #Amount to #18
             break
           when = $YE_Local_Tax                               !State EIC for IN
           when = '14I'                                       !State EIC for WI
             add #Amount to #19
             break
          end-evaluate
     else
       if $TaxForm_ID = 'P'
          evaluate $D.Box
              when = $YE_Wages                                !Puerto Rico wages
                add #Amount to #17
                break
              when = $YE_Tax_Withheld                         !Puerto Rico tax
                add #Amount to #18
                break
          end-evaluate
       else
          evaluate $D.Box
            when = $YE_Fed_Wages                              !Territory wages
              add #Amount to #17
              break
            when = $YE_Fed_Tax                                !Territory tax
              add #Amount to #18
            break
          end-evaluate
       end-if
     end-if
end-procedure

begin-procedure Convert-Parameters
  move $RC_US_YE.LOCTX_EEDT_FLAG   to $EEDetailFlag
end-procedure

begin-procedure Get-EE-Details
begin-SELECT DISTINCT
CC.W2_REPORTING_CO () on-break print=never level=1 after=Print-Co-totals
DD.STATE   () on-break print=never level=2 after=Print-State-Summary
DD.EMPLID  () on-break print=never level=3 after=After-EE
DD.BOX
DD.W2_AMOUNT
DD.TAXFORM_ID
FF.YE_LNAME
FF.YE_MIDDLE_NAME
FF.YE_FNAME
FF.YE_SUFFIX
FF.SSN


   if &DD.State <> $State
      move &DD.State     to $State
      do State-Break
   end-if

   move &DD.W2_Amount      to #Amount
   move &FF.YE_FName       to $W2_FName
   move &FF.YE_Middle_Name to $W2_MName
   move &FF.YE_LName       to $W2_LName
   move &FF.YE_Suffix      to $W2_Suffix
   move &FF.SSN            to $W2_SSN
   move &DD.TaxForm_ID     to $TAXFORM_ID


   let $D.Box = RTRIM(&DD.Box,' ')

   do Compute-Territory-State-Wages


FROM  PS_YE_AMOUNTS DD, PS_YE_EE EE, PS_YE_DATA FF, PS_W2_COMPANY CC
WHERE CC.W2_REPORTING_CO = &A.W2_Reporting_Co
  AND DD.COMPANY         = CC.COMPANY
  AND DD.CALENDAR_YEAR   = CC.CALENDAR_YEAR
  !AND DD.TAXFORM_ID      = CC.TAXFORM_ID
  AND DD.COMPANY         = EE.COMPANY
  AND DD.EMPLID          = EE.EMPLID
  AND DD.TAXFORM_ID      = EE.TAXFORM_ID
  AND DD.CALENDAR_YEAR   = EE.CALENDAR_YEAR
  AND DD.COMPANY         = FF.COMPANY
  AND DD.EMPLID          = FF.EMPLID
  AND DD.TAXFORM_ID      = FF.TAXFORM_ID
  AND DD.CALENDAR_YEAR   = FF.CALENDAR_YEAR
  AND DD.SEQUENCE_NUMBER = FF.SEQUENCE_NUMBER
  AND DD.CALENDAR_YEAR   = &TX.Balance_Year
  AND DD.STATE           <> ' '
  AND (   (DD.BOX            IN ('08','14')
           AND DD.TAXFORM_ID     = 'P')
       OR (DD.BOX            IN ($YE_Fed_Wages,$YE_Fed_Tax)
           AND DD.TAXFORM_ID    IN ('I','M','S'))
       OR (DD.BOX            IN ($YE_State_Wages, $YE_State_Tax, '14I')
           AND DD.TAXFORM_ID     = 'W')
       OR (DD.BOX            = $YE_Local_Tax
           AND DD.TAXFORM_ID = 'W'
           AND DD.LOCALITY   = '99990'))
  AND EE.PROCESS_FLAG   <> 'V'
ORDER BY CC.W2_REPORTING_CO,DD.STATE,DD.TAXFORM_ID,FF.SSN
end-SELECT
end-procedure

begin-procedure State-Break
  move &A.W2_Reporting_Co to $Company
  do Get-State-Tax-Data
end-procedure

begin-procedure Print-Co-totals

   if $EEDetailFlag = 'Y'


     print 'Number of'              (+3,47)

    print 'W-2 Statements'         (+1,42)
    print 'Total State Wages'      (,67)
    print 'Total State Income Tax' (,87)
    print 'Total State EIC'        (,112)

    print 'W2 Rpt Company Totals:'   (+2,1)


    do Format-Number(#Count-company, $out, 'B99,999')               !Count
    print $out  (,49)

    do Format-Number(#17-company, $out, 'B9999999999.99')        !State wages
    print $out  (,70)

    do Format-Number(#18-company, $out, 'B9999999999.99')        !State tax
    print $out  (,95)

    do Format-Number(#19-company, $out, 'B9999999999.99')        !State EIC
    print $out  (,113)

  else

     print 'W2 Rpt Company Totals:'   (+2,1)

     do Format-Number(#Count-company, $out, 'B99,999')       !Total Count
     print $out  (,30)

     do Format-Number(#17-company, $out, 'B9999999999.99')           !State wages
     print $out  (,45)

     do Format-Number(#18-company, $out, 'B9999999999.99')           !State tax
     print $out  (,70)

     do Format-Number(#19-company, $out, 'B9999999999.99')           !State EIC
     print $out  (,88)

   end-if

   add #Count-company to #Count-co-grand
   add #17-company to #17-co-grand
   add #18-company to #18-co-grand
   add #19-company to #19-co-grand

   move 0 to #Count-company
   move 0 to #17-company
   move 0 to #18-company
   move 0 to #19-company
   new-page
end-procedure

begin-procedure After-EE
   let $SSN = ' '
   move $W2_SSN to $S123 xxx
   if $S123 = &Temp_SSN_Mask or RTRIM($W2_SSN, ' ') = ''
     move 'Unavailable' to $W2_SSN
   else
     move $W2_SSN to $SSN xxx-xx-xxxx
   end-if

   let $W2_FName  = rtrim($W2_FName,' ') ||' '
   let $W2_MName  = rtrim($W2_MName,' ') ||' '
   let $W2_LName  = rtrim($W2_LName,' ') ||' '
   let $W2_Suffix = rtrim($W2_Suffix,' ') ||' '

   if $W2_MName <> ' '
      let $YE_Whole_Name = $W2_FName || $W2_MName || $W2_LName || $W2_Suffix
   else
      let $YE_Whole_Name = $W2_FName || $W2_LName || $W2_Suffix
   end-if

   print $SSN             (+1,23)
   print $YE_Whole_Name   (,45,22)
   do Format-Number(#17, $out, 'B9999999999.99')           !State wages
   print $out  (,70)
   do Format-Number(#18, $out, 'B9999999999.99')           !State tax
   print $out  (,95)
   do Format-Number(#19, $out, 'B9999999999.99')           !State EIC
   print $out  (,113)

   let #Dtl_Count = #Dtl_Count + 1
   let #Dtl_Amt17 = #Dtl_Amt17 + #17
   let #Dtl_Amt18 = #Dtl_Amt18 + #18
   let #Dtl_Amt19 = #Dtl_Amt19 + #19

   let #Amount = 0
   let #17 = 0
   let #18 = 0
   let #19 = 0
end-procedure

begin-procedure Print-State-Summary
  let $Total_Label = RTRIM($State, ' ') || ' Totals:'

  print 'Number of'              (+3,47)

  print 'W-2 Statements'         (+1,42)
  print 'Total State Wages'      (,67)
  print 'Total State Income Tax' (,87)
  print 'Total State EIC'        (,112)

  print $Total_Label             (+2,10,19)

  do Format-Number(#Dtl_Count, $out, 'B99,999')               !Count
  print $out  (,49)

  do Format-Number(#Dtl_Amt17, $out, 'B9999999999.99')        !State wages
  print $out  (,70)

  do Format-Number(#Dtl_Amt18, $out, 'B9999999999.99')        !State tax
  print $out  (,95)

  do Format-Number(#Dtl_Amt19, $out, 'B9999999999.99')        !State EIC
  print $out  (,113)

  add #Dtl_Count to #Count-company
  add #Dtl_Amt17 to #17-company
  add #Dtl_Amt18 to #18-company
  add #Dtl_Amt19 to #19-company

  move 0 to #Dtl_Count
  move 0 to #Dtl_Amt17
  move 0 to #Dtl_Amt18
  move 0 to #Dtl_Amt19
  new-page
end-procedure

#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'txrnctl2.sqc'  !Get-Year-End-Tax-Reporting-Run-Controls procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'w2.sqc'        !Translation of W2 Boxes

