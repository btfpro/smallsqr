!***********************************************************************
!  TAX704: Print contents of Company State Tax Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2009 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2009/02/25:09:27:39                        
!       $Release:  HR9                                        
!    $Resolution:  766648                                     
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
! HP00001       Add $PublicSector indicator                            *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'TAX704' to $ReportID
  move 'Company State Tax Table' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Init-Number
  do Get-EandG                                                          !HP00001
  do Get-Current-DateTime
  do Stdapi-Init
end-procedure

begin-HEADING 8
  #Include 'stdhdg02.sqc'

  print ' '                             (+2,1,5)
  print 'Eff'                           (0,7)
  print '<-Exempt->'                    (0,18)
  print '              '                (0,29)
  print '    Experience'                (0,52)
  print 'Effective'                     (0,68)
  print ' '                             (0,78)
  print 'Branch'                        (0,97)
  print 'Non Res'                       (0,104)
  print 'MA Rate'                       (0,113)
  print 'WA UBI'                        (0,124)
  print 'VT Hlth'                       (0,134)
  print 'VDI/FLI Information'           (0,144)


  print 'State'                         (+1,1,5)
  print 'Date'                          (0,7)
  print 'SUT   SDI '                    (0,18)
  print 'Employer ID''s'                (0,29)
  print '       Rate  '                 (0,52)
  print ' Yr   Qtr'                     (0,68)
  print 'GL Accounts'                   (0,78)
  print 'Code'                          (0,98)
  print 'Reqd   '                       (0,104)
  print 'Override/'                     (0,113)
  print 'Number/'                       (0,124)
  print 'Care'                          (0,134)
  print 'Plan Number'                   (0,144)
  print '   Eff Yr'                     (0,155)
  print 'EE Rate'                       (0,167)

  print 'Hlth Rate'                     (+1,113)
  print 'EAF Rate'                      (0,124)
  print 'Covg'                          (0,134)
  print 'Admin Code'                    (0,144)
  print '   Eff Qtr'                    (0,155)
  print 'ER Rate'                       (0,167)

  PRINT ' '                             (+1,1)
end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

COMPANY                    () on-break print=never before=Before-Company
STATE                      (1,1,2)
EFFDT
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out               (0,7)

EFF_STATUS
  if &Eff_Status = 'I'
    print 'I'              (0,0,1)
  else
    print ' '              (0,0,1)
  end-if
EXEMPT_SUT                 (0,19)
EXEMPT_DIS                 (0,25)
EMPLOYER_ID_SWT
EMPLOYER_ID_SUT
EMPLOYER_ID_DIS                                                         !HP99999
  move 1 to #line
  if RTRIM(&Employer_ID_SWT,' ') <> ''
    print 'SWT '           (#line,29)
    print &Employer_ID_SWT (#line,0,20)
    add 1 to #line
  end-if
  if RTRIM(&Employer_ID_SUT,' ') <> ''
    print 'SUT '           (#line,29)
    print &Employer_ID_SUT (#line,0,20)
    if $PublicSector = 'Y'                                              !HP00001
      add 1 to #line                                                    !HP99999
    end-if                                                              !HP00001
  end-if
  if RTRIM(&Employer_ID_DIS,' ') <> ''                                  !HP99999
     and $PublicSector = 'Y'                                            !HP00001
    print 'DIS '           (#line,29)                                   !HP99999
    print &Employer_ID_DIS (#line,0,20)                                 !HP99999
  end-if                                                                !HP99999
SHI_OVERRIDE
SHI_RATE
UBI_NUMBER
EAF_RATE
HEALTH_CARE_COV
  if &SHI_OVERRIDE = 'Y'
    print &SHI_OVERRIDE    (1,115)
  end-if
  if &SHI_RATE <> 0
    do Format-Number(&SHI_RATE, $out, '9.999999')
    print $out             (2,114)
  end-if
  if &UBI_NUMBER <> 0
    do Format-Number(&UBI_NUMBER, $out, '999999999999')
    print $out      (1,124)
  end-if
  if &EAF_RATE <> 0
    do Format-Number(&EAF_RATE, $out, '99.99999')
    print $out             (2,124)
  end-if
  if &HEALTH_CARE_COV = 'Y'
    print &HEALTH_CARE_COV    (1,135)
  end-if
SUT_EXPERIENCE_RT
SUT_EFF_TAX_YR
SUT_EFF_TAX_QTR
DIS_EXPERIENCE_RT
DIS_EFF_TAX_YR
DIS_EFF_TAX_QTR
  move 1 to #line
  if &SUT_Eff_Tax_Yr <> 0
    print 'SUT  '          (#line,54)
    do Format-Number(&SUT_Experience_Rt, $out, '99.99999')
    print $out             (#line,+1)
    print &SUT_Eff_Tax_Yr  (#line,+1)
    print &SUT_Eff_Tax_Qtr (#line,+1)
    add 1 to #line
  end-if
  if &Dis_Eff_Tax_Yr <> 0
    print 'SDI  '          (#line,54)
    do Format-Number(&Dis_Experience_Rt, $out, '99.99999')
    print $out             (#line,+1)
    print &Dis_Eff_Tax_Yr  (#line,+1)
    print &Dis_Eff_Tax_Qtr (#line,+1)
  end-if
BRANCH_CD
  if &BRANCH_CD <> 0
    print &BRANCH_CD (1,99)
  end-if
GL_SWT
GL_SUT
GL_SDI
GL_SUT_ER
GL_SDI_ER
GL_VDI_EE
GL_VDI_ER
GL_ST_EIC
VDI_PLAN           !        (1,130)
VDI_PLAN_NBR       !        (1,136)
VDI_ADMIN_CODE     !        (2,136)
VDI_EFF_TAX_YR     !        (1,150)
VDI_EFF_TAX_QTR    !        (2,150)
VDI_EE_RT
VDI_ER_RT
PNA_FLI_PLAN
PNA_FLI_PLAN_NBR
PNA_FLI_ADMIN_CODE
PNA_FLI_EFF_TAX_YR
PNA_FLI_EFF_TX_QTR
PNA_FLI_EE_RT
PNA_FLI_ER_RT
PNA_GL_FLI_EE
PNA_GL_VFLI_EE
PNA_GL_VFLI_ER
  move 1 to #line
  if RTRIM(&GL_SWT,' ') <> ''
    print 'SWT  '          (#line,78)
    print &GL_SWT         (#line,+1)
    add 1 to #line
  end-if
  if RTRIM(&GL_SUT,' ') <> ''
    print 'SUT  '          (#line,78)
    print &GL_SUT         (#line,+1)
    add 1 to #line
  end-if
  if RTRIM(&GL_SDI,' ') <> ''
    print 'SDI  '          (#line,78)
    print &GL_SDI         (#line,+1)
    add 1 to #line
  end-if
  if RTRIM(&GL_SDI_ER,' ') <> ''
    print 'SDIER'          (#line,78)
    print &GL_SDI_ER      (#line,+1)
    add 1 to #line
  end-if
  if RTRIM(&GL_SUT_ER,' ') <> ''
    print 'SUTER'          (#line,78)
    print &GL_SUT_ER      (#line,+1)
    add 1 to #line
  end-if

  if &VDI_PLAN = 'N'
    print &VDI_PLAN_NBR               (1,144)
    print &VDI_ADMIN_CODE             (2,144)
    if &VDI_EFF_TAX_YR <> 0
      print &VDI_EFF_TAX_YR             (1,158)
      print &VDI_EFF_TAX_QTR            (2,158)
    end-if
    if &VDI_EE_RT <> 0
      do Format-Number(&VDI_EE_RT, $out, '99.999')
      print $out             (1,167)
    end-if

    if &VDI_ER_RT <> 0
      do Format-Number(&VDI_ER_RT, $out, '99.999')
      print $out             (2,167)
    end-if
    if RTRIM(&GL_VDI_EE,' ') <> ''
      print 'VDI EE'          (#line,78)
      print &GL_VDI_EE       (#line,+1)
      add 1 to #line
    end-if

    if RTRIM(&GL_VDI_ER,' ') <> ''
      print 'VDI ER'          (#line,78)
      print &GL_VDI_ER       (#line,+1)
      add 1 to #line
    end-if

  else
      if RTRIM(&GL_VDI_EE,' ') <> ''
        print 'VDI EE'          (#line,78)
        print &GL_VDI_EE        (#line,+1)
        print &VDI_PLAN_NBR     (#line,144)
      end-if
      if &VDI_EFF_TAX_YR <> 0
        print &VDI_EFF_TAX_YR   (#line,158)
      end-if
      if &VDI_EE_RT <> 0
        do Format-Number(&VDI_EE_RT, $out, '99.999')
        print $out            (#line,167)
      end-if
      add 1 to #line

      if RTRIM(&GL_VDI_ER,' ') <> ''
        print 'VDI ER'          (#line,78)
        print &GL_VDI_ER        (#line,+1)
      end-if
      print &VDI_ADMIN_CODE   (#line,144)
      if &VDI_EFF_TAX_YR <> 0
        print &VDI_EFF_TAX_QTR  (#line,158)
      end-if
      if &VDI_ER_RT <> 0
        do Format-Number(&VDI_ER_RT, $out, '99.999')
        print $out            (#line,167)
      end-if
      add 1 to #line
  end-if

  move #line to #line2
  add  1 to #line2
  print &PNA_FLI_PLAN_NBR               (#line,144)
  print &PNA_FLI_ADMIN_CODE             (#line2,144)
  if &PNA_FLI_EFF_TAX_YR <> 0
    print &PNA_FLI_EFF_TAX_YR             (#line,158)
    print &PNA_FLI_EFF_TX_QTR             (#line2,158)
  end-if

  if &PNA_FLI_EE_RT <> 0
    do Format-Number(&PNA_FLI_EE_RT, $out, '99.999')
    print $out             (#line,167)
  end-if

  if &PNA_FLI_ER_RT <> 0
    do Format-Number(&PNA_FLI_ER_RT, $out, '99.999')
    print $out             (#line2,167)
  end-if

  if RTRIM(&PNA_GL_FLI_EE,' ') <> ''
    print 'FLI EE'          (#line,78)
    print &PNA_GL_FLI_EE       (#line,+1)
    add 1 to #line
  end-if

  if RTRIM(&PNA_GL_VFLI_EE,' ') <> ''
    print 'VFLIEE'          (#line,78)
    print &PNA_GL_VFLI_EE        (#line,+1)
    add 1 to #line
  end-if

  if RTRIM(&PNA_GL_VFLI_ER,' ') <> ''
    print 'VFLIER'          (#line,78)
    print &PNA_GL_VFLI_ER        (#line,+1)
    add 1 to #line
  end-if

  if RTRIM(&GL_ST_EIC,' ') <> ''
    print 'ST EIC'          (#line,78)
    print &GL_ST_EIC       (#line,+1)
  end-if

NON_RES_DECLR_REQ           (1,104)

  NEXT-LISTING SKIPLINES=1 NEED=10

FROM PS_CO_STATETAX_TBL

ORDER BY COMPANY, STATE, EFFDT DESC

END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Before-Company
  if $Company <> ''
    new-page
  end-if
  move &Company to $Company
END-PROCEDURE

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'geteandg.sqc'  !Get-EandG procedure                           !HP00001
#Include 'stdapi.sqc'    !Update Process API
