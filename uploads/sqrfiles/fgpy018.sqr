!***********************************************************************
!  FGPY018:  Create Wage and Separation file (Form ES-931)             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                $Date:  2006/07/06:13:00:07                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
#Include 'setup31.sqc'   !Printer and page-size initialization
#Include 'usarpt.sqc'    !USA-specific variables
begin-setup

Declare-Printer HPLASERJET
    type  =  HPLASERJET
!    orientation =  Portrait
!    top-margin  = .2
 !   left-margin = .05
    font        =  5
    point-size  =  11.6
  !  char-size   =  7.2
!    line-size   =  3
!page-size 150 150
end-declare

end-setup

begin-report

    do Init-Report
    do Process-Main
    do Print-Form
    do Stdapi-Term

end-report

!**************************
begin-procedure Init-Report
!**************************

  let #quarter-cnt = 1
  move 'FGPY018' to $ReportID
  move 'Wage and Separation Interface' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  LET $ASOFDATE = DATENOW()
  do Format-DateTime($ASOFDATE, $ASOFDATE, {DEFCMP}, '', '')
  do Format-DateTime($ASOFDATE, $ASOFDATE, {DEFCMP}, '', 'native')
  do Stdapi-Init

end-procedure       !Init-Report

!***************************
begin-procedure Process-Main
!***************************

  do Get-FGPY018-Parameters
  do select-company

! SHOW 'COMPANY    ' $COMPANY ' '  ' ' 'ASOFDATE ' $ASOFDATE
  do Get-Company-Data
  let $Compnyadd3 = $Compnycity || ',' || &CT.STATE || ' ' || &CT.POSTAL
  let $SetCntrlValue = $company
  do getsetid('BALANCE_ID_TBL','',$setid)
  do Process-Quarter
  do Get-Employment
  do Get-UCFE-data
  do Process-Enrollment

end-procedure

!******************************
begin-procedure Process-Quarter
!******************************

while #quarter-cnt <= 6

  do Process-Qtrly-Wages

    evaluate #quarter-cnt
    when = 1
      let $date1 = $end-dt
      let #quarter1 = &C.TxGrs_QTD
      let $year1 = #Balance-Year
      let $name = &PN.NAME_PSFORMAT
      let $ssn = edit(&D1.NATIONAL_ID,'xxx-xx-xxxx')
      let $birthdate = datetostr(&D.BIRTHDATE,'MM/DD/YYYY')
    when = 2
      let $date2 = $end-dt
      let #quarter2 = &C.TxGrs_QTD
      let $year2 = #Balance-Year
    when = 3
      let $date3 = $end-dt
      let #quarter3 = &C.TxGrs_QTD
      let $year3 = #Balance-Year
    when = 4
      let $date4 = $end-dt
      let #quarter4 = &C.TxGrs_QTD
      let $year4 = #Balance-Year
    when = 5
      let $date5 = $end-dt
      let #quarter5 = &C.TxGrs_QTD
      let $year5 = #Balance-Year
    when = 6
      let $date6 = $end-dt
      let #quarter6 = &C.TxGrs_QTD
      let $year6 = #Balance-Year
    end-evaluate

    if #Balance-Qtr = 1
        let #Balance-Qtr = 4
        let #Balance-Year = #Balance-Year - 1
    else
        let #Balance-Qtr = #Balance-Qtr - 1
    end-if

    let #quarter-cnt = #quarter-cnt + 1

end-while
end-procedure


!**********************************
begin-procedure Process-Qtrly-Wages
!**********************************

begin-SELECT
C.COMPANY
C.BALANCE_PERIOD
C.TAX_CLASS
D1.NATIONAL_ID
PN.NAME_PSFORMAT
D.BIRTHDATE
SUM(C.TXGRS_QTD) &C.TxGrs_QTD
SUM(C.TAX_QTD)   &C.Tax_QTD

     do Get-Quarter-Date

  FROM  PS_TAX_BALANCE C, PS_PERSON_NAME PN, PS_PERSON D, PS_PERS_NID D1
  WHERE D.EMPLID  = $emplid
    AND D.EMPLID  = PN.EMPLID
    AND D.EMPLID  = C.EMPLID
    AND D1.EMPLID = D.EMPLID
    AND D1.COUNTRY = {NID_Country}
    AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
    AND C.EMPLID        = D.EMPLID
    AND C.STATE         = '$U'
    AND C.LOCALITY      = ' '
    AND C.TAX_CLASS     = 'H'
    AND C.BALANCE_ID    = $Balance-ID
    AND C.BALANCE_YEAR  = #Balance-Year
    AND C.BALANCE_QTR   = #Balance-Qtr
    AND C.BALANCE_PERIOD  =
        (SELECT MAX(BALANCE_PERIOD)
         FROM   PS_TAX_BALANCE
         WHERE  EMPLID        = C.EMPLID
           AND  COMPANY       = C.COMPANY
           AND  BALANCE_ID    = C.BALANCE_ID
           AND  BALANCE_YEAR  = C.BALANCE_YEAR
           AND  BALANCE_QTR   = C.BALANCE_QTR
           AND  STATE         = C.STATE
           AND  LOCALITY      = C.LOCALITY
           AND  TAX_CLASS     = C.TAX_CLASS)
  GROUP BY C.COMPANY, C.BALANCE_PERIOD, D1.NATIONAL_ID, PN.NAME_PSFORMAT, D.BIRTHDATE, C.TAX_CLASS
  ORDER BY C.COMPANY, C.BALANCE_PERIOD, D1.NATIONAL_ID, PN.NAME_PSFORMAT, D.BIRTHDATE, C.TAX_CLASS

  end-SELECT
end-procedure

!*******************************
 begin-procedure Get-Quarter-Date
!*******************************

!rtrim(&c.balance_period,' ')

  evaluate &c.balance_period
   when = 1
   when = 2
   when = 3
      let #balance_period = 3
      break
   when = 4
   when = 5
   when = 6
      let #balance_period = 6
      break
   when = 7
   when = 8
   when = 9
      let #balance_period = 9
      break
   when = 10
   when = 11
   when = 12
      let #balance_period = 12
      break
  end-evaluate


 begin-select
DEF.END_DT

   let $end-dt = datetostr(&DEF.END_DT,'Month DD')

 FROM PS_BALANCE_ID_DEF DEF
 WHERE DEF.SETID        = $setid
   AND DEF.BALANCE_ID   = $Balance-ID
   AND DEF.BALANCE_YEAR = #Balance-Year
   AND DEF.BALANCE_PERIOD  = #balance_period !&C.BALANCE_PERIOD

 end-select
 end-procedure

!*****************************
begin-procedure Get-Employment
!*****************************


begin-select
EMP.TERMINATION_DT
EMP.BUSINESS_TITLE
EMP.LAST_DATE_WORKED
JOB.EFFDT
JOB.POSITION_NBR
JOB.FULL_PART_TIME
JOB.STD_HOURS
JOB.ACTION
JOB.ACTION_REASON
JOB.PAYGROUP
JOB.EMPL_RCD
LOC.CITY
LOC.STATE
LOC.COUNTRY
JUSF.GVT_WORK_SCHED
JUSF.GVT_ANNUITY_OFFSET


  if not isnull(&EMP.TERMINATION_DT)
      let $term-date = datetostr(&EMP.TERMINATION_DT,'MM/DD/YYYY')
  end-if
  let $title = rtrim(&EMP.BUSINESS_TITLE,' ')

!**** OLD BEGIN
  if not isnull(&EMP.LAST_DATE_WORKED)
      let $last-date = datetostr(&EMP.LAST_DATE_WORKED,'MM/DD/YYYY')
  end-if
!**** OLD END

  if &JOB.FULL_PART_TIME = 'F'
      let $full-time = 'X'
      let $part-time = ' '
  else
      let $part-time = 'X'
      let $full-time = ' '
  end-if

  do Retrieve-Sep-Reason
  do Calculate-Work-Hrs

FROM PS_EMPLOYMENT EMP, PS_JOB JOB, PS_LOCATION_TBL LOC, PS_JOB_USF JUSF
WHERE EMP.EMPLID = $emplid
  AND EMP.EMPLID = JOB.EMPLID
  AND EMP.EMPL_RCD = JOB.EMPL_RCD
  AND JOB.EFFDT =  (SELECT MAX(JOB1.EFFDT)
                  FROM  PS_JOB JOB1
                  WHERE JOB1.EMPLID    = JOB.EMPLID
                    AND JOB1.EMPL_RCD = JOB.EMPL_RCD
                    AND JOB1.EFFDT     <=  $AsOfToday)
  AND JOB.EFFSEQ = (SELECT MAX(JOB2.EFFSEQ)
                  FROM  PS_JOB JOB2
                  WHERE JOB2.EMPLID    = JOB.EMPLID
                    AND JOB2.EMPL_RCD = JOB.EMPL_RCD
                    AND JOB2.EFFDT     = JOB.EFFDT)
   AND LOC.LOCATION = JOB.LOCATION
   AND JOB.EMPLID   = JUSF.EMPLID
   AND JOB.EMPL_RCD = JUSF.EMPL_RCD
   AND JOB.EFFDT    = JUSF.EFFDT
   AND JOB.EFFSEQ   = JUSF.EFFSEQ
end-select

end-procedure


!**********************************
begin-procedure Get-UCFE-data
!**********************************


BEGIN-SELECT

UCFE.EMPLID
UCFE.GVT_LOCAL_OFFICE
UCFE.GVT_REQUEST_DATE
UCFE.NAME
UCFE.NATIONAL_ID
UCFE.GVT_CLAIM_DATE
UCFE.BIRTHDATE
UCFE.DESCR
UCFE.CITY
UCFE.STATE
UCFE.COUNTRY
UCFE.LAST_DATE_WORKED
UCFE.GVT_WORK_SCHED
UCFE.GVT_DATA_OVERRIDE
UCFE.GVT_FEDERAL_SVC
UCFE.GVT_SF8_CHECKBOX
UCFE.GVT_RECEIVE_SF8
UCFE.DESCR100
UCFE.GVT_PAYMENT
UCFE.GVT_PAYMENT_AMT
UCFE.GVT_PAYMENT_DT
UCFE.GVT_ANN_LEAVE_DAYS
UCFE.GVT_ANN_LEAVE_HOUR
UCFE.GVT_LEAVE_BEG_TIME
UCFE.GVT_LEAVE_END_TIME
UCFE.GVT_LEAVE_BEGIN_DT
UCFE.GVT_LEAVE_END_DT
UCFE.GVT_SEVERANCE_PAY
UCFE.GVT_SEV_PAY_WEEKLY
UCFE.GVT_TOTAL_SEV_PAY
UCFE.GVT_NUM_WEEKS_SEV
UCFE.GVT_SEV_BEGIN_DT
UCFE.GVT_SEV_PAY_END_DT

  let $local_office = rtrim(&UCFE.GVT_LOCAL_OFFICE,' ')
  let $title = rtrim(&UCFE.DESCR,' ')

  let $SSN = EDIT(&UCFE.NATIONAL_ID,'XXX-XX-XXXX')

  if not isnull(&UCFE.LAST_DATE_WORKED)
      let $last-date = datetostr(&UCFE.LAST_DATE_WORKED,'MM/DD/YYYY')
  end-if

  if not isnull(&ucfe.gvt_request_date)
     let $claim_date = datetostr(&ucfe.gvt_request_date, 'MM/DD/YYYY')
  end-if

  if not isnull(&ucfe.gvt_claim_date)
     let $request_date = datetostr(&ucfe.gvt_claim_date, 'MM/DD/YYYY')
  end-if

  if not isnull(&ucfe.birthdate)
     let $birthdate = datetostr(&ucfe.birthdate, 'MM/DD/YYYY')
  end-if

 let $place_of_employment = rtrim(&ucfe.city,' ') || ', ' || rtrim(&ucfe.state,' ') ||  ' ' || rtrim(&ucfe.country,' ')

 if &UCFE.GVT_WORK_SCHED = 'B'
 OR &UCFE.GVT_WORK_SCHED = 'F'
 OR &UCFE.GVT_WORK_SCHED = 'G'
    LET $full_time = 'X'
    let $part_time = ''
 ELSE
    LET $part_time = 'X'
    let $full_time = ''
 end-if

 do get-last-day-of-active-pay


 FROM PS_GVT_UCFE UCFE
 WHERE UCFE.EMPLID = $emplid
   AND UCFE.GVT_LOCAL_OFFICE = &UCFE_R.GVT_LOCAL_OFFICE
   AND UCFE.GVT_REQUEST_DATE = &UCFE_R.GVT_REQUEST_DATE

END-SELECT

end-procedure Get-UCFE-data

!*****************************************
begin-procedure get-last-day-of-active-pay
!*****************************************


begin-select

MAX(JOB.EFFDT) &last_day_active_pay

 let $last_day = datetostr(&last_day_active_pay,'MM/DD/YYYY')

FROM PS_JOB JOB
WHERE JOB.EMPLID = $emplid
  AND JOB.EMPL_STATUS <> 'A'
  AND JOB.EFFDT >= &EMP.LAST_DATE_WORKED

end-select
end-procedure get-last-day-of-active-pay

!**********************************
begin-procedure Retrieve-Sep-Reason
!**********************************

begin-select
ACTN.DESCR

  let $sep-reason = &ACTN.DESCR

FROM PS_ACTN_REASON_TBL ACTN
WHERE ACTN.ACTION = &JOB.ACTION
  AND ACTN.ACTION_REASON = &JOB.ACTION_REASON
  AND ACTN.EFFDT = (SELECT MAX(EFFDT)
                  FROM PS_ACTN_REASON_TBL ACTN1
                  WHERE ACTN1.ACTION = ACTN.ACTION
                    AND ACTN1.ACTION_REASON = ACTN.ACTION_REASON
                    AND ACTN1.EFFDT <= &JOB.EFFDT)

end-select

end-procedure

!*********************************
begin-procedure Calculate-Work-Hrs
!*********************************

let #pos-cnt = 0

begin-select
POS.MON_HRS
POS.TUES_HRS
POS.WED_HRS
POS.THURS_HRS
POS.FRI_HRS
POS.SAT_HRS
POS.SUN_HRS

  if &POS.MON_HRS > 0
      let #pos-cnt = #pos-cnt + 1
  end-if

  if &POS.TUES_HRS > 0
      let #pos-cnt = #pos-cnt + 1
  end-if

  if &POS.WED_HRS > 0
      let #pos-cnt = #pos-cnt + 1
  end-if

  if &POS.THURS_HRS > 0
      let #pos-cnt = #pos-cnt + 1
  end-if

  if &POS.FRI_HRS > 0
      let #pos-cnt = #pos-cnt + 1
  end-if

  if &POS.SAT_HRS > 0
      let #pos-cnt = #pos-cnt + 1
  end-if

  if &POS.SUN_HRS > 0
      let #pos-cnt = #pos-cnt + 1
  end-if

  if #pos-cnt > 0
      let #day-hours = &JOB.STD_HOURS / #pos-cnt
  end-if

FROM PS_POSITION_DATA POS
WHERE POS.POSITION_NBR = &JOB.POSITION_NBR
  AND POS.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_POSITION_DATA POS1
                 WHERE POS1.POSITION_NBR = POS.POSITION_NBR
                   AND POS1.EFFDT <= &JOB.EFFDT)
  AND POS.EFF_STATUS = 'A'
end-select

end-procedure




!*********************************
begin-procedure Process-Enrollment
!*********************************

begin-select
CLS.GVT_ERN_ACCR_CLASS
CLS.GVT_PAY_TERM_ERNCD

  do Get-Payout

FROM PS_GVT_ERN_ACR_CLS CLS, PS_GVT_ACCR_PLAN PLAN1
WHERE CLS.GVT_EARN_TYPE = 'AN'
AND CLS.EFF_STATUS = 'A'
AND CLS.EFFDT = (SELECT MAX(EFFDT) FROM PS_GVT_ERN_ACR_CLS CLS1
                  WHERE CLS1.GVT_ERN_ACCR_CLASS = CLS.GVT_ERN_ACCR_CLASS
                   AND CLS1.EFFDT <= $AsOfDate)
AND PLAN1.EMPLID = $emplid
AND PLAN1.GVT_ERN_ACCR_CLASS = CLS.GVT_ERN_ACCR_CLASS
AND PLAN1.EFFDT = (SELECT MAX(EFFDT) FROM PS_GVT_ACCR_PLAN PLAN2
                   WHERE PLAN2.EMPLID = PLAN1.EMPLID
                     AND PLAN2.EMPL_RCD = PLAN1.EMPL_RCD
                     AND PLAN2.GVT_ERN_ACCR_CLASS = PLAN1.GVT_ERN_ACCR_CLASS
                     AND PLAN2.EFFDT <= $AsOfDate)

end-select

end-procedure

!*************************
begin-procedure Get-Payout
!*************************

begin-select

OTH.PAY_END_DT
SUM(OTH.OTH_EARNS) &oth.oth_earns
SUM(OTH.OTH_HRS)   &oth.oth_hrs

  let $pay-end-dt = datetostr(&OTH.PAY_END_DT,'MM/DD/YYYY')

FROM PS_PAY_OTH_EARNS OTH, PS_PAY_EARNINGS PAY
WHERE PAY.COMPANY = $company
  AND PAY.PAYGROUP = &JOB.PAYGROUP
  AND PAY.EMPLID = $emplid
  AND PAY.PAY_END_DT = (SELECT MAX(PAY_END_DT) FROM PS_PAY_EARNINGS PAY1
                         WHERE PAY1.COMPANY = PAY.COMPANY
                           AND PAY1.PAYGROUP = PAY.PAYGROUP
                           AND PAY1.OFF_CYCLE = PAY.OFF_CYCLE
                           AND PAY1.PAGE_NUM = PAY.PAGE_NUM
                           AND PAY1.LINE_NUM = PAY.LINE_NUM
                           AND PAY1.ADDL_NBR = PAY.ADDL_NBR
                           AND PAY1.PAY_END_DT <= $AsOfDate)
  AND PAY_LINE_STATUS = 'F'
  AND OTH.COMPANY = PAY.COMPANY
  AND OTH.PAYGROUP = PAY.PAYGROUP
  AND OTH.OFF_CYCLE = PAY.OFF_CYCLE
  AND OTH.PAGE_NUM = PAY.PAGE_NUM
  AND OTH.LINE_NUM = PAY.LINE_NUM
  AND OTH.ADDL_NBR = PAY.ADDL_NBR
  AND OTH.PAY_END_DT = PAY.PAY_END_DT
  AND OTH.ERNCD = &CLS.GVT_PAY_TERM_ERNCD
GROUP BY OTH.PAY_END_DT, OTH.OTH_EARNS, OTH.OTH_HRS
end-select
end-procedure

!*************************************
begin-procedure Get-FGPY018-Parameters
!*************************************

 do select-parameters-ucfe

begin-select distinct LOOPS=1
TRUN.BALANCE_YEAR &TRUN.BALANCE_YEAR
TRUN.BALANCE_QTR  &TRUN.BALANCE_QTR
TRUN.BALANCE_ID   &TRUN.BALANCE_ID

  let #Balance-Year = &TRUN.BALANCE_YEAR
  let #Balance-Qtr  = &TRUN.BALANCE_QTR
  let $Balance-ID   = &TRUN.BALANCE_ID

FROM PS_TAX_BALANCE TRUN
WHERE TRUN.EMPLID = $emplid
ORDER BY TRUN.BALANCE_YEAR DESC, TRUN.BALANCE_QTR DESC

end-select



 end-procedure

!*************************************
begin-procedure select-parameters-ucfe
!*************************************


BEGIN-SELECT
UCFE_R.EMPLID            &UCFE_R.EMPLID
UCFE_R.GVT_LOCAL_OFFICE  &UCFE_R.GVT_LOCAL_OFFICE
UCFE_R.GVT_REQUEST_DATE  &UCFE_R.GVT_REQUEST_DATE

  let $emplid       =  &UCFE_R.EMPLID
  let $local_office =  &UCFE_R.GVT_LOCAL_OFFICE
  let $request_date =  &UCFE_R.GVT_REQUEST_DATE
 FROM PS_GVT_RUNCTL_UCFE UCFE_R
 WHERE OPRID       = $Prcs_OprID
   AND RUN_CNTL_ID = $prcs_run_cntl_id

END-SELECT
end-procedure


!*************************************
begin-procedure select-company
!*************************************


begin-select
A.COMPANY

 let $company = &A.COMPANY

FROM PS_JOB A
  WHERE A.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_JOB A_ED
        WHERE A.EMPLID = A_ED.EMPLID
          AND A.EMPL_RCD = A_ED.EMPL_RCD
          AND A_ED.EFFDT <= $asofdate)
    AND A.EFFSEQ =
        (SELECT MAX(A_ES.EFFSEQ) FROM PS_JOB A_ES
        WHERE A.EMPLID = A_ES.EMPLID
          AND A.EMPL_RCD = A_ES.EMPL_RCD
          AND A.EFFDT = A_ES.EFFDT)
 AND A.EMPLID = $emplid
END-SELECT


end-procedure select-company

!************************************************************
begin-procedure Print-Form
!************************************************************
let #hor_line_len_full = 121
!  ALTER-PRINTER
! POINT-SIZE = 7
!FONT=4

  Graphic (0,61,61) HORZ-LINE
  Graphic (0,61,3) VERT-LINE
  Graphic (0,122,3) VERT-LINE

  position (1,1)
  Print 'D.C DEPARTMENT OF EMPLOYMENT SERVICES' (0,5) BOLD

  Print 'OFFICE OF UNEMPLOYMENT COMPENSATION'   (+1,5) BOLD
  Print 'LOCAL OFFICE'                          (0,63)
  print $local_office (0,85)

  Graphic (0,93,2) VERT-LINE
  Graphic (0,61,61) HORZ-LINE

  Print 'REQUEST FOR WAGE AND SEPARATION'       (+1,06) BOLD
  Print 'Date New Claim Filed'                  (0,63)
  Print 'Date of Request'                       (0,94)

  Print 'INFORMATION - UCFE'                    (+1,11) BOLD
  Graphic (0,61,61) HORZ-LINE
  PRINT $claim_date                             (0,65)
  print $request_date                           (0,95)

  Print 'SECTION 1. IDENTIFICATION DATA'        (+1,45) BOLD
  Graphic (0,1,121) HORZ-LINE
  Graphic (0,1,6)   VERT-LINE
  Graphic (0,61,6)  VERT-LINE
  GRAPHIC (0,122,6) VERT-LINE
  !Graphic (0,40,36) HORZ-LINE
  Graphic (0,61,6)  VERT-LINE
  !Graphic (0,61,4)  VERT-LINE
 ! Graphic (0,61,6)  VERT-LINE
    Graphic (0,93,4) VERT-LINE

  Print '1. NAME(Last,First,Middle,Maiden,if any)' (+1,2)
  Print '2. SOCIAL SECURITY NUMBER(S)'             (0,63)
  Print '3. DATE OF BIRTH'                         (0,94)

  Graphic (+1,1,121) HORZ-LINE
  !GRAPHIC(1, 122,) VERT-LINE
  !Graphic (0,40,36) HORZ-LINE
  Print &UCFE.NAME (0,4)
  Print $ssn (0,65)
  Print $birthdate (0,95)

  Print '4. POSITION TITLE'                        (+1,2)
  Print '5. PLACE OF EMPLOYMENT'                   (0,63)
  Print '6. DATE OF SEPARATION'                    (0,94)

  Graphic (+1,1,121) HORZ-LINE
  !Graphic (0,63,36) HORZ-LINE
  Print $title (0,4)

   ALTER-PRINTER
    POINT-SIZE = 7
   FONT=5

  print $place_of_employment                       (0,65)
  Print $term-date (0,95)

   ALTER-PRINTER
     POINT-SIZE = 8
     FONT=5
  Print '7a. Is payroll office address based on SF-8?'     (+1,2)
  Print ' Yes [   ]'                                        (0,35)
  Print 'No [   ]'                                         (0,42)

  IF &UCFE.GVT_SF8_CHECKBOX = 'Y'
     PRINT 'X' (0,38)
  ELSE
     PRINT 'X' (0,45)
  END-IF

  Print '8. Claimant states he was:'                       (0,63)
  Print ' a. [   ] regular full-time employee'              (0,85)
  Print $full_time (0,88)
  Print 'b. If "No" does claimant state he/she received SF-8?' (+1,3)
  Print ' Yes [   ]'                                        (0,42)
  Print 'No [   ]'                                         (0,49)
  Print ' b. [   ] intermittent or part-time employee'      (0,85)

  IF &UCFE.GVT_RECEIVE_SF8 = 'Y'
     PRINT 'X' (0,44)
  END-IF
  IF &UCFE.GVT_RECEIVE_SF8 = 'N'
     PRINT 'X' (0,46)
  END-IF

  print $part_time (0,86)
  Graphic (0,1,121) HORZ-LINE
 ! Graphic (0,61,61) HORZ-LINE

!**************************************************************
! ************SECTION II
!***********************************************************

  Graphic (+3,1,121) HORZ-LINE
  Graphic (0,1,5)   VERT-LINE
  Graphic (0,61,5)  VERT-LINE
  Graphic (0,122,5) VERT-LINE

 ! Graphic (+1,61,61) HORZ-LINE
  Print 'SECTION II. FEDERAL AGENCY REPLY'         (0,45) BOLD

  Print 'INSTRUCTIONS: Complete Section II and return Original and 1' (+1,63) BOLD
  Print $CompanyName (0,4)

  Print 'copy within 4 days.'                      (+1,65) BOLD
  Print $Compnyadd1 (0,4)
  Print $Compnyadd2 (+1,4)

  Print $Compnyadd3 (+1,4)

  Graphic (+1,1,121) HORZ-LINE
  !Graphic (+1,1,57) HORZ-LINE

  Print '1. FEDERAL CIVILIAN SERVICE'              (+3,45) BOLD
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  graphic (0, 1,3) vert-line
  graphic (0, 122,3) vert-line
  LET #LINE_NUM = #CURRENT-LINE
  Print '1a. Did this person perform "Federal civilian service" (as defined for UCFE purposes) for your agency at any time during or after the period shown in'  (+1,1)
  !Print '' (0,34)
  LET #LINE_NUM = #LINE_NUM + 1
  POSITION (#LINE_NUM,1)

  Print 'Item 2a below?' (+1,3)

  IF &UCFE.GVT_FEDERAL_SVC = 'Y'
     Print 'Yes [ X ]      No [   ],   if "No" explain.' (0,23)
     !PRINT 'X' (0,13)
  ELSE
     Print 'Yes [   ]      No [ X ],   if "No" explain.' (0,23)
     !Print 'X' (0,18)
  end-if

  Print &ucfe.descr100    (0,31)

  Graphic (0,1,121) HORZ-LINE

  LET #LINE_NUM = #CURRENT-LINE
  Print '1b. DUTY STATION: Enter State of this person''s last employment with your agency (or, if outside U.S. enter country):'  (+1,1)
!  Print ''                       (0,37)
  LET #LINE_NUM = #LINE_NUM + 1
  POSITION (#LINE_NUM,1)


  Graphic (0,1,121) HORZ-LINE

  Print '2. WAGES'                                (+3,45) BOLD
  Graphic (0,1,121) HORZ-LINE

  Graphic (0,1,8)  VERT-LINE
  Graphic (0,61,9) VERT-LINE
  Graphic (0,122,9) VERT-LINE

  Print '*2a.  REPORT OF WAGES' (+1,1)
  Print '2b.  REPORT OF DUTY HOURS' (0,63)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  Graphic (0,36,7) VERT-LINE
  Graphic (0,43,7) VERT-LINE
 ! Graphic (0,61,2) VERT-LINE



  let #dollar_col_pos = 44
  let #qtr_end_col_pos = 4
  let #year_col_pos = 37
  let #qtr_wage_col_pos = 55

  Print 'QUARTER ENDING' (+1,4)
  Print 'YEAR' (0,37)
  Print 'GROSS WAGES' (0,45)
  Print 'NUMBER OF DUTY HOURS WORKDAY' (0,63)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  Print #day-hours (0,105) edit bb9.99


  ! --------------------------------------------------------------
  Print $date1 (+1,#qtr_end_col_pos)
  Print $year1 (0,#year_col_pos) edit 9999
  Print '$' (0,#dollar_col_pos)
  Print #quarter1 (0,#qtr_wage_col_pos) edit b,bbb,b99.99
  Print 'NUMBER OF DUTY HOURS: BASIC WORKWEEK' (0,63)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  Print &JOB.STD_HOURS (0,105) edit b99.99
 !-------------------------------------------------------------------------
  Print $date2 (+1,#qtr_end_col_pos)
  Print $year2 (0,#year_col_pos) edit 9999
  Print '$' (0,#dollar_col_pos)
  Print #quarter2 (0,#qtr_wage_col_pos) edit b,bbb,b99.99
  Print '2c.  IDENTIFICATION: if incorrect data shown in Section 1' (0,63)
  Graphic (0,1,60) HORZ-LINE

  !--------------------------------------------------

  Print '$' (+1,#dollar_col_pos)
  Print 'enter correction(s)' (0,63)

  Graphic (0,1,60) HORZ-LINE
  Print $date3 (0,#qtr_end_col_pos)
  Print $year3 (0,#year_col_pos) edit 9999
  Print #quarter3 (0,#qtr_wage_col_pos) edit b,bbb,b99.99
 !------------------------------------------------------------------
  Print '$' (+1,#dollar_col_pos)
  Graphic (0,1,60) HORZ-LINE
  Print $date4 (0,#qtr_end_col_pos)
  Print $year4 (0,#year_col_pos) edit 9999
  Print #quarter4 (0,#qtr_wage_col_pos) edit b,bbb,b99.99

  Print '$' (+1,#dollar_col_pos)
  Graphic (0,1,60) HORZ-LINE
  Print $date5 (0,#qtr_end_col_pos)
  Print $year5 (0,#year_col_pos) edit 9999
  Print #quarter5 (0,#qtr_wage_col_pos) edit b,bbb,b99.99

  Print '$' (+1,#dollar_col_pos)
  Graphic (0,1,60) HORZ-LINE
  Print $date6 (0,#qtr_end_col_pos)
  Print $year6 (0,#year_col_pos) edit 9999
  Print #quarter6 (0,#qtr_wage_col_pos) edit b,bbb,b99.99


  Print '$' (+1,#dollar_col_pos)
  Graphic (0,1,121) HORZ-LINE

!------------------------------------------------------------------------
  Print '3. TERMINAL ANNUAL LEAVE AND SEPARATION INFORMATION' (+6,27) BOLD
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  graphic (0,1,15) vert-line
  graphic (0,122,15) vert-line

  LET #LINE_NUM = #CURRENT-LINE
  Print '3a.(1) Did this person receive a lump-sum payment(s) for terminal annual leave on or after the beginning date of the base period shown in' (+1,3)
!  Print '     '         (0,35)
  LET #LINE_NUM = #LINE_NUM + 1
  POSITION (#LINE_NUM,1)
 !---------------------------------------------------------
  Print 'Item 2a above?' (+1,5)

  IF &UCFE.GVT_PAYMENT = 'Y'
     Print 'Yes [ X ]      No [   ]' (0,25)
  ELSE
     Print 'Yes [   ]      No [ X ]' (0,25)
  END-IF


  Print 'If "Yes" or if currently entitled to such a payment, record data below for each payment (or entitlement) since such date' (+1,5)
!  Print '              ' (0,32)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  Graphic (0,43,5) VERT-LINE
  Graphic (0,122,8) VERT-LINE
  Print '(2) Amount of Payment' (+1,4)
  Print '(4) Amount of Terminal Annual Leave' (0,43)
  Print '(5) Period of Terminal Leave' (0,75)
  Graphic (0,59,3) VERT-LINE
  Graphic (0,73,3) VERT-LINE
  Graphic (0,93,3) VERT-LINE

  Print 'Days' (+1,45)
  Print 'Hours' (0,61)
  Print 'Time' (0,74)
  Print 'Date' (0,94)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  !Print &oth.oth_earns (0,6) edit bbb,b99.99
  IF &UCFE.GVT_PAYMENT = 'Y'
     PRINT &UCFE.GVT_PAYMENT_amt (0,6) EDIT $$$,$$9.99
  end-if

  Print '(3) Date of Payment' (+1,4)
  Print 'From:' (0,74)
  Graphic (0,43,79) HORZ-LINE

  IF &UCFE.GVT_PAYMENT = 'Y'
     print &ucfe.gvt_payment_dt (0,25) edit mm/dd/yyyy
     print &ucfe.gvt_ann_leave_days (0,45) edit bb9
     print &ucfe.gvt_ann_leave_hour (0,61) edit bb9
     print &ucfe.gvt_leave_beg_time (0,83) edit 'HH:MI PM'
     print &ucfe.gvt_leave_begin_dt (0,99) edit mm/dd/yyyy
  end-if

  Print 'To:' (+1,74)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  !Print $pay-end-dt (0,6)
  !Print &oth.oth_hrs (0,37) edit b,bb9.99

  IF &UCFE.GVT_PAYMENT = 'Y'
     print &ucfe.gvt_leave_end_time (0,83) edit 'HH:MI PM'
     print &ucfe.gvt_leave_end_dt (0,99) edit mm/dd/yyyy
  end-if

  Print '3b. Date of Separation' (+1,3)
  Print '3c. Date of Last Day of Active pay Status' (0,45)
  !Print $term-date (0,14)
  !Print $last-date (0,48)
  LET $TERM-DATE =  EDIT(&UCFE.LAST_DATE_WORKED,'MM/DD/YYYY')
  PRINT $TERM-DATE (0,25)
  print $last_day (0,90)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE

  Print '3d. REASON FOR SEPARATION OR NONPAY STATUS:' (+1,3)
  Graphic (+1,1,#hor_line_len_full) HORZ-LINE
  Print $sep-reason (0,6)


  if &ucfe.gvt_severance_pay = 'Y'
     Print '3e.(1) Did this person receive or is he/she entitled to receive severance pay provided by any Federal law or agency-employee agreement? [X] Yes [ ] No.'  (+1,2)

  ELSE
     Print '3e.(1) Did this person receive or is he/she entitled to receive severance pay provided by any Federal law or agency-employee agreement? [ ] Yes [X] No.' (+1,2)
  END-IF

  Print 'If yes, complete the following information.  Weekly amount' (+1,4)
  Print 'Total entitlement' (0,61)
  Print 'No. of weeks' (0,93)
  Graphic (0,50,9) HORZ-LINE
  Graphic (0,74,13) HORZ-LINE
  Graphic (0,103,7) HORZ-LINE

  if &ucfe.gvt_severance_pay = 'Y'
     PRINT &UCFE.GVT_SEV_PAY_WEEKLY (0,50) edit $$,$$$.99
     PRINT &UCFE.GVT_TOTAL_SEV_PAY  (0,73) edit $$$,$$$,$$$.99
     print &ucfe.gvt_num_weeks_sev (0,103)  edit 99999
  end-if


  Print 'beginning' (+1,4)
  Print 'ending' (0,29)
  Graphic (0,12,9) HORZ-LINE
  Graphic (0,35,9) HORZ-LINE

  if &ucfe.gvt_severance_pay = 'Y'
     PRINT &UCFE.gvt_sev_begin_dt (0,12) edit mm/dd/yyyy
     PRINT &UCFE.GVT_sev_pay_end_dt  (0,35) edit mm/dd/yyyy
  end-if

  Print '(2) Is this person receiving, or is he/she entitled to receive retirement pay? [  ] Yes  [  ] No, If yes, give monthly amount:  ' (+1,4)


  IF &JUSF.GVT_ANNUITY_OFFSET <> 0
     PRINT 'X' (0,60)
     PRINT &JUSF.GVT_ANNUITY_OFFSET (0,95) edit $$,$$$.99
  ELSE
     PRINT 'X' (0,66)
  END-IF

  Graphic (0,60,7) HORZ-LINE
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  Print 'I CERTIFY THAT I have examined this report (including the instructions on the reverse of this form), that this report constututes ' (+2,3)
  Print 'the findings of this agency under Federal Law (5 U.S.G. 8506 (a)) and, to the best of my knowledge it is a correct and complete report.' (+1,3)
!  Print '    ' (0,36)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE
  Graphic (0,61,9) VERT-LINE
  Graphic (0,91,4) VERT-LINE
  graphic (0,1,4)vert-line

  Print 'SIGNATURE OF OFFICIAL' (+1,3)
  Print 'TITLE'                 (0,62)
  Print 'DATE'                  (0,92)
  Graphic (+1,61,61) HORZ-LINE

  Print 'TELEPHONE NO'          (+1,62)
  Print 'EXTENSION'             (0,92)

  Graphic (+1,1,#hor_line_len_full) HORZ-LINE

  Print '4. NAME OF PARENT FEDERAL AGENCY (e.g. Dept Army, FPC,' (+1,3)
  Print '5. NAME OF AGENCY COMPONENT AND ADDRESS OF PAYROLL OFFICE (If' (0,62)

  Print 'Dept Interior, NASA) and I.D. Code Number' (+1,4)
  Print 'different from address shown above)'       (0,62)

  Graphic (+3,1,#hor_line_len_full) HORZ-LINE


  Print 'Mail to: D.C.Department of Employment Services, Office of Unemployment Compensation, 500 C Street, N.W. Washington, D.C. 20001' (+1,3) BOLD
!  Print '' (0,35) BOLD



  Print 'SECTION III. STATE AGENCY TO FILL IN' (+3,30) BOLD
  Graphic (0,1,#hor_line_len_full) HORZ-LINE

  Print 'DATE UCFE Central Control Form ES-932 mailed to UCFE Control Unit, U.S. Department of Labor, Washington D.C.' (+1,3)
!  Print '' (0,35)
  Graphic (0,1,#hor_line_len_full) HORZ-LINE

  Print '95-1015' (+1,3)
  Print 'ES-931 (MA 9-36)' (0,91)

  Print 'DC DOES (Rev.9/92)' (+1,91)



!*******************************************************************

  ! This will print the Correction Section of the report Section II.2c
  let #offset = 34
  let #correction_col = 63
  let #correction_data_col = 77
  if &ucfe.name  <> &PN.NAME_PSFORMAT
     print 'Name:  ' (#offset, #correction_col)
     print &PN.NAME_PSFORMAT (#offset,#correction_data_col)
     add +1 to #offset
  end-if

  if &ucfe.national_id <> &d1.national_id
     print 'SSN:  ' (#offset, #correction_col)
     print &d1.national_id (#offset,#correction_data_col)
     add +1 to #offset
  end-if

  if &ucfe.birthdate <> &d.birthdate
     print 'Birthdate:  ' (#offset, #correction_col)
     print &d.birthdate (#offset,#correction_data_col)
     add +1 to #offset
  end-if

  if &ucfe.descr <> &emp.business_title
     print 'Postion:  ' (#offset, #correction_col)
     print &emp.business_title (#offset,#correction_data_col)

     add +1 to #offset
  end-if
  if &ucfe.city <> &LOC.city
  OR &ucfe.state <> &LOC.state OR
  &ucfe.country <> &LOC.country
      let $out_city = rtrim(&LOC.city,' ') || ', ' || rtrim(&LOC.state,' ') ||  ' ' || rtrim(&LOC.country,' ')
      print $out_city (#offset,#correction_data_col)
      print 'Address:  ' (#offset, #correction_col)
     add +1 to #offset
  end-if



  if &ucfe.last_date_worked <> &emp.last_date_worked
     print 'Last Day worked:  ' (#offset, #correction_col)
     print &emp.last_date_worked (#offset,#correction_data_col)
     add +1 to #offset
  end-if



 if &ucfe.gvt_work_sched <> &jusf.gvt_work_sched
     print 'Work Sched:  ' (#offset, #correction_col)
     print &jusf.gvt_work_sched (#offset,#correction_data_col)
     add +1 to #offset
 end-if
!***************************************************************


  next-listing need = 70

END-procedure

#Include 'fgrunctl.sqc'  !Get run control parameter values
!#include 'fggetval.sqc'  !Get values mask routines
#Include 'reset1.sqc'     !Reset printer
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'curdttim.sqc'  !Routines to get current date/time
#include 'rotname1.sqc'  !Routine to reformat name
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'tranctrl.sqc'  !Commit-Transaction
#Include 'getcodta.sqc'  !Company Information
#Include 'fsgetshr.sqc'  !GetSetID procedure
