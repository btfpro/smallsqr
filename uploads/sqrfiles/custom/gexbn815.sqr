!***********************************************************************
! GEXBN815:  BP C01,C02,C03,C04,C08                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXBN815: BP C01,C02,C03,C04,C08                                     *
!                                                                      *        
! Narrative:        All employees that are currently in a dependent    *
!                      life benefit plan or spouse life and do not have*
!                      supplemental life need to be manually adjusted. *
!                      This only applies to corporate Benefit Programs *
!                      C01, C02, C03, C04, and C08.                    *
!                                                                      *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                             #debug8 key variable values              *
!                             #debug7 data returned from sql calls     * 
!                                                                      *  
! SQL Tables:           job                                            *
!                       ben_prog_partic                                *
!                       life_add_ben                                   *
!                                                                      *
! Written by:           Gene Davis  08/27/1998                         *       
!                                                                      *
! Normally Run:         Monthly                                        *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! ID#        INITIALS        DATE            DESCRIPTION OF THE CHANGE *
!***********************************************************************
! GBNC071    GXD             08/27/98        Initial Creation          *
! GBNC071    SXK             06/23/1999      Modified to select latest
!					row and check if it is an elect*                                                                      *
!                                                                      * 
!            CWB             07/17/2001      Added Benefit Program C08 *     
!                                                                      * 
! GEX_SEC_REENG 2015-06-24 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

begin-report
#debug9 Show 'begin-report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P000-Init-Report
  do P200-Process-Main
  do Get-Current-DateTime
  do End-Report
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
end-report


begin-heading 6
#debug9 Show 'begin-heading'
 #Include 'stdhdg01.sqc'
!  Do GEXXX951-Select-Oracle-Instance

!  Print 'Oracle DB: '             (1,1)
!  Print $GEXXX951_Oracle_Instance    ()
!  Print 'Giant Eagle'                () Center

!  Print 'ReportID:  '      (+1,1)
!  Print $ReportID              ()
!  Print $ReportTitle           () Center
!  Print 'SQR Begin Time '  (+1,1)
!  Print $ReportDate            ()
!  Print $ReportTime        (0,+2)
   position (-1)
  Print $ReportTitle2          () Center
!  Print ' '                (+1,1)

  PRINT 'Emplid'            (+3,1)
  PRINT 'Name'              (0,15)
  PRINT 'Rcd'               (0,49)

end-heading

!**************************************************************************
begin-procedure P000-Init-Report
#debug9 Show 'P000-Init-Report'

  move 0 to #emp_count

  move 'GEXBN815'                  to $ReportID

! CWB 07/17/2001 BEGIN
!  move 'BP C01, C02, C03, and C04' to $ReportTitle
  move 'BP C01, C02, C03, C04, and C08' to $ReportTitle
! CWB 07/17/2001 END

  move 'Note: These Employees have either dependent life or spouse coverage, but not supplemental life' to $ReportTitle2

  do Stdapi-Init

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportTitle2=' $ReportTitle2
  Show '$ReportDate=' $ReportDate
  Show '$ReportTime=' $ReportTime

  let $X000_ORDER_BY                               = 'EMPLID'  

  let $X000_Where_Select_AsOfDate = $AsOfToday
  
  Display $X000_Where_Select_AsOfDate

end-procedure

!***********************************************************************
begin-procedure P200-Process-Main
#debug9 Show 'P200-Process-Main'

  Let $Data-Found-Flag = 'N'

begin-SELECT

BPP.EMPLID                  &emplid
   move &emplid          to $emplid

BPP.EMPL_RCD               &empl_rcd
   move &empl_rcd        to #empl_rcd

BPP.EFFDT
   move &BPP.EFFDT       to $effective_date

   Do Get-Employee-Name

   Do Get-Life-Add-Ben

FROM   PS_JOB JB,
       PS_BEN_PROG_PARTIC BPP

WHERE  JB.EMPLID            = BPP.EMPLID
AND    JB.EMPL_RCD         = BPP.EMPL_RCD

! CWB 07/17/2001 BEGIN
!AND    BPP.BENEFIT_PROGRAM IN ('C01','C02','C03','C04')
AND    BPP.BENEFIT_PROGRAM IN ('C01','C02','C03','C04','C08')
! CWB 07/17/2001 END

AND    JB.EFFDT = (SELECT MAX(EFFDT)
		   FROM PS_JOB 
		   WHERE EMPLID     = JB.EMPLID
		   AND   EMPL_RCD  = JB.EMPL_RCD
		   AND   EFFDT     <= $X000_Where_Select_AsofDate)

AND    JB.EFFSEQ = (SELECT MAX(EFFSEQ)
		    FROM PS_JOB 
		    WHERE EMPLID     = JB.EMPLID
		    AND   EMPL_RCD  = JB.EMPL_RCD
		    AND   EFFDT      = JB.EFFDT)

AND    BPP.EFFDT = (SELECT MAX(EFFDT)
		    FROM PS_BEN_PROG_PARTIC 
		    WHERE EMPLID     = BPP.EMPLID
		    AND   EMPL_RCD  = BPP.EMPL_RCD
		    AND   EFFDT     <= $X000_Where_Select_AsofDate)

ORDER BY BPP.BENEFIT_PROGRAM, JB.EMPLID

end-SELECT

  If $Data-Found-Flag = 'N'
     Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
  Else
     do Format-Number(#emp_count,$emp_count,'9999')

     Print 'Total Employees Processed: '  (+2,1)
     Print $emp_count                     (0,+2)
  End-If

end-procedure

!*************************************************************************
begin-procedure Get-Life-Add-Ben 
#debug9 Show 'Get-Life-Add-Ben'

begin-SELECT
LA.PLAN_TYPE             &LA.PLAN_TYPE
   move &LA.PLAN_TYPE to $plan_type

   Do Print-Record

   Let $Data-Found-Flag = 'Y'


FROM PS_LIFE_ADD_BEN LA
WHERE LA.EMPLID         = $emplid
AND   LA.EMPL_RCD      = #empl_rcd
AND   LA.PLAN_TYPE     IN ('25','2Z')
AND   LA.COVERAGE_ELECT = 'E'
AND   LA.EFFDT = (SELECT MAX(EFFDT)
		  FROM PS_LIFE_ADD_BEN
		  WHERE EMPLID = $emplid
		  AND   EMPL_RCD = #empl_rcd
		  AND   PLAN_TYPE = LA.PLAN_TYPE
!SXK 06/23/1999 begin
!		  AND   COVERAGE_ELECT = 'E'
!SXK 06/23/1999 End
		  AND   EFFDT <= $X000_Where_Select_AsOfDate)
AND   NOT EXISTS
      (SELECT NULL
       FROM PS_LIFE_ADD_BEN xy
       WHERE xy.EMPLID = $emplid
       AND   xy.EMPL_RCD = #empl_rcd
       AND   xy.PLAN_TYPE = '21'
       AND   xy.COVERAGE_ELECT = 'E'
!SXK 06/23/1999 Begin
       and   xy.effdt = (SELECT MAX(EFFDT)
		  FROM PS_LIFE_ADD_BEN
		  WHERE EMPLID = $emplid
		  AND   EMPL_RCD = #empl_rcd
		  AND   PLAN_TYPE = xy.PLAN_TYPE
		  AND   EFFDT <= $X000_Where_Select_AsOfDate))
!SXK 06/23/1999 End
end-SELECT
end-procedure

!*********************************************************************
!*********************************************************************
begin-procedure Print-Record
#debug9 Show 'Print-Record'

   if #current-line > 51
      new-page
   end-if

   Let #emp_count = #emp_count + 1

   let $name = substr($name,1,30)

print $emplid             (+1,1)  !edit xxx-xx-xxxx ! GEX_SEC_REENG 2015-06-24 Vahini Katta  
print $name               (0,15)
print #empl_rcd           (0,50)  edit 9
print ' '                 (+1,1)

end-procedure

!*********************************************************************
begin-procedure End-Report
  
  Do Get-Current-DateTime
  Print 'SQR End Time '    (+2,1)
  Print $ReportDate            ()
  Print $ReportTime        (0,+2)

end-procedure

!**************************************************************
#include 'gexxx951.sqc'  !Get Oracle Database ID 
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'getempnm.sqc'  !Get-Employee-Name
!**************************************************************
