!***********************************************************************
! GEXBN024:  5500 Totals                                               *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXBN024 :            5500 Totals                                    *
!                                                                      *        
! Narrative:            This report shows the total number of union    *
!                       and non-union employees in each company along  *
!                       with the percentages of union and non-union    *
!                       employees compared to the total number of      *
!                       employees in each company.                     *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           * 
!                                                                      *  
! SQL Tables:           paygroup_tbl                                   *
!                       gex_dept_tbl                                   *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *       
!                                                                      *
! Normally Run:         Semi-Annually                                  *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Company/Paygroups (to include)               *
!                         Employee Statuses (to include)               *
!                         Jobcodes (to exclude)                        *
!                         As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS      DATE            DESCRIPTION OF THE CHANGE              *
!***********************************************************************
! JDH     06/08/98        Initial Creation                             *
! JDH     09/30/99        Modified according to new spec.              *
! SXK     12/13/99        Removed union cd from employment and         *
!				added it from job labor                *
!                                                                      *
! AXL     06/06/2001      Add TOL & COL to union code logic.           *
! SCM     8/23/2001       Add 'HGN' Hartville/Greed rank file to union *
!                             code logic.                              *
! CWB     02/01/2002      Add 'MOD' union code for Maryland stores     * 
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


#define #column1 001
#define #column2 013
#define #column3 026
#define #column4 057
#define #column5 071
#define #column6 085
#define #column7 100
#define #column8 113


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 7
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'

  Let $X000_temp_out = 'Report as of ' || $X000_Report_Heading_AsOfDate

  Print $X000_temp_out         (3,)  Center

  Print '     TOTAL'           (6,{#column4})
  Print 'COMPANY'              (+1,{#column1})
  Print 'PAYGROUP'             (+0,{#column2})
  Print 'DESCRIPTION'          (+0,{#column3})
  Print ' EMPLOYEES'           (+0,{#column4})
  Print '#NON-UNION'           (+0,{#column5})
  Print '    #UNION'           (+0,{#column6})
  Print '%NON-UNION'           (+0,{#column7})
  Print '    %UNION'           (+0,{#column8})
End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXBN024'
  Let $ReportTitle   =   '5500 TOTALS'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  Let $GEXXX901_Company_Alias  = 'j.company'
  Let $GEXXX901_Paygroup_Alias = 'j.paygroup'
  Let $GEXXX903_Emp_Stat_Alias = 'j.empl_status'
  Let $GEXXX916_Jobcode_Alias  = 'j.jobcode'

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Do Format-DateTime($X000_Where_Select_AsOfDate, $X000_Report_Heading_AsOfDate, {DEFDATE}, '', '')
  Let $X000_Order_By = 'Company, Paygroup'

  Show '$GEXXX901_Include_Company_Paygroup_Display_Criteria = ' $GEXXX901_Include_Company_Paygroup_Display_Criteria
  Show '$GEXXX903_Include_Emp_Stat_Display_Criteria         = ' $GEXXX903_Include_Emp_Stat_Display_Criteria
  Show '$GEXXX916_Exclude_Jobcode_Display_Criteria          = ' $GEXXX916_Exclude_Jobcode_Display_Criteria
  Show '$X000_Where_Select_AsOfDate = ' $X000_Where_Select_AsOfDate

  Do P130-Print-Cover-Page
End-Procedure


Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Do Ask-As-Of-Date
  Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate

  Display 'Enter Company/Paygroup or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''')'
      Else
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || $In-Paygroup || ''')'
      End-If
      Add 1 To #GEX_RC_PAYGROUP_ROWS
      If #GEX_RC_PAYGROUP_ROWS = 1
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While

  If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If

  Let $EmplStatusString = ''''
  Display 'Enter Employee Status or leave blank to exit.'
  While 1=1
    Input $In-EmplStatus Maxlen=1 'Employee Status'
    Uppercase $In-EmplStatus
    If Rtrim($In-EmplStatus, ' ') = ''
      Concat '''' With $EmplStatusString
      Break
    End-If
    If $EmplStatusString <> ''''
      Concat ''',''' With $EmplStatusString
    End-If
    Concat $In-EmplStatus With $EmplStatusString
  End-While

  If $EmplStatusString = ''''''
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = '1=1'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = $GEXXX903_Emp_Stat_Alias || ' In (' || $EmplStatusString || ')'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
  End-If

  Let $JobcodeString = ''''
  Display 'Enter Jobcodes to exclude or leave blank to exit.'
  While 1=1
    Input $In-Jobcode Maxlen=6 'Jobcode'
    Uppercase $In-Jobcode
    If Rtrim($In-Jobcode, ' ') = ''
      Concat '''' With $JobcodeString
      Break
    End-If
    If $JobcodeString <> ''''
      Concat ''',''' With $JobcodeString
    End-If
    Concat $In-Jobcode With $JobcodeString
  End-While

  If $JobcodeString = ''''''
    Let $GEXXX916_EXCLUDE_JOBCODE_CRITERIA = '1=1'
    Let $GEXXX916_EXCLUDE_JOBCODE_DISPLAY_CRITERIA = 'NONE'
  Else
    Let $GEXXX916_EXCLUDE_JOBCODE_CRITERIA = $GEXXX916_Jobcode_Alias || ' Not In (' || $JobcodeString || ')'
    Let $GEXXX916_EXCLUDE_JOBCODE_DISPLAY_CRITERIA = $GEXXX916_EXCLUDE_JOBCODE_CRITERIA
  End-If
End-Procedure


Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXRCBN1-SELECT-PARAMETERS

  Let $X000_Where_Select_AsOfDate = &GEX_RC_BEN.ASOFDATE
  Let $AsOfDate = $X000_WHERE_SELECT_ASOFDATE
  If Rtrim (&GEX_RC_BEN.ASOFDATE, ' ') = ''
    Let $X000_Where_Select_AsOfDate = $AsOfToday
    Let $AsOfDate = $AsOfToday
  End-If

  Do GEXXX901-Select-Company-Paygroup-Parameters
  Do GEXXX903-Select-Emp-Stat-Parameters
  Do GEXXX916-Select-Jobcode-Parameters
End-Procedure


begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX901_Include_Company_Paygroup_Display_Criteria ='   (+1,5)
  Print $GEXXX901_Include_Company_Paygroup_Display_Criteria       (0,+2)
  Print '$GEXXX903_Include_Emp_Stat_Display_Criteria         ='   (+1,5)
  Print $GEXXX903_Include_Emp_Stat_Display_Criteria               (0,+2)
  Print '$GEXXX916_Exclude_Jobcode_Display_Criteria          ='   (+1,5)
  Print $GEXXX916_Exclude_Jobcode_Display_Criteria                (0,+2)

  Print 'As Of Date :'                      (+2,5)
  Print $X000_Report_Heading_AsOfDate       (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'   (+5,2)
  Print $X000_Order_By                      (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select
j.company    ()  On-Break  Level=1
                           Print=Never

j.paygroup   ()  On-Break  Level=2
                           Print=Never
                           After=P230-After-Paygroup-Break
!SXK 12/13/1999 BEgin
!e.union_cd
jl.union_Cd
!SXK 12/13/1999 End
count(distinct j.emplid)  &count

  Let $Data-Found-Flag = 'Y'

  Let $company  = &j.company
  Let $paygroup = &j.paygroup

  Add &count To #union-non-union-count

  If &jl.union_cd = 'NON' Or 
     &jl.union_cd = 'SEC' Or
     &jl.union_cd = 'COL' Or	!AXL 6/6/2001 - T.45.0755 Toledo add
     &jl.union_cd = 'HGN' Or	!SCM 8/23/2001 -T.45.1085 Hartville/Greed rank file ee's -ADD
     &jl.union_cd = 'TOL' Or	!AXL 6/6/2001 - T.45.0755 Toledo add
     &jl.union_cd = 'MOD' 	!CWB 2/01/2002 -  Maryland add
    Add &count To #non-union-count
  Else
    Add &count To #union-count
  End-If

From   ps_gex_dept_tbl  gdt,
!SXK 12/13/1999 Begin 
!       ps_employment    e,
       ps_job_labor jl,
!SXK 12/13/1999 End
       ps_job           j

Where  j.effdt                 =
      (Select Max(effdt)
       From   ps_job
       Where  emplid           = j.emplid
       And    empl_rcd        = j.empl_rcd
       And    effdt           <= $X000_Where_Select_AsOfDate)

And    j.effseq                =
      (Select Max(effseq)
       From   ps_job
       Where  emplid           = j.emplid
       And    empl_rcd        = j.empl_rcd
       And    effdt            = j.effdt)

!SXK 12/13/1999 Begin
!And    e.emplid                = j.emplid
!And    e.empl_rcd             = j.empl_rcd

And    jl.emplid              = j.emplid
And    jl.empl_rcd           = j.empl_rcd

And    jl.effdt                 =
      (Select Max(effdt)
       From   ps_job_labor
       Where  emplid           = jl.emplid
       And    empl_rcd        = jl.empl_rcd
       And    effdt           <= $X000_Where_Select_AsOfDate)

And    jl.effseq                =
      (Select Max(effseq)
       From   ps_job_labor
       Where  emplid           = jl.emplid
       And    empl_rcd        = jl.empl_rcd
       And    effdt            = jl.effdt)

!SXK 12/13/99 End

And    gdt.gex_company_div_cd <> 'IND'

And    gdt.deptid              = j.deptid

And    gdt.effdt               =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid           = gdt.deptid
       And    effdt           <= $X000_Where_Select_AsOfDate)

And    gdt.sequence_number     =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid           = gdt.deptid
       And    effdt            = gdt.effdt)

And   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
And   [$GEXXX903_Include_Emp_Stat_Criteria]
And   [$GEXXX916_Exclude_Jobcode_Criteria]

Group By j.company, j.paygroup, jl.union_cd
Order By j.company, j.paygroup
End-Select
End-Procedure


Begin-Procedure P230-After-Paygroup-Break
#debug9 Show 'P230-After-Paygroup-Break'
  Do Get-Paygroup-Data

  If #union-non-union-count = 0
    Let #percent-non-union = 0
    Let #percent-union = 0
  Else  
    Let #percent-non-union = (#non-union-count / #union-non-union-count) * 100
    Let #percent-union = 100 - #percent-non-union
  End-If

  Print $company               (+1,{#column1})
  Print $paygroup              (+0,{#column2})
  Print $PayGroupName          (+0,{#column3})
  Print #union-non-union-count (+0,{#column4}) Edit 99,999,999
  Print #non-union-count       (+0,{#column5}) Edit 99,999,999
  Print #union-count           (+0,{#column6}) Edit 99,999,999
  Print #percent-non-union     (+0,{#column7}) Edit 9999999.99
  Print #percent-union         (+0,{#column8}) Edit 9999999.99

  Add #union-non-union-count To #total-union-non-union-count
  Add #non-union-count       To #total-non-union-count
  Add #union-count           To #total-union-count

  Let #union-non-union-count = 0
  Let #non-union-count       = 0
  Let #union-count           = 0
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  Else
    If #total-union-non-union-count = 0
      Let #total-percent-non-union = 0
      Let #total-percent-union = 0
    Else  
      Let #total-percent-non-union = (#total-non-union-count / #total-union-non-union-count) * 100
      Let #total-percent-union = 100 - #total-percent-non-union
    End-If
    Print 'TOTALS'                     (+2,{#column1})
    Print #total-union-non-union-count (+0,{#column4})  Edit 99,999,999
    Print #total-non-union-count       (+0,{#column5})  Edit 99,999,999
    Print #total-union-count           (+0,{#column6})  Edit 99,999,999
    Print #total-percent-non-union     (+0,{#column7})  Edit 9999999.99
    Print #total-percent-union         (+0,{#column8})  Edit 9999999.99
  End-If

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  show ' '

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
End-Procedure


#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx916.sqc'  !Get jobcode multiple row table
#include 'gexxx920.sqc'  !Get ben single row run control
#include 'getpgdta.sqc'  !Get-paygroup-data procedure
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
