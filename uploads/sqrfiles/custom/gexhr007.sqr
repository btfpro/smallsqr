!***********************************************************************
!  GEXHR007:  SENIORITY LIST                                           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXHR007:             Seniority List                                 *
!                                                                      *
! Narrative:            This program lists employees' hire date        *
!                        information.                                  *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           PERSONAL_DATA                                  *
!                       EMPLOYMENT                                     *
!                       JOB                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Monthly                                        *
!                                                                      *
! Control Parms:        AsOfDate                                       *
!                       Company(s)                                     *
!                       Deptid(s)                                      *
!                       Union Code(s)                                  *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  JDH          10/05/99     Initial Creation                          *
!                                                                      *
!  AXL          02/03/2000   Modified to get union info from job_labor 
!  AXC		08/01/2002   Modified to get union info from JOb. 8.3
!				Upgrade				       *
! VENDTKK       02/12/2009 Added INCLUDE GETPOSNM.SQC to pull          *
!                  	           JOBTITLE from POSITION_DATA.DESCR   *
!				   field instead of JOBCODE_TBL.DESCR  *
!			           	                               *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Lanscape)


#define #column1     1
#define #column2    13
#define #column3    24
#define #column4    36
#define #column5    52
#define #column6    87
#define #column7   101
#define #column8   112
#define #column9   127
#define #column10  142
#define #column11  157


Begin-Heading 9
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'

  print 'Union Cd:'           (3,1)
  print $union_cd             ( ,13)

  print 'ACCOUNT'             (6,{#column4})
  print 'UNION'               ( ,{#column10})
  print 'COMPANY'             ( ,{#column11})

  print 'ACCOUNT'             (7,{#column3})
  print 'CODE'                ( ,{#column4})
  print 'JOB'                 ( ,{#column7})
  print 'JOB'                 ( ,{#column8})
  print 'SERVICE'             ( ,{#column9})
  print 'SENIORITY'           ( ,{#column10})
  print 'SENIORITY'           ( ,{#column11})

  print 'COMPANY'             (8,{#column1})
  print 'DEPTID'              ( ,{#column2})
  print 'CODE'                ( ,{#column3})
  print 'DESCRIPTION'         ( ,{#column4})
  print 'NAME'                ( ,{#column5})
  print 'EMPLID'              ( ,{#column6})
  print 'CODE'                ( ,{#column7})
  print 'TITLE'               ( ,{#column8})
  print 'DATE'                ( ,{#column9})
  print 'DATE'                ( ,{#column10})
  print 'DATE'                ( ,{#column11})

  Print '-'                   (9,{#column1} , 7)  Fill
  Print '-'                   ( ,{#column2} , 6)  Fill
  Print '-'                   ( ,{#column3} , 7)  Fill
  Print '-'                   ( ,{#column4} ,11)  Fill
  Print '-'                   ( ,{#column5} ,30)  Fill
  Print '-'                   ( ,{#column6} , 9)  Fill
  Print '-'                   ( ,{#column7} , 6)  Fill
  Print '-'                   ( ,{#column8} ,10)  Fill
  Print '-'                   ( ,{#column9} ,10)  Fill
  Print '-'                   ( ,{#column10},10)  Fill
  Print '-'                   ( ,{#column11},10)  Fill
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXHR007'
  Let $ReportTitle   = 'SENIORITY LIST'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  Let $GEXXX900_Company_Alias  = 'J.COMPANY'
  Let $GEXXX902_Deptid_Alias   = 'J.DEPTID'
  Let $GEXXX913_UNION_CD_Alias = 'J.UNION_CD'

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    do GEXRCHR1-SELECT-PARAMETERS
    let $AsOfDate = $GEX_RC_HR.ASOFDATE
    If Rtrim($AsOfDate,' ') = ''
      Let $AsOfDate = $AsOfToday
    End-If

    do GEXXX900-Select-Company-Parameters
    do GEXXX902-Select-Deptid-Parameters
    do GEXXX913-Select-UNION-CD-Parameters
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Let $X000_Order_By = 'Union Code, Union Seniority Date, Name'
  do P130-Print-Cover-Page
End-Procedure


Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Do Ask-As-Of-Date

  Let $CompanyString = ''''
  Display 'Enter Company or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Concat '''' With $CompanyString
      Break
    End-If
    If $CompanyString <> ''''
      Concat ''',''' With $CompanyString
    End-If
    Concat $In-Company With $CompanyString
  End-While

  If $CompanyString = ''''''
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '1=1'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_Company_Alias || ' In (' || $CompanyString || ')'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA
  End-If


  Let $DeptIDString = ''''
  Display 'Enter DeptID or leave blank to exit.'
  While 1=1
    Input $In-DeptID Maxlen=10 'DeptID'
    Uppercase $In-DeptID
    If Rtrim($In-DeptID, ' ') = ''
      Concat '''' With $DeptIDString
      Break
    End-If
    If $DeptIDString <> ''''
      Concat ''',''' With $DeptIDString
    End-If
    Concat $In-DeptID With $DeptIDString
  End-While

  If $DeptIDString = ''''''
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
  End-If


  Let $UnionCdString = ''''
  Display 'Enter Union Code or leave blank to exit.'
  While 1=1
    Input $In-UnionCd Maxlen=3 'Union Code'
    Uppercase $In-UnionCd
    If Rtrim($In-UnionCd, ' ') = ''
      Concat '''' With $UnionCdString
      Break
    End-If
    If $UnionCdString <> ''''
      Concat ''',''' With $UnionCdString
    End-If
    Concat $In-UnionCd With $UnionCdString
  End-While

  If $UnionCdString = ''''''
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_Union_CD_Alias || ' In (' || $UnionCdString || ')'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  End-If
End-Procedure


begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX900_Include_Company_Display_Criteria          ='   (+1,5)
  Print $GEXXX900_Include_Company_Display_Criteria                (0,+2)
  Print '$GEXXX902_Include_Deptid_Display_Criteria           ='   (+1,5)
  Print $GEXXX902_Include_Deptid_Display_Criteria	          (0,+2)
  Print '$GEXXX913_Include_Union_Cd_Display_Criteria         ='   (+1,5)
  Print $GEXXX913_Include_Union_Cd_Display_Criteria               (0,+2)

  Print 'As Of Date         ='              (+2,5)
  Print $AsOfDate                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'   (+5,2)
  Print $X000_Order_By                      (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select
!AXL 2/3/2000 7.5 Table Changes Begin
J.UNION_CD  ()  On-Break  Level=1
                          Print=Never
                          After=P210-New-Page
!AXL 2/3/2000 7.5 Table Changes End
J.COMPANY
J.DEPTID
J.JOBCODE
J.ACCT_CD
J.EMPLID
PD.NAME
E.SERVICE_DT
E.CMPNY_SENIORITY_DT
J.UNION_SENIORITY_DT	!AXL 2/3/2000 7.5 Table Changes
J.Position_nbr 			!VENDTKK ITG#56786   12-Feb-2009
  Let $Data-Found-Flag = 'Y'

  Let $union_cd = &J.UNION_CD	!AXL 2/3/2000 7.5 Table Changes
  Let $acctcd   = &J.ACCT_CD
  Let $jobcode  = &J.JOBCODE

  do Get-Acct-cd-descr
  do Get-Job-Title
  
   !VENDTKK Begin   ITG#56786   12-Feb-2009
        Let $Position_Nbr = &J.Position_nbr
 	 Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosNameAbbrv = '' 
  		Let $JobTitleAbbrv = $JobTitleAbbrv
	 Else
 		Let $JobTitleAbbrv = $PosNameAbbrv
	 End-if
  !VENDTKK End   ITG#56786  12-Feb-2009

  do P220-Print-Record

FROM   PS_PERSONAL_DATA PD,
       PS_EMPLOYMENT    E,
       PS_JOB           J
!       PS_JOB_LABOR     JL	!AXL 2/3/2000 7.5 Table Changes

WHERE  PD.EMPLID              = J.EMPLID

AND    E.EMPLID               = J.EMPLID
AND    E.EMPL_RCD            = J.EMPL_RCD
!AND    J.EMPLID               = JL.EMPLID	!AXL 2/3/2000 7.5 Table Changes
!AND    J.EMPL_RCD            = JL.EMPL_RCD	!AXL 2/3/2000 7.5 Table Changes

AND    J.EFFDT                =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID          = J.EMPLID
       AND    EMPL_RCD       = J.EMPL_RCD
       AND    EFFDT          <= $AsOfDate)

AND    J.EFFSEQ               =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB
       WHERE  EMPLID          = J.EMPLID
       AND    EMPL_RCD       = J.EMPL_RCD
       AND    EFFDT           = J.EFFDT)
!AXL 2/3/2000 7.5 Table Changes Begin
! and  jl.effdt                   =
!      (Select Max(effdt)
!       From   ps_job_labor
!       Where  emplid             = jl.emplid
!       And    empl_rcd          = jl.empl_rcd
!       And    effdt             <= $AsOfDate)
!  and  jl.effseq                  =
!      (Select Max(effseq)
!       From   ps_job_labor
!       Where  emplid             = jl.emplid
!       And    empl_rcd          = jl.empl_rcd
!       And    effdt              = jl.effdt)

AND   [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND   [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]

ORDER BY J.UNION_CD, NVL(J.UNION_SENIORITY_DT,E.SERVICE_DT), PD.NAME
!AXL 2/3/2000 7.5 Table Changes End
End-Select
End-Procedure


Begin-Procedure P210-New-Page
#debug9 Show 'P210-New-Page'
  New-Page
End-Procedure


Begin-Procedure P220-Print-Record
#debug9 Show 'P220-Print-Record'
  Do Format-DateTime(&E.SERVICE_DT, $service_dt, {DEFDATE}, '', '')
  Do Format-DateTime(&E.CMPNY_SENIORITY_DT, $cmpny_seniority_dt, {DEFDATE}, '', '')
  Do Format-DateTime(&J.UNION_SENIORITY_DT, $union_seniority_dt, {DEFDATE}, '', '')

  Print &J.COMPANY           (+1,{#column1})
  Print &J.DEPTID            (  ,{#column2})
  Print &J.ACCT_CD           (  ,{#column3})
  Print $AcctNameAbbrv       (  ,{#column4})
  Print &PD.NAME             (  ,{#column5},30)
  Print &J.EMPLID            (  ,{#column6})
  Print &J.JOBCODE           (  ,{#column7})
  Print $JobTitleAbbrv       (  ,{#column8})
  Print $service_dt          (  ,{#column9})
  Print $union_seniority_dt  (  ,{#column10})
  Print $cmpny_seniority_dt  (  ,{#column11})
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
End-Procedure


#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'gexxx900.sqc'  !Get multiple company run controls
#Include 'gexxx902.sqc'  !Get multiple deptid run controls
#Include 'gexxx913.sqc'  !Get multiple union_cd run controls
#Include 'gexxx921.sqc'  !Get GEX_RC_HR run controls
#Include 'getjobtl.sqc'  !Get-Job-Title procedure
#Include 'getactnm.sqc'  !Get-Acct-cd-descr procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   12-Feb-2009  