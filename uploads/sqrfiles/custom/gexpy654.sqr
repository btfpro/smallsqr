!***********************************************************************
!  GEXPY654:  Manulife 401K Interface, Weekly Report and File                                   *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY654:             Manulife 401K Interface                        *
!                                                                      *
! Narrative:		This program creates a contribution file and   *
!                        an address file for Manulife Financial        *
!                        containing 401K data.                         *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			#debug8 key variable values                    *
!			#debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_calendar                                   *
!                       pay_deduction                                  *
!                       personal_data                                  *
!                                                                      *
! Written by:		Carl Buckrop                                   *
!                                                                      *
! Normally Run:		Weekly                                         *
!                                                                      *
! Control Parms:	FOR WHERE CLAUSE:                              *
!                         Multiple Deptid's                            *
!                         AsOfDate                                     *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!   CWB       11/24/2003   Cloned from GEXPY631 to produce a weekly    *
!                          file and report in addition to the monthly  *
!                          GEXPY631 file.                              *
!                                                                      *
!***********************************************************************


#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization
!#Define OUTFILE /apps/hr/hrms90/hrdev90/outgoing/	!For Testing

Begin-Setup
  Declare-Report Contribution-Report
!Printer-Type=HPLASERJET
    Layout=Default
  End-Declare

  Declare-Report Address-Report
!Printer-Type=HPLASERJET
    Layout=Default
  End-Declare
End-Setup


#define #column1c     1
#define #column2c    11
#define #column3c    32
#define #column4c    41
#define #column5c    50
#define #column6c    59
#define #column7c    68
#define #column8c    77
#define #column9c    86
#define #column10c   95
#define #column11c  104
#define #column12c  113
#define #column13c  122
#define #column14c  131
#define #column15c  140
#define #column16c  149
#define #column17c  158
#define #column18c  167


#define #column1a     1
#define #column2a    24
#define #column3a    47
#define #column4a    59
#define #column5a    92
#define #column6a   125
#define #column7a   153
#define #column8a   161


Begin-Heading 7  For-Reports=(Contribution-Report)
#debug9 Show 'Begin-Heading'
#Include 'stdhdg01.sqc'

  Print $Reporting_Period      (3,) Center 

  Print 'SSN'                  (+3,{#column1c})
  Print 'NAME'                 (+0,{#column2c})
  Print '   EEDEF'             (+0,{#column3c})
  Print '   EEMAN'             (+0,{#column4c})
  Print '    QMAC'             (+0,{#column5c})
  Print '   ERMAT'             (+0,{#column6c})
  Print '    ERPS'             (+0,{#column7c})
  Print '   ERCON'             (+0,{#column8c})
  Print '    QNEC'             (+0,{#column9c})
  Print '   EEVND'             (+0,{#column10c})
  Print '    EEVD'             (+0,{#column11c})
  Print '    EERC'             (+0,{#column12c})
  Print ' REPAY 1'             (+0,{#column13c})
  Print ' REPAY 2'             (+0,{#column14c})
  Print ' REPAY 3'             (+0,{#column15c})
  Print ' REPAY 4'             (+0,{#column16c})
  Print ' REPAY 5'             (+0,{#column17c})
  Print ' REPAY 6'             (+0,{#column18c})

  Print '-'                    (+1,{#column1c},9)   Fill
  Print '-'                    (+0,{#column2c},20)  Fill
  Print '-'                    (+0,{#column3c},8)   Fill
  Print '-'                    (+0,{#column4c},8)   Fill
  Print '-'                    (+0,{#column5c},8)   Fill
  Print '-'                    (+0,{#column6c},8)   Fill
  Print '-'                    (+0,{#column7c},8)   Fill
  Print '-'                    (+0,{#column8c},8)   Fill
  Print '-'                    (+0,{#column9c},8)   Fill
  Print '-'                    (+0,{#column10c},8)  Fill
  Print '-'                    (+0,{#column11c},8)  Fill
  Print '-'                    (+0,{#column12c},8)  Fill
  Print '-'                    (+0,{#column13c},8)  Fill
  Print '-'                    (+0,{#column14c},8)  Fill
  Print '-'                    (+0,{#column15c},8)  Fill
  Print '-'                    (+0,{#column16c},8)  Fill
  Print '-'                    (+0,{#column17c},8)  Fill
  Print '-'                    (+0,{#column18c},8)  Fill
End-Heading


Begin-Heading 7  For-Reports=(Address-Report)
#debug9 Show 'Begin-Heading'
#Include 'stdhdg01.sqc'

  Print $Reporting_Period      (3,) Center 

  Print 'Last Name'            (+3,{#column1a})
  Print 'First Name'           (+0,{#column2a})
  Print 'SSN'                  (+0,{#column3a})
  Print 'Address 1'            (+0,{#column4a})
  Print 'Address 2'            (+0,{#column5a})
  Print 'City'                 (+0,{#column6a})
  Print 'State'                (+0,{#column7a})
  Print 'Zip Code'             (+0,{#column8a})

  Print '-'                    (+1,{#column1a},20)  Fill
  Print '-'                    (+0,{#column2a},20)  Fill
  Print '-'                    (+0,{#column3a},9)   Fill
  Print '-'                    (+0,{#column4a},30)  Fill
  Print '-'                    (+0,{#column5a},30)  Fill
  Print '-'                    (+0,{#column6a},25)  Fill
  Print '-'                    (+0,{#column7a},5)   Fill
  Print '-'                    (+0,{#column8a},10)  Fill
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'
! Do Set-Optimizer-Goal

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Do P100-Start
  Do P200-Main-Process
  Do P300-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report


Begin-Procedure Set-Optimizer-Goal
  Begin-SQL
    Alter Session Set OPTIMIZER_GOAL=RULE;
  End-SQL
End-Procedure


Begin-Procedure P100-Start
#debug9 Show 'P100-Start'
  Move 'GEXPY654'                To $ReportID
  Move 'MANULIFE 401K WEEKLY INTERFACE' To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show ' '

  Let $GEXXX902_Deptid_Alias = 'pc.deptid'

  if $prcs_process_instance = ''
    Do Ask-As-Of-Date
    Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate

    Let $DeptIDString = ''''
    Display 'Enter DeptID or leave blank to exit.'
    While 1=1
      Input $In-DeptID Maxlen=10 'DeptID'
      Uppercase $In-DeptID
      If Rtrim($In-DeptID, ' ') = ''
        Concat '''' With $DeptIDString
        Break
      End-If
      If $DeptIDString <> ''''
        Concat ''',''' With $DeptIDString
      End-If
      Concat $In-DeptID With $DeptIDString
    End-While

    If $DeptIDString = ''''''
      Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
      Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
      Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
    End-If

  else

    Do GEXXX902-Select-Deptid-Parameters
    Do GEXXX922-Select-Parameters
    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate
    If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  end-if

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')

  Show ' '
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = '  $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$X000_WHERE_SELECT_ASOFDATE               = '  $X000_WHERE_SELECT_ASOFDATE
  Show ' '

  Let $X000_ORDER_BY = 'SSN'

  Do P140-Get-Last-Confirmed-Pay

  Use-Report Contribution-Report
  Do P150-Print-Cover-Page
  Use-Report Address-Report
  Do P150-Print-Cover-Page

  Do P160-Open-File
  Do P170-Constants
End-Procedure


Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'
Begin-Select Loops=1
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'MMDDYYYY')               &payenddt
To_Char(pay_end_dt,'fmMonth YYYY')           &period

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let $payenddt         = &payenddt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = 'Week Ending ' || $pay_end_dt

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  Else
!    Show 'Report will contain balances for the month of ' $Reporting_Period
    Show 'Report will contain deductions for the week ending ' $pay_end_dt
    Show 'Last confirmed pay end date:  ' $pay_end_dt
  End-If

From   ps_pay_calendar

Where  pay_end_dt              =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt      <= $X000_WHERE_SELECT_ASOFDATE
       And    pay_confirm_run  = 'Y')

And    pay_confirm_run         = 'Y'
End-Select
End-Procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+1,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,2)
  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA ='         (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	      (0,+2)
  Print 'As Of Date :'                                        (+1,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)

!  Print 'Report will contain balances for the month of'       (+5,2)
  Print 'Report will contain deductions for the week ending'   (+5,2)
!  Print $Reporting_Period                                     (0,+1)
  Print $pay_end_dt                                           (0,+1)
  Print 'Last confirmed pay end date:'                        (+1,5)
  Print $pay_end_dt                                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+1,5)

  Let #PAGE-COUNT = 0
  New-Page
End-Procedure


Begin-Procedure P160-Open-File
#debug9 Show 'P160-Open-File'
  Let $File1 = '{OUTFILE}' || Lower($ReportID) || '.dat'
  Let $File2 = '{OUTFILE}' || Lower($ReportID) || '.da2'

  Open $File1
    As 1
    For-Writing
    Record=242:Fixed
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Opening output file.  Program terminating.'
    Stop
  End-If


  Open $File2
    As 2
    For-Writing
    Record=181:Fixed
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Opening output file.  Program terminating.'
    Stop
  End-If
End-Procedure


Begin-Procedure P170-Constants
#debug9 Show 'P170-Constants'
  Let $Transaction_Code    = '00000405'
  Let $Contract_Number     = '70431'
  Let $Contribution_Period = $payenddt
  Let $Loan_ID_1           = '000001'
  Let $Loan_ID_2           = '000002'
  Let $Loan_ID_3           = '000003'
  Let $Loan_ID_4           = '000004'
  Let $Loan_ID_5           = '000005'
  Let $Loan_ID_6           = '000006'
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
  Let $Data-Found-Flag = 'N'

Begin-Select
pc.emplid  ()  On-Break  Level=1
                         Print=Never
                         Before=P210-Before-Emplid
                         After=P220-After-Emplid
pd.dedcd
Sum(pd.ded_cur) &pd.ded_cur

  Let $Data-Found-Flag = 'Y'

  Let $SSN = &pc.emplid

  Evaluate &pd.dedcd
    When = '4MANUF'
      Let #EEDEF = &pd.ded_cur
    When = 'LOAN01'
      Let #Loan_Repay_1 = &pd.ded_cur
    When = 'LOAN02'
      Let #Loan_Repay_2 = &pd.ded_cur
    When = 'LOAN03'
      Let #Loan_Repay_3 = &pd.ded_cur
    When = 'LOAN04'
      Let #Loan_Repay_4 = &pd.ded_cur
    When = 'LOAN05'
      Let #Loan_Repay_5 = &pd.ded_cur
    When = 'LOAN06'
      Let #Loan_Repay_6 = &pd.ded_cur
  End-Evaluate

From   ps_pay_check      pc,
       ps_pay_deduction  pd

Where  pc.company           = pd.company
And    pc.paygroup          = pd.paygroup
And    pc.pay_end_dt        = pd.pay_end_dt
And    pc.off_cycle         = pd.off_cycle
And    pc.page_num             = pd.page_num
And    pc.line_num             = pd.line_num
And    pc.sepchk            = pd.sepchk

and    PC.PAY_END_DT        = $pay_end_dt 

And    pd.dedcd            In ('4MANUF',
                               'LOAN01','LOAN02','LOAN03',
                               'LOAN04','LOAN05','LOAN06')

And   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]

Group By pc.emplid, pd.dedcd
Order By pc.emplid
End-Select
End-Procedure


Begin-Procedure P210-Before-Emplid
#debug9 Show 'P210-Before-Emplid'
  Let #EEDEF = 0
  Let #EEMAN = 0
  Let #QMAC  = 0
  Let #ERMAT = 0
  Let #ERPS  = 0
  Let #ERCON = 0
  Let #QNEC  = 0
  Let #EEVND = 0
  Let #EEVD  = 0
  Let #EERC  = 0

  Let #Loan_Repay_1 = 0
  Let #Loan_Repay_2 = 0
  Let #Loan_Repay_3 = 0
  Let #Loan_Repay_4 = 0
  Let #Loan_Repay_5 = 0
  Let #Loan_Repay_6 = 0
End-Procedure


Begin-Procedure P220-After-Emplid
#debug9 Show 'P220-After-Emplid'
  Do P230-Personal-Data

  Let $Employee_Name = &pd2.name

  Let $Name = &pd2.name
  Do Rotate-Name

  Do P250-Accumulate-Totals
  Do P260-Print-Record
  Do P270-Write-Record
End-Procedure


Begin-Procedure P230-Personal-Data
#debug9 Show 'P230-Personal-Data'
Begin-Select
pd2.name
pd2.address1
pd2.address2
pd2.city
pd2.state
pd2.postal
From   ps_personal_data pd2
Where  pd2.emplid = $SSN
End-Select
End-Procedure


Begin-Procedure P250-Accumulate-Totals
#debug9 Show 'P250-Accumulate-Totals'
  Add #EEDEF To #Total_EEDEF
  Add #EEMAN To #Total_EEMAN
  Add #QMAC  To #Total_QMAC
  Add #ERMAT To #Total_ERMAT
  Add #ERPS  To #Total_ERPS
  Add #ERCON To #Total_ERCON
  Add #QNEC  To #Total_QNEC
  Add #EEVND To #Total_EEVND
  Add #EEVD  To #Total_EEVD
  Add #EERC  To #Total_EERC

  Add #Loan_Repay_1 To #Total_Loan_Repay_1
  Add #Loan_Repay_2 To #Total_Loan_Repay_2
  Add #Loan_Repay_3 To #Total_Loan_Repay_3
  Add #Loan_Repay_4 To #Total_Loan_Repay_4
  Add #Loan_Repay_5 To #Total_Loan_Repay_5
  Add #Loan_Repay_6 To #Total_Loan_Repay_6
End-Procedure


Begin-Procedure P260-Print-Record
#debug9 Show 'P260-Print-Record'
  Use-Report Contribution-Report
  Print $SSN                  (+1,{#column1c})
  Print $Employee_Name        (+0,{#column2c},20)
  Print #EEDEF                (+0,{#column3c})   Edit 9,990.00
  Print #EEMAN                (+0,{#column4c})   Edit 9,990.00
  Print #QMAC                 (+0,{#column5c})   Edit 9,990.00
  Print #ERMAT                (+0,{#column6c})   Edit 9,990.00
  Print #ERPS                 (+0,{#column7c})   Edit 9,990.00
  Print #ERCON                (+0,{#column8c})   Edit 9,990.00
  Print #QNEC                 (+0,{#column9c})   Edit 9,990.00
  Print #EEVND                (+0,{#column10c})  Edit 9,990.00
  Print #EEVD                 (+0,{#column11c})  Edit 9,990.00
  Print #EERC                 (+0,{#column12c})  Edit 9,990.00
  Print #Loan_Repay_1         (+0,{#column13c})  Edit 9,990.00
  Print #Loan_Repay_2         (+0,{#column14c})  Edit 9,990.00
  Print #Loan_Repay_3         (+0,{#column15c})  Edit 9,990.00
  Print #Loan_Repay_4         (+0,{#column16c})  Edit 9,990.00
  Print #Loan_Repay_5         (+0,{#column17c})  Edit 9,990.00
  Print #Loan_Repay_6         (+0,{#column18c})  Edit 9,990.00


  Use-Report Address-Report
  Print $LastName             (+1,{#column1a})
  Print $FirstName            (+0,{#column2a})
  Print $SSN                  (+0,{#column3a})
  Print &pd2.address1         (+0,{#column4a})
  Print &pd2.address2         (+0,{#column5a})
  Print &pd2.city             (+0,{#column6a})
  Print &pd2.state            (+0,{#column7a})
  Print &pd2.postal           (+0,{#column8a})
End-Procedure


Begin-Procedure P270-Write-Record
#debug9 Show 'P270-Write-Record'
  Move #EEDEF To $EEDEF 999990.00
  Move #EEMAN To $EEMAN 999990.00
  Move #QMAC  To $QMAC  999990.00
  Move #ERMAT To $ERMAT 999990.00
  Move #ERPS  To $ERPS  999990.00
  Move #ERCON To $ERCON 999990.00
  Move #QNEC  To $QNEC  999990.00
  Move #EEVND To $EEVND 999990.00
  Move #EEVD  To $EEVD  999990.00
  Move #EERC  To $EERC  999990.00

  Move #Loan_Repay_1 To $Loan_Repay_1 999990.00
  Move #Loan_Repay_2 To $Loan_Repay_2 999990.00
  Move #Loan_Repay_3 To $Loan_Repay_3 999990.00
  Move #Loan_Repay_4 To $Loan_Repay_4 999990.00
  Move #Loan_Repay_5 To $Loan_Repay_5 999990.00
  Move #Loan_Repay_6 To $Loan_Repay_6 999990.00

  Add 1 To #recs-written

  Write 1 From $Transaction_Code:8       !Column   1 -   8
               $Contract_Number:5        !Column   9 -  13
               $SSN:9                    !Column  14 -  22
               $Employee_Name:32         !Column  23 -  54
               $Contribution_Period:8    !Column  55 -  62
               $EEDEF:9                  !Column  63 -  71
               $EEMAN:9                  !Column  72 -  80
               $QMAC:9                   !Column  81 -  89
               $ERMAT:9                  !Column  90 -  98
               $ERPS:9                   !Column  99 - 107
               $ERCON:9                  !Column 108 - 116
               $QNEC:9                   !Column 117 - 125
               $EEVND:9                  !Column 126 - 134
               $EEVD:9                   !Column 135 - 143
               $EERC:9                   !Column 144 - 152
               $Loan_ID_1:6              !Column 153 - 158
               $Loan_Repay_1:9           !Column 159 - 167
               $Loan_ID_2:6              !Column 168 - 173
               $Loan_Repay_2:9           !Column 174 - 182
               $Loan_ID_3:6              !Column 183 - 188
               $Loan_Repay_3:9           !Column 189 - 197
               $Loan_ID_4:6              !Column 198 - 203
               $Loan_Repay_4:9           !Column 204 - 212
               $Loan_ID_5:6              !Column 213 - 218
               $Loan_Repay_5:9           !Column 219 - 227
               $Loan_ID_6:6              !Column 228 - 233
               $Loan_Repay_6:9           !Column 234 - 242
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Writing output file.  Program terminating.'
    Stop
  End-If


  Add 1 To #recs-written-2

  Write 2 From $Contract_Number:5        !Column   1 -   5
               $LastName:20              !Column   6 -  25
               $FirstName:20             !Column  26 -  45
               $SSN:9                    !Column  46 -  54
               &pd2.address1:30          !Column  55 -  84
               &pd2.address2:30          !Column  85 - 114
               &pd2.city:25              !Column 115 - 139
               &pd2.state:2              !Column 140 - 141
               &pd2.postal:10            !Column 142 - 151
               ' ':30                    !Column 152 - 181

  If #Filestat != 0
    Display 'Error Writing output file.  Program terminating.'
    Stop
  End-If
End-Procedure


Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'
  Do P310-Close-File
  Do P320-Display-File-Totals
  Do P330-Print-Report-Totals

  Do P400-Email-File                     

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  show ' '
End-Procedure


Begin-Procedure P310-Close-File
#debug9 Show 'P310-Close-File'
  Close 1
  Close 2
End-Procedure


Begin-Procedure P320-Display-File-Totals
#debug9 Show 'P320-Display-File-Totals'
  Move #recs-written   To $recs-written     888,888,888
  Move #recs-written-2 To $recs-written-2   888,888,888

  Display ''
  Display $recs-written          Noline
  Display ' records written to ' Noline
  Display $File1
  Display $recs-written-2        Noline
  Display ' records written to ' Noline
  Display $File2
  Display ''
End-Procedure


Begin-Procedure P330-Print-Report-Totals
#debug9 Show 'P330-Print-Report-Totals'
  If $Data-Found-Flag <> 'Y'
    Use-Report Contribution-Report
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
    Use-Report Address-Report
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  Else
    Do P340-Print-Totals
  End-If
End-Procedure


Begin-Procedure P340-Print-Totals
#debug9 Show 'P340-Print-Totals'
  Use-Report Contribution-Report

  Print '-'                    (+2,{#column3c},8)   Fill
  Print '-'                    (+0,{#column4c},8)   Fill
  Print '-'                    (+0,{#column5c},8)   Fill
  Print '-'                    (+0,{#column6c},8)   Fill
  Print '-'                    (+0,{#column7c},8)   Fill
  Print '-'                    (+0,{#column8c},8)   Fill
  Print '-'                    (+0,{#column9c},8)   Fill
  Print '-'                    (+0,{#column10c},8)  Fill
  Print '-'                    (+0,{#column11c},8)  Fill
  Print '-'                    (+0,{#column12c},8)  Fill
  Print '-'                    (+0,{#column13c},8)  Fill
  Print '-'                    (+0,{#column14c},8)  Fill
  Print '-'                    (+0,{#column15c},8)  Fill
  Print '-'                    (+0,{#column16c},8)  Fill
  Print '-'                    (+0,{#column17c},8)  Fill
  Print '-'                    (+0,{#column18c},8)  Fill

  Print 'Report Totals'             (+1,{#column1c})
  Print #Total_EEDEF                (+0,{#column3c})   Edit 9,990.00
  Print #Total_EEMAN                (+0,{#column4c})   Edit 9,990.00
  Print #Total_QMAC                 (+0,{#column5c})   Edit 9,990.00
  Print #Total_ERMAT                (+0,{#column6c})   Edit 9,990.00
  Print #Total_ERPS                 (+0,{#column7c})   Edit 9,990.00
  Print #Total_ERCON                (+0,{#column8c})   Edit 9,990.00
  Print #Total_QNEC                 (+0,{#column9c})   Edit 9,990.00
  Print #Total_EEVND                (+0,{#column10c})  Edit 9,990.00
  Print #Total_EEVD                 (+0,{#column11c})  Edit 9,990.00
  Print #Total_EERC                 (+0,{#column12c})  Edit 9,990.00
  Print #Total_Loan_Repay_1         (+0,{#column13c})  Edit 9,990.00
  Print #Total_Loan_Repay_2         (+0,{#column14c})  Edit 9,990.00
  Print #Total_Loan_Repay_3         (+0,{#column15c})  Edit 9,990.00
  Print #Total_Loan_Repay_4         (+0,{#column16c})  Edit 9,990.00
  Print #Total_Loan_Repay_5         (+0,{#column17c})  Edit 9,990.00
  Print #Total_Loan_Repay_6         (+0,{#column18c})  Edit 9,990.00
End-Procedure

!GBD 08/14/2003 - Begin
!******************************************************************
Begin-Procedure P400-Email-File
!******************************************************************
   do GEXXX951-Select-Oracle-Instance
   let $db_name = lower($GEXXX951_Oracle_Instance)
   Do GEXXX971-Select-Trans-Type-Parameters
   if ($GEXXX971_EMAIL_ADDR_CRITERIA = '') and ($GEXXX971_FTP_ADDR_CRITERIA = '')
      show 'Either no rows exist on GEX_INTFC_RULE/DATA for this Oprid/Run Control ID'
      show 'or the most current rows under this ID have been set to inactive. Data will'
      show 'not be sent.'
   else
      if $GEXXX971_GEX_TRANS_TYPE = 'E'
         show $GEXXX971_EMAIL_ADDR_CRITERIA
         show 'This is the email statement produced: '

         if $GEXXX971_INT_EXT = 'E'

            let $UNIX_MAIL_COMMAND = '/usr/local/bin/todos -v $PS_HOME/outgoing/'||$new_filename||' '||
            '&&/usr/local/bin/mailer.pl "'||$GEXXX971_EMAIL_ADDR_CRITERIA||'" "MANULIFE 401K WEEKLY INTERFACE" '||
            '"$BINDIR/gexpy654_msg.sh" $PS_HOME/outgoing/gexpy654.dat $PS_HOME/outgoing/gexpy654.da2 '
            show $UNIX_MAIL_COMMAND

            CALL SYSTEM USING $UNIX_MAIL_COMMAND #system_status
            
            if #system_status <> 0
               add 1 to #errorcount
               display 'eMail of ' noline
               display $new_filename noline
               display ' failed'
               STOP
            else
               display $new_filename noline
               display ' was emailed successfully'
            end-if
         else
              let $jobname = 'gexpy654'||substr($prcs_run_cntl_id,9,2)
              Let $emailtext =  $jobname || ' is complete.   Please create diskette.'

              let $UNIX_MAIL_COMMAND = 'echo ' || $emailtext || '|' || ' mailx 
                                       -s"Create offsite diskette" ' || $GEXXX971_EMAIL_ADDR_CRITERIA ||' '  

              show $UNIX_MAIL_COMMAND

              CALL SYSTEM USING $UNIX_MAIL_COMMAND #system_status

              if #system_status <> 0                              
                 add 1 to #errorcount                             
                 display 'Email notification to OPS failed'       
              else                                                
                 display 'Email sent to OPS for diskette creation'
              end-if                                              
         end-if

      end-if
      if $GEXXX971_GEX_TRANS_TYPE = 'F'
         show $GEXXX971_FTP_ADDR_CRITERIA
         show 'Need to insert a Call FTP shell script to run ftp'
      end-if
   end-if
End-Procedure
!GBD 08/14/2003 - End

!*****************************************************************
#Include 'gexxx902.sqc'  !Get deptid multiple row table
#Include 'gexxx922.sqc'  !Get pay single row run control
#Include 'askaod.sqc'    !Ask-As-Of-Date routine
#Include 'rotname3.sqc'  !Routines to rotate name
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'gexxx951.sqc'  !Oracle Database Name             !GBD 08/14/2003
#Include 'gexxx971.sqc'  !Email and FTP Parameters         !GBD 08/14/2003
