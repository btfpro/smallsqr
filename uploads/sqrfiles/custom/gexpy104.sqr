!***********************************************************************
! GEXPY104:  Updates Pension Flag for those Emps from C B Tracey File  *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY104 : Updates Pension Flag for those Emps from C B Tracey File  *
!                                                                      *
! Narrative:            This SQR is designed to read an interface file *
!                         from C B Tracey and updates the pension flag *
!                         on FED_TAX_DATA                              *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           FED_TAX_DATA                                   *
!                                                                      *
! Written by:           Joe Besagar                                    *
!                                                                      *
! Normally Run:         Yearly                                         *
!                                                                      *
! Control Parms:        AsOfDate                                       *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! JNB       10/13/99    Initial Creation                               *
! JNB       12/17/99    Uncommented  Rollback                          *
! CWB       11/07/2001  Commented out Rollback, so the update          *
!                       would work. Fixed asofdate logic, because      *
!                       panel has no entry for as of date.             *
! CWB       12/10/2001  Added DEPTID to the reports.                   *
! CXA       01/06/2006  Added Company to FED TAX DATA criteria         *
! RKK       01/04/2011  Changed to read CSV file                       *
! ISDVMZA   01/12/2012  Zero fill the ssn & dept format                *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Landscape)


#define #column1     1
#define #column2    13
#define #column3    27
#define #column4    46
#define #column5    57
#define #column6    66
#define #column7    85
#define #column8   100
#define #column9   118


Begin-Setup
  Declare-Report Exception-Report
Printer-Type=HPLASERJET
    Layout=Default
  End-Declare

  Declare-Report Update-Report
Printer-Type=HPLASERJET
    Layout=Default
  End-Declare
End-Setup


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P400-Finalization
End-Report


Begin-Heading 8 For-Reports=(Exception-Report)
#debug9 Show 'Begin-Heading (Exception-Report)'
  Let $ReportID      =   'PY104E'
  Let $ReportTitle   =   'Updates Pension Flag'
  Let $ReportTitle2  =   'Exception Report'

  #Include 'stdhdg01.sqc'
  Print $ReportTitle2      (3,) Center

  Print 'EMPLID'           (+3,{#column2})
  Print 'DEPTID'           (+0,{#column3})

  Print '-'                (+1,{#column2},9)  Fill
  Print '-'                (+0,{#column3},9)  Fill

End-Heading


Begin-Heading 8 For-Reports=(Update-Report)
#debug9 Show 'Begin-Heading (Update-Report)'
  Let $ReportID      =   'PY104U'
  Let $ReportTitle   =   'Updates Pension Flag'
  Let $ReportTitle2  =   'Update Report'

  #Include 'stdhdg01.sqc'
  Print $ReportTitle2      (3,) Center

  Print 'EMPLID'           (+3,{#column2})
  Print 'DEPTID'           (+0,{#column3})


  Print '-'                (+1,{#column2},9)  Fill
  Print '-'                (+0,{#column3},9)  Fill

End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPY104'
  Let $ReportTitle   =   'Updates Pension Flag for those Emps from C B Tracey File'

  LET $MESSAGE = 'Pension Flag set to Y'
  LET $MESSAGE1 = 'Not Found on FED_TAX_TABLE'
  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

!  enter today's date for as of date.
  If $prcs_process_instance = ''
    Do Ask-As-Of-Date
    Let $WHERE_SELECT_ASOFDATE = $AsOfDate
  Else
     Let $WHERE_SELECT_ASOFDATE = $AsOfToday
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '

  Do P140-Open-File
End-Procedure


Begin-Procedure P140-Open-File
#debug9 Show 'P140-Open-File'

  Let $file1 = '{INFILE}' || Lower($ReportID) || '.csv'
Display '$file1 ' noline
display $file1
  Open $file1
       As 1
       For-Reading
       Record=16:Vary
       Status=#filestat

  If #filestat != 0
     Show 'Error opening input file.  Program terminating.'
     Stop
  End-If
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
  While 1=1
    read 1 into $input:16
    If #end-file
      Break
    Else
      #debug show 'in while'
      unstring $input by ',' into
      $deptcb
      $empssn

      move $deptcb to $deptcb '0000'         !ISDVMZA
      !Commented by Ujwal Dyasani 07-Dec-2016 - PT852 - Begin
	  !move $empssn to $empssn '000000000'    !ISDVMZA
	  !Commented by Ujwal Dyasani 07-Dec-2016 - PT852 - End
	  !Added by Ujwal Dyasani 07-Dec-2016 - PT852 - Begin
	  move $empssn to $empssn '0000000'
	  Show '$empssn :' $empssn
	  !Added by Ujwal Dyasani 07-Dec-2016 - PT852 - End

      Add 1 To #recs-read
      Add 1 To #trans-read
      Do P215-Obtain-Company
      Do P220-Find-Emp-Match
    End-If
  End-While
End-Procedure

Begin-Procedure P215-Obtain-Company
#debug9 Show 'P215-Obtain-Company'
Begin-Select
D.DEPTID
D.COMPANY
   move &d.company to $COMPANY
FROM PS_DEPT_TBL D
WHERE D.DEPTID = $deptcb
   AND D.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_DEPT_TBL
        WHERE D.DEPTID = DEPTID
            AND EFFDT <= $WHERE_SELECT_ASOFDATE)
End-Select
End-Procedure


Begin-Procedure P220-Find-Emp-Match
#debug9 Show 'P220-Find-Emp-Match'
 Let $Match_Found = 'N'
 Let #tax-recs-found = 0

Begin-Select
F.EMPLID
F.COMPANY

  Let $Match_Found = 'Y'

     Add 1 To #tax-recs-found
     Do P245-Update-Pension-Plan

     add 1 to #rows-Updated
     Add 1 To #u-recs-printed
     Do P250-Print-Update-Report
FROM PS_FED_TAX_DATA F
WHERE F.EMPLID = $empssn
    AND F.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_FED_TAX_DATA
        WHERE F.EMPLID = EMPLID
          AND F.COMPANY = COMPANY
          AND EFFDT <= $WHERE_SELECT_ASOFDATE)
 AND F.COMPANY = $COMPANY

End-Select

 If #tax-recs-found > 1
   Show $empssn
 End-If

 If $Match_Found = 'N'
     Do P260-Print-Exception-Report
     add 1 to #e-recs-printed
 End-If

End-Procedure


Begin-Procedure P245-Update-Pension-Plan
#debug9 Show 'P245-Update-Pension-Plan'
Begin-SQL
Update PS_FED_TAX_DATA F1

set F1.PENSION_PLAN = 'Y'

WHERE F1.EMPLID = $empssn
    AND F1.COMPANY = &F.COMPANY
    AND F1.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_FED_TAX_DATA
        WHERE F1.EMPLID = EMPLID
            and F1.COMPANY = COMPANY
            AND F1.EFFDT <= $WHERE_SELECT_ASOFDATE)
End-SQL
End-Procedure


Begin-Procedure P250-Print-Update-Report
#debug9 Show 'P250-Print-Update-Report'
      Use-Report Update-Report
      Let $U-Data-Found-Flag = 'Y'

  Print $empssn                   (+1,{#column2})
  Print $deptcb                   (+0,{#column3})
  Print $message                  (+0,{#column4})

End-Procedure

Begin-Procedure P260-Print-Exception-Report
#debug9 Show 'P260-Print-Exception-Report'
     Use-Report Exception-Report
      Let $E-Data-Found-Flag = 'Y'


  Print $empssn                    (+1,{#column2})
  Print $deptcb                    (+0,{#column3})
  Print $message1                  (+0,{#column4})

End-Procedure


Begin-Procedure P400-Finalization
#debug9 Show 'P400-Finalization'

  Do P410-Close-File

  If $E-Data-Found-Flag <> 'Y'
    Use-Report Exception-Report
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
  End-If

  If $U-Data-Found-Flag <> 'Y'
    Use-Report Update-Report
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
  End-If

  Move #trans-read To $trans-read 888,888,888
  Show $trans-read ' transaction records read from input file'

  Move #e-recs-printed To $e-recs-printed 888,888,888
  Show $e-recs-printed ' records written to exception report'

  Move #u-recs-printed To $u-recs-printed 888,888,888
  Show $u-recs-printed ' records written to update report'

  Show ' '

  Move #rows-updated To $rows-updated 888,888,888
  Show $rows-updated ' rows updated on ps_fed_Tax_data table'

  Show ' '


  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Procedure


Begin-Procedure P410-Close-File
#debug9 Show 'P410-Close-File'
  Close 1

  If #filestat != 0
     Show 'Error closing input file.  Program terminating.'
     Stop
  End-If
End-Procedure

Begin-Procedure P290-SQL-Error-found($Action)
#debug9 Show 'P290-SQL-Error'
  Show $Action ' failed'
  Show 'SQL Error:  '  $_sql-error
  Stop
End-Procedure

#Include 'gexxx922.sqc'  !Run Control parameters from GEX_RC_PAY record
#include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
