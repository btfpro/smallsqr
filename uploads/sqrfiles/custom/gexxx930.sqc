!**************************************************************************
!  GEXXX930:  Retrieve "Multiple" Emp_Type Run Controls and               *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX930_Emp_Type_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA                                  *
!    $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA                                  *
!    $GEXXX930_INCLUDE_EMP_TYPE_DISPLAY_CRITERIA                          *
!    $GEXXX930_EXCLUDE_EMP_TYPE_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX930_Emp_Type_Alias = 'J.EMPL_TYPE'                       *
!    Do GEXXX930-Select-Emp-Type-Parameters                               *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX930_INCLUDE_EMP_TYPE_CRITERIA]                          *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		02/11/99 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX930-Select-Emp-Type-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_EMP_TYPE_ROWS = 0
  Let $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA = ''
  Let $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA = ''
  Let $GEXXX930_INCLUDE_EMP_TYPE_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX930_EXCLUDE_EMP_TYPE_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_EMP_TYPE.EMPL_TYPE

  Add 1 To #GEX_RC_EMP_TYPE_ROWS

  If #GEX_RC_EMP_TYPE_ROWS = 1
    Let $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA = '(' || $GEXXX930_Emp_Type_Alias || ' In ('''     || &GEX_RC_EMP_TYPE.EMPL_TYPE || ''''
    Let $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA = '(' || $GEXXX930_Emp_Type_Alias || ' Not In (''' || &GEX_RC_EMP_TYPE.EMPL_TYPE || ''''
  Else
    Let $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA = $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA || ',''' || &GEX_RC_EMP_TYPE.EMPL_TYPE || ''''
    Let $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA = $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA || ',''' || &GEX_RC_EMP_TYPE.EMPL_TYPE || ''''
  End-If

From  PS_GEX_RC_EMP_TYPE GEX_RC_EMP_TYPE

Where GEX_RC_EMP_TYPE.OPRID                    = [$OprID]
And   GEX_RC_EMP_TYPE.RUN_CNTL_ID              = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_EMP_TYPE.EMPL_TYPE, ' ') != ' '

End-Select

  If #GEX_RC_EMP_TYPE_ROWS = 0
    Let $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA = $GEXXX930_Emp_Type_Alias || ' = ' || $GEXXX930_Emp_Type_Alias
    Let $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA = $GEXXX930_Emp_Type_Alias || ' = ' || $GEXXX930_Emp_Type_Alias
  Else
    Let $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA = $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA || '))'
    Let $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA = $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA || '))'
    Let $GEXXX930_INCLUDE_EMP_TYPE_DISPLAY_CRITERIA = $GEXXX930_INCLUDE_EMP_TYPE_CRITERIA
    Let $GEXXX930_EXCLUDE_EMP_TYPE_DISPLAY_CRITERIA = $GEXXX930_EXCLUDE_EMP_TYPE_CRITERIA
  End-If

End-Procedure
