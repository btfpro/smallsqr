!***********************************************************************
! GEXPY148:  This quarterly report will show who paid local tax        *
!            during the previous quarter.                              *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS    DATE           DESCRIPTION OF THE CHANGE                *
!***********************************************************************
!  ISDVPZM         03/11/2009     INITIAL CREATION                         *
!                                                                      *
!***********************************************************************


#include 'setenv.sqc' !Set environment

Begin-Report
    
  do Initialization  
  do report
  do Commit-Transaction
  do Close-File 
  do stdapi-term
  do reset
End-Report

!****************************************************************************
Begin-Procedure Initialization

  do Init-Number
  do stdapi-init
  
  Let $ReportID      = 'GEXPY148'  
  Let $GEXPY148_Locality_Alias  = 'B.LOCALITY'
  
 If $prcs_process_instance = ''
    Input $balance_year Maxlen=4 'Balance Year'
    Input $balance_qtr  Maxlen=2 'Balance Quarter'
    Let #balance_year = $balance_year
    Let #balance_qtr  = $balance_qtr
  Else
    Do Select-Parameters
    Let #balance_year = &A.Balance_Year
    Let #balance_qtr  = &A.Balance_Qtr
    Do GEXPY148-Select-Locality-Parameters
    
  End-If

  If #balance_year = 0 Or
     #balance_qtr  = 0
    Do Get-Default-Quarter
  End-If
  
    
  Move #balance_year To $balance_year 9999
  Move #balance_qtr  To $balance_qtr  9
  
   do Open-File
   
   let $file_header='Tax Year,Tax Quarter,SSN,Last Name,First Name,Middle Initial,Address 1,Address 2,City,State,Zip Code,Amount,PSD Number'
   write 2 from $file_header
   
End-Procedure  
 
!****************************************************************************
Begin-Procedure Select-Parameters

begin-select

A.BALANCE_YEAR
A.BALANCE_QTR

FROM PS_GEX_RC_PY148 A

WHERE OPRID          = $Prcs_OprID
AND   RUN_CNTL_ID    = $Prcs_Run_Cntl_ID
         
end-select

End-Procedure 

!****************************************************************************
Begin-Procedure Get-Default-Quarter
begin-select loops=1
balance_year
balance_period

  let #balance_year   = &balance_year
  let #balance_period = &balance_period

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  evaluate #balance_qtr
    when = 1
      subtract 1 from #balance_year
      let #balance_qtr = 4
      break
    when-other
      subtract 1 from #balance_qtr
  end-evaluate

from   ps_balance_id_def
where  balance_id = 'CY'
and    sysdate between begin_dt and end_dt
end-select
End-Procedure

!****************************************************************************
Begin-Procedure Open-File
       
      Let $Member_File   = '{OUTFILE}' || Lower($ReportID) || '_' ||$prcs_process_instance|| '.csv'
     
      OPEN $Member_File as 2 for-writing record=480:vary status = #Open_mem_cp
   
    
   if (#Open_mem_cp <> 0)
      let $Error-Message = 'PROGRAM ABORTED - Problem opening file : ' || $Member_File
      show $Error-Message
      STOP
   end-if
 
End-Procedure

!****************************************************************************
Begin-Procedure Report
  
  do Process-Employee-Data
  
End-Procedure

!****************************************************************************
Begin-Procedure Process-Employee-Data
   
begin-select
/*+ index(c,ps_personal_data) */  
B.BALANCE_YEAR 
B.BALANCE_QTR 
B.EMPLID 
C.LAST_NAME 
C.FIRST_NAME 
C.MIDDLE_NAME
C.ADDRESS1
C.ADDRESS2
C.CITY
C.STATE  
C.POSTAL 
B.TAX_QTD 
 
       do print-detail-info
               
FROM   PS_PERSONAL_DATA  C, 
       PS_TAX_BALANCE    B 
WHERE  C.EMPLID            = B.EMPLID 
AND    B.BALANCE_ID        = 'CY' 
AND    B.BALANCE_YEAR      = #balance_year 
AND    B.BALANCE_PERIOD    = 
      (SELECT MAX(BALANCE_PERIOD) 
       FROM   PS_TAX_BALANCE 
       WHERE  EMPLID       = B.EMPLID 
       AND    COMPANY      = B.COMPANY 
       AND    BALANCE_ID   = B.BALANCE_ID 
       AND    BALANCE_YEAR = B.BALANCE_YEAR 
       AND    BALANCE_QTR  = #balance_qtr 
       AND    TAX_CLASS    = B.TAX_CLASS 
       AND    LOCALITY     = B.LOCALITY) 
AND    [$GEXPY148_INCLUDE_LOCALITY_CRITERIA] 
ORDER BY B.COMPANY, B.LOCALITY, B.EMPLID
end-select
    
End-Procedure
!****************************************************************************
Begin-Procedure GEXPY148-Select-Locality-Parameters

 
  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_LOCALITY_ROWS = 0
  Let $GEXPY148_INCLUDE_LOCALITY_CRITERIA = ''
       
Begin-Select

PY.LOCALITY
  
  Add 1 To #GEX_RC_LOCALITY_ROWS
 
  If #GEX_RC_LOCALITY_ROWS = 1
   Let $GEXPY148_INCLUDE_LOCALITY_CRITERIA = '(' || $GEXPY148_Locality_Alias || ' In ('''     || &PY.LOCALITY || ''''
  Else
    Let $GEXPY148_INCLUDE_LOCALITY_CRITERIA = $GEXPY148_INCLUDE_LOCALITY_CRITERIA || ',''' || &PY.LOCALITY  || ''''
  End-If
  
   
From PS_GEX_RC_PY148 PY

Where PY.OPRID                = [$OprID]
And   PY.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(PY.LOCALITY, ' ') != ' '
    
    
End-Select

 If #GEX_RC_LOCALITY_ROWS = 0
    Let $GEXPY148_INCLUDE_LOCALITY_CRITERIA = $GEXPY148_Locality_Alias || ' = ' || $GEXPY148_Locality_Alias
 Else
    Let $GEXPY148_INCLUDE_LOCALITY_CRITERIA = $GEXPY148_INCLUDE_LOCALITY_CRITERIA || '))'
 End-If
  
SHOW 'AFTER ALL LOGIC- $GEXPY148_INCLUDE_LOCALITY_CRITERIA ==' $GEXPY148_INCLUDE_LOCALITY_CRITERIA

End-Procedure

!****************************************************************************
Begin-Procedure print-detail-info
   encode '-' into $hyphen 
   Let $SSN             =  substr(&B.EMPLID,1,3)||$hyphen||substr(&B.EMPLID,4,2)||$hyphen||substr(&B.EMPLID,6,4)   
   Let $middle_name     =  substr($C.MIDDLE_NAME,1,1)
   Let $ADDRESS1 	=  ltrim(rtrim(&C.ADDRESS1,' '),' ')
   Let $ADDRESS2 	=  ltrim(rtrim(&C.ADDRESS2,' '),' ')
   Let $CITY 	        =  ltrim(rtrim(&C.CITY,' '),' ')
   Let #PSD             =  022103  
   string &B.BALANCE_YEAR  &B.BALANCE_QTR $SSN &C.LAST_NAME &C.FIRST_NAME $middle_name $ADDRESS1 $ADDRESS2 $CITY &C.STATE &C.POSTAL &B.TAX_QTD #PSD by ',' into $write_row
   write 2 from $write_row
End-Procedure 

!****************************************************************************
Begin-Procedure SQL-Error-Found($Proc_Name) 
 
  show 'SQL Error in ' (,34)
  show $Proc_Name ()  
  show $_sql-error () 
  show 'sql-status:' ()
  show #_sql-status () ! edit 99999 
 
End-Procedure

!****************************************************************************
Begin-Procedure Close-File
  
     Close 2
  If #filestat != 0
     Show 'Error closing output file.  Program terminating.'
     Stop
  End-If
    
End-Procedure

!****************************************************************************

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'validdt.sqc'   !Validates Native Date
