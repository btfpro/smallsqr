!**************************************************************************
!  GEXXX910:  Retrieve "Multiple" Emplid Run Controls and                 *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX910_Emplid_Alias                                               *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX910_INCLUDE_Emplid_CRITERIA                                    *
!    $GEXXX910_EXCLUDE_Emplid_CRITERIA                                    *
!    $GEXXX910_INCLUDE_Emplid_DISPLAY_CRITERIA                            *
!    $GEXXX910_EXCLUDE_Emplid_DISPLAY_CRITERIA                            *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX910_Emplid_Alias = 'J.Emplid'                              *
!    Do GEXXX910-Select-Emplid-Parameters                                 *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX910_INCLUDE_Emplid_CRITERIA]                            *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JNB 		04/02/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX910-Select-Emplid-Parameters

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_Emplid_ROWS = 0
  Let $GEXXX910_INCLUDE_Emplid_CRITERIA = ''
  Let $GEXXX910_EXCLUDE_Emplid_CRITERIA = ''
  Let $GEXXX910_INCLUDE_Emplid_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX910_EXCLUDE_Emplid_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_Emplid.Emplid

  Add 1 To #GEX_RC_Emplid_ROWS

  If #GEX_RC_Emplid_ROWS = 1
    Let $GEXXX910_INCLUDE_Emplid_CRITERIA = '(' || $GEXXX910_Emplid_Alias || ' In ('''     || &GEX_RC_Emplid.Emplid || ''''
    Let $GEXXX910_EXCLUDE_Emplid_CRITERIA = '(' || $GEXXX910_Emplid_Alias || ' Not In (''' || &GEX_RC_Emplid.Emplid || ''''
  Else
    Let $GEXXX910_INCLUDE_Emplid_CRITERIA = $GEXXX910_INCLUDE_Emplid_CRITERIA || ',''' || &GEX_RC_Emplid.Emplid  || ''''
    Let $GEXXX910_EXCLUDE_Emplid_CRITERIA = $GEXXX910_EXCLUDE_Emplid_CRITERIA || ',''' || &GEX_RC_Emplid.Emplid  || ''''
  End-If

From  PS_GEX_RC_Emplid GEX_RC_Emplid

Where GEX_RC_Emplid.OPRID               = [$OprID]
And   GEX_RC_Emplid.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_Emplid.Emplid, ' ') != ' ' 
End-Select

  If #GEX_RC_Emplid_ROWS = 0
    Let $GEXXX910_INCLUDE_Emplid_CRITERIA = $GEXXX910_Emplid_Alias || ' = ' || $GEXXX910_Emplid_Alias
    Let $GEXXX910_EXCLUDE_Emplid_CRITERIA = $GEXXX910_Emplid_Alias || ' = ' || $GEXXX910_Emplid_Alias
  Else
    Let $GEXXX910_INCLUDE_Emplid_CRITERIA = $GEXXX910_INCLUDE_Emplid_CRITERIA || '))'
    Let $GEXXX910_EXCLUDE_Emplid_CRITERIA = $GEXXX910_EXCLUDE_Emplid_CRITERIA || '))'
    Let $GEXXX910_INCLUDE_Emplid_DISPLAY_CRITERIA = $GEXXX910_INCLUDE_Emplid_CRITERIA
    Let $GEXXX910_EXCLUDE_Emplid_DISPLAY_CRITERIA = $GEXXX910_EXCLUDE_Emplid_CRITERIA
  End-If

End-Procedure
