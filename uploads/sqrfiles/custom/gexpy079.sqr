!***********************************************************************
! GEXPY079:  OK_TO_PAY Audit                                           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY079 :            OK_TO_PAY Audit                                *
!                                                                      *
! Narrative:            This SQR will find any paysheet for a multiple *
!                       job EE that is being calculated in each of the *
!                       Axx and Bxx runs to set OK_TO_PAY = 'N' for    *
!                       the Bxx payline while the Axx payline is       *
!                       calculated and confirmed.  Once the Axx        *
!                       confirmation is done, the job should be rerun  *
!                       which will set OK_TO_PAY = 'Y' for the         *
!                       multiple job EEs.                              *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                       #debug6 issues a rollback                      *
!                                                                      *
! SQL Tables:           pay_earnings                                   *
!                       pay_line                                       *
!                       pay_calendar                                   *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Prior to each Axx pay calc                     *
!                          and                                         *
!                       After the pay confirmation of the Axx run      *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Run ID                                       *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! JDH       12/18/98    Initial Creation                               *
! JDH       01/18/99    Changed program to include setup01.sqc instead *
!                        of setup31.sqc                                *
!                                                                      *
! GBD       11/22/2002  Add Off_Cycle='N' to select stmnt.             *
!                                                                      *
! GBD       02/28/2003  v8.3 Upgrade - added production changes.       *
! GEX_SEC_REENG 2016-06-07 Vahini Katta                                 
! Changes related to gex_emplid_mapp/7 digit emplid                     
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


#define #column1     1
#define #column2    20 
#define #column3    32
#define #column4    47
#define #column5    63
#define #column6    78
#define #column7    90
#define #column8   102
#define #column9   114


Begin-Heading 8
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'

  Print 'EMPL'                 (+2,{#column2})
  Print 'PAY END'              (+0,{#column5})
  Print 'OFF'                  (+0,{#column6})
  Print 'OK_TO_PAY'            (+0,{#column9})

  Print 'EMPLID'               (+1,{#column1})
  Print 'RCD#'                 (+0,{#column2})
  Print 'COMPANY'              (+0,{#column3})
  Print 'PAYGROUP'             (+0,{#column4})
  Print 'DATE'                 (+0,{#column5})
  Print 'CYCLE'                (+0,{#column6})
  Print 'PAGE_NUM'                (+0,{#column7})
  Print 'LINE_NUM'                (+0,{#column8})
  Print 'SET TO'               (+0,{#column9})

  Print '-'                    (+1,{#column1},11)    Fill
  Print '-'                    (+0,{#column2},04)    Fill
  Print '-'                    (+0,{#column3},07)    Fill
  Print '-'                    (+0,{#column4},08)    Fill
  Print '-'                    (+0,{#column5},08)    Fill
  Print '-'                    (+0,{#column6},05)    Fill
  Print '-'                    (+0,{#column7},05)    Fill
  Print '-'                    (+0,{#column8},05)    Fill
  Print '-'                    (+0,{#column9},09)    Fill
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPY079'
  Let $ReportTitle   =   'OK_TO_PAY AUDIT'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  If $prcs_process_instance = ''
    While 1=1
      Input $RunID Maxlen=3 'Pay Run ID'
      Uppercase $RunID
      If Rtrim($RunID, ' ') <> ''
        Show $RunID
        Break
      End-If
    End-While
  Else
    Do GEXXX922-Select-Parameters
    Let $RunID = $GEX_RC_PAY.RUN_ID
    If Rtrim($RunID, ' ') = ''
      Show 'Pay Run ID is required for this audit'
      Stop
    End-If
  End-If

  Date-time () HH:MI:SS &timeBegan
  Date-time () MM/DD/YYYY &dateBegan
  Show 'Process Began at ' &timeBegan ' on ' &dateBegan
  Show ' '
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select On-Error=P290-SQL-Error-Found($RunID,'P200-Main-Process')
pl.emplid
pl.company
pl.paygroup
pl.pay_end_dt
pc.pay_confirm_run

  #debug7 Show ' '
  #debug7 Show '&pl.emplid          = ' &pl.emplid
  #debug7 Show '&pl.company         = ' &pl.company
  #debug7 Show '&pl.paygroup        = ' &pl.paygroup
  #debug7 Show '&pl.pay_end_dt      = ' &pl.pay_end_dt
  #debug7 Show '&pc.pay_confirm_run = ' &pc.pay_confirm_run

  If &pc.pay_confirm_run = 'N'
    Let $ok-to-pay-flag = 'N'
  Else
    Let $ok-to-pay-flag = 'Y'
  End-If

  Do P210-Get-Other-Pay-Runs

From   ps_pay_calendar  pc,
       ps_pay_line      pl

Where  pc.run_id               = $RunID

And    pc.pay_sheets_run       = 'Y'

And    pc.company              = pl.company
And    pc.paygroup             = pl.paygroup
And    pc.pay_end_dt           = pl.pay_end_dt
And    pl.off_cycle            = 'N'                 !GBD 11/22/2002 - added

And    Exists
      (Select 'x'
       From   ps_pay_calendar pc2,
              ps_pay_line     pl2
       Where  pc2.company      = pl2.company
       And    pc2.paygroup     = pl2.paygroup
       And    pc2.pay_end_dt   = pl2.pay_end_dt
       And    pc2.pay_end_dt   = pl.pay_end_dt
       And    pl2.off_cycle    = 'N'                 !GBD 11/22/2002 - added
       And    pl2.emplid       = pl.emplid
       And    pc2.run_id      != pc.run_id)

Order By pl.emplid
End-Select
End-Procedure


Begin-Procedure P210-Get-Other-Pay-Runs
#debug9 Show 'P210-Get-Other-Pay-Runs'
Begin-Select On-Error=P290-SQL-Error-Found($RunID,'P210-Get-Other-Pay-Runs')
pl3.company
pl3.paygroup
pl3.pay_end_dt
pl3.off_cycle
pl3.page_num
pl3.line_num
pl3.emplid
pl3.empl_rcd

  #debug7 Show '&pl3.company    = ' &pl3.company
  #debug7 Show '&pl3.paygroup   = ' &pl3.paygroup
  #debug7 Show '&pl3.pay_end_dt = ' &pl3.pay_end_dt
  #debug7 Show '&pl3.off_cycle  = ' &pl3.off_cycle
  #debug7 Show '&pl3.page_num      = ' &pl3.page_num
  #debug7 Show '&pl3.line_num      = ' &pl3.line_num
  #debug7 Show '&pl3.emplid     = ' &pl3.emplid
  #debug7 Show '&pl3.empl_rcd  = ' &pl3.empl_rcd

  Do P220-Print
  Do P230-Update

From   ps_pay_line     pl3

Where  pl3.emplid       = &pl.emplid
And   (pl3.company     != &pl.company
Or     pl3.paygroup    != &pl.paygroup)
And    pl3.pay_end_dt   = &pl.pay_end_dt
And    pl3.off_cycle    = 'N'                        !GBD 11/22/2002 - added
End-Select
End-Procedure


Begin-Procedure P220-Print
#debug9 Show 'P220-Print'
  Do Format-DateTime (&pl3.pay_end_dt, $pay_end_dt, {DEFMDY}, '', '')

  Print &pl3.emplid           (+1,{#column1})  ! Edit xxx-xx-xxxx ! GEX_SEC_REENG 2016-06-07 Vahini Katta 
  Print &pl3.empl_rcd        (+0,{#column2})  Edit 9999
  Print &pl3.company          (+0,{#column3})
  Print &pl3.paygroup         (+0,{#column4})
  Print $pay_end_dt           (+0,{#column5})
  Print &pl3.off_cycle        (+0,{#column6})
  Print &pl3.page_num            (+0,{#column7})  Edit 99999
  Print &pl3.line_num            (+0,{#column8})  Edit 99999
  Print $ok-to-pay-flag       (+0,{#column9})

  Add 1 To #Recs-Printed
End-Procedure


Begin-Procedure P230-Update
#debug9 Show 'P230-Update'
Begin-SQL On-Error=P290-SQL-Error-Found($RunID,'P230-Update')
  Update ps_pay_earnings
     Set ok_to_pay  = $ok-to-pay-flag
   Where emplid     = &pl3.emplid
     And company    = &pl3.company
     And paygroup   = &pl3.paygroup
     And pay_end_dt = &pl3.pay_end_dt
     And off_cycle  = &pl3.off_cycle
     And page_num      = &pl3.page_num
     And line_num      = &pl3.line_num
End-SQL
  Add #sql-count To #Recs-Updated
End-Procedure


Begin-Procedure P290-SQL-Error-Found($Run_ID,$Proc_Name)
#debug9 Show 'P290-SQL-Error-Found'
  Show ' '
  Show 'SQL Error in ' $Proc_Name 
  Show 'SQL Error:  ' $_sql-error
  Stop
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Move #Recs-Printed To $Recs-Printed 888,888,888
  Move #Recs-Updated To $Recs-Updated 888,888,888

  Show $Recs-Printed ' EEs were found in multiple pay runs'
  Show $Recs-Updated ' paysheets were updated'
  Show ' '

  #debug6 Rollback
  #debug6 Show 'A rollback has been issued'
  #debug6 Show ' '

  Date-time () HH:MI:SS &timeEnded
  Date-time () MM/DD/YYYY &dateEnded
  Show 'Process Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of process'
End-Procedure


#include 'gexxx922.sqc'  !Get pay single row run control
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
