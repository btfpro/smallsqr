!**************************************************************************
!  GEXXX935:  Retrieve "Multiple" Department Run Controls and             *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX935_Department_Alias                                           *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA                                *
!    $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA                                *
!    $GEXXX935_INCLUDE_DEPARTMENT_DISPLAY_CRITERIA                        *
!    $GEXXX935_EXCLUDE_DEPARTMENT_DISPLAY_CRITERIA                        *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX935_Department_Alias = 'GDT.DEPARTMENT'                    *
!    Do GEXXX935-Select-Department-Parameters                             *
!    Begin-Select                                                         *
!    DEPTID                                                               *
!    FROM PS_GEX_DEPT_TBL GDT                                             *
!    WHERE [$GEXXX935_INCLUDE_DEPARTMENT_CRITERIA]                        *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE         *
!**************************************************************************
!	JDH 		10/13/99 	INITIAL CREATION                  *
!       JDH             10/29/99        When no parms found, let 1=1      *
!**************************************************************************


Begin-Procedure GEXXX935-Select-Department-Parameters

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DEPARTMENT_ROWS = 0
  Let $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA = ''
  Let $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA = ''
  Let $GEXXX935_INCLUDE_DEPARTMENT_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX935_EXCLUDE_DEPARTMENT_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_DEPARTMENT.DEPARTMENT

  Add 1 To #GEX_RC_DEPARTMENT_ROWS

  If #GEX_RC_DEPARTMENT_ROWS = 1
    Let $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA = '(' || $GEXXX935_Department_Alias || ' In ('''     || &GEX_RC_DEPARTMENT.DEPARTMENT || ''''
    Let $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA = '(' || $GEXXX935_Department_Alias || ' Not In (''' || &GEX_RC_DEPARTMENT.DEPARTMENT || ''''
  Else
    Let $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA = $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA || ',''' || &GEX_RC_DEPARTMENT.DEPARTMENT  || ''''
    Let $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA = $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA || ',''' || &GEX_RC_DEPARTMENT.DEPARTMENT  || ''''
  End-If

From  PS_GEX_RC_DPRTMNT GEX_RC_DEPARTMENT

Where GEX_RC_DEPARTMENT.OPRID               = [$OprID]
And   GEX_RC_DEPARTMENT.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DEPARTMENT.DEPARTMENT, ' ') != ' ' 
End-Select

  If #GEX_RC_DEPARTMENT_ROWS = 0
    Let $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA = '1=1'
    Let $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA = '1=1'
  Else
    Let $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA = $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA || '))'
    Let $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA = $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA || '))'
    Let $GEXXX935_INCLUDE_DEPARTMENT_DISPLAY_CRITERIA = $GEXXX935_INCLUDE_DEPARTMENT_CRITERIA
    Let $GEXXX935_EXCLUDE_DEPARTMENT_DISPLAY_CRITERIA = $GEXXX935_EXCLUDE_DEPARTMENT_CRITERIA
  End-If

End-Procedure
