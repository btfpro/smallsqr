!*************************************************************************
!  GEXPY058 - Giant Eagle-Check Writer Control Listing(Advices) for B run*
!  (Previously DDP004.sqr Payroll Advice Register)                       *
!*************************************************************************
!*************************************************************************
!                                                                        * 
!               Confidentiality Information:                             *
!                                                                        *
! This module is the confidential and proprietary information of         *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted  *
! in any form, by any means, in whole or in part, nor is it to be used   *
! for any purpose other than that for which it is expressly provided     *
! without the written permission of Giant Eagle.                         *
!                                                                        *
!*************************************************************************
!                                                                        *
!  GEXPY58B - Giant Eagle-Advice Writer Control Listing                  *
!                                                                        *
! Entered Run                                                            *
! Control Parm:	Uses the Payroll-Report-Initialization sqc               *
!                                                                        *
! SQL Tables:	PS_PAY_CALENDAR   A,                                     *
!     		PS_PAY_DISTRIBUTN B,                                     *
!      		PS_PAY_CHECK    PAY                                      *
!                                                                        *
! Written by:		Surya Sobha  09-16-2014                          *
!                                                                        *
! Normally Run:		After each confirmed pay period.                 *
! Modified the Pay Check advice register to print  according to the      *
! Home-Deptid, Deptid, Sort order and Paygroup                           *
!*************************************************************************

#include 'readxlat.sqc'  !Read-Translate-Table procedure	
#include 'setenv.sqc'  !Set environment

begin-setup

#include 'setup32a.sqc' !Printer and page-size initialization

declare-report detail-report
Printer-Type=HPLASERJET
  layout=default
end-declare

declare-report summary-report
Printer-Type=HPLASERJET
  layout=default
end-declare

end-setup

begin-report
  do Init-Report
  do Stdapi-Term
end-report

!******************************
begin-procedure Init-Report
!******************************
#debug9 show 'Init-Report'

  move 'GEXPY058' to $ReportID			
  show $ReportID					
  move 'Payroll Advice Register' to $ReportTitle
  show $ReportTitle					

  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Payroll-Report-Initialization
end-procedure

begin-heading 8 for-reports=(detail-report)

  move 'GEXPY058' to $ReportID			
  #Include 'stdhdgpi.sqc'
  #Include 'stdhdg02.sqc'

  print 'Paygroup:'				(+1,1)	
  print $x000_paygroup				(0,13)	

  print '                                               Sep                ' (+2,1)
  print '                                                     ' ()
  print 'Pay   Address'                             ()

#ifdef USA
  print 'Advice No.          Check Date   Deposit Amt   Chk  Employee Name  ' (+1,1)
#endif

#ifdef CANADA
  print 'Advice No.          Cheque Date  Deposit Amt   Chk  Employee Name  ' (+1,1)
#endif

  print '                Employee ID  Dept. ID    Acct Cd' ()
  print '   Group  Option'                               ()
end-heading

begin-heading 5 for-reports=(summary-report)
  move 'PY058ADV' to $ReportID
  #Include 'stdhdgpi.sqc'
  #Include 'stdhdg01.sqc'
  if $Company <> $priorCompany
    do Get-Company-Data
    move $Company to $priorCompany
    move ''       to $priorPayGroup  !for use in Standard Heading #6
  end-if
  
  print 'Home-Deptid'        (+1,1)
  print 'Deptid'             (+0,15)
  print 'Paygroup'           (+0,25)
  print 'Beginning Nbr'      (+0,40)
  print 'Ending Nbr'         (+0,60)
  print 'Number of Forms'    (+0,80)
end-heading

!*****************************
begin-procedure Report
!*****************************
#debug9 show 'Report'

 let $x100_Get_Employees_Data_Flag = 'N'

 let $deptid_begin_nbr_flag = 'N'
 let $paygroup_begin_nbr_flag = 'N'
 let $company_begin_nbr_flag = 'N'
 let $homedeptid_begin_nbr_flag = 'N' 

begin-SELECT
D.DIVISION,
D.DEPTID_HOME () on-break print=never after=HomeDeptid-Change 
					before=homedeptid-before-change	
					level=01    
					save=$prev_homedeptid
D.SORT_ORDER,
PAY.DEPTID ()	 on-break print=never after=Deptid-Change				
					before=Deptid-Before-Change			
					level=02
					save=$prev_deptid

A.COMPANY () on-break print=never after=Company-Change 
					before=Company-Before-Change			
					level=03						
                                        save=$prev_company 
B.PAYGROUP () on-break print=never after=Paygroup-Change			
					before=Paygroup-Before-Change			
					level=04						
                                        save=$prev_paygroup 				
B.FORM_ID () on-break print=never after=FormID-Change
					before=FormID-Before-Change			
					level=05						
B.PAYCHECK_NBR			
B.PAY_END_DT
B.OFF_CYCLE,
B.PAGE_NUM,
B.LINE_NUM
B.SEPCHK

#ifdef DB2ALL
DECIMAL(SUM(B.DEPOSIT_AMOUNT), 15, 3) &NetDeposit
#else
SUM(B.DEPOSIT_AMOUNT)        &NetDeposit
#end-if

  do Get-Check-Data
  do Get-Job-Data
  do Print-Line

 let $x100_Get_Employees_Data_Flag = 'Y'

FROM  PS_PAY_CALENDAR   A,
      PS_PAY_DISTRIBUTN B,
      PS_PAY_CHECK    PAY,
      PS_GEX_DEPT_TBl D
WHERE [$SlctCalendar]
  AND B.OFF_CYCLE IN ($SlctOffCycleA, $SlctOffCycleB)
  AND B.PAGE_NUM BETWEEN #SlctPageFrom AND #SlctPageThru
  AND B.COMPANY          = A.COMPANY
  AND B.PAYGROUP         = A.PAYGROUP
  AND B.PAY_END_DT       = A.PAY_END_DT
  AND B.ACCOUNT_TYPE    <> '$'
  AND B.COMPANY          = PAY.COMPANY	
  AND B.PAYGROUP         = PAY.PAYGROUP	
  AND B.PAY_END_DT       = PAY.PAY_END_DT	
  AND B.OFF_CYCLE 	 = PAY.OFF_CYCLE	
  AND B.PAGE_NUM		 = PAY.PAGE_NUM	
  AND B.LINE_NUM		 = PAY.LINE_NUM	
  AND B.SEPCHK		 = PAY.SEPCHK	
  AND D.DEPTID          =PAY.DEPTID
  AND D.EFFDT           =
    (SELECT MAX(EFFDT)
       FROM PS_GEX_DEPT_TBL D1
      WHERE D1.SETID   = D.SETID
        AND D1.DEPTID  = D.DEPTID
        AND D1.EFFDT     <= A.PAY_END_DT) 
  AND D.SEQUENCE_NUMBER =
    (SELECT MAX(D2.SEQUENCE_NUMBER) 
     FROM PS_GEX_DEPT_TBL D2 
     WHERE D2.SETID=D.SETID 
     AND D2.DEPTID=D.DEPTID 
     AND D2.EFFDT=D.EFFDT) 
GROUP BY D.DIVISION , D.DEPTID_HOME , D.SORT_ORDER, PAY.DEPTID, A.COMPANY, B.PAYGROUP , B.FORM_ID, B.PAYCHECK_NBR,B.PAY_END_DT,B.OFF_CYCLE,B.PAGE_NUM,B.LINE_NUM,B.SEPCHK  
ORDER BY D.DIVISION DESC, D.DEPTID_HOME , D.SORT_ORDER, PAY.DEPTID, A.COMPANY, B.PAYGROUP DESC, B.FORM_ID, B.PAYCHECK_NBR,B.PAY_END_DT,B.OFF_CYCLE,B.PAGE_NUM,B.LINE_NUM,B.SEPCHK
end-SELECT

 if $x100_Get_Employees_Data_Flag = 'N'
	print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) CENTER
 else

  print 'Grand Totals:  ' (+2,1)
  do Format-Number(#GrandNetDeposit, $out, '$,$$$,$$$,$$9.99')			
  print $out  (0,30)			
  do Format-Number(#GrandVouchers, $out, '999,999')
  print $out      (0,+2)
  if #GrandVouchers <> 1
    print 'Vouchers'        (0,+1)
  else
    print 'Voucher '        (0,+1)
  end-if

end-if

  do Reset

end-procedure

begin-procedure Get-Check-Data
begin-SELECT
C.NAME
C.EMPLID
C.EMPL_RCD
C.DEPTID
C.LOCATION
C.CHECK_DT
C.PAYCHECK_OPTION
C.PAYCHECK_ADDR_OPTN
C.PAY_SHEET_SRC

  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn

#ifdef DB2ALL
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif
#ifdef MICROSOFT
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif

FROM  PS_PAY_CHECK C
WHERE C.COMPANY          = &A.Company
  AND C.PAYGROUP         = &B.PayGroup
  AND C.PAY_END_DT       = &B.Pay_End_Dt
  AND C.OFF_CYCLE        = &B.Off_Cycle
  AND C.PAGE_NUM            = &B.Page_NUM
  AND C.LINE_NUM            = &b.line_num
  AND C.SEPCHK           = &B.SepChk
end-SELECT

end-procedure


begin-procedure Get-Job-Data
begin-select
j.acct_cd

From   ps_job j

Where  j.emplid    = &c.emplid
And    j.empl_rcd = &c.empl_rcd

And    j.effdt                   =
      (Select Max(effdt)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt             <= &b.pay_end_dt)

And    j.effseq                  =
      (Select Max(effseq)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt              = j.effdt)
end-select
end-procedure


begin-procedure Print-Line
#debug9 show 'Print-Line'

  if $000-skip-line-flag = 'Y' 	 		
  	let $000-skip-line-flag = 'N' 		
	print ' '				(+1,1)	
  end-if							

  if $paygroup_begin_nbr_flag = 'N'
     if &b.paycheck_nbr <> 0
       move &b.paycheck_nbr to $paygroup_begin_nbr  999999999
       let $paygroup_begin_nbr_flag = 'Y'
     end-if
  end-if
  
  if &b.paycheck_nbr <> 0
     move &b.paycheck_nbr to $paygroup_end_nbr  999999999
     add 1 to #paygroup_tot_deposits
  end-if

  if $homedeptid_begin_nbr_flag = 'N'
     if &b.paycheck_nbr <> 0
       move &b.paycheck_nbr to $homedeptid_begin_nbr  999999999 
       let $homedeptid_begin_nbr_flag = 'Y'
     end-if
  end-if
  if &b.paycheck_nbr <> 0
     move &b.paycheck_nbr to $homedeptid_end_nbr  999999999 
     add 1 to #homedeptid_tot_Checks 
  end-if					

  if $deptid_begin_nbr_flag = 'N'
     if &b.paycheck_nbr <> 0
       move &b.paycheck_nbr to $deptid_begin_nbr  999999999 
       let $deptid_begin_nbr_flag = 'Y'
     end-if
  end-if
  if &b.paycheck_nbr <> 0
     move &b.paycheck_nbr to $deptid_end_nbr  999999999 
     add 1 to #deptid_tot_checks
  end-if

  if $company_begin_nbr_flag = 'N'
     if &b.paycheck_nbr <> 0
       move &b.paycheck_nbr to $company_begin_nbr  999999999
       let $company_begin_nbr_flag = 'Y'
     end-if
  end-if
  if &b.paycheck_nbr <> 0
     move &b.paycheck_nbr to $company_end_nbr  999999999
     add 1 to #company_tot_deposits
  end-if

  print &B.paycheck_nbr          (+1,2)  edit 0999999999
  if &C.Pay_Sheet_Src = 'O'
     print 'On-line'   (0,12)
  end-if
  do Format-DateTime(&C.Check_Dt, $out, {DEFDATE}, '', '')
  print $out       (0,21)

  print &B.SepChk          (0,49)  edit b
  print &C.Name            (0,53,30)
  print &C.EmplID          (0,84)
  print &C.DeptID          (0,97)
  print &J.Acct_Cd         (0,111)
  print &B.PayGroup        (0,120)

  move &C.Check_Dt to $AsOfDate  !for included procedures

#ifdef DB2ALL
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif
#ifdef MICROSOFT
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif

!  LookUp Address-Options $PayCheck_Addr_Optn $AddressOption	
  let $FieldName  = 'PAYCHECK_ADDR_OPTN'
  let $FieldValue = $PayCheck_Addr_Optn
  do Read-Translate-Table
  move $XLatShortName to $AddressOption


  print $AddressOption            (0,126)

  print &NetDeposit               (0,33)  edit 99,999,999.99

  add &NetDeposit to #TotalNetDeposit
  add 1           to #TotalVouchers

  add &NetDeposit to #x000_Paygroup_TotalNetDeposit	
  add &NetDeposit to #x000_Deptid_TotalNetDeposit	
  add &NetDeposit to #x000_FormID_TotalNetDeposit	

  add 1	      to #x000_Paygroup_TotalVouchers	
  add 1           to #x000_Deptid_TotalVouchers		
  add 1           to #x000_FormID_TotalVouchers		

end-procedure

begin-procedure Company-Change

  let $000-skip-line-flag = 'Y' 			
  let $000-temp-out = 'Company ' || $Company || ' Totals:' 
  print $000-temp-out			(+1,1)	

  do Format-Number(#TotalNetDeposit, $out, '$,$$$,$$$,$$9.99')
				
  print $out  (0,30)					

  do Format-Number(#TotalVouchers, $out, '999,999')
  print $out      (0,+2)
  if #TotalVouchers <> 1
    print 'Vouchers'        (0,+1)
  else
    print 'Voucher '        (0,+1)
  end-if

  add #TotalVouchers   to #GrandVouchers
  add #TotalNetDeposit to #GrandNetDeposit

  move 0 to #TotalVouchers
  move 0 to #TotalNetDeposit

  let $000-new-page-flag = 'Y' 		

  if #level = 0
    new-page
  end-if

end-procedure

begin-procedure HomeDeptid-Change		

use-report summary-report
  if #homedeptid_tot_checks <> 0

    print '----------------------------------------------' (+1,40)    
    print 'Total'      (+1,25) 
    print $homedeptid_begin_nbr (+0,40) 
    print $homedeptid_end_nbr   (+0,60) 
    move #homedeptid_tot_Checks to $homedeptid_nbr_forms 99999
    print $homedeptid_nbr_forms (+0,80)
    print ' '                 (+2,1)
  end-if
  
  move 'N' to $homedeptid_begin_nbr_flag 
  move 'Y' to $company_begin_nbr_flag 
  move 'N' to $paygroup_begin_nbr_flag 
  move ' ' to $homedeptid_begin_nbr
  move ' ' to $homedeptid_end_nbr
  move ' ' to $homedeptid_nbr_forms
  move 0 to #homedeptid_tot_Checks 
use-report detail-report

 !  new-page
   let $000-new-page-flag = 'Y' 		

end-procedure

begin-procedure Paygroup-Change		

  let $000-skip-line-flag = 'Y' 		
  let $000-temp-out = 'Paygroup ' || $x000_paygroup || ' Totals:'
  print $000-temp-out 			(+1,1)
  do Format-Number(#x000_Paygroup_TotalNetDeposit, $out, '$,$$$,$$$,$$9.99')
  print $out      (0,30)
  do Format-Number(#x000_Paygroup_TotalVouchers, $out, '999,999')
  print $out      (0,+2)

  if #x000_Paygroup_TotalVouchers <> 1
    print 'Vouchers'        (0,+1)
  else
    print 'Voucher '        (0,+1)
  end-if

use-report summary-report
  if #paygroup_tot_deposits <> 0
    print $prev_homedeptid      (+1,1)
    print $prev_deptid    (0,15)
    print $prev_paygroup      (0,25)
    print $paygroup_begin_nbr (+0,40)
    print $paygroup_end_nbr   (+0,60)
    move #paygroup_Tot_deposits to $paygroup_nbr_forms 99999
    print $paygroup_nbr_forms (+0,80)
  end-if
  move 'N' to $paygroup_begin_nbr_flag
  move 'Y' to $company_begin_nbr_flag
  move ' ' to $paygroup_begin_nbr
  move ' ' to $paygroup_end_nbr
  move ' ' to $paygroup_nbr_forms
  move 0 to #paygroup_tot_deposits
use-report detail-report

  move 0 to #x000_Paygroup_TotalVouchers
  move 0 to #x000_Paygroup_TotalNetDeposit

  let $000-new-page-flag = 'Y'

end-procedure


begin-procedure Deptid-Change			

  let $000-skip-line-flag = 'Y' 		
  let $000-temp-out = 'Deptid ' || $deptid || ' Totals:'
  print $000-temp-out			(+1,1)
  do Format-Number(#x000_Deptid_TotalNetDeposit, $out, '$,$$$,$$$,$$9.99')
  print $out      (0,30)
  do Format-Number(#x000_Deptid_TotalVouchers, $out, '999,999')
  print $out      (0,+2)

  if #x000_Deptid_TotalVouchers <> 1
    print 'Vouchers'        (0,+1)
  else
    print 'Voucher '        (0,+1)
  end-if

use-report detail-report

  move 0 to #x000_Deptid_TotalVouchers
  move 0 to #x000_Deptid_TotalNetDeposit

  let $000-new-page-flag = 'Y'

end-procedure


begin-procedure FormID-Change			

  let $000-skip-line-flag = 'Y' 		
  let $000-temp-out = 'FormID ' || $Form_ID || ' Totals:'
  print $000-temp-out 				(+1,1)
  do Format-Number(#x000_FormID_TotalNetDeposit, $out, '$,$$$,$$$,$$9.99')
  print $out      (0,30)
  do Format-Number(#x000_FormID_TotalVouchers, $out, '999,999')
  print $out      (0,+2)

  if #x000_FormID_TotalVouchers <> 1
    print 'Vouchers'        (0,+1)
  else
    print 'Voucher '        (0,+1)
  end-if

  let $000-new-page-flag = 'Y'

  add #x000_FormID_TotalVouchers to #GrandChecks
  add #x000_FormID_TotalNetDeposit to #GrandNetPay
  move 0 to #x000_FormID_TotalVouchers
  move 0 to #x000_FormID_TotalNetDeposit

end-procedure


begin-procedure Company-Before-Change			

   if $000-new-page-flag = 'Y'
      let $000-new-page-flag = 'N'
      new-page
   end-if
   move &A.Company  to $Company				

end-procedure


begin-procedure Paygroup-Before-Change			

   if $000-new-page-flag = 'Y'
      let $000-new-page-flag = 'N'
      new-page
   end-if
   move &B.PayGroup to $x000_paygroup

end-procedure


begin-procedure Deptid-Before-Change			
#debug9 show 'Deptid-Before-Change'

   if $000-new-page-flag = 'Y'
      let $000-new-page-flag = 'N'
   end-if
   move &PAY.DeptID to $deptid

end-procedure


begin-procedure FormID-Before-Change			

  if $000-new-page-flag = 'Y'
      let $000-new-page-flag = 'N'
  end-if
  move &B.Form_ID to $Form_ID					

end-procedure

begin-procedure homedeptid-before-change			
  if $000-new-page-flag = 'Y'
      let $000-new-page-flag = 'N'
      new-page
  end-if
end-procedure

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'getlocnm.sqc'  !Get-Location-Name procedure
#Include 'payinit.sqc'   !Report Initialization and Timing
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
