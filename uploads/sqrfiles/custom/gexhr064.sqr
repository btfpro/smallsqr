!***********************************************************************
! GEXHR064.SQR:                                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-2006 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXHR064.SQR :                                                       *
!                                                                      *
! Narrative:            Create a program that will report terminated   *
!			corporate employee on a Monthly basis.	       *
!			ITG-35732.				       *
!								       *
! #Debug Used:          x                                              *
!								       *
! SQL Tables:           PS_JOB, PS_PERSONAL_DATA,                      *
!								       *
! Written by:           Rapidigm, Deepak. S, 04/19/06                  *
!                                                                      *
! Normally Run:         This process will be run Monthly.	       *
!                                                                      *
! Control Parms:        Reads the run control table PS_GEX_RUN_CNTL    *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup02.sqc'   !Printer and Page-Size Initialization (Landscape)

#define #column1  001
#define #column2  010
#define #column3  020
#define #column4  042
#define #column5  050
#define #column6  063
#define #column7  071
#define #column8  080
#define #column9  088
#define #column10  116
#define #column11  130
#define #column12  143
#define #column13  162
#define #column14  169

!***********************************************************************
! Defines Heading of the report
!***********************************************************************
Begin-Heading 5
  let $ReportID = 'GEXHR064'
  #include 'stdhdg01.sqc'

  Print 'Corporate Office Terminations'     (3,1) center
  Print $rpt-subtitle (+1,1) center
  Print 'COMPANY'		(+2,{#column1}) underline
  Print 'PAYGROUP'		(0,{#column2})	underline
  Print 'NAME'			(0,{#column3})	underline	
  Print 'STATUS'		(0,{#column4})	underline
  Print 'ACTION DATE'		(0,{#column5})	underline
  Print 'ACTION'		(0,{#column6})	underline
  Print 'REASON'		(0,{#column7})	underline
  Print 'DEPTID'		(0,{#column8})	underline
  Print 'ADDRESS1'		(0,{#column9})	underline
  Print 'ADDRESS2'		(0,{#column10})	underline
  Print 'ADDRESS3'		(0,{#column11})	underline
  Print 'CITY'			(0,{#column12})	underline
  Print 'STATE'			(0,{#column13})	underline
  Print 'POSTAL'		(0,{#column14})	underline


End-Heading


!***********************************************************************
! Start of Program
!***********************************************************************
Begin-Report

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Stdapi-Init             !stdapi.sqc
  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = 'gexhr064'
  end-if

  ! get asofdate from run control page
  do Get-Run-Control	     !gexrnctl.sqc  

  if ltrim(rtrim(&asofdate,' '),' ') = '' 
	let $asofdate = $current-date
  else
	let $asofdate = &asofdate
  end-if

  #debugx show '<' $asofdate '><' $current-date '>'

  let $report-from-date = edit($asofdate,'MM/DD/YYYY')
  let $report-thru-date = edit($current-date,'MM/DD/YYYY')
  let $rpt-subtitle = 'Report as of ' || $report-from-date || ' thru ' || $report-thru-date 

  do Build-Dynamic-Where
  do Select-Data
  do stdapi-term

End-Report


!***********************************************************************
! Builds the Where clause for Company, Paygroup, Empl Status
!***********************************************************************
Begin-Procedure Build-Dynamic-Where
	Let $GEXXX903_Emp_Stat_Alias = 'A.EMPL_STATUS'
	Do GEXXX903-Select-Emp-Stat-Parameters
	Let $GEXXX901_Company_Alias  = 'A.COMPANY'
	Let $GEXXX901_Paygroup_Alias = 'A.PAYGROUP'
	do GEXXX901-Select-Company-Paygroup-Parameters

	#debugx show $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
	#debugx show $GEXXX903_INCLUDE_EMP_STAT_CRITERIA 

End-Procedure


!***********************************************************************
! Main Procedure which selects the data.
!***********************************************************************
Begin-Procedure Select-Data
Begin-Select 
A.COMPANY				(+2,{#column1})
A.PAYGROUP				(0,{#column2})
SUBSTR(B.NAME,1,20)			(0,{#column3})
A.EMPL_STATUS				(0,{#column4})
TO_CHAR(A.ACTION_DT,'YYYY-MM-DD')	(0,{#column5})
A.ACTION				(0,{#column6})	
A.ACTION_REASON				(0,{#column7})	
A.DEPTID				(0,{#column8})
B.ADDRESS1				(0,{#column9})
B.ADDRESS2				(0,{#column10})
B.ADDRESS3				(0,{#column11})
B.CITY					(0,{#column12})
B.STATE					(0,{#column13})
B.POSTAL				(0,{#column14})

FROM PS_JOB A, PS_PERSONAL_DATA B
 WHERE A.EFFDT =
      (SELECT MAX(A_ED.EFFDT) FROM PS_JOB A_ED
        WHERE A.EMPLID = A_ED.EMPLID
          AND A.EMPL_RCD = A_ED.EMPL_RCD
          AND A_ED.EFFDT <= SYSDATE)
   AND A.EFFSEQ =
        (SELECT MAX(A_ES.EFFSEQ) FROM PS_JOB A_ES
        WHERE A.EMPLID = A_ES.EMPLID
          AND A.EMPL_RCD = A_ES.EMPL_RCD
          AND A.EFFDT = A_ES.EFFDT)
   AND A.EMPLID = B.EMPLID
   AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
   AND [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
   AND A.ACTION_DT between $asofdate and $current-date
!   AND A.COMPANY in (list from run control)
 !  AND A.PAYGROUP IN (list from run control)
  ! AND A.EMPL_STATUS IN (list from run control)
  ! AND A.ACTION_DT > TO_DATE((As Of Date),'YYYY-MM-DD')
ORDER BY 5
End-Select
End-Procedure


#include 'stdapi.sqc'    !Process Scheduler Interface
#include 'reset.sqc'     !Reset Printer Procedure
#include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date math
#include 'gexrnctl.sqc'  !Routine for Get-Run-Control
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#include 'gexxx901.sqc'	 !Get Company/Paygroup from Run Control Page
#include 'gexxx903.sqc'  !Get Emp_Stat from Run Control Page