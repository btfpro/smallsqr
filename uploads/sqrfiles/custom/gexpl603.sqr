!***********************************************************************
!  GEXPL603:  Local 23 Health & Welfare Interface                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL603:             Local 23 Health & Welfare Interface            *
!                                                                      *
! Narrative:		This program creates a monthly report for      *
!                       Local 23 and a file that contains employee     *
!                       information required by the union to           *
!                       administer the Health & Welfare benefit.       *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_calendar                                   *
!                       ben_defn_optn                                  *
!                       ben_defn_pgm                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       benef_plan_tbl                                 *
!                       pay_line                                       *
!                       pay_check                                      *
!                       pay_deduction                                  *
!                       health_benefit                                 *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:	        Monthly                                        *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Plan Type(s)                                 *
!                         Vendor ID(s)                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS   DATE       DESCRIPTION OF THE CHANGE                     *
!***********************************************************************
!  JDH        02/08/99   Initial Creation                              *
!                                                                      *
!  JDH        02/26/99   Pull pay_check rows by the check date of the  *
!                          pay end rather than the check date of the   *
!                          pay_check row.                              *
!                        Add descending sort on effseq.                *
!                        $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA being set*
!                          incorrectly under process scheduler.        *
!                        Corrected syntax of SQL to obtain term date.  *
!                                                                      *
!  JDH        03/01/99   Added missing line to select.                 *
!                        Modified logic to get deduction for the month *
!                          of the check date associated with the last  *
!                          confirmed pay period.                       *
!                                                                      *
!  JDH        04/13/99   Get the effdt, action, action_reason from     *
!                          the job row that originally set the         *
!                          empl_status to its current value for        *
!                          empl_status in ('L','P','R','T').           *
!                                                                      *
!  JDH        05/19/99   Corrected invalid variable reference.         *
!                                                                      *
!  JDH        06/29/99   Changed logic to get action, action_reason,   *
!                          effdt.                                      *
!                                                                      *
!  SXK        11/23/99   Changed from ssn to emplid                    *
!                                                                      *
!  SXK        11/29/99   Changed from zip to postal                    *
!                                                                      *
!  AXL        12/13/00   Added a step to check max effseq from JOB for *
!                        change of status date.                        *
!  GBD        07/15/02   Added Fieldvalue to effective dated selection *
!                        of the Load-Lookup procedure.                 *
!                        Corrected spacing issue with report output.   *
!  GBD        09/12/02   v8.3 Upgrade - Provider field changed to      *
!                        Vendor ID.                                    *
!                                                                      *
!  GBD        10/16/02   Modified code to eliminate multiple selects.  *
!                                                                      *
!  AXL        06/01/04   Changed the setup sqc. Doublespaced the       *
!                        output.                                       *
!GEX-RBC        11/19/2007      Made Changes to display the date in the 
!				format mm/dd/yy.
!GEX-TLL     04/22/2008  Upgrade 9.0                    	       *
!VENDTKK     02/02/2009  Added INCLUDE GETPOSNM.SQC to pull 	       *
!                  	 JOBTITLE from POSITION_DATA.DESCR   	       *
!			 field instead of JOBCODE_TBL.DESCR            *
! VENDKXY     10/07/2009      Done Changes requested for ITG 67862     *	
! VENDKXY     12/08/2009 Done Changes requested for ITG 70472	       *
!                           when store changes in month being processed*
!ISDVAWD     04/22/2014     ITG# 827  - Changed file path  '\\nt5\' *
!			       with '\\corp.gianteagle.com\'`	       *
!***********************************************************************


#Include 'setenv.sqc'    !Set environment
!AXL 6/1/2004 New sqc for Mobius - Begin
!#Include 'setup32.sqc'   Printer and page-size initialization
begin-setup		  !Printer and page-size initialization

declare-printer DEFAULT-PD
  point-size=7.6
  pitch=17
  font=3
end-declare

#define PAGE_ORIENTATION LANDSCAPE
#define PAGE_MAX_COLS    177
#define PAGE_MAX_LINES   58
#Define ColR 160 

! Define Parameters
    #define PAGE_PAPER_SIZE (11,8.5)
    #define LINE_HEIGHT 9.5 

    #if {PRINTER_TYPE} = 'LINEPRINTER'
      #define PAGE_LEFT_MARG 0.25			!AXL was 0.0
      #define PAGE_TOP_MARG  0.5			!AXL was .125
      #define CHAR_WIDTH 4.32
    #endif
    
 declare-layout default
  paper-size={PAGE_PAPER_SIZE}
  orientation={PAGE_ORIENTATION}
  max-columns={PAGE_MAX_COLS}
  max-lines={PAGE_MAX_LINES}
  left-margin={PAGE_LEFT_MARG}
  top-margin={PAGE_TOP_MARG}
  line-height={LINE_HEIGHT}              ! 72/printer_point-size
  char-width={CHAR_WIDTH}                ! points, to handle max cols
end-declare

end-setup   
!AXL 6/1/2004 - End

!GBD 07/15/2002 Begin
#define #column1     1
#define #column2    33
#define #column3    44
#define #column4    50
!#define #column5    60
#define #column5    61
!#define #column6    70
#define #column6    72
!#define #column7   102
#define #column7   103
!#define #column8   114
#define #column8   115
!#define #column9   120
#define #column9   121
!#define #column10  128
#define #column10  129
#define #column11  136
!#define #column12  146
#define #column12  147
!#define #column13  157
#define #column13  159
#define #column14  173


Begin-Heading 8
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'

  !Print 'Provider:'            (3,1)
  Print 'VendorID:'            (3,1)
  Print $Provider              (3,13)

  Print $Reporting_Period      (3,) Center 

  Do P210-Get-Benefit-Program-Name

  Print 'Ben Pgm:'             (4,1)
  Print $Benefit_Program       (4,13)
  Print $BenefitProgramDescr   (4,) Center

  Print 'SERVICE'              (+2,{#column4})
  Print 'BIRTH'                (+0,{#column5})
  Print 'JOB'                  (+0,{#column6})
  Print 'REG'                  (+0,{#column7})
  Print 'EMPL'                 (+0,{#column8})
  Print 'ACTION'               (+0,{#column10})
  Print 'EFF'                  (+0,{#column11})
  Print 'BENEFIT'              (+0,{#column12})
  Print 'HEALTH AND'           (+0,{#column13})

  Print 'EMPLOYEE NAME'        (+1,{#column1})
  Print 'SSN'                  (+0,{#column2})
  Print 'DEPT'                 (+0,{#column3})
  Print 'DATE'                 (+0,{#column4})
  Print 'DATE'                 (+0,{#column5})
  Print 'CODE'                 (+0,{#column6})
  Print 'TEMP'                 (+0,{#column7})
  Print 'STAT'                 (+0,{#column8})
  Print 'ACTION'               (+0,{#column9})
  Print 'REASON'               (+0,{#column10})
  Print 'DATE'                 (+0,{#column11})
  Print 'TERM DATE'            (+0,{#column12})
  Print 'W/F AMT'              (+0,{#column13})
  Print 'VL'                   (+0,{#column14})

  Print '-'                    (+1,{#column1},30)  Fill
  Print '-'                    (+0,{#column2},09)  Fill
  Print '-'                    (+0,{#column3},04)  Fill
!  Print '-'                    (+0,{#column4},08)  Fill
  Print '-'                    (+0,{#column4},10)  Fill
!  Print '-'                    (+0,{#column5},08)  Fill
  Print '-'                    (+0,{#column5},10)  Fill
  Print '-'                    (+0,{#column6},30)  Fill
  Print '-'                    (+0,{#column7},10)  Fill
  Print '-'                    (+0,{#column8},04)  Fill
  Print '-'                    (+0,{#column9},06)  Fill
  Print '-'                    (+0,{#column10},06) Fill
!  Print '-'                    (+0,{#column11},08) Fill
  Print '-'                    (+0,{#column11},10) Fill
!  Print '-'                    (+0,{#column12},09) Fill
  Print '-'                    (+0,{#column12},10) Fill
  Print '-'                    (+0,{#column13},10) Fill
  Print '-'                    (+0,{#column14},2) Fill
!GBD 07/15/2002 End
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'
 MOVE $CURRENT-DATE TO $CURRDT 'MMDDYY'   !VENDKXY Done the Changes for ITG 70472
 !let $report-name = '\\corp.gianteagle.com\mobius\hrreports\' ||'PL603PDF.pdf'  !VENDKXY Done the changes for ITG 67862 !VENDKXY Done the changes for ITG 70472
!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - - Changed Report name 
!*****************************************************************
! let $report-name = '\\nt5\ftproot\benefits\Local 23_GEXPL603\' ||'PL603PDF_' || $CURRDT || '.pdf'  !VENDKXY Done the changes for ITG 70472
 let $report-name = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\Local 23_GEXPL603\' ||'PL603PDF_' || $CURRDT || '.pdf' 
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************

  new-report  $report-name   !VENDKXY Done the changes for ITG 67862
!  Do Set-Optimizer-Goal    !Jyotsna - Ptools and OS upgrade

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc
  
  Do P100-Start
   Do Openfile     !VENDKXY Done the changes for ITG 67862
  Do P200-Main-Process
  Do P300-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report


Begin-Procedure Set-Optimizer-Goal
  Begin-SQL
!    Alter Session Set OPTIMIZER_GOAL=RULE;	!GEX-CXB Commented for Upgrade 9.0 on 10/23/2007
     Alter Session Set OPTIMIZER_MODE=RULE;	!GEX-CXB Added for Upgrade 9.0 on 10/23/2007
  End-SQL
End-Procedure

!VENDKXY Done the changes for ITG 67862-- Begin
Begin-Procedure Openfile

    !Close 1
    encode '<009>' into $delim
    
       
    !Let $file603 = '\\corp.gianteagle.com\mobius\hrreports\' || 'PL603XLS' || '.xls'   !VENDKXY Done the changes for ITG 70472

!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - Updated File path   
!*****************************************************************  
!    Let $file603 = '\\nt5\ftproot\benefits\Local 23_GEXPL603\' || 'PL603XLS_' ||  $CURRDT || '.xls' !VENDKXY Done the changes for ITG 70472
    Let $file603 = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\Local 23_GEXPL603\' || 'PL603XLS_' ||  $CURRDT || '.xls' 
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************
   	  		
    Open $file603 As 2 For-Writing Record=1000:Vary
    

   Move 'PL603XLS'			To $Report  
   encode '<012>' into $newline 
   String 'Report ID:' $Report  ' ' ' ' ' ' ' ' ' ' $ReportTitle by $delim into $gexpl603
   Write 2 from $gexpl603
   Write 2 from $newline 
   
    
    String 'VENDORID' 'BEN PGM' 'STORENAME' 'DEPTID' 'EMPLOYEE NAME' 'SSN' 'SERVICE DATE' 'BIRTH DATE' 'JOB TITLE' 'REG TEMP' 'EMPL STAT' 
    'ACTION' 'ACTION REASON' 'EFF DATE' 'BENEFIT TERM DATE' 'HEALTH AND W/F AMT' 'VL' by $delim into $gexpl603
   
    Write 2 from $gexpl603
    
End-Procedure

!VENDKXY Done the changes for ITG 67862-- End


Begin-Procedure P100-Start
#debug9 Show 'P100-Start'

  !Move 'GEXPL603'                    To $ReportID  !VENDKXY Done the changes for ITG 67862
  Move 'PL603PDF'		      To $ReportID    !VENDKXY Done the changes for ITG 67862
  Move 'LOCAL 23 HEALTH & WELFARE INTERFACE' To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  If $prcs_process_instance = ''

    Move '''' to $ProviderString
    Display 'Enter Provider ID or leave blank to exit.'
    While 1=1
      Input $Provider maxlen=6 'Provider ID'
      if RTRIM($Provider, ' ') = ''
        concat '''' with $ProviderString
        break
      end-if
      if $ProviderString <> ''''
        concat ''',''' with $ProviderString
      end-if
      concat $Provider with $ProviderString
    End-While

    Let $Vendor_ID = $Provider

    if $ProviderString = ''''''
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'ALL'
    else
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = 'bpt.vendor_id In (' || $ProviderString || ')'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
    end-if

    Move '''' to $PlanTypeString
    Display 'Enter Plan Type or leave blank to exit.'
    While 1=1
      Input $PlanType Maxlen=2 'Plan Type'
      Uppercase $PlanType
      if RTRIM($PlanType, ' ') = ''
        concat '''' with $PlanTypeString
        break
      end-if
      if $PlanTypeString <> ''''
        concat ''',''' with $PlanTypeString
      end-if
      concat $PlanType with $PlanTypeString
    End-While

    if $PlanTypeString = ''''''
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
    else
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = 'hb1.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = 'bpt.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    end-if

  Else

    Let $GEXXX912_Plan_Type_Alias = 'hb1.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'hb1.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA

    Let $GEXXX912_Plan_Type_Alias = 'bpt.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters

    Let $GEXXX927_Vendor_ID_Alias = 'bpt.vendor_id'
    Do GEXXX927-Select-Vendor-ID-Parameters

  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  If $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
    Display 'At least one plan type parameter must be specified'
    Stop
  End-If

  If $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
    Display 'At least one vendor id parameter must be specified'
    Stop
  End-If

  Let $X000_ORDER_BY = 'Vendor ID, Benefit Program, Employee Name'

  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA  = '   $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA
  Show '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA      = '   $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA

  Do Get-Calendar-Year-Id    !getbalid.sqc

  Do P140-Get-Pay-Period
  Do P150-Print-Cover-Page
  Do P160-Open-File
  Do P170-Get-Xlat-Lookup
End-Procedure


Begin-Procedure P140-Get-Pay-Period
#debug9 Show 'P140-Get-Pay-Period'

Begin-Select
pay_end_dt
To_Number(To_Char(pay_end_dt-7,'MM'))   &hours_period
To_Number(To_Char(pay_end_dt-7,'YYYY')) &hours_year
To_Char(check_dt,'fmMonth YYYY')        &period
Last_Day(Add_Months(check_dt,-1))       &prev_month_end
Last_Day(Add_Months(check_dt,1))        &next_month_end

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #hours_year       = &hours_year
  Let #hours_period     = &hours_period
  Let $Reporting_Period = &period

  Let $next_month_end      = &next_month_end
  Let $prev_month_begin    = '01-'||Substr(&prev_month_end,4,8)

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  Else
    Show 'This is the ' $Reporting_Period ' bill'
  End-If

  Do P145-Get-Begin-Date

  Show 'Report will show deductions taken between ' $begin_date ' and ' $pay_end_dt

From   ps_pay_calendar

Where  company  = 'GEI'
And    paygroup = 'RET'

And    pay_end_dt =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  company = 'GEI'
       And    paygroup = 'RET'
       And    pay_end_dt <= $AsOfToday
       And    pay_confirm_run = 'Y'
       And    pay_period = 1)
End-Select
End-Procedure


Begin-Procedure P145-Get-Begin-Date
#debug9 Show 'P145-Get-Begin-Date'
Begin-Select
pc1.pay_end_dt

  Let $begin_date = &pc1.pay_end_dt

From   ps_pay_calendar pc1

Where  company  = 'GEI'
And    paygroup = 'RET'

And    pay_end_dt =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  company = 'GEI'
       And    paygroup = 'RET'
       And    pay_end_dt <= $pay_end_dt
       And    pay_confirm_run = 'Y'
       And    pay_period = 2)
End-Select
End-Procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  !AXL 6/1/2004  Initialize for Mobius
  let $Provider  = 'XXXXX'
  let $Benefit_Program = 'XXX'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+2,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,2)
  Print '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = '  (+2,5)
  Print $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA       (+1,10)
  Print '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA    = '  (+2,5)
  Print $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA          (+1,10)

  Print 'This is the'                                         (+2,5)
  Print $Reporting_Period                                     (0,+1)
  Print 'bill'                                                (0,+1)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  New-Page
End-Procedure


Begin-Procedure P160-Open-File
#debug9 Show 'P160-Open-File'

  !Let $File1 = '{OUTFILE}' || Lower($ReportID) || '.dat'    !VENDKXY Done the changes for ITG 70472
  Let $File1 = '{OUTFILE}' || 'gexpl603.dat'    !VENDKXY Done the changes for ITG 70472
  !Let $File1 = '//apps/hr/hrms90/hrdev90/outgoing/' || Lower($ReportID) || '.dat' !GEX-TLL TESTING

  Open $File1
    As 1
    For-Writing
    Record=222:Fixed
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Opening output file.  Program terminating.'
    Stop
  End-If
End-Procedure


begin-procedure P170-Get-Xlat-Lookup
#debug9 Show 'P170-Get-Xlat-Lookup'

  Let $Where = '     x.fieldname   = ''REG_TEMP''' ||
	       ' And x.eff_status  = ''A''' ||
!	       ' And x.language_cd = ''ENG''' ||	!GEX-CXB Commented for Upgrade 9.0 on 10/23/2007
	       ' And x.effdt       = (Select Max(effdt)' ||
!	       '                      From   xlattable x1' ||	!GEX-CXB Commented for Upgrade 9.0 on 10/23/2007
	       '                      From   PSXLATITEM x1' ||	!GEX-CXB Added for Upgrade 9.0 on 10/23/2007
	       '                      Where  x1.fieldname    = x.fieldname' ||
!GBD 07/15/2002 Begin
!	       '                      And    x1.language_cd  = ''ENG''' ||
!	       '                      And    x1.language_cd  = x.language_cd' ||	!GEX-CXB Commented for Upgrade 9.0 on 10/23/2007
	       '                      And    x1.fieldvalue  = x.fieldvalue' ||
!GBD 07/15/2002 End
	       '                      And    x1.effdt       <= ''' || $pay_end_dt || '''' ||
	       '                      And    x1.eff_status   = ''A'')'

  Load-Lookup
    Name=XlatLookup
    Rows=10
    !Table='XLATTABLE X'	!GEX-CXB Commented for Upgrade 9.0 on 10/23/2007
    Table='PSXLATITEM X'	!GEX-CXB Added for Upgrade 9.0 on 10/23/2007
    Key='X.FIELDVALUE'
    Return_Value='X.XLATSHORTNAME'
    Where=$Where
    Quiet
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
 
  Let $Data-Found-Flag = 'N'
  Let $Position_Nbr = ''	 !VENDTKK	ITG#56786   2-Feb-2009	
Begin-Select
bpt.vendor_id        ()     On-Break   Level=1
                                       Print=Never
                                       Save=$Save_Vendor_ID

bdo.benefit_program  ()     On-Break   Level=2
                                       Print=Never
                                       Save=$Save_Benefit_Program
                                       After=P280-After-BP-Break

per.name             ()     On-Break   Level=3
                                       Print=Never


j.emplid             ()     On-Break   Level=4
                                       Print=Never

j.empl_rcd          ()     On-Break   Level=5
                                       Print=Never
                                       After=P290-Employee-Break

bpt.plan_type

e.service_dt

per.birthdate
per.emplid  !SXK 11/23/99 Changed from ssn to emplid
per.sex
per.address1
per.address2
per.city
per.state
!SXK 11/29/1999 Changed from zip to postal
per.postal

j.effdt
j.deptid
j.jobcode
j.empl_status
j.reg_temp
j.full_part_time
j.gex_volun_low_hrs

pd.ded_cur
J.Position_nbr 
  Let $Position_Nbr = &J.Position_nbr    !VENDTKK	ITG#56786   2-Feb-2009	
  Let $Data-Found-Flag = 'Y'

  Let $Provider            = &bpt.vendor_id
  Let $Vendor_ID           = &bpt.vendor_id
  Let $Benefit_Program     = &bdo.benefit_program
  Let $per.name            = &per.name
  Let $j.emplid            = &j.emplid
  Let #j.empl_rcd          = &j.empl_rcd
  Let $bpt.plan_type       = &bpt.plan_type
  Let $e.service_dt        = &e.service_dt
  Let $per.birthdate       = &per.birthdate
  Let $per.ssn             = &per.emplid !SXK 11/23/99
  Let $per.sex             = &per.sex
  Let $per.address1        = &per.address1
  Let $per.address2        = &per.address2
  Let $per.city            = &per.city
  Let $per.state           = &per.state
  Let $per.zip             = &per.postal
  Let $j.effdt             = &j.effdt
  Let $j.deptid            = &j.deptid
  Let $j.jobcode           = &j.jobcode
  Let $j.empl_status       = &j.empl_status
  Let $j.reg_temp          = &j.reg_temp
  Let $j.full_part_time    = &j.full_part_time
  Let $j.gex_volun_low_hrs = &j.gex_volun_low_hrs
  Let #ded_cur             = &pd.ded_cur

  If &bpt.plan_type = '1S'
    Let #ded_cur = #ded_cur * -1
  End-If

  Add #ded_cur To #grs_mtd

From   ps_personal_data          per,
       ps_employment             e,
       ps_job                    j,
       ps_ben_defn_optn          bdo,
       ps_benef_plan_tbl         bpt,
       ps_pay_line               pl,
       ps_pay_check              pc,
       ps_pay_deduction          pd

Where  per.emplid                = j.emplid

And    e.emplid                  = j.emplid
And    e.empl_rcd               = j.empl_rcd

And    j.effdt                   =
      (Select Max(effdt)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt             <= $pay_end_dt)

And    j.effseq                  =
      (Select Max(effseq)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt              = j.effdt)

And    bpt.effdt                 =
      (Select Max(effdt)
       From   ps_benef_plan_tbl
       Where  plan_type          = bpt.plan_type
       And    benefit_plan       = bpt.benefit_plan
       And    effdt             <= $pay_end_dt)

And    bdo.plan_type             = bpt.plan_type
And    bdo.benefit_plan          = bpt.benefit_plan

And    bdo.effdt                 =
      (Select Max(effdt)
       From   ps_ben_defn_optn
       Where  benefit_program    = bdo.benefit_program
!GBD 10/16/2002 Begin
       And    effdt             <= $pay_end_dt)
!       And    effdt             <= $pay_end_dt
!       And    plan_type          = bdo.plan_type
!       And    option_id          = bdo.option_id)
!GBD 10/16/2002 End

And    pc.emplid                 = j.emplid
And    pc.empl_rcd              = j.empl_rcd

And    pc.pay_end_dt          Between $begin_date And $pay_end_dt

And    pd.company                = pc.company
And    pd.paygroup               = pc.paygroup
And    pd.pay_end_dt             = pc.pay_end_dt
And    pd.off_cycle              = pc.off_cycle
And    pd.page_num                  = pc.page_num
And    pd.line_num                  = pc.line_num
And    pd.sepchk                 = pc.sepchk

And    pl.company                = pc.company
And    pl.paygroup               = pc.paygroup
And    pl.pay_end_dt             = pc.pay_end_dt
And    pl.off_cycle              = pc.off_cycle
And    pl.page_num                  = pc.page_num
And    pl.line_num                  = pc.line_num

And    pd.plan_type              = bpt.plan_type
And    pd.benefit_plan           = bpt.benefit_plan
And    pd.ded_class              = 'N'

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA]
And   [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA]

Order By bpt.vendor_id,
       bdo.benefit_program,
       per.name,
       j.emplid,
       j.empl_rcd

End-Select
End-Procedure


Begin-Procedure P210-Get-Benefit-Program-Name
#debug9 Show 'P210-Get-Benefit-Program-Name'

Begin-Select
bdp.descr

  Let $BenefitProgramDescr = &bdp.descr

From   ps_ben_defn_pgm   bdp

Where  bdp.benefit_program    = $Benefit_Program

And    bdp.effdt              =
      (Select Max(effdt)
       From   ps_ben_defn_pgm
       Where  benefit_program = bdp.benefit_program
       And    effdt          <= $pay_end_dt)
End-Select
End-Procedure


Begin-Procedure P220-Get-Benefit-Termination-Row
#debug9 Show 'P220-Get-Benefit-Termination-Row'
Begin-Select Loops=1
hb1.coverage_begin_dt

From   ps_health_benefit hb1

Where  hb1.emplid         = $j.emplid
And    hb1.empl_rcd      = #j.empl_rcd

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2]

And    hb1.effdt          =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid      = hb1.emplid
       And    empl_rcd   = hb1.empl_rcd
       And    plan_type   = hb1.plan_type)

And    hb1.coverage_begin_dt Between $prev_month_begin
                                 And $next_month_end

And    hb1.coverage_elect = 'T'

Union

(
Select hb1.coverage_begin_dt

From   ps_life_add_ben hb1

Where  hb1.emplid         = $j.emplid
And    hb1.empl_rcd      = #j.empl_rcd

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2]

And    hb1.effdt          =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid      = hb1.emplid
       And    empl_rcd   = hb1.empl_rcd
       And    plan_type   = hb1.plan_type)

And    hb1.coverage_begin_dt Between $prev_month_begin
                                 And $next_month_end

And    hb1.coverage_elect = 'T'
)

Order By 1
End-Select
End-Procedure


Begin-Procedure P230-Get-Leave-Begin-Date
#debug9 Show 'P230-Get-Leave-Begin-Date'
  Let $Status-Change = 'N'
Begin-Select
JOB.EFFDT
JOB.EMPL_STATUS
JOB.ACTION
JOB.ACTION_REASON

  If &JOB.EMPL_STATUS = $J.EMPL_STATUS And
      $Status-Change = 'N'
    Let $Effective-Date = &JOB.EFFDT
    !AXL 12/13/2000  To be determined elsewhere - Begin
    !Let $Action         = &JOB.ACTION
    !Let $Action_Reason  = &JOB.ACTION_REASON
    !AXL 12/13/2000  To be determined elsewhere - End
    Let $Status-Change = 'N'
  Else
    Let $Status-Change = 'Y'
  End-If

FROM   PS_JOB JOB

WHERE  EMPLID     = $J.EMPLID
AND    EMPL_RCD  = #J.EMPL_RCD
AND    EFFDT     <= $J.EFFDT
ORDER BY JOB.EFFDT DESC, JOB.EFFSEQ DESC
End-Select

  Do Check-For-Max-Seq	!AXL 12/13/2000  New check max effseq
  
End-Procedure

!AXL 12/13/2000 - Set Action/Rsn from max effseq row - Begin 
Begin-Procedure Check-For-Max-Seq
#debug9 Show 'Check-For-Max-Seq'
  
Begin-Select

JOB2.ACTION
JOB2.ACTION_REASON

  #debug8 display '&JOB2.ACTION ' noline
  #debug8 display  &JOB2.ACTION
  #debug8 display '&JOB2.ACTION_RSN ' noline
  #debug8 display  &JOB2.ACTION_REASON

  Let $Action         = &JOB2.ACTION
  Let $Action_Reason  = &JOB2.ACTION_REASON
 
FROM   PS_JOB JOB2

WHERE  JOB2.EMPLID     = $J.EMPLID
AND    JOB2.EMPL_RCD  = #J.EMPL_RCD

AND    JOB2.EFFDT                 =
      (Select Max(effdt) From   ps_job
       Where  job2.emplid         = emplid
       And    job2.empl_rcd      = empl_rcd
       And    EFFDT <= $Effective-Date)
And    job2.effseq                  =
      (Select Max(effseq) From   ps_job
       Where  job2.emplid         = emplid
       And    job2.empl_rcd          = empl_rcd
       And    job2.effdt              = EFFDT)

End-Select
      
End-Procedure
!AXL 12/13/2000 - Set Action/Rsn from max effseq row - End

Begin-Procedure P235-Get-Status-Date
#debug9 Show 'P235-Get-Status-Date'
  Let $Reg-Temp-Change = 'N'
Begin-Select
J1.EFFDT
J1.REG_TEMP
J1.ACTION
J1.ACTION_REASON
J1.EMPL_STATUS

  If &J1.REG_TEMP = $J.REG_TEMP And
      $Reg-Temp-Change = 'N'
    Let $Reg-Temp-Change = 'N'
    If &J1.EMPL_STATUS <> 'T'
      Let $Effective-Date = &J1.EFFDT
      Let $Action         = &J1.ACTION
      Let $Action_Reason  = &J1.ACTION_REASON
    End-If
  Else
    Let $Reg-Temp-Change = 'Y'
  End-If

FROM   PS_JOB J1

WHERE  EMPLID     = $J.EMPLID
AND    EMPL_RCD  = #J.EMPL_RCD
AND    EFFDT     <= $J.EFFDT
ORDER BY J1.EFFDT DESC, J1.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P250-Format-Record
#debug9 Show 'P250-Format-Record'

  Let $name     = $per.name
  Let $address1 = $per.address1
  Let $address2 = $per.address2
  Let $city     = $per.city
  Let $state    = $per.state

  Uppercase $name
  Uppercase $address1
  Uppercase $address2
  Uppercase $city
  Uppercase $state

  Lookup XlatLookup $j.reg_temp $reg_temp

  Let $JobCode = $j.jobcode
  Do Get-Job-Title
  
  !VENDTKK Begin   ITG#56786   2-Feb-2009

   Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosName = '' 
  		Let $jobtitle = $jobtitle
	 Else
 		Let $jobtitle = $PosName
	 End-if
 !VENDTKK End   ITG#56786   2-Feb-2009	

  If $j.gex_volun_low_hrs = 'Y'
    Let $gex_volun_low_hrs = 'Y'
  Else
    Let $gex_volun_low_hrs = ''
  End-If

  Do Format-DateTime($e.service_dt, $Service_Dt, {DEFMDY}, '', '')   !VENDRBC !GEX-MXT UNCOMMENTED FOR ISSUE 415(DATE FORMAT)
  !let $ser_dt= edit($e.service_dt,'mm/dd/yy') ! VENDRBC Added for upgarde 9.0 on !GEX-MXT COMMENTED FOR ISSUE 415(DATE FORMAT)

  Let $Year4 = '1'
  Do Format-DateTime($e.service_dt, $ServiceDt, {DEFYMD}, '', '')

  Do Format-DateTime($per.birthdate, $Birth_Date, {DEFMDY}, '', '') !VENDRBC !GEX-MXT UNCOMMENTED FOR ISSUE 415(DATE FORMAT)
  !Let $Birth_Date = edit($per.birthdate,'mm/dd/yy') ! VENDRBC Added for upgarde 9.0 on !GEX-MXT COMMENTED FOR ISSUE 415(DATE FORMAT)

  Let $Year4 = '1'
  Do Format-DateTime($per.birthdate, $Birthdate, {DEFYMD}, '', '') 
   
  Let $Year4 = '1'
  Do Format-DateTime($Effective-Date, $effdt, {DEFYMD}, '', '') 
  Do Format-DateTime($Effective-Date, $eff_dt, {DEFMDY}, '', '') !VENDRBC	!GEX-MXT UNCOMMENTED FOR ISSUE 415(DATE FORMAT)
  !Let $effdt = edit($Effective-Date,'mm/dd/yy') ! VENDRBC Added for upgarde 9.0 on !GEX-MXT COMMENTED FOR ISSUE 415(DATE FORMAT)
  
  Do Format-DateTime(&hb1.coverage_begin_dt, $Term_Date, {DEFMDY}, '', '') !GEX-MXT UNCOMMENTED FOR ISSUE 415(DATE FORMAT)
  !Let $Term_Date=edit(&hb1.coverage_begin_dt,'mm/dd/yy') ! VENDRBC Added for upgarde 9.0 on !GEX-MXT COMMENTED FOR ISSUE 415(DATE FORMAT)

  Let $Year4 = '1'
  Do Format-DateTime(&hb1.coverage_begin_dt, $TermDate, {DEFYMD}, '', '')

  Move #grs_mtd To #HW_Contribution

  Move #HW_Contribution To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 099999999
  Let $HW_Contribution = $string_int_ed || Substr($string_after_Dec,1,2)

  If Substr($HW_Contribution,1,1) != '-'
    Let $HW_Contribution = '+' || Substr($HW_Contribution,2,10)
  End-If
End-Procedure


Begin-Procedure P260-Print-Record
#debug9 Show 'P260-Print-Record'

  do Get-Emp-Ssn ($per.ssn,$Ssn_Out) !GEX-TLL 04/22/2008 Added
  Print $per.name                  (+1,{#column1},30)
  !Print $per.ssn                  (+0,{#column2})  !GEX-TLL 04/22/2008 Commented
  Print $Ssn_Out                   (+0,{#column2})   !GEX-TLL 04/22/2008 Added
  Print $j.deptid                  (+0,{#column3})
  Print $Service_Dt                (+0,{#column4})	!GEX-MXT changed back to $Service_Dt as in 83
  Print $Birth_Date                (+0,{#column5})
  Print $JobTitle                  (+0,{#column6})
  Print $reg_temp                  (+0,{#column7})
  Print $j.empl_status             (+0,{#column8})
  Print $Action                    (+0,{#column9})
  Print $Action_Reason             (+0,{#column10})
  Print $eff_dt                    (+0,{#column11})
  Print $Term_Date                 (+0,{#column12})
  Print #grs_mtd                   (+0,{#column13})  Edit 999,999.99
  Print $gex_volun_low_hrs         (+0,{#column14})
  Print ' '                        (+1,{#column1})   !AXL 6/1/2004 Double Space
  
    !VENDKXY Done the changes for ITG 67862-- Begin 
    Let $deptid = $j.deptid
    Do Get-Department-Name   
            
   STRING $Vendor_ID $Benefit_Program $DeptName $deptid $per.name $Ssn_Out  $e.service_dt $per.birthdate $JobTitle $reg_temp $j.empl_status $Action  $Action_Reason $Effective-Date &hb1.coverage_begin_dt #grs_mtd $gex_volun_low_hrs BY $DELIM INTO $GEXPL603
   WRITE 2 FROM $GEXPL603


  !Vendkxy done the changes for ITG 67862-- End
End-Procedure


Begin-Procedure P270-Write-Record
#debug9 Show 'P270-Write-Record'
  
  do Get-Emp-Ssn ($per.ssn,$Ssn_Out)  !GEX-TLL 04/22/2008 Added

  Add 1 To #recs-written

  Write 1 From $benefit_program:3
               !$per.ssn:9 !GEX-TLL 04/22/2008 Commented
               $Ssn_Out:9  !GEX-TLL 04/22/2008 Added
               $name:30
               $per.sex:1
               $gex_volun_low_hrs:1
               $j.full_part_time:1
               $j.reg_temp:1
               $Action:3
               $Action_Reason:3
               $Birthdate:10
               $ServiceDt:10
               $effdt:10
               $TermDate:10
               $address1:35
               $address2:35
               $city:30
               $state:2
               $per.zip:10
               '+000000':7
               $HW_Contribution:11
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Writing output file.  Program terminating.'
    Stop
  End-If
End-Procedure


Begin-Procedure P280-After-BP-Break
#debug9 Show 'P280-After-BP-Break'

  Print '-'                        (+2,{#column13},010)   Fill
  Print 'Benefit Program'          (+1,{#column8})
  Print $Save_Benefit_Program      (0,+1)
  Print 'Totals'                   (0,+1)
  Print #BP-HW-Amount              (0,{#column13})        Edit 999,999.99
  Let #BP-HW-Amount = 0
  New-Page
End-Procedure


Begin-Procedure P290-Employee-Break
#debug9 Show 'P290-Employee-Break'

  Do P220-Get-Benefit-Termination-Row

  Evaluate $j.empl_status
    When = 'L'
    When = 'P'
    When = 'R'
    When = 'T'
      Do P230-Get-Leave-Begin-Date
      Break
    When-Other
      Do P235-Get-Status-Date
  End-Evaluate

  Do P250-Format-Record
  Do P260-Print-Record
  Do P270-Write-Record

  Add #grs_mtd To #BP-HW-Amount
  Let #grs_mtd = 0
End-Procedure


Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'

  Do P310-Close-File
  Do P320-Display-File-Totals
  Do P330-Print-Report-Totals

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
End-Procedure


Begin-Procedure P310-Close-File
#debug9 Show 'P310-Close-File'

  Close 1
End-Procedure


Begin-Procedure P320-Display-File-Totals
#debug9 Show 'P320-Display-File-Totals'

  Move #recs-written To $recs-written   888,888,888

  Display ''
  Display $recs-written          Noline
  Display ' records written to ' Noline
  Display $File1
  Display ''
End-Procedure


Begin-Procedure P330-Print-Report-Totals
#debug9 Show 'P330-Print-Report-Totals'

  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
End-Procedure


#include 'gexxx912.sqc'  !Get plan type multiple row table
#include 'gexxx927.sqc'  !Get vendor id multiple row table
#include 'getjobtl.sqc'  !Get-Job-Title procedure
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'getgeid.sqc'   !Get 7 digit Emplid  !GEX-TLL 04/22/2008 Added
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   28-Jan-2009  
#include 'getdptnm.sqc'  !Get department name  !VENDKXY Done the changes for ITG 67862
