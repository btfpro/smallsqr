!**************************************************************************
!  GEXXX978:  Retrieve "Multiple" Schedule IDs Run Controls and          *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX978_SCHED_ID_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX978_INCLUDE_SCHED_ID_CRITERIA                                  *
!    $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA                                  *
!    $GEXXX978_INCLUDE_SCHED_ID_DISPLAY_CRITERIA                          *
!    $GEXXX978_EXCLUDE_SCHED_ID_DISPLAY_CRITERIA                          *
!                                                                         *                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX978_SCHED_ID_Alias = 'bas.SCHED_ID'                         *
!    Do GEXXX978-Select-sched-id-Parameters                               *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM ps_bas_activity bas                                             *
!    WHERE[$GEXXX978_INCLUDE_SCHED_ID_CRITERIA]                           *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	ISDVNPK		12/16/2010 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX978-Select-sched-id-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_SCHED_ID_ROWS = 0
  Let $GEXXX978_INCLUDE_SCHED_ID_CRITERIA = ''
  Let $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA = ''
  Let $GEXXX978_INCLUDE_SCHED_ID_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX978_EXCLUDE_SCHED_ID_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_SCHED_ID.SCHED_ID

  Add 1 To #GEX_RC_SCHED_ID_ROWS

  If #GEX_RC_SCHED_ID_ROWS = 1
    Let $GEXXX978_INCLUDE_SCHED_ID_CRITERIA = '(' || $GEXXX978_SCHED_ID_Alias || ' In ('''     || &GEX_RC_SCHED_ID.SCHED_ID || ''''
    Let $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA = '(' || $GEXXX978_SCHED_ID_Alias || ' Not In (''' || &GEX_RC_SCHED_ID.SCHED_ID || ''''
  Else
    Let $GEXXX978_INCLUDE_SCHED_ID_CRITERIA = $GEXXX978_INCLUDE_SCHED_ID_CRITERIA || ',''' || &GEX_RC_SCHED_ID.SCHED_ID || ''''
    Let $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA = $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA || ',''' || &GEX_RC_SCHED_ID.SCHED_ID || ''''
  End-If

From  PS_GEX_RC_SCHED_ID GEX_RC_SCHED_ID

Where GEX_RC_SCHED_ID.OPRID                    = [$OprID]
And   GEX_RC_SCHED_ID.RUN_CNTL_ID              = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_SCHED_ID.SCHED_ID, ' ') != ' '

End-Select

  If #GEX_RC_SCHED_ID_ROWS = 0
    Let $GEXXX978_INCLUDE_SCHED_ID_CRITERIA = $GEXXX978_SCHED_ID_Alias || ' = ' || $GEXXX978_SCHED_ID_Alias
    Let $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA = $GEXXX978_SCHED_ID_Alias || ' = ' || $GEXXX978_SCHED_ID_Alias
  Else
    Let $GEXXX978_INCLUDE_SCHED_ID_CRITERIA = $GEXXX978_INCLUDE_SCHED_ID_CRITERIA || '))'
    Let $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA = $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA || '))'
    Let $GEXXX978_INCLUDE_SCHED_ID_DISPLAY_CRITERIA = $GEXXX978_INCLUDE_SCHED_ID_CRITERIA
    Let $GEXXX978_EXCLUDE_SCHED_ID_DISPLAY_CRITERIA = $GEXXX978_EXCLUDE_SCHED_ID_CRITERIA
  End-If

End-Procedure