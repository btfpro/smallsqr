!***********************************************************************
! GEXHR063.SQR:                                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-2006 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXHR063.SQR :                                                       *
!                                                                      *
! Narrative:            Create a program that will report employee head*
!			counts on an Annual basis.  		       *
!			ITG-35733.				       *
!								       *
! #Debug Used:          x                                              *
!								       *
! SQL Tables:           PS_JOB, PS_EMPLOYMENT, PS_DEPT_TBL             * 
!								       *
! Written by:           Rapidigm, Deepak. S, 04/19/06                  *
!                                                                      *
! Normally Run:         This process will run annually.  	       *
!                                                                      *
! Control Parms:        Reads the run control table PS_GEX_RUN_CNTL    *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)

#define #column1  001
#define #column2  010
#define #column3  050

!***********************************************************************
! Defines Heading of the report
!***********************************************************************
Begin-Heading 5
  let $ReportID = 'GEXHR063'
  #include 'stdhdg01.sqc'

  Print 'OSHA Counts'     (3,1) center
  print $rpt-subtitle  (+1,1) center

  Print 'DEPTID'		(+2,{#column1}) underline
  Print 'DESCR'			(0,{#column2})	underline	
  Print 'COUNT'			(0,{#column3})	underline

End-Heading


!***********************************************************************
! Start of Program
!***********************************************************************
Begin-Report

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Stdapi-Init             !stdapi.sqc
  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = 'gexhr063'
  end-if

  ! get hire_date, term_date and sal_date from run control page
  do Get-Run-Control	     !gexrnctl.sqc  

if isnull(&thrudate)
  	do Select-Pay-End-Date
	let $thrudate = $xxx_pay_end_dt
  else
        let $thrudate = &thrudate
  end-if

  if isnull(&fromdate)
    	do Select-Pay-End-Date
	let $fromdate = $xxx_pay_begin_dt
  else
	let $fromdate = &fromdate
  end-if

  let $report-date = edit($thrudate,'MM/DD/YYYY')
  let $rpt-subtitle = 'Report as of ' || $report-date

  #debugx show '<' $fromdate '><' $thrudate '>'

  do Build-Dynamic-Where
  do Select-Data
  do stdapi-term

End-Report


!********************************************************
! Procedure used to select current pay_begin_dt and 
! pay_end_dt if none specified on the run control page
!********************************************************
begin-procedure Select-Pay-End-Date
begin-select distinct
max(xxx.pay_end_dt)	&xxx.pay_end_dt
max(xxx.pay_begin_dt)   &xxx.pay_begin_dt

   move &xxx.pay_end_dt to $xxx_pay_end_dt
   move &xxx.pay_begin_dt to $xxx_pay_begin_dt
  
from ps_pay_calendar xxx
where xxx.pay_confirm_run = 'Y'
and xxx.company <= '999'
and xxx.pay_end_dt <= sysdate

end-select
end-procedure 


!***********************************************************************
! Builds the Where clause for Company, Deptid, Empl_Status
!***********************************************************************
Begin-Procedure Build-Dynamic-Where

	Let $GEXXX900_Company_Alias = 'A.COMPANY'
	Do GEXXX900-Select-Company-Parameters   
	Let $GEXXX902_Deptid_Alias = 'A.DEPTID'
	Do GEXXX902-Select-Deptid-Parameters 
	Let $GEXXX903_Emp_Stat_Alias = 'A.EMPL_STATUS'
	Do GEXXX903-Select-Emp-Stat-Parameters  
	Let $GEXXX907_DIV_CD_Alias = 'D.GEX_COMPANY_DIV_CD'
	do GEXXX907-Select-DIV-CD-Parameters

	#debugx show $GEXXX900_INCLUDE_COMPANY_CRITERIA
	#debugx show $GEXXX902_INCLUDE_DEPTID_CRITERIA
	#debugx show $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
	#debugx show $GEXXX907_INCLUDE_DIV_CD_CRITERIA

End-Procedure


!***********************************************************************
! Main Procedure which selects the data.
!***********************************************************************
Begin-Procedure Select-Data
Begin-Select 
A.DEPTID	(+2,{#column1})
C.DESCR		(0,{#column2})
COUNT(*)	(0,{#column3})


FROM PS_JOB A, PS_EMPLOYMENT B, PS_DEPT_TBL C, PS_GEX_DEPT_TBL D
 WHERE A.EFFDT =
      (SELECT MAX(A_ED.EFFDT) FROM PS_JOB A_ED
        WHERE A.EMPLID = A_ED.EMPLID
          AND A.EMPL_RCD = A_ED.EMPL_RCD
          AND A_ED.EFFDT <= $thrudate)
   AND A.EFFSEQ =
      (SELECT MAX(A_ES.EFFSEQ) FROM PS_JOB A_ES
        WHERE A.EMPLID = A_ES.EMPLID
          AND A.EMPL_RCD = A_ES.EMPL_RCD
          AND A.EFFDT = A_ES.EFFDT)
   AND A.EMPLID = B.EMPLID
   AND A.EMPL_RCD = B.EMPL_RCD
   AND C.SETID = A.SETID_DEPT
   AND C.DEPTID = A.DEPTID
   AND C.EFFDT =
      (SELECT MAX(C_ED.EFFDT) FROM PS_DEPT_TBL C_ED
        WHERE C.SETID = C_ED.SETID
          AND C.DEPTID = C_ED.DEPTID
          AND C_ED.EFFDT <= SYSDATE)
 AND D.SETID = C.SETID
 AND D.DEPTID = C.DEPTID
 AND D.EFFDT = 
  	  	(SELECT MAX(D_ED.EFFDT) FROM PS_GEX_DEPT_TBL D_ED
  	  	WHERE D.SETID = D_ED.SETID
		AND D.DEPTID = D_ED.DEPTID
		AND D.EFFDT <= SYSDATE)	   
!  AND D.GEX_COMPANY_DIV_CD IN ('GEM')
 AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
 AND [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
 AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
 AND ( [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
!   AND A.COMPANY IN ('BRM','GEI','RFI','RTP','TMK')
 !  AND A.DEPTID IN ('W009','W010')
  ! AND (A.EMPL_STATUS IN ('A','L','P','S')
      OR B.TERMINATION_DT BETWEEN $fromdate AND $thrudate)
GROUP BY A.DEPTID, C.DESCR
ORDER BY 1
End-Select
End-Procedure


#include 'stdapi.sqc'    !Process Scheduler Interface
#include 'reset.sqc'     !Reset Printer Procedure
#include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date math
#include 'gexrnctl.sqc'  !Routine for Get-Run-Control
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#include 'gexxx900.sqc'	 !Get Company from Run Control Page
#include 'gexxx903.sqc'  !Get Emp_Status from Run Control Page
#include 'gexxx902.sqc'  !Get Deptid from Run Control Page
#include 'gexxx907.sqc'	 !INCLUDE_DIV_CD_CRITERIA