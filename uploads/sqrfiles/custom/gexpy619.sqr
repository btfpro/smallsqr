!***********************************************************************
! GEXPY619:  941 TAX PAYMENTS                                          *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY619:             941 Tax Payments                               *
!                                                                      *
! Narrative:            This program creates a report subtotalled by   *
!                       franchise and a file containing information    *
!                       used by the payroll department for the 941.    *
!                       The file is loaded into an Excel spreadsheet   *
!                       weekly and at the end of the quarter, the file *
!                       is used to print the 941 forms.                *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           gex_dept_tbl                                   *
!                       pay_calendar                                   *
!                       pay_tax                                        *
!                       pay_check                                      *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS      DATE          DESCRIPTION OF THE CHANGE                *
!***********************************************************************
! JDH           05/20/98      Initial Creation.                        *
!                                                                      *
! SXK           09/08/98      Added OUTFILE variable.                  *
!                                                                      *
! JNB           02/05/99      The report needs a sub total by company. *
!                             The file should only have totals by cmpy *
!                             File is missing check date.              *
!                             Earned income field is showing negative  *
!                             signs twice.                             *
!                             For the interface amount fields the      *
!                             implied decimal was changed to explicit  *
!                             decimal.                                 *
!                                                                      *
! SXK           04/14/99      Changed to combine off cycle check       *
!			      information with on cycle information    *
!			      and use on cycle check date in both      *
!                             file and report.                         *
!                                                                      *
! JDH           04/19/99      Added total combined tax column to the   *
!                             report.                                  *
!                                                                      *
! AXL           11/07/01      Modify PAY_CALENDAR select logic to      *
!                             exclude prior dates not within the max   *
!                             effdt range.                             *
!                                                                      *
! GBD           07/11/2003    Added code to send data file in an email.*
!GEX_PY268      08/22/2007    No chnages are made to sqr.              *
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA Include Add'l Medicare Tax *
!                      Replaced the EIC column with Add'l Medicare Tax *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization


#define #column1     1
#define #column2     9
#define #column3    17
#define #column4    33
#define #column5    49
#define #column6    65
#define #column7    81
#define #column8    97
#define #column9   113
#define #column10  129
#define #column11  145
#define #column12  161


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 7
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'

  do Format-DateTime($Pay_End_Dt, $Out1, {DEFMDY},'','')
  Print 'Pay End Dt:'          (3,1)
  Print $Out1                  (,13)

  do Format-DateTime($Check_Dt, $Out2, {DEFMDY},'','')
  Print 'Check Date:'          (4,1)
  Print $Out2                  (,13)

  Let $X000_temp_out = 'Sorted by ' || $X000_Order_By
  Print $X000_temp_out         (3,)  Center

  Print '      WV STATE'       (+3,{#column3})
  Print '      OH STATE'       (+0,{#column4})
  Print '      PA STATE'       (+0,{#column5})
  Print '    EE FEDERAL'       (+0,{#column6})
  Print '     EE SOCIAL'       (+0,{#column7})
  Print '     ER SOCIAL'       (+0,{#column8})
  Print '   EE MEDICARE'       (+0,{#column9})
  Print '   ER MEDICARE'       (+0,{#column10})
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
!  Print ' EARNED INCOME'       (+0,{#column11})
  Print '    ADDITIONAL'       (+0,{#column11})
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
  Print '         TOTAL'       (+0,{#column12})

  Print 'COMPANY'              (+1,{#column1})
  Print 'DEPTID'               (+0,{#column2})
  Print '    INCOME TAX'       (+0,{#column3})
  Print '    INCOME TAX'       (+0,{#column4})
  Print '    INCOME TAX'       (+0,{#column5})
  Print '    INCOME TAX'       (+0,{#column6})
  Print '  SECURITY TAX'       (+0,{#column7})
  Print '  SECURITY TAX'       (+0,{#column8})
  Print '  MEDICARE TAX'       (+0,{#column9})
  Print '  MEDICARE TAX'       (+0,{#column10})
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
!  Print '        CREDIT'       (+0,{#column11})
  Print '  MEDICARE TAX'       (+0,{#column11})
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
  Print '  COMBINED TAX'       (+0,{#column12})
End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPY619'
  Let $ReportTitle   =   'FRANCHISE TAX PAYMENTS'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show '$ReportDate  = ' $ReportDate
  Show '$ReportTime  = ' $ReportTime

  If $prcs_process_instance = ''
    Do Ask-As-Of-Date
    Let $X000_Where_Select_AsOfDate = $AsOfDate
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  Do P130-Select-Check-Date

  Do Format-DateTime($X000_Where_Select_AsOfDate, $X000_Report_Heading_AsOfDate, {DEFDATE}, '', '')
  Let $X000_Order_By = 'Company, Department'
  Show '$X000_Where_Select_AsOfDate = ' $X000_Where_Select_AsOfDate

  Do P140-Open-File
  Do P150-Initialize-Fields
  Do P160-Initialize-Totals
End-Procedure


Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXXX922-Select-Parameters

  Let $X000_Where_Select_AsOfDate = $GEX_RC_PAY.AsOfDate
  If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
    Let $X000_Where_Select_AsOfDate = $AsOfToday
  End-If
End-Procedure


Begin-Procedure P130-Select-Check-Date
#debug9 Show 'P130-Select-Check-Date'
Begin-Select Loops=1
PC.PAY_END_DT
PC.CHECK_DT

  Let $pay_end_dt = &PC.PAY_END_DT
  Let $check_dt   = &PC.CHECK_DT

  Let $Year4 = '1'
  do Format-DateTime($pay_end_dt, $Week-End-Date, {DEFYMD},'','')

  Let $Year4 = '1'
  do Format-DateTime($check_dt, $Check-Date, {DEFYMD},'','')

FROM   PS_PAY_CALENDAR PC
!AXL 11/07/2001 - Begin
!WHERE  PC.COMPANY BETWEEN '000' AND '999'
!Use GEI/RET to guarantee Retail pay end and check dates
WHERE  PC.COMPANY = 'GEI' AND PC.PAYGROUP ='RET'
!AXL 11/07/2001 - End

AND    PC.PAY_END_DT           =
      (SELECT MAX(PAY_END_DT)
       FROM   PS_PAY_CALENDAR
       WHERE  COMPANY          = PC.COMPANY
       AND    PAYGROUP         = PC.PAYGROUP
       AND    PAY_END_DT      <= $X000_Where_Select_AsOfDate
       AND    PAY_CONFIRM_RUN  = 'Y'
       AND    COMPANY = 'GEI'	!AXL 11/07/2001
       AND    PAYGROUP ='RET')	!AXL 11/07/2001
AND    PC.PAY_CONFIRM_RUN      = 'Y'

End-Select
End-Procedure


begin-procedure P140-Open-File
#debug9 Show 'P140-Open-File'
  Let $file1 = '{OUTFILE}' || Lower($ReportID) || '.dat'

  Open $file1
       As 1
       For-Writing
       Record=171:Fixed
       Status=#filestat

  If #filestat != 0
     Show 'Error opening output file.  Program terminating.'
     Stop
  End-If
End-Procedure


Begin-Procedure P150-Initialize-Fields
#debug9 Show 'P150-Initialize-Fields'
 Let #WV-Income-Tax             = 0
  Let #OH-Income-Tax             = 0
  Let #PA-Income-Tax             = 0
  Let #Federal-Income-Tax        = 0
  Let #EE-SS-Tax                 = 0
  Let #ER-SS-Tax                 = 0
  Let #EE-Medicare-Tax           = 0
  Let #ER-Medicare-Tax           = 0
  Let #Addl-Medicare-Tax         = 0             !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
!  Let #Earned-Income-Credit      = 0            !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
  Let #Total-Combined-Tax        = 0
End-Procedure


Begin-Procedure P155-Initialize-Aft-Company
#debug9 Show 'P155-Initialize-Aft-Company'
  Let #Company-WV-Income-Tax        = 0
  Let #Company-OH-Income-Tax        = 0
  Let #Company-PA-Income-Tax        = 0
  Let #Company-Federal-Income-Tax   = 0
  Let #Company-EE-SS-Tax            = 0
  Let #Company-ER-SS-Tax            = 0
  Let #Company-EE-Medicare-Tax      = 0
  Let #Company-ER-Medicare-Tax      = 0
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
  Let #Company-Addl-Medicare-Tax    = 0
!  Let #Company-Earned-Income-Credit = 0
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
  Let #Company-Total-Combined-Tax   = 0

  Let $Company-WV-Income-Tax             = '000000000000'
  Let $Company-OH-Income-Tax             = '000000000000'
  Let $Company-PA-Income-Tax             = '000000000000'
  Let $Company-Federal-Income-Tax        = '000000000000'
  Let $Company-EE-SS-Tax                 = '000000000000'
  Let $Company-ER-SS-Tax                 = '000000000000'
  Let $Company-EE-Medicare-Tax           = '000000000000'
  Let $Company-ER-Medicare-Tax           = '000000000000'
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
  Let $Company-Addl-Medicare-Tax         = '000000000000'
!  Let $Company-Earned-Income-Credit      = '000000000000'
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************

  Let $Company-WV-Income-Tax-Sign        = ' '
  Let $Company-OH-Income-Tax-Sign        = ' '
  Let $Company-PA-Income-Tax-Sign        = ' '
  Let $Company-Federal-Income-Tax-Sign   = ' '
  Let $Company-EE-SS-Tax-Sign            = ' '
  Let $Company-ER-SS-Tax-Sign            = ' '
  Let $Company-EE-Medicare-Tax-Sign      = ' '
  Let $Company-ER-Medicare-Tax-Sign      = ' '
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
  Let $Company-Addl-Medicare-Tax-Sign      = ' '
!  Let $Company-Earned-Income-Credit-Sign = ' '
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
End-Procedure


Begin-Procedure P160-Initialize-Totals
#debug9 Show 'P160-Initialize-Totals'
  Let #Total-WV-Income-Tax        = 0
  Let #Total-OH-Income-Tax        = 0
  Let #Total-PA-Income-Tax        = 0
  Let #Total-Federal-Income-Tax   = 0
  Let #Total-EE-SS-Tax            = 0
  Let #Total-ER-SS-Tax            = 0
  Let #Total-EE-Medicare-Tax      = 0
  Let #Total-ER-Medicare-Tax      = 0
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
  Let #Total-Addl-Medicare-Tax    = 0
!  Let #Total-Earned-Income-Credit = 0
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
  Let #Total-Total-Combined-Tax   = 0
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select
PT.COMPANY     ()  On-Break  Level=1
                             Print=Never
                             After=P225-Company-Break

PC.DEPTID      ()  On-Break  Level=2
                             Print=Never
                             After=P220-Dump-Record

PT.STATE
PT.TAX_CLASS

Sum(PT.TAX_CUR) &PT.TAX_CUR

  Let $Data-Found-Flag = 'Y'
  Do P210-Process-Company-Deptid

From   PS_PAY_CHECK  PC,
       PS_PAY_TAX    PT

Where  PC.PAY_END_DT = $pay_end_dt

And    PT.COMPANY    = PC.COMPANY
And    PT.PAYGROUP   = PC.PAYGROUP
And    PT.PAY_END_DT = PC.PAY_END_DT
And    PT.OFF_CYCLE  = PC.OFF_CYCLE
And    PT.PAGE_NUM      = PC.PAGE_NUM
And    PT.LINE_NUM      = PC.LINE_NUM
And    PT.SEPCHK     = PC.SEPCHK

And  ((PT.STATE     IN ('$U','PA','OH','WV')
And    PT.TAX_CLASS  = 'H')
Or    (PT.STATE      = '$U'
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
And    PT.TAX_CLASS IN ('H','D','E','F','C','7')))
!And    PT.TAX_CLASS IN ('H','D','E','F','C')))
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************

And    RTRIM(PT.LOCALITY, ' ') IS NULL

And    PC.COMPANY BETWEEN '000' AND '999'

Group By PT.COMPANY,
         PC.DEPTID,
         PT.STATE,
         PT.TAX_CLASS

Order By PT.COMPANY,
         PC.DEPTID,
         PT.STATE,
         PT.TAX_CLASS
End-Select
End-Procedure


Begin-Procedure P210-Process-Company-Deptid
#debug9 Show 'P210-Process-Company-Deptid'
  Evaluate &PT.TAX_CLASS
    When = 'H'
      Evaluate &PT.STATE
        When = '$U'
          Let $Company       = &PT.COMPANY
          Let $Deptid        = &PC.DEPTID
          Move &PT.TAX_CUR To #Federal-Income-Tax
          Break
        When = 'WV'
          Move &PT.TAX_CUR To #WV-Income-Tax
          Break
        When = 'OH'
          Move &PT.TAX_CUR To #OH-Income-Tax
          Break
        When = 'PA'
          Move &PT.TAX_CUR To #PA-Income-Tax
          Break
      End-Evaluate
      Break
    When = 'D'
      Move &PT.TAX_CUR To #EE-SS-Tax
      Break
    When = 'F'
      Move &PT.TAX_CUR To #EE-Medicare-Tax
      Move &PT.TAX_CUR To #ER-Medicare-Tax
      Break
    When = 'E'
      Move &PT.TAX_CUR To #ER-SS-Tax
      Break
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
!    When = 'C'
!      Move &PT.TAX_CUR To #Earned-Income-Credit
    When = '7'
      Move &PT.TAX_CUR To #Addl-Medicare-Tax
      Break
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
  End-Evaluate
End-Procedure


Begin-Procedure P220-Dump-Record
#debug9 Show 'P220-Dump-Record'
  Let #Total-Combined-Tax = #Federal-Income-Tax +
                            #EE-SS-Tax +
                            #ER-SS-Tax +
                            #EE-Medicare-Tax +
                            #ER-Medicare-Tax +
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
                            #Addl-Medicare-Tax
!                            #Earned-Income-Credit
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************

  Do P222-Print-Record
  Do P300-Accum-Company-Totals
  Do P150-Initialize-Fields
End-Procedure


Begin-Procedure P222-Print-Record
#debug9 Show 'P222-Print-Record'
  if #current-line > 48
    new-page
  end-if

  Print $Company              (+1,{#column1})
  Print $Deptid               (+0,{#column2})
  Print #WV-Income-Tax        (+0,{#column3})     Edit 999,999,999.99
  Print #OH-Income-Tax        (+0,{#column4})     Edit 999,999,999.99
  Print #PA-Income-Tax        (+0,{#column5})     Edit 999,999,999.99
  Print #Federal-Income-Tax   (+0,{#column6})     Edit 999,999,999.99
  Print #EE-SS-Tax            (+0,{#column7})     Edit 999,999,999.99
  Print #ER-SS-Tax            (+0,{#column8})     Edit 999,999,999.99
  Print #EE-Medicare-Tax      (+0,{#column9})     Edit 999,999,999.99
  Print #ER-Medicare-Tax      (+0,{#column10})    Edit 999,999,999.99
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
  Print #Addl-Medicare-Tax    (+0,{#column11})    Edit 999,999,999.99
!  Print #Earned-Income-Credit (+0,{#column11})    Edit 999,999,999.99
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
  Print #Total-Combined-Tax   (+0,{#column12})    Edit 999,999,999.99
End-Procedure


Begin-Procedure P225-Company-Break
#debug9 Show 'P225-Company-Break'
   Do Get-Original-Dept

   Print '-'                           (+1,{#column3},14)   Fill
   Print '-'                           (+0,{#column4},14)   Fill
   Print '-'                           (+0,{#column5},14)   Fill
   Print '-'                           (+0,{#column6},14)   Fill
   Print '-'                           (+0,{#column7},14)   Fill
   Print '-'                           (+0,{#column8},14)   Fill
   Print '-'                           (+0,{#column9},14)   Fill
   Print '-'                           (+0,{#column10},14)  Fill
   Print '-'                           (+0,{#column11},14)  Fill
   Print '-'                           (+0,{#column12},14)  Fill

   Print $Company                      (+1,{#column1})
   Print 'Totals'                      (+0,{#column2})
   Print #Company-WV-Income-Tax        (+0,{#column3})      Edit 999,999,999.99
   Print #Company-OH-Income-Tax        (+0,{#column4})      Edit 999,999,999.99
   Print #Company-PA-Income-Tax        (+0,{#column5})      Edit 999,999,999.99
   Print #Company-Federal-Income-Tax   (+0,{#column6})      Edit 999,999,999.99
   Print #Company-EE-SS-Tax            (+0,{#column7})      Edit 999,999,999.99
   Print #Company-ER-SS-Tax            (+0,{#column8})      Edit 999,999,999.99
   Print #Company-EE-Medicare-Tax      (+0,{#column9})      Edit 999,999,999.99
   Print #Company-ER-Medicare-Tax      (+0,{#column10})     Edit 999,999,999.99
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
   Print #Company-Addl-Medicare-Tax    (+0,{#column11})     Edit 999,999,999.99
!   Print #Company-Earned-Income-Credit (+0,{#column11})     Edit 999,999,999.99
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
   Print #Company-Total-Combined-Tax   (+0,{#column12})     Edit 999,999,999.99
   Print ' '                           (+3,{#column1})

   Do P224-Format-Record
   Do P226-Accumulate-Totals
   Do P228-Write-Record
   Do P155-Initialize-Aft-Company
End-Procedure


Begin-Procedure Get-Original-dept
#debug9 Show 'Get-Original-dept'
Begin-Select
d.department   &department

  Let $deptid = &department

From   ps_gex_dept_tbl d

Where  d.deptid          = $deptid

And    d.effdt           =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid     = d.deptid
       And    effdt     <= $X000_Where_Select_AsOfDate)

And    d.sequence_number =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid     = d.deptid
       And    effdt      = d.effdt)
End-Select
End-Procedure


Begin-Procedure P224-Format-Record
#debug9 Show 'P224-Format-Record'
  Move #Company-Federal-Income-Tax   To $Company-Federal-Income-Tax   000000000.00MI
  Move #Company-WV-Income-Tax        To $Company-WV-Income-Tax        000000000.00MI
  Move #Company-OH-Income-Tax        To $Company-OH-Income-Tax        000000000.00MI
  Move #Company-PA-Income-Tax        To $Company-PA-Income-Tax        000000000.00MI
  Move #Company-EE-SS-Tax            To $Company-EE-SS-Tax            000000000.00MI
  Move #Company-ER-SS-Tax            To $Company-ER-SS-Tax            000000000.00MI
  Move #Company-EE-Medicare-Tax      To $Company-EE-Medicare-Tax      000000000.00MI
  Move #Company-ER-Medicare-Tax      To $Company-ER-Medicare-Tax      000000000.00MI

!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
  Move #Company-Addl-Medicare-Tax    To $Company-Addl-Medicare-Tax    000000000.00MI
!  Move #Company-Earned-Income-Credit To $Company-Earned-Income-Credit 000000000.00MI
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************

  If #Company-Federal-Income-Tax < 0
    Let $Company-Federal-Income-Tax-Sign = '-'
  End-If

  If #Company-WV-Income-Tax < 0
    Let $Company-WV-Income-Tax-Sign = '-'
  End-If

  If #Company-OH-Income-Tax < 0
    Let $Company-OH-Income-Tax-Sign = '-'
  End-If

  If #Company-PA-Income-Tax < 0
    Let $Company-PA-Income-Tax-Sign = '-'
  End-If

  If #Company-EE-SS-Tax < 0
    Let $Company-EE-SS-Tax-Sign = '-'
  End-If

  If #Company-ER-SS-Tax < 0
    Let $Company-ER-SS-Tax-Sign = '-'
  End-If

  If #Company-EE-Medicare-Tax < 0
    Let $Company-EE-Medicare-Tax-Sign = '-'
  End-If

  If #Company-ER-Medicare-Tax < 0
    Let $Company-ER-Medicare-Tax-Sign = '-'
  End-If

!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
  If #Company-Addl-Medicare-Tax < 0
    Let $Company-Addl-Medicare-Tax-Sign = '-'
  End-If
!  If #Company-Earned-Income-Credit < 0
!    Let $Company-Earned-Income-Credit-Sign = '-'
!  End-If
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************
End-Procedure


Begin-Procedure P226-Accumulate-Totals
#debug9 Show 'P226-Accumulate-Totals'
  Add #Company-Federal-Income-Tax    To #Total-Federal-Income-Tax
  Add #Company-WV-Income-Tax         To #Total-WV-Income-Tax
  Add #Company-OH-Income-Tax         To #Total-OH-Income-Tax
  Add #Company-PA-Income-Tax         To #Total-PA-Income-Tax
  Add #Company-EE-SS-Tax             To #Total-EE-SS-Tax
  Add #Company-ER-SS-Tax             To #Total-ER-SS-Tax
  Add #Company-EE-Medicare-Tax       To #Total-EE-Medicare-Tax
  Add #Company-ER-Medicare-Tax       To #Total-ER-Medicare-Tax
  Add #Company-Addl-Medicare-Tax     To #Total-Addl-Medicare-Tax          !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
!  Add #Company-Earned-Income-Credit  To #Total-Earned-Income-Credit      !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
  Add #Company-Total-Combined-Tax    To #Total-Total-Combined-Tax
End-Procedure


Begin-Procedure P300-Accum-Company-Totals
#debug9 Show 'P300-Accum-Company-Totals'
  Add #Federal-Income-Tax    To #Company-Federal-Income-Tax
  Add #WV-Income-Tax         To #Company-WV-Income-Tax
  Add #OH-Income-Tax         To #Company-OH-Income-Tax
  Add #PA-Income-Tax         To #Company-PA-Income-Tax
  Add #EE-SS-Tax             To #Company-EE-SS-Tax
  Add #ER-SS-Tax             To #Company-ER-SS-Tax
  Add #EE-Medicare-Tax       To #Company-EE-Medicare-Tax
  Add #ER-Medicare-Tax       To #Company-ER-Medicare-Tax
  Add #Addl-Medicare-Tax     To #Company-Addl-Medicare-Tax              !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
!  Add #Earned-Income-Credit  To #Company-Earned-Income-Credit          !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
  Add #Total-Combined-Tax    To #Company-Total-Combined-Tax
End-Procedure


Begin-Procedure P228-Write-Record
#debug9 Show 'P228-Write-Record'
  Add 1 To #recs-written

  Write 1 From $Company:3
               ';':1
               &department:10
               ';':1
               $Week-End-Date:10
               ';':1
               $Check-Date:10
               ';':1
               $Company-WV-Income-Tax:12
               ';':1
               $Company-WV-Income-Tax-Sign:1
               ';':1
               $Company-OH-Income-Tax:12
               ';':1
               $Company-OH-Income-Tax-Sign:1
               ';':1
               $Company-PA-Income-Tax:12
               ';':1
               $Company-PA-Income-Tax-Sign:1
               ';':1
               $Company-Federal-Income-Tax:12
               ';':1
               $Company-Federal-Income-Tax-Sign:1
               ';':1
               $Company-EE-SS-Tax:12
               ';':1
               $Company-EE-SS-Tax-Sign:1
               ';':1
               $Company-ER-SS-Tax:12
               ';':1
               $Company-ER-SS-Tax-Sign:1
               ';':1
               $Company-EE-Medicare-Tax:12
               ';':1
               $Company-EE-Medicare-Tax-Sign:1
               ';':1
               $Company-ER-Medicare-Tax:12
               ';':1
               $Company-ER-Medicare-Tax-Sign:1
               ';':1
!********************************************************
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  Begin   *
               $Company-Addl-Medicare-Tax:12               !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
!               $Company-Earned-Income-Credit:12           !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
               ';':1
               $Company-Addl-Medicare-Tax-Sign:1           !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
               !$Company-Earned-Income-Credit-Sign:1       !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
!GEXHR_848_E158770_01  09/25/2013   ISDVMZA --  End     *
!********************************************************

    Status=#filestat

  If #filestat != 0
     Show 'Error writing output file.  Program terminating.'
     Stop
  End-If
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  Else
    Do P310-Print-Totals
  End-If

  Do Format-Number(#recs-written,$recs-written,'888,888,888')
  Show ''
  Show $recs-written ' records written to ' $file1
  Show ''
  Do P320-Close-File
  !Do Email-File                                         !GBD 07/11/2003

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'


End-Procedure


Begin-Procedure P310-Print-Totals
#debug9 Show 'P310-Print-Totals'

  Print '-'                           (+2,{#column3},14)   Fill
  Print '-'                           (+0,{#column4},14)   Fill
  Print '-'                           (+0,{#column5},14)   Fill
  Print '-'                           (+0,{#column6},14)   Fill
  Print '-'                           (+0,{#column7},14)   Fill
  Print '-'                           (+0,{#column8},14)   Fill
  Print '-'                           (+0,{#column9},14)   Fill
  Print '-'                           (+0,{#column10},14)  Fill
  Print '-'                           (+0,{#column11},14)  Fill
  Print '-'                           (+0,{#column12},14)  Fill

  Print 'Grand Totals'                (+1,{#column1})
  Print #Total-WV-Income-Tax          (+0,{#column3},014)  Edit 999,999,999.99
  Print #Total-OH-Income-Tax          (+0,{#column4},014)  Edit 999,999,999.99
  Print #Total-PA-Income-Tax          (+0,{#column5},014)  Edit 999,999,999.99
  Print #Total-Federal-Income-Tax     (+0,{#column6},014)  Edit 999,999,999.99
  Print #Total-EE-SS-Tax              (+0,{#column7},014)  Edit 999,999,999.99
  Print #Total-ER-SS-Tax              (+0,{#column8},014)  Edit 999,999,999.99
  Print #Total-EE-Medicare-Tax        (+0,{#column9},014)  Edit 999,999,999.99
  Print #Total-ER-Medicare-Tax        (+0,{#column10},014) Edit 999,999,999.99
  Print #Total-Addl-Medicare-Tax      (+0,{#column11},014) Edit 999,999,999.99    !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
!  Print #Total-Earned-Income-Credit   (+0,{#column11},014) Edit 999,999,999.99   !GEXHR_848_E158770_01  09/25/2013   ISDVMZA
  Print #Total-Total-Combined-Tax     (+0,{#column12},014) Edit 999,999,999.99
End-Procedure


Begin-Procedure P320-Close-File
#debug9 Show 'P320-Close-File'
  Close 1

  If #filestat != 0
     Show 'Error closing output file.  Program terminating.'
     Stop
  End-If
End-Procedure

!GBD 07/11/2003 Begin
!**************************************************************************************
Begin-Procedure Email-File
!**************************************************************************************
   do GEXXX951-Select-Oracle-Instance
   let $db_name = lower($GEXXX951_Oracle_Instance)
   Do GEXXX971-Select-Trans-Type-Parameters
   if ($GEXXX971_EMAIL_ADDR_CRITERIA = '') and ($GEXXX971_FTP_ADDR_CRITERIA = '')
      show 'Either no rows exist on GEX_INTFC_RULE/DATA for this Oprid/Run Control ID'
      show 'or the most current rows under this ID have been set to inactive. Data will'
      show 'not be sent.'
   else
      if $GEXXX971_GEX_TRANS_TYPE = 'E'
         show $GEXXX971_EMAIL_ADDR_CRITERIA
         show 'This is the email statement produced: '

         let $UNIX_MAIL_COMMAND = '/usr/local/bin/todos -v $PS_HOME/outgoing/gexpy619.dat' ||
         ' &&/usr/local/bin/mailer.pl "'||$GEXXX971_EMAIL_ADDR_CRITERIA||'" "941 Independents" '||
         '"$BINDIR/gexpy619_msg.sh" $PS_HOME/outgoing/gexpy619.dat'
         show $UNIX_MAIL_COMMAND

         CALL SYSTEM USING $UNIX_MAIL_COMMAND #system_status

        if #system_status <> 0
            add 1 to #errorcount
            display 'eMail of gexpy619.dat failed'
            STOP
         else
            display 'gexpy619.dat was emailed successfully'
         end-if

      end-if
      if $GEXXX971_GEX_TRANS_TYPE = 'F'
         show $GEXXX971_FTP_ADDR_CRITERIA
         show 'Need to insert a Call FTP shell script to run ftp'
      end-if
   end-if

End-Procedure
!GBD 07/11/2003 End

!**************************************************************************************
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
#Include 'gexxx951.sqc'  !Oracle Database Name             !GBD 07/11/2003
#Include 'gexxx971.sqc'  !Email and FTP Parameters         !GBD 07/11/2003
