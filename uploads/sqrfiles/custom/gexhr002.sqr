!***********************************************************************
!  GEXHR002:  EMPLOYEES WITH FIVE TO FIFTY YRS SERVICE                 *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXHR002:  		Employees with Five to Fifty Yrs Service       *
!                                                                      *
! Narrative:		This program will produce a summary report by  *
!                       company of employees who have completed 5 yrs, *
!                       10 yrs, 15 yrs, 20 yrs, 25 yrs, 30 yrs,        *
!                       35 yrs, 40 yrs, 45 yrs, and 50 yrs of service. *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values              *
!			      #debug7 data returned from sql calls     *
!                                                                      *
! SQL Tables:		gex_dept_tbl                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:		Jim Hutchison  01/26/98                        *
!                                                                      *
! Normally Run:		Monthly                                        *
!                                                                      *
! Control Parms:		FOR WHERE CLAUSE:                      *
!				  Company Division Code                *
!				  Company  	                       *
!				  Paygroup  	                       *
!				  Deptid	                       *
!				  Employee Status	               *
!				  Account Code                         *
!				  As Of Date                           *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE      *
!***********************************************************************
!	JDH		01/26/98	Initial Creation               *
!                                                                      *
!       AXL             05/09/2002      Alter evaluate stmts in        *
!                                       Accumulate_Years procedure to  *
!                                       limit annivesaries added to    *
!                                       added to categories.           *
! ISDVNPK   08/17/2007 Upgrade 8.3 - 9.0 Added PS_PER_ORG_INST for ORIG_HIRE_DT
! GEX-MXT   04/02/2008 ADDED THE join condition for PER_ORG_INST       *
!***********************************************************************


#include 'setenv.sqc'        !Set environment

#include 'setup01.sqc'       !Printer and page-size initialization


begin-report
#debug9 Show 'begin-report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Init-Report
  do P100-Process-Main
  do Reset                   !reset.sqc

  do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
end-report


begin-heading 5
#debug9 Show 'begin-heading'

  #include 'stdhdg01.sqc'

  Let $X000_temp_out = 'Sorted by ' || $X000_ORDER_BY
  Print $X000_temp_out         (3,)  center
  Let $X000_temp_out = 'Report as of ' || $X000_REPORT_HEADING_ASOFDATE
  Print $X000_temp_out         (4,)  center
end-heading


begin-procedure Init-Report
#debug9 Show 'Init-Report'

  Let $ReportID    = 'GEXHR002'
  Let $ReportTitle = 'EMPLOYEES WITH FIVE TO FIFTY YRS SERVICE'
  do Stdapi-Init

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate='  $ReportDate
  Show '$ReportTime='  $ReportTime

  if $prcs_process_instance = ''
    let $GEXXX907_INCLUDE_DIV_CD_CRITERIA           = '1=1'           
    let $GEXXX900_INCLUDE_COMPANY_CRITERIA          = '1=1'
    let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1' 
    let $GEXXX902_INCLUDE_DEPTID_CRITERIA	          = '1=1'
    let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA         = '1=1'
    let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA          = '1=1'
    let $X000_WHERE_SELECT_ASOFDATE                 = $AsOfToday
  else
    Do GEXRCHR1-SELECT-PARAMETERS

    Let $GEXXX900_Company_Alias  = 'J.COMPANY'
    Do GEXXX900-Select-Company-Parameters

    Let $GEXXX901_Company_Alias  = 'J.COMPANY'
    Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
    Do GEXXX901-Select-Company-Paygroup-Parameters

    Let $GEXXX902_Deptid_Alias = 'J.DEPTID'
    Do GEXXX902-Select-Deptid-Parameters

    Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'
    Do GEXXX903-Select-Emp-Stat-Parameters

    Let $GEXXX907_Div_CD_Alias = 'GDT.GEX_COMPANY_DIV_CD'                    
    Do GEXXX907-Select-Div-CD-Parameters 
          
    Let $GEXXX909_Acct_CD_Alias = 'J.ACCT_CD'
    Do GEXXX909-Select-Acct-CD-Parameters

    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_HR.AsOfDate
    If Rtrim ($GEX_RC_HR.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  End-If

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Let $X000_ORDER_BY = 'Company'

  Show '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA
  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA
  Show '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  Do P050-Print-Cover-Page
end-procedure


begin-procedure P050-Print-Cover-Page
#debug9 Show 'P050-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+2,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                 (0,+2)
  Print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	            (0,+2)
  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


begin-procedure P100-Process-Main
#debug9 Show 'P100-Process-Main'

  Let $Data-Found-Flag = 'N'
  do P200-Initialize-Counters

begin-SELECT
J.COMPANY               () on-break
                           print=never
                           after=P400-Company-Change
                           save=$Previous_Company

NVL(E.CMPNY_SENIORITY_DT,INST.ORIG_HIRE_DT) &ServiceDate  ! PD.ORIG_HIRE_DT  !ISDVNPK 8/17/2007 Modified for Upgrade 9.0 

  Let $Data-Found-Flag = 'Y'
  do Diff-Date($X000_WHERE_SELECT_ASOFDATE,&ServiceDate,#Years,#Months,#Days)
  do P300-Accumulate_Years

FROM  ps_gex_dept_tbl   gdt,
      PS_PERSONAL_DATA  PD,
      PS_EMPLOYMENT     E,
      PS_JOB            J,
      PS_PER_ORG_INST INST  !ISDVNPK 8/17/2007 Modified for Upgrade 9.0 

WHERE PD.EMPLID         = J.EMPLID
AND   E.EMPLID          = J.EMPLID
AND   E.EMPL_RCD       = J.EMPL_RCD
AND   INST.EMPLID      = PD.EMPLID  !ISDVNPK 8/17/2007 Modified for Upgrade 9.0
AND   INST.ORG_INSTANCE_ERN  = J.EMPL_RCD	!GEX-MXT added for PER_ORG_INST join
AND   J.EFFDT           =
     (SELECT MAX(EFFDT)
      FROM   PS_JOB
      WHERE  EMPLID     = J.EMPLID
      AND    EMPL_RCD  = J.EMPL_RCD
      AND    EFFDT     <= $X000_WHERE_SELECT_ASOFDATE)
AND   J.EFFSEQ          =
     (SELECT MAX(EFFSEQ)
      FROM   PS_JOB
      WHERE  EMPLID     = J.EMPLID
      AND    EMPL_RCD  = J.EMPL_RCD
      AND    EFFDT      = J.EFFDT)
AND [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
AND [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
AND [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
And    j.deptid                  = gdt.deptid
And    gdt.effdt                 =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid             = gdt.deptid
       And    effdt             <= $X000_WHERE_SELECT_ASOFDATE)
And    gdt.sequence_number       =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid             = gdt.deptid
       And    effdt              = gdt.effdt)
ORDER BY J.COMPANY
End-Select
  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
end-procedure


begin-procedure P200-Initialize-Counters
#debug9 Show 'P200-Initialize-Counters'

  Let #total_counter = 0
  Let #50yrs_counter = 0
  Let #45yrs_counter = 0
  Let #40yrs_counter = 0
  Let #35yrs_counter = 0
  Let #30yrs_counter = 0
  Let #25yrs_counter = 0
  Let #20yrs_counter = 0
  Let #15yrs_counter = 0
  Let #10yrs_counter = 0
  Let #5yrs_counter  = 0
end-procedure


begin-procedure P300-Accumulate_Years
#debug9 Show 'P300-Accumulate_Years'

  EVALUATE #Years
    when = 50			!AXL 5/9/2002 Used to say >=
      add 1 to #50yrs_counter
      add 1 to #total_counter
      break
    when = 45			!AXL 5/9/2002 Used to say >=
      add 1 to #45yrs_counter
      add 1 to #total_counter
      break
    when = 40			!AXL 5/9/2002 Used to say >=
      add 1 to #40yrs_counter
      add 1 to #total_counter
      break
    when = 35			!AXL 5/9/2002 Used to say >=
      add 1 to #35yrs_counter
      add 1 to #total_counter
      break
    when = 30			!AXL 5/9/2002 Used to say >=
      add 1 to #30yrs_counter
      add 1 to #total_counter
      break
    when = 25			!AXL 5/9/2002 Used to say >=
      add 1 to #25yrs_counter
      add 1 to #total_counter
      break
    when = 20			!AXL 5/9/2002 Used to say >=
      add 1 to #20yrs_counter
      add 1 to #total_counter
      break
    when = 15			!AXL 5/9/2002 Used to say >=
      add 1 to #15yrs_counter
      add 1 to #total_counter
      break
    when = 10			!AXL 5/9/2002 Used to say >=
      add 1 to #10yrs_counter
      add 1 to #total_counter
      break
    when = 5			!AXL 5/9/2002 Used to say >=
      add 1 to #5yrs_counter
      add 1 to #total_counter
      break
  END-EVALUATE
end-procedure


begin-procedure P400-Company-Change
#debug9 Show 'P400-Company-Change'

  print 'Company:'   (,1)
  print $Previous_Company (,+2)
  print 'Fifty years service:'  (,25)
  print #50yrs_counter (,67) edit 999,999
  print 'Forty-five years service:'  (+1,25)
  print #45yrs_counter (,67) edit 999,999
  print 'Forty years service:'  (+1,25)
  print #40yrs_counter (,67) edit 999,999
  print 'Thirty-five years service:'  (+1,25)
  print #35yrs_counter (,67) edit 999,999
  print 'Thirty years service:'  (+1,25)
  print #30yrs_counter (,67) edit 999,999
  print 'Twenty-five years service:'  (+1,25)
  print #25yrs_counter (,67) edit 999,999
  print 'Twenty years service:'  (+1,25)
  print #20yrs_counter (,67) edit 999,999
  print 'Fifteen years service:'  (+1,25)
  print #15yrs_counter (,67) edit 999,999
  print 'Ten years service:'  (+1,25)
  print #10yrs_counter (,67) edit 999,999
  print 'Five years service:'  (+1,25)
  print #5yrs_counter (,67) edit 999,999
  print 'Total Employees in Company'  (+2,25)
  print $Previous_Company (,+1)
  print ':' ()
  print #total_counter (,67) edit 999,999
  new-page
  do P200-Initialize-Counters
end-procedure


!#include 'GEXXX900.sqc'  !Get company multiple row table
!#include 'GEXXX901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table

#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx904.sqc'  !Get grade multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx908.sqc'  !Get sal_admin_plan multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx921.sqc'  !Get hr single row run control

#include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'reset.sqc'     !Reset printer procedure
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'stdapi.sqc'    !Routines to Update Run Status
