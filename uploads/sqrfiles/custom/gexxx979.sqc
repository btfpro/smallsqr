!**************************************************************************
!  GEXXX979:  Retrieve "Multiple" BAS ACTION SOURCE Run Controls and      *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX979_BAS_ACTION_SOURCE_Alias                                    *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA                         *
!    $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA                         *
!    $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_DISPLAY_CRITERIA                 *
!    $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_DISPLAY_CRITERIA                 *
!                                                                         *                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX979_BAS_ACTION_SOURCE_Alias = 'HB.BAS_ACTION_SOURCE'       *
!    Do GEXXX979-Select-bas-action-source-Parameters                      *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM ps_bas_activity HB                                              *
!    WHERE [$GEXXX979_EXCLUDE_COVRG_CD_CRITERIA]                          *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	ISDVNPK		12/16/2010 	INITIAL CREATION          *
!**************************************************************************

Begin-Procedure GEXXX979-Select-bas-action-source-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_BAS_ACTION_SOURCE_ROWS = 0
  Let $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA = ''
  Let $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA = ''
  Let $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_BAS_ACTION_SOURCE.BAS_ACTION_SOURCE

  Add 1 To #GEX_RC_BAS_ACTION_SOURCE_ROWS

  If #GEX_RC_BAS_ACTION_SOURCE_ROWS = 1
    Let $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA = '(' || $GEXXX979_BAS_ACTION_SOURCE_Alias || ' In ('''     || &GEX_RC_BAS_ACTION_SOURCE.BAS_ACTION_SOURCE || ''''
    Let $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA = '(' || $GEXXX979_BAS_ACTION_SOURCE_Alias || ' Not In (''' || &GEX_RC_BAS_ACTION_SOURCE.BAS_ACTION_SOURCE || ''''
  Else
    Let $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA = $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA || ',''' || &GEX_RC_BAS_ACTION_SOURCE.BAS_ACTION_SOURCE || ''''
    Let $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA = $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA || ',''' || &GEX_RC_BAS_ACTION_SOURCE.BAS_ACTION_SOURCE || ''''
  End-If

From  PS_GEX_RC_ACTN_SRC GEX_RC_BAS_ACTION_SOURCE

Where GEX_RC_BAS_ACTION_SOURCE.OPRID                    = [$OprID]
And   GEX_RC_BAS_ACTION_SOURCE.RUN_CNTL_ID              = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_BAS_ACTION_SOURCE.BAS_ACTION_SOURCE, ' ') != ' '

End-Select

  If #GEX_RC_BAS_ACTION_SOURCE_ROWS = 0
    Let $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA = $GEXXX979_BAS_ACTION_SOURCE_Alias || ' = ' || $GEXXX979_BAS_ACTION_SOURCE_Alias
    Let $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA = $GEXXX979_BAS_ACTION_SOURCE_Alias || ' = ' || $GEXXX979_BAS_ACTION_SOURCE_Alias
  Else
    Let $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA = $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA || '))'
    Let $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA = $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA || '))'
    Let $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_DISPLAY_CRITERIA = $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA
    Let $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_DISPLAY_CRITERIA = $GEXXX979_EXCLUDE_BAS_ACTION_SOURCE_CRITERIA
  End-If

End-Procedure