!**************************************************************************
!  GEXXX968:  Retrieve "Multiple" SAL_ADMIN_PLAN Run Controls and         *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX968_SAL_ADMIN_PLAN_Alias                                       *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX968_SAL_ADMIN_PLAN_CRITERIA                                    *
!    $GEXXX968_SAL_ADMIN_PLAN_DISPLAY_CRITERIA                            *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX968_SAL_ADMIN_PLAN_Alias = 'JB.SAL_ADMIN_PLAN'             *
!    Do GEXXX968-Select-SAL_ADMIN_PLAN-Parameters                         *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB JB                                                        *
!    WHERE [$GEXXX968_SAL_ADMIN_PLAN_CRITERIA]                    *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!  HR-10078	CWB 		04/18/2002 	INITIAL CREATION          *
!  HR-10313     GBD             08/13/2002      Added INCLUD_EXCLUDE_IND  *
!                                               to get-grade procedure.   *
!                               08/23/2002      Rewrote get-grade and     *
!                                               get-step procedures to    *
!                                               concatenate Grade & Step  *
!                                               values for processing.    *

!**************************************************************************


Begin-Procedure GEXXX968-Select-Sal-Admin-Plan-Parameters
       

  Let #PS_GEX_RC_SAL_SEL_ROWS = 0
  Let $GEXXX968_SAL_ADMIN_PLAN_CRITERIA = ''
  Let $GEXXX968_SAL_ADMIN_PLAN_DISPLAY_CRITERIA = 'ALL'
  Let $Grade_Alias = 'JB.GRADE'
  Let $Step_Alias  = 'JB.STEP'

Begin-Select

GEX_RC_SAL_SEL.SAL_ADMIN_PLAN

  Add 1 To #PS_GEX_RC_SAL_SEL_ROWS
  move 0 To #PS_GEX_RC_SAL_GRAD_ROWS
  Do get-grade  
!  Let $plan_condition = '(' || $GEXXX968_SAL_ADMIN_PLAN_Alias || ' = '''  || &GEX_RC_SAL_SEL.SAL_ADMIN_PLAN  
!                            || ''' AND ' || $grade_criteria || ')'
  Let $plan_condition =  $GEXXX968_SAL_ADMIN_PLAN_Alias || ' = '''  || &GEX_RC_SAL_SEL.SAL_ADMIN_PLAN  
                            || ''' AND (' || $grade_criteria || ')'

  If #PS_GEX_RC_SAL_SEL_ROWS = 1
    Let $GEXXX968_SAL_ADMIN_PLAN_CRITERIA = '(' ||  $plan_condition
  Else
    Let $GEXXX968_SAL_ADMIN_PLAN_CRITERIA = $GEXXX968_SAL_ADMIN_PLAN_CRITERIA || ' OR ' || $plan_condition
    Let $plan_condition = $plan_condition || ',''' || &GEX_RC_SAL_SEL.SAL_ADMIN_PLAN  || ''''
  End-If

From  PS_GEX_RC_SAL_SEL GEX_RC_SAL_SEL

Where GEX_RC_SAL_SEL.GEX_SEQ            = #gex_seq
And   Rtrim(GEX_RC_SAL_SEL.SAL_ADMIN_PLAN, ' ') != ' ' 
End-Select

  If #PS_GEX_RC_SAL_SEL_ROWS = 0
    Let $GEXXX968_SAL_ADMIN_PLAN_CRITERIA = '1=1'
  Else
    Let $GEXXX968_SAL_ADMIN_PLAN_CRITERIA = $GEXXX968_SAL_ADMIN_PLAN_CRITERIA || ')'
    Let $GEXXX968_SAL_ADMIN_PLAN_DISPLAY_CRITERIA = $GEXXX968_SAL_ADMIN_PLAN_CRITERIA

  End-If

End-Procedure

Begin-procedure get-grade
!  show 'Begin-procedure get-grade'

   Let $GRADE_CRITERIA = ''                      !GBD 08/13/2002 

Begin-Select

GEX_RC_SAL_GRAD.INCLUD_EXCLUDE_IND               !GBD 08/13/2002
GEX_RC_SAL_GRAD.GRADE
GEX_RC_SAL_GRAD.SAL_ADMIN_PLAN

  Add 1 To #PS_GEX_RC_SAL_GRAD_ROWS
  move 0 to #PS_GEX_RC_SAL_STEP_ROWS
  Do Get-Step


!GBD 08/13/2002 Begin
  If $STEP_CRITERIA = '1=1'
     If &GEX_RC_SAL_GRAD.INCLUD_EXCLUDE_IND = '-'
        Let $grade_condition =  $GRADE_Alias || ' != '''  || &GEX_RC_SAL_GRAD.GRADE 
                                   || ''' AND ' || $step_criteria 
     Else
        Let $grade_condition =  $GRADE_Alias || ' = '''  || &GEX_RC_SAL_GRAD.GRADE 
                                   || ''' AND ' || $step_criteria 
     End-if

     If #PS_GEX_RC_SAL_GRAD_ROWS = 1
        Let $GRADE_CRITERIA = '(' || $grade_condition 
     Else
        If &GEX_RC_SAL_GRAD.INCLUD_EXCLUDE_IND = '-'
           Let $GRADE_CRITERIA = $GRADE_CRITERIA || ') AND (' || $grade_condition 
        Else
           Let $GRADE_CRITERIA = $GRADE_CRITERIA || ') OR (' || $grade_condition 
        End-if
     End-if
  Else
     If #PS_GEX_RC_SAL_GRAD_ROWS = 1
        Let $GRADE_CRITERIA = '(' || $STEP_CRITERIA 
     Else
        If &GEX_RC_SAL_GRAD.INCLUD_EXCLUDE_IND = '-'
           Let $GRADE_CRITERIA = $GRADE_CRITERIA || ') AND (' || $STEP_CRITERIA 
        Else
           Let $GRADE_CRITERIA = $GRADE_CRITERIA || ') OR (' || $STEP_CRITERIA 
        End-if
     End-if

     Let $STEP_CRITERIA = ''
  End-if
!GBD 08/13/2002 End

From  PS_GEX_RC_SAL_GRAD GEX_RC_SAL_GRAD

Where GEX_RC_SAL_GRAD.GEX_SEQ            = #gex_seq
And   Rtrim(GEX_RC_SAL_GRAD.GRADE, ' ') != ' ' 
And   GEX_RC_SAL_GRAD.SAL_ADMIN_PLAN = &GEX_RC_SAL_SEL.SAL_ADMIN_PLAN
ORDER BY GEX_RC_SAL_GRAD.INCLUD_EXCLUDE_IND                            !GBD 08/13/2002
End-Select

  If #PS_GEX_RC_SAL_GRAD_ROWS = 0
    Let $GRADE_CRITERIA = '1=1'
!    Let $GRADE_CRITERIA = $GRADE_CRITERIA || ')'
  Else
    Let $GRADE_CRITERIA = $GRADE_CRITERIA || ')'
  End-If
    Let $GRADE_DISPLAY_CRITERIA = $GRADE_CRITERIA

End-Procedure

Begin-procedure get-step
!  show 'Begin-procedure get-step'

Begin-Select

GEX_RC_SAL_STEP.STEP
GEX_RC_SAL_STEP.GRADE
GEX_RC_SAL_STEP.SAL_ADMIN_PLAN

!  show 'GEX_RC_SAL_STEP.SAL_ADMIN_PLAN: ' &GEX_RC_SAL_STEP.SAL_ADMIN_PLAN
!  show 'GEX_RC_SAL_STEP.GRADE: ' &GEX_RC_SAL_STEP.GRADE
!  show 'GEX_RC_SAL_STEP.STEP: '  &GEX_RC_SAL_STEP.STEP
  move &GEX_RC_SAL_STEP.STEP to $GEX_RC_SAL_STEP.STEP

  Add 1 To #PS_GEX_RC_SAL_STEP_ROWS

  If #PS_GEX_RC_SAL_STEP_ROWS = 1
    Let $STEP_CRITERIA = $STEP_Alias || ' In (' || $GEX_RC_SAL_STEP.STEP 
  Else
    Let $STEP_CRITERIA = $STEP_CRITERIA || ',' || $GEX_RC_SAL_STEP.STEP 
  End-If

From  PS_GEX_RC_SAL_STEP GEX_RC_SAL_STEP

Where GEX_RC_SAL_STEP.GEX_SEQ             = #gex_seq
And   GEX_RC_SAL_STEP.SAL_ADMIN_PLAN = &GEX_RC_SAL_GRAD.SAL_ADMIN_PLAN
And   GEX_RC_SAL_STEP.GRADE = &GEX_RC_SAL_GRAD.GRADE
And   GEX_RC_SAL_STEP.STEP <> 0 

End-Select

  If #PS_GEX_RC_SAL_STEP_ROWS = 0
    Let $STEP_CRITERIA  = '1=1'
!    Let $STEP_CRITERIA = $STEP_CRITERIA || ')'
  Else
    Let $STEP_CRITERIA = $STEP_CRITERIA || ')'
  End-If
    Let $STEP_DISPLAY_CRITERIA = $STEP_CRITERIA

End-Procedure
