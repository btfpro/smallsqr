!***********************************************************************
! GEXPY127:           OCC TAX FLAG UPDATES IN PS_LOCAL_TAX_DATA        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY127:             OCC TAX FLAG INSERTS & REPORT FOR RETIREES     *
!                                                                      *
! Narrative:            This program updates rows in LOCAL_TAX_DATA    *
!                       to set SPECIAL_LWT_STATUS to 'E' for EMS tax   *                 
!                       for retirees who are not required to pay this  *
!                       tax, formerly called OCC TAX.                  *  
!                       This field was set to 'N' for all emps in      *
!                       GEXPY126.SQR. This SQR overrides it.           *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    * 
!                       #debug7 data returned from sql calls           * 
!                                                                      *
! SQL Tables:           PS_LOCAL_TAX_DATA                              * 
!                                                                      *
! Written by:           Carl Buckrop                                   *                                                           
!                                                                      *
! Normally run:         Year end.                                      *
!                                                                      *
! Entered Run                                                          *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS    DATE         DESCRIPTION OF THE CHANGE          *
!***********************************************************************
!            CWB       11/24/2006    Initial creation.                 *
!                                                                      *
! GEX_SEC_REENG Vahini Katta 09/18/2016                                *
! Changes related to SSN conversion                                    *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup02.sqc' 

#define #x000_col_selection_criteria_a  02
#define #x000_col_selection_criteria_b  05

#define #x000_col_emplid                06
#define #x000_col_company               16
#define #x000_col_state                 25
#define #x000_col_locality              32
#define #x000_col_status                43
#define #x000_col_emp_stat              57

!*********************************************************************
begin-report
!*********************************************************************
#debug9 show 'begin-report'

  do Initialization
  do select-employees
  do stdapi-term
  do EOJ

end-report

!***********************************************************************
begin-heading 09
!***********************************************************************

#debug9 show 'begin-heading'

 #Include 'stdhdg01.sqc'

  print $x000_header_1       (3,) center
  print $x000_header_2       (+1) center

  print '-' (+2,1,70) fill
  print 'Emplid'             (+1,{#x000_col_emplid})    bold 
  print 'Company'             (0,{#x000_col_company})   bold 
  print 'State'               (0,{#x000_col_state})     bold 
  print 'Locality'            (0,{#x000_col_locality})  bold 
  print 'LWT Status'          (0,{#x000_col_status})    bold 
  print 'Empl Status'         (0,{#x000_col_emp_stat})  bold 

  print '-' (+1,1,70) fill
 
end-heading  

!***********************************************************************
begin-procedure Initialization
!***********************************************************************
#debug9 show 'Initialization'

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
     show '$asoftoday = ' $asoftoday
     show ' '
  do stdapi-init
  
  
  let #empl-count = 0

  if $Prcs_process_instance = ''

    Do Ask-As-Of-Date
    Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate
  else
    Do GEXXX922-Select-Parameters
    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.ASOFDATE
  end-if

  if rtrim($X000_WHERE_SELECT_ASOFDATE, ' ' ) = ''
       let $X000_WHERE_SELECT_ASOFDATE = $asoftoday
       LET $AsOfDate = $X000_WHERE_SELECT_ASOFDATE 
  else
       LET $AsOfDate = $X000_WHERE_SELECT_ASOFDATE 
  end-if

  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan
  display ' '
  let $ReportID           = 'GEXPY127'
  let $ReportTitle        = 'EMS TAX STATUS UPDATES'                                     
  let $Country            = 'USA'

  show '$ReportID= '     $ReportID
  show '$ReportTitle = ' $ReportTitle   
    
  show '$X000_WHERE_SELECT_ASOFDATE        ='  $X000_WHERE_SELECT_ASOFDATE 

  do Print-Cover-Page
  new-page

end-procedure

!***********************************************************************
Begin-Procedure Print-Cover-Page
!***********************************************************************

   print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,{#x000_col_selection_criteria_a})
   print '$Prcs_OPRID          ='                              (+2,{#x000_col_selection_criteria_b})
   print $Prcs_OPRID                                           (0,+2)
   print '$PRCS_RUN_CNTL_ID    ='                              (+1,{#x000_col_selection_criteria_b})
   print $PRCS_RUN_CNTL_ID                                     (0,+2)

   print 'SELECTION CRITERIA FOR THIS REPORT RUN:'           (+5,{#x000_col_selection_criteria_a})
   print '$X000_WHERE_SELECT_ASOFDATE                     ='   (+1,{#x000_col_selection_criteria_b})
   print  $X000_WHERE_SELECT_ASOFDATE                          (0,+2)
 
 
   let #page-count=0 

end-procedure

!********************************************************************
begin-procedure Select-employees
!********************************************************************
#debug9 show 'Select-employees'

begin-select ON-ERROR=SQL-Error-Found('select-employees')
L.EMPLID                         
L.COMPANY   () on-break print=never level=1 after=after-company save=$prev_company                   
L.EFFDT                          
L.STATE                         
L.LOCALITY  () on-break print=never level=2 after=after-locality save=$prev_locality                   
L.RESIDENT                        
L.LWT_ALLOWANCES                   
L.LWT_AMT                   
L.LWT_PCT                   
L.LOCALITY_LINK                   
L.SPECIAL_LWT_STATUS                   
L.LWT_MAR_STATUS                   
J.empl_status
 
  add 1 to #empl_count

  do print-employee
  do update-local-tax

FROM   PS_LOCAL_TAX_DATA L, PS_LOCAL_TAX_TBL T, PS_JOB J
WHERE l.state = t.state 
AND l.locality = t.locality
AND t.tax_class = 'P' 		
AND l.effdt = (SELECT MAX(effdt) FROM PS_LOCAL_TAX_DATA
             WHERE emplid = l.emplid
        	 AND state = l.state
		 AND locality = l.locality
                 AND effdt <= $asofdate)
AND t.effdt = (SELECT MAX(effdt) FROM PS_LOCAL_TAX_TBL
             WHERE state = t.state
		 AND locality = t.locality
		 AND effdt <= $asofdate)
AND l.emplid = j.emplid			
!*************************************************************
AND j.empl_status IN ('Q','U','V')
!*************************************************************
AND j.effdt = (SELECT MAX(jj.effdt) FROM PS_JOB jj
             WHERE jj.emplid = j.emplid
		 AND jj.empl_rcd = j.empl_rcd
                 AND jj.effdt <= $asofdate)
AND j.effseq = (SELECT MAX(j2.effseq) FROM PS_JOB j2
             WHERE j2.emplid = j.emplid
		 AND j2.empl_rcd = j.empl_rcd
                 AND j2.effdt = j.effdt)

order by l.company,l.locality,l.emplid
end-select
end-procedure  !select-employees

!********************************************************************
begin-procedure update-local-tax
!********************************************************************
#debug9 show 'update-local-tax'

begin-sql
update ps_local_tax_data lu
  set lu.special_lwt_status = 'E'
where lu.emplid   = &l.emplid
  and lu.company  = &l.company
  and lu.state    = &l.state 
  and lu.locality = &l.locality 
  and lu.effdt    = &l.effdt
end-sql
#debug9 show 'end-sql update-local-tax, Txn nbr: '#empl_count  
end-procedure   !update-local-tax
          
!********************************************************************
begin-procedure after-company
  new-page
end-procedure
!********************************************************************

!********************************************************************
begin-procedure after-locality
  print ' ' (+1,1,80) fill
end-procedure
!********************************************************************

!********************************************************************
begin-procedure Print-Employee
!********************************************************************
#debug9 show 'print-employee'
!  show  &l.emplid ', ' &l.company ', ' &l.state ', ' &l.locality ', ' &l.effdt
  show ' '
  print &l.emplid             (+1,05)
  print &l.company             (0,18)
  print &l.state               (0,27)
  print &l.locality            (0,32)  
  print 'E'                    (0,49)
  print &j.empl_status         (0,61)
end-procedure
 
!********************************************************************
begin-procedure SQL-Error-Found($Proc_Name) 
!********************************************************************
  do error-found
  display 'SQL Error in ' 
  display $Proc_Name 
  display $_sql-error
  display 'sql-status:' 
  display #_sql-status 
  STOP
end-procedure

!********************************************************************
begin-procedure Error-Found
!********************************************************************
  move 'Y' to $ErrorFound
  do Format-Number(#InputTran, $out, '99999')
  print $out (+1,1)
  ! GEX_SEC_REENG Vahini Katta 09/18/2016  Begins
  !let $EmplId_1st3 = substr($EmplId,1,3)
  !let $EmplId_mid2 = substr($EmplId,4,2)
  !let $EmplId_last4 = substr($EmplId,6,4)
 
  !String $EmplId_1st3 $EmplID_mid2 $EmplId_last4 by '-' into $EmplId11

  !print $EmplID11 (,8)
  print $EmplId (,8)
  ! GEX_SEC_REENG Vahini Katta 09/18/2016 Ends   
end-procedure

!***********************************************************************
Begin-Procedure EOJ
!***********************************************************************
 
  display ' '
  display #empl_count 99,999 noline
  display ' Employees Processed, EMS tax rows updated ' 
  display ' '

  date-time () hh:mi:ss &timeProcess
  display 'Report ended: ' noline
  display &timeProcess

  Show 'Successful end of report'

end-procedure

#include 'tranctrl.sqc'  !Common Transaction Control Procedures
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions

#Include 'gexxx950.sqc'  !Get SQRW run controls
#Include 'gexxx922.sqc'  !Run Control parameters from GEX_RC_PAY record 

