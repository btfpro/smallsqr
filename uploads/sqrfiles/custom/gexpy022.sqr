!***********************************************************************
!  GEXPY022 :  PA QTD Occupational Tax Report                          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! PeopleSoft, Inc.; it is not to be copied, reproduced, or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of PeopleSoft.                        *
!                                                                      *
! Copyright (c) 1988-1997 PeopleSoft, Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
!      $Date:: 04/16/99         $                                      *
!  $Revision:: 1                $                                      *
!  $Workfile:: GEXPY022.SQR     $                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!  SQL Tables:      Pay_Tax                                            *
!                   Pay_Check                                          *
!                   Pay_Cal_Bal_Id                                     *
!                                                                      *
!  Normally Run:    Quarterly                                          *
!                                                                      *
!***********************************************************************
!  INITIALS   DATE        DESCRIPTION                                  *
!***********************************************************************
!  JDH        09/17/1999  Initial Creation.                            *
!  JNB        03/10/2000  Added new-page after locality                *
!  JNB        03/13/2000  &locality_name changed to $localname         *
!GEX-TLL      03/05/2008  HCM 9.0 Retrofitting 			       *
!	  		 (Add total empl count to each locality total) *
!GEX-TLL      03/05/2008  HCM 9.0 Retrofitting 			       *
!GEX-TLL      04/25/2008  HCM 9.0 Retrofitting 			       *
!***********************************************************************


#include 'setenv.sqc'
#Include 'setup32.sqc'


#define #column1d     1
#define #column2d    36
#define #column3d    53
#define #column4d    69
#define #column5d   106

#define #column1s     1
#define #column2s    36
#define #column3s    53


Begin-Setup
  Declare-Report Detail
Printer-Type=HPLASERJET
  End-Declare
  Declare-Report Summary
Printer-Type=HPLASERJET
  End-Declare
End-Setup


begin-heading 7 For-Reports=(Detail)
#debug9 Show 'Detail Report Heading'
  move 'PY022DET' to $ReportID
  #Include 'stdhdg02.sqc'
  let $header = 'Balance Quarter ' || $balance_qtr || ' Balance Year ' || $balance_year
  print $header                                (2,)    Center   
  print 'Locality ID/Name'                     (6,{#column1d})
  print 'Deptid'                               (0,{#column2d})
  print 'Emplid'                               (0,{#column3d})
  print 'Name'                                 (0,{#column4d})
  print '       QTD Tax'                       (0,{#column5d})

  print '-'                                    (7,{#column1d},28)  Fill
  print '-'                                    (0,{#column2d},10)  Fill
  print '-'                                    (0,{#column3d},9)   Fill
  print '-'                                    (0,{#column4d},30)  Fill
  print '-'                                    (0,{#column5d},14)  Fill
end-heading


begin-heading 7 For-Reports=(Summary)
#debug9 Show 'Summary Report Heading'
  move 'PY022SUM' to $ReportID
  #Include 'stdhdg02.sqc'
  let $header = 'Balance Quarter ' || $balance_qtr || ' Balance Year ' || $balance_year
  position (-1)
  print $header                                (2,)    Center   
  print 'Locality ID/Name'                     (6,{#column1s})
  print 'Deptid'                               (0,{#column2s})
  print '       QTD Tax'                       (0,{#column3s})

  print '-'                                    (7,{#column1s},28)  Fill
  print '-'                                    (0,{#column2s},10)  Fill
  print '-'                                    (0,{#column3s},14)  Fill
end-heading


begin-report
  let $ReportID    = 'GEXPY022'
  let $ReportTitle = 'PA QTD Occupational Tax Report'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show ' '

  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Get-Current-DateTime

  let $AsOfDate = $AsOfToday
  let $state    = 'PA'

  if $prcs_process_instance = ''
    Show ' '
    input $balance_year 'Balance Year'    Maxlen=4
    input $balance_qtr  'Balance Quarter' Maxlen=1
    let #balance_year = $balance_year
    let #balance_qtr  = $balance_qtr
    Input $GEX_SUMMARY_FLAG 'Report Type  (D)etail or (S)ummary' Maxlen=1
    Uppercase $GEX_SUMMARY_FLAG
  else
    do GEXXX922-SELECT-PARAMETERS
    do GEXRCBN1-SELECT-PARAMETERS
    let #balance_year     = #GEX_RC_PAY.Balance_Year
    let #balance_qtr      = #GEX_RC_PAY.Balance_Qtr
    move #balance_year to $balance_year 9999
    move #balance_qtr  to $balance_qtr  9
    let $GEX_SUMMARY_FLAG = &GEX_RC_BEN.GEX_SUMMARY_FLAG
  end-if

  If #balance_year = 0 Or
     #balance_qtr  = 0
    Do Get-Default-Quarter
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  let $X000_ORDER_BY = 'Company, Locality, Deptid, Emplid' 

  if $GEX_SUMMARY_FLAG <> 'S'
    use-report detail
    do Print-Cover-Page
  end-if

  if $GEX_SUMMARY_FLAG <> 'D'
    use-report summary
    do Print-Cover-Page
  end-if

  do Main-Process

  Do Reset
  do Stdapi-Term

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
end-report


begin-procedure Get-Default-Quarter
begin-select loops=1
balance_year
balance_period

  let #balance_year   = &balance_year
  let #balance_period = &balance_period

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  evaluate #balance_qtr
    when = 1
      subtract 1 from #balance_year
      let #balance_qtr = 4
      break
    when-other
      subtract 1 from #balance_qtr
  end-evaluate

  move #balance_year to $balance_year 9999
  move #balance_qtr  to $balance_qtr  9

from   ps_balance_id_def
where  balance_id = 'CY'
and    sysdate between begin_dt and end_dt
end-select
end-procedure



begin-procedure Print-Cover-Page
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  print 'Balance Year:'                                           (+2,5)
  print $Balance_Year                                             (0,+2)
  print 'Balance Quarter:'                                        (+1,5)
  print $Balance_Qtr                                              (0,+2)

  print 'Report Type:'                                            (+2,5)
  print $GEX_SUMMARY_FLAG                                         (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,2)
  print 'Order By:'                                               (+2,5)
  print $X000_ORDER_BY                                            (0,+2)

  let #page-count = 0
  new-page
  let #page-count = 1
end-procedure
!GEX-TLL, 04/25/2008, Procedure to pull employee name
Begin-Procedure Get-Employee-Name
Begin-Select
name

  Let $name = &name

FROM PS_PERSONAL_DATA
WHERE EMPLID = $emplid
End-Select
End-Procedure
!GEX-TLL, 04/25/2008 - End

begin-procedure Main-Process
#debug9 Show 'Main-Process'
begin-SELECT
PT.COMPANY   () on-break level=1 print=never
                                 after=after-company

PT.LOCALITY  () on-break level=2 print=never
                                 before=before-locality
                                 after=after-locality

PC.DEPTID    () on-break level=3 print=never
                                 after=after-deptid

PC.EMPLID

!PC.NAME	!GEX-TLL, 04/25/2008 Commented

SUM(PT.TAX_CUR) &PT.TAX_CUR

  let $data-found-flag = 'Y'

  let $company  = &PT.COMPANY
  let $local    = &PT.LOCALITY
  let $deptid   = &PC.DEPTID
  let $emplid   = &PC.EMPLID
  !let $name     = &PC.NAME	!GEX-TLL, 04/25/2008 Commented
  DO Get-Employee-Name		!GEX-TLL, 04/25/2008 Added

  add &PT.TAX_CUR to #dept-total
  add 1 To           #emplid-total    !GEX-TLL 03/05/08  Retrofitting

  if $GEX_SUMMARY_FLAG <> 'S'
    use-report detail
    if $local <> $hold-local
      let $hold-local = $local
      print $local                               (+1,{#column1d})
      print '/'                                  ()
      print $Localname                           ()!JNB 03/13/00 &locality_name () 7.5 changes
    else
      print ' '                                  (+1,{#column1d})
    end-if

    if $deptid <> $hold-deptid
      let $hold-deptid = $deptid
      print $deptid                              (0,{#column2d})
    end-if

    print &PC.EMPLID                             (0,{#column3d})
   !print &PC.NAME                               (0,{#column4d},30)	!GEX-TLL, 04/25/2008 Added
    print $NAME					 (0,{#column4d},30)	!GEX-TLL, 04/25/2008 Commented
    print &PT.TAX_CUR                            (0,{#column5d})  Edit 999,999,999.99
  end-if

FROM  PS_PAY_CHECK      PC,
      PS_PAY_TAX        PT,
      PS_PAY_CAL_BAL_ID PCBI

WHERE PC.COMPANY        = PT.COMPANY
  AND PC.PAYGROUP       = PT.PAYGROUP
  AND PC.PAY_END_DT     = PT.PAY_END_DT
  AND PC.OFF_CYCLE      = PT.OFF_CYCLE
  AND PC.PAGE_NUM          = PT.PAGE_NUM
  AND PC.LINE_NUM          = PT.LINE_NUM
  AND PC.SEPCHK         = PT.SEPCHK

  AND PT.PAY_END_DT     = PCBI.PAY_END_DT
  AND PT.STATE          = 'PA'
  AND PT.LOCALITY      <> ' '
  AND PT.TAX_CLASS      = 'P'
  AND PT.TAX_CUR       <> 0

  AND PCBI.COMPANY      = PT.COMPANY
  AND PCBI.PAYGROUP     = PT.PAYGROUP
  AND PCBI.PAY_END_DT   = PT.PAY_END_DT
  AND PCBI.BALANCE_ID   = 'CY'
  AND PCBI.BALANCE_YEAR = #balance_year
  AND PCBI.BALANCE_QTR  = #balance_qtr

!GROUP BY PT.COMPANY, PT.LOCALITY, PC.DEPTID, PC.EMPLID, PC.NAME	!GEX-TLL, 04/25/2008, Should not GROUP BY Name
GROUP BY PT.COMPANY, PT.LOCALITY, PC.DEPTID, PC.EMPLID			!GEX-TLL, 04/25/2008, Should not GROUP BY Name
ORDER BY PT.COMPANY, PT.LOCALITY, PC.DEPTID, PC.EMPLID
end-SELECT

  if $data-found-flag = 'Y'
    if $GEX_SUMMARY_FLAG <> 'S'
      use-report detail
      print 'Grand Total'                          (+1,{#column1d})
      print #grand-total                           (0,{#column5d})  Edit 999,999,999.99
      print #empl-grand-total                      (0,{#column3d})  !Edit 999,999,999.99  !GEX-TLL  03/05/08 Retrofitting 
      
    end-if

    if $GEX_SUMMARY_FLAG <> 'D'
      use-report summary
      print 'Grand Total'                          (+2,{#column1s})
      print #grand-total                           (0,{#column3s})  Edit 999,999,999.99
    end-if
  else
    if $GEX_SUMMARY_FLAG <> 'S'
      use-report detail
      print 'No data selected'    (+5,)  center
    end-if
    if $GEX_SUMMARY_FLAG <> 'D'
      use-report summary
      print 'No data selected'    (+5,)  center
    end-if
  end-if
end-procedure


begin-procedure After-Company
#debug9 Show 'After-Company'
  if $GEX_SUMMARY_FLAG <> 'S'
    use-report detail
    print 'Company'                              (+1,{#column1d})
    print $company                               (0,+1)
    print 'Total'                                (0,+1)
    print #company-total                         (0,{#column5d})  Edit 999,999,999.99
    print #empl-company-total                    (0,{#column3d})  !GEX-TLL  03/05/08 Retrofitting
  end-if

  if $GEX_SUMMARY_FLAG <> 'D'
    use-report summary
    print 'Company'                              (+2,{#column1s})
    print $company                               (0,+1)
    print 'Total'                                (0,+1)
    print #company-total                         (0,{#column3s})  Edit 999,999,999.99
  end-if

  add #company-total to #grand-total
  move 0             to #company-total
  
  add #empl-company-total to #empl-grand-total   !GEX-TLL 03/05/08 Retrofitting                 
  move 0                  to #empl-company-total !GEX-TLL 03/05/08 Retrofitting        
  

  if $GEX_SUMMARY_FLAG <> 'S'
    use-report detail
    new-page
  end-if

  if $GEX_SUMMARY_FLAG <> 'D'
    use-report summary
    new-page
  end-if
end-procedure


begin-procedure Before-Locality
#debug9 Show 'Before-Locality'
  move &PT.Locality to $Locality
  do Get-Local-Tax-Data
end-procedure


begin-procedure after-locality
#debug9 Show 'after-locality'
  if $GEX_SUMMARY_FLAG <> 'S'
    use-report detail
    print 'Locality'                             (+1,{#column1d})
    print $local                                 (0,+1)
    print 'Total'                                (0,+1)
    print #locality-total                        (0,{#column5d})  Edit 999,999,999.99
    print #full-total                            (0,{#column3d})  !GEX-TLL  04/03/08 Retrofitting
    Print ' '                                    (+1,{#column1d})
    !print #full-total                            (0,{#column3d})  !GEX-TLL  03/05/08 Retrofitting
    new-page        !jnb 03/10/1999
  end-if

!  if $GEX_SUMMARY_FLAG <> 'D'
!    use-report summary
!    print 'Locality'                             (+2,{#column1s})
!    print $local                                 (0,+1)
!    print 'Total'                                (0,+1)
!    print #locality-total                        (0,{#column3s})  Edit 999,999,999.99
!    Print ' '                                    (+1,{#column1s})
!  end-if

  add #locality-total to #company-total
  move 0              to #locality-total
  add #full-total to #empl-company-total         !GEX-TLL  03/05/08 Retrofitting
  move 0              to #full-total             !GEX-TLL  03/05/08 Retrofitting
end-procedure


begin-procedure after-deptid
#debug9 Show 'after-deptid'
  if $GEX_SUMMARY_FLAG <> 'D'
    use-report summary
    if $local <> $hold2-local
      let $hold2-local = $local
      print $local                               (+1,{#column1s})
      print '/'                                  ()
      print $Localname                           ()!JNB 03/13/00 &locality_name () 7.5 changes
    else
      print ' '                                  (+1,{#column1s})
    end-if
    print $deptid                                (0,{#column2s})
    print #dept-total                            (0,{#column3s})  Edit 999,999,999.99
  end-if

  if $GEX_SUMMARY_FLAG <> 'S'
    use-report detail
    print 'Deptid'                               (+2,{#column1d})
    print $deptid                                (0,+1)
    print 'Total'                                (0,+1)
    print #emplid-total                          (0,{#column3d})  !GEX-TLL 03/05/08 Retrofitting
    print #dept-total                            (0,{#column5d})  Edit 999,999,999.99
    print ' '                                    (+1,{#column1d})
    let $hold-local = ''
  end-if

  add #dept-total to #locality-total
  move 0          to #dept-total
  add #emplid-total to #full-total                 !GEX-TLL  03/05/08 Retrofitting
  move 0            to #emplid-total               !GEX-TLL  03/05/08 Retrofitting
end-procedure


#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getlcdta.sqc'  !Get-Local-Tax-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Date-Math Functions
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'gexxx920.sqc'  !Get GEX_RC_BEN run control parameters
#Include 'gexxx922.sqc'  !Get GEX_RC_PAY run control parameters
