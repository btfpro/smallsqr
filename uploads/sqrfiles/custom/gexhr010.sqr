!***********************************************************************
!  GEXHR010:  Employees Min/Mid/Max Salary Listing                     *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXHR010:             Employees Min/Mid/Max Salary Listing           *
!                                                                      *        
! Narrative:		This program will produce a listing of each    *
!                       employee's min, mid, max salaries along with   *
!                       their plan, grade, jobcode and jobfamily in    *
!                       each department in the selected company.       *
!                                                                      *  
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values                    *
!			      #debug7 data returned from sql calls           * 
!                                                                      *  
! SQL Tables:		gex_dept_tbl                                   *
!                       jobcode_tbl                                    *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:		Jamie Baer  03/05/98                           *       
!                                                                      *
! Normally Run:		On Demand                                      *
!                                                                      *
! Control Parms:		FOR WHERE CLAUSE:                              *
!				  Company Division Code                        *
!				  Company  	                                   *
!				  Paygroup  	                             *
!				  Deptid	                                   *
!				  Employee Status	                             *
!				  Account Code                                 *
!				  As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE                *
!***********************************************************************
!	JNB		03/05/98	Initial Creation                         *
!	JDH		06/10/98	Added annual_rt and hourly_rt            *
!                                                                      *
! ISDVNPK 08/15/2007 Added PS_PER_ORG_ASGN as part of 9.0 upgrade
!	ISDVMXD         02/13/2009	ITG 56876 - jobtitle changes   *  
!***********************************************************************


!***********************************************************************
!
!  1.	I modeled this report after the PSoft delivered CMP003
!
!  2. I changed Mid_Rt_Annual(#MROut) Min_Rt_Annual (#MinOut), 
!	Max_Rt_Annual (#MaxOut) From 15 to 12 places to fit report on page.
!
!  3.	#Include 'setup02.sqc' (per guideline) was not used since modeled after 
!	PSoft report
!
!  4. Added Procedure GEXXX900-select-company-parameters from 
!	SQC GEXXX900 for multiple companies as input parms.  
!	
!  5.	Added Procedure GEXXX903-select-emp-stat-parameters
!	from SQC GEXXX903 for  multiple empl status as input parms
!		                                                                   
!****************************************************************************
!  Questions/Comments
!
!  1.	I need to check the Company Id that is entered to make sure it's valid 
!	This is not needed since this is used for dev only & all users will
!	use a run control panel ---CLOSED----
!	
!  2.	There is a field JOB family on the jobcode table.  the spec refers to it in the 
!	description, but it is not in the mockup.  Is it needed?
!	Per conversation with P. Palmer 03/10/98 - No - exclude for now.---CLOSED---
!
!  3.	The spec refers to employee type.  Not explained, or mocked up - is it needed?
!	Per conversation with P. Palmer 03/10/98 - No - exclude for now.---CLOSED---
!	
!  4.	The functionality to specify the Company for which the report will be run 								     
!	was added to this report.  Might there be a need to run this for multiple 
!	companies at the same time?
!	Per Patty Yes - would like to integrate proposed 'common run control' 
!	into this*** See Panel GEX_RC_GEXHR010
!
!  5.	The spec includes a finite list of employee status' for inclusion.  
!	Could that change in the future?
!	Per Patty - yes - same as above #4***See Panel GEX_RC_GEXHR010
!
!  6.	Sort Order Questioned - by jobcode & name - dept & company are informaitonal -
!	check with End User      
!	Per Patty this is the way they'd like to see this report.  With the addition of 
!	more than one Company, the sort order will include Company ---CLOSED--
!
!  7. Page break is by deptid - but sort is not done this way. Sorting by Company then 
!	Jobcode.	Add break on Jobcode. Add break on Company
!	***Added page break on Company and 2 line break on jobcode break --CLOSED--
!
!  8.	SQR Guidelines were not followed 100% since this is a modification of a 
!	delivered report.--CLOSED---
!
!  9.  Need to move the Company to the heading. -- CLOSED --					!                     
!
!  10. Developed may need to comment code etc. to run from SQR exe Icon  
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


begin-report
#debug9 show 'begin-report'

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do P100-Process-Main
  do Reset
  do Stdapi-Term
  Show 'Successful end of report'
end-report


begin-heading 7
#debug9 show 'begin-heading'

!  do Get_Field_Information ('CMP003',   'REPORT_TITLE', $REPORTTITLE,   #DW)
  #Include 'stdhdg02.sqc'

  Let $X000_temp_out = 'Sorted by ' || $X000_ORDER_BY
  Print $X000_temp_out         (3,)  center
  Let $X000_temp_out = 'Report as of ' || $X000_REPORT_HEADING_ASOFDATE
  Print $X000_temp_out         (4,)  center

  do Get_Field_Information ('CMP003',   'MIDPOINT',     $MID,           #DW)
!  do Get_Field_Information ('CMP003',   'COMP_RATE1',   $RATIO1,        #DW)
!  do Get_Field_Information ('CMP003',   'COMP_RATE2',   $RATIO2,        #DW)
!  do Get_Field_Information ('HR',       'ANNUAL_RT',    $ANNUAL,        #DW)
!  do Get_Field_Information ('HR',       'GRADE1',       $GRADE1,        #DW)
!  do Get_Field_Information ('HR',       'GRADE2',       $GRADE2,        #DW)
  do Get_Field_Information ('HR',       'JOBCODE',      $JOB,           #DW)
  do Get_Field_Information ('HR',       'JOBTITLE',     $JOBTITLE,      #DW)
  do Get_Field_Information ('HR',       'EMPL_NAME',    $NAME,          #DW)
  do Get_Field_Information ('HR',       'DEPTID',       $DEPTID,        #DW)
  do Get_Field_Information ('HR',       'CONV_TO_BASE', $CONVBASE,      #DW)
  
!  if #MultiCurr
!    print $CONVBASE   (3,65)
!    print $ReportCurr (0,108)
!    print '*'         (7,9)
!    print '*'         (7,126)
!  end-if

  print 'Company:'						  (3,1)
  print $Companyid						  (3,10)
  print $REPORTTITLE                                    (2) CENTER
  print 'Annual'                                        (6,107)
  print 'Hourly'						        (0,120)
  print 'Sal Plan/'                                     (0,131)
  print 'Minimum '                                      (0,144)
  print 'Midpoint'                                      (0,155)
  print 'Maximum'                                       (0,168)
  print $DEPTID                                         (7,1)
  print 'Empl Id'         					  (0,13)
  print $NAME                                           (0,25)
  print $JOB                                            (0,60)
  print $JOBTITLE                                       (0,70)
  print 'Salary'                                        (0,107)
  print '  Rate'						        (0,120)
  print 'Grade'                                         (0,131)
  print ' Salary'                                       (0,144)
  print '  Salary'                          	        (0,155)
  print ' Salary'                                       (0,168)	
end-heading


begin-procedure Init-Report
#debug9 Show 'Init-Report'
  
  MOVE 'Employees Min/Mid/Max Salary Listing' to $REPORTTITLE
  move 'GEXHR010' to $ReportID
  show $ReportID 
  move $BaseCurr to $ReportCurr
  show '$BaseCurr=' $BaseCurr
  show '$ReportCurr = ' $ReportCurr
  move $BaseRtType to $ReportRtType
  move 'W' to $ReportMsgType  !Warning

  do Stdapi-Init

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate=' $ReportDate
  Show '$ReportTime=' $ReportTime

  If $prcs_process_instance = ''
    let $GEXXX907_INCLUDE_DIV_CD_CRITERIA           = '1=1'           
    let $GEXXX900_INCLUDE_COMPANY_CRITERIA          = '1=1'
    let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1' 
    let $GEXXX902_INCLUDE_DEPTID_CRITERIA	          = '1=1'
    let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA         = '1=1'
    let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA          = '1=1'
    let $X000_WHERE_SELECT_ASOFDATE                 = $AsOfToday
    let $X000_ORDER_BY = ' '

  Else

    Do GEXRCHR1-SELECT-PARAMETERS

    Let $GEXXX900_Company_Alias  = 'C.COMPANY'
    Do GEXXX900-Select-Company-Parameters

    Let $GEXXX901_Company_Alias  = 'C.COMPANY'
    Let $GEXXX901_Paygroup_Alias = 'C.PAYGROUP'
    Do GEXXX901-Select-Company-Paygroup-Parameters

    Let $GEXXX902_Deptid_Alias = 'C.DEPTID'
    Do GEXXX902-Select-Deptid-Parameters

    Let $GEXXX903_Emp_Stat_Alias = 'C.EMPL_STATUS'
    Do GEXXX903-Select-Emp-Stat-Parameters

    Let $GEXXX907_Div_CD_Alias = 'GDT.GEX_COMPANY_DIV_CD'                    
    Do GEXXX907-Select-Div-CD-Parameters 
          
    Let $GEXXX909_Acct_CD_Alias = 'C.ACCT_CD'
    Do GEXXX909-Select-Acct-CD-Parameters

    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_HR.AsOfDate
    If Rtrim ($GEX_RC_HR.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  End-If

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Let $X000_ORDER_BY = 'Company, Jobcode, Name'

  Show '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA
  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA
  Show '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  do Init_Printer
  do Init_Report_Translation($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

  Do P040-Print-Cover-Page
end-procedure


begin-procedure Get-Values
   let $language_cd = $prcs_language_cd
end-procedure


begin-procedure P040-Print-Cover-Page
#debug9 Show 'P040-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+2,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                 (0,+2)
  Print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	            (0,+2)
  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


begin-procedure P100-Process-Main
#debug9 Show 'P100-Process-Main'

  let $x100_Get_Job_Data_Flag = 'N'

begin-SELECT		    
C.EFFDT        &PersEffdt
C.CURRENCY_CD  &PersCur
C.COMPANY	   ()	      ON-BREAK LEVEL=1
                                 PRINT=NEVER
                                 BEFORE=P200-BEFORE-COMPANY-CHANGE
                                 AFTER=P300-AFTER-COMPANY-CHANGE
C.DEPTID       
C.EMPLID	   
A.NAME         
C.JOBCODE   ()   		ON-BREAK LEVEL=2
                                 PRINT=NEVER
                                 AFTER=P400-AFTER-JOBCODE-CHANGE
D.DESCR        
C.SAL_ADMIN_PLAN   
C.GRADE        
C.ANNUAL_RT
C.HOURLY_RT
C.POSITION_NBR			!isdvmxd added for ITG 56876 on 02/13/2009
PDT.DESCR			!isdvmxd added for ITG 56876 on 02/13/2009

  PRINT &C.DEPTID       	(+1,1)
  PRINT &C.EMPLID	   		(0,13)
  PRINT &A.NAME         	(0,25,30)
  PRINT &C.JOBCODE      	(0,60)  
  !isdvmxd added for ITG 56876 on 02/13/2009  --Begin		 
  If isblank(&PDT.DESCR)		
  	PRINT &D.DESCR        	(0,70)
  Else
  	PRINT &PDT.DESCR         (0,70)
  End-IF
  !isdvmxd added for ITG 56876 on 02/13/2009  -- End
  PRINT &C.SAL_ADMIN_PLAN     (0,131)  EDIT XXX/
  PRINT &C.GRADE        	(0,135)

 #debug7 show 'Deptid = ' &c.deptid
  let $x100_Get_Job_Data_Flag = 'Y'

  if &C.Sal_Admin_Plan <> $SalAdminPlan or &C.Grade <> $Grade
   #DEBUG8 SHOW 'IN FIRST IF STMT' 
    move &C.Sal_Admin_Plan to $SalAdminPlan
   #DEBUG8 SHOW '&C.Sal_Admin_Plan= '&C.Sal_Admin_Plan 
   #DEBUG8 SHOW '$SalAdminPlan= '$SalAdminPlan
    move &C.Grade    to $Grade
    do Get-Salary-Grade-Data
    if #MultiCurr and &Sal_Grade_Cur <> $ReportCurr And
      Rtrim(&Sal_Grade_Cur, ' ') <> ''
      do ConvertCurrency(&C.Annual_Rt,&Sal_Grade_Cur,$ReportCurr,
      $ReportRtType,&PersEffDt,#AROut,$ReportMsgType)
   #DEBUG8 SHOW '#AROUT= '#AROut
      do ConvertCurrency(&C.Hourly_Rt,&Sal_Grade_Cur,$ReportCurr,
      $ReportRtType,&PersEffDt,#HROut,$ReportMsgType)
   #DEBUG8 SHOW '#HROUT= '#HROut
      do ConvertCurrency(&SG.Mid_Rt_Annual,&Sal_Grade_Cur,$ReportCurr,
      $ReportRtType,&PersEffDt,#MROut,$ReportMsgType)
   #DEBUG8 SHOW '#MROUT= '#MROut
	do ConvertCurrency(&SG.Min_Rt_Annual,&Sal_Grade_Cur,$ReportCurr,
    $ReportRtType,&PersEffDt,#MinOut,$ReportMsgType)  
   #DEBUG8 SHOW '#MINOUT= '#MinOut
	do ConvertCurrency(&SG.Max_Rt_Annual,&Sal_Grade_Cur,$ReportCurr,
    $ReportRtType,&PersEffDt,#MaxOut,$ReportMsgType)  
   #DEBUG8 SHOW '#MaxOUT= '#MaxOut 
 else
   #DEBUG8 SHOW 'FIRST ELSE STMT'
      move &C.Annual_Rt      to #AROut
      move &C.Hourly_Rt      to #HROut
      move &SG.Mid_Rt_Annual to #MROut
	move &SG.Min_Rt_Annual to #MinOut
	move &SG.Max_Rt_Annual to #MaxOut    
    end-if  
  end-if

  do Format-Number(#AROut, $arout, '999,999,999')
  print $arout   (0,102)
  do Format-Number(#HROut, $hrout, '999,999.99')
  print $hrout   (0,116)
  do Format-Number(#MinOut, $minout, '999,999,999')
  print $minout  (0,140)
  do Format-Number(#MROut, $out, '999,999,999')
  print $out     (0,152)
  do Format-Number(#MaxOut, $maxout, '999,999,999')
  print $maxout  (0,164)

FROM  ps_gex_dept_tbl gdt,
      PS_PERSONAL_DATA A,
      PS_EMPLOYMENT B,
      PS_JOB C,
      PS_JOBCODE_TBL D,
      PS_PER_ORG_ASGN PER,               ! ISDVNPK 08/15/2007 Added as part of 9.0 upgrade
      PS_POSITION_DATA PDT		 !isdvmxd added for ITG 56876 on 02/13/2009	

WHERE ! A.PER_STATUS = 'E' AND  ! ISDVNPK 08/15/2007 Added as part of 9.0 upgrade
  A.EMPLID = B.EMPLID
  AND C.EMPLID = B.EMPLID
  AND C.EMPL_RCD = B.EMPL_RCD
  ! ISDVNPK 08/15/2007 Added as part of 9.0 upgrade
  AND C.EMPLID = PER.EMPLID
  AND C.EMPL_RCD = PER.EMPL_RCD
  AND PER.PER_ORG = 'EMP'
  AND C.POSITION_NBR = PDT.POSITION_NBR			!isdvmxd added for ITG 56876 on 02/13/2009
  AND PDT.EFF_STATUS = 'A'				!isdvmxd added for ITG 56876 on 02/13/2009
  ! ISDVNPK 08/15/2007 Added as part of 9.0 upgrade
!  AND C.COMPANY IN ('BSN', 'F65')
  AND C.EFFDT =  
	  (SELECT MAX(H.EFFDT)
         FROM PS_JOB H
         WHERE H.EMPLID = C.EMPLID
         AND H.EMPL_RCD = C.EMPL_RCD
         AND H.EFFDT <= $X000_WHERE_SELECT_ASOFDATE)
  AND C.EFFSEQ =
      (SELECT MAX(I.EFFSEQ)
       FROM PS_JOB I
       WHERE I.EMPLID = C.EMPLID
       AND I.EMPL_RCD = C.EMPL_RCD
       AND I.EFFDT = C.EFFDT)
  AND D.JOBCODE = C.JOBCODE
  AND D.EFFDT =
      (SELECT MAX(J.EFFDT)
       FROM PS_JOBCODE_TBL J
       WHERE J.JOBCODE = D.JOBCODE
       AND J.EFFDT <= C.EFFDT)
  AND PDT.EFFDT = (SELECT MAX(EFFDT) FROM PS_POSITION_DATA P_ED		!ISDVMXD ADDED FOR ITG 56876 ON 02/13/2009
  			WHERE P_ED.POSITION_NBR = PDT.POSITION_NBR
  			AND P_ED.EFF_STATUS = 'A'
  			AND P_ED.EFFDT <= $X000_WHERE_SELECT_ASOFDATE) 
AND [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
AND [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
AND [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
And    c.deptid                  = gdt.deptid
And    gdt.effdt                 =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid             = gdt.deptid
       And    effdt             <= $X000_WHERE_SELECT_ASOFDATE)
And    gdt.sequence_number       =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid             = gdt.deptid
       And    effdt              = gdt.effdt)

ORDER BY  C.COMPANY ASC, C.JOBCODE ASC, A.NAME ASC   !**C.SAL_ADMIN_PLAN, C.GRADE DESC,
end-SELECT

  if $x100_Get_Job_Data_Flag = 'N'
	print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) CENTER
  end-if

end-procedure


Begin-Procedure P200-BEFORE-COMPANY-CHANGE
#debug9 Show 'P200-BEFORE-COMPANY-CHANGE'

 move &c.company to $companyid
End-Procedure P200-BEFORE-COMPANY-CHANGE


Begin-Procedure P300-AFTER-COMPANY-CHANGE
#debug9 Show 'P300-AFTER-COMPANY-CHANGE'

 new-page
End-Procedure P300-AFTER-COMPANY-CHANGE


Begin-Procedure P400-AFTER-JOBCODE-CHANGE
#debug9 Show 'P400-AFTER-JOBCODE-CHANGE'

 PRINT '  ' (+2,1)
End-Procedure P400-AFTER-JOBCODE-CHANGE


#Include 'getcodta.sqc'  !Get Company Data
#Include 'getslgrd.sqc'  !Get-Salary-Grade-Data procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to update run status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'

!#include 'GEXXX900.sqc'  !Get company multiple row table
!#include 'GEXXX901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table

#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx904.sqc'  !Get grade multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx908.sqc'  !Get sal_admin_plan multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx921.sqc'  !Get hr single row run control
