!******************************************************************************
! GEXPY225: This program will load the Employer Benefit Contribution for      *
!           Independents to DEDUCTION Balance table.                          *
!******************************************************************************
!                                                                             *
!               Confidentiality Information:                                  *
!                                                                             *
! This module is the confidential and proprietary information of              *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted       *
! in any form, by any means, in whole or in part, nor is it to be used        *
! for any purpose other than that for which it is expressly provided          *
! without the written permission of Giant Eagle.                              *
!                                                                             *
!                                                                             *
!******************************************************************************
!******************************************************************************
!                         MODIFICATION HISTORY                                *
!******************************************************************************
!  ID#     INITIALS    DATE           DESCRIPTION OF THE CHANGE               *
!******************************************************************************
! ISDVMZA    MVA       11/30/2012     INITIAL CREATION                        *
!                                     Load Employer Benefit Contribution to   *
!                                     Deduction Balance table.                *
!                                     This program should be run prior to the *
!                                     W2 load process.                        *
!******************************************************************************

#include 'setenv.sqc' !Set environment

!*********************************************************************
!Initial processing, retrieving current dates and calls the main
!procedure to do the processing.
!*********************************************************************

begin-report

  do Init-DateTime
  do Init-Number
  do stdapi-init
  do Get-Current-DateTime
  do Report
  do stdapi-term

end-report

begin-procedure Report
#debug9  show 'Main-Process'
   display $AsOfNow

   do P100-Open-File
   do Get-Tax-Reporting-Run-Controls
   do P200-Process-File

   close 1

end-procedure Report

!**********************************************************************
! Open file for processing
!**********************************************************************
begin-procedure P100-Open-File
#debug9  show 'P100-Open-File'

#debug9 show 'Procedure = P100-Open-File'

   !let $folder = '/apps/hr/hrms90/hrtst90/incoming/'     !***  Mel Testing
   let $folder = '\\corp.gianteagle.com\common\HR\Payroll\Private\UPLOADS\'

   let $file = $folder || 'ercontrib.txt'

   OPEN $file as 1 for-reading record=80 vary status = #file_status

   if #file_status <> 0
      let $Open-Msg = 'Error Opening '||$file || ' status = ' || to_char(#file_status)
      display $Open-Msg
      stop quiet
   end-if

end-procedure P100-Open-File


!************************************************************************
!*  Read the input file for processing
!************************************************************************
begin-procedure P200-Process-File
#debug9  show 'P200-Process-File'

   move 'N' to $Found_cal
   move 0 to #end-file

   while 1
      read 1 into $record1:80
      if #end-file
         show 'End of file reached.'
         break   ! End of file reached.
      end-if

      unstring $record1 by ',' into $company
                                    $emplid
                                    $amt

      show 'Company: ' $company ' - Emplid: ' $emplid ' - ' $amt

      move 'INDERB'         to $dedcd
      do P300-Get-Dedcd

      if $rec-found = 'Y'



         move &DE.PLAN_TYPE   to $plan_type
         move &DC.DED_CLASS   to $ded_class

         move 999              to #benefit_rcd_nbr
         move ' '              to $benefit_plan
         move &TX.BALANCE_ID   to $balance_id
         move &TX.Balance_Year to #balance_year
         move &TX.Balance_Qtr  to #balance_qtr
         move 12               to #balance_period
         move $amt             to #ded_ytd
         move $amt             to #ded_qtd
         move $amt             to #ded_mtd

        do P700-Insert-Ded-Bal
      else
         show 'The Deduction Code is not yet set-up: ' $dedcd
      end-if

   end-while

end-procedure


!************************************************************************
Begin-Procedure P300-Get-Dedcd
#debug9  show 'P300-Get-Dedcd'

   move 'N' to $rec-found

begin-select on-error=P900-sql-error-found('P300-Get-Dedcd')
DE.PLAN_TYPE
DE.DEDCD
DC.DED_CLASS

   move 'Y' to $rec-found

from PS_DEDUCTION_TBL DE,
     PS_DEDUCTION_CLASS DC
 WHERE DE.PLAN_TYPE = DC.PLAN_TYPE
   AND DE.DEDCD = DC.DEDCD
   AND DE.EFFDT = DC.EFFDT
   AND DE.DEDCD = $dedcd
end-SELECT

end-procedure P300-Get-Dedcd

!************************************************************************
! Insert Deduction Balance
!************************************************************************
Begin-Procedure P700-Insert-Ded-Bal
#debug9  show 'P700-Insert-Ded-Bal'

begin-SQL on-error=P900-sql-error-found('P700-Insert-Ded-Bal')

INSERT INTO PS_DEDUCTION_BAL(EMPLID,
                             COMPANY,
                             BALANCE_ID,
                             BALANCE_YEAR,
                             BALANCE_QTR,
                             BALANCE_PERIOD,
                             BENEFIT_RCD_NBR,
                             PLAN_TYPE,
                             BENEFIT_PLAN,
                             DEDCD,
                             DED_CLASS,
                             DED_YTD,
                             DED_QTD,
                             DED_MTD)
                    VALUES  ($emplid,
                             $company,
                             $balance_id,
                             #balance_year,
                             #balance_qtr,
                             #balance_period,
                             #benefit_rcd_nbr,
                             $plan_type,
                             $benefit_plan,
                             $dedcd,
                             $ded_class,
                             #ded_ytd,
                             #ded_qtd,
                             #ded_mtd)
end-SQL

end-procedure P700-Insert-Ded-Bal


!********************************************************************
!This procedure displays in the error report if there are any sql erros
!happened in the program
!********************************************************************

begin-procedure P900-SQL-Error-Found($Proc_Name)

   display 'SQL Error in ' noline
   display $Proc_Name
   display $_sql-error
   display 'sql-status: '  noline
   display #_sql-status  99990
   show 'COMPANY   = ' $company
   show 'EMPLID    = ' $emplid
   show 'DEDCD     = ' $dedcd
   show 'AMOUNT    = ' #amt

end-procedure

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Date Arithmetic functions
#Include 'number.sqc'    !Routines to format numbers
#Include 'taxrnctl.sqc'  !Select-Parameters
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
