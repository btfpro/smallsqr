!**************************************************************************
!  GEXXX966:  Retrieve "Multiple" Jobcode Run Controls and                *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX966_Jobcode_Alias                                              *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX966_JOBCODE_CRITERIA                                           *
!    $GEXXX966_JOBCODE_DISPLAY_CRITERIA                                   *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX966_Jobcode_Alias = 'J.JOBCODE'                            *
!    Do GEXXX966-Select-Jobcode-Parameters                                *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX966_JOBCODE_CRITERIA]                                     *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!  HR-10078	CWB 		04/18/2002 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX966-Select-Jobcode-Parameters

  Let #GEX_RC_JOB_SEL_ROWS = 0
  Let $GEXXX966_JOBCODE_CRITERIA = ''
  Let $GEXXX966_JOBCODE_DISPLAY_CRITERIA = 'ALL'

Begin-Select

GEX_RC_JOB_SEL.JOBCODE
GEX_RC_JOB_SEL.INCLUD_EXCLUDE_IND

  Add 1 To #GEX_RC_JOB_SEL_ROWS

  If #GEX_RC_JOB_SEL_ROWS = 1
    If &GEX_RC_JOB_SEL.INCLUD_EXCLUDE_IND = '+'
      Let $GEXXX966_JOBCODE_CRITERIA = '(' || $GEXXX966_Jobcode_Alias || ' In ('''     || &GEX_RC_JOB_SEL.JOBCODE || ''''
    else
      Let $GEXXX966_JOBCODE_CRITERIA = '(' || $GEXXX966_Jobcode_Alias || ' Not In (''' || &GEX_RC_JOB_SEL.JOBCODE || ''''
    end-if
  Else
    Let $GEXXX966_JOBCODE_CRITERIA = $GEXXX966_JOBCODE_CRITERIA || ',''' || &GEX_RC_JOB_SEL.JOBCODE  || ''''
  End-If
  Let $GEXXX966_JOBCODE_DISPLAY_CRITERIA = $GEXXX966_JOBCODE_CRITERIA
From  PS_GEX_RC_JOB_SEL GEX_RC_JOB_SEL

Where GEX_RC_JOB_SEL.GEX_SEQ             = #GEX_SEQ
And   Rtrim(GEX_RC_JOB_SEL.JOBCODE, ' ') != ' '

End-Select

  If #GEX_RC_JOB_SEL_ROWS = 0
    Let $GEXXX966_JOBCODE_CRITERIA =  $GEXXX966_Jobcode_Alias || ' = ' || $GEXXX966_Jobcode_Alias
    Let $GEXXX966_JOBCODE_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX966_JOBCODE_CRITERIA = $GEXXX966_JOBCODE_CRITERIA || '))'
    Let $GEXXX966_JOBCODE_DISPLAY_CRITERIA = $GEXXX966_JOBCODE_CRITERIA
  End-If

End-Procedure
