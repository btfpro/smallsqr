!***********************************************************************
!  GEXHR034:  EMPL SATISFACTION SURVEY LISTING                         *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXHR034:             Empl Satisfaction Survey Listing               *
!                                                                      *        
! Narrative:		This program will generate a detail report of  *
!                       employees.                                     *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values                    *
!			      #debug7 data returned from sql calls           * 
!                                                                      *  
! SQL Tables:		gex_dept_tbl                                   *
!                       personal_data                                  *
!                       job                                            *
!                                                                      *
! Written by:		Jim Hutchison  02/12/98                        *       
!                                                                      *
! Normally Run:		Monthly                                        *
!                                                                      *
! Control Parms:		FOR WHERE CLAUSE:                              *
!				  Company Division Code                        *
!				  Company  	                                   *
!				  Paygroup  	                             *
!				  Deptid	                                   *
!				  Employee Status	                             *
!				  Account Code                                 *
!				  As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE                *
!***********************************************************************
!	JDH		02/12/98	Initial Creation                         *
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and page-size initialization


begin-report
#debug9 Show 'begin-report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P000-Init-Report
  do P100-Process-Main
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
end-report


begin-heading 6
#debug9 Show 'begin-heading'

  #Include 'stdhdg02.sqc'

  Let $X000_temp_out = 'Report as of ' || $X000_REPORT_HEADING_ASOFDATE
  Print $X000_temp_out         (3,)  center
  Let $X000_temp_out = 'Sorted by ' || $X000_ORDER_BY
  Print $X000_temp_out         (4,)  center

  PRINT   'DEPT'                    (+2,1)
  PRINT   'NAME'                    (,26)
  PRINT   'EMPLOYEE NUMBER'         (,81)
  PRINT   'STORE #'                 (,113)
end-heading


begin-procedure P000-Init-Report
#debug9 Show 'P000-Init-Report'

  move 'GEXHR034' to $ReportID
  move 'EMPL SATISFACTION SURVEY LISTING' to $ReportTitle

  do Stdapi-Init

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate=' $ReportDate
  Show '$ReportTime=' $ReportTime

  if $prcs_process_instance = ''
    let $GEXXX907_INCLUDE_DIV_CD_CRITERIA           = '1=1'           
    let $GEXXX900_INCLUDE_COMPANY_CRITERIA          = '1=1'
    let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1' 
    let $GEXXX902_INCLUDE_DEPTID_CRITERIA	          = '1=1'
    let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA         = '1=1'
    let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA          = '1=1'
    let $X000_WHERE_SELECT_ASOFDATE                 = $AsOfToday
    let $X000_ORDER_BY = ' '

  Else

    Do GEXRCHR1-SELECT-PARAMETERS

    Let $GEXXX900_Company_Alias  = 'J.COMPANY'
    Do GEXXX900-Select-Company-Parameters

    Let $GEXXX901_Company_Alias  = 'J.COMPANY'
    Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
    Do GEXXX901-Select-Company-Paygroup-Parameters

    Let $GEXXX902_Deptid_Alias = 'J.DEPTID'
    Do GEXXX902-Select-Deptid-Parameters

    Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'
    Do GEXXX903-Select-Emp-Stat-Parameters

    Let $GEXXX907_Div_CD_Alias = 'GDT.GEX_COMPANY_DIV_CD'                    
    Do GEXXX907-Select-Div-CD-Parameters 
          
    Let $GEXXX909_Acct_CD_Alias = 'J.ACCT_CD'
    Do GEXXX909-Select-Acct-CD-Parameters

    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_HR.AsOfDate
    If Rtrim ($GEX_RC_HR.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  End-If

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Let $X000_ORDER_BY = 'Company, Dept, Name, Employee Number'

  Show '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA
  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA
  Show '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  Do P050-Print-Cover-Page
end-procedure


begin-procedure P050-Print-Cover-Page
#debug9 Show 'P050-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+2,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                 (0,+2)
  Print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	            (0,+2)
  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


begin-procedure P100-Process-Main
#debug9 Show 'P100-Process-Main'

  Let $Data-Found-Flag = 'N'

begin-SELECT
j.company  ()      on-break  level=1
                             print=never
                             after=P200-After-Company-Change
j.acct_cd  (+1,1)  on-break  level=2
                             print=change/top-page
                             save=$previous-dept
                             after=P300-After-Dept-Change
pd.name    (,26)
j.emplid   (,81)   edit xxx-xx-xxxx
j.deptid   (,113)
  Let $Data-Found-Flag = 'Y'
  let $Company = &j.company
  add 1 to #emp_count
  add 1 to #total_emp_count
  FROM ps_gex_dept_tbl  gdt,
       ps_personal_data pd,
       ps_job           j
 WHERE pd.emplid        = j.emplid
   AND j.effdt          =
      (SELECT MAX(effdt)
         FROM ps_job
        WHERE emplid    = j.emplid
          AND empl_rcd = j.empl_rcd
          AND effdt    <= $X000_WHERE_SELECT_ASOFDATE)
   AND j.effseq         =
      (SELECT MAX(effseq)
         FROM ps_job
        WHERE emplid    = j.emplid
          AND empl_rcd = j.empl_rcd
          AND effdt     = j.effdt)
AND [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
AND [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
AND [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
And    j.deptid                  = gdt.deptid
And    gdt.effdt                 =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid             = gdt.deptid
       And    effdt             <= $X000_WHERE_SELECT_ASOFDATE)
And    gdt.sequence_number       =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid             = gdt.deptid
       And    effdt              = gdt.effdt)
 ORDER BY j.company, j.acct_cd, pd.name, j.emplid
end-SELECT
  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  Else
    let $Company = ''
    print 'TOTAL EMPLOYEE COUNT:'   (+2,16)
    print #total_emp_count          (,+2)   edit 9,999,999
  End-If
end-procedure


begin-procedure P200-After-Company-Change
#debug9 Show 'P200-After-Company-Change'

  new-page
end-procedure


begin-procedure P300-After-Dept-Change
#debug9 Show 'P300-After-Dept-Change'

  print 'EMPLOYEE COUNT:'         (+2,16)
  print #emp_count                (,+2)   edit 9,999,999
  print ''                        (+2,)
  move 0 to #emp_count
end-procedure


#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx904.sqc'  !Get grade multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx908.sqc'  !Get sal_admin_plan multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx921.sqc'  !Get hr single row run control

#include 'askaod.sqc'    !Ask As Of Date input
#include 'getcodta.sqc'  !Get company data
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
