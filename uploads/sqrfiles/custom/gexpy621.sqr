!***********************************************************************
! GEXPY621:  New Hire File                                             *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXPY621 :            NEW HIRE FILE                                  *
!                                                                      *        
! Narrative:            This program creates a file of information on  *
!                       new hires.                                     *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           * 
!                                                                      *  
! SQL Tables:           personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         From Date                                    *
!                         Thru Date                                    *
!                         Contact Name                                 *
!                         Contact Phone                                *
!                         Contact Phone Ext                            *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! JDH       07/16/1998  Initial Creation                               *
! SXK	    09/08/1998	Added OUTFILE variable                         *
! JDH       01/22/1999  Added logic to populate from and thru dates    *
!                        if left blank.  Added SQRW run control logic. *
! JDH       12/01/1999  Changed hire date to point to the effdt of the *
!                        job row.                                      *
! SXR       06/03/2008  SSN to emplid conversion                       *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Portrait)

#Include 'usarpt.sqc'    !Get USA variables

Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXPY621'
  Let $ReportTitle   = 'NEW HIRE FILE'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show '$ReportDate  = ' $ReportDate
  Show '$ReportTime  = ' $ReportTime

  If $prcs_process_instance = ''
    Do Ask-From-Thru-Date
    Input $GEX_RC_PAY.Last_Name         Maxlen=30  'Enter Contact Last Name'
    Input $GEX_RC_PAY.First_Name        Maxlen=30  'Enter Contact First Name'
    Input $GEX_RC_PAY.Contact_Phone     Maxlen=12  'Enter Contact Phone'
    Input $GEX_RC_PAY.Contact_Phone_Ext Maxlen=4   'Enter Contact Phone Ext'
  Else
    Do GEXXX922-Select-Parameters
    Let $FromDate = $GEX_RC_PAY.FromDate
    Let $ThruDate = $GEX_RC_PAY.ThruDate
  End-If

  If Rtrim ($FromDate, ' ') = '' Or
     Rtrim ($ThruDate, ' ') = ''
    Do Get-Default-From-Thru-Dates
  End-If

  Let $Date_Range_Criteria = 'j.action_dt Between ''' ||
                             $FromDate                ||
                             ''' And '''              ||
                             $ThruDate                ||
                             ''''

  Display 'Date_Range_Criteria:  ' Noline
  Display $Date_Range_Criteria

  Let $Year4 = '1'
  Do Format-DateTime($ThruDate, $ThruDate_YMD, {DEFYMD}, '', '')
  Let $ThruDate_YMD2 = Substr($ThruDate_YMD,1,4) ||
                       Substr($ThruDate_YMD,6,2) ||
                       Substr($ThruDate_YMD,9,2)

  Do P140-Open-File
  Do P150-Row-Count
  Do P160-Write-Header-Record
End-Procedure


Begin-Procedure Get-Default-From-Thru-Dates
Begin-Select
Next_Day($AsOfToday,'Sunday')-14 &from_date
Next_Day($AsOfToday,'Sunday')-8 &thru_date

  Let $FromDate = &from_date
  Let $ThruDate = &thru_date

From   dual
End-Select
End-Procedure


begin-procedure P140-Open-File
#debug9 Show 'P140-Open-File'
  Let $file1 = '{OUTFILE}' || Lower($ReportID) || '.dat'

  Open $file1
       As 1
       For-Writing
       Record=537:Fixed
       Status=#filestat

  If #filestat != 0
     Show 'Error opening output file.  Program terminating.'
     Stop
  End-If
End-Procedure


begin-procedure P150-Row-Count
#debug9 show 'P150-Row-Count'
Begin-Select
Count(*) &row_count

  Let #Row_Count = &row_count
  Move #row_count To $Row_Count 099999999

From   ps_personal_data pd,
       ps_employment    e,
       ps_job           j

Where  pd.emplid        = j.emplid

And    j.emplid         = e.emplid
And    j.empl_rcd      = e.empl_rcd

And    j.action         IN ('HIR','REH')
And   [$Date_Range_Criteria]

End-Select
End-Procedure


Begin-Procedure P160-Write-Header-Record
#debug9 Show 'P160-Write-Header-Record'
  Add 1 To #recs-written

  Write 1 From '01':2
               $ThruDate_YMD2:8
               $Row_Count:9
               ' ':518
    Status=#filestat

  If #filestat != 0
     Show 'Error writing output file.  Program terminating.'
     Stop
  End-If
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select
j.company   ()  On-Break  Print=Never  Before=P210-Get-Company-Data
j.emplid
j.effdt
j.action_dt

pd.name
pd.birthdate
!PSR 09/19/2002 V8.3 Upgrade Begin
!pd.national_id
p1.national_id
!PSR 09/19/2002 V8.3 Upgrade Begin
pd.address1
pd.address2
pd.address3
pd.city
pd.state
pd.postal


  Do P220-Format-Record
  Do P230-Write-Detail-Record

From   ps_personal_data pd,
       ps_employment    e,
       ps_job           j,
!PSR 09/19/2002 V8.3 Upgrade Begin
       PS_PERS_NID P1
!PSR 09/19/2002 V8.3 Upgrade End


Where  pd.emplid         = j.emplid
And    j.emplid          = e.emplid
And    j.empl_rcd       = e.empl_rcd

And    j.action         IN ('HIR','REH')
And   [$Date_Range_Criteria]
!PSR 09/19/2002 V8.3 Upgrade Begin
AND P1.EMPLID           = pd.EMPLID
AND P1.COUNTRY          = {NID_COUNTRY}
AND P1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
!PSR 09/19/2002 V8.3 Upgrade End
Order By j.company, pd.name
End-Select
End-Procedure


Begin-Procedure P210-Get-Company-Data
#debug9 Show 'P210-Get-Company-Data'
Begin-Select
ct.federal_ein
ct.descr
ct.address1
ct.address2
ct.address3
ct.city
ct.state
ct.postal
From  ps_company_tbl ct
Where ct.company = &j.company
And   ct.effdt   =
     (Select Max(effdt)
      From   ps_company_tbl
      Where  company  = ct.company
      And    effdt   <= $ThruDate)
End-Select
End-Procedure


Begin-Procedure P220-Format-Record
#debug9 Show 'P220-Format-Record'
  Let $ct.postal = Translate(&ct.postal,'-','')
  Let $pd.postal = Translate(&pd.postal,'-','')

  If Length($ct.postal) <> 9
    Let $ct.postal = Substr($ct.postal,1,5)
  End-If

  If Length($pd.postal) <> 9
    Let $pd.postal = Substr($pd.postal,1,5)
  End-If

  Let $GEX_RC_PAY.Contact_Phone = Translate($GEX_RC_PAY.Contact_Phone,'/-','')

  Let $Name = &pd.name
  Do Rotate-Name

  Let $Year4 = '1'
  Do Format-DateTime(&pd.birthdate, $Birthdate, {DEFYMD}, '', '')
  Let $Birthdate2 = Substr($Birthdate,1,4) ||
                    Substr($Birthdate,6,2) ||
                    Substr($Birthdate,9,2)

  Let $Year4 = '1'
  Do Format-DateTime(&j.effdt, $Hire_Dt, {DEFYMD}, '', '')
  Let $Hire_Dt2 = Substr($Hire_Dt,1,4) ||
                  Substr($Hire_Dt,6,2) ||
                  Substr($Hire_Dt,9,2)
End-Procedure


Begin-Procedure P230-Write-Detail-Record
#debug9 Show 'P230-Write-Detail-Record'
  Add 1 To #recs-written
do Get-Emp-Ssn (&j.emplid, $Ssn_Out)    !isdvsxr 06-03-2008  ITG-42069
  Write 1 From '02':2
               &ct.federal_ein:9
               &ct.descr:45
               &ct.address1:40
               &ct.address2:40
               &ct.address3:40
               &ct.city:25
               &ct.state:2
               $ct.postal:9
               $GEX_RC_PAY.FIRST_NAME:16
               $GEX_RC_PAY.LAST_NAME:30
               $GEX_RC_PAY.Contact_Phone:10
               $GEX_RC_PAY.Contact_Phone_Ext:5
               ! &j.emplid:9           !isdvsxr 06-03-2008  ITG-42069
               $Ssn_Out:9              !isdvsxr 06-03-2008  ITG-42069
               $FirstName:16
               $MidInitial:16
               $LastName:30
               $Birthdate2:8
               &ct.state:2
               $Hire_Dt2:8
               &pd.address1:40
               &pd.address2:40
               &pd.address3:40
               &pd.city:25
               &pd.state:2
               $pd.postal:9
               ' ':19
    Status=#filestat

  If #filestat != 0
     Show 'Error writing output file.  Program terminating.'
     Stop
  End-If
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  Do Format-Number(#recs-written,$recs-written,'888,888,888')

  Show ''
  Show $recs-written ' records written to ' $file1
  Show ''

  Do P310-Close-File

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
End-Procedure


Begin-Procedure P310-Close-File
#debug9 Show 'P310-Close-File'
  Close 1

  If #filestat != 0
     Show 'Error closing output file.  Program terminating.'
     Stop
  End-If
End-Procedure


#include 'gexxx922.sqc'  !Get pay single row run control
#include 'askftd.sqc'    !Ask-From-Thru-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
#Include 'rotname3.sqc'  !Rotate-Name Routine
#Include 'usarpt.sqc'    !Get USA variables
#Include 'getgeid.sqc'   !Get 7 digit Emplid      !isdvsxr 06-03-2008  ITG-42069           