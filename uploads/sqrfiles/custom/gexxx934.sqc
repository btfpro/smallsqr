!**************************************************************************
!  GEXXX934:  Retrieve "Multiple" Dreyfus Plan Run Controls and           *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX934_Dreyfus Plan_Alias                                         *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA                              *
!    $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA                              *
!    $GEXXX934_INCLUDE_DREYFUS_PLAN_DISPLAY_CRITERIA                      *
!    $GEXXX934_EXCLUDE_DREYFUS_PLAN_DISPLAY_CRITERIA                      *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX934_Dreyfus_Plan_Alias = 'GDT.DREYFUS_PLAN'                *
!    Do GEXXX934-Select-Dreyfus-Plan-Parameters                           *
!    Begin-Select                                                         *
!    GEX_DREYFUS_PLAN                                                     *
!    FROM PS_GEX_DREYFUS_TBL GDT                                          *
!    WHERE [$GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA]                      *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!  INITIALS     DATE            DESCRIPTION OF THE CHANGE                 *
!**************************************************************************
!  JDH          09/28/99        INITIAL CREATION                          *
!  JDH          10/29/99        When no parms found, let 1=1              *
!**************************************************************************


Begin-Procedure GEXXX934-Select-Dreyfus-Plan-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DFS_PLAN_ROWS = 0
  Let $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA = ''
  Let $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA = ''
  Let $GEXXX934_INCLUDE_DREYFUS_PLAN_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX934_EXCLUDE_DREYFUS_PLAN_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_DFS_PLAN.GEX_DREYFUS_PLAN

  Add 1 To #GEX_RC_DFS_PLAN_ROWS

  If #GEX_RC_DFS_PLAN_ROWS = 1
    Let $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA = '(' || $GEXXX934_Dreyfus_Plan_Alias || ' In ('''     || &GEX_RC_DFS_PLAN.GEX_DREYFUS_PLAN || ''''
    Let $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA = '(' || $GEXXX934_Dreyfus_Plan_Alias || ' Not In (''' || &GEX_RC_DFS_PLAN.GEX_DREYFUS_PLAN || ''''
  Else
    Let $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA = $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA || ',''' || &GEX_RC_DFS_PLAN.GEX_DREYFUS_PLAN  || ''''
    Let $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA = $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA || ',''' || &GEX_RC_DFS_PLAN.GEX_DREYFUS_PLAN  || ''''
  End-If

From  PS_GEX_RC_DFS_PLAN GEX_RC_DFS_PLAN

Where GEX_RC_DFS_PLAN.OPRID                = [$OprID]
And   GEX_RC_DFS_PLAN.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DFS_PLAN.GEX_DREYFUS_PLAN, ' ') != ' '

End-Select

  If #GEX_RC_DFS_PLAN_ROWS = 0
    Let $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA = '1=1'
    Let $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA = '1=1'
  Else
    Let $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA = $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA || '))'
    Let $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA = $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA || '))'
    Let $GEXXX934_INCLUDE_DREYFUS_PLAN_DISPLAY_CRITERIA = $GEXXX934_INCLUDE_DREYFUS_PLAN_CRITERIA
    Let $GEXXX934_EXCLUDE_DREYFUS_PLAN_DISPLAY_CRITERIA = $GEXXX934_EXCLUDE_DREYFUS_PLAN_CRITERIA
  End-If

End-Procedure
