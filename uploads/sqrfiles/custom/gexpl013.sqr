!***********************************************************************
!  GEXPL013:  Erie Legal Aid                                           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL013:             Erie Legal Aid                                 *
!                                                                      *
! Narrative:            This program creates a monthly report for      *
!                       Erie that contains employee information        *
!                       required by the union to administer the        *
!                       legal aid benefit.                             *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           xlattable                                      *
!                       pay_calendar                                   *
!                       ben_defn_optn                                  *
!                       ben_defn_cost                                  *
!                       ben_defn_pgm                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       benef_plan_tbl                                 *
!                       pay_line                                       *
!                       pay_check                                      *
!                       pay_deduction                                  *
!                       health_benefit                                 *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Monthly                                        *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Plan Type(s)                                 *
!                         Vendor ID(s)                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS   DATE       DESCRIPTION OF THE CHANGE                     *
!***********************************************************************
!  JDH        01/30/98   Initial Creation                              *
!                        $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA being    *
!                          set incorrectly under process scheduler.    *
!  JDH        03/01/99   Added missing line to select.                 *
!  JDH        03/12/99   Fixed problem with gex_hrs_bal select.        *
!  JDH        04/13/99   Get the effdt, action, action_reason from     *
!                          the job row that originally set the         *
!                          empl_status to its current value for        *
!                          empl_status in ('L','P','R','T').           *
!  JDH        06/29/99   Changed logic to get action, action_reason,   *
!                          effdt.                                      *
!                                                                      *                  
!  SXK        10/21/99   Modified to have union from job_labor         *
!                                                                      *                  
!  CWB        03/21/2001 Added /*+ RULE */ hints to selects.           *  
!                                                                      *                  
!  CWB        03/28/2001 Moved union_cd from main select to its own    *
!                        procedure.                                    *       
!                                                                      *                  
!  GBD        07/15/2002 Added Fieldvalue to effective dated selection *
!                        of the Load-Lookup procedure.                 *
!                        Corrected spacing issue with report output.   *
!
!  AMS	     09/10/2007	8.3 TO 9.0 UPGRADE RETROFIT CHANGES 	       *
!
! VENDTKK   02/05/2009  Added INCLUDE GETPOSNM.SQC to pull OBTITLE from *
!			            POSITION_DATA.DESCR field instead of JOBCODE_TBL.DESCR*
! VENDKXY   11/20/2009  Done Changes requested for ITG 69941 			    * 
!                           when store changes in month being processed*
!ISDVAWD     04/22/2014     ITG# 827  - Changed file path  '\\nt5\' *
!			       with '\\corp.gianteagle.com\'`	       *
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************


#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


#define #column1     1
#define #column2    32
#define #column3    42
!GBD 07/15/2002 Begin
!#define #column4    51
#define #column4    53
!#define #column5    57
#define #column5    59
!#define #column6    68
#define #column6    70
!#define #column7    79
#define #column7    81
!#define #column8   110
#define #column8   112
!#define #column9   115
#define #column9   117
!#define #column10  122
#define #column10  124
!#define #column11  129
#define #column11  131
!#define #column12  138
#define #column12  142
!#define #column13  148
#define #column13  153
!#define #column14  162
#define #column14  165
#define #column15 154
!GBD 07/15/2002 End

Begin-Heading 8
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'

  Do Get-Department-Name
  Print 'Deptid:'              (3,1)
  Print $Deptid                (3,13)
  Print $DeptName              (,+2)

  Print $Reporting_Period      (3,) Center 

  Print 'SERVICE'              (+3,{#column3})
  Print 'UNION'                (+0,{#column4})
  Print 'REG'                  (+0,{#column5})
  Print 'IN STORE'             (+0,{#column6})
  Print 'EMPL'                 (+0,{#column8})
  Print 'ACTION'               (+0,{#column10})
  Print 'EFF'                  (+0,{#column11}) 
  Print 'BENEFIT'              (+0,{#column12})

  Print 'EMPLOYEE NAME'        (+1,{#column1})
  Print 'SSN'                  (+0,{#column2})
  Print 'DATE'                 (+0,{#column3})
  Print 'CODE'                 (+0,{#column4})
  Print 'TEMP'                 (+0,{#column5})
  Print 'DEPARTMENT'           (+0,{#column6})
  Print 'JOB TITLE'            (+0,{#column7})
  Print 'STAT'                 (+0,{#column8})
  Print 'ACTION'               (+0,{#column9})
  Print 'REASON'               (+0,{#column10})
  Print 'DATE'                 (+0,{#column11})
  Print 'TERM DATE'            (+0,{#column12})
!GBD 07/15/2002 Begin
!  Print '    MTD HOURS'        (+0,{#column13})
  Print ' MTD HOURS'           (+0,{#column15})
!  Print '    LEGAL AID'        (+0,{#column14})
  Print '   LEGAL AID'         (+0,{#column14})

  Print '-'                    (+1,{#column1},30)  Fill
  Print '-'                    (+0,{#column2},09)  Fill
!  Print '-'                    (+0,{#column3},08)  Fill
  Print '-'                    (+0,{#column3},10)  Fill
  Print '-'                    (+0,{#column4},05)  Fill
  Print '-'                    (+0,{#column5},10)  Fill
  Print '-'                    (+0,{#column6},10)  Fill
  Print '-'                    (+0,{#column7},30)  Fill
  Print '-'                    (+0,{#column8},04)  Fill
  Print '-'                    (+0,{#column9},06)  Fill
  Print '-'                    (+0,{#column10},06) Fill
!  Print '-'                    (+0,{#column11},08) Fill
  Print '-'                    (+0,{#column11},10) Fill
!  Print '-'                    (+0,{#column12},09) Fill
  Print '-'                    (+0,{#column12},10) Fill
!  Print '-'                    (+0,{#column13},13) Fill
  Print '-'                    (+0,{#column13},11) Fill
!  Print '-'                    (+0,{#column14},13) Fill
  Print '-'                    (+0,{#column14},12) Fill
!GBD 07/15/2002 End
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'
  MOVE $CURRENT-DATE TO $CURRDT 'MMDDYY'   !VENDKXY Done the Changes for ITG 69941
!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - - Changed Report name 
!*****************************************************************
!  let $report-name = '\\nt5\ftproot\benefits\Erie_GEXPL013\' ||'PL013PDF_'|| $CURRDT || '.pdf'  !VENDKXY Done the changes for ITG 69941
  let $report-name = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\Erie_GEXPL013\' ||'PL013PDF_'|| $CURRDT || '.pdf'  
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************
  new-report  $report-name   !VENDKXY Done the changes for ITG 69941
  Do Set-Optimizer-Goal

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Do P100-Start
  Do Openfile     !VENDKXY Done the changes for ITG 69441
  Do P200-Main-Process
  Do P300-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report


Begin-Procedure Set-Optimizer-Goal
#debug9 Show 'Set-Optimizer-Goal'
  Begin-SQL
!    Alter Session Set OPTIMIZER_GOAL=RULE;
  End-SQL
End-Procedure

!VENDKXY Done the changes for ITG 69941-- Begin
Begin-Procedure Openfile

    encode '<009>' into $delim

!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - Updated File path   
!*****************************************************************  
!    let $file013 = '\\nt5\ftproot\benefits\Erie_GEXPL013\' ||'PL013XLS_'|| $CURRDT || '.xls' 
    let $file013 = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\\Erie_GEXPL013\' ||'PL013XLS_'|| $CURRDT || '.xls' 
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************  

   	  		
    Open $file013 As 1 For-Writing Record=1000:Vary
    

   Move 'PL013XLS'			To $Report  
   encode '<012>' into $newline 
   String 'Report ID:' $Report  ' ' ' ' ' ' ' ' ' ' $ReportTitle by $delim into $gexpl013
   Write 1 from $gexpl013
   Write 1 from $newline 
   
    
    String 'VENDORID' 'DEPTID' 'EMPLOYEE NAME' 'SSN' 'SERVICE DATE' 'UNION CODE' 'REG TEMP' 'JOB TITLE' 'EMPL STAT' 
    'ACTION' 'ACTION REASON' 'EFF DATE' 'BENEFIT TERM DATE' 'MTD HRS' 'LEGAL AID'  by $delim into $gexpl013
   
    Write 1 from $gexpl013
    
End-Procedure

!VENDKXY Done the changes for ITG 69941-- End


Begin-Procedure P100-Start
#debug9 Show 'P100-Start'

  !Move 'GEXPL013'                    To $ReportID  !VENDKXY Done the changes for ITG 69941
  Move 'PL013PDF'			To $ReportID    !VENDKXY Done the changes for ITG 69941
  Move 'ERIE LEGAL AID'  To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  If $prcs_process_instance = ''

    Move '''' to $ProviderString
    Display 'Enter Vendor ID or leave blank to exit.'
    While 1=1
      Input $Provider maxlen=6 'Provider ID'
      Uppercase $Provider
      if RTRIM($Provider, ' ') = ''
	concat '''' with $ProviderString
	break
      end-if
      if $ProviderString <> ''''
	concat ''',''' with $ProviderString
      end-if
      concat $Provider with $ProviderString
    End-While

    Let $Vendor_ID = $Provider

    if $ProviderString = ''''''
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'ALL'
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2 = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA2 = 'ALL'
    else
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = 'bpt.VENDOR_ID In (' || $ProviderString || ')'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2 = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA2 = $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA
    end-if

    Move '''' to $PlanTypeString
    Display 'Enter Plan Type or leave blank to exit.'
    While 1=1
      Input $PlanType Maxlen=2 'Plan Type'
      Uppercase $PlanType
      if RTRIM($PlanType, ' ') = ''
	concat '''' with $PlanTypeString
	break
      end-if
      if $PlanTypeString <> ''''
	concat ''',''' with $PlanTypeString
      end-if
      concat $PlanType with $PlanTypeString
    End-While

    if $PlanTypeString = ''''''
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA2 = 'ALL'
    else
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = 'bpt.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = 'bpt2.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    end-if

  Else

    Let $GEXXX912_Plan_Type_Alias = 'bpt2.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt2.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA

    Let $GEXXX912_Plan_Type_Alias = 'bpt.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters

    Let $GEXXX927_Vendor_ID_Alias = 'bpt2.VENDOR_ID'
    Do GEXXX927-Select-Vendor-ID-Parameters
    Let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2 = 'bpt2.vendor_id In (' || $ProviderString || ')'
    Let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA2 = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA

    Let $GEXXX927_Vendor_ID_Alias = 'bpt.VENDOR_ID'
    Do GEXXX927-Select-Vendor-ID-Parameters

  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  If $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
    Display 'At least one plan type parameter must be specified'
    Stop
  End-If

  If $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
    Display 'At least one vendor id parameter must be specified'
    Stop
  End-If

  Let $X000_ORDER_BY = 'Deptid, Hire Date, Employee Name'

  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA  = '   $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA
  Show '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA      = '   $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA

  Do Get-Calendar-Year-Id    !getbalid.sqc

  Do P140-Get-Last-Confirmed-Pay
  Do P150-Print-Cover-Page
  Do P170-Get-Xlat-Lookup
End-Procedure


Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

Begin-Select Loops=1
pay_period
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'fmMonth YYYY')           &period
Last_Day(pay_end_dt)                         &current_month_end
Last_Day(Add_Months(pay_end_dt,-1))          &prev_month_end
Last_Day(Add_Months(pay_end_dt,1))           &next_month_end

  Let #weeks-in-month   = &pay_period
  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period

  Let $current_month_begin = '01-'||Substr($pay_end_dt,4,8)
  Let $current_month_end   = &current_month_end
  Let $next_month_end      = &next_month_end
  Let $prev_month_begin    = '01-'||Substr(&prev_month_end,4,8)

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  Show 'Report will contain balances for the month of ' $Reporting_Period
  Show 'Last confirmed pay end date:  ' $pay_end_dt

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_calendar

Where  pay_end_dt         =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt <= $AsOfToday
       And    pay_confirm_run = 'Y')

And    pay_confirm_run = 'Y'
End-Select
End-Procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+2,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,2)
  Print '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = '  (+2,5)
  Print $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA    = '  (+2,5)
  Print $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA          (0,+2)

  Print 'Report will contain balances for the month of'       (+2,5)
  Print $Reporting_Period                                     (0,+1)
  Print 'Last confirmed pay end date:'                        (+2,5)
  Print $pay_end_dt                                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


begin-procedure P170-Get-Xlat-Lookup
#debug9 Show 'P170-Get-Xlat-Lookup'

  Let $Where = '     x.fieldname   = ''REG_TEMP''' ||
	       ' And x.eff_status  = ''A''' ||
!	       ' And x.language_cd = ''ENG''' ||
	       ' And x.effdt       = (Select Max(effdt)' ||
!	       '                      From   xlattable x1' ||																	!AMS 09/10/2007
	       '                      From   psxlatitem x1' ||																!AMS 09/10/2007
	       '                      Where  x1.fieldname    = x.fieldname' ||
!GBD 07/15/2002 Begin
!	       '                      And    x1.language_cd  = ''ENG''' ||
!	       '                      And    x1.language_cd  = x.language_cd' ||								!AMS 09/10/2007
	       '                      And    x1.fieldvalue  = x.fieldvalue' ||
!GBD 07/15/2002 End
	       '                      And    x1.effdt       <= ''' || $pay_end_dt || '''' ||
	       '                      And    x1.eff_status   = ''A'')'

  Load-Lookup
    Name=XlatLookup
    Rows=10
!    Table='XLATTABLE X'																		!AMS 09/10/2007
		Table='PSXLATITEM X'																		!AMS 09/10/2007
    Key='X.FIELDVALUE'
    Return_Value='X.XLATSHORTNAME'
    Where=$Where
    Quiet
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
 
  Let $Data-Found-Flag = 'N'

Begin-Select
pl.deptid         ()        On-Break
                            Level=1
			    Print=Never
			    Save=$Save_Deptid
			    After=P290-After-Deptid-Break

e.service_dt
! CWB 03/28/2001 BEGIN
!jl.union_cd  !SXK 10/21/99
! CWB 03/28/2001 END

per.name
per.birthdate

bpt.plan_type
bpt.benefit_plan

j.emplid
j.empl_rcd
j.effdt
j.company
j.paygroup
j.reg_temp
j.empl_status
j.acct_cd
j.jobcode
j.Position_nbr 			!VENDTKK   ITG#56786   5-Feb-2009
bpt.VENDOR_ID    !VENDKXY Done the changes for ITG 69941
Sum(pd.ded_cur) &pd.ded_cur


  Let $Data-Found-Flag = 'Y'

  Let $Deptid = &pl.deptid

  Let #contribution_amt = &pd.ded_cur

  Let $Vendor_ID = &bpt.VENDOR_ID    !VENDKXY Done the changes for ITG 69941
! CWB 03/28/2001 BEGIN
  Do P180-get-union-code
! CWB 03/28/2001 END

  
  Do P220-Get-Benefit-Termination-Row

  Evaluate &j.empl_status
    When = 'L'
    When = 'P'
    When = 'R'
    When = 'T'
      Do P230-Get-Leave-Begin-Date
      Break
    When-Other
      Do P235-Get-Status-Date
  End-Evaluate

  Do P240-Get-MTD-Hours
  Do P250-Format-Record
  Do P260-Print-Record

  Add #hrs_mtd          To #Deptid-Hours
  Add #contribution_amt To #Deptid-Amount

From   ps_personal_data          per,
       ps_employment             e,
       ps_job                    j,
       ps_benef_plan_tbl         bpt,
       ps_pay_cal_bal_id         pcbi,
       ps_pay_line               pl,
       ps_pay_check              pc,
       ps_pay_deduction          pd

! CWB 03/28/2001 BEGIN
!       ps_job_labor jl                   !SXK 10/21/99
! CWB 03/28/2001 END

Where  per.emplid                = j.emplid

And    e.emplid                  = j.emplid
And    e.empl_rcd               = j.empl_rcd
!SXK 10/21/1999 begin
!SXK 10/21/1999 end
And    j.effdt                   =
      (Select Max(effdt)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt             <= $pay_end_dt)

And    j.effseq                  =
      (Select Max(effseq)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt              = j.effdt)

And    bpt.effdt                 =
      (Select Max(effdt)
       From   ps_benef_plan_tbl
       Where  plan_type          = bpt.plan_type
       And    benefit_plan       = bpt.benefit_plan
       And    effdt             <= $pay_end_dt)

And    pcbi.company              = pc.company
And    pcbi.paygroup             = pc.paygroup
And    pcbi.balance_id           = 'CY'
And    pcbi.balance_year         = #balance_year
And    pcbi.balance_period       = #balance_period

And    pc.emplid                 = j.emplid
And    pc.empl_rcd              = j.empl_rcd

And    pd.company                = pc.company
And    pd.paygroup               = pc.paygroup
And    pd.pay_end_dt             = pc.pay_end_dt
And    pd.off_cycle              = pc.off_cycle
And    pd.page_num                  = pc.page_num
And    pd.line_num                  = pc.line_num
And    pd.sepchk                 = pc.sepchk

And    pc.pay_end_dt             = pcbi.pay_end_dt

And    pl.company                = pc.company
And    pl.paygroup               = pc.paygroup
And    pl.pay_end_dt             = pc.pay_end_dt
And    pl.off_cycle              = pc.off_cycle
And    pl.page_num                  = pc.page_num
And    pl.line_num                  = pc.line_num

And    pd.plan_type              = bpt.plan_type
And    pd.benefit_plan           = bpt.benefit_plan
And    pd.ded_class              = 'N'

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA]
And   [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA]

Group By pl.deptid,
       e.service_dt,

! CWB 03/28/2001 BEGIN
!       jl.union_cd,  !SXK 10/21/99 Changed from e.union_cd
! CWB 03/28/2001 BEGIN
       per.name,
       per.birthdate,
       bpt.plan_type,
       bpt.benefit_plan,
       j.emplid,
       j.empl_rcd,
       j.effdt,
       j.company,
       j.paygroup,
       j.reg_temp,
       j.empl_status,
       j.acct_cd,
       j.jobcode,
       j.Position_nbr, 			!VENDTKK   ITG#56786   5-Feb-2009
       bpt.VENDOR_ID    !VENDKXY Done the changes for ITG 69941
Order By pl.deptid, e.service_dt, per.name

End-Select
End-Procedure


Begin-Procedure P220-Get-Benefit-Termination-Row
#debug9 Show 'P220-Get-Benefit-Termination-Row'
Begin-Select
hb.coverage_begin_dt

From   ps_health_benefit hb

Where  hb.emplid         = &j.emplid
And    hb.empl_rcd      = &j.empl_rcd
And    hb.plan_type      = &bpt.plan_type

And    hb.effdt          =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid     = &j.emplid
       And    empl_rcd  = &j.empl_rcd
       And    plan_type  = &bpt.plan_type)

And    hb.coverage_begin_dt Between $prev_month_begin
                                And $next_month_end

And    hb.coverage_elect = 'T'
End-Select
End-Procedure


Begin-Procedure P230-Get-Leave-Begin-Date
#debug9 Show 'P230-Get-Leave-Begin-Date'
  Let $Status-Change = 'N'
Begin-Select
JOB.EFFDT
JOB.EMPL_STATUS
JOB.ACTION
JOB.ACTION_REASON

  If &JOB.EMPL_STATUS = &J.EMPL_STATUS And
      $Status-Change = 'N'
    Let $Effective-Date = &JOB.EFFDT
    Let $Action         = &JOB.ACTION
    Let $Action_Reason  = &JOB.ACTION_REASON
    Let $Status-Change = 'N'
  Else
    Let $Status-Change = 'Y'
  End-If

FROM   PS_JOB JOB

WHERE  EMPLID     = &J.EMPLID
AND    EMPL_RCD  = &J.EMPL_RCD
AND    EFFDT     <= &J.EFFDT
ORDER BY JOB.EFFDT DESC, JOB.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P235-Get-Status-Date
#debug9 Show 'P235-Get-Status-Date'
  Let $Reg-Temp-Change = 'N'
Begin-Select
J1.EFFDT
J1.REG_TEMP
J1.ACTION
J1.ACTION_REASON
J1.EMPL_STATUS

  If &J1.REG_TEMP = &J.REG_TEMP And
      $Reg-Temp-Change = 'N'
    Let $Reg-Temp-Change = 'N'
    If &J1.EMPL_STATUS <> 'T'
      Let $Effective-Date = &J1.EFFDT
      Let $Action         = &J1.ACTION
      Let $Action_Reason  = &J1.ACTION_REASON
    End-If
  Else
    Let $Reg-Temp-Change = 'Y'
  End-If

FROM   PS_JOB J1

WHERE  EMPLID     = &J.EMPLID
AND    EMPL_RCD  = &J.EMPL_RCD
AND    EFFDT     <= &J.EFFDT
ORDER BY J1.EFFDT DESC, J1.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P240-Get-MTD-Hours
#debug9 Show 'P240-Get-MTD-Hours'

  Let #hrs_mtd = 0

Begin-Select
Sum(ghb.total_hrs) &ghb.total_hrs

  Let #hrs_mtd = &ghb.total_hrs

From   ps_gex_hrs_bal ghb

!Where ghb.company            = &j.company
!And   ghb.paygroup           = &j.paygroup
!And   ghb.emplid             = &j.emplid
Where  ghb.emplid             = &j.emplid
And    ghb.empl_rcd          = &j.empl_rcd
And    ghb.plan_type          = &bpt.plan_type
And    ghb.benefit_plan       = &bpt.benefit_plan
And    ghb.check_dt     Between $current_month_begin
                            And $current_month_end
End-Select
End-Procedure


Begin-Procedure P250-Format-Record
#debug9 Show 'P250-Format-Record'

  Lookup XlatLookup &j.reg_temp $reg_temp

  Let $JobCode = &j.jobcode
  Do Get-Job-Title

!VENDTKK Begin ITG#56786   5-Feb-2009
  Let $Position_Nbr = &j.Position_nbr
  Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosName = '' 
  		Let $jobtitle = $jobtitle
	 Else
 		Let $jobtitle = $PosName
	 End-if
!VENDTKK End ITG#56786   5-Feb-2009

  Let $acctcd = &j.acct_cd
  Do Get-Acct-cd-descr

  Do Format-DateTime(&e.service_dt, $Service_Dt, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime(&e.service_dt, $ServiceDt, {DEFYMD}, '', '')

  Do Format-DateTime(&per.birthdate, $Birthdate, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime($Effective-Date, $effdt, {DEFYMD}, '', '')

  Do Format-DateTime($Effective-Date, $eff_dt, {DEFMDY}, '', '')

  Do Format-DateTime(&hb.coverage_begin_dt, $Term_Date, {DEFMDY}, '', '')

  Move #contribution_amt To #Legal_Hours
End-Procedure


Begin-Procedure P260-Print-Record
#debug9 Show 'P260-Print-Record'
 do Get-Emp-Ssn (&j.emplid, $Ssn_Out) !GEX_SEC_REENG 2015-04-29 Vahini Katta 
  add 1 to #emp-counter
  Print &per.name                  (+1,{#column1},30)
  !Print &j.emplid                  (+0,{#column2}) !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $Ssn_Out                   (+0,{#column2})  !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $Service_Dt                (+0,{#column3})
  Print &jl.union_cd                (+0,{#column4}) !SXK 10/21/99
  Print $reg_temp                  (+0,{#column5})
  Print $AcctNameAbbrv             (+0,{#column6})
  Print $JobTitle                  (+0,{#column7})
  Print &j.empl_status             (+0,{#column8})
  Print $Action                    (+0,{#column9})
  Print $Action_Reason             (+0,{#column10})
  Print $eff_dt                    (+0,{#column11})
  Print $Term_Date                 (+0,{#column12})
!GBD 07/15/2002 Begin
!  Print #hrs_mtd                   (+0,{#column13})  Edit 99,999,999.99
  Print #hrs_mtd                   (+0,{#column15})  Edit 999,999.99
!  Print #contribution_amt          (+0,{#column14})  Edit 99,999,999.99
  Print #contribution_amt          (+0,{#column14})  Edit 9,999,999.99
!GBD 07/15/2002 End

  !VENDKXY Done the changes for ITG 69941-- Begin 
            
   !STRING $Vendor_ID $Deptid &per.name &j.emplid &e.service_dt &jl.union_cd $reg_temp $JobTitle &j.empl_status $Action  $Action_Reason  $Effective-Date &hb.coverage_begin_dt #hrs_mtd #contribution_amt BY $DELIM INTO $GEXPL013 !GEX_SEC_REENG 2015-04-29 Vahini Katta
   STRING $Vendor_ID $Deptid &per.name $Ssn_Out &e.service_dt &jl.union_cd $reg_temp $JobTitle &j.empl_status $Action  $Action_Reason  $Effective-Date &hb.coverage_begin_dt #hrs_mtd #contribution_amt BY $DELIM INTO $GEXPL013   !GEX_SEC_REENG 2015-04-29 Vahini Katta
   WRITE 1 FROM $GEXPL013

  !Vendkxy done the changes for ITG 69941-- End

End-Procedure


Begin-Procedure P290-After-Deptid-Break
#debug9 Show 'P290-After-Deptid-Break'

  Print 'Deptid'                   (+2,{#column10})
  Print $Save_Deptid               (0,+1)
  Print 'Totals'                   (0,+1)
!GBD 07/15/2002 Begin
!  Print #Deptid-Hours              (0,{#column13})   Edit 99,999,999.99
  Print #Deptid-Hours              (0,{#column15})   Edit 999,999.99
!  Print #Deptid-Amount             (0,{#column14})   Edit 99,999,999.99
  Print #Deptid-Amount             (0,{#column14})   Edit 9,999,999.99
!GBD 07/15/2002 End
  Add #Deptid-Hours  To #Report-Hours
  Add #Deptid-Amount To #Report-Amount

  Let #Deptid-Hours  = 0
  Let #Deptid-Amount = 0
  New-Page
End-Procedure


Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'

  Do P310-Print-Report-Totals
  show ' '
  show 'Employees reported: ' #emp-counter
  show ' '
  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
End-Procedure


Begin-Procedure P310-Print-Report-Totals
#debug9 Show 'P310-Print-Report-Totals'

  If $Data-Found-Flag = 'Y'
    Let $Deptid   = ''
    Let $DeptName = ''
    Print 'Report Totals'            (+2,{#column10})
!GBD 07/15/2002 Begin
!    Print #Report-Hours              (0,{#column13})    Edit 99,999,999.99
    Print #Report-Hours              (0,{#column15})    Edit 999,999.99
!    Print #Report-Amount             (0,{#column14})    Edit 99,999,999.99
    Print #Report-Amount             (0,{#column14})    Edit 9,999,999.99
!GBD 07/15/2002 End
  Else
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
  End-If
End-Procedure

! CWB 03/28/2001 BEGIN
Begin-procedure p180-get-union-code
begin-select
jl.union_cd

from ps_job jl
Where    JL.EMPLID    = &J.EMPLID
AND    JL.EMPL_RCD   = &J.EMPL_RCD

AND    JL.EFFDT                =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID          = JL.EMPLID
       AND    EMPL_RCD       = JL.EMPL_RCD
       AND    EFFDT          <= $pay_end_dt)

AND    Jl.EFFSEQ               =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB
       WHERE  EMPLID          = JL.EMPLID
       AND    EMPL_RCD       = JL.EMPL_RCD
       AND    EFFDT           = JL.EFFDT)

end-select
end-procedure
! CWB 03/28/2001 END


#include 'gexxx912.sqc'  !Get plan type multiple row table
#include 'gexxx927.sqc'  !Get vendor id multiple row table
#include 'getdptnm.sqc'  !Get department name
#include 'getactnm.sqc'  !Get account code name
#include 'getjobtl.sqc'  !Get job title
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   5-Feb-2009 