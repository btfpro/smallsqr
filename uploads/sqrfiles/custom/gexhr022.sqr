!***********************************************************************
!  GEXHR022:  EMPLOYEE HOME ADDRESS LISTING (WITH BIRTHDAYS)           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXHR022:             Employee Home Address Listing (with birthdays) *
!                                                                      *
! Narrative:            This program will generate a listing of        *
!                       employees' home addresses based on run-time    *
!                       select criteria.                               *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           * 
!                                                                      *  
! SQL Tables:           gex_dept_tbl                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *       
!                                                                      *
! Normally Run:	        Monthly                                        *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Company Division Code                        *
!                         Company  	                               *
!                         Paygroup  	                               *
!                         Deptid	                               *
!                         Employee Status	                       *
!                         Account Code                                 *
!                         From Deparment                               *
!                         Thru Department                              *
!                         As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  JDH          02/13/98     Initial Creation                          *
!  JDH          09/03/99     Added home telephone number.              *
!  SXK          11/23/99     Changed from zip to postal                *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


begin-report
#debug9 Show 'begin-report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Init-Report
  do P100-Process-Main
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc
  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  Show 'Successful end of report'
end-report


begin-heading 7
#debug9 Show 'begin-heading'

  #Include 'stdhdg02.sqc'

  Let $X000_temp_out = 'Report as of ' || $X000_REPORT_HEADING_ASOFDATE
  Print $X000_temp_out         (3,)  center

  let $ReportTitle2 = 'Paygroup    '||$Paygroup
  PRINT $ReportTitle2          (4,1)

  PRINT $DeptName              (4,)  center

  PRINT 'EMPLOYEE NAME'        (+2,1)
  PRINT 'ADDRESS'              (,34)
  PRINT 'CITY'                 (,83)
  PRINT 'STATE'                (,116)
  PRINT 'ZIP'                  (,124)
  PRINT 'BIRTHDATE'            (,137)
  PRINT 'HIRE DATE'            (,150)
  PRINT 'HOME PHONE'           (,163)

  PRINT '-'        (+1,1,30)  Fill
  PRINT '-'        (,34,46)   Fill
  PRINT '-'        (,83,30)   Fill
  PRINT '-'        (,116,5)   Fill
  PRINT '-'        (,124,10)  Fill
  PRINT '-'        (,137,10)  Fill
  PRINT '-'        (,150,10)  Fill
  PRINT '-'        (,163,12)  Fill

end-heading


begin-procedure Init-Report
#debug9 Show 'Init-Report'

  move 'GEXHR022' to $ReportID
  move 'EMPLOYEE HOME ADDRESS LISTING (WITH BIRTHDAYS)' to $ReportTitle

  do Stdapi-Init

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate=' $ReportDate
  Show '$ReportTime=' $ReportTime

  Let $GEXXX900_Company_Alias        = 'J.COMPANY'
  Let $GEXXX901_Company_Alias        = 'J.COMPANY'
  Let $GEXXX901_Paygroup_Alias       = 'J.PAYGROUP'
  Let $GEXXX902_Deptid_Alias         = 'J.DEPTID'
  Let $GEXXX903_Emp_Stat_Alias       = 'J.EMPL_STATUS'
  Let $GEXXX904_Grade_Alias          = 'J.GRADE'    
  Let $GEXXX904_Sal_Admin_Plan_Alias = 'J.SAL_ADMIN_PLAN'
  Let $GEXXX907_Div_CD_Alias         = 'GDT.GEX_COMPANY_DIV_CD'
  Let $GEXXX909_Acct_CD_Alias        = 'J.ACCT_CD'

  if $prcs_process_instance = ''
    do GEXXX950-Input-Parameters
    let $GEXXX904_INCLUDE_GRADE_CRITERIA = '1=1'
    let $Deptid_Range_Criteria = '1=1'
  Else
    Do GEXXX922-Select-Parameters

    Do GEXXX900-Select-Company-Parameters
    Do GEXXX901-Select-Company-Paygroup-Parameters
    Do GEXXX902-Select-Deptid-Parameters
    Do GEXXX903-Select-Emp-Stat-Parameters
    Do GEXXX904-Select-Grade-Parameters   
    Do GEXXX907-Select-Div-CD-Parameters 
    Do GEXXX909-Select-Acct-CD-Parameters

    if Rtrim($GEX_RC_PAY.DEPTID_FROM, ' ') = '' or
       Rtrim($GEX_RC_PAY.DEPTID_THRU, ' ') = ''
      let $Deptid_Range_Criteria = '1=1'
      let $GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA = 'ALL'
      let $Deptid_Range_Display_Criteria = 'ALL'
    else
      let $Deptid_Range_Criteria = 'J.DEPTID BETWEEN ''' || $GEX_RC_PAY.DEPTID_FROM || ''' AND ''' || $GEX_RC_PAY.DEPTID_THRU || ''''
    end-if

    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate
    If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Let $Deptid_Range_Display_Criteria = $Deptid_Range_Criteria

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Let $X000_ORDER_BY = 'Company, Paygroup, Department, Name'

  Show '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA
  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA
  Show '$GEXXX904_INCLUDE_GRADE_CRITERIA                    ='   $GEXXX904_INCLUDE_GRADE_CRITERIA		  
  Show '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA
  Show '$Deptid_Range_Criteria =' $Deptid_Range_Display_Criteria
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  Do P050-Print-Cover-Page
end-procedure


begin-procedure P050-Print-Cover-Page
#debug9 Show 'P050-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+2,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                 (0,+2)
  Print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	          (0,+2)
  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA            ='   (+1,5)
  Print $GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA	          (0,+2)
  Print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)
  Print '$Deptid_Range_Criteria                              ='   (+1,5)
  Print $Deptid_Range_Display_Criteria                            (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


begin-procedure P100-Process-Main
#debug9 Show 'P100-Process-Main'

  Let $Data-Found-Flag = 'N'

begin-SELECT
J.COMPANY               ()         ON-BREAK    LEVEL=1
                                               PRINT=NEVER

J.PAYGROUP              ()         ON-BREAK    LEVEL=2
                                               PRINT=NEVER

J.DEPTID                ()         ON-BREAK    LEVEL=3
                                               PRINT=NEVER
                                               SAVE=$previous_deptid
                                               AFTER=P200-Deptid-Change

PD.NAME
PD.ADDRESS1
PD.ADDRESS2
PD.CITY
PD.STATE
PD.POSTAL !SXK 11/23/1999 Changed from zip
PD.PHONE
PD.BIRTHDATE
E.HIRE_DT

  Let $Data-Found-Flag = 'Y'

  Let $Company = &J.COMPANY
  Let $Paygroup = &J.PAYGROUP

  let $Year4 = '1'
  do Format-DateTime(&pd.birthdate,$birthdate,{DEFMDY},'','')

  let $Year4 = '1'
  do Format-DateTime(&e.hire_dt,$hire_dt,{DEFMDY},'','')

  if $DeptName = ''
    LET $DeptID = &J.DEPTID
    do Get-Department-Name
  end-if

  let $address = rtrim(&PD.ADDRESS1,' ') || ' ' || ltrim(&PD.ADDRESS2,' ')

  print &PD.NAME                 (+1,1,30)
  print $address                 (,34)
  print &PD.CITY                 (,83)
  print &PD.STATE                (,116)
  print &PD.POSTAL               (,124)
  print $birthdate               (,137)
  print $hire_dt                 (,150)

  If &PD.PHONE <> '000/000-0000'
    Print &PD.PHONE              (,163)
  End-If

FROM   ps_gex_dept_tbl   gdt,
       PS_PERSONAL_DATA  PD,
       PS_EMPLOYMENT     E,
       PS_JOB            J

WHERE  PD.EMPLID           = J.EMPLID

AND    J.EMPLID            = E.EMPLID
AND    J.EMPL_RCD         = E.EMPL_RCD

AND    J.EFFDT             =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID       = J.EMPLID
       AND    EMPL_RCD    = J.EMPL_RCD
       AND    EFFDT       <= $X000_WHERE_SELECT_ASOFDATE)

AND    J.EFFSEQ            =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB
       WHERE  EMPLID       = J.EMPLID
       AND    EMPL_RCD    = J.EMPL_RCD
       AND    EFFDT        = J.EFFDT)

And    j.deptid            = gdt.deptid

And    gdt.effdt           =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid       = gdt.deptid
       And    effdt       <= $X000_WHERE_SELECT_ASOFDATE)

And    gdt.sequence_number =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid       = gdt.deptid
       And    effdt        = gdt.effdt)

AND   [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
AND   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND   [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
AND   [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
AND   [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
AND   [$GEXXX904_INCLUDE_GRADE_CRITERIA]

AND   [$Deptid_Range_Criteria]

ORDER BY J.COMPANY, J.PAYGROUP, J.DEPTID, PD.NAME
end-SELECT

  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
end-procedure


begin-procedure P200-Deptid-Change
  new-page
  let $DeptName = ''
end-procedure


#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx904.sqc'  !Get grade multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx908.sqc'  !Get sal_admin_plan multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx950.sqc'  !Get SQRW run controls

#include 'askaod.sqc'    !Ask As Of Date input
#include 'getcodta.sqc'  !Get company data
#include 'getdptnm.sqc'  !Get department name
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
