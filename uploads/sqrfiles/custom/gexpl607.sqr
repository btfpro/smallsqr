!***********************************************************************
!  GEXPL607:   UFCW - Pension Office (not eligible) Report and File    *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 2003 Giant Eagle,Inc. All Rights Reserved              *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL607:             UFCW Pension Office                            *
!                       (not eligible) Report                          *
!                                                                      *
! Narrative:		This program creates a monthly report for      *
!                       UFCW that contains employee information        *
!                       required by the union to administer the pension*     
!                       benefit. It shows ee's that are eligible (or   * 
!                       becoming eligible for pension but not yet      *
!                       enrolled.                                      *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!                                                                      *
! SQL Tables:		pay_calendar                                   *
!                       ben_defn_pgm                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       ben_prog_partic                                *
!                       earnings_bal                                   *
!                       health_benefit                                 *
!                                                                      *
! Written by:		Tony Labra                                     *
!                                                                      *
! Normally Run:		Monthly                                        *
!                                                                      *
! Control Parms:	FOR WHERE CLAUSE:                              *
!                         Plan Type(s)                                 *
!                         Benefit Program(s)                           *
!                         Union Code(s)                                *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS	DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  AXL          07/23/2003   Initial Creation.                         *
!  								       *
!  DXS          02/14/2008   Changed criteria in Health-Benefit	       *
!								       *
!  TCSXSSS      03/25/2015   Modified to add the Moveit SQC and changes*
!								       *
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#include 'setup32.sqc'   !Printer and page-size initialization

#define #column1     1
#define #column2    35
#define #column3    48
#define #column4    60
#define #column5    72
#define #column6    81
#define #column7    88
#define #column8    96
#define #column9   106
#define #column10  116
#define #column11  127
#define #column12  144
#define #column13  161

!***********************************************************************
Begin-Heading 8
!***********************************************************************
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'

  Do Get-Benefit-Program-Name

  Print 'Ben Pgm:'             (3,1)
  Print $Benefit_Program       (3,13)
  Print $BenefitProgramDescr   (3,) Center

  Print $Reporting_Period      (4,) Center 

  print 'SERVICE'              (+2,{#column3})
  print 'BIRTH'                (+0,{#column4})
  print 'UNION'                (+0,{#column5})
  print 'EMPL'                 (+0,{#column7})
  print 'ACTION'               (+0,{#column9}) 
  print 'EFF'                  (+0,{#column10})
  print '           MTD'       (+0,{#column12})
  print '         TOTAL'       (+0,{#column13})

  print 'EMPLOYEE NAME'        (+1,{#column1}) 
  print 'SSN'                  (+0,{#column2})
  print 'DATE'                 (+0,{#column3})
  print 'DATE'                 (+0,{#column4})
  print 'CODE'                 (+0,{#column5})
  print 'F/P'                  (+0,{#column6})
  print 'STAT'                 (+0,{#column7})
  print 'ACTION'               (+0,{#column8}) 
  print 'REASON'               (+0,{#column9}) 
  print 'DATE'                 (+0,{#column10})
  print '     MTD HOURS'       (+0,{#column11})
  print '    VACN HOURS'       (+0,{#column12})
  print '     MTD HOURS'       (+0,{#column13})

  print '-'                    (+1,{#column1},30)  FILL
  print '-'                    (+0,{#column2},09)  FILL
  print '-'                    (+0,{#column3},08)  FILL
  print '-'                    (+0,{#column4},08)  FILL
  print '-'                    (+0,{#column5},05)  FILL
  print '-'                    (+0,{#column6},03)  FILL
  print '-'                    (+0,{#column7},04)  FILL
  print '-'                    (+0,{#column8},06)  FILL
  print '-'                    (+0,{#column9},06)  FILL
  print '-'                    (+0,{#column10},08) FILL
  print '-'                    (+0,{#column11},14) FILL
  print '-'                    (+0,{#column12},14) FILL
  print '-'                    (+0,{#column13},14) FILL
End-Heading

!***********************************************************************
Begin-Report
!***********************************************************************
#debug9 Show 'Begin-Report'

  !Do Set-Optimizer-Goal

  Do Init-DateTime
  Do Init-Number
  Do Get-Current-DateTime
  Do Stdapi-Init

  Do Start
  Do Main-Process
  Do Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report

!***********************************************************************
Begin-Procedure Set-Optimizer-Goal
!***********************************************************************
  Begin-SQL
    Alter Session Set OPTIMIZER_GOAL=RULE;
  End-SQL
End-Procedure

!***********************************************************************
Begin-Procedure Start
!***********************************************************************
#debug9 Show 'Start'

  move 'GEXPL607'                       To $ReportID
  move 'UFCW PENSION (NOT ELIGIBLE)'	To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  Let $GEXXX911_Benefit_Program_Alias = 'bpp.benefit_program'
  Let $GEXXX913_Union_cd_Alias 	      = 'J.UNION_CD'

  If $prcs_process_instance = ''

    Let $BenefitProgramString = ''''
    Display 'Enter Benefit Program or leave blank to exit.'
    While 1=1
      Input $In-Benefit-Program Maxlen=3 'Benefit Program'
      Uppercase $In-Benefit-Program
      If Rtrim($In-Benefit-Program, ' ') = ''
        Concat '''' With $BenefitProgramString
        Break
      End-If
      If $BenefitProgramString <> ''''
        Concat ''',''' With $BenefitProgramString
      End-If
      Concat $In-Benefit-Program With $BenefitProgramString
    End-While

    If $BenefitProgramString = ''''''
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = '1=1'
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX911_Benefit_Program_Alias || ' In (' || $BenefitProgramString || ')'
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA
    End-If

    Let $UnionCdString = ''''
    Display 'Enter Union Code or leave blank to exit.'
    While 1=1
      Input $In-UnionCd Maxlen=3 'Union Code'
      Uppercase $In-UnionCd
      If Rtrim($In-UnionCd, ' ') = ''
        Concat '''' With $UnionCdString
        Break
      End-If
      If $UnionCdString <> ''''
        Concat ''',''' With $UnionCdString
      End-If
        Concat $In-UnionCd With $UnionCdString
    End-While

    If $UnionCdString = ''''''
      Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
      Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_Union_CD_Alias || ' In (' || $UnionCdString || ')'
      Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
    End-If

  Else

    Do GEXXX911-Select-Benefit-Program-Parameters
    Do GEXXX913-Select-UNION-CD-Parameters

  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  If $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = '1=1'
    Display 'At least one benefit program parameter must be specified'
    Stop
  End-If

  If $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
    Display 'At least one union code parameter must be specified'
    Stop
  End-If

  Let $X000_ORDER_BY = 'Benefit Program, Employee Name'

  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA    = '   $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA
  Show '$GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = '   $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA
  Show '$GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA        = '   $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA

  Do Get-Calendar-Year-Id    !getbalid.sqc

  Do Get-Last-Confirmed-Pay
  Do Print-Cover-Page
  Do Open-File

End-Procedure

!***********************************************************************
Begin-Procedure Get-Last-Confirmed-Pay
!***********************************************************************
#debug9 Show 'Get-Last-Confirmed-Pay'

Begin-Select
pay_period
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'fmMonth YYYY')           &period
Last_Day(pay_end_dt)                         &current_month_end

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  Show 'Report will contain balances for the month of ' $Reporting_Period
  Show 'Last confirmed pay end date:  ' $pay_end_dt

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_calendar

Where  company                = 'GEI'
And    paygroup               = 'RET'

And    pay_end_dt             =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  company         = 'GEI'
       And    paygroup        = 'RET'
       And    pay_end_dt     <= $AsOfToday
       And    pay_confirm_run = 'Y')

And    pay_confirm_run = 'Y'
End-Select
End-Procedure

!***********************************************************************
begin-procedure Print-Cover-Page
!***********************************************************************
#debug9 Show 'Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+2,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                   (+5,2)
  Print '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA     = '    (+2,5)
  Print $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA             (0,+2)
  Print '$GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA  = '    (+2,5)
  Print $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA          (0,+2)
  Print '$GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA         = '    (+2,5)
  Print $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA                 (0,+2)

  Print 'Report will contain balances for the month of'       (+2,5)
  Print $Reporting_Period                                     (0,+1)
  Print 'Last confirmed pay end date:'                        (+2,5)
  Print $pay_end_dt                                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  New-Page
End-Procedure

!***********************************************************************
Begin-Procedure Open-File
!***********************************************************************
#debug9 Show 'Open-File'

  Let $File1 = '{OUTFILE}' || Lower($ReportID) || '.dat'

  show '$file1 ' $file1

  Open $File1
    As 1
    For-Writing
    Record=207:Fixed
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Opening output file.  Program terminating.'
    Stop
  End-If
End-Procedure

!***********************************************************************
Begin-Procedure Main-Process
!***********************************************************************
#debug9 Show 'Main-Process'
  let #Total-Recs-Printed  = 0                  
 
Begin-Select
bpp.benefit_program  ()     On-Break   Level=1
                                       Print=Never
                                       Save=$Save_Benefit_Program
                                       After=After-BP-Break

bpp.emplid
bpp.empl_rcd
bpp.cobra_event_id

e.service_dt
e.termination_dt

pd.name
pd.Last_Name
pd.First_Name
pd.Middle_Name
pd.birthdate
pd.emplid  
pd.sex
pd.address1
pd.address2
pd.city
pd.state
pd.postal  

j.emplid
j.empl_rcd
j.effdt
j.action
j.action_Reason
j.company
j.deptid
j.empl_status
j.full_part_time
j.union_cd

  Let $Benefit_Program = &bpp.benefit_program

  Let $Health-Benefit-Row-Found = 'N'

  Do Health-Benefit

  If ($Health-Benefit-Row-Found = 'N') Or       !If not enrolled in ben pgm or election is termed
     (&hb.coverage_elect = 'T')
    	Let $Data-Found-Flag = 'Y'

    	Let #MTD-Total-Hours    = 0
    	Let #YTD-Total-Hours    = 0
    	Let #MTD-Vacation-Hours = 0
    	Let #MTD-Hours          = 0

    	If Substr($Benefit_Program,1,1) <> 'C'
      		Let $spcl_balance = '''Y'''
      		Let $erncd = '(''WK2'')'
      		Do Get-Hours
      		Let #MTD-Total-Hours = #hrs_mtd
      		Let #YTD-Total-Hours = #hrs_ytd

      		Let $spcl_balance = '''N'''
      		Let $erncd = '(''513'',''515'')'
      		Do Get-Hours
      		Let #MTD-Vacation-Hours = #hrs_mtd
      		Let #MTD-Hours = #MTD-Total-Hours - #MTD-Vacation-Hours
    	End-If

    	Do Format-Record
      do Get-Emp-Ssn (&pd.emplid, $Ssn_Out) !GEX_SEC_REENG 2015-04-29 Vahini Katta
    	Do Print-Record
    	Do Write-Record

    	Add #MTD-Hours           To #BP-MTD-Hours
    	Add #MTD-Vacation-Hours  To #BP-MTD-Vacation-Hours
    	Add #MTD-Total-Hours     To #BP-MTD-Total-Hours
  End-If

From   ps_personal_data          pd,
       ps_employment             e,
       ps_job                    j,
       ps_ben_prog_partic        bpp

Where [$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA]

And   [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]

And    bpp.effdt                 =
      (Select Max(effdt)
       From   ps_ben_prog_partic
       Where  emplid             = bpp.emplid
       And    empl_rcd           = bpp.empl_rcd
       And    cobra_event_id     = bpp.cobra_event_id
       And    effdt             <= $pay_end_dt)

And    j.emplid                  = bpp.emplid
And    j.empl_rcd                = bpp.empl_rcd
And    j.empl_status             in ('A', 'L', 'P', 'S')     

And    pd.emplid                 = bpp.emplid

And    e.emplid                  = bpp.emplid
And    e.empl_rcd                = bpp.empl_rcd

And    j.effdt                   =
      (Select Max(effdt)
       From   ps_job
       Where  emplid             = bpp.emplid
       And    empl_rcd           = bpp.empl_rcd
       And    effdt             <= $pay_end_dt)

And    j.effseq                  =
      (Select Max(effseq)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd           = j.empl_rcd
       And    effdt              = j.effdt)

Order By bpp.benefit_program, pd.name
End-Select
End-Procedure

!***********************************************************************
Begin-Procedure Health-Benefit
!***********************************************************************
#debug9 Show 'Health-Benefit'

Begin-Select
hb.coverage_elect

  Let $Health-Benefit-Row-Found = 'Y'

From   ps_health_benefit hb

Where  hb.emplid              = &bpp.emplid
And    hb.empl_rcd            = &bpp.empl_rcd
And    hb.cobra_event_id      = &bpp.cobra_event_id
and    hb.plan_type = '1X'					! DXS, 2/14, added during upgrade 9.0 system testing
And    hb.coverage_begin_dt   =
      (Select Max(coverage_begin_dt)
       From   ps_health_benefit hb1
       Where  hb1.emplid          = hb.emplid
       And    hb1.empl_rcd        = hb.empl_rcd
       And    hb1.cobra_event_id  = hb.cobra_event_id
       And    hb1.plan_type       = hb.plan_type
       And    hb1.benefit_nbr     = hb.benefit_nbr
       And    hb1.effdt          <= $pay_end_dt)
End-Select
End-Procedure

!***********************************************************************
Begin-Procedure Get-Benefit-Program-Name
!***********************************************************************
#debug9 Show 'Get-Benefit-Program-Name'

Begin-Select
bdp.descr

  Let $BenefitProgramDescr = &bdp.descr

From   ps_ben_defn_pgm   bdp

Where  bdp.benefit_program    = $benefit_program

And    bdp.effdt              =
      (Select Max(effdt)
       From   ps_ben_defn_pgm
       Where  benefit_program = bdp.benefit_program
       And    effdt          <= $pay_end_dt)
End-Select
End-Procedure

!***********************************************************************
Begin-Procedure Get-Hours
!***********************************************************************
#debug9 Show 'Get-Hours'
  Let #hrs_ytd = 0
  Let #hrs_mtd = 0

Begin-Select
eb.balance_period
eb.hrs_mtd
eb.hrs_ytd

  Add &eb.hrs_ytd To #hrs_ytd

  If &eb.balance_period = #balance_period
    Add &eb.hrs_mtd To #hrs_mtd
  End-If

From   ps_earnings_bal eb

Where  eb.emplid              = &j.emplid
and    eb.empl_rcd            = &j.empl_rcd	
And    eb.company             = &j.company
And    eb.balance_id          = 'CY'
And    eb.balance_year        = #balance_year

And    eb.balance_period      =
      (Select Max(balance_period)
       From   ps_earnings_bal
       Where  emplid          = eb.emplid
       and    empl_rcd        = eb.empl_rcd	
       And    company         = eb.company
       And    balance_id      = eb.balance_id
       And    balance_year    = eb.balance_year
       And    balance_period <= #balance_period
       And    spcl_balance    = eb.spcl_balance
       And    erncd           = eb.erncd)

And    eb.spcl_balance        = [$spcl_balance]
And    eb.erncd              In [$erncd]
End-Select
End-Procedure

!***********************************************************************
Begin-Procedure Format-Record
!***********************************************************************
#debug9 Show 'Format-Record'

  Let $Middle_Init = substr(&pd.Middle_Name,1,1)
  Let $marital = 'S'

  Do Format-DateTime(&e.service_dt, $Service_Dt, {DEFMDY}, '', '')
  Do Format-DateTime(&pd.birthdate, $Birth_Dt,   {DEFMDY}, '', '')
  Do Format-DateTime(&j.effdt,      $eff_Dt,     {DEFMDY}, '', '')

  Do Format-DateTime(&e.service_dt, $ServiceDt, {DEFCMP}, '', '')

  Do Format-DateTime(&pd.birthdate, $BirthDt,   {DEFCMP}, '', '')

  Do Format-DateTime(&e.termination_dt, $TermDt, {DEFCMP}, '', '')

  let $percovcym =
        datetostr(strtodate($pay_end_dt,'DD-MON-YYYY'),'YYYYMM')

  Move #MTD-Hours           To #Pension_MTD_Hours
  Move #MTD-Vacation-Hours  To #Pension_MTD_Vacation_Hours
  Move #MTD-Total-Hours     To #Pension_MTD_Total_Hours
  Move #YTD-Total-Hours     To #Pension_YTD_Total_Hours

  !Move #Pension_MTD_Total_Hours To $string
  !Unstring $string By '.' Into $string_int $string_after_Dec
  !Move $string_int To $string_int_ed 09999
  !Let $Pension_MTD_Total_Hours = $string_int_ed || Substr($string_after_Dec,1,2)
  !If Substr($Pension_MTD_Total_Hours,1,1) != '-'
  !  Let $Pension_MTD_Total_Hours = '+' || Substr($Pension_MTD_Total_Hours,2,6)
  !End-If

  let $Pension_MTD_Hrs = ltrim(edit(#Pension_MTD_Total_Hours,'0999.99'), ' ')

  

End-Procedure

!***********************************************************************
Begin-Procedure Print-Record
!***********************************************************************
#debug9 Show 'Print-Record'

  Add 1 To #BP-Recs-Printed
  Add 1 to #Total-Recs-Printed                        

  Print &pd.name                   (+1,{#column1},30)
  !Print &pd.emplid                 (+0,{#column2}) !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $Ssn_Out                   (+0,{#column2})  !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $Service_Dt                (+0,{#column3})
  Print $Birth_Dt                  (+0,{#column4})
  Print &j.union_cd                (+0,{#column5}) 
  Print &J.full_part_time          (+0,{#column6})
  Print &j.empl_status             (+0,{#column7})
  Print &j.action                  (+0,{#column8})
  Print &j.Action_Reason           (+0,{#column9})
  Print &j.effdt                   (+0,{#column10})
  Print #MTD-Hours                 (+0,{#column11})  Edit 999,999,999.99
  Print #MTD-Vacation-Hours        (+0,{#column12})  Edit 999,999,999.99
  Print #MTD-Total-Hours           (+0,{#column13})  Edit 999,999,999.99
End-Procedure

!***********************************************************************
Begin-Procedure Write-Record
!***********************************************************************
#debug9 Show 'Write-Record'

   Add 1 To #recs-written

   write 1 from $Ssn_Out                ! 1 - 9 !GEX_SEC_REENG 2015-04-29 Vahini Katta
                !&pd.emplid:9		        ! 1 - 9 !GEX_SEC_REENG 2015-04-29 Vahini Katta
                $Benefit_Program:3	    ! 10 - 12
                $Benefit_Plan:6		      ! 13 - 18  Blank
                &j.deptid:5		          ! 19 - 23
	              &pd.Last_Name:20	      ! 24 - 43
                &pd.First_Name:15	      ! 44 - 58
                $Middle_Init:1		      ! 59 - 59
                $Units_Contrib:7        ! 60 - 66  Blank
                $Rate:7			            ! 67 - 73  Blank
		            $Amount:7		            ! 74 - 80  Blank
		            &pd.address1:25		      ! 81 - 105
		            &pd.address2:25		      ! 106 - 130
                &pd.city:17		          ! 131 - 147
                &pd.state:2		          ! 148 - 149
                &pd.postal:9		        ! 150 - 158
		            &j.union_cd:10		      ! 159 - 168
                $marital:1       	      ! 169 - 169
		            &pd.sex:1		            ! 170 - 170
		            $BirthDt:8		          ! 171 - 178 
	              $ServiceDt:8		        ! 179 - 186
		            $TermDt:8		            ! 187 - 194 Blank
		            $percovcym:6		        ! 195 - 200
 		            $Pension_MTD_Hrs:7      ! 201 - 207 Blank

   Status=#Filestat

   If #Filestat != 0
      Display 'Error Writing output file.  Program terminating.'
      Stop
   End-If
End-Procedure

!***********************************************************************
Begin-Procedure After-BP-Break
!***********************************************************************
#debug9 Show 'After-BP-Break'

  If #BP-Recs-Printed > 0
    Print 'Benefit Program'          (+3,{#column2})
    Print $Save_Benefit_Program      (0,+1)
    Print 'Totals'                   (0,+1)

    Print #BP-MTD-Hours              (+0,{#column11})   Edit 999,999,999.99
    Print #BP-MTD-Vacation-Hours     (+0,{#column12})   Edit 999,999,999.99
    Print #BP-MTD-Total-Hours        (+0,{#column13})   Edit 999,999,999.99

    Let #BP-MTD-Hours           = 0
    Let #BP-MTD-Vacation-Hours  = 0
    Let #BP-MTD-Total-Hours     = 0

    New-Page
    Let #BP-Recs-Printed = 0
  End-If
End-Procedure

!***********************************************************************
Begin-Procedure Finish
!***********************************************************************
#debug9 Show 'Finish'

  !Close File
  Close 1
  Do Display-File-Totals
  Do Print-Report-Totals

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  
  Do Call_MovIt_Commands($Prcs_OPRID,$PRCS_RUN_CNTL_ID,$ReportID,$File1, $comments ) !TCSXSSS Surya added the moveit folder changes - gexmovit.sqc
End-Procedure

!***********************************************************************
Begin-Procedure Display-File-Totals
!***********************************************************************
#debug9 Show 'Display-File-Totals'

  Move #recs-written To $recs-written   888,888,888

  Display ''
  Display $recs-written          Noline
  Display ' records written to ' Noline
  Display $File1
  Display ''
End-Procedure

!***********************************************************************
Begin-Procedure Print-Report-Totals
!***********************************************************************
#debug9 Show 'Print-Report-Totals'

  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  else                                                            
      print 'Grand Total Records:'  (+2,{#column2}                
      print #Total-Recs-Printed	    (+0,{#column4}) Edit 999,999     
  End-If
End-Procedure

#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#include 'gexxx912.sqc'  !Get plan type multiple row table
#include 'gexxx911.sqc'  !Get benefit program multiple row table
#include 'gexxx913.sqc'  !Get union code multiple row table
#include 'gexxx951.sqc'  !Oracle dbase name  
#include 'gexmovit.sqc'  !Move the files to MoveIt Folder 
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta
