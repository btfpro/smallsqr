!***********************************************************************
!  GEXPL600:  Local 23 Legal Aid Interface                             *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL600:             Local 23 Legal Aid Interface                   *
!                                                                      *
! Narrative:		This program creates a monthly report for      *
!                       Local 23 and a file that contains employee     *
!                       information required by the union to           *
!                       administer the Legal Aid benefit.              *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			#debug8 key variable values                    *
!			#debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:		pay_calendar                                   *
!                       ben_defn_optn                                  *
!                       ben_defn_pgm                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       benef_plan_tbl                                 *
!                       pay_line                                       *
!                       pay_check                                      *
!                       pay_deduction                                  *
!                       gex_hrs_bal                                    *
!                       health_benefit                                 *
!                                                                      *
! Written by:		Jim Hutchison                                  *
!                                                                      *
! Normally Run:		Monthly                                        *
!                                                                      *
! Control Parms:	FOR WHERE CLAUSE:                              *
!                         Plan Type(s)                                 *
!                         Vendor ID(s)                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  JDH          01/20/99     Initial Creation.                         *
!  JDH          02/26/99     Add descending sort on effseq.            *
!                            Performance Tuning.                       *
!  JDH          03/01/99     Added missing line to select.             *
!  JDH          03/12/99     Fixed problem with gex_hrs_bal select.    *
!  JDH          04/13/99     Get the effdt, action, action_reason from *
!                             the job row that originally set the      *
!                             empl_status to its current value for     *
!                             empl_status in ('L','P','R','T').        *
!  JDH          06/29/99     Changed logic to get action,              *
!                             action_reason, effdt.                    *
!  SXK          11/23/99     Changed ssn to emplid                     *
!  SXK          11/29/99     Changed from zip to postal                *
!  GBD          09/12/02     v8.3 Upgrade - Provider field changed to  *
!                            Vendor ID.                                *
!  GBD          10/16/02     Modified code to eliminate multiple select*
! ISDVASS	03/26/08     Upgrade 9.0 Changes                       *
! GEX-TLL	04/16/08     Upgrade 9.0 Changes		       *
! VENDTKK       02/05/09     Added INCLUDE GETPOSNM.SQC to pull        *
!                  	           JOBTITLE from POSITION_DATA.DESCR   *
!				               field instead of JOBCODE_TBL.DESCR  *
!			           	                               *
! TCSXKWK       04/06/15     New changes for printing in excel format and 
!                            add new field vendor id.
! TCSXMZB        02/08/16    Combining unix and NT files and retrieving the*
!                            data accordingly.
!***********************************************************************

#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

#define #unix_column1     1!GEXPL customization start Mahesh babu 02/08/16
#define #unix_column2    33
#define #unix_column3    44
#define #unix_column4    50
#define #unix_column5    60
#define #unix_column6    70
#define #unix_column7   103 
#define #unix_column8   110
#define #unix_column9   118
#define #unix_column10  126
#define #unix_column11  136
#define #unix_column12  147
#define #unix_column13  161

#define #NT_column1     1
#define #NT_column2    31
#define #NT_column3    42
#define #NT_column4    53
#define #NT_column5    63
#define #NT_column6    73
#define #NT_column7    83
#define #NT_column8   113 
#define #NT_column9   115
#define #NT_column10  121
#define #NT_column11  130
#define #NT_column12  140
#define #NT_column13  150
#define #NT_column14  161!GEXPL customization end Mahesh babu 02/08/16

Begin-Heading 8
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'

!  Print 'Provider:'            (3,1)
  Print 'VendorID:'            (3,1)
  Print $Provider              (3,13)

  Print $Reporting_Period      (3,) Center 
  Print $BenefitProgramDescr   (4,) Center

#ifdef UNIX !GEXPL customization start Mahesh babu 02/08/16
  Print 'SERVICE'              (+2,{#unix_column4})
  Print 'BIRTH'                (+0,{#unix_column5})
  Print 'JOB'                  (+0,{#unix_column6})
  Print 'EMPL'                 (+0,{#unix_column7})
  Print 'ACTION'               (+0,{#unix_column9})
  Print 'EFF'                  (+0,{#unix_column10})
  Print 'BENEFIT'              (+0,{#unix_column11})
  Print '   MTD LEGAL'         (+0,{#unix_column12})
  Print '     LEGAL AID'       (+0,{#unix_column13})
  Print 'EMPLOYEE NAME'        (+1,{#unix_column1})
  Print 'SSN'                  (+0,{#unix_column2})
  Print 'DEPT'                 (+0,{#unix_column3})
  Print 'DATE'                 (+0,{#unix_column4})
  Print 'DATE'                 (+0,{#unix_column5})
  Print 'CODE'                 (+0,{#unix_column6})
  Print 'STAT'                 (+0,{#unix_column7})
  Print 'ACTION'               (+0,{#unix_column8})
  Print 'REASON'               (+0,{#unix_column9})
  Print 'DATE'                 (+0,{#unix_column10})
  Print 'TERM DATE'            (+0,{#unix_column11})
  Print '       HOURS'         (+0,{#unix_column12})
  Print '        AMOUNT'       (+0,{#unix_column13})
  Print '-'                    (+1,{#unix_column1},30)  Fill
  Print '-'                    (+0,{#unix_column2},09)  Fill
  Print '-'                    (+0,{#unix_column3},04)  Fill
  Print '-'                    (+0,{#unix_column4},08)  Fill
  Print '-'                    (+0,{#unix_column5},08)  Fill
  Print '-'                    (+0,{#unix_column6},30)  Fill
  Print '-'                    (+0,{#unix_column7},04)  Fill
  Print '-'                    (+0,{#unix_column8},06)  Fill
  Print '-'                    (+0,{#unix_column9},06)  Fill
  Print '-'                    (+0,{#unix_column10},08) Fill
  Print '-'                    (+0,{#unix_column11},09) Fill
  Print '-'                    (+0,{#unix_column12},12) Fill
  Print '-'                    (+0,{#unix_column13},14) Fill
#endif !GEXPL customization end Mahesh babu 02/08/16

#ifdef NT !GEXPL customization start Mahesh babu 02/08/16
  Print 'SERVICE'              (+2,{#NT_column5})
  Print 'BIRTH'                (+0,{#NT_column6})
  Print 'JOB'                  (+0,{#NT_column7})
  Print 'EMPL'                 (+0,{#NT_column8})
  Print 'ACTION'               (+0,{#NT_column10})
  Print 'EFF'                  (+0,{#NT_column11})
  Print 'BENEFIT'              (+0,{#NT_column12})
  Print '   MTD LEGAL'         (+0,{#NT_column13})
  Print '     LEGAL AID'       (+0,{#NT_column14})  ! TCSXKWK       04/06/15     New changes MOD Ends
  Print 'EMPLOYEE NAME'        (+1,{#NT_column1})
  Print 'SSN'                  (+0,{#NT_column2})
  Print 'VENDOR ID'            (+0,{#NT_column3})
  Print 'DEPT'                 (+0,{#NT_column4})
  Print 'DATE'                 (+0,{#NT_column5})
  Print 'DATE'                 (+0,{#NT_column6})
  Print 'CODE'                 (+0,{#NT_column7})
  Print 'STAT'                 (+0,{#NT_column8})
  Print 'ACTION REASON'        (+0,{#NT_column9})! TCSXKWK       04/06/15     New changes MOD Ends
  Print 'DATE'                 (+0,{#NT_column11})
  Print 'TERM DATE'            (+0,{#NT_column12})
  Print '       HOURS'         (+0,{#NT_column13})
  Print '        AMOUNT'       (+0,{#NT_column14})
  Print '-'                    (+2,{#NT_column1},30)  Fill! TCSXKWK       04/06/15     New changes MOD Ends
  Print '-'                    (+0,{#NT_column2},09)  Fill
  Print '-'                    (+0,{#NT_column3},10)  Fill
  Print '-'                    (+0,{#NT_column4},04)  Fill! TCSXKWK       04/06/15     New changes MOD Ends
  Print '-'                    (+0,{#NT_column5},08)  Fill
  Print '-'                    (+0,{#NT_column6},08)  Fill
  Print '-'                    (+0,{#NT_column7},30)  Fill
  Print '-'                    (+0,{#NT_column8},04)  Fill! TCSXKWK       04/06/15     New changes MOD Ends
  Print '-'                    (+0,{#NT_column9},06)  Fill
  Print '-'                    (+0,{#NT_column10},06)  Fill
  Print '-'                    (+0,{#NT_column11},08) Fill
  Print '-'                    (+0,{#NT_column12},09) Fill
  Print '-'                    (+0,{#NT_column13},12) Fill
  Print '-'                    (+0,{#NT_column14},14) Fill!TCSXKWK       04/06/15     New changes MOD Ends
#endif !GEXPL customization end Mahesh babu 02/08/16

End-Heading

Begin-Report
#debug9 Show 'Begin-Report'

!  Do Set-Optimizer-Goal					! ISDVASS Upgrade 9.0 Changes

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc
  Do P100-Start
  Do P200-Main-Process
  Do P300-Finish
  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report

Begin-Procedure Set-Optimizer-Goal
  Begin-SQL
    Alter Session Set OPTIMIZER_GOAL=RULE;
  End-SQL
End-Procedure

Begin-Procedure P100-Start
#debug9 Show 'P100-Start'

  Move 'GEXPL600'                     To $ReportID
  Move 'LOCAL 23 LEGAL AID INTERFACE' To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  If $prcs_process_instance = ''

    Move '''' to $ProviderString
    Display 'Enter Vendor ID or leave blank to exit.'
    While 1=1
      Input $Provider maxlen=6 'Provider ID'
      if RTRIM($Provider, ' ') = ''
        concat '''' with $ProviderString
        break
      end-if
      if $ProviderString <> ''''
        concat ''',''' with $ProviderString
      end-if
      concat $Provider with $ProviderString
    End-While

    Let $Vendor_ID = $Provider

    if $ProviderString = ''''''
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'ALL'
    else
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = 'bpt.vendor_id In (' || $ProviderString || ')'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
    end-if

    Move '''' to $PlanTypeString
    Display 'Enter Plan Type or leave blank to exit.'
    While 1=1
      Input $PlanType Maxlen=2 'Plan Type'
      Uppercase $PlanType
      if RTRIM($PlanType, ' ') = ''
        concat '''' with $PlanTypeString
        break
      end-if
      if $PlanTypeString <> ''''
        concat ''',''' with $PlanTypeString
      end-if
      concat $PlanType with $PlanTypeString
    End-While

    if $PlanTypeString = ''''''
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
    else
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = 'bpt.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    end-if

  Else

    Let $GEXXX912_Plan_Type_Alias = 'bpt.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters

    Let $GEXXX927_Vendor_ID_Alias = 'bpt.vendor_id'
    Do GEXXX927-Select-Vendor-ID-Parameters

  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  If $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
    Display 'At least one plan type parameter must be specified'
    Stop
  End-If

  If $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
    Display 'At least one vendor id parameter must be specified'
    Stop
  End-If

  Let $X000_ORDER_BY = 'Vendor ID, Benefit Plan, Employee Name'

  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA  = '   $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA
  Show '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA      = '   $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA

  Do Get-Calendar-Year-Id    !getbalid.sqc

  Do P140-Get-Last-Confirmed-Pay
  Do P150-Print-Cover-Page
  Do P160-Open-File
End-Procedure

Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

Begin-Select Loops=1
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'fmMonth YYYY')           &period
Last_Day(pay_end_dt)                         &current_month_end
Last_Day(Add_Months(pay_end_dt,-1))          &prev_month_end
Last_Day(Add_Months(pay_end_dt,1))           &next_month_end

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period

  Let $current_month_begin = '01-'||Substr($pay_end_dt,4,8)
  Let $current_month_end   = &current_month_end
  Let $next_month_end      = &next_month_end
  Let $prev_month_begin    = '01-'||Substr(&prev_month_end,4,8)

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  Show 'Report will contain balances for the month of ' $Reporting_Period
  Show 'Last confirmed pay end date:  ' $pay_end_dt

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_calendar

Where  pay_end_dt         =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt <= $AsOfToday
       And    pay_confirm_run = 'Y')

And    pay_confirm_run = 'Y'
End-Select
End-Procedure

begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+2,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,2)
  Print '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = '  (+2,5)
  Print $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA    = '  (+2,5)
  Print $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA          (0,+2)

  Print 'Report will contain balances for the month of'       (+2,5)
  Print $Reporting_Period                                     (0,+1)
  Print 'Last confirmed pay end date:'                        (+2,5)
  Print $pay_end_dt                                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
End-Procedure

Begin-Procedure P160-Open-File
#debug9 Show 'P160-Open-File'

#ifdef UNIX !GEXPL customization start Mahesh babu 02/08/16

  Let $File1 = '{OUTFILE}' || Lower($ReportID) || '.dat'  
  Open $File1
    As 1
    For-Writing
    Record=222:Fixed
#endif !GEXPL customization end Mahesh babu 02/08/16
#ifdef NT !GEXPL customization start Mahesh babu 02/08/16
  !Let $File1 = '{OUTFILE}' || Lower($ReportID) || '.dat'  ! TCSXKWK       04/06/15     New changes MOD start
 !Let $File1 = '\\corp.gianteagle.com\peoplesoft\PSHR\Kokila\'|| Lower($ReportID)  || '.xls'! TCSXKWK       04/06/15     New changes MOD Ends
 Let $File1 = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\GEXPL600\'|| Lower($ReportID)  || '.xls'! TCSXKWK       04/06/15  New changes MOD Ends

  Open $File1
    As 1
    For-Writing

    Record=160:Fixed
#endif !GEXPL customization end Mahesh babu 02/08/16

    Status=#Filestat

  If #Filestat != 0
    Display 'Error Opening output file.  Program terminating.'
    Stop
  End-If

#ifdef NT !GEXPL customization start Mahesh babu 02/08/16
  Do P269-Write-Record ! TCSXKWK       04/06/15     New changes MOD
#endif !GEXPL customization end Mahesh babu 02/08/16
End-Procedure

Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
 
  Let $Data-Found-Flag = 'N'

Begin-Select
bpt.vendor_id     ()     On-Break   Level=1
                                    Print=Never
                                    Save=$Save_Vendor_ID
                                    After=P290-After-Vendor-ID-Break

bpt.benefit_plan  ()     On-Break   Level=2
                                    Print=Never
                                    Save=$Save_Benefit_Plan
                                    Before=P280-Before-BP-Break

bpt.plan_type
e.service_dt
per.name
per.birthdate
per.emplid !SXK 11/23/1999 Changed ssn to emplid
per.sex
per.address1
per.address2
per.city
per.state
per.postal
j.company
j.paygroup
j.emplid
j.empl_rcd
j.effdt
j.deptid
j.jobcode
j.empl_status
j.reg_temp
j.full_part_time
j.gex_volun_low_hrs
j.Position_nbr          !VENDTKK    ITG#56786   5-Feb-2009
Sum(pd.ded_cur) &pd.ded_cur

  Let $Data-Found-Flag = 'Y'

  Let $Provider  = &bpt.vendor_id
  Let $Vendor_ID = &bpt.vendor_id

  Let #grs_mtd = &pd.ded_cur

  Do P220-Get-Benefit-Termination-Row

  Evaluate &j.empl_status
    When = 'L'
    When = 'P'
    When = 'R'
    When = 'T'
      Do P230-Get-Leave-Begin-Date
      Break
    When-Other
      Do P235-Get-Status-Date
  End-Evaluate

  Do P240-Get-MTD-Legal-Hours
  Do P250-Format-Record
  Do P260-Print-Record
  Do P270-Write-Record

  Add #hrs_mtd To #Vendor-ID-Legal-Hours
  Add #grs_mtd To #Vendor-ID-Legal-Amount

From   ps_personal_data          per,
       ps_employment             e,
       ps_job                    j,
       ps_benef_plan_tbl         bpt,
       ps_pay_cal_bal_id         pcbi,
       ps_pay_check              pc,
       ps_pay_deduction          pd

Where  per.emplid                = j.emplid

And    e.emplid                  = j.emplid
And    e.empl_rcd               = j.empl_rcd

And    j.effdt                   =
      (Select Max(effdt)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt             <= $pay_end_dt)

And    j.effseq                  =
      (Select Max(effseq)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt              = j.effdt)

And    bpt.effdt                 =
      (Select Max(effdt)
       From   ps_benef_plan_tbl
       Where  plan_type          = bpt.plan_type
       And    benefit_plan       = bpt.benefit_plan
       And    effdt             <= $pay_end_dt)

And    pcbi.company              = pc.company
And    pcbi.paygroup             = pc.paygroup
And    pcbi.balance_id           = 'CY'
And    pcbi.balance_year         = #balance_year
And    pcbi.balance_period       = #balance_period

And    pc.emplid                 = j.emplid
And    pc.empl_rcd              = j.empl_rcd

And    pc.pay_end_dt             = pcbi.pay_end_dt

And    pd.company                = pc.company
And    pd.paygroup               = pc.paygroup
And    pd.pay_end_dt             = pc.pay_end_dt
And    pd.off_cycle              = pc.off_cycle
And    pd.page_num                  = pc.page_num
And    pd.line_num                  = pc.line_num
And    pd.sepchk                 = pc.sepchk

And    pd.plan_type              = bpt.plan_type
And    pd.benefit_plan           = bpt.benefit_plan
And    pd.ded_class              = 'N'

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA]
And   [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA]

Group By bpt.vendor_id,
       bpt.benefit_plan,
       bpt.plan_type,
       e.service_dt,
       per.name,
       per.birthdate,
       per.emplid,  !SXK 11/23/1999 Changed ssn to emplid
       per.sex,
       per.address1,
       per.address2,
       per.city,
       per.state,
       per.postal,
       j.company,
       j.paygroup,
       j.emplid,
       j.empl_rcd,
       j.effdt,
       j.deptid,
       j.jobcode,
       j.empl_status,
       j.reg_temp,
       j.full_part_time,
       j.gex_volun_low_hrs,
       j.Position_nbr          !VENDTKK    ITG#56786   5-Feb-2009
Order By bpt.vendor_id,
       bpt.benefit_plan,
       per.name

End-Select
End-Procedure

Begin-Procedure P210-Get-Benefit-Program-Name
#debug9 Show 'P210-Get-Benefit-Program-Name'

Begin-Select
bdp.benefit_program
bdp.descr

  Let $BenefitProgramDescr = &bdp.descr

From   ps_ben_defn_pgm   bdp,
       ps_ben_defn_optn  bdo

Where  bdp.benefit_program    = bdo.benefit_program

And    bdp.effdt              =
      (Select Max(effdt)
       From   ps_ben_defn_pgm
       Where  benefit_program = bdp.benefit_program
       And    effdt          <= $pay_end_dt)

!GBD 10/16/2002 Begin
And    bdo.benefit_program    = bdp.benefit_program
And    bdo.effdt              = bdp.effdt
And    bdo.plan_type          = &bpt.plan_type
And    bdo.benefit_plan       = &bpt.benefit_plan

!And    bdo.effdt              =
!      (Select Max(effdt)
!       From   ps_ben_defn_optn
!       Where  benefit_program = bdo.benefit_program
!       And    effdt          <= $pay_end_dt
!       And    plan_type       = bdo.plan_type
!       And    option_id       = bdo.option_id)
!GBD 10/16/2002 End
End-Select
End-Procedure

Begin-Procedure P220-Get-Benefit-Termination-Row
#debug9 Show 'P220-Get-Benefit-Termination-Row'
Begin-Select
hb1.coverage_begin_dt

From   ps_health_benefit hb1

Where  hb1.emplid         = &j.emplid
And    hb1.empl_rcd      = &j.empl_rcd
And    hb1.plan_type      = &bpt.plan_type

And    hb1.effdt          =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid      = &j.emplid
       And    empl_rcd   = &j.empl_rcd
       And    plan_type   = &bpt.plan_type)

And    hb1.coverage_begin_dt Between $prev_month_begin
                                 And $next_month_end

And    hb1.coverage_elect = 'T'
End-Select
End-Procedure


Begin-Procedure P230-Get-Leave-Begin-Date
#debug9 Show 'P230-Get-Leave-Begin-Date'
  Let $Status-Change = 'N'
Begin-Select
JOB.EFFDT
JOB.EMPL_STATUS
JOB.ACTION
JOB.ACTION_REASON

  If &JOB.EMPL_STATUS = &J.EMPL_STATUS And
      $Status-Change = 'N'
    Let $Effective-Date = &JOB.EFFDT
    Let $Action         = &JOB.ACTION
    Let $Action_Reason  = &JOB.ACTION_REASON
    Let $Status-Change = 'N'
  Else
    Let $Status-Change = 'Y'
  End-If

FROM   PS_JOB JOB

WHERE  EMPLID     = &J.EMPLID
AND    EMPL_RCD  = &J.EMPL_RCD
AND    EFFDT     <= &J.EFFDT
ORDER BY JOB.EFFDT DESC, JOB.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P235-Get-Status-Date
#debug9 Show 'P235-Get-Status-Date'
  Let $Reg-Temp-Change = 'N'
Begin-Select
J1.EFFDT
J1.REG_TEMP
J1.ACTION
J1.ACTION_REASON
J1.EMPL_STATUS

  If &J1.REG_TEMP = &J.REG_TEMP And
      $Reg-Temp-Change = 'N'
    Let $Reg-Temp-Change = 'N'
    If &J1.EMPL_STATUS <> 'T'
      Let $Effective-Date = &J1.EFFDT
      Let $Action         = &J1.ACTION
      Let $Action_Reason  = &J1.ACTION_REASON
    End-If
  Else
    Let $Reg-Temp-Change = 'Y'
  End-If

FROM   PS_JOB J1

WHERE  EMPLID     = &J.EMPLID
AND    EMPL_RCD  = &J.EMPL_RCD
AND    EFFDT     <= &J.EFFDT
ORDER BY J1.EFFDT DESC, J1.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P240-Get-MTD-Legal-Hours
#debug9 Show 'P240-Get-MTD-Legal-Hours'

  Let #hrs_mtd = 0

Begin-Select
Sum(ghb.total_hrs) &ghb.total_hrs

  Let #hrs_mtd = &ghb.total_hrs

From   ps_gex_hrs_bal ghb

!Where ghb.company            = &j.company
!And   ghb.paygroup           = &j.paygroup
!And   ghb.emplid             = &j.emplid
Where  ghb.emplid             = &j.emplid
And    ghb.empl_rcd          = &j.empl_rcd
And    ghb.plan_type          = &bpt.plan_type
And    ghb.benefit_plan       = &bpt.benefit_plan
And    ghb.check_dt     Between $current_month_begin
                            And $current_month_end
End-Select
End-Procedure


Begin-Procedure P250-Format-Record
#debug9 Show 'P250-Format-Record'

  Let $JobCode = &j.jobcode
  Do Get-Job-Title

  !VENDTKK Begin   ITG#56786   5-Feb-2009
 
  Let $Position_Nbr = &j.Position_nbr
  Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosName = '' 
  		Let $jobtitle = $jobtitle
	 Else
 		Let $jobtitle = $PosName
	 End-if
 !VENDTKK End   ITG#56786   5-Feb-2009	

  If &j.gex_volun_low_hrs = 'Y'
    Let $gex_volun_low_hrs = 'Y'
  Else
    Let $gex_volun_low_hrs = ''
  End-If

  Let $name     = &per.name
  Let $address1 = &per.address1
  Let $address2 = &per.address2
  Let $city     = &per.city
  Let $state    = &per.state

  Let $vendor   =&bpt.vendor_id 

  Uppercase $name
  Uppercase $address1
  Uppercase $address2
  Uppercase $city
  Uppercase $state

  Do Format-DateTime(&e.service_dt, $Service_Dt, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime(&e.service_dt, $ServiceDt, {DEFYMD}, '', '')

  Do Format-DateTime(&per.birthdate, $Birth_Date, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime(&per.birthdate, $Birthdate, {DEFYMD}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime($Effective-Date, $effdt, {DEFYMD}, '', '')

  Do Format-DateTime($Effective-Date, $eff_dt, {DEFMDY}, '', '')

  Do Format-DateTime(&hb1.coverage_begin_dt, $Term_Date, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime(&hb1.coverage_begin_dt, $TermDate, {DEFYMD}, '', '')

  Move #hrs_mtd To #Legal_Hours
  Move #grs_mtd To #Legal_Contribution

  Move #Legal_Hours To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 09999
  Let $Legal_Hours = $string_int_ed || Substr($string_after_Dec,1,2)

  If Substr($Legal_Hours,1,1) != '-'
    Let $Legal_Hours = '+' || Substr($Legal_Hours,2,6)
  End-If

  Move #Legal_Contribution To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 099999999
  Let $Legal_Contribution = $string_int_ed || Substr($string_after_Dec,1,2)

  If Substr($Legal_Contribution,1,1) != '-'
    Let $Legal_Contribution = '+' || Substr($Legal_Contribution,2,10)
  End-If
End-Procedure


Begin-Procedure P260-Print-Record
#debug9 Show 'P260-Print-Record'
  Do Get-Emp-Ssn (&per.emplid, $Ssn_Out)  		!GEX-TLL 04/16/08 Added
#ifdef UNIX !GEXPL customization start Mahesh babu 02/08/16
  Print &per.name                  (+1,{#unix_column1},30)
  Print $Ssn_Out		           (+0,{#unix_column2})	!GEX-TLL 04/16/08 Added	
  Print &j.deptid                  (+0,{#unix_column3})
  Print $Service_Dt                (+0,{#unix_column4})
  Print $Birth_Date                (+0,{#unix_column5})
  Print $JobTitle                  (+0,{#unix_column6})
  Print &j.empl_status             (+0,{#unix_column7})
  Print $Action                    (+0,{#unix_column8})
  Print $Action_Reason             (+0,{#unix_column9})
  Print $eff_dt                    (+0,{#unix_column10})
  Print $Term_Date                 (+0,{#unix_column11})
  Print #Legal_Hours               (+0,{#unix_column12})  Edit 9,999,999.99
  Print #grs_mtd                   (+0,{#unix_column13})  Edit 999,999,999.99
#endif !GEXPL customization end Mahesh babu 02/08/16
#ifdef NT !GEXPL customization start Mahesh babu 02/08/16
  Print &per.name                  (+1,{#NT_column1},30)
  Print $Ssn_Out		           (+0,{#NT_column2})	!GEX-TLL 04/16/08 Added	
  Print $Vendor_ID                 (+0,{#NT_column3})
  Print &j.deptid                  (+0,{#NT_column4})
  Print $Service_Dt                (+0,{#NT_column5})
  Print $Birth_Date                (+0,{#NT_column6})
  Print $JobTitle                  (+0,{#NT_column7})
  Print &j.empl_status             (+0,{#NT_column8})
  Print $Action                    (+0,{#NT_column9})
  Print $Action_Reason             (+0,{#NT_column10})
  Print $eff_dt                    (+0,{#NT_column11})
  Print $Term_Date                 (+0,{#NT_column12})
  Print #Legal_Hours               (+0,{#NT_column13})  Edit 9,999,999.99
  Print #grs_mtd                   (+0,{#NT_column14})  Edit 999,999,999.99 ! TCSXKWK       04/06/15     New changes Ends
#endif !GEXPL customization end Mahesh babu 02/08/16

End-Procedure


Begin-Procedure P269-Write-Record! TCSXKWK       04/06/15     New changes start
#debug9 Show 'P269-Write-Record'
  let $tab = chr(9) 

let $header = 'Vendor ID' || $tab || 'Dept ID' || $tab || 'Name' || $tab || 'Ssn' || $tab ||  'Service Date' || $tab ||  'Birth Date'
  || $tab || 'Job Code' || $tab || 'Empl Status' || $tab ||'Action' || $tab || 'Action Reason' || $tab || 'Eff Date' || $tab || 'Term Date' 
   || $tab || 'Legal Hours' || $tab || 'Legal Contribution'
   
Write 1 from $header
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Writing output file.  Program terminating.'
    Stop
  End-If
  
  
End-Procedure! TCSXKWK       04/06/15     New changes Ends


Begin-Procedure P270-Write-Record
#debug9 Show 'P270-Write-Record'


  Add 1 To #recs-written
  Do Get-Emp-Ssn (&per.emplid, $Ssn_Out)    !GEX-TLL 04/16/08 Added	


#ifdef NT !GEXPL customization start Mahesh babu 02/08/16

let $NTname = substr($name, 1,30)    !Jyotsna - 08/25/2016 - To substr name to 30 chars
 !let $line =  $Vendor_ID ||$tab ||&j.deptid || $tab || $name  || $tab ||  $Ssn_Out || $tab ||  $Service_Dt ||$tab ||! TCSXKWK       04/06/15     New changes starts
  let $line =  $Vendor_ID ||$tab ||&j.deptid || $tab || $NTname  || $tab ||  $Ssn_Out || $tab ||  $Service_Dt ||$tab || !Jyotsna - 08/25/2016 - To substr name
               $Birth_Date || $tab ||$JobTitle|| $tab ||&j.empl_status  ||$tab || $Action || $tab || $Action_Reason ||$tab ||
               $effdt || $tab ||$Term_Date ||$tab ||Edit( &ghb.total_hrs ,'9,999,999.99') || $tab ||  Edit(&pd.ded_cur , '999,999,999.99')
              
               ! $Legal_Hours || $tab || $Legal_Contribution
               
               
	
  Write 1 From $line! TCSXKWK       04/06/15     New changes Ends
#endif !GEXPL customization end Mahesh babu 02/08/16

#ifdef UNIX !GEXPL customization start Mahesh babu 02/08/16
  Write 1 From &bdp.benefit_program:3
               $Ssn_Out:9		    !GEX-TLL 04/16/08 Added	
               $name:30
               &per.sex:1
               $gex_volun_low_hrs:1
               &j.full_part_time:1
               &j.reg_temp:1
               $Action:3
               $Action_Reason:3
               $Birthdate:10
               $ServiceDt:10
               $effdt:10
               $TermDate:10
               $address1:35
               $address2:35
               $city:30
               $state:2
               &per.postal:10
               $Legal_Hours:7
               $Legal_Contribution:11
#endif !GEXPL customization end Mahesh babu 02/08/16

    Status=#Filestat
  If #Filestat != 0
    Display 'Error Writing output file.  Program terminating.'
    Stop
  End-If
End-Procedure


Begin-Procedure P280-Before-BP-Break
#debug9 Show 'P280-Before-BP-Break'
  New-Page
  Do P210-Get-Benefit-Program-Name
End-Procedure

Begin-Procedure P290-After-Vendor-ID-Break
#debug9 Show 'P290-After-Vendor-ID-Break'

#ifdef UNIX !GEXPL customization start Mahesh babu 02/08/16
  Print '-'                        (+2,{#unix_column12},012)   Fill
  Print '-'                        (+0,{#unix_column13},014)   Fill
  Print 'Vendor ID'                (+2,{#unix_column8})
  Print $Save_Vendor_ID            (0,+1)
  Print 'Totals'                   (0,+1)
  Print #Vendor-ID-Legal-Hours     (0,{#unix_column12})        Edit 9,999,999.99
  Print #Vendor-ID-Legal-Amount    (0,{#unix_column13})        Edit 999,999,999.99
  Print ' '                        (+1,001)
#endif !GEXPL customization end Mahesh babu 02/08/16

#ifdef NT !GEXPL customization start Mahesh babu 02/08/16
  Print '-'                        (+2,{#NT_column12},012)   Fill
  Print '-'                        (+0,{#NT_column13},014)   Fill
  Print 'Vendor ID'                (+2,{#NT_column8})
  Print $Save_Vendor_ID            (0,+1)
  Print 'Totals'                   (0,+1)
  Print #Vendor-ID-Legal-Hours     (0,{#NT_column12})        Edit 9,999,999.99
  Print #Vendor-ID-Legal-Amount    (0,{#NT_column13})        Edit 999,999,999.99
  Print ' '                        (+1,001)
#endif  !GEXPL customization end Mahesh babu 02/08/16

  Let #Vendor-ID-Legal-Hours  = 0
  Let #Vendor-ID-Legal-Amount = 0
End-Procedure

Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'

  Do P310-Close-File
  Do P320-Display-File-Totals
  Do P330-Print-Report-Totals

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
End-Procedure

Begin-Procedure P310-Close-File
#debug9 Show 'P310-Close-File'

  Close 1
End-Procedure


Begin-Procedure P320-Display-File-Totals
#debug9 Show 'P320-Display-File-Totals'

  Move #recs-written To $recs-written   888,888,888

  Display ''
  Display $recs-written          Noline
  Display ' records written to ' Noline
  Display $File1
  Display ''
End-Procedure


Begin-Procedure P330-Print-Report-Totals
#debug9 Show 'P330-Print-Report-Totals'

  If $Data-Found-Flag <> 'Y'
    New-Page
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
End-Procedure


#include 'gexxx912.sqc'  !Get plan type multiple row table
#include 'gexxx927.sqc'  !Get vendor id multiple row table
#include 'getjobtl.sqc'  !Get-Job-Title procedure
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'getgeid.sqc'	 			!GEX-TLL 04/16/2008 Added
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   5-Feb-2009  
