!**************************************************************************
!  GEXXX903:  Retrieve "Multiple" Emp_Stat Run Controls and               *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX903_Emp_Stat_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX903_INCLUDE_EMP_STAT_CRITERIA                                  *
!    $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA                                  *
!    $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA                          *
!    $GEXXX903_EXCLUDE_EMP_STAT_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'                       *
!    Do GEXXX903-Select-Emp-Stat-Parameters                               *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]                          *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX903-Select-Emp-Stat-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_EMP_STAT_ROWS = 0
  Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = ''
  Let $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA = ''
  Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX903_EXCLUDE_EMP_STAT_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_EMP_STAT.EMPL_STATUS

  Add 1 To #GEX_RC_EMP_STAT_ROWS

  If #GEX_RC_EMP_STAT_ROWS = 1
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = '(' || $GEXXX903_Emp_Stat_Alias || ' In ('''     || &GEX_RC_EMP_STAT.EMPL_STATUS || ''''
    Let $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA = '(' || $GEXXX903_Emp_Stat_Alias || ' Not In (''' || &GEX_RC_EMP_STAT.EMPL_STATUS || ''''
  Else
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = $GEXXX903_INCLUDE_EMP_STAT_CRITERIA || ',''' || &GEX_RC_EMP_STAT.EMPL_STATUS || ''''
    Let $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA = $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA || ',''' || &GEX_RC_EMP_STAT.EMPL_STATUS || ''''
  End-If

From  PS_GEX_RC_EMP_STAT GEX_RC_EMP_STAT

Where GEX_RC_EMP_STAT.OPRID                    = [$OprID]
And   GEX_RC_EMP_STAT.RUN_CNTL_ID              = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_EMP_STAT.EMPL_STATUS, ' ') != ' '

End-Select

  If #GEX_RC_EMP_STAT_ROWS = 0
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = $GEXXX903_Emp_Stat_Alias || ' = ' || $GEXXX903_Emp_Stat_Alias
    Let $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA = $GEXXX903_Emp_Stat_Alias || ' = ' || $GEXXX903_Emp_Stat_Alias
  Else
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = $GEXXX903_INCLUDE_EMP_STAT_CRITERIA || '))'
    Let $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA = $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA || '))'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
    Let $GEXXX903_EXCLUDE_EMP_STAT_DISPLAY_CRITERIA = $GEXXX903_EXCLUDE_EMP_STAT_CRITERIA
  End-If

End-Procedure
