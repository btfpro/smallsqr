!*************************************************************
!  GetStDta:  Get selected fields from the State Tax Tables  *
!*************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2008 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2008/11/12:12:06:31                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  741930                                              !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
! GEXPY_848_E158770_01 2013-09-25 Vahini Katta                         * 
! Added Tax class 7 Additional Medicare                                *
!***********************************************************************

!*********************************!
begin-procedure Get-State-Tax-Data
!*********************************!

  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
PS_STATE_NAMES_TBL.DESCR,PS_STATE_NAMES_TBL.NUMERIC_CD
FROM  PS_STATE_NAMES_TBL
WHERE COUNTRY = {NID_COUNTRY}
  AND STATE   = $State
end-SELECT

  let $StateName = RTRIM(&PS_State_Names_Tbl.Descr, ' ')
  move &PS_State_Names_Tbl.Numeric_Cd to $StateNumber

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!

  if $curr_language_cd <> $Psoptions_Language_Cd
     do Get-Rel-Lang-State
  end-if


begin-SELECT
EMPLOYER_ID_SWT,EMPLOYER_ID_SUT,EMPLOYER_ID_DIS                                !HP99999
GL_SWT,GL_SDI,GL_SUT,GL_SDI_ER                                             !HP99999
GL_SUT_ER,BRANCH_CD,SUT_EXPERIENCE_RT,DIS_EXPERIENCE_RT,GL_VDI_EE,GL_VDI_ER
GL_ST_EIC,
UBI_NUMBER,EAF_RATE,HEALTH_CARE_COV,PNA_GL_FLI_EE,PNA_GL_VFLI_EE,PNA_GL_VFLI_ER
FROM  PS_CO_STATETAX_TBL
WHERE COMPANY = $Company
  AND STATE   = $State
  AND EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_CO_STATETAX_TBL
       WHERE  COMPANY = $Company
         AND  STATE   = $State
         AND  EFFDT  <= $AsOfDate)
end-SELECT

  move 0 to #OASDIRate
  move 0 to #MEDIRate
  move 0 to #FUTARate
  move 0 to #SDIRate
  move 0 to #SpecialUIRate
  move 0 to #EmployeeUIRate



begin-SELECT
OT.STATE
OT.TAX_CLASS
OT.TAX_RT * 1  &OT.Tax_Rt
  if rtrim(&OT.State, ' ') =  '$U' and &OT.Tax_Class = 'U'
    move &OT.Tax_Rt to #FUTARate
  end-if
  if rtrim(&OT.State, ' ') =  '$U' and &OT.Tax_Class = 'F'
    move &OT.Tax_Rt to #MediRate
  end-if
  ! GEXPY_848_E158770_01 2013-09-25 Vahini Katta Begins
  if rtrim(&OT.State, ' ') =  '$U' and &OT.Tax_Class = '7'
    move &OT.Tax_Rt to #AddlMEDIRate
  end-if
  ! GEXPY_848_E158770_01 2013-09-25 Vahini Katta Ends
  if rtrim(&OT.State, ' ') =  '$U' and &OT.Tax_Class = 'D'
    move &OT.Tax_Rt to #OASDIRate
  end-if
  if rtrim(&OT.State, ' ') <> '$U' and &OT.Tax_Class = 'D'
    move &OT.Tax_Rt to #SDIRate
  end-if
  if rtrim(&OT.State, ' ') <> '$U' and &OT.Tax_Class = 'S'
    move &OT.Tax_Rt to #SpecialUIRate
  end-if
  if rtrim(&OT.State, ' ') <> '$U' and &OT.Tax_Class = 'L'
    move &OT.Tax_Rt to #SWAFRate
  end-if
  if rtrim(&OT.State, ' ') <> '$U' and &OT.Tax_Class = 'M'
    move &OT.Tax_Rt to #WFDPRate
  end-if
  if rtrim(&OT.State, ' ') <> '$U' and &OT.Tax_Class = 'N'
    move &OT.Tax_Rt to #HCSFRate
  end-if
  if rtrim(&OT.State, ' ') <> '$U' and &OT.Tax_Class = 'V'
    move &OT.Tax_Rt to #EmployeeUIRate
  end-if
FROM  PS_ST_OTH_TAX_TBL OT
WHERE OT.STATE     IN  ('$U',$State)
  ! GEXPY_848_E158770_01 2013-09-25 Vahini Katta Begins
  !AND OT.TAX_CLASS IN  ('U','D','F','L','M','N','S','V')
  AND OT.TAX_CLASS IN  ('U','D','F','L','M','N','S','V','7')
  ! GEXPY_848_E158770_01 2013-09-25 Vahini Katta Ends
  AND OT.EFFDT     =
      (SELECT MAX(EFFDT)
       FROM   PS_ST_OTH_TAX_TBL
       WHERE  STATE   = OT.STATE
         AND  EFFDT  <= $AsOfDate)
end-SELECT

end-procedure


!*********************************!
begin-procedure Get-Rel-Lang-State
!*********************************!

begin-SELECT
STL.DESCR

    if rtrim(&STL.Descr, ' ') <> ''
       let $StateName = RTRIM(&STL.Descr, ' ')
    end-if

FROM  PS_STATE_NAMES_LNG STL
WHERE STL.COUNTRY     = {NID_COUNTRY}
  AND STL.LANGUAGE_CD = $Curr_language_cd
  AND STL.STATE       = $State

end-SELECT

end-procedure
