!***********************************************************************
! GEXEMAIL:  Update EMAILID for 90 GOLIVE                              *
!                                                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! NPK       06/03/2008  Initial Creation                               *
!
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


Begin-Report
  Do Initialization
  Do Get-OPRIDS
  Do Finalization
End-Report


Begin-Procedure Initialization
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXEMAIL'
  Let $ReportTitle   = 'Update EMAILID for 90 GOLIVE'

  Show ' '
  Show ' '
  Show 'Report ID:    ' $ReportID
  Show 'Report Title: ' $ReportTitle
  Show ' '

  Do Get-Current-DateTime    !curdttim.sqc
  Show 'Report Started on ' $ReportDate ' at ' $ReportTime
  Show ' '

End-Procedure

Begin-Procedure Get-OPRIDS

  Let #oprid_count = 0
  Let #emailid_upd_count = 0
  Let #emailid_blank_count = 0
  Let #emailaddress_ins_count = 0
  Let #emailaddress_upd_count = 0
  Let $OPRID    = '' 
  Let $EMPLID   = ''
Begin-Select 
O.OPRID 
O.EMPLID

  add 1 to #oprid_count

  Let $OPRID    = &O.OPRID 
  Let $EMPLID   = &O.EMPLID 

  Show '***************************************************************'
  Show 'OPRID / EMPLID ' $OPRID '/' $EMPLID 

  Do GET-JOB-DEPTID
  
  
From PSOPRDEFN O
WHERE LENGTH(TRIM(TRANSLATE(O.oprid, '0123456789', ' '))) is null
AND LENGTH(O.oprid) = 7
!and o.oprid = '1004007' ! TEST
Order By O.OPRID, O.EMPLID
End-Select
 
End-Procedure


Begin-Procedure GET-JOB-DEPTID

 let $deptid = ''
Begin-Select 
CJ.DEPTID

 let $deptid = &CJ.DEPTID
 
  Show 'OPRID / EMPLID / DEPTID ' $OPRID '/' $EMPLID '/' $deptid

 Do GET-GENERIC-EMAILID
  
   
From PS_CURRENT_JOB CJ 
WHERE CJ.EMPLID = $emplid
and CJ.EMPL_RCD = 0
End-Select

 If $deptid = ''
  Show 'DEPTID is Blank in JOB for this OPRID / EMPLID /DEPTID ' $OPRID '/' $EMPLID '/' $deptid
 End-If
 
End-Procedure


Begin-Procedure GET-GENERIC-EMAILID
  let $emailid = ''

Begin-Select 
OP.EMAILID

 
 let $emailid = &OP.EMAILID

 Show 'OPRID / EMPLID / EMAILID ' $OPRID '/' $EMPLID '/' $emailid 


  Do Update-OPRDEFN
  Do Update-ROLEXLATOPR
  Do Update-USEREMAIL
  Do CHECk-EMAIL-ADDRESSES
  add 1 to #emailid_upd_count


From PSOPRDEFN_FNl OP
WHERE SUBSTR(OP.OPRID,2,4) = $DEPTID
and SUBSTR(OP.OPRID,6,3) = 'PEM'
End-Select

 If ltrim(rtrim($emailid, ' '), ' ') = ''
  add 1 to #emailid_blank_count
  Show 'Generic EMAILID is Not found in OPRDEFN for this OPRID / EMPLID /EMAILID ' $OPRID '/' $EMPLID '/' $emailid 
 End-If


End-Procedure



Begin-Procedure Update-OPRDEFN
  Begin-SQL On-Error=P250-SQL-Error-Found('Update-OPRDEFN')
    Update PSOPRDEFN
       Set emailid      = $emailid
     Where OPRID        = $oprid
  End-SQL
 
End-Procedure


Begin-Procedure Update-ROLEXLATOPR
  Begin-SQL On-Error=P250-SQL-Error-Found('Update-ROLEXLATOPR')
    Update PS_ROLEXLATOPR
       Set emailid         = $emailid
     Where ROLEUSER        = $oprid
  End-SQL
 
End-Procedure


Begin-Procedure Update-USEREMAIL
  Begin-SQL On-Error=P250-SQL-Error-Found('Update-USEREMAIL')
    Update PSUSEREMAIL
       Set emailid      = $emailid
     Where OPRID        = $oprid
  End-SQL
  
End-Procedure


Begin-Procedure CHECk-EMAIL-ADDRESSES
 let $email_fnd = 'N'
Begin-Select 
em.email_ADDR 

  let $email_Addr = &em.email_ADDR
  let $email_fnd = 'Y'
 
  Show 'OPRID / EMPLID / EMAILADDRESS in EMAILADDRESS TABLE Before UPDATE ' $OPRID '/' $EMPLID '/' $email_Addr
 
From PS_EMAIL_ADDRESSES em
WHERE em.EMPLID = $emplid
End-Select

 If $email_fnd = 'N'
     add 1 to #emailaddress_ins_count
     do insert-EMAIL-ADDRESSES
  else
   add 1 to #emailaddress_upd_count
   do update-EMAIL-ADDRESSES
 End-If
  
End-Procedure


Begin-Procedure insert-EMAIL-ADDRESSES

  Begin-SQL On-Error=P250-SQL-Error-Found('Update-USEREMAIL')
        INSERT INTO PS_EMAIL_ADDRESSES
               (EMPLID,
                E_ADDR_TYPE,
                EMAIL_ADDR,
                PREF_EMAIL_FLAG)                     
        VALUES ($emplid,
                'BUSN',
                $emailid,
                'Y') 
  End-SQL
  
End-Procedure


Begin-Procedure update-EMAIL-ADDRESSES
  Begin-SQL On-Error=P250-SQL-Error-Found('Update-USEREMAIL')
    Update PS_EMAIL_ADDRESSES
       Set email_ADDR   = $emailid
     Where emplid       = $emplid
  End-SQL
  
End-Procedure


Begin-Procedure P250-SQL-Error-Found($Proc_Name) 
  Show 'SQL Error in ' $Proc_Name
  Show $_sql-error
  Stop
End-Procedure


Begin-Procedure Finalization
  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Do Get-Current-DateTime    !curdttim.sqc
  Show ' '
  Show '############################################################################## '
  show 'OPRIDS Selected Count ' #oprid_count
  show 'EmailIds in OPRDEFN updated Count ' #emailid_upd_count
  show 'EmailIds NOT updated in OPRDEFN Count '#emailid_blank_count
  show 'EmailAddresses inserted in PS_EMAIL_ADDRESSES Count ' #emailaddress_ins_count
  show 'EmailAddresses updated in PS_EMAIL_ADDRESSES Count '#emailaddress_upd_count
  show '############################################################################## '
  Show ' '

  Show 'Report Ended on ' $ReportDate ' at ' $ReportTime
End-Procedure

#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'validdt.sqc'   ! Validate date routine
#Include 'number.sqc'    !Routines to format numbers
