!
!
!***********************************************************************
!  GEXHR030:  Employee Rate Salary Grade List
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       * 
!                                                                      *
!***********************************************************************
!
! GEXHR030:  		EMPL RAT SAL GR LST
!
! Narrative:		Prints detail informtion of employees.
!
! #Debugx Used:		#debug9 paragraph trace
!			      #debug8 key variable values
!			      #debug7 data returned from sql calls
!
! SQL Tables:		PERSONAL_DATA
!				JOB
!				GEX_DEPT_TBL 
!				JOBCODE_TBL
!
! Written by:		Dave Herzog-SoftLink Inc.  02-12-98
!
! Normally Run:		Monthly
!
! Entered Run 
! Control Parms:	FOR WHERE CLAUSE:
!			Company Division Code
!			Company  	
!			Deptid	
!			Hourly_Rt	
!			Empl_Status	
!			Grade		
!			Weekly_Rt	
!
!			Date		(perhaps need to enter a date or month for 
!					which the report should be run)
!
!			FOR ORDER BY CLAUSE:
!					1) by GRADE, HOURLY_RT, WEEKLY_RT (calculated field)
!					2) by DEPTID, ACCT_CD, NAME		
!               	
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!ID#		INITIALS	DATE		DESCRIPTION OF THE CHANGE         
!***********************************************************************
!
!		DAH		02/12/98	Initial Creation
!GEX001	JDH		05/19/98	Remove fields weekly rate and
!                                     employee status from report. 
!                                   Fixed job title to avoid truncation.
! ISDVMXD 	02/02/2009	ITG 56799 JT.DEscr Changes in SQRs     *
!***********************************************************************


!selection criteria
#define #x000_col_selection_criteria_a 	02
#define #x000_col_selection_criteria_b 	05

!line 1
#define #x000_col_company		02
#define #x000_col_deptid		10
#define #x000_col_acct_cd		20
#define #x000_col_position_nbr	29
#define #x000_col_EEO1Code		40
#define #x000_col_EEO1Code_descr	42
#define #x000_col_emplid		56
#define #x000_col_name			68
#define #x000_col_grade			95
#define #x000_col_hourly_rt		100
!#define #x000_col_weekly_rt		111	!GEX001-DEL	
!#define #x000_col_empl_status	123	!GEX001-DEL
!#define #x000_col_jobcode		130   !GEX001-DEL
!#define #x000_col_job_title		142   !GEX001-DEL
#define #x000_col_jobcode		111   !GEX001-ADD
#define #x000_col_job_title		123   !GEX001-ADD
	
#Include 'setenv.sqc'
#Include 'setup02.sqc'		!landscape

!***********************************************************************
begin-heading 8
!***********************************************************************
#debug9 show 'begin-heading'

 let $Company = rtrim($x000_Company,' ')


 #Include 'stdhdg01.sqc'

 let $x000_temp_out =  'Sorted by ' || $X000_ORDER_BY 
 print $x000_temp_out 			(3,) center 

 let $x000_temp_out =  'Report as of ' || $X000_REPORT_HEADING_ASOFDATE 
 print $x000_temp_out 	            (4,) center 

!line 1
 print 'Company'		(+3,{#x000_col_company})
 print 'Deptid'		(0,{#x000_col_deptid})
 print 'Acct'		(0,{#x000_col_acct_cd})
 print 'Position'		(0,{#x000_col_position_nbr})
 print 'EEO1CODE/Descr'	(0,{#x000_col_EEO1Code})
 print 'Name'		(0,{#x000_col_name})
 print 'Emplid'		(0,{#x000_col_emplid})
 print 'Grade'		(0,{#x000_col_grade})	
 print '   Hourly'	(0,{#x000_col_hourly_rt})
! print '   Weekly'	(0,{#x000_col_weekly_rt})	!GEX001-DEL
! print 'Empl'		(0,{#x000_col_empl_status})	!GEX001-DEL
 print 'JobCode'		(0,{#x000_col_jobcode})
 print 'JobTitle'		(0,{#x000_col_job_title})

!line 2
 print 'Cd'			(+1,{#x000_col_acct_cd})
 print 'Number'		(0,{#x000_col_position_nbr})
 print '   Rate'		(0,{#x000_col_hourly_rt})
! print '   Rate'		(0,{#x000_col_weekly_rt})		!GEX001-DEL
! print 'St'			(0,{#x000_col_empl_status}) 	!GEX001-DEL

end-heading


!***********************************************************************
begin-report
!***********************************************************************
#debug9 show 'begin-report'

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  show '$asoftoday=' $asoftoday 
 
  do StdAPI-Init
  do P010-Init-Variables
  do P100-Get-Employees-Data

  do StdAPI-Term
  do Reset

  show 'Successful end of report'

end-report


!***********************************************************************
begin-procedure P010-Init-Variables
!***********************************************************************
#debug9 show 'P010-Init-Variables'

  let $x000_new_page_flag = 'N'

  let $ReportID    = 'GEXHR030'
  let $ReportTitle = 'Employee Rate/Salary/Grade Report'
  let $Country     = 'USA'

  show '$ReportID=' $ReportID 
  show '$ReportTitle=' $ReportTitle
  show '$ReportDate=' $ReportDate
  show '$ReportTime=' $ReportTime

  if $Prcs_Process_Instance = ''
	do P015-get-input
  else

!	get parameters from single row run control record 
	Do GEXXX922-Select-Parameters
      let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.ASOFDATE
      if rtrim($GEX_RC_PAY.ASOFDATE,' ') = ''
        let $X000_WHERE_SELECT_ASOFDATE = $asoftoday
      end-if
      do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE ,{DEFDATE}, '', '')


!	get parameters from multiple row run control record
    	Let $GEXXX900_Company_Alias  = 'JOB.COMPANY'                           
	Do GEXXX900-Select-Company-Parameters 

      Let $GEXXX901_Company_Alias  = 'JOB.COMPANY' 
      Let $GEXXX901_Paygroup_Alias = 'JOB.PAYGROUP'    
      Do GEXXX901-Select-Company-Paygroup-Parameters    

 	Let $GEXXX902_Deptid_Alias = 'JOB.DEPTID'  
	Do GEXXX902-Select-Deptid-Parameters 

	Let $GEXXX903_Emp_Stat_Alias = 'JOB.EMPL_STATUS'  
	Do GEXXX903-Select-Emp-Stat-Parameters          

    	Let $GEXXX904_Grade_Alias = 'JOB.GRADE'    
	Let $GEXXX904_Sal_Admin_Plan_Alias = 'JOB.SAL_ADMIN_PLAN'
	Do GEXXX904-Select-Grade-Parameters   

	Let $GEXXX905_Hrly_Rt_Alias = 'JOB.HOURLY_RT'   
	Do GEXXX905-Select-Hrly-Rt-Parameters

	Let $GEXXX906_Wkly_Sal_Alias = 'JOB.ANNUAL_RT/52'
	Do GEXXX906-Select-Wkly-Sal-Parameters

	Let $GEXXX907_Div_CD_Alias = 'GEX_DEPT.GEX_COMPANY_DIV_CD'
	Do GEXXX907-Select-Div-CD-Parameters

	Let $GEXXX908_Sal_Admin_Plan_Alias = 'JOB.SAL_ADMIN_PLAN'
	Do GEXXX908-Select-SAL-ADMIN-PLAN-Parameters
          
	Let $GEXXX909_Acct_CD_Alias = 'JOB.ACCT_CD'
	Do GEXXX909-Select-Acct-CD-Parameters

  end-if  

!order by for GEXHR030 reports
	let $X000_ORDER_BY = 'JOB.GRADE, JOB.HOURLY_RT, JOB.ANNUAL_RT/52 '
!order by for GEXHR031 reports
!	let $X000_ORDER_BY = 'DEPTID, ACCT_CD, NAME'

	show '$GEXXX907_INCLUDE_DIV_CD_CRITERIA                =' $GEXXX907_INCLUDE_DIV_CD_CRITERIA
	show '$GEXXX900_INCLUDE_COMPANY_CRITERIA               =' $GEXXX900_INCLUDE_COMPANY_CRITERIA
	show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA      =' $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA   
	show '$GEXXX902_INCLUDE_DEPTID_CRITERIA                =' $GEXXX902_INCLUDE_DEPTID_CRITERIA
	show '$GEXXX903_INCLUDE_EMP_STAT_CRITERIA              =' $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
	show '$GEXXX904_INCLUDE_GRADE_CRITERIA                 =' $GEXXX904_INCLUDE_GRADE_CRITERIA		  
	show '$GEXXX905_INCLUDE_HRLY_RT_CRITERIA               =' $GEXXX905_INCLUDE_HRLY_RT_CRITERIA
	show '$GEXXX906_INCLUDE_WKLY_SAL_CRITERIA              =' $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA
	show '$GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA        =' $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA 
	show '$GEXXX909_INCLUDE_ACCT_CD_CRITERIA               =' $GEXXX909_INCLUDE_ACCT_CD_CRITERIA         
      show '$X000_WHERE_SELECT_ASOFDATE                      =' $X000_WHERE_SELECT_ASOFDATE 

 	print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,{#x000_col_selection_criteria_a})
	print '$Prcs_OPRID          ='                              (+1,{#x000_col_selection_criteria_b})
	print $Prcs_OPRID                                           (0,+2)
	print '$PRCS_RUN_CNTL_ID    ='                              (+1,{#x000_col_selection_criteria_b})
	print $PRCS_RUN_CNTL_ID                                     (0,+2)

 	print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,{#x000_col_selection_criteria_a})
	print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA 		(0,+2)
 	print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,{#x000_col_selection_criteria_b})
 	print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA            (0,+2)
 	print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,{#x000_col_selection_criteria_b})
 	print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA   (0,+2)
	print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	            (0,+2)
	print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA           (0,+2)
	print '$GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA            ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA	            (0,+2)
	print '$GEXXX905_INCLUDE_HRLY_RT_DISPLAY_CRITERIA          ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX905_INCLUDE_HRLY_RT_DISPLAY_CRITERIA            (0,+2)
	print '$GEXXX906_INCLUDE_WKLY_SAL_DISPLAY_CRITERIA         ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX906_INCLUDE_WKLY_SAL_DISPLAY_CRITERIA           (0,+2)
	print '$GEXXX908_INCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA   ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA     (0,+2)
	print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,{#x000_col_selection_criteria_b})
	print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA            (0,+2)

      print '$X000_WHERE_SELECT_ASOFDATE                         ='   (+1,{#x000_col_selection_criteria_b})
      print $X000_WHERE_SELECT_ASOFDATE                           (0,+2)
 
	print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,{#x000_col_selection_criteria_a})
 	print '$X000_ORDER_BY                                      ='   (+1,{#x000_col_selection_criteria_b})
 	print $X000_ORDER_BY 	(0,+2)

 	let #page-count = 0
	NEW-PAGE
	LET #PAGE-COUNT = 1

end-procedure

!***********************************************************************
begin-procedure P015-get-input
!***********************************************************************
#debug9 show 'P015-get-input'
      let $GEXXX907_INCLUDE_DIV_CD_CRITERIA           = '1=1'           
	let $GEXXX900_INCLUDE_COMPANY_CRITERIA 		= '1=1'
	let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1' 
	let $GEXXX902_INCLUDE_DEPTID_CRITERIA		= '1=1'
	let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA 		= '1=1'
	let $GEXXX904_INCLUDE_GRADE_CRITERIA 		= '1=1'		  
	let $GEXXX905_INCLUDE_HRLY_RT_CRITERIA 		= '1=1'
	let $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA         = '1=1'
	let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA 	= '1=1'
	let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA          = '1=1'

      let $X000_WHERE_SELECT_ASOFDATE                 = $asoftoday

!order by for GEXHR030 reports
	let $X000_ORDER_BY = 'JOB.GRADE, JOB.HOURLY_RT, JOB.ANNUAL_RT/52 '
!order by for GEXHR031 reports
!	let $X000_ORDER_BY = 'DEPTID, ACCT_CD, NAME'

end-procedure


!***********************************************************************
begin-procedure P100-Get-Employees-Data
!***********************************************************************
#debug9 show 'P100-Get-Employees-Data'

 let $x100_Get_Employees_Data_Flag = 'N'

begin-SELECT 

JOB.EMPLID
JOB.EMPL_RCD
JOB.EFFDT
JOB.EFFSEQ
JOB.ACCT_CD
JOB.COMPANY
JOB.PAYGROUP
JOB.JOBCODE
JOB.POSITION_NBR
JOB.DEPTID
JOB.EMPL_STATUS
JOB.SAL_ADMIN_PLAN 
JOB.GRADE
JOB.ANNUAL_RT
JOB.ANNUAL_RT/52 &JOB_weekly_rt
JOB.HOURLY_RT

PER.EMPLID
PER.NAME

GEX_DEPT.DEPTID
GEX_DEPT.EFFDT
GEX_DEPT.SEQUENCE_NUMBER
GEX_DEPT.GEX_COMPANY_DIV_CD

	!Show 'for emplid = ' &JOB.EMPLID
 let $JobCode = &JOB.JOBCODE
 Let $Position_Nbr = Ltrim(RTrim(&JOB.POSITION_NBR,' '),' ')		!isdvmxd added for ITG 56799 on 02/02/2009
 !ISDVMXD added for ITG 56799 on 02/02/2009  -- Begin
 Do Get-Position-Title	
 If $PosName = ' '
 	do P900-Get-Jobcode-Tbl
 	let $x000_JobTitle = $JobTitle
 	!Show 'pos flag = N.. so jobtitle =' $x000_JobTitle
 Else 
 	!Show 'pos flag = Y.. So posname ='$x000_JobTitle
 	let $x000_JobTitle = $PosName
 End-If
 !ISDVMXD added for ITG 56799 on 02/02/2009  -- End

 let $FieldName  = 'EEO1CODE'
 let $FieldValue = &JT.EEO1CODE
 do Read-Translate-Table
 move $XlatShortName to $X000_EEO1CODE
 #debug8 show 'xlat=' $XlatShortName ' '  $XlatLongName

 do P800-Print-Detail-Line

 let $x100_Get_Employees_Data_Flag = 'Y'

FROM 	PS_PERSONAL_DATA PER,
	PS_JOB JOB,
	PS_GEX_DEPT_TBL GEX_DEPT

WHERE	 
 	JOB.EMPLID = PER.EMPLID

 	AND [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
	AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
	AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
	AND [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
	AND [$GEXXX904_INCLUDE_GRADE_CRITERIA]
	AND [$GEXXX905_INCLUDE_HRLY_RT_CRITERIA]
	AND [$GEXXX906_INCLUDE_WKLY_SAL_CRITERIA]
	AND [$GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA]
	AND [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]

	AND JOB.EFFDT = (SELECT MAX(JOB1.EFFDT) FROM PS_JOB JOB1
			WHERE JOB1.EMPLID = JOB.EMPLID AND
				JOB1.EMPL_RCD = JOB.EMPL_RCD AND
				JOB1.EFFDT <= $X000_WHERE_SELECT_ASOFDATE) 
	AND JOB.EFFSEQ = (SELECT MAX(JOB2.EFFSEQ) FROM PS_JOB JOB2
			WHERE JOB2.EMPLID = JOB.EMPLID AND
				JOB2.EMPL_RCD = JOB.EMPL_RCD AND
				JOB2.EFFDT = JOB.EFFDT)

	AND JOB.DEPTID = GEX_DEPT.DEPTID 
	AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
	AND GEX_DEPT.EFFDT = (SELECT MAX(GEX_DEPT1.EFFDT) FROM PS_GEX_DEPT_TBL GEX_DEPT1
			WHERE GEX_DEPT1.DEPTID = GEX_DEPT.DEPTID AND
				GEX_DEPT1.EFFDT <= $X000_WHERE_SELECT_ASOFDATE) 
	AND GEX_DEPT.SEQUENCE_NUMBER = (SELECT MAX(GEX_DEPT2.SEQUENCE_NUMBER) FROM PS_GEX_DEPT_TBL GEX_DEPT2
			WHERE GEX_DEPT2.DEPTID = GEX_DEPT.DEPTID AND
				GEX_DEPT2.EFFDT = GEX_DEPT.EFFDT)

ORDER BY [$X000_ORDER_BY]

end-select 

 if $x100_Get_Employees_Data_Flag = 'N'
	print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) CENTER
 end-if


end-procedure



!***********************************************************************
begin-procedure P800-Print-Detail-Line
!***********************************************************************
#debug9 show 'P800-Print-Detail-Line'

 print &JOB.COMPANY		(+1,{#x000_col_company})
 print &JOB.DEPTID		(0,{#x000_col_deptid})
 print &JOB.ACCT_CD		(0,{#x000_col_acct_cd},7)
 print &JOB.POSITION_NBR      (0,{#x000_col_position_nbr})
 print &JT.EEO1CODE       	(0,{#x000_col_EEO1Code})
 print $X000_EEO1CODE		(0,{#x000_col_EEO1Code_Descr})
 print &PER.NAME       	      (0,{#x000_col_name},25)
 print &PER.EMPLID		(0,{#x000_col_emplid})
 print &JOB.GRADE		      (0,{#x000_col_grade})	 
 print &JOB.HOURLY_RT		(0,{#x000_col_hourly_rt})  edit 99,990.00
! print &JOB_weekly_rt		(0,{#x000_col_weekly_rt}) edit 999,990.00	!GEX001-DEL
! print &JOB.EMPL_STATUS 	(0,{#x000_col_empl_status})			!GEX001-DEL
 print &JOB.JOBCODE		(0,{#x000_col_jobcode})
 print $x000_JobTitle 		(0,{#x000_col_job_title}) 

end-procedure


!***********************************************************************
begin-procedure P900-Get-Jobcode-Tbl
!***********************************************************************
#debug9 show 'P900-Get-Jobcode-Tbl'

  if rtrim($JobCode,' ') = ''
    move '' to $JobTitle
    move '' to $JobTitleAbbrv
    goto P901_GetJobTlExit
  end-if
 
begin-SELECT
JT.DESCR
JT.DESCRSHORT
JT.EEO1CODE

FROM  PS_JOBCODE_TBL JT
WHERE JT.JOBCODE = $JobCode
  AND JT.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_JOBCODE_TBL
       WHERE  JOBCODE = $JobCode
         AND  EFFDT  <= $X000_WHERE_SELECT_ASOFDATE)
end-SELECT
  let $JobTitle       = rtrim(&JT.Descr,' ')
  let $JobTitleAbbrv  = rtrim(&JT.DescrShort,' ')

P901_GetJobTlExit:

end-procedure
!*********************************************************************
#include 'setenv.sqc'    !set database environment
#include 'getcodta.sqc'  !Get-Company_Data procedure
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'getdptnm.sqc'  !Get-Department-Name procedure
#include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'datetime.sqc'  !general date and time formatting procedures
#include 'number.sqc'    !general number formatting procedures
#include 'stdapi.sqc'    !StdAPI-Init procedure
#include 'reset.sqc'     !Reset printer procedure
#include 'getbalid.sqc'  !Get-Calendar-Year-Id
#include 'gexregtp.sqc'	 !Get full-part time code
!#include 'GetJobtl.sqc' !Get job title 
#include 'getposnm.sqc'	 !Get-Position_Title 		!isdvmxd added for ITG 56799 on 02/02/2009 
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx904.sqc'  !Get grade multiple row table
#include 'gexxx905.sqc'  !Get hourly rate multiple row table
#include 'gexxx906.sqc'  !Get weekly rate multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx908.sqc'  !Get sal_admin_plan multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control





