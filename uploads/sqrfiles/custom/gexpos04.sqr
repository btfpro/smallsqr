!***********************************************************************
!  GEXPOS04: Active Position History Report                            *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!***********************************************************************
! This module contains confidential and proprietary information of     *
! PeopleSoft, Inc.; it is not to be copied, reproduced, or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! under the applicable license agreement.                              *
!                                                                      *
! Copyright (c) 1988-2001 PeopleSoft, Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
!       $Release:  HRMS830                                             *
!      $Revision:  1                                                   *
!    $Version-ID:  \main\HRMS800_stg\HRMS830_stg\1                     *
!                                                                      *
!***********************************************************************
! ITG 38136	DXS	10/24/2006 This report was cloned from POS004. *
!				   Modify POS004 to show active	       *
!				   Positions grouped by Dept number.   *
!				   Place the Dept name and number in   *
!				   the header of the report. Page break*
!				   on Department number.	       *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#include 'setup02.sqc'   !Printer and Page-Size Initialization (Landscape)

#define #column1  001
#define #column2  007
#define #column3  017
#define #column4  050
#define #column5  060
#define #column6  068
#define #column7  078
#define #column8  083
#define #column9  090
#define #column10  120
#define #column11  140
#define #column12  150


!***********************************************************************
begin-Report
!***********************************************************************
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Init-Report
   do Process-Main
   do Reset
   do Stdapi-Term
end-report


!***********************************************************************
begin-Heading 6
!***********************************************************************

  let $ReportID = 'GEXPOS04'
  #include 'stdhdg01.sqc'

  Print 'Position Information Report'     (3,1) center
  print $rpt-subtitle  (+1,1) center

  ! 1st or top row
  Print 'Dept'			(+2,{#column1}) 
  Print 'Position'		(0,{#column2})	
  Print 'Position'		(0,{#column3})
  Print 'Jobcode'		(0,{#column4})
  Print 'Grade'			(0,{#column5})
  Print 'Location'		(0,{#column6})	
  Print 'FT/'			(0,{#column7})
  Print 'Reg/'			(0,{#column8})
  Print 'Incumbent'		(0,{#column9})
  Print 'Reports'		(0,{#column10})
  Print 'Position'		(0,{#column11})	
  Print 'Budgeted'		(0,{#column12})

  ! 2nd or bottom row
  Print 'ID'			(+1,{#column1})	
  Print 'No.'			(0,{#column2}) 
  Print 'Title'			(0,{#column3})
  Print 'PT'			(0,{#column7})
  Print 'Temp'			(0,{#column8})
  Print 'To'			(0,{#column10}) 
  Print 'Status'		(0,{#column11}) 
  Print 'Position'		(0,{#column12})

end-Heading


!***********************************************************************
begin-procedure Init-Report
!***********************************************************************

  move 'GEXPOS04' to $ReportID
  do Stdapi-Init

  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = 'gexpos04'
  end-if
  
  do Select-Parameters
  
  let #total_budgeted_posn = 0
  let #total_posn = 0

end-procedure


!***********************************************************************
begin-procedure Select-Parameters
!***********************************************************************
	let $asofdate = ''
begin-select
hr.asofdate	&asofdate
hr.deptid	&deptid

	move &asofdate to $asofdate
	move &deptid to $deptid1

from ps_run_cntl_hr hr
where oprid = $Prcs_OprID
and run_cntl_id = $Prcs_Run_Cntl_ID
end-select
	
	if $asofdate = ''
		let $asofdate = $current-date
	end-if

        if $deptid1 = ' '
		let $deptid_clause = '1 = 1' 
	else
		move $deptid1 to $deptid
                let $deptid_clause = ' A.DEPTID = ''' || $deptid || ''''
        end-if

	show '$asofdate ' $asofdate 
	show '$deptid ' $deptid
	show '$deptid_clause  ' $deptid_clause 

end-procedure 


!***********************************************************************
begin-procedure Process-Main
!***********************************************************************

begin-SELECT
A.DEPTID	() on-break print=never after=Print-Total
A.POSITION_NBR
A.DESCR
A.JOBCODE
j.emplid
j.grade 
A.LOCATION
A.FULL_PART_TIME
A.REG_TEMP
p.NAME
rto.gex_ceo
rto.gex_evp
rto.gex_svpvp
rto.gex_dir
rto.gex_mgr
rto.gex_sup
A.POSN_STATUS
A.BUDGETED_POSN

	move &A.DEPTID to $deptid
	move &A.POSITION_NBR to $position_nbr
	move &A.DESCR	to $descr
	move &A.JOBCODE	to $jobcode
	move &j.emplid to $emplid
	move &j.grade to $grade
	move &A.LOCATION to $location
	move &A.FULL_PART_TIME to $full_part_time
	move &A.REG_TEMP to $reg_temp
	move &p.NAME to $incumbent
	move &A.POSN_STATUS to $posn_status
	move &A.BUDGETED_POSN to $budgeted_posn

	move &rto.gex_ceo to $gex_ceo
	move &rto.gex_evp to $gex_evp
	move &rto.gex_svpvp to $gex_svpvp
	move &rto.gex_dir to $gex_dir
	move &rto.gex_mgr to $gex_mgr
	move &rto.gex_sup to $gex_sup

	let $rpt-subtitle = 'Position for Department ' || $deptid

	if $budgeted_posn = 'Y'
		let #total_budgeted_posn = #total_budgeted_posn + 1
	end-if

	move 'POSN_STATUS' to $Fieldname
        move $posn_status to  $FieldValue
	do Read-Translate-Table

	do Get-ReportsTo

	add 1 to #total_posn
	do Print-Data	
	
	
FROM ps_job j, ps_personal_data p, PS_POSITION_DATA A, ps_gex_mgmt_report rto
WHERE A.EFF_STATUS = 'A'
AND A.EFFDT = (SELECT MAX(EFFDT) FROM PS_POSITION_DATA
	       WHERE POSITION_NBR = A.POSITION_NBR
	       AND EFFDT <= $asofdate)
AND [$deptid_clause]				! A.DEPTID = $deptid
AND  j.position_nbr = a.position_nbr   ! '00000040'
AND j.empl_status  = 'A'
AND j.effdt = (SELECT MAX(j_ed.effdt) FROM ps_job j_ed
		 WHERE j_ed.emplid = j.emplid
		 AND j_ed.empl_rcd = j.empl_rcd
		 AND j_ed.effdt <= $asofdate)
AND j.effseq = (SELECT   MAX(j_es.effseq) FROM ps_job j_es
		 WHERE j_es.emplid = j.emplid
		 AND j_es.empl_rcd = j.empl_rcd
		 AND j_es.effdt <= j.effdt)
AND j.emplid = p.emplid
AND a.deptid = rto.deptid
ORDER BY A.DEPTID, A.POSITION_NBR
end-SELECT
end-procedure


!***********************************************************************
begin-procedure Get-ReportsTo
!***********************************************************************
  !show '<' $gex_sup '><' $gex_mgr '><' $gex_dir '><' $gex_svpvp '><' $gex_evp '><' $gex_ceo '>'

	if $gex_sup = ' ' or $gex_sup = $emplid
		if $gex_mgr = ' ' or $gex_mgr = $emplid
			if $gex_dir = ' ' or $gex_dir = $emplid
				if $gex_svpvp = ' ' or $gex_svpvp = $emplid
					if $gex_evp = ' ' or $gex_evp = $emplid
						if $gex_ceo = ' ' or $gex_ceo = $emplid
							let $reports_to_emplid = 'NONE'
						else
							let $reports_to_emplid = $gex_ceo 
						end-if
					else
						let $reports_to_emplid = $gex_evp 
					end-if
				else
					let $reports_to_emplid = $gex_svpvp 
				end-if
			else
				let $reports_to_emplid = $gex_dir
			end-if
		else
			let $reports_to_emplid = $gex_mgr
		end-if
	else
		let $reports_to_emplid = $gex_sup 
	end-if

 ! show '$reports_to_emplid ' $reports_to_emplid 

if $reports_to_emplid != 'NONE'

begin-select
rpt.name	

	move &rpt.name to $reports_to
!	show '$reports_to ' $reports_to

from ps_personal_data rpt
where rpt.emplid = $reports_to_emplid
end-select

else
	let $reports_to = 'NONE'
end-if

end-procedure 


!***********************************************************************
begin-procedure Print-Data
!***********************************************************************

	print $deptid		(+2,{#column1}) 
	print $position_nbr	(0,{#column2}) 
	print $descr		(0,{#column3}) 
	print $jobcode		(0,{#column4})
	print $grade		(0,{#column5}) 
	print $location		(0,{#column6}) 
	print $full_part_time	(0,{#column7}) 	
	print $reg_temp		(0,{#column8}) 
	print $incumbent	(0,{#column9}) 
	print $reports_to	(0,{#column10}) 	
	print $XlatShortName    (0,{#column11}) 	! $posn_status	
	print $budgeted_posn	(0,{#column12}) 	

end-procedure 


!***********************************************************************
begin-procedure Print-Total
!***********************************************************************
	print 'Total Budgeted Positons: ' (+2,1)
	print #total_budgeted_posn (,28)
	print 'Total Positions: ' (+1,1)
	print #total_posn (,28)
	
	let #total_budgeted_posn = 0
	let #total_posn  = 0
	new-page

end-procedure 


!***********************************************************************
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'datemath.sqc'  ! ROUTINES FOR DATEMATH
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#include 'readxlat.sqc'
