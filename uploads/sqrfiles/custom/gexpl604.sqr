!***********************************************************************
!  GEXPL604:   UFCW Int'l Pension Meat EE's                            *
!***********************************************************************
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!***********************************************************************
!                                                                      *
! GEXPL604:             UFCW Int'l Pension Interface for Meat EE's     *
!                                                                      *
! Narrative:            This program creates a monthly pension file    *
!                       & contribution report for Meat EE's. This file *
!                       is emailed to fund administrators (Zenith).    *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           acct_cd_tbl,          providr_tbl              *
!                       dept_tbl,             gex_rc_ben_plan          *
!                       gex_rc_vendor,        gex_flat_rate            *
!                       flat_rate_tbl,        svc_rate_tbl             *
!                       svc_covg_tbl,         pay_calendar             *
!                       pay_cal_bal_id,       ben_defn_optn            *
!                       ben_defn_cost,        ben_defn_pgm             *
!                       personal_data,        employment               *
!                       job,                  benef_plan_tbl           *
!                       pay_line,             pay_check                *
!                       pay_deduction,        earnings_bal             *
!                       health_benefit                                 *
! Normally Run:         Monthly                                        *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:  Plan Type, Vendor ID        *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  SCM         05/06/2002    Initial Creation.                         *
!                                                                      *
!  GBD         07/03/2002    Print Total Contribution Amount Paid on   *
!                            the report file but not on the data file. *
!                            Removed #recs-written variable accumulator*
!                            from Write-Pension-Record procedure. This *
!                            accumulator was producing a doubled count *
!                            because it is also in Print-Record.       *
!                                                                      *
!  AXC         08/01/2002    Changed Line and Page# to Lune_num and    *
!                            Page_num Changed provider to vendor _id   * 
!			     Union code is now on Job. Removed select  *
!                            from Job_labor.                           *
!                                                                      *
!  CWB         09/10/2002    Added additional eMail address at the     *
!                            request of the 3rd party administrator.   *
!  GBD         09/12/2002    v8.3 Upgrade - Provider field changed to  *
!                            Vendor ID.                                *
!                                                                      *      
!  GBD         10/16/2002    Modified code to eliminate multiple row   *
!                            select.                                   *
!                                                                      *      
!  CWB         01/28/2003    Modified the eMail address at the         *
!                            request of the 3rd party administrator.   *
!                                                                      *
!  AXL         02/20/2003    Add empl_rcd to Earnings Bal join.        *
!                                                                      *      
!  GBD         01/20/2003    Add code to select email parameters from  *
!                            GEX_INTFC_RULE/DATA.                      *
!                                                                      *
!  GBD         07/29/2003    Remove email from sqr - moved to script.  *
!                                                                      *
!  CWB         06/07/2004    Expanded ADDRESS1 and ADDRESS2 from       *
!                            35 characters to 55 in the work-table.    *
!                                                                      *
!  GEX-Chandra  09/11/2007  Retrofitted Sqr			        *
!VENDKXY     04/30/2009      Done Changes requested from S3 12651638   *
! GEXBN_848_E115294_01 2011-12-13 Vahini Katta				 *
! 1) Add asofdate to the run control					 *
! 2) Remove the Rule hint				               *
! GEXBN_848_E117219_01 2011-03-21 kundan yeedibilli - Added csv file   *
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!  JN          07/20/2016    Corrected the main fetch query to select  *
!                            Benefit Plans correctly based on VendorID *
!
!	Ujwal Dyasani	30-Jan-2017		1. To stop .dat file from being generated						*
!									2. to change directory of .xls file being generated				*
!									~ With these changes, gexpl604a_sh in UC4 has to be inactivated	*
!									~ Previously, the process used to run in UNIX, but after these 	*
!									  changes, it will be run in NT									*
!****************************************************************************************************


#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

#define #column1     1
#define #column2     5
#define #column3    14
#define #column4    19
#define #column5    42
#define #column5A   52
#define #column5B   80
#define #column5C   89
#define #column5D   92
#define #column6    98
#define #column7    107
#define #column8    116
#define #column9    125
#define #column10   130
#define #column11   138
#define #column12   145
#define #column13   155
#define #column14   165
!GBD 07/03/2002 Begin
#define #column15   160
!GBD 07/03/2002 End

Begin-Heading 8
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'  
  Print $Reporting_Period      (3,) Center 
  Print ' BENEFIT'             (+2,{#column1})  
  Print 'BIRTH'                (+0,{#column6})
  Print 'SERVICE'              (+0,{#column7})
  Print 'BENEFIT'              (+0,{#column8})
  Print '  CONTRIB'            (+0,{#column14})
  
  Print 'PGM'                  (+1,{#column1})
  Print 'PLAN'                 (+0,{#column2})
  Print 'DEPT'                 (+0,{#column3})
  Print 'EMPLOYEE NAME'        (+0,{#column4})
  Print 'SSN'                  (+0,{#column5})
  Print 'ADDRESS'              (+0,{#column5A})
  Print 'DATE'                 (+0,{#column6})
  Print 'DATE'                 (+0,{#column7})
  Print 'TERMDT'               (+0,{#column8})
  Print 'SEX'                  (+0,{#column9})
  Print 'MARITAL'              (+0,{#column10})
  Print 'UNION'                (+0,{#column11})
  Print 'RATE'                 (+0,{#column12})
  Print 'MTD HOURS'            (+0,{#column13})
  Print '   AMOUNT'            (+0,{#column14})

  Print '-'                    (+1,{#column1},7)  Fill
  Print '-'                    (+0,{#column2},6)  Fill
  Print '-'                    (+0,{#column3},4)  Fill
  Print '-'                    (+0,{#column4},20)  Fill
  Print '-'                    (+0,{#column5},9)  Fill
  Print '-'                    (+0,{#column5A},45)  Fill
  Print '-'                    (+0,{#column6},8)  Fill
  Print '-'                    (+0,{#column7},7)  Fill
  Print '-'                    (+0,{#column8},8)  Fill
  Print '-'                    (+0,{#column9},3)  Fill
  Print '-'                    (+0,{#column10},7) Fill
  Print '-'                    (+0,{#column11},5) Fill
  Print '-'                    (+0,{#column12},6) Fill
  Print '-'                    (+0,{#column13},10) Fill
  Print '-'                    (+0,{#column14},12) Fill
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Do P100-Start
  Do P198-Open-Output                     
  Do P200-Main-Process
  Do P400-Select-From-Work-Table                    !create file for pension administrators
  Do P900-Drop-Work-Table
  Close 1
  Close 2
  Do P925-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report

Begin-Procedure P100-Start
#debug9 Show 'P100-Start'

  Move 'GEXPL604'                       To $ReportID
  Move 'UFCW INTL Pension Fund (Meat) - Audit Report' To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  Create-Array Name=Plan-Array Size=150 
    Field=plantype:Char:2               
    Field=benefitplan:Char:6
    Field=benefitpgm:Char:3
    Field=gexspclaccumcd:Char:3
    Field=serviceintervals:Number
    Field=hourlyrate:Number
    Field=flatrate:Number                            !monthly rate

  Clear-Array Name=Plan-Array

  If $prcs_process_instance = ''
    Do Ask-As-Of-Date 												!GEXBN_848_E115294_01 2011-12-13 
    Move '''' to $ProviderString
    Display 'Enter Vendor ID or leave blank to exit.'
    While 1=1
      Input $Provider maxlen=6 'Provider ID'
      Uppercase $Provider
      if RTRIM($Provider, ' ') = ''
	concat '''' with $ProviderString
	break
      end-if
      if $ProviderString <> ''''
	concat ''',''' with $ProviderString
      end-if
      concat $Provider with $ProviderString
    End-While

    Let $Vendor_ID = $Provider

    if $ProviderString = ''''''
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'ALL'
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2 = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA2 = 'ALL'
    else
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = 'bpt.Vendor_id In (' || $ProviderString || ')'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2 = 'bpt2.vendor_id In (' || $ProviderString || ')'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA2 = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
    end-if

    Move '''' to $PlanTypeString
    Display 'Enter Plan Type or leave blank to exit.'
    While 1=1
      Input $PlanType Maxlen=2 'Plan Type'
      Uppercase $PlanType
      if RTRIM($PlanType, ' ') = ''
	concat '''' with $PlanTypeString
	break
      end-if
      if $PlanTypeString <> ''''
	concat ''',''' with $PlanTypeString
      end-if
      concat $PlanType with $PlanTypeString
    End-While

    if $PlanTypeString = ''''''
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA2 = 'ALL'
    else
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = 'bpt.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = 'bpt2.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    end-if

  Else
    !GEXBN_848_E115294_01 2011-12-13 Vahini Katta Begins
    do Get-Run-Control
    move &asofdate to $AsofDate
    !GEXBN_848_E115294_01 2011-12-13 Vahini Katta Ends
    
    Let $GEXXX912_Plan_Type_Alias = 'bpt2.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt2.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters
    show $gexxx912_include_benefit_plan_criteria
    show #gex_rc_ben_plan_rows
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA

    Let $GEXXX912_Plan_Type_Alias = 'bpt.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters

    Let $GEXXX927_Vendor_ID_Alias = 'bpt2.vendor_id'
    Do GEXXX927-Select-Vendor-ID-Parameters
    Let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2 = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
    Let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA2 = $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA

    Let $GEXXX927_Vendor_ID_Alias = 'bpt.vendor_id'
    Do GEXXX927-Select-Vendor-ID-Parameters

  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  !GEXBN_848_E115294_01 2011-12-13 Vahini Katta Begins
  if isnull($AsofDate)
    Move $AsofToday to $AsofDate
  end-if
  show '$AsofDate:' $AsofDate
  !GEXBN_848_E115294_01 2011-12-13 Vahini Katta Ends
  If $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
    Display 'At least one plan type parameter must be specified'
    Stop
  End-If

  If $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
    Display 'At least one vendor id parameter must be specified'
    Stop
  End-If

  Let $X000_ORDER_BY = 'Benefit Plan, Benefit Program, Deptid, Rate, Name'

  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA  = '   $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA
  Show '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA      = '   $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA

  Do Get-Calendar-Year-Id    !getbalid.sqc

  Do P140-Get-Last-Confirmed-Pay
  Do P150-Print-Cover-Page
  Do P170-Load-Plan-Array
  Do P195-Create-Work-Table

!  The following code will show the Plan-Array
!  Let #j = 1
!  While #j <= #i
!    Get $PLANTYPE $BENEFITPLAN $BENEFITPGM $GEXSPCLACCUMCD #MONTHSSERVICE #HOURLYRATE #FLATRATE From Plan-Array(#j)
!    Show $PLANTYPE $BENEFITPLAN '  ' $BENEFITPGM '  ' $GEXSPCLACCUMCD '  ' #MONTHSSERVICE '  ' #HOURLYRATE '  ' #FLATRATE
!    Add 1 To #j
!  End-While

  Show 'Plan-Array successfully loaded'
End-Procedure


Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

Begin-Select
pay_period
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'fmMonth YYYY')           &period
Last_Day(pay_end_dt)                         &current_month_end
Last_Day(Add_Months(pay_end_dt,-1))          &prev_month_end
Last_Day(Add_Months(pay_end_dt,1))           &next_month_end

  Let #weeks-in-month   = &pay_period
  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period
!***format pay_end_dt from 30-DEC-2002 to 12/30/2002 for output file reporting period
!***  create format ccyymm for output file  
  Let $Year4 = '1'
  Do Format-DateTime($pay_end_dt, $pay_end_dt_reformat, {DEFMDY}, '', '')
  let $reporting_period_ccyy = substr($pay_end_dt_reformat,7,4)
  let $reporting_period_mm   = substr($pay_end_dt_reformat,1,2)
  Let $Reporting_Period_ccyymm = $reporting_period_ccyy || $reporting_period_mm
  show 'Reporting period sent on output file:  ' $Reporting_Period_ccyymm

  Let $current_month_begin = '01-'||Substr($pay_end_dt,4,8)
  Let $current_month_end   = &current_month_end
  Let $next_month_end      = &next_month_end
  Let $prev_month_begin    = '01-'||Substr(&prev_month_end,4,8)

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  Show 'Report will contain balances for the month of ' $Reporting_Period
  Show 'Last confirmed pay end date:  ' $pay_end_dt

  Do P145-Get-First-Pay-Period

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_calendar
Where  company                = 'GEI'
And    paygroup               = 'RET'
And    pay_end_dt             =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  company         = 'GEI'
       And    paygroup        = 'RET'
       !GEXBN_848_E115294_01 2011-12-13 Vahini Katta Begins
       !And    pay_end_dt     <= $AsOfToday
       And    pay_end_dt     <= $AsOfDate
       !GEXBN_848_E115294_01 2011-12-13 Vahini Katta Ends
       And    pay_confirm_run = 'Y')
And    pay_confirm_run = 'Y'
End-Select
End-Procedure


Begin-Procedure P145-Get-First-Pay-Period
#debug9 Show 'P145-Get-First-Pay-Period'
Begin-Select
pay_end_dt &first_pay_end_dt

  Let $first_pay_end_dt = &first_pay_end_dt
  Do Convert-to-DTU-Date($first_pay_end_dt, $first_pay_end_dt_dtu)

From   ps_pay_calendar

Where  company    = 'GEI'
And    paygroup   = 'RET'
And    To_Char(check_dt,'YYYYMM')
		  = To_Char(To_Date($pay_end_dt),'YYYYMM')
And    pay_period = 1
End-Select
End-Procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+2,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,2)
  Print '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = '  (+2,5)
  Print $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA     = '  (+2,5)
  Print $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA           (0,+2)

  Print 'Report will contain balances for the month of'       (+2,5)
  Print $Reporting_Period                                     (0,+1)
  Print 'Last confirmed pay end date:'                        (+2,5)
  Print $pay_end_dt                                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  New-Page
End-Procedure


Begin-Procedure P170-Load-Plan-Array
#debug9 Show 'P170-Load-Plan-Array'
show $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2
show $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2
Begin-Select
BDP.BENEFIT_PROGRAM
BPT2.PLAN_TYPE
BPT2.BENEFIT_PLAN
BDP.GEX_SPCL_ACCUM_CD
BDC.RATE_TYPE
BDC.RATE_TBL_ID

  If &BDC.RATE_TYPE = '2'
    Do P180-Flat-Rate
  Else
    If &BDC.RATE_TYPE = '4'
      Do P190-Service-Rate
    Else
      Display 'Unexpected rate type found.'
      Display 'Code to handle rate type ' Noline
      Display &BDC.RATE_TYPE Noline
      Display ' needs to be added.'
      Stop
    End-If
  End-If

FROM   PS_BEN_DEFN_PGM    BDP,
       PS_BEN_DEFN_COST   BDC,
       PS_BEN_DEFN_OPTN   BDO,
       PS_BENEF_PLAN_TBL  BPT2
WHERE [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2]
AND   [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA2]
AND    BPT2.EFFDT  = (SELECT MAX(EFFDT) FROM   PS_BENEF_PLAN_TBL
		       WHERE  PLAN_TYPE        = BPT2.PLAN_TYPE
	       	AND    BENEFIT_PLAN     = BPT2.BENEFIT_PLAN
       		AND    EFFDT           <= $pay_end_dt)
AND    BDO.BENEFIT_PROGRAM     = BDP.BENEFIT_PROGRAM
AND    BDO.EFFDT               = BDP.EFFDT
AND    BDO.PLAN_TYPE           = BPT2.PLAN_TYPE
AND    BDO.BENEFIT_PLAN        = BPT2.BENEFIT_PLAN
AND    BDC.BENEFIT_PROGRAM     = BDO.BENEFIT_PROGRAM
AND    BDC.EFFDT               = BDO.EFFDT
AND    BDC.PLAN_TYPE           = BDO.PLAN_TYPE
AND    BDC.OPTION_ID           = BDO.OPTION_ID
AND    BDP.EFFDT   =  (SELECT MAX(EFFDT) FROM   PS_BEN_DEFN_PGM
       		  WHERE  BENEFIT_PROGRAM  = BDP.BENEFIT_PROGRAM
       		  AND    EFFDT           <= $pay_end_dt)
ORDER BY BDP.BENEFIT_PROGRAM, BPT2.PLAN_TYPE, BPT2.BENEFIT_PLAN
End-Select
End-Procedure

Begin-Procedure P180-Flat-Rate
#debug9 Show 'P180-Flat-Rate'

   Let $flat_rate_found = 'N'								!Vendkxy changed for the S3 12651638

Begin-Select
FRT.BN_EMPLR_RATE										!Vendkxy changed for the S3 12651638
 Let $flat_rate_found = 'Y'									!Vendkxy changed for the S3 12651638
  If &FRT.BN_EMPLR_RATE = 0									!Vendkxy changed for the S3 12651638
    Do P185-GEX-Flat-Rate                 !this para puts hrly rate into array
  else
!                                   in normal circumstances, we would use the special accum
!                                   to determine hours... but plan administrator wants a
!                                   '1' hardcoded.  So I used the special accum code to indicate
!                                   a monthly rate.
    let $accum_cd = 'MMM'
    Add 1 To #i
    Put &BPT2.PLAN_TYPE &BPT2.BENEFIT_PLAN &BDP.BENEFIT_PROGRAM $accum_cd 0 0 &FRT.BN_EMPLR_RATE Into Plan-Array(#i)		!Vendkxy changed for the S3 12651638
  
  end-if

FROM PS_BN_RATE_DATA  FRT											!Vendkxy changed for the S3 12651638 
WHERE  FRT.RATE_TBL_ID     = &BDC.RATE_TBL_ID								!Vendkxy changed for the S3 12651638 
AND    FRT.EFFDT       =    (SELECT MAX(EFFDT)  FROM PS_BN_RATE_DATA					!Vendkxy changed for the S3 12651638 
       			 WHERE RATE_TBL_ID = FRT.RATE_TBL_ID					!Vendkxy changed for the S3 12651638 
       			 AND    EFFDT        <= $pay_end_dt)
End-Select
	!Vendkxy changed for the S3 12651638
	if  $flat_rate_found = 'N'
	 Do P185-GEX-Flat-Rate
	end-if
	!Vendkxy changed for the S3 12651638

End-Procedure
!******************************************************
!**********    this gets the hourly rate amount
Begin-Procedure P185-GEX-Flat-Rate
#debug9 Show 'P185-GEX-Flat-Rate'
Begin-Select
GFR.GEX_RATE_AMT

  Add 1 To #i
  Put &BPT2.PLAN_TYPE &BPT2.BENEFIT_PLAN &BDP.BENEFIT_PROGRAM '' 0 &GFR.GEX_RATE_AMT 0 Into Plan-Array(#i)

FROM   PS_GEX_FLAT_RATE GFR
WHERE  GFR.RATE_TBL_ID  = &BDC.RATE_TBL_ID
AND    GFR.EFFDT  =    (SELECT MAX(EFFDT) FROM   PS_GEX_FLAT_RATE
              	   WHERE  RATE_TBL_ID  = GFR.RATE_TBL_ID
      			   AND    EFFDT        <= $pay_end_dt)
End-Select
End-Procedure

Begin-Procedure P190-Service-Rate
#debug9 Show 'P190-Service-Rate'
!Show 'P190-Service-Rate'
Begin-Select
SCT.EMPLR_RATE
SCT.SERVICE_INTERVALS

  Add 1 To #i
  If Rtrim(&BDP.GEX_SPCL_ACCUM_CD, ' ') = ''
    If &BDP.BENEFIT_PROGRAM = 'R02'
      Let $accum_cd = 'RAB'
    Else
      Show 'P190-Service-Rate para: Special accum not found for benef pgm ' &BDP.BENEFIT_PROGRAM
      Stop
    End-If
  Else
    Let $accum_cd = &BDP.GEX_SPCL_ACCUM_CD
  End-If
  Put &BPT2.PLAN_TYPE &BPT2.BENEFIT_PLAN &BDP.BENEFIT_PROGRAM $accum_cd &SCT.SERVICE_INTERVALS
   0 &SCT.EMPLR_RATE Into Plan-Array(#i)

FROM   PS_SVC_RATE_TBL SRT,
       PS_SVC_COVG_TBL SCT
WHERE  SRT.SVC_RATE_TBL_ID     = &BDC.RATE_TBL_ID
AND    SRT.EFFDT  =   (SELECT MAX(EFFDT) FROM   PS_SVC_RATE_TBL
       		  WHERE  SVC_RATE_TBL_ID  = SRT.SVC_RATE_TBL_ID
       		  AND    EFFDT           <= $pay_end_dt)
AND    SCT.SVC_RATE_TBL_ID     = SRT.SVC_RATE_TBL_ID
AND    SCT.EFFDT  =   (SELECT MAX(EFFDT)  FROM   PS_SVC_COVG_TBL
       		  WHERE  SVC_RATE_TBL_ID  = SCT.SVC_RATE_TBL_ID
       		  AND    EFFDT           <= $pay_end_dt)
ORDER BY SERVICE_INTERVALS DESC
End-Select
End-Procedure

!******************************************
begin-procedure P195-create-work-table
!******************************************
#debug9 show 'P195-create-work-table'

 Let $tablename = 'PS_WT_GEXPL604'

begin-sql 
                               
CREATE TABLE [$tablename] 
       (BENEFIT_PROGRAM VARCHAR2(3)             NOT NULL, 
        BENEFIT_PLAN    VARCHAR2(6)             NOT NULL,   	
	DEPTID		VARCHAR2(10)		NOT NULL,                
	NAME		VARCHAR2(50)		NOT NULL,
	EMPLID		VARCHAR2(11)		NOT NULL,
        HOURLY_RATE     NUMBER(18,6)		NOT NULL,
        FLAT_RATE       NUMBER(18,6)		NOT NULL,
        HRS_MTD         NUMBER(18,6)		NOT NULL,
        CONTRIB_AMT     NUMBER(18,6)		NOT NULL,         
        ADDRESS1        VARCHAR2(55)            NOT NULL,                 
        ADDRESS2        VARCHAR2(55),                                  
        CITY            VARCHAR2(30)            NOT NULL,                
        STATE           VARCHAR2(6)             NOT NULL,                                                                             
        POSTAL          VARCHAR2(12)            NOT NULL, 
        UNION_CD        VARCHAR2(3)             NOT NULL,
        MAR_STATUS      VARCHAR2(1)             NOT NULL,    
        SEX             VARCHAR2(1)             NOT NULL,       
        BIRTHDATE       VARCHAR2(8)             NOT NULL,    
        HIRE_DT         VARCHAR2(8)             NOT NULL,    ! Service date       
        TERMINATION_DT  VARCHAR2(8),                         ! Benefit term date
        VENDOR_ID     VARCHAR2(6),
        PLAN_TYPE       VARCHAR2(2))   

        TABLESPACE TE7TOOLS001

End-Sql
End-Procedure

!******************************************
Begin-Procedure P198-Open-Output
!******************************************
#debug9 Show 'P198-Open-Output'
 
! Commented by Ujwal Dyasani 30Jan2017 - Begin
!Let $File1 = '{OUTFILE}' || Lower($ReportID) || '.dat'
! Commented by Ujwal Dyasani 30Jan2017 - End
!let $delim = ','  
encode '<009>' into $delim						!GEXBN_848_E117219_01 changes done by kundan yeedibilli
! Commented by Ujwal Dyasani 30Jan2017 - Begin
!Let $File2 = '{OUTFILE}' || Lower($ReportID) || '.xls'			!GEXBN_848_E117219_01 changes done by kundan yeedibilli
! Commented by Ujwal Dyasani 30Jan2017 - End
! Added by Ujwal Dyasani 30Jan2017 - Begin
Let $File2 = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\GEXPL604\' || Lower($ReportID) || '.xls'
! Added by Ujwal Dyasani 30Jan2017 - End
  ! Commented by Ujwal Dyasani 30Jan2017 - Begin
  !Open $File1 As 1 For-Writing Record=200:Fixed  Status=#Filestat

  !If #Filestat != 0
	!	Display 'Error Opening output file.  Program terminating.'
	!	Stop
	!End-If
  ! Commented by Ujwal Dyasani 30Jan2017 - End
  
 !GEXBN_848_E117219_01 changes done by kundan yeedibilli--Begin
 
 Open $File2 As 2 For-Writing Record=500:Fixed  Status=#Filestat1

 If #Filestat1 != 0
    Display 'Error Opening output file.  Program terminating.'
    Stop
  End-If

  string 'EMPLID' 'BENEFIT PROGRAM' 'BENEFIT PLAN' 'DEPTID' 'LASTNAME' 'FIRSTNAME' 'MIDINITIAL' 'HRS MTD' 'RATE' 'CONTRIB AMT' 'ADDRESS1' 
  'ADDRESS2' 'CITY' 'STATE' 'POSTAL' 'UNION CD' 'MARTIAL STATUS' 'SEX' 'BIRTHDATE' 'HIRE DT' 'TERMINATION DT' 'REPORTING PERIOD' by $delim into $csvfile
  write 2 from $csvfile
  
 !GEXBN_848_E117219_01 changes done by kundan yeedibilli--End 
show 'Opened filename for output: ' noline
! Commented by Ujwal Dyasani 30Jan2017 - Begin
!show $file1
! Commented by Ujwal Dyasani 30Jan2017 - End
show $file2	!GEXBN_848_E117219_01 changes done by kundan yeedibilli

end-procedure     

!******************************************
Begin-Procedure P200-Main-Process
!**    This para gets the contribution amount (which is a summed current deductions from ps_pay_deduction)
!******************************************
#debug9 Show 'P200-Main-Process'
 
  Let $Data-Found-Flag = 'N'
  show 'Balance Period:        ' #balance_period
  show 'Balance Year  :        ' #balance_year

Begin-Select
bpt.Vendor_id 
pl.deptid 
bpt.plan_type
bpt.benefit_plan
e.service_dt                                                                                                          
j.emplid
j.empl_rcd
j.effdt
j.company
j.paygroup
j.union_cd 
Sum(pd.ded_cur) &pd.ded_cur

  Let $Data-Found-Flag = 'Y'
  Let $p200_Deptid = &PL.DEPTID
  Let $p200_Vendor_ID = &BPT.Vendor_id
  Let #contribution_amt = &pd.ded_cur
 
  Do P205-Get-Personal-Data                !split out for performance reasons
  Do P220-Get-Benefit-Termination-Row

  Do Convert-to-DTU-Date(&e.service_dt, $service_dt_dtu)
  Do dtu-diff-months($service_dt_dtu, $first_pay_end_dt_dtu, #months_service)

 Let #j = 1
  While #j <= #i
    Get $PLANTYPE $BENEFITPLAN $BENEFITPGM $GEXSPCLACCUMCD #MONTHSSERVICE #HOURLYRATE #FLATRATE From Plan-Array(#j)
      If &BPT.PLAN_TYPE     = $PLANTYPE      And
	   &BPT.BENEFIT_PLAN  = $BENEFITPLAN   And
         #months_service   >= #MONTHSSERVICE
        Let $spcl_accum_cd = $GEXSPCLACCUMCD
        Let #hourly_rate = #HOURLYRATE
        Let #flat_rate   = #FLATRATE                      !monthly rate
        Break
      Else
        Add 1 To #j
      End-If
  End-While

!  show 'emplid, benefit plan, benefitpgm, $GEXSPCLACCUMCD: '  &j.emplid $BENEFITPLAN $BENEFITPGM $GEXSPCLACCUMCD
!  show 'deptid, vendor_id, plan, contrib amt:  ' &PL.DEPTID &BPT.VENDOR_ID &bpt.plan_type #contribution_amt

!******** this is what you would normally use to get hours, but just send hardcoded '1'.
!If $GEXSPCLACCUMCD = ''
!    Do P240-GEX-HRS-BAL
!  Else
!    Do P245-EARNINGS-BAL
!  End-If
!*************************** 
  If $GEXSPCLACCUMCD = 'MMM'            !its a monthly rate if accum = 'MMM'
    let #hrs_mtd = 1                    !administrators requested a 1    
  Else
     Do P240-GEX-HRS-BAL                    
  End-If

  Do P250-Format-Dates
  Do P255-Insert-Into-Work-Table

From   ps_employment             e,
       ps_job                    j,
       ps_benef_plan_tbl         bpt,
       ps_pay_cal_bal_id         pcbi,
       ps_pay_line               pl,
       ps_pay_check              pc,
       ps_pay_deduction          pd                            
Where  e.emplid    = j.emplid
And    e.empl_rcd  = j.empl_rcd
And    j.effdt     =      (Select Max(effdt) From   ps_job
       		 	Where  emplid             = j.emplid
       			And    empl_rcd         = j.empl_rcd
			       And    effdt             <= $pay_end_dt)
And    j.effseq    =      (Select Max(effseq) From   ps_job
			       Where  emplid             = j.emplid
			       And    empl_rcd         = j.empl_rcd
			       And    effdt              = j.effdt)
And    bpt.effdt   =		(Select Max(effdt)  From   ps_benef_plan_tbl
			       Where  plan_type          = bpt.plan_type
			       And    benefit_plan       = bpt.benefit_plan
			       And    effdt             <= $pay_end_dt
			       !And    vendor_id          = bpt.vendor_id) !GEXBN_848 changes done by Jyotsna Negi
             )                                           !GEXBN_848 changes done by Jyotsna Negi
And    pcbi.company      = pc.company
And    pcbi.paygroup     = pc.paygroup
And    pcbi.balance_id   = 'CY'
And    pcbi.balance_year = #balance_year
And    pcbi.balance_period   = #balance_period
And    pc.pay_end_dt     = pcbi.pay_end_dt
And    j.emplid          = pc.emplid
And    j.empl_rcd        = pc.empl_rcd
And    pd.company        = pc.company
And    pd.paygroup       = pc.paygroup
And    pd.pay_end_dt     = pc.pay_end_dt
And    pd.off_cycle      = pc.off_cycle
And    pd.page_num       = pc.page_num
And    pd.line_num       = pc.line_num
And    pd.sepchk         = pc.sepchk
And    pl.company        = pc.company
And    pl.paygroup       = pc.paygroup
And    pl.pay_end_dt     = pc.pay_end_dt
And    pl.off_cycle      = pc.off_cycle
And    pl.page_num       = pc.page_num
And    pl.line_num       = pc.line_num
And    pd.plan_type      = bpt.plan_type
And    pd.benefit_plan   = bpt.benefit_plan
And    pd.ded_class      = 'N'
And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA]
And   [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA]
Group By 
     bpt.vendor_id, 
     pl.deptid, 
     bpt.plan_type,
     bpt.benefit_plan,
     e.service_dt,                                                                                                          
     j.emplid,
     j.empl_rcd,
     j.effdt,
     j.company,
     j.paygroup,
     j.union_cd

End-Select
End-Procedure


Begin-Procedure P205-Get-Personal-Data 
#debug9 Show 'P205-Get-Personal-Data'
Begin-Select
per.name
per.birthdate
per.mar_status           
per.sex                  
per.address1             
per.address2             
per.city                 
per.state                                                                    
per.postal

from PS_PERSONAL_DATA PER
Where  per.emplid                = &j.emplid
 
end-select
end-procedure

!Begin-Procedure P210-Get-Union-Code        
!#debug9 Show 'P210-Get-Union-Code'
!Begin-Select
!jl.union_cd
!from ps_job_labor JL  
!WHERE   JL.EMPLID    = &J.EMPLID
!AND     JL.EMPL_RCD = &J.EMPL_RCD
!AND     JL.EFFDT                =
!      (SELECT MAX(EFFDT)
!       FROM   PS_JOB_LABOR
!       WHERE  EMPLID          = JL.EMPLID
!       AND    EMPL_RCD       = JL.EMPL_RCD
!       AND    EFFDT          <= $pay_end_dt)
!AND    Jl.EFFSEQ               =
!      (SELECT MAX(EFFSEQ)
!       FROM   PS_JOB_LABOR
!       WHERE  EMPLID          = JL.EMPLID
!       AND    EMPL_RCD       = JL.EMPL_RCD
!       AND    EFFDT           = JL.EFFDT)
!end-select
!end-procedure
                     
Begin-Procedure P220-Get-Benefit-Termination-Row
#debug9 Show 'P220-Get-Benefit-Termination-Row'
Begin-Select
hb.coverage_begin_dt

From   ps_health_benefit hb

Where  hb.emplid         = &j.emplid
And    hb.empl_rcd     = &j.empl_rcd
And    hb.plan_type      = &bpt.plan_type

And    hb.effdt          =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid     = &j.emplid
       And    empl_rcd  = &j.empl_rcd
       And    plan_type  = &bpt.plan_type)

And    hb.coverage_begin_dt Between $prev_month_begin
				And $next_month_end

And    hb.coverage_elect = 'T'
End-Select
End-Procedure


Begin-Procedure P240-GEX-HRS-BAL
#debug9 Show 'P240-GEX-HRS-BAL'

  Let #hrs_mtd = 0

Begin-Select
Sum(ghb.total_hrs) &ghb.total_hrs

  Let #hrs_mtd = &ghb.total_hrs

!From   ps_gex_hrs_bal ghb			!GEXBN_848_E117219_01 changes done by kundan yeedibilli
From   sysadm.PS_GEX_HRS_BAL_DPT ghb		!GEXBN_848_E117219_01 changes done by kundan yeedibilli
Where  ghb.company            = &j.company
And    ghb.paygroup           = &j.paygroup
And    ghb.deptid           = &pl.deptid		!GEXBN_848_E117219_01 changes done by kundan yeedibilli
And    ghb.emplid             = &j.emplid
And    ghb.empl_rcd          =  &j.empl_rcd
And    ghb.plan_type          = &bpt.plan_type
And    ghb.benefit_plan       = &bpt.benefit_plan
And    ghb.check_dt     Between $current_month_begin
			    And $current_month_end
End-Select
End-Procedure

Begin-Procedure P245-EARNINGS-BAL
#debug9 Show 'P245-EARNINGS-BAL'

  Let #hrs_mtd = 0

Begin-Select
eb.hrs_mtd

  Let #hrs_mtd = &eb.hrs_mtd

From   ps_earnings_bal eb

Where  eb.emplid             = &j.emplid
and    eb.empl_rcd           = &j.empl_rcd	!AXL 02/20/2003
And    eb.company            = &j.company
And    eb.balance_id         = 'CY'
And    eb.balance_year       = #balance_year
And    eb.balance_qtr        = #balance_qtr
And    eb.balance_period     = #balance_period
And    eb.spcl_balance       = 'Y'
And    eb.erncd              = $spcl_accum_cd
End-Select
End-Procedure


Begin-Procedure P250-Format-Dates
#debug9 Show 'P250-Format-Dates'
! format dates for Pension File     (need ccyymmdd)
  Do Format-DateTime(&e.service_dt, $Outfile_Hire_Dt, {DEFCMP}, '', '')
  Do Format-DateTime(&per.birthdate, $Outfile_Birthdt, {DEFCMP}, '', '')
  Do Format-DateTime(&hb.coverage_begin_dt, $Outfile_Term_dt, {DEFCMP}, '', '')

End-Procedure
 
!*********************************************
begin-procedure P255-insert-into-work-table
!*********************************************
#debug9 show 'P255-insert-into-work-table'
  show '*********************************'
  show $BENEFITPGM '|'
        &BPT.BENEFIT_PLAN '|' 
        $P200_DEPTID '|'	                  
	&PER.NAME '|'
        &J.EMPLID '|'      
	#HOURLY_RATE '|'
	#FLAT_RATE '|'
        #HRS_MTD '|'
        #CONTRIBUTION_AMT '|'                
        &PER.ADDRESS1 '|'             
        &PER.ADDRESS2 '|'              
        &PER.CITY '|'                  
        &PER.STATE '|'                 
        &PER.POSTAL '|'
        &J.UNION_CD '|'
        ' ' '|'   
        &PER.SEX '|'                         
        $OUTFILE_BIRTHDT '|'
        $OUTFILE_HIRE_DT '|'
        $OUTFILE_TERM_DT '|'
	&bpt.vendor_id '|' 
        &bpt.plan_type

Begin-SQL

Insert Into [$tablename]
   Values ($BENEFITPGM,
        &BPT.BENEFIT_PLAN, 
        $P200_DEPTID,	                  
	&PER.NAME,
        &J.EMPLID,      
	#HOURLY_RATE,
	#FLAT_RATE,
        #HRS_MTD,
        #CONTRIBUTION_AMT,                 
        &PER.ADDRESS1,              
        &PER.ADDRESS2,              
        &PER.CITY,                  
        &PER.STATE,                 
        &PER.POSTAL, 
        &J.UNION_CD,
        ' ',     		   !&PER.MAR_STATUS   since GE doesn't populate, send blank      
        &PER.SEX,                         
        $OUTFILE_BIRTHDT,          !ccyymmdd
        $OUTFILE_HIRE_DT,          !service date     ccyymmdd
        $OUTFILE_TERM_DT,          !benefit termdate ccyymmdd
	&bpt.vendor_id, 
        &bpt.plan_type)
        
End-SQL
End-Procedure

!********************************************
begin-procedure P400-Select-from-work-table
!********************************************
#debug9 show 'P400-Select-from-work-table'
 Move 0 To #recs-written
!GBD 07/03/2002 Begin
 Move 0 to #total-contrib
!GBD 07/03/2002 End

begin-SELECT 

WT.BENEFIT_PROGRAM 
WT.BENEFIT_PLAN       	
WT.DEPTID		                
WT.NAME		 
WT.EMPLID		 
WT.HOURLY_RATE      
WT.FLAT_RATE       
WT.HRS_MTD          
WT.CONTRIB_AMT         
WT.ADDRESS1              
WT.ADDRESS2                    
WT.CITY                       
WT.STATE                                                                                       
WT.POSTAL           
WT.UNION_CD         
WT.MAR_STATUS       
WT.SEX                     
WT.BIRTHDATE         
WT.HIRE_DT          
WT.TERMINATION_DT   
WT.vendor_id      
WT.PLAN_TYPE       
    
  do Get-Emp-Ssn (&WT.EMPLID, $Ssn_Out) !GEX_SEC_REENG 2015-04-29 Vahini Katta   
  Do P605-format-pension-record            
  Do P610-write-pension-record             
  Do P620-print-record                       
                      
FROM  [$tablename] WT

ORDER BY WT.BENEFIT_PROGRAM, WT.BENEFIT_PLAN, WT.DEPTID, WT.HOURLY_RATE, WT.FLAT_RATE, WT.NAME
					
end-SELECT
end-procedure
 
!*******   F O R M A T     P E N S I O N     R E C O R D
!*******  This para formats fields from the temporary work file before writing to pension file.          
Begin-Procedure P605-Format-Pension-Record
#debug9 Show 'P605-Format-Pension-Record'
   
  move &wt.name to $name
  let $name = RTRIM($name,' ')
  do Rotate-Name               !returned as 3 sep fields ($LastName $FirstName $MidInitial) 

  move 0 to #rate
  Move &wt.hourly_rate to #wt.hourly_rate
  if #wt.hourly_rate = 0                      !if hourly rate is 0, then its a monthly rate
     move &wt.flat_rate to #wt.flat_rate
     move #wt.flat_rate to #rate               
  else
     Move #wt.hourly_rate To #rate
  end-if
  move #rate to $string
  show '$string: ' $string
  show '$string_int $string_after_Dec:  ' $string_int $string_after_Dec
  unstring $string by '.' into $string_int $string_after_Dec
  move $string_int to $string_int_ed 099
  let $format_rate = $string_int_ed || Substr($string_after_Dec,1,4)   
 

  Move &wt.hrs_mtd To #wt.hrs_mtd  
  Move #wt.hrs_mtd To #hrs_mtd

  move #hrs_mtd to $string
  show '#hrs_mtd:                 ' $string
  unstring $string by '.' into $string_int $string_after_Dec
  move $string_int to $string_int_ed 09999
  let $format_hrs_mtd = $string_int_ed || Substr($string_after_Dec,1,2)   
 
  Move &wt.contrib_amt to #wt.contrib_amt

  move #wt.contrib_amt to $string
  unstring $string by '.' into $string_int $string_after_Dec
  move $string_int to $string_int_ed 09999
  let $format_contrib_amt = $string_int_ed || Substr($string_after_Dec,1,2) 
 
!  zero fill union code (requested by administrators)            !commented.  administrator
!  move '0000000' to $zero_fill 9999999                          !decided to accept GE union as is.
!  move &wt.union_cd to $unioncd
!  let $format_union_cd = $zero_fill || $unioncd   

end-procedure
  
 
!*******   WRITE OUTPUT FILE for PENSION ADMINISTRATORS      
Begin-Procedure P610-Write-Pension-Record
#debug9 Show 'P610-Write-Pension-Record'
	
	! Commented by Ujwal Dyasani 30Jan2017 - Begin
	  !Write 1 From $Ssn_Out:9     !GEX_SEC_REENG 2015-04-29 Vahini Katta
      !           !&wt.emplid:9  !GEX_SEC_REENG 2015-04-29 Vahini Katta
      !           &wt.benefit_program:3 
      !           &wt.benefit_plan:6
      !           &wt.deptid:5              
      !           $LastName:20 
      !           $FirstName:15
      !           $MidInitial:1 
      !           $format_hrs_mtd:7               !99999.99 no decimal point
      !           $format_rate:7                  !999.9999   no decimal point
      !           $format_contrib_amt:7           !99999.99 no decimal point
      !           &wt.address1:25
      !           &wt.address2:25
      !           &wt.city:17
      !           &wt.state:2
      !           &wt.postal:9
      !           &wt.union_cd:10                           !ge union code
      !           &wt.mar_status:1                          !this will be blank since ge doesnt store marital
      !           &wt.sex:1
      !           &wt.Birthdate:8                           !ccyymmdd
      !           &wt.Hire_Dt:8                             !ccyymmdd
      !           &wt.Termination_Dt:8                      !ccyymmdd
      !           $Reporting_Period_ccyymm:6                !ccyymm
      !	
      !Status=#Filestat
      !If #Filestat != 0
      !  Display 'Error Writing output file.  Program terminating.'
      !  Stop
      !End-If
    ! Commented by Ujwal Dyasani 30Jan2017 - End
  
  !GEXBN_848_E117219_01 changes done by kundan yeedibilli-- Begin
  !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
    !string &wt.emplid &wt.benefit_program &wt.benefit_plan &wt.deptid $LastName $FirstName $MidInitial #WT.HRS_MTD #rate &WT.CONTRIB_AMT &wt.address1 &wt.address2 
    string $Ssn_Out &wt.benefit_program &wt.benefit_plan &wt.deptid $LastName $FirstName $MidInitial #WT.HRS_MTD #rate &WT.CONTRIB_AMT &wt.address1 &wt.address2
     &wt.city &wt.state &wt.postal &wt.union_cd &wt.mar_status &wt.sex &wt.Birthdate &wt.Hire_Dt &wt.Termination_Dt $Reporting_Period_ccyymm by $delim into $csvfile
     !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
    write 2 from $csvfile  
                
  !GEXBN_848_E117219_01 changes done by kundan yeedibilli--End

!GBD 07/03/2002 Begin
!   Add 1 To #recs-written
!GBD 07/03/2002 End
End-Procedure

!*******************************************
!*****    PRINT AUDIT TRAIL REPORT    ******
!*******************************************
Begin-Procedure P620-Print-Record
#debug9 Show 'P620-Print-Record'

   print &WT.BENEFIT_PROGRAM   (+1,{#column1}) 
   print &WT.BENEFIT_PLAN      (+0,{#column2})      	
   print &WT.DEPTID            (+0,{#column3})		                
   print &WT.NAME	       (+0,{#column4})	 
   print &WT.EMPLID            (+0,{#column5})
   print &WT.ADDRESS1          (+0,{#column5A},25)
   print &WT.CITY              (+0,{#column5B},10)
   print &WT.STATE             (+0,{#column5C},2)
   print &WT.POSTAL            (+0,{#column5D},5)
   print &WT.BIRTHDATE         (+0,{#column6})         
   print &WT.HIRE_DT           (+0,{#column7})          
   print &WT.TERMINATION_DT    (+0,{#column8})
   
   print &WT.SEX               (+0,{#column9})               
   print &WT.MAR_STATUS        (+0,{#column10})    		 
   print &WT.UNION_CD          (+0,{#column11})    
   print #rate                 (+0,{#column12}) Edit 99.9999      
   print #WT.HRS_MTD           (+0,{#column13}) Edit 9999.99          
   print &WT.CONTRIB_AMT       (+0,{#column14}) Edit 99,999.99 

   add +1 to #recs-written      
!GBD 07/03/2002 Begin
   Add #wt.contrib_amt to #total-contrib
!GBD 07/03/2002 End
             
End-Procedure 
  
!************************************
begin-procedure P900-drop-work-table
!************************************
#debug9 show 'P900-drop-work-table' 

begin-sql 
DROP TABLE [$tablename]
end-sql
end-procedure
 

Begin-Procedure P925-Finish
#debug9 Show 'P925-Finish'

  If $Data-Found-Flag <> 'Y'
    New-Page
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
  else
    show 'Number of Pension Employees on Interface:  ' #recs-written
    Print 'Number of Pension Employees:  '      (+3,{#column1})       
    Print #recs-written   			(+0,{#column5})  edit 99,999
!GBD 07/03/2002 Begin
    show 'Total Contribution Amount on Interface:    ' #total-contrib
    Print 'Total Contributions:          '      (+0,{#column11})
    Print #total-contrib                        (+0,{#column15})  edit bbb,bbb,b99.99
!GBD 07/03/2002 End
  end-if

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
End-Procedure


#include 'gexxx912.sqc'  !Get plan type multiple row table
#include 'gexxx927.sqc'  !Get vendor id multiple row table
#include 'getdptnm.sqc'  !Get department name
#include 'getprvnm.sqc'  !Get vendor id name
#include 'getactnm.sqc'  !Get account code name
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'rotname3.sqc'  !Routines to rotate name                      
#include 'gexxx951.sqc'  !Oracle dbase name                                                 
#Include 'gexxx971.sqc'  !Email and FTP parameters
#include 'gexrnctl.sqc'  !GEXBN_848_E115294_01 2011-12-13 Vahini Katta
#Include 'askaod.sqc'    !Ask-As-Of-Date routine
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta