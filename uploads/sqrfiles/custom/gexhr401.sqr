!***********************************************************************
! GEXHR401 Generate daily report for Request from Leave, email results *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! PeopeSoft, Inc.; it is not to be copied, reproduced, or transmitted  *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of PeopleSoft.                        *
!                                                                      *
! Copyright (c) 1988-1997 PeopleSoft, Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
!      $Date:: 10/20/09         $                                      *
!  $Revision:: 0                $                                      *
!  $Workfile:: GEXHR401.SQR     $                                      *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS    DATE		DESCRIPTION OF THE CHANGE      *        
!***********************************************************************
!           RKK      12/20/2010         INITIAL CREATION               *
! GEX001    VK       01/12/2011         Add Leave Request Reason       *
!GEX_SEC_REENG TCSXSSS 05/11/2015    Changes related to                *
!				       gex_emplid_mapp/7 digit emplid  *
! Ujwal Dyasani		  10-Jan-2017		Updated "psmail" command to    *
!										Linux after PT852 upgrade	   *
!***********************************************************************
!***********************************************************************
#include 'setenv.sqc' 				! set enviroment
!***********************************************************************
!***********************************************************************

#Include 'setup32.sqc'   !Printer and page-size initialization

Begin-Report
#debug9 Show 'Begin-Report'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc
  
  Let $Quote = '"'
  Let $Comma = ','

  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan

  let $ReportID = 'GEXHR401'

  do openfile
  do Select-Deptid-Parameters

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
 
end-report
!***********************************************************************
Begin-Procedure Openfile
!***********************************************************************

   Let $fileout = '{OUTFILE}' || 'REQ_FOR_LOA.csv'   
   Open $fileout As 1 For-Writing Record=1500:Vary
   Move 'GEXHR401'			To $Report  
   let $ReportTitle = 'Request for Leave Report'
   encode '<012>' into $newline 
   write 1 from $Quote '                     ' $Quote $Comma
                $Quote 'Report ID:           ' $Quote $Comma
                $Quote $Report                 $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote $ReportTitle            $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote $Comma
                $Quote '                     ' $Quote 

End-Procedure


!***********************************************************************
!***********************************************************************
begin-procedure select-req-for-loa
#debug9 show 'select-req-for-loa'
  Move '' to $Leave_Reason !GEX001
begin-SELECT DISTINCT
B.NAME_DISPLAY
D.EMPLID
A.SUBMITTED_DT
A.BEGIN_DT
A.GEX_UNKNOWN
A.LASTUPDOPRID
C.OPRDEFNDESC
A.LASTUPDDTTM
A.NOTIFY_FLAG
A.NOTIFIED_DT
A.EMAIL_ADDR
A.ADDRESS1
A.ELIGIBLE
A.ADDRESS2
A.CITY
A.STATE
A.POSTAL
A.LOCATION
A.JOBCODE
A.DEPTID
A.ACCT_CD
A.PHONE
A.BENEFIT_PROGRAM 
A.GEX_LEAVE_REQUEST
   
  !GEX001-Begins 
  let $FieldName  = 'GEX_LEAVE_REQUEST'
  let $FieldValue = &A.GEX_LEAVE_REQUEST
  do Read-Translate-Table
  move $XlatLongName to $Leave_Reason
  !GEX001-Ends 
	Do Write-Record
	
FROM PS_GEX_LEAVE_RQST A, PS_PERSONAL_DATA B, PSOPRDEFN C, PS_GEX_EMPLID_MAPP D 
  WHERE A.EMPLID = B.EMPLID 
     AND [$INCLUDE_DEPTID_CRITERIA]
     AND A.SUBMITTED_DT BETWEEN sysdate - 30 AND sysdate
     AND A.LASTUPDOPRID = C.OPRID 
     !AND D.SSN   = A.EMPLID		!GEX_SEC_REENG TCSXSSS 05/11/2015
     AND D.EMPLID   = A.EMPLID		!GEX_SEC_REENG TCSXSSS 05/11/2015

end-SELECT

do create-to-list
do send-it           
close 1 
do openfile 

end-procedure
!***********************************************************************
!***********************************************************************
begin-procedure  create-to-list
#debug9 display 'create-to-list'

   let $to_list = &GEX_EMAIL_ADDR.EMAILID || ';'
   
end-procedure
!***********************************************************************
!***********************************************************************
begin-procedure  send-it
#debug9 display 'send-it'

let $TEXT_ID = $ReportID || '_SUBJ' 
do daily-req-for-loa-verbiage
let $subject = &A.HR_SSTEXT_TEXT || $DEPTID

let $enter = chr(10)

let $TEXT_ID = $ReportID || '_BODY' 
do daily-req-for-loa-verbiage

let $body_txt = &A.HR_SSTEXT_TEXT 
! Commented by Ujwal 20161223 - Begin
!let $mail-cmd = 'psmail -TO"'||$to_list||'" -SUBJECT"'||$subject||'" -BODY"'||$body_txt||'" -FILE"'||$fileout
! Commented by Ujwal 20161223 - End

! Added by Ujwal 20161223 - Start
let $mail-cmd = 'psmail -TO"'||$to_list||'" -FROM"pshrprd_pshrproddb1@gianteagle.com" -SUBJECT"'||$subject||'" -BODY"'||$body_txt||'" -FILE"'||$fileout||'"'
show 'mail comand: ' $mail-cmd
! Added by Ujwal 20161223 - End

CALL SYSTEM USING $mail-cmd #Status 

end-procedure
!***********************************************************************
!***********************************************************************
begin-procedure  daily-req-for-loa-verbiage
#debug9 display 'daily-req-for-loa-verbiage'

Begin-Select
A.HR_SSTEXT_TEXT

 from ps_HR_SSTEXT_TEXT A 
 WHERE A.OBJECTOWNERID = 'HHR' 
 AND A.HR_SSTEXT_SUB_ID = 'GEX' 
 AND A.TEXT_ID = $TEXT_ID 
 AND A.EFFDT = (SELECT MAX(B.EFFDT) FROM PS_HR_SSTEXT_EFFDT B 
 WHERE B.OBJECTOWNERID = A.OBJECTOWNERID 
 AND B.HR_SSTEXT_SUB_ID = A.HR_SSTEXT_SUB_ID 
 AND B.TEXT_ID = A.TEXT_ID AND B.EFFDT <= SYSDATE)

End-Select
 
end-procedure

!**********************************************************************
!*       Write Header                                                 *
!**********************************************************************
Begin-procedure Write-Header
#debug9 show 'Write-Header'
  
  Write 1 from $newline
  write 1 from    $Quote 'Team Member Name     ' $Quote $Comma
                  $Quote 'Team Member ID       ' $Quote $Comma
                  $Quote 'Request Submit Date  ' $Quote $Comma
                  $Quote 'Expected Begin Date  ' $Quote $Comma
                  $Quote 'Begin Date Unknown   ' $Quote $Comma
                  $Quote 'Submitted By Emplid  ' $Quote $Comma
                  $Quote 'Submitted by Name    ' $Quote $Comma
                  $Quote 'Submit Date and Time ' $Quote $Comma
                  $Quote 'Notified             ' $Quote $Comma
                  $Quote 'Date Notified        ' $Quote $Comma
                  $Quote 'Email	Address        ' $Quote $Comma
                  $Quote 'Address 1            ' $Quote $Comma
                  $Quote 'Address 2            ' $Quote $Comma
                  $Quote 'City                 ' $Quote $Comma
                  $Quote 'State                ' $Quote $Comma
                  $Quote 'Postal               ' $Quote $Comma
                  $Quote 'Eligibla             ' $Quote $Comma
                  $Quote 'Location             ' $Quote $Comma
                  $Quote 'Job Code             ' $Quote $Comma
                  $Quote 'DeptID               ' $Quote $Comma
                  $Quote 'Combo Code           ' $Quote $Comma
                  $Quote 'Phone                ' $Quote $Comma
                  !GEX001-Begins
                  !$Quote 'Ben Progrm           ' $Quote 
                  $Quote 'Ben Progrm           ' $Quote $Comma
                  $Quote 'Reason for Leave Rqst' $Quote
                  !GEX001-Ends
                  
 
do Reset-Variable

end-procedure !Write-File-Header

!**********************************************************************
!*       Write File                                                   *
!**********************************************************************

Begin-procedure Write-Record
#debug9 show 'Write-Record'
   
   let $submit_dt = edit(&A.SUBMITTED_DT,'MM/DD/YYYY')
   let $begin_dt = edit(&A.BEGIN_DT,'MM/DD/YYYY')
   let $LASTUPDDTTM = edit(&A.LASTUPDDTTM,'MM/DD/YYYY_HH:MI:SS_AM')
   let $NOTIFIED_DT = edit(&A.NOTIFIED_DT,'MM/DD/YYYY')
 
   write 1 from    $Quote &B.NAME_DISPLAY         $Quote $Comma
                   $Quote &D.EMPLID               $Quote $Comma
                   $Quote $submit_dt              $Quote $Comma
                   $Quote $begin_dt               $Quote $Comma
                   $Quote &A.GEX_UNKNOWN          $Quote $Comma
                   $Quote &A.LASTUPDOPRID         $Quote $Comma
                   $Quote &C.OPRDEFNDESC          $Quote $Comma
                   $Quote $LASTUPDDTTM            $Quote $Comma
                   $Quote &A.NOTIFY_FLAG          $Quote $Comma
                   $Quote $NOTIFIED_DT            $Quote $Comma
                   $Quote &A.EMAIL_ADDR           $Quote $Comma
                   $Quote &A.ADDRESS1             $Quote $Comma
                   $Quote &A.ADDRESS2             $Quote $Comma
                   $Quote &A.CITY                 $Quote $Comma
                   $Quote &A.STATE                $Quote $Comma
                   $Quote &A.POSTAL               $Quote $Comma
                   $Quote &A.ELIGIBLE             $Quote $Comma
                   $Quote &A.LOCATION             $Quote $Comma
                   $Quote &A.JOBCODE              $Quote $Comma
                   $Quote &A.DEPTID               $Quote $Comma
                   $Quote &A.ACCT_CD              $Quote $Comma
                   $Quote &A.PHONE                $Quote $Comma
                   !GEX001-Begins
                   !$Quote &A.BENEFIT_PROGRAM      $Quote 
                   $Quote &A.BENEFIT_PROGRAM      $Quote $Comma
                   $Quote $Leave_Reason           $Quote
                   !GEX001-Ends
                                              
do Reset-Variable

end-procedure
!***********************************************************************
!***********************************************************************
Begin-Procedure Reset-Variable
#debug9 Show 'Reset-Variable'

Let $gexhr401  =  '' 

End-Procedure  Reset-Variable
!***********************************************************************
!***********************************************************************
Begin-Procedure Select-Deptid-Parameters
#debug9 Show 'Select-Deptid-Parameters'

Begin-Select

DISTINCT(GEX_EMAIL_ADDR.EMAILID) &gex_email_addr.emailid
  
  do Select-Deptids
  do Write-Header
  do select-req-for-loa	

From  SYSADM.PS_GEX_EMAIL_ADDR GEX_EMAIL_ADDR

Where Rtrim(GEX_EMAIL_ADDR.DEPTID, ' ') != ' ' 
And  RTRIM(GEX_EMAIL_ADDR.GEX_EMAILTYPE, ' ') in ('RLOA')

End-Select

close 1 

End-Procedure
!***********************************************************************
!***********************************************************************
begin-procedure  Select-Deptids
#debug9 display 'Select-Deptids'

  Let #GEX_RC_DEPTID_ROWS = 0
  Let $INCLUDE_DEPTID_CRITERIA = ''
  Let $INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
  let $Department_Alias = 'A.DEPTID'
  
Begin-Select

GEX_EMAIL_ADDR1.DEPTID

  Add 1 To #GEX_RC_DEPTID_ROWS

  If #GEX_RC_DEPTID_ROWS = 1
    Let $INCLUDE_DEPTID_CRITERIA = '(' || $Department_Alias || ' In ('''     || &GEX_EMAIL_ADDR1.DEPTID || ''''
  Else
    Let $INCLUDE_DEPTID_CRITERIA = $INCLUDE_DEPTID_CRITERIA || ',''' || &GEX_EMAIL_ADDR1.DEPTID  || ''''
  End-If

From  SYSADM.PS_GEX_EMAIL_ADDR GEX_EMAIL_ADDR1
Where GEX_EMAIL_ADDR1.EMAILID      = &GEX_EMAIL_ADDR.EMAILID
And  RTRIM(GEX_EMAIL_ADDR1.GEX_EMAILTYPE, ' ') in ('RLOA')

End-Select

  If #GEX_RC_DEPTID_ROWS = 0
    Let $INCLUDE_DEPTID_CRITERIA = $Department_Alias || ' = ' || $Department_Alias
  Else
    Let $INCLUDE_DEPTID_CRITERIA = $INCLUDE_DEPTID_CRITERIA || '))'
    Let $INCLUDE_DEPTID_DISPLAY_CRITERIA = $INCLUDE_DEPTID_CRITERIA
  End-If

End-Procedure

!***********************************************************************
!***********************************************************************
#include 'reset.sqc'        ! Reset printer
#include 'curdttim.sqc'     ! Current Date and Time
#Include 'datetime.sqc'     ! Routines for date and time formatting
#Include 'number.sqc'       !Routines to format numbers
#include 'getlocnm.sqc'     !Get-Location-Name procedure
#Include 'stdapi.sqc'       !Routines to Update Run Status
#include 'getdptnm.sqc'     !Get-Department-Name procedure
#include 'getjobtl.sqc'     !Get-Job-Title procedure
#include 'readxlat.sqc'     !Read-Translate-Table procedure
#Include 'getposnm.sqc'     !Get-Position-Title
