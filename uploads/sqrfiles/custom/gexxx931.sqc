!**************************************************************************
!  GEXXX931:  Retrieve "Multiple" Company/Paygroup/Deptid Run Controls    *
!               and return a Formatted Where-Clause                       *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX931_Company_Alias                                              *
!    $GEXXX931_Paygroup_Alias                                             *
!    $GEXXX931_Deptid_Alias                                               *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA                           *
!    $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA                           *
!    $GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA                   *
!    $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA                   *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX931_Company_Alias  = 'J.COMPANY'                           *
!    Let $GEXXX931_Paygroup_Alias = 'J.PAYGROUP'                          *
!    Let $GEXXX931_Deptid_Alias   = 'J.DEPTID'                            *
!    Do GEXXX931-Select-Paygroup-Deptid-Parameters                        *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA]                   *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		02/15/99 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX931-Select-Paygroup-Deptid-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_PAYGROUP_DEPTID_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = ''
  Let $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA = ''
  Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_PAYGROUP_DEPTID.COMPANY
GEX_RC_PAYGROUP_DEPTID.PAYGROUP
GEX_RC_PAYGROUP_DEPTID.DEPTID

  Add 1 To #GEX_RC_PAYGROUP_DEPTID_ROWS

  If Rtrim(&GEX_RC_PAYGROUP_DEPTID.PAYGROUP, ' ') = ''
    Let $Include_Condition =   '(' || $GEXXX931_Company_Alias  || ' = '''  || &GEX_RC_PAYGROUP_DEPTID.COMPANY  || ''')'
    Let $Exclude_Condition =   '(' || $GEXXX931_Company_Alias  || ' != ''' || &GEX_RC_PAYGROUP_DEPTID.COMPANY  || ''')'
  Else
    If Rtrim(&GEX_RC_PAYGROUP_DEPTID.DEPTID, ' ') = ''
      Let $Include_Condition = '(' || $GEXXX931_Company_Alias  || ' = '''  || &GEX_RC_PAYGROUP_DEPTID.COMPANY  ||
                         ''' And ' || $GEXXX931_Paygroup_Alias || ' = '''  || &GEX_RC_PAYGROUP_DEPTID.PAYGROUP || ''')'
      Let $Exclude_Condition = '(' || $GEXXX931_Company_Alias  || ' != ''' || &GEX_RC_PAYGROUP_DEPTID.COMPANY  ||
                          ''' Or ' || $GEXXX931_Paygroup_Alias || ' != ''' || &GEX_RC_PAYGROUP_DEPTID.PAYGROUP || ''')'
    Else
      Let $Include_Condition = '(' || $GEXXX931_Company_Alias  || ' = '''  || &GEX_RC_PAYGROUP_DEPTID.COMPANY  ||
                         ''' And ' || $GEXXX931_Paygroup_Alias || ' = '''  || &GEX_RC_PAYGROUP_DEPTID.PAYGROUP ||
                         ''' And ' || $GEXXX931_Deptid_Alias   || ' = '''  || &GEX_RC_PAYGROUP_DEPTID.DEPTID   || ''')'
      Let $Exclude_Condition = '(' || $GEXXX931_Company_Alias  || ' != ''' || &GEX_RC_PAYGROUP_DEPTID.COMPANY  ||
                          ''' Or ' || $GEXXX931_Paygroup_Alias || ' != ''' || &GEX_RC_PAYGROUP_DEPTID.PAYGROUP ||
                          ''' Or ' || $GEXXX931_Deptid_Alias   || ' != ''' || &GEX_RC_PAYGROUP_DEPTID.DEPTID   || ''')'
    End-If
  End-If

  If #GEX_RC_PAYGROUP_DEPTID_ROWS = 1
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = '(' || $Include_Condition
    Let $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA = $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA || ' Or '  || $Exclude_Condition
  End-If

From  PS_GEX_RC_PYGP_DPT GEX_RC_PAYGROUP_DEPTID

Where GEX_RC_PAYGROUP_DEPTID.OPRID                 = [$OprID]
And   GEX_RC_PAYGROUP_DEPTID.RUN_CNTL_ID           = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_PAYGROUP_DEPTID.COMPANY,  ' ') != ' '
!And  Rtrim(GEX_RC_PAYGROUP_DEPTID.PAYGROUP, ' ') != ' '
!And  Rtrim(GEX_RC_PAYGROUP_DEPTID.DEPTID, ' ')   != ' '

End-Select

  If #GEX_RC_PAYGROUP_DEPTID_ROWS = 0
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = $GEXXX931_Company_Alias  || ' = '  || $GEXXX931_Company_Alias  || ' And ' ||
                                                     $GEXXX931_Paygroup_Alias || ' = '  || $GEXXX931_Paygroup_Alias || ' And ' ||
                                                     $GEXXX931_Deptid_Alias   || ' = '  || $GEXXX931_Deptid_Alias
    Let $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA = $GEXXX931_Company_Alias  || ' != ' || $GEXXX931_Company_Alias  || ' Or ' ||
                                                     $GEXXX931_Paygroup_Alias || ' != ' || $GEXXX931_Paygroup_Alias || ' Or ' ||
                                                     $GEXXX931_Deptid_Alias   || ' != ' || $GEXXX931_Deptid_Alias
  Else
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA =         $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA || ')'
    Let $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA =         $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA || ')'
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA = $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA
    Let $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA = $GEXXX931_EXCLUDE_PAYGROUP_DEPTID_CRITERIA
  End-If

End-Procedure
