!***********************************************************************
! GEXBN811:  BP R17, W05, W06, R36, W10                                *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXBN811 :            Benefit Programs R17, W05, W06, R36, W10       *
!                                                                      *
! Narrative:            This report will generate a list of employees  *
!                       belonging to benefit programs R17, W05, or W06 *
!                       who have weekly deductions without any pay.    *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           personal_data                                  *
!                       job                                            *
!                       ben_prog_partic                                *
!                       health_benefit                                 *
!                       pay_earnings                                   *
!                       pay_oth_earns                                  *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         None                                         *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
!                                                                      *
! JDH       08/25/98    Initial Creation                               *
! SXK       09/30/98	Modified $AsOfDate as the latest pay end date  *
! SXK       12/02/98    Should be selecting paid leave EEs also        *
! JDH       01/05/99    Performance Tuning                             *
! JDH       05/05/99    Added logic to only look at on-cycle checks.   *
! AXL       11/28/2000   Benefit programs perviously hard-coded into   *
!                        logic.  Added code to select Benefit Program  *
!                        from user prompts or panel run control.       *
! CJH       11/28/2000  Added BPs R36 and W10 logic for COD            *
! GBD       07/15/2002  Added Fieldvalue to effective dated selection  *
!                       of the Load-Lookup procedure.                  *
! ISDVNPK 08/23/2007 Changed XLATTABLE to PSXLATITEM as part of 9.0 upgrade 
! VENDAXG 07/27/2009  Added DEPTID and Jobcode Descr from ITG#62876    *
! GEX_SEC_REENG 2015-06-24 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


#define #column1  001

!Vendaxg Added for ITG#62876 on 07/27/2009 - Begin
!#define #column2  036
!#define #column3  108
#define #column2  015
#define #column3  050
#define #column4  070
#define #column5  080
!Vendaxg Added for ITG#62876 on 07/27/2009 - End


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 7
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'

  Print 'As Of Date: '           (3,1)
  Do Format-DateTime($AsOfDate, $out, {DEFDATE}, '', '')
  Print $out ()

  If #BP-recs-printed > 0
    Print 'Benefit Program: '    (4,1)
    Print $save_benefit_program  ()
  End-If

  Print $ReportTitle2            (3,) Center
  Print $ReportTitle3            (4,) Center

  Print 'EMPLOYEE ID'            (+2,{#column1})
  Print 'NAME'                   (+0,{#column2})
  Print 'EMPLOYEE STATUS'        (+0,{#column3})
  !Vendaxg Added for ITG#62876 on 07/27/2009 - Begin
  Print 'DEPTID'		 (+0,{#column4})
  Print 'JOBCODE DESCR'		 (+0,{#column5})
!Vendaxg Added for ITG#62876 on 07/27/2009 - End
  Print '-'                      (+1,{#column1},11)  Fill
  Print '-'                      (+0,{#column2},50)  Fill
  Print '-'                      (+0,{#column3},25)  Fill
  !Vendaxg Added for ITG#62876 on 07/27/2009 - Begin
  Print '-'                      (+0,{#column4},15)  Fill
  Print '-'                      (+0,{#column5},30)  Fill
  !Vendaxg Added for ITG#62876 on 07/27/2009 - End
End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXBN811'
!  Let $ReportTitle   = 'Benefit Programs R17, W05, W06'
  Let $ReportTitle   = 'Benefit Programs R17, W05, W06, R36, W10'
  Let $ReportTitle2  = 'These employee deductions/contributions need to'
  Let $ReportTitle3  = 'be rolled back, before the pay cycle is confirmed'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show ' '
  Show 'Report Began:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime

  Do Get-Pay-End-Dt
  Let $AsOfDate = $pay_end_dt

  Do P130-Get-Xlat-Lookup
End-Procedure


Begin-Procedure Get-Pay-End-Dt
  Move 'N' To $found

Begin-Select Loops=1 On-Error=P260-sql-error-found('Get-Pay-End-Dt')
PAY.PAY_BEGIN_DT
PAY.PAY_END_DT

  Move &PAY.PAY_END_DT   To $pay_end_dt
  Move &PAY.PAY_BEGIN_DT To $pay_begin_dt
  Display ' '
  Display 'Pay End Date: ' noline
  Display $pay_end_Dt
  Move 'Y' To $found

FROM   PS_PAY_CALENDAR PAY

WHERE  PAY.PAY_END_DT         =
      (SELECT MAX(PAY_END_DT)
       FROM   PS_PAY_CALENDAR
       WHERE  PAY_END_DT     <= $AsOfToday
       AND    PAY_SHEETS_RUN  = 'Y'
       !01/02/2001 - add criteria to ignore off-cycle calendar entries
      AND PAY_OFF_CYCLE_CAL = 'N')
      AND PAY.PAY_OFF_CYCLE_CAL = 'N'
      !01/02/2001 - end 
End-Select

  If $found = 'N'
    Display 'No Previous Pay Cycle as of: ' noline
    Display $AsOfToday
    Stop
  End-If
End-Procedure


Begin-Procedure P130-Get-Xlat-Lookup
#debug9 Show 'P130-Get-Xlat-Lookup'
  Let $Where = '     x.fieldname   = ''EMPL_STATUS''' ||
               ' And x.eff_status  = ''A''' ||
 !              ' And x.language_cd = ''ENG''' ||
               ' And x.effdt       = (Select Max(effdt)' ||
               '                      From   PSxlatitem x1' ||
               '                      Where  x1.fieldname    = x.fieldname' ||
!GBD 07/15/2002 Begin
!               '                      And    x1.language_cd  = ''ENG''' ||
     !          '                      And    x1.language_cd  = x.language_cd' ||
               '                      And    x1.fieldvalue   = x.fieldvalue'  ||
!GBD 07/15/2002 End
               '                      And    x1.effdt       <= ''' || $AsOfDate || '''' ||
               '                      And    x1.eff_status   = ''A'')'

  Load-Lookup
    Name=XlatLookup
    Rows=11
    Table='PSXLATITEM X'   ! ISDVNPK 08/23/2007 Changed XLATTABLE to PSXLATITEM as part of 9.0 upgrade 
    Key='X.FIELDVALUE'
    Return_Value='X.XLATSHORTNAME'
    Where=$Where
    Quiet
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select On-Error=P260-SQL-Error-Found('P200-Main-Process')
bpp.benefit_program  ()  On-Break  Level=1
                                   Print=Never
                                   Save=$save_benefit_program
                                   After=P250-After-BP-Break
bpp.emplid
bpp.empl_rcd
bpp.cobra_event_id
pd.name
j.company
j.paygroup
j.empl_status
j.deptid 	!Vendaxg Added for ITG#62876 on 07/27/2009
j.jobcode	!Vendaxg Added for ITG#62876 on 07/27/2009

! 11/28/2000 - add BPs for COD - begin
!  If &bpp.benefit_program = 'R17'
  If &bpp.benefit_program = 'R17' or 
     &bpp.benefit_program = 'R36' or 
     &bpp.benefit_program = 'W10'
! 11/28/2000 - end
    Let $Plan_Type_Criteria = 'hb.plan_type   In (''1U'',''1X'',''1Y'')'
  Else
    Let $Plan_Type_Criteria = 'hb.plan_type    = ''1X'''
  End-If
  !Vendaxg Added for ITG#62876 on 07/27/2009 - Begin
  Let $Deptid = Ltrim(Rtrim(&j.deptid,' '),' ')
  Let $JobCode = Ltrim(Rtrim(&j.jobcode,' '),' ')
  Do Get-Jobcode-Descr
  !Vendaxg Added for ITG#62876 on 07/27/2009 - End
  Do P210-Health-Benefit

From   ps_personal_data       pd,
       ps_job                 j,
       ps_ben_prog_partic     bpp

Where  pd.emplid              = bpp.emplid

And    j.emplid               = bpp.emplid
And    j.empl_rcd            = bpp.empl_rcd

And    j.effdt                =
      (Select Max(effdt)
       From   ps_job
       Where  emplid          = bpp.emplid
       And    empl_rcd       = bpp.empl_rcd
       And    effdt          <= $AsOfDate)

And    j.effseq               =
      (Select Max(effseq)
       From   ps_job
       Where  emplid          = j.emplid
       And    empl_rcd       = j.empl_rcd
       And    effdt           = j.effdt)

And    bpp.effdt              =
      (Select Max(effdt)
       From   ps_ben_prog_partic
       Where  emplid          = bpp.emplid
       And    empl_rcd       = bpp.empl_rcd
       And    cobra_event_id  = bpp.cobra_event_id
       And    effdt          <= $AsOfDate)

! 11/28/2000 - add R36 and W10 Benefit Programs for COD
!  And    bpp.benefit_program   In ('R17','W05','W06')
And    bpp.benefit_program   In ('R17','W05','W06', 'R36','W10')
! 11/28/2000 - end
Order  By bpp.benefit_program, bpp.emplid
End-Select
End-Procedure


Begin-Procedure P210-Health-Benefit
#debug9 Show 'P210-Health-Benefit'
Begin-Select Loops=1 On-Error=P260-SQL-Error-Found('P210-Health-Benefit')
'x'

  Do P220-Pay-Earnings

From   ps_health_benefit     hb

Where  hb.emplid              = &bpp.emplid
And    hb.empl_rcd           = &bpp.empl_rcd
And    hb.cobra_event_id      = &bpp.cobra_event_id
And    hb.coverage_elect      = 'E'

And   [$Plan_Type_Criteria]

And    hb.effdt               =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid          = &bpp.emplid
       And    empl_rcd       = &bpp.empl_rcd
       And    cobra_event_id  = &bpp.cobra_event_id
       And    plan_type       = hb.plan_type
       And    effdt          <= $AsOfDate)
End-Select
End-Procedure


Begin-Procedure P220-Pay-Earnings
#debug9 Show 'P220-Pay-Earnings'
Begin-Select On-Error=P260-SQL-Error-Found('P220-Pay-Earnings')
pe.company
pe.paygroup
pe.pay_end_dt
pe.off_cycle
pe.page_num
pe.line_num
Sum(pe.reg_hrs)     &pe.reg_hrs
Sum(pe.ot_hrs)      &pe.ot_hrs
Sum(pe.reg_pay)     &pe.reg_pay
Sum(pe.reg_earns)   &pe.reg_earns

  Let #pe.reg_hrs   = &pe.reg_hrs
  Let #pe.ot_hrs    = &pe.ot_hrs
  Let #pe.reg_pay   = &pe.reg_pay
  Let #pe.reg_earns = &pe.reg_earns

  If #pe.reg_hrs    = 0  And
     #pe.ot_hrs     = 0  And
     #pe.reg_pay    = 0  And
     #pe.reg_earns  = 0

    Do P230-Pay-Oth-Earns

    If #poe.oth_hrs   = 0  And
       #poe.oth_pay   = 0  And
       #poe.oth_earns = 0
      Let $Data-Found-Flag = 'Y'
      Do P240-Print-Record
    End-If

  End-If

From   ps_pay_earnings    pe

Where  pe.company         = &j.company
And    pe.paygroup        = &j.paygroup
And    pe.pay_end_dt      = $AsOfDate
And    pe.emplid          = &bpp.emplid
And    pe.empl_rcd       = &bpp.empl_rcd
And    pe.off_cycle       = 'N'

Group  By pe.company,
       pe.paygroup,
       pe.pay_end_dt,
       pe.off_cycle,
       pe.page_num,
       pe.line_num
End-Select
End-Procedure


Begin-Procedure P230-Pay-Oth-Earns
#debug9 Show 'P230-Pay-Oth-Earns'
  Let #poe.oth_hrs   = 0
  Let #poe.oth_pay   = 0
  Let #poe.oth_earns = 0

Begin-Select On-Error=P260-SQL-Error-Found('P230-Pay-Oth-Earns')
Sum(poe.oth_hrs)   &poe.oth_hrs
Sum(poe.oth_pay)   &poe.oth_pay
Sum(poe.oth_earns) &poe.oth_earns

  Let #poe.oth_hrs   = &poe.oth_hrs
  Let #poe.oth_pay   = &poe.oth_pay
  Let #poe.oth_earns = &poe.oth_earns

From   ps_pay_oth_earns   poe

Where  poe.company     = &pe.company
And    poe.paygroup    = &pe.paygroup
And    poe.pay_end_dt  = &pe.pay_end_dt
And    poe.off_cycle   = &pe.off_cycle
And    poe.page_num       = &pe.page_num
And    poe.line_num       = &pe.line_num
End-Select
End-Procedure


Begin-Procedure P240-Print-Record
#debug9 Show 'P240-Print-Record'
  Lookup XlatLookup &j.empl_status $empl_status

  Print &bpp.emplid      (+1,{#column1})      !Edit xxx-xx-xxxx  ! GEX_SEC_REENG 2015-06-24 Vahini Katta  
  Print &pd.name         (+0,{#column2},050)
  Print $empl_status     (+0,{#column3})
  !Vendaxg Added for ITG#62876 on 07/27/2009 - Begin
  Print $DeptID          (+0,{#column4})
  Print $Jobcode_Descr   (+0,{#column5})
  !Vendaxg Added for ITG#62876 on 07/27/2009 - End

  Add 1 To #BP-recs-printed
End-Procedure


Begin-Procedure P250-After-BP-Break
#debug9 Show 'P250-After-BP-Break'
  If #BP-recs-printed > 0
    New-Page
    Let #BP-recs-printed = 0
  End-If
End-Procedure


Begin-Procedure P260-SQL-Error-Found($Proc_Name) 
#debug9 Show 'P260-SQL-Error-Found'
  Display 'SQL Error in ' 
  Display $Proc_Name 
  Display $_sql-error
  Display 'sql-status:' 
  Display #_sql-status 
  Do P270-BP-Emplid-in-Error
End-Procedure


Begin-Procedure P270-BP-Emplid-in-Error
#debug9 Show 'P270-BP-Emplid-in-Error'
  Display ''
  Display 'Error occurred while processing BP (' Noline
  Display &bpp.benefit_program Noline
  Display '), emplid ' Noline
  Display &bpp.emplid
  Display ''
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Do Get-Current-DateTime    !curdttim.sqc

  Show ' '
  Show 'Report Ended:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime
  Show ' '
  Show 'Successful end of report'
End-Procedure
!Vendaxg Added for ITG#62876 on 07/27/2009 - Begin
Begin-Procedure Get-Jobcode-Descr
Begin-Select
JBCDE.DESCR

	Let $Jobcode_Descr = Ltrim(Rtrim(&JBCDE.DESCR,' '),' ')

FROM PS_JOBCODE_TBL JBCDE
WHERE JBCDE.JOBCODE = $JobCode
AND JBCDE.SETID = 'COMMN'
AND JBCDE.EFFDT = (SELECT MAX(JBCDE1.EFFDT) FROM PS_JOBCODE_TBL JBCDE1 WHERE JBCDE.SETID = JBCDE1.SETID
				AND JBCDE.JOBCODE = JBCDE1.JOBCODE
				AND JBCDE1.EFFDT <= SYSDATE)
End-Select 

End-Procedure Get-Jobcode-Descr
!Vendaxg Added for ITG#62876 on 07/27/2009 - End
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
