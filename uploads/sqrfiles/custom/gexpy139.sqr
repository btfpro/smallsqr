!***********************************************************************
!  GEXPY139:  WEEKLY DISABILITY PAYMENTS REPORT BY BENEFIT PROGRAM
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY139:             Weekly Disability Payments Report              *
!                                                                      *
! Narrative:		This program reports employees that have any   *
!                       year-to-date disability earnings.              *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:		personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       earnings_bal                                   *
!                       pay_earnings                                   *
!                       pay_oth_earns                                  *
!                       ben_prog_partic
!                                                                      *
! Written by:		Carl Buckrop                                   *
!                       Cloned from GEXPY059                           *
!                                                                      *
! Normally Run:		Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Multiple Company/Paygroup/Deptid 	       *
!                         AsOfDate                                     *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS    DATE        DESCRIPTION OF THE CHANGE                   *
!***********************************************************************
!  CWB         06/09/2005    Initial Creation                          *
!                            Cloned from GEXPY059 to sort by Benefit Pgm
!  MXR         07/27/2007  ITG # 42710 -new disability earnings code (526)*
!                          added to the hard coding                    *
!  GEX_PY245   08/21/2007   Sqr from 8.3 is moved as it is to 9.0      *
!  MXD	       02/16/2009   ITG 56749  Replace LOOKUP for Jobtitle     *
!				with GETPOSNM.SQC 		       *	
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization (Landscape)

#define #column1     1
#define #column2    11
#define #column3    25
#define #column4    58
#define #column5    91 
#define #column5y  100  
#define #column6   108
#define #column7   125
#define #column8   142
#define #column9   159

!***********************************************************************
Begin-Setup
  Declare-Report Detail-Report
    Printer-Type=LINEPRINTER
    Layout=Default
  End-Declare

  Declare-Report Summary-Report
    Printer-Type=LINEPRINTER
    Layout=Default
  End-Declare
End-Setup
!***********************************************************************

Begin-Heading 7 For-Reports=(Detail-Report)
#debug9 Show 'Begin-Heading (Detail-Report)'

  move 'PY139DET'                                  to $ReportID
  move 'WEEKLY DISABILITY PAYMENTS DETAIL REPORT'  to $ReportTitle

  #Include 'stdhdg01.sqc'

  If $ben_pgm <> $priorBenPgm
    Lookup Ben_Pgm_Lookup $ben_pgm $Ben_pgm_Descr
    Let $priorBenPgm = $ben_pgm
  End-If

  PRINT 'Benefit Program'     (4,1)
  PRINT $ben_pgm              (4,+2)

  If Rtrim($ben_pgm,' ') <> ''
    PRINT $ben_pgm_Descr         (4,+2)
  End-If

  PRINT '       CURRENT'       (+2,{#column5})
  PRINT '       CURRENT'       (,{#column6})
  PRINT '           MTD'       (,{#column7})
  PRINT '           YTD'       (,{#column8})
  PRINT '    SICK HOURS'       (,{#column9})

  PRINT 'STORE #'              (+1,{#column1})
  PRINT 'SSN #'                (,{#column2})
  PRINT 'NAME'                 (,{#column3})
  PRINT 'JOB TITLE'            (,{#column4})
  PRINT '   REGULAR PAY'       (,{#column5})
  PRINT 'DISABILITY AMT'       (,{#column6})
  PRINT 'DISABILITY AMT'       (,{#column7})
  PRINT 'DISABILITY AMT'       (,{#column8})
  PRINT '          USED'       (,{#column9})

  PRINT '-'                    (+1,{#column1},7) Fill
  PRINT '-'                    (,{#column2},11)  Fill
  PRINT '-'                    (,{#column3},30)  Fill
  PRINT '-'                    (,{#column4},30)  Fill
  PRINT '-'                    (,{#column5},14)  Fill
  PRINT '-'                    (,{#column6},14)  Fill
  PRINT '-'                    (,{#column7},14)  Fill
  PRINT '-'                    (,{#column8},14)  Fill
  PRINT '-'                    (,{#column9},14)  Fill
End-Heading

Begin-Heading 7 For-Reports=(Summary-Report)
#debug9 Show 'Begin-Heading (Summary-Report)'

  move 'PY139SUM'                                  to $ReportID
  move 'WEEKLY DISABILITY PAYMENTS SUMMARY REPORT' to $ReportTitle

  #Include 'stdhdg01.sqc'

  PRINT 'CURRENT WEEK'         (,{#column4})
  PRINT 'CALENDAR '            (,{#column5})
  PRINT $cal_year              (,{#column5y}) 
  PRINT '    YTD'              (,{#column6})

  PRINT 'BENEFIT PROGRAM'       (+1,{#column1})
  PRINT 'CURRENT WEEK $'       (,35)
  PRINT 'NBR EMPLOYEES'        (,{#column4})
  PRINT 'YTD $'                (,{#column5})
  PRINT 'NBR EMPLOYEES'        (,{#column6})

  PRINT '-'                    (+1,{#column1},15) Fill
  PRINT '-'                    (,35,14)  Fill
  PRINT '-'                    (,{#column4},13)  Fill
  PRINT '-'                    (,{#column5},14)  Fill
  PRINT '-'                    (,{#column6},13)  Fill
End-Heading

Begin-Report
#debug9 Show 'Begin-Report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P100-Init-Report
  do P200-Process-Main
  do P260-Print-Totals
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Report


begin-procedure P100-Init-Report
#debug9 Show 'P100-Init-Report'

  do Stdapi-Init

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate='  $ReportDate
  Show '$ReportTime='  $ReportTime

  Let $GEXXX931_Company_Alias  = 'J.COMPANY'
  Let $GEXXX931_Paygroup_Alias = 'J.PAYGROUP'
  Let $GEXXX931_Deptid_Alias   = 'J.DEPTID'

  if $prcs_process_instance = ''
  let $OprID = 'HRBATCH'
  LET $Run_Cntl_ID = 'gexpy139'

    Do P130-SQRW-Run-Controls
  else
    Do GEXXX922-Select-Parameters
    Do GEXXX931-Select-Paygroup-Deptid-Parameters

    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate
    If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Let $X000_ORDER_BY = 'Benefit Program, Name'
  Show ' '
  Show 'Sort Order: ' $X000_ORDER_BY
  Show ' '

  Show '$GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA ='   $GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  Do P140-Get-Last-Confirmed-Pay
  Do P150-Print-Cover-Page
  Do P160-Jobcode-Lookup
  Do P170-Ben-Pgm-Lookup

  let $Current_Year = Substr($X000_WHERE_SELECT_ASOFDATE,8,4)
  move $Current_Year to #Current_Year
  let $Current_Month = Substr($X000_REPORT_HEADING_ASOFDATE,1,2)
  move $Current_Month to #Current_Month
End-Procedure


Begin-Procedure P130-SQRW-Run-Controls
#debug9 Show 'P130-SQRW-Run-Controls'
  Do Ask-As-Of-Date
  Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate

  Display 'Enter Company/Paygroup/Deptid or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Add 1 To #GEX_RC_PAYGROUP_DEPTID_ROWS
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX931_Company_Alias || ' = '''  || $In-Company || ''')'
        If #GEX_RC_PAYGROUP_DEPTID_ROWS = 1
          Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = '(' || $Include_Condition
        Else
          Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA || ' Or ' || $Include_Condition
        End-If
        Break
      Else
        Input $In-DeptID Maxlen=10 'DeptID'
        Uppercase $In-DeptID
        If Rtrim($In-DeptID, ' ') = ''
          Let $Include_Condition = '(' || $GEXXX931_Company_Alias  || ' = ''' || $In-Company  || ''' And '
                                       || $GEXXX931_Paygroup_Alias || ' = ''' || $In-Paygroup || ''')'
        Else
          Let $Include_Condition = '(' || $GEXXX931_Company_Alias  || ' = ''' || $In-Company  || ''' And '
                                       || $GEXXX931_Paygroup_Alias || ' = ''' || $In-Paygroup || ''' And '
                                       || $GEXXX931_Deptid_Alias   || ' = ''' || $In-Deptid   || ''')'
        End-If
        If #GEX_RC_PAYGROUP_DEPTID_ROWS = 1
          Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = '(' || $Include_Condition
        Else
          Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA || ' Or ' || $Include_Condition
        End-If
      End-If
    End-If
  End-While

  If #GEX_RC_PAYGROUP_DEPTID_ROWS = 0
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = '1=1'
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA = $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA || ')'
    Let $GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA = $GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA
  End-If
End-Procedure


Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

Begin-Select Loops=1
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'fmMonth YYYY')           &period

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let $PayEndDate       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  Else
    Show 'Last confirmed pay end date:  ' $pay_end_dt
  End-If

From   ps_pay_calendar

Where  pay_end_dt              =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt      <= $X000_WHERE_SELECT_ASOFDATE
       And    pay_confirm_run  = 'Y')

And    pay_confirm_run         = 'Y'
End-Select
End-Procedure


Begin-Procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA ='    (+1,5)
  Print $GEXXX931_INCLUDE_PAYGROUP_DEPTID_DISPLAY_CRITERIA        (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure P160-Jobcode-Lookup
#debug9 Show 'P160-Jobcode-Lookup'

  Let $Where = 'JT.EFFDT = (SELECT MAX(EFFDT)'                   ||
               '            FROM   PS_JOBCODE_TBL'               ||
               '            WHERE  JOBCODE  = JT.JOBCODE'        ||
               '            AND    EFFDT   <= ''' || $pay_end_dt || ''')'

  Load-Lookup
    Name=Jobcode_Lookup
    Rows=100
    Table='PS_JOBCODE_TBL JT'
    Key='JT.JOBCODE'
    Return_Value='JT.DESCR'
    Where=$Where
    Quiet
End-Procedure


Begin-Procedure P170-ben-pgm-Lookup
#debug9 Show 'P170-ben-pgm-Lookup'

  Let $Where = 'BDP.EFFDT = (SELECT MAX(EFFDT)'                    ||
               '            FROM   PS_BEN_DEFN_PGM'                  ||
               '            WHERE  BENEFIT_PROGRAM  = BDP.BENEFIT_PROGRAM'||
               '            AND    EFF_STATUS = ''A''' ||  
               '            AND    EFFDT    <= ''' || $pay_end_dt || ''')'

  Load-Lookup
    Name=Ben_Pgm_Lookup
    Rows=125
    Table='PS_BEN_DEFN_PGM BDP'
    Key='BDP.BENEFIT_PROGRAM'
    Return_Value='BDP.DESCR'
    Where=$Where
    Quiet
End-Procedure


Begin-Procedure P200-Process-Main
#debug9 Show 'P200-Process-Main'
Show 'Entering Procedure P200-Process-Main.'
  Let $Data-Found-Flag = 'N'

Begin-Select
J.COMPANY      

J.PAYGROUP     
BP.BENEFIT_PROGRAM  ()   ON-BREAK
                    LEVEL=1
                    PRINT=NEVER
                    SAVE=$Save_ben_pgm
                    AFTER=P240-ben-pgm-Change


PD.NAME

J.DEPTID
J.EMPLID
J.EMPL_RCD
J.JOBCODE
J.POSITION_NBR			!ISDVMXD added for ITG 56749 on 02/16/2009
BL.BALANCE_PERIOD
BL.BALANCE_YEAR
BL.BALANCE_QTR
  Let $Data-Found-Flag = 'Y'

  Let $Company  = &j.company
  Let $Paygroup = &j.paygroup
  Let $ben_pgm  = &BP.BENEFIT_PROGRAM
  Let $Position_Nbr = &J.POSITION_NBR		!ISDVMXD added for ITG 56749 on 02/16/2009

  !Lookup Jobcode_Lookup &j.jobcode $JobTitle
  Do Get-Position-Title		 		!ISDVMXD added for ITG 56749 on 02/16/2009
	Show 'emplid = ' &J.EMPLID
	show 'posname = ' $PosName
  Let $JobTitle = $PosName			!ISDVMXD added for ITG 56749 on 02/16/2009
  Let #dis_earnings_mtd = 0
  Let #reg_earnings_cur = 0
  Let #dis_earnings_cur = 0

  do select-earnings-bal

  move &BL.BALANCE_YEAR to $cal_year 9999
  Let #dis_earnings_ytd = &eb2.grs_ytd

  If &EB2.BALANCE_PERIOD = &bl.balance_period
    Let #dis_earnings_mtd = &eb2.grs_mtd
  End-If

  Do P210-Pay-Earnings
  Do P220-Sick-Hours

  If #dis_earnings_cur = 0
    Let #reg_earnings_cur = 0
  End-If
  Show 'Company  : '$Company
  Do P230-Print-Detail-Record

From   PS_PERSONAL_DATA    PD,
       PS_JOB              J,
       PS_PAY_CAL_BAL_ID   BL,
       PS_BEN_PROG_PARTIC  BP

Where  j.effdt                =
      (Select Max(effdt)
       From   ps_job
       Where  emplid          = j.emplid
       And    empl_rcd       = j.empl_rcd
       And    effdt          <= $pay_end_dt)

And    j.effseq               =
      (Select Max(effseq)
       From   ps_job
       Where  emplid          = j.emplid
       And    empl_rcd       = j.empl_rcd
       And    effdt           = j.effdt)

And    bp.emplid              = j.emplid
And    bp.empl_rcd            = j.empl_rcd
and    bp.effdt               =
      (Select Max(effdt)
       From   ps_ben_prog_partic
       Where  emplid          = bp.emplid
       And    empl_rcd        = bp.empl_rcd
       And    effdt          <= $pay_end_dt)

And    pd.emplid              = j.emplid

and    bl.company = j.company
and    bl.paygroup = j.paygroup
and    bl.pay_end_Dt = $pay_end_Dt
and    bl.balance_id = 'CY'

and 0 < (select sum(eb1.grs_ytd) from ps_earnings_bal eb1
where  EB1.EMPLID             = J.EMPLID
And    EB1.EMPL_RCD           = J.EMPL_RCD
And    EB1.COMPANY            = J.COMPANY
And    EB1.BALANCE_ID         = 'CY'
And    EB1.BALANCE_YEAR       = bl.balance_year
and    eb1.balance_period     = (Select max(balance_period)
			from ps_earnings_bal
			where emplid = eb1.emplid
			and company = eb1.company
			and balance_id = eb1.balance_id
			and balance_year = eb1.balance_year
			and erncd = eb1.erncd
                        and empl_rcd = eb1.empl_rcd
			and spcl_balance = eb1.spcl_balance
			and balance_period <= bl.balance_period)
And    EB1.SPCL_BALANCE       = 'N'
!And    EB1.ERNCD              = '516')
And EB1.ERNCD IN ('516','526')) !ISDVMXR ITG # 42710 Added erncd 526.
!and j.emplid ='209627192'
And   [$GEXXX931_INCLUDE_PAYGROUP_DEPTID_CRITERIA] 


Order By BP.BENEFIT_PROGRAM,PD.NAME  
End-Select

  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
End-Procedure
!SXK 06/20/1999 Begin

Begin-Procedure select-earnings-bal
begin-select
eb2.balance_period
sum(eb2.grs_mtd) &eb2.grs_mtd
sum(eb2.grs_ytd) &eb2.grs_ytd

from ps_earnings_bal eb2
where eb2.emplid = &j.emplid
and eb2.company = &j.company
and eb2.balance_id = 'CY'
and eb2.balance_year = &bl.balance_year
and eb2.empl_rcd     = &j.empl_rcd
and eb2.balance_period = (select max(eb2.balance_period)
		from ps_earnings_bal
		where emplid = eb2.emplid
		and company = eb2.company
		and balance_id = 'CY'
		and balance_year = &bl.balance_year
		and balance_period <= &bl.balance_period
		and spcl_balance = 'N'
                and empl_rcd = eb2.empl_rcd
		and erncd = eb2.erncd)
and eb2.spcl_balance = 'N'
!and eb2.erncd = '516' !ITG # 42710 isdvmxr 07/27/2007 
and eb2.erncd in ('516','526') !ITG # 42710 isdvmxr 07/27/2007 

group by eb2.balance_period
end-select
end-procedure
!SXK 06/20/1999 End

Begin-Procedure P210-Pay-Earnings
#debug9 Show 'P210-Pay-Earnings'

Begin-Select
PE.COMPANY
PE.PAYGROUP
PE.PAY_END_DT
PE.OFF_CYCLE
PE.PAGE_NUM
PE.LINE_NUM
PE.ADDL_NBR
PE.REG_PAY

  Add &pe.reg_pay To #reg_earnings_cur

  Do P215-Pay-Oth-Earnings

From   PS_PAY_EARNINGS     PE

Where  pe.company          = &j.company
And    pe.paygroup         = &j.paygroup
And    pe.pay_end_dt       = $pay_end_dt
And    pe.emplid           = &j.emplid
And    pe.empl_rcd        = &j.empl_rcd
End-Select
End-Procedure


Begin-Procedure P215-Pay-Oth-Earnings
#debug9 Show 'P220-Pay-Oth-Earnings'

Begin-Select
POE.ERNCD
Sum(POE.OTH_EARNS) &poe.oth_earns

  If &POE.ERNCD = 'REG'
    Add &poe.oth_earns To #reg_earnings_cur
  End-If

  If (&POE.ERNCD = '516' or  &POE.ERNCD = '526')!ITG # 42710 isdvmxr 07/27/2007 
 
    Add &poe.oth_earns To #dis_earnings_cur
  End-If

From   PS_PAY_OTH_EARNS    POE

Where  poe.company         = &pe.company
And    poe.paygroup        = &pe.paygroup
And    poe.pay_end_dt      = &pe.pay_end_dt
And    poe.off_cycle       = &pe.off_cycle
And    poe.page_num        = &pe.page_num
And    poe.line_num        = &pe.line_num
And    poe.addl_nbr        = &pe.addl_nbr

!And    poe.erncd          In ('REG','516')!07/31/2007 ITG # 42710 isdvmxr  Added erncd 526
And    poe.erncd          In ('REG','516','526')!07/31/2007 ITG # 42710 isdvmxr  Added erncd 526



Group By POE.ERNCD
End-Select
End-Procedure


Begin-Procedure P220-Sick-Hours
#debug9 Show 'P220-Sick-Hours'

  Let #sick_hrs_ytd = 0

Begin-Select
Sum(EB.HRS_YTD) &eb.hrs_ytd

  Let #sick_hrs_ytd = &eb.hrs_ytd

From   PS_EARNINGS_BAL     EB

Where  EB.EMPLID              = &J.EMPLID
And    EB.COMPANY             = &J.COMPANY
And    EB.BALANCE_ID          = 'CY'
And    EB.BALANCE_YEAR        = &bl.balance_year
And    eb.empl_rcd	      = &j.empl_rcd

And    EB.BALANCE_PERIOD      =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_EARNINGS_BAL
       WHERE  EMPLID          = EB.EMPLID
       AND    COMPANY         = EB.COMPANY
       AND    BALANCE_ID      = EB.BALANCE_ID
       AND    BALANCE_YEAR    = EB.BALANCE_YEAR
       AND    BALANCE_PERIOD <= &bl.balance_period
       AND    SPCL_BALANCE    = EB.SPCL_BALANCE
       And    empl_rcd        = eb.empl_rcd
       AND    ERNCD           = EB.ERNCD)

And    EB.SPCL_BALANCE        = 'N'
And    EB.ERNCD               = '501'
End-Select
End-Procedure


begin-procedure P230-Print-Detail-Record
#debug9 Show 'P230-Print-Detail-Record'

  Use-Report Detail-Report

  add 1                      to #ben_pgm_count

  add #reg_earnings_cur      to #ben_pgm_reg_earnings_cur
  if #dis_earnings_cur > 0
    add 1 to #summ_cur_emps
  end-if
  add #dis_earnings_cur      to #ben_pgm_dis_earnings_cur
  add #dis_earnings_cur      to #summ_dis_earnings_cur
  add #dis_earnings_mtd      to #ben_pgm_dis_earnings_mtd
  if #dis_earnings_ytd > 0
    add 1 to #summ_ytd_emps
  end-if
  add #dis_earnings_ytd      to #ben_pgm_dis_earnings_ytd
  add #dis_earnings_ytd      to #summ_dis_earnings_ytd
  add #sick_hrs_ytd          to #ben_pgm_sick_hrs_ytd

  print &J.DEPTID                   (+1,{#column1})
  print &J.EMPLID                   (,{#column2})     edit xxx-xx-xxxx
  print &PD.NAME                    (,{#column3},30)
  print $JobTitle                   (,{#column4})
  print #reg_earnings_cur           (,{#column5})     edit 999,999,999.99
  print #dis_earnings_cur           (,{#column6})     edit 999,999,999.99
  print #dis_earnings_mtd           (,{#column7})     edit 999,999,999.99
  print #dis_earnings_ytd           (,{#column8})     edit 999,999,999.99
  print #sick_hrs_ytd               (,{#column9})     edit 999,999,999.99
end-procedure


begin-procedure P240-ben-pgm-Change
#debug9 Show'P240-Ben-Pgm-Change'

  print '-'                         (+2,{#column5},14)  fill
  print '-'                         (,{#column6},14)    fill
  print '-'                         (,{#column7},14)    fill
  print '-'                         (,{#column8},14)    fill
  print '-'                         (,{#column9},14)    fill
  print 'Benefit Program'                  (+1,1)
  print $Save_ben_pgm              (,+1)
  print 'Count:'                    (,+1)
  print #ben_pgm_count                (,+2)             edit 9,999,999
  print #ben_pgm_reg_earnings_cur     (,{#column5})     edit 999,999,999.99
  print #ben_pgm_dis_earnings_cur     (,{#column6})     edit 999,999,999.99
  print #ben_pgm_dis_earnings_mtd     (,{#column7})     edit 999,999,999.99
  print #ben_pgm_dis_earnings_ytd     (,{#column8})     edit 999,999,999.99
  print #ben_pgm_sick_hrs_ytd         (,{#column9})     edit 999,999,999.99

  Use-Report Summary-Report
  do P250-Print-Summary-Record
  Use-Report Detail-Report

  move 0 to #ben_pgm_count
  move 0 to #ben_pgm_reg_earnings_cur
  move 0 to #ben_pgm_dis_earnings_cur
  move 0 to #ben_pgm_dis_earnings_mtd
  move 0 to #ben_pgm_dis_earnings_ytd
  move 0 to #ben_pgm_sick_hrs_ytd

  new-page

end-procedure

begin-procedure P250-Print-Summary-Record
#debug9 Show 'P250-Print-Summary-Record'

  Lookup Ben_Pgm_Lookup $save_ben_pgm $summ_Ben_pgm_Descr

  print $Save_ben_pgm                 (+1,2)
  print $summ_ben_pgm_descr           (,+1)
  print #summ_dis_earnings_cur        (,35)     edit 999,999,999.99
  print #summ_cur_emps                (,{#column4})      edit 9,999,999
  print #summ_dis_earnings_ytd        (,{#column5})     edit 999,999,999.99
  print #summ_ytd_emps                (,{#column6})     edit 9,999,999

  add #summ_dis_earnings_cur to #tot_dis_earnings_cur
  add #summ_cur_emps to #tot_cur_emps
  add #summ_dis_earnings_ytd to #tot_dis_earnings_ytd 
  add #summ_ytd_emps to #tot_ytd_emps  

  move 0 to #summ_dis_earnings_cur
  move 0 to #summ_cur_emps 
  move 0 to #summ_dis_earnings_ytd 
  move 0 to #summ_ytd_emps   

end-procedure

begin-procedure P260-Print-Totals
#debug9 Show'P260-Print-Totals'

  Use-Report Summary-Report

  PRINT '-'                          (+2,35,14)  Fill
  PRINT '-'                          (,{#column4},13)  Fill
  PRINT '-'                          (,{#column5},14)  Fill
  PRINT '-'                          (,{#column6},13)  Fill
  print 'Grand Totals'               (+1,2)
  print #tot_dis_earnings_cur        (,35)     edit 999,999,999.99
  print #tot_cur_emps                (,{#column4})      edit 9,999,999
  print #tot_dis_earnings_ytd        (,{#column5})     edit 999,999,999.99
  print #tot_ytd_emps                (,{#column6})     edit 9,999,999

end-procedure

#include 'gexxx931.sqc'  !Get company/paygroup multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'getcodta.sqc'  !Get-Company-Data procedure
#include 'getpgdta.sqc'  !Get-Paygroup-Data procedure
#include 'askaod.sqc'    !Ask As Of Date input
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'getposnm.sqc'  !Get-Position-Title procedure			!ISDVMXD added for ITG 56749 on 02/16/2009