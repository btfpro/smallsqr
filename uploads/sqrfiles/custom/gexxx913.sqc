!**************************************************************************
!  GEXXX913:  Retrieve "Multiple" Union Codes Run Control and             * 
!             Return a Formatted Where-Clause                             *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX913_UNION_CD_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX913_INCLUDE_UNION_CD_CRITERIA                                  *
!    $GEXXX913_EXCLUDE_UNION_CD_CRITERIA                                  *
!    $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA                          *
!    $GEXXX913_EXCLUDE_UNION_CD_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX913_UNION_CD_Alias = 'J.UNION_CD'                          *
!    Do GEXXX913-Select-UNION-CD-Parameters                               *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]                          *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JWD 		06/03/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX913-Select-UNION-CD-Parameters

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_UNION_CD_ROWS = 0
  Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = ''
  Let $GEXXX913_EXCLUDE_UNION_CD_CRITERIA = ''
  Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX913_EXCLUDE_UNION_CD_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_UNION_CD.UNION_CD

  Add 1 To #GEX_RC_UNION_CD_ROWS

  If #GEX_RC_UNION_CD_ROWS = 1
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '(' || $GEXXX913_UNION_CD_Alias || ' In ('''     || &GEX_RC_UNION_CD.UNION_CD || ''''
    Let $GEXXX913_EXCLUDE_UNION_CD_CRITERIA = '(' || $GEXXX913_UNION_CD_Alias || ' Not In (''' || &GEX_RC_UNION_CD.UNION_CD || ''''
  Else
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA || ',''' || &GEX_RC_UNION_CD.UNION_CD  || ''''
    Let $GEXXX913_EXCLUDE_UNION_CD_CRITERIA = $GEXXX913_EXCLUDE_UNION_CD_CRITERIA || ',''' || &GEX_RC_UNION_CD.UNION_CD  || ''''
  End-If

From  PS_GEX_RC_UNION_CD GEX_RC_UNION_CD

Where GEX_RC_UNION_CD.OPRID               = [$OprID]
And   GEX_RC_UNION_CD.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_UNION_CD.UNION_CD, ' ') != ' ' 
End-Select

  If #GEX_RC_UNION_CD_ROWS = 0
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_UNION_CD_Alias || ' = ' || $GEXXX913_UNION_CD_Alias
    Let $GEXXX913_EXCLUDE_UNION_CD_CRITERIA = $GEXXX913_UNION_CD_Alias || ' = ' || $GEXXX913_UNION_CD_Alias
  Else
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA || '))'
    Let $GEXXX913_EXCLUDE_UNION_CD_CRITERIA = $GEXXX913_EXCLUDE_UNION_CD_CRITERIA || '))'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
    Let $GEXXX913_EXCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_EXCLUDE_UNION_CD_CRITERIA
  End-If

End-Procedure
