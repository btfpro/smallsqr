!***************************************
!  GEXHR004:  Personnel Actions History  *
!***************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! PeopleSoft, Inc.; it is not to be copied, reproduced, or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of PeopleSoft.                        *
!                                                                      *
! Copyright (c) 1988-1996 PeopleSoft, Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
!      $Date:: 11/15/96 6:02 $                                         *
!  $Revision:: 9   $                                                   *
!  $Workfile:: GEXHR004.SQR $                                          *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS	DATE		DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! UNKNOWN      		Initial Creation 				           *
! UNKNOWN               Changed atleast once                           *
! JNB		11/30/1998  Changed source so that the from date and thru  *
!                       date will default to pay_begin_dt & pay_end_dt *
!                       when users leave these fields blank or forget  *
!                       to enter run cntl parameters.                  * 
! SXK           02/03/1999 Changed to include right headings and job title
! MXR           04/14/2008 Changed to print correct Action description *
! VENDTKK       02/11/2009 Added INCLUDE GETPOSNM.SQC to pull	       *
!                  	   JOBTITLE from POSITION_DATA.DESCR   	       *
!			   field instead of JOBCODE_TBL.DESCR          *    
!			           	                               *	
!************************************************************************
#include 'setenv.sqc'    !Set environment

!#include 'setup01.sqc'   !Printer and page-size initialization - Portrait
#Include 'setup02.sqc'   !Printer and page-size initialization - Landscape


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-heading 9
  move 'PERSONNEL ACTIONS HISTORY' to $reporttitle
  #include 'stdhdg01.sqc'

do Get_Field_Information ('PER015',   'REPORT_TITLE',         $REPORTTITLE,   #DW)
do Get_Field_Information ('PER015',   'CONAME',               $CONAME,        #DW)
do Get_Field_Information ('PER015',   'ACCOUNT',              $ACCOUNT,       #DW)
do Get_Field_Information ('PER015',   'TERMDATE',             $TERMDATE,      #DW)
do Get_Field_Information ('PER015',   'PERSONNEL',            $PERSONNEL,     #DW)
do Get_Field_Information ('HR',         'EMP_TYP1',             $EMPL_TYPE1,    #DW)
do Get_Field_Information ('HR',         'EMP_TYP2',             $EMPL_TYPE2,    #DW)
do Get_Field_Information ('HR',         'REG_TMP1',             $REG_TEMP1,     #DW)
do Get_Field_Information ('HR',         'REG_TMP2',             $REG_TEMP2,     #DW)
do Get_Field_Information ('HR',         'FULL_PART_TM1',        $FULL_PART_TM1, #DW)
do Get_Field_Information ('HR',         'FULL_PART_TM2',        $FULL_PART_TM2, #DW)
do Get_Field_Information ('HR',         'CURRENCY_CD1',         $CURRENCY_CD1,  #DW)
do Get_Field_Information ('HR',         'CURRENCY_CD2',         $CURRENCY_CD2,  #DW)
do Get_Field_Information ('HR',         'COMPRATE',             $COMPRATE,      #DW)
do Get_Field_Information ('HR',         'EFFDT1',               $EFFDT1,        #DW)
do Get_Field_Information ('HR',         'EFFDT2',               $EFFDT2,        #DW)
do Get_Field_Information ('HR',         'EMPLID',               $EMPLID,        #DW)
do Get_Field_Information ('HR',         'ACTION_REASON1',       $ACT_RSN1,      #DW)
do Get_Field_Information ('HR',         'ACTION_REASON2',       $ACT_RSN2,      #DW)
do Get_Field_Information ('HR',         'EMPL_NAME',            $NAME,          #DW)
do Get_Field_Information ('HR',         'HIRE_DT1',             $HIRE_DT1,      #DW)
do Get_Field_Information ('HR',         'HIRE_DT2',             $HIRE_DT2,      #DW)
do Get_Field_Information ('HR',         'JOBCODE1',             $JOBCODE1,      #DW)
do Get_Field_Information ('HR',         'JOBCODE2',             $JOBCODE2,      #DW)
do Get_Field_Information ('HR',         'JOBTITLE',             $JOBTITLE,      #DW)
do Get_Field_Information ('HR',         'GRADE1',               $GRADE1,        #DW)
do Get_Field_Information ('HR',         'GRADE2',               $GRADE2,        #DW)
do Get_Field_Information ('HR',         'SUPERVISOR_NAME',      $SUPER,         #DW)
do Get_Field_Information ('HR',         'SEX',                  $SEX,           #DW)

  
  if $Company  <> $priorCompany
     do Get-Company-Data
     move $Company to $priorCompany      
     move $CompanyName to $PrintCompanyName    !for use in Heading
     move $Company to $PriorCompnay
  end-if

 
  if $Action <> $priorAction
     ! move 'ACTION'  to $FieldName             !MXR 04/14/2008
    ! move $Action   to $FieldValue            !MXR 04/14/2008
    ! do Read-Translate-Table                  !MXR 04/14/2008
      do Get-Action                            !MXR 04/14/2008
     move $Action_DescrShort to $ActionName         !for use in Heading !MXR 04/14/2008
     move $Action to $priorAction
  end-if
  
!  PRINT $CONAME                 (3,1)
  print $COMPANY                (3,1) edit xxx--
  print $PrintCompanyName       ()

!  PRINT $PERSONNEL              (4,1)
  print $Action                 (+1,1) edit xxx--
  print $ActionName             ()
  Position (+1)

  do Get_Field_Information ('GEXHR004', 'PERIOD_FROM',  $FROM,  #DW)
  do Get_Field_Information ('GEXHR004', 'PERIOD_THRU',  $THRU,  #DW)
  
  print 'FROM:'            (+1,1)
  
  do Format-DateTime($FromDate, $out, {DEFDATE}, '', '')
  print $out             (0,+1)
  print 'THRU:'            (0,+1)
  do Format-DateTime($ThruDate, $out, {DEFDATE}, '', '')
  print $out             (0,+1)

  
LET $REG1 = $REG_TEMP1 || '/'
LET $FULL1 = $FULL_PART_TM1 || '/'

PRINT $EFFDT1                   (+2,1)
PRINT $ACT_RSN1                 (0,12)
PRINT $HIRE_DT1                 (0,72)

PRINT $JOBCODE1                 (0,103)

PRINT $EFFDT2                   (+1,1)
PRINT $ACT_RSN2                 (0,12)
PRINT $NAME                     (0,23)

PRINT $EMPLID                   (0,60)
PRINT $HIRE_DT2                 (0,72)
PRINT $SEX                      (0,84)
PRINT 'ACCT CD'                 (0,90)
PRINT $JOBCODE2                 (0,103)
print '   JOB TITLE   '         (0,114)
PRINT 'TERM DATE'               (0,144)

print '-' (+1,1,160) fill
end-heading

begin-procedure Init-Report

  move 'GEXHR004' to $ReportID
  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-From-Thru-Date
    do Ask-Actions
  else
    do Select-Parameters
  end-if
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

end-procedure

begin-procedure Get-Values

     let $language_cd = $prcs_language_cd
     do Get-From-Thru-Date

     If $FromDate = $Century_Begin_Dt and 
        $ThruDate = $AsOfToday

      Do Obtain-pay-end-dt

       let $FromDate = $pay_begin_dt
       let $ThruDate = $pay_end_dt

     end-if
     do Get-Actions
   
     
end-procedure

begin-procedure Obtain-pay-end-dt
begin-select
p.pay_end_dt   &p.pay_end_dt
p.pay_begin_dt &p.pay_begin_dt

  move &p.pay_end_dt   to $pay_end_dt
  move &p.pay_begin_dt to $pay_begin_dt

from ps_pay_calendar p
 
where p.pay_end_dt = 
      (select max(pay_end_dt)
      from ps_pay_calendar
      where pay_end_dt <= $AsOfToday) 
end-select 

end-procedure
 

begin-procedure Process-Main
begin-SELECT
C.COMPANY ()    on-break print=never level=1 procedure=Company-Change
   move &C.Company        to $Company

C.ACTION ()     on-break print=never level=2 procedure=Action-Change
  move &C.Action         to $Action

C.DEPTID ()     on-break print=never procedure=Dept-Change
  if $firstRec <> 'N'
    do Initialize-Subheading
    move 'N' to $firstRec
  end-if

C.EFFDT
  do Format-DateTime(&C.EffDt, $effdate, {DEFDATE}, '', '')
  print $effdate                        (+1,1)

C.ACTION_REASON
  move &C.Action_Reason  to $Action_Reason
  move &C.EffDt          to $AsOfDate
  do Get-Action-Reason
  print $Action_Reason_DescrShort   (0,12)
 

A.NAME                              (0,23)

A.EMPLID                            (0,60)
B.HIRE_DT
  do Format-DateTime(&B.Hire_Dt, $out, {DEFDATE}, '', '')
  print $out                      (0,72)

A.SEX                                (0,85)
C.ACCT_CD                            (0,92)
C.JOBCODE                            (0,103)
C.Position_nbr
  move &C.JobCode to $Jobcode
  do get-job-title
 !VENDTKK Begin   ITG#56786   11-Feb-2009
  
   Let $Position_Nbr = &C.Position_nbr
 	 Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosName = '' 
  		Let $jobtitle = $jobtitle
	 Else
 		Let $jobtitle = $PosName
	 End-if
 !VENDTKK End   ITG#56786   11-Feb-2009	
  
  print $jobtitle (0,114)
B.TERMINATION_DT
  do Format-DateTime(&B.TERMINATION_DT, $TermDate, {DEFDATE}, '', '')
  print $TermDate                    (0,144)

FROM  PS_PERSONAL_DATA A,
      PS_EMPLOYMENT B,
      PS_JOB C
WHERE A.EMPLID = B.EMPLID
  AND B.EMPLID = C.EMPLID
  AND B.EMPL_RCD = C.EMPL_RCD
  AND C.EFFDT BETWEEN $FromDate AND $ThruDate
  AND C.ACTION IN ([$ACTIONSTRING])
!  AND C.COMPANY = RUN_CNTL_HR.COMPANY
ORDER BY C.COMPANY,C.ACTION,C.DEPTID,C.EFFDT
end-SELECT
end-procedure

begin-procedure Action-Change
  new-page
end-procedure

begin-procedure Company-Change
  new-page
end-procedure

begin-procedure Dept-Change
  if #current-line >= 51  !ensure a minimum of 2 employees is listed
    new-page
  else
    position (+1)
  end-if
  do Initialize-Subheading
end-procedure

begin-procedure Initialize-Subheading
  move &C.DeptID to $DeptID
  do Get-Department-Name
  do Print-Subheading
end-procedure

begin-procedure Print-Subheading
  print 'Department:' (+1,1)
  print $DeptID      (0,+1)
  print $DeptName    (0,+2)
  position (+1)
end-procedure

#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrrnctl2.sqc'  !Get Action values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askftd.sqc'    !From Thru Date input
#include 'askactn.sqc'   !Action input

#include 'getdptnm.sqc'  !Get-Department-Name procedure
#include 'getjobtl.sqc'  !Get-Job-Title procedure
#include 'getempnm.sqc'  !Get-Employee-Name procedure
#include 'getactrs.sqc'  !Get-Action-Reason procedure
#include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'datemath.sqc'  !Routines for date arithmetic
#include 'reset.sqc'     !Reset printer procedure
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'stdapi.sqc'    !Routines to Update Run Status
#include 'getcodta.sqc'  !To get the company details
#include 'sqrtrans.sqc'
!#include 'useprntr.sqc'
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   11-Feb-2009  