!***********************************************************************
! GEXPY135:           WEEKLY TRANSFER REPORT                           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY135:             WEEKLY TRANSFERS                               *
!                                                                      *
! Narrative:            This program reports employees that were       *
!                       transferred from one company to another        *
!                       in the last week                               *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    * 
!                       #debug7 data returned from sql calls           * 
!                                                                      *
! SQL Tables:           PS_JOB                                         * 
!                                                                      *
! Written by:           Sreeni Kutam                                   *                                                           
!                                                                      *
! Normally run:         Weekly                                         *
!                                                                      *
! Entered Run                                                          *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS    DATE         DESCRIPTION OF THE CHANGE          *
!***********************************************************************
!            SRK       04/22/2003   Initial creation.                  *
!            CXA       10/26/2004   Add to Where clause: In company    *
!                                   But not in Dept                    *
!            CXA       02/17/2006   Remove above change, only want     *
!                                   company to company transfers       *
!	Vendkxy			    Done few changes for testing
! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project
!GEX_SEC_REENG Vahini Katta 09/18/2016
!Changes related to SSN Conversion                                     *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup02.sqc' 

#define #x000_col_selection_criteria_a  02
#define #x000_col_selection_criteria_b  05

#define #x000_col_name                  03
#define #x000_col_emplid                42
#define #x000_col_empl_rcd              53
#define #x000_col_effdt                 66
#define #x000_col_company               79
#define #x000_col_paygroup              92
! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project Begins
#define #x000_col_company1              102
#define #x000_col_dedcd                 117
#define #x000_col_dedamt                127
#define #x000_col_goalamt               137
#define #x000_col_goalbal               147
! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project Ends

!*********************************************************************
begin-report
!*********************************************************************
#debug9 show 'begin-report'

  do Initialization
  do select-employees
  do stdapi-term
  do EOJ

end-report

!***********************************************************************
begin-heading 09
!***********************************************************************

#debug9 show 'begin-heading'

 #Include 'stdhdg01.sqc'

  print $x000_header_1       (3,) center
  print $x000_header_2       (+1) center
  print 'Period from '       (+1, 72)
  print $start_dt            ()
  print ' To '               ()
  print $x000_where_select_asofdate ()

  print '-' (+2,1,155) fill
  print 'Name'                (+1,{#x000_col_name})     bold
  print 'Emplid'              (0,{#x000_col_emplid})    bold 
  print 'Empl_Rcd'            (0,{#x000_col_empl_rcd})  bold
  print 'Effdt'               (0,{#x000_col_effdt})     bold
  print 'To Company'          (0,{#x000_col_company})   bold 
  print 'Paygroup'            (0,{#x000_col_paygroup})  bold
  ! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project
  Print 'From Company'        (0,{#x000_col_company1})  bold
  Print 'Dedcd'               (0,{#x000_col_dedcd})     bold
  Print 'Flat Amt'            (0,{#x000_col_dedamt})   bold
  Print 'Goal Amt'            (0,{#x000_col_goalamt})   bold 
  Print 'Goal Bal'            (0,{#x000_col_goalbal})    bold 
  ! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project
  print '-' (+1,1,155) fill
 
end-heading  

!***********************************************************************
begin-procedure Initialization
!***********************************************************************
#debug9 show 'Initialization'

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
     show '$asoftoday = ' $asoftoday
     show ' '
  do stdapi-init
  
  
  let #empl-count = 0

  if $Prcs_process_instance = ''

    Do Ask-As-Of-Date
    Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate
  else
    Do GEXXX922-Select-Parameters
    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.ASOFDATE
  end-if

  if rtrim($X000_WHERE_SELECT_ASOFDATE, ' ' ) = ''
       let $X000_WHERE_SELECT_ASOFDATE = $asoftoday
       LET $AsOfDate = $X000_WHERE_SELECT_ASOFDATE 
  else
       LET $AsOfDate = $X000_WHERE_SELECT_ASOFDATE 
  end-if

  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan
  display ' '
  let $ReportID           = 'GEXPY135'
  let $ReportTitle        = 'WEEKLY TRANSFERS REPORT'                                     
  let $Country            = 'USA'

  show '$ReportID= '     $ReportID
  show '$ReportTitle = ' $ReportTitle   
    
  show '$X000_WHERE_SELECT_ASOFDATE        ='  $X000_WHERE_SELECT_ASOFDATE 
  do convert-to-dtu-date($X000_WHERE_SELECT_ASOFDATE,$asofdate_dtu)
  #debug8 show 'asofdate_dtu ' $asofdate_dtu

  do dtu-add-days($asofdate_dtu,-7,$start_dt_dtu)
  #debug8 show 'Start date dtu: ' $start_dt_dtu

  do convert-from-dtu-date($start_dt_dtu,$start_dt)
  #debug8 show 'Start Dt: ' $start_dt

  do Print-Cover-Page
  new-page

end-procedure

!***********************************************************************
Begin-Procedure Print-Cover-Page
!***********************************************************************

   print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,{#x000_col_selection_criteria_a})
   print '$Prcs_OPRID          ='                              (+2,{#x000_col_selection_criteria_b})
   print $Prcs_OPRID                                           (0,+2)
   print '$PRCS_RUN_CNTL_ID    ='                              (+1,{#x000_col_selection_criteria_b})
   print $PRCS_RUN_CNTL_ID                                     (0,+2)

   print 'SELECTION CRITERIA FOR THIS REPORT RUN:'           (+5,{#x000_col_selection_criteria_a})
   print '$X000_WHERE_SELECT_ASOFDATE                     ='   (+1,{#x000_col_selection_criteria_b})
   print  $X000_WHERE_SELECT_ASOFDATE                          (0,+2)
 
 
   let #page-count=0 

end-procedure

!********************************************************************
begin-procedure Select-employees
!********************************************************************
#debug9 show 'Select-employees'
 Move 0 to #empl_count
begin-select ON-ERROR=SQL-Error-Found('select-employees')
A.EMPLID
A.EMPL_RCD                         
A.EFFDT                          
A.EFFSEQ
A.COMPANY   
A.PAYGROUP
A.POSITION_NBR
B.NAME
A.ACTION_DT
 
  add 1 to #empl_count
  
    Let $emplid    =  &a.emplid 
    Let $company   =  &a.company  
    Let $action_dt =  &A.ACTION_DT
    
    show '$emplid        :'   $emplid
    show '$company       :'   $company
    show '$action_dt     :'   $action_dt
    
    do Select-employees-new
    do P230-Genl-Deduction
    !do print-employee    ! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project
        
FROM   PS_JOB A, PS_PERSONAL_DATA B
where a.emplid = b.emplid
and a.effdt = (select max(effdt)
               from ps_job
	   where emplid = a.emplid
	   and empl_rcd = a.empl_rcd
	   and action_Dt between $start_dt and $x000_where_select_asofdate)	   
and a.effseq = (select max(effseq)
               from ps_job
	   where emplid = a.emplid
	   and empl_rcd = a.empl_rcd
           and effdt = a.effdt)
and exists (select 'X'
	from ps_job j1
	where j1.emplid = a.emplid
	and j1.empl_rcd = a.empl_rcd
        and j1.company <> a.company
!	and (j1.company <> a.company		!CXA 10/26/2004 Begin
!	 or (j1.company = a.company
!	and j1.deptid <> a.deptid))		!CXA 10/26/2004 End
	and j1.effdt = (select max(j2.effdt)
			from ps_job j2
			where j2.emplid = a.emplid
			and j2.empl_rcd = a.empl_rcd
!			and j2.effdt < $start_dt                              !vendnrr
!                        and j2.effdt < a.effdt)
                        and j2.action_dt < a.action_dt)
       and j1.effseq =(Select max(effseq) 
                        from ps_job j2 
                        where j2.emplid = a.emplid 
                        and j2.empl_rcd = a.empl_rcd 
                        and j2.effdt = j1.effdt))                                                     
order by a.emplid
end-select
 if #empl_count = 0
    Print 'No Data Found on this Report' (+2,{#x000_col_emplid})    bold 
 end-if
end-procedure  !select-employees

!********************************************************************
begin-procedure Select-employees-new
!********************************************************************
#debug9 show 'Select-employees-new'
Begin-Select 
C.EMPLID
C.EMPL_RCD
C.EFFDT 
C.EFFSEQ
C.COMPANY 
C.PAYGROUP 
C.POSITION_NBR 
D.NAME 
C.ACTION_DT


    Let $emplid1    =  &c.emplid 
    Let $company1   =  &c.company  
    Let $action_dt1 =  &c.ACTION_DT
    
    !show '$emplid1        :'   $emplid1
    !show '$company1       :'   $company1
    !show '$action_dt1     :'   $action_dt1
                      
FROM PS_JOB C, 
     PS_PERSONAL_DATA D 
where C.EMPLID=D.EMPLID
AND C.EFFDT = (select max(effdt) from ps_job 
               where emplid = C.emplid 
               and empl_rcd = C.empl_rcd 
               and action_Dt < $action_dt)
AND C.EFFSEQ = (select max(effseq) from ps_job 
               where emplid = C.emplid 
                and empl_rcd = C.empl_rcd 
                and effdt = C.effdt) 
and D.EMPLID = $emplid
order by C.emplid
end-select
end-procedure 

Begin-Procedure P230-Genl-Deduction
#debug9 Show 'P230-Genl-Deduction'
Begin-Select
gd_A.EMPLID
gd_A.COMPANY
gd_A.DEDCD 
gd_A.EFFDT
gd_A.DED_CALC 
gd_A.DEDUCTION_END_DT
! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project Begins
gd_A.GOAL_BAL
gd_A.GOAL_AMT
gd_A.DED_ADDL_AMT
! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project Ends

 let $ded_emplid    =  &gd_A.EMPLID 
 Let $ded_company   =  &gd_A.COMPANY
 Let $dedcd         =  &gd_A.DEDCD
 Let $ded_end_dt    =  &gd_A.DEDUCTION_END_DT
 Let #Goal_Bal      =  &gd_A.GOAL_BAL
 Let #Goal_Amt      =  &gd_A.GOAL_AMT
 Let #Flat_Amt      =  &gd_A.DED_ADDL_AMT
 
 show '$ded_emplid    :'   $ded_emplid 
 show '$ded_company   :'   $ded_company
 show '$ded_end_dt    :'   $ded_end_dt
 show '$dedcd         :'   $dedcd
 show '#Goal_Bal      :'   #Goal_Bal
 do print-employee
  
FROM   PS_GENL_DEDUCTION GD_A
WHERE  EMPLID            = $emplid1
AND    COMPANY = $company1
AND    EFFDT   = 
       (SELECT MAX(EFFDT)
       FROM PS_GENL_DEDUCTION 
       WHERE EMPLID    =  $emplid1
        AND COMPANY    =  $company1
       AND   DEDCD     =  GD_A.DEDCD) 
AND  ((GD_A.dedcd not like ('UN%') and GD_A.dedcd not like ('AS%')) or (GD_A.dedcd ='UNWAY'))
And ((rtrim(GD_A.deduction_end_dt,' ') is null)  or (GD_A.deduction_end_dt > $X000_WHERE_SELECT_ASOFDATE))            
order by gd_A.EMPLID 
End-Select
End-Procedure
!*************************************************************************

!********************************************************************
begin-procedure Print-Employee
!********************************************************************
 show '--------------------------------------'
 show '$ded_emplid    :'   $ded_emplid
 show '$emplid        :'   $emplid
 show '$Prev_Emplid   :'   $Prev_Emplid
 show '--------------------------------------'
  
 If $emplid = $ded_emplid 
  if &b.name <> $Prev_Name
       print &b.name               (+1,{#x000_col_name})
       print $emplid                (0,{#x000_col_emplid})
       print &a.empl_rcd            (0,{#x000_col_empl_rcd}) edit 9
       print &a.effdt               (0,{#x000_col_effdt})
       print $company               (0,{#x000_col_company})
       print &a.paygroup            (0,{#x000_col_paygroup})
       ! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project Begins
       print $ded_company           (0,{#x000_col_company1})
       print $dedcd                 (0,{#x000_col_dedcd}) 
       print #Flat_Amt              (0,{#x000_col_dedamt})   edit 99999.99 
       print #Goal_Amt              (0,{#x000_col_goalamt})  edit 99999.99 
       print #Goal_Bal              (0,{#x000_col_goalbal})  edit 99999.99 
  else      
       print $dedcd                 (+1,{#x000_col_dedcd}) 
       print #Flat_Amt              (0,{#x000_col_dedamt})   edit 99999.99 
       print #Goal_Amt              (0,{#x000_col_goalamt})  edit 99999.99 
       print #Goal_Bal              (0,{#x000_col_goalbal})  edit 99999.99 
       ! ISDVVXK 05/12/2015 Changes related to V2020 Payroll Inefficiences Project Ends
  end-if     
 End-if
  Move &b.name to $Prev_Name
end-procedure


!********************************************************************
begin-procedure SQL-Error-Found($Proc_Name) 
!********************************************************************
  do error-found
  display 'SQL Error in ' 
  display $Proc_Name 
  display $_sql-error
  display 'sql-status:' 
  display #_sql-status 
  STOP
end-procedure

!********************************************************************
begin-procedure Error-Found
!********************************************************************
  move 'Y' to $ErrorFound
  do Format-Number(#InputTran, $out, '99999')
  print $out (+1,1)
 !GEX_SEC_REENG Vahini Katta 09/18/2016 Begins
  !let $EmplId_1st3 = substr($EmplId,1,3)
  !let $EmplId_mid2 = substr($EmplId,4,2)
  !let $EmplId_last4 = substr($EmplId,6,4)
 
  !String $EmplId_1st3 $EmplID_mid2 $EmplId_last4 by '-' into $EmplId11

  !print $EmplID11 (,8)
  print $EmplId (,8)
!GEX_SEC_REENG Vahini Katta 09/18/2016 Ends
end-procedure

!***********************************************************************
Begin-Procedure EOJ
!***********************************************************************
 
  display ' '
  display #empl_count 99999 noline
  display ' Employees Processed ' 
  display ' '

  date-time () hh:mi:ss &timeProcess
  display 'Report ended: ' noline
  display &timeProcess

  Show 'Successful end of report'

end-procedure

#include 'tranctrl.sqc'  !Common Transaction Control Procedures
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions

#Include 'gexxx950.sqc'  !Get SQRW run controls
#Include 'gexxx922.sqc'  !Run Control parameters from GEX_RC_PAY record 

