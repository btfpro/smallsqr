!**************************************************************************
!  GEXXX915:  Retrieve "Multiple" Locality Run Controls and               *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX915_Locality_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX915_INCLUDE_LOCALITY_CRITERIA                                  *
!    $GEXXX915_EXCLUDE_LOCALITY_CRITERIA                                  *
!    $GEXXX915_INCLUDE_LOCALITY_DISPLAY_CRITERIA                          *
!    $GEXXX915_EXCLUDE_LOCALITY_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX915_Locality_Alias = 'LOCALITY'                            *
!    Do GEXXX915-Select-Locality-Parameters                               *
!    Begin-Select                                                         *
!    LOCALITY                                                             *
!    FROM PS_W2_AMOUNTS                                                   *
!    WHERE [$GEXXX915_INCLUDE_LOCALITY_CRITERIA]                          *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		06/04/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX915-Select-Locality-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_LOCALITY_ROWS = 0
  Let $GEXXX915_INCLUDE_LOCALITY_CRITERIA = ''
  Let $GEXXX915_EXCLUDE_LOCALITY_CRITERIA = ''
  Let $GEXXX915_INCLUDE_LOCALITY_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX915_EXCLUDE_LOCALITY_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_LOCALITY.LOCALITY

  Add 1 To #GEX_RC_LOCALITY_ROWS

  If #GEX_RC_LOCALITY_ROWS = 1
    Let $GEXXX915_INCLUDE_LOCALITY_CRITERIA = '(' || $GEXXX915_Locality_Alias || ' In ('''     || &GEX_RC_LOCALITY.LOCALITY || ''''
    Let $GEXXX915_EXCLUDE_LOCALITY_CRITERIA = '(' || $GEXXX915_Locality_Alias || ' Not In (''' || &GEX_RC_LOCALITY.LOCALITY || ''''
  Else
    Let $GEXXX915_INCLUDE_LOCALITY_CRITERIA = $GEXXX915_INCLUDE_LOCALITY_CRITERIA || ',''' || &GEX_RC_LOCALITY.LOCALITY  || ''''
    Let $GEXXX915_EXCLUDE_LOCALITY_CRITERIA = $GEXXX915_EXCLUDE_LOCALITY_CRITERIA || ',''' || &GEX_RC_LOCALITY.LOCALITY  || ''''
  End-If

From  PS_GEX_RC_LOCALITY GEX_RC_LOCALITY

Where GEX_RC_LOCALITY.OPRID                = [$OprID]
And   GEX_RC_LOCALITY.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_LOCALITY.LOCALITY, ' ') != ' '

End-Select

  If #GEX_RC_LOCALITY_ROWS = 0
    Let $GEXXX915_INCLUDE_LOCALITY_CRITERIA = $GEXXX915_Locality_Alias || ' = ' || $GEXXX915_Locality_Alias
    Let $GEXXX915_EXCLUDE_LOCALITY_CRITERIA = $GEXXX915_Locality_Alias || ' = ' || $GEXXX915_Locality_Alias
  Else
    Let $GEXXX915_INCLUDE_LOCALITY_CRITERIA = $GEXXX915_INCLUDE_LOCALITY_CRITERIA || '))'
    Let $GEXXX915_EXCLUDE_LOCALITY_CRITERIA = $GEXXX915_EXCLUDE_LOCALITY_CRITERIA || '))'
    Let $GEXXX915_INCLUDE_LOCALITY_DISPLAY_CRITERIA = $GEXXX915_INCLUDE_LOCALITY_CRITERIA
    Let $GEXXX915_EXCLUDE_LOCALITY_DISPLAY_CRITERIA = $GEXXX915_EXCLUDE_LOCALITY_CRITERIA
  End-If

End-Procedure
