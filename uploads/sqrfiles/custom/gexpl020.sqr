!***********************************************************************
!
!              EMPLOYEE STATUS BY UNION REPORTS (4 REPORTS)  
!
!  GEXPL020A:  Employee Status Change by Union Report
!  GEXPL020B:  Employee Status by Union Report
!  GEXPL020C:  Employee Status "MoonLighter" by Union Report
!  GEXPL020D:  Employee Status Terminations by Union Report
!
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       * 
!                                                                      *
!***********************************************************************
! This SQR is used to produce one of four reports:
!                 GEXPL020A: Employee Status Change by Union Report
!                 GEXPL020B: Employee Status by Union Report
!                 GEXPL020C: Employee Status "MoonLighter" by Union Report
!                 GEXPL020D: Employee Status Terminations by Union Report
!
! Narrative:      Prints the employees detail information based upon which
!                 report was requested. Report information is very similar
!                 on all four reports.
!
! #Debugx Used:	  #debug9 paragraph trace
!                 #debug8 key variable values
!                 #debug7 data returned from sql calls
!
! SQL Tables:     Gex_Dept_Tbl
!                 Personal_Data
!                 Job
!                 Employment
!
! Written by:     Dave Herzog - Soft Link Inc.  06-26-98
!
! Normally Run:   Monthly
!
! Prerequisite:   None 
!
! Entered Run 
! Control Parms:  Division Cd (Allow one, or multiple number
!                              of division codes to be enterd)	 
!
!                 Union       (Allow one, or a multiple 
!			       number of unions to be entered)
!
!		  From Date   (Usually monthbeg date, but can 	
!                              be any date.)
!
!		  Thru Date   (Usually monthend date, but can 
!                              be any date.)
!
!                 Report Type (Select one of the four reports to run)
!                            
!               	
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE      *
!***********************************************************************
!           DAH		06/26/98    Initial Creation	               *
!                                                                      *
!           SXK         09/22/98    Added New Mobius ReportID          *
!                                                                      *
!           JDH         11/11/98    Added logic to populate from and   *
!                                   thru dates based on the pay        *
!                                   calendar when date parameters are  *
!                                   not supplied                       *
!                                                                      *
!           DAH         02/05/99    Added union code description to    *
!                                   headings and added 'retired' to the*
!                                   termination report selection.      *
!                                   Change selection on the term rpt   *
!                                   from action code to empl_status.   *
!                                   (GEX001)                           *
!                                                                      *
!           JDH         02/09/99    Removed term date from Report D    *
!                                   Removed status from Report D       *
!                                   Added action to Report D           *
!                                                                      *
!                                   Changed effective date and action  *
!                                   date to be the dates from the job  *
!                                   row that set the reg_temp to       *
!                                   moonlighter on Report C            *
!                                                                      *
!	    sxk         10/21/99    Modified to have union from        *
!                                   job_labor	                       *
!                                                                      *
!           AXL         02/28/2000  Added pay_off_cycle_cal = 'N to    *
!                                   procedures that determine begin &  *
!                                   end dates to avoid "Bad return     *
!                                   fetching row from db" errors when  *
!                                   run at year end and off cycle cal  *
!                                   exists.                            *
!           PSR      08/12/2002     Modified UNION_CD FROM JOB_LABOR   * 
!                                   TO JOB - Upgrade 8.3               *
!   GEX-CXB 	      10/05/2007   No table structure chnaged 9.0.
!GEX-RBC        11/19/2007      Made Changes to display the date in the 
!				format mm/dd/yy.
!ISDVSXP        02/18/2009          ITG 57503
!  ISDVMXD	02/02/2009	   ITG 56799 JT.Descr changes in SQRs  *
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************

#include 'setenv.sqc'           !Set environment

begin-setup

!#include 'setup32a.sqc' PSR 08/12/02 8 Upgrade
#include 'ptpsl177.sqc' 
  declare-report GEXPL020A
Printer-Type=HPLASERJET
   layout=DEFAULT
  end-declare

  declare-report GEXPL020B
Printer-Type=HPLASERJET
   layout=DEFAULT
  end-declare

  declare-report GEXPL020C
Printer-Type=HPLASERJET
   layout=DEFAULT
  end-declare

  declare-report GEXPL020D
Printer-Type=HPLASERJET
   layout=DEFAULT
  end-declare

end-setup


!***********************************************************************
begin-heading 8 for-reports=(GEXPL020A)	!Employee Change Status Rpt
!***********************************************************************
#debug9 show 'begin-heading-GEXPL020A'
!SXK 09/22/1998 Begin
let $ReportID = 'PL020STC'
!SXK 09/22/1998 End
 #Include 'stdhdg01.sqc'

 print 'Union CD:'			(3,1)
 print $x000_Union_CD			(0,13)
 
 do Get-Union-Cd-Info                                       !GEX001-add
 print $x000_Union_Cd_Descr         (0,+2)                  !GEX001-add

 let $x000_temp_out =  'Sorted by ' || $X000_ORDER_BY 
 print $x000_temp_out 			(0,) center 

 print $X000_REPORT_HEADING_DATE    (+1,) center

!line 1
 print 'STATUS'         (+2,#x000_col_effdt)

!line 2
 print 'EMPLOYEE'		(+1,#x000_col_name)
 print 'SERVICE'        (0,#x000_col_service_dt)
! print 'NEW'            (0,#x000_col_status)
 print 'EFFECTIVE'      (0,#x000_col_effdt)
 print 'ACTION'         (0,#x000_col_action_dt)
 print 'JOB'            (0,#x000_col_jobcode)
 print 'JOB'            (0,#x000_col_jobcode_desc)

!line 3
 print 'NAME'		(+1,#x000_col_name)
 print 'SSNO'           (0,#x000_col_emplid)
 print 'DEPTID'         (0,#x000_col_deptid)  !ITG 57503 ISDVSXP 02/18/2009 added deptid 
 print 'DATE'           (0,#x000_col_service_dt)
 print 'STATUS'         (0,#x000_col_status)
 print 'DATE'           (0,#x000_col_effdt)
 print 'DATE'           (0,#x000_col_action_dt)
 print 'CODE'           (0,#x000_col_jobcode)
 print 'DESC'           (0,#x000_col_jobcode_desc)

end-heading


!***********************************************************************
begin-heading 8 for-reports=(GEXPL020B)	!Employee Status Rpt
!***********************************************************************
#debug9 show 'begin-heading-GEXPL020B'
!SXK 09/22/1998 Begin
let $ReportID = 'PL020STU'
!SXK 09/22/1998 End
 #Include 'stdhdg01.sqc'

 print 'Union CD:'			(3,1)
 print $x000_Union_CD			(0,13)

 do Get-Union-Cd-Info                                       !GEX001-add
 print $x000_Union_Cd_Descr         (0,+2)                  !GEX001-add
 show '$x000_Union_Cd_Descr=' $x000_Union_Cd_Descr          !GEX001-add
	
 let $x000_temp_out =  'Sorted by ' || $X000_ORDER_BY 
 print $x000_temp_out 			(0,) center 

 print $X000_REPORT_HEADING_DATE    (+1,) center


!line 1
 print 'STATUS'         (+2,#x000_col_effdt)

!line 2
 print 'EMPLOYEE'		(+1,#x000_col_name)
 print 'SERVICE'        (0,#x000_col_service_dt)
 print 'EFFECTIVE'      (0,#x000_col_effdt)
 print 'ACTION'         (0,#x000_col_action_dt)
 print 'JOB'            (0,#x000_col_jobcode)
 print 'JOB'            (0,#x000_col_jobcode_desc)

!line 3
 print 'NAME'		(+1,#x000_col_name)
 print 'SSNO'           (0,#x000_col_emplid)
 print 'DATE'           (0,#x000_col_service_dt)
 print 'STATUS'         (0,#x000_col_status)
 print 'DATE'           (0,#x000_col_effdt)
 print 'DATE'           (0,#x000_col_action_dt)
 print 'CODE'           (0,#x000_col_jobcode)
 print 'DESC'           (0,#x000_col_jobcode_desc)

end-heading


!***********************************************************************
begin-heading 8 for-reports=(GEXPL020C)	!Employee MoonLighters Rpt
!***********************************************************************
#debug9 show 'begin-heading-GEXPL020C'
!SXK 09/22/1998 Begin
let $ReportID = 'PL020STM'
!sxk 09/22/1998 End
 #Include 'stdhdg01.sqc'

 print 'Union CD:'			(3,1)              
 print $x000_Union_CD			(0,13)             

 do Get-Union-Cd-Info                                       !GEX001-add
 print $x000_Union_Cd_Descr         (0,+2)                  !GEX001-add

 let $x000_temp_out =  'Sorted by ' || $X000_ORDER_BY 
 print $x000_temp_out 			(0,) center 

 print $X000_REPORT_HEADING_DATE    (+1,) center

!line 1
 print 'STATUS'         (+2,#x000_col_effdt)

!line 2
 print 'EMPLOYEE'		(+1,#x000_col_name)
 print 'SERVICE'        (0,#x000_col_service_dt)
 print 'EFFECTIVE'      (0,#x000_col_effdt)
 print 'ACTION'         (0,#x000_col_action_dt)
 print 'JOB'            (0,#x000_col_jobcode)
 print 'JOB'            (0,#x000_col_jobcode_desc)

!line 3
 print 'NAME'		(+1,#x000_col_name)
 print 'SSNO'           (0,#x000_col_emplid)
 print 'DEPTID'         (0,#x000_col_deptid)  !ITG 57503 ISDVSXP 02/18/2009 added deptid 
 print 'DATE'           (0,#x000_col_service_dt)
 print 'STATUS'         (0,#x000_col_status)
 print 'DATE'           (0,#x000_col_effdt)
 print 'DATE'           (0,#x000_col_action_dt)
 print 'CODE'           (0,#x000_col_jobcode)
 print 'DESC'           (0,#x000_col_jobcode_desc)

end-heading


!***********************************************************************
begin-heading 8 for-reports=(GEXPL020D)	!Employee Terminations Rpt
!***********************************************************************
#debug9 show 'begin-heading-GEXPL020D'
!SXK 09/22/1998 Begin
let $ReportID = 'PL020STT'
!SXK 09/22/1998 End
 #Include 'stdhdg01.sqc'
	
 print 'Union CD:'			(3,1)
 print $x000_Union_CD			(0,13)

 do Get-Union-Cd-Info                                       !GEX001-add
 print $x000_Union_Cd_Descr         (0,17)                  !GEX001-add

 let $x000_temp_out =  'Sorted by ' || $X000_ORDER_BY 
 print $x000_temp_out 			(0,) center 

 print $X000_REPORT_HEADING_DATE    (+1,) center

!line 1
 print 'STATUS'         (+2,#x000_col_effdt)

!line 2
 print 'EMPLOYEE'		(+1,#x000_col_name)
 print 'SERVICE'        (0,#x000_col_service_dt)
! print 'TERM'           (0,#x000_col_termination_dt)
! print 'NEW'            (0,#x000_col_status)
 print 'EFFECTIVE'      (0,#x000_col_effdt)
 print 'ACTION'         (0,#x000_col_action_dt)
 print 'JOB'            (0,#x000_col_jobcode)
 print 'JOB'            (0,#x000_col_jobcode_desc)

!line 3
 print 'NAME'		(+1,#x000_col_name)
 print 'SSNO'           (0,#x000_col_emplid)
 print 'DEPTID'         (0,#x000_col_deptid) !ITG 57503 ISDVSXP 02/18/2009 added deptid 
 print 'DATE'           (0,#x000_col_service_dt)
! print 'DATE'           (0,#x000_col_termination_dt)
! print 'STATUS'         (0,#x000_col_status)
 print 'ACTION'         (0,#x000_col_action)
 print 'DATE'           (0,#x000_col_effdt)
 print 'DATE'           (0,#x000_col_action_dt)
 print 'CODE'           (0,#x000_col_jobcode)
 print 'DESC'           (0,#x000_col_jobcode_desc)

end-heading


!***********************************************************************
begin-report
!***********************************************************************
#debug9 show 'begin-report'


  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  show '$asoftoday=' $asoftoday 

  do StdAPI-Init
  do P010-Init-Variables
  do P200-Get-Current-Job-Data
  do StdAPI-Term
  do Reset
 
  show 'Successful End of Report'

end-report

!***********************************************************************
begin-procedure P010-Init-Variables
!***********************************************************************
#debug9 show ' '
#debug9 show 'P010-Init-Variables'

  if $Prcs_Process_Instance = ''
	do P015-get-input
  else	
      !get parameters from single row run control record 
	Do GEXXX922-Select-Parameters
      let $FromDate = &GEX_RC_PAY.FROMDATE
      let $ThruDate = &GEX_RC_PAY.THRUDATE  
      if rtrim($FromDate,' ') = '' or
         rtrim($ThruDate,' ') = ''
        do Get-From-Date
        do Get-Thru-Date
        let $FromDate = &from_date
        let $ThruDate = &thru_date
      end-if
      do Format-DateTime($FromDate, $out1, {DEFDATE}, '', '')
      do Format-DateTime($ThruDate, $out2, {DEFDATE}, '', '')
      let $X000_REPORT_HEADING_DATE = 'From ' || $out1 || ' Thru ' || $out2 
      show '$X000_REPORT_HEADING_DATE=' $X000_REPORT_HEADING_DATE   
      let $x000_Report_Type = &GEX_RC_PAY.GEX_REPORT_TYPE
 	Let $GEXXX907_Div_CD_Alias = 'GDT.GEX_COMPANY_DIV_CD'
	Do GEXXX907-Select-Div-CD-Parameters
      let $GEXXX913_UNION_CD_Alias  = 'J.UNION_CD'  !SXK 10/21/99
      Do GEXXX913-Select-UNION-CD-Parameters    
  end-if
           
  Evaluate $x000_Report_Type
  When='A'
    let $ReportID    = 'GEXPL020A'
    let $ReportTitle = 'Employee Status Change Report'
    let $Country     = 'USA'
!SXK 10/21/99 Changed from E.UNION_CD
    let $X000_ORDER_BY = 'J.UNION_CD, PD.NAME, J.EFFDT DESC, J.EFFSEQ DESC ' 
    let $x000_report_type_display_criteria = 'Report Type = A    (GEXPL020A) '
    let $x000_effdt_select_criteria = ''
    let $x000_effseq_select_criteria = '' 
    let $x000_moonlighter_select_criteria = ''
    let $x000_termination_select_criteria = '' 
    !selection criteria
    let #x000_col_selection_criteria_a 	= 02
    let #x000_col_selection_criteria_b 	= 05
    !heading and detail lines
    let #x000_col_name         = 01
    let #x000_col_emplid       = 30
    !ITG 57503 ISDVSXP 02/18/2009 added deptid,changed print column numbers of below fields -start
    let #x000_col_deptid       = 42   
    let #x000_col_service_dt   = 50
    let #x000_col_status       = 60
    let #x000_col_effdt        = 71
    let #x000_col_action_dt    = 82
    let #x000_col_jobcode      = 93
    let #x000_col_jobcode_desc = 100
    !ITG 57503 ISDVSXP 02/18/2009 added deptid -- end
    !total lines
    let #x000_col_totals_literal  = 4
    let #x000_col_totals_literal2 = 20
    let #x000_col_totals_literal3 = 30
    let #x000_col_union_cd_total  = 40
    use-report GEXPL020A
  When='B'
    let $ReportID    = 'GEXPL020B'
    let $ReportTitle = 'Employee Status Report'
    let $Country     = 'USA'
!SXK 10/21/99 Changed from E.UNION_CD
    let $X000_ORDER_BY = 'J.UNION_CD, PD.NAME ' 
    let $x000_report_type_display_criteria = 'Report Type = B    (GEXPL020B) '
    let $x000_effdt_select_criteria = ' AND J.EFFDT  = (SELECT MAX(J2.EFFDT) FROM PS_JOB J2 -
            WHERE J2.EMPLID  = J.EMPLID -
            AND J2.EMPL_RCD = J.EMPL_RCD -
            AND J2.EFFDT    <= ''' || $ThruDate || ''') '
    let $x000_effseq_select_criteria = ' AND J.EFFSEQ = (SELECT MAX(J3.EFFSEQ) FROM PS_JOB J3 -
            WHERE J3.EMPLID  = J.EMPLID -
            AND J3.EMPL_RCD = J.EMPL_RCD -
            AND J3.EFFDT     = J.EFFDT) '
    let $x000_moonlighter_select_criteria = ''
    let $x000_termination_select_criteria = ''
    !selection criteria
    let #x000_col_selection_criteria_a 	= 02
    let #x000_col_selection_criteria_b 	= 05 
    !heading and detail lines
    let #x000_col_name         = 01
    let #x000_col_emplid       = 30
    let #x000_col_service_dt   = 42
    let #x000_col_status       = 52
    let #x000_col_effdt        = 63
    let #x000_col_action_dt    = 74
    let #x000_col_jobcode      = 85
    let #x000_col_jobcode_desc = 92
    !total lines
    let #x000_col_totals_literal  = 4
    let #x000_col_totals_literal2 = 20
    let #x000_col_totals_literal3 = 30
    let #x000_col_union_cd_total  = 40
    use-report GEXPL020B
  When='C'
    let $ReportID    = 'GEXPL020C'
    let $ReportTitle = 'Employee Moonlighters Report'
    let $Country     = 'USA'
!SXK 10/21/99 
    let $X000_ORDER_BY = 'J.UNION_CD, PD.NAME'
    let $x000_report_type_display_criteria = 'Report Type = C    (GEXPL020C) '
    let $x000_effdt_select_criteria = ' AND J.EFFDT  = (SELECT MAX(J2.EFFDT) FROM PS_JOB J2 -
            WHERE J2.EMPLID  = J.EMPLID -
            AND J2.EMPL_RCD = J.EMPL_RCD -
            AND J2.EFFDT    <= ''' || $ThruDate || ''') '
    let $x000_effseq_select_criteria = ' AND J.EFFSEQ = (SELECT MAX(J3.EFFSEQ) FROM PS_JOB J3 -
            WHERE J3.EMPLID  = J.EMPLID -
            AND J3.EMPL_RCD = J.EMPL_RCD -
            AND J3.EFFDT     = J.EFFDT) '
    let $x000_moonlighter_select_criteria = ' AND J.GEX_MOON_IND = ''Y'' '
    let $x000_termination_select_criteria = '' 
    !selection criteria
    let #x000_col_selection_criteria_a 	= 02
    let #x000_col_selection_criteria_b 	= 05
    !heading and detail lines
    let #x000_col_name         = 01
    let #x000_col_emplid       = 30
     !ITG 57503 ISDVSXP 02/18/2009 added deptid,changed print column numbers of below fields -start
    let #x000_col_deptid       = 42  
    let #x000_col_service_dt   = 50
    let #x000_col_status       = 60
    let #x000_col_effdt        = 71
    let #x000_col_action_dt    = 82
    let #x000_col_jobcode      = 93
    let #x000_col_jobcode_desc = 100
    !ITG 57503 ISDVSXP 02/18/2009 added deptid --end
    !total lines
    let #x000_col_totals_literal  = 4
    let #x000_col_totals_literal2 = 20
    let #x000_col_totals_literal3 = 30
    let #x000_col_union_cd_total  = 40
    use-report GEXPL020C
  When='D'
    let $ReportID    = 'GEXPL020D'
    let $ReportTitle = 'Employee Terminations Report'
    let $Country     = 'USA'
    let $X000_ORDER_BY = 'J.UNION_CD, PD.NAME' !SXK 10/21/99
    let $x000_report_type_display_criteria = 'Report Type = D    (GEXPL020D) '
    let $x000_effdt_select_criteria = ' AND J.EFFDT  = (SELECT MAX(J2.EFFDT) FROM PS_JOB J2 -
            WHERE J2.EMPLID  = J.EMPLID -
            AND J2.EMPL_RCD = J.EMPL_RCD -
            AND J2.EFFDT    <= ''' || $ThruDate || ''') '
    let $x000_effseq_select_criteria = ' AND J.EFFSEQ = (SELECT MAX(J3.EFFSEQ) FROM PS_JOB J3 -
            WHERE J3.EMPLID  = J.EMPLID -
            AND J3.EMPL_RCD = J.EMPL_RCD -
            AND J3.EFFDT     = J.EFFDT) '

    let $x000_moonlighter_select_criteria = ''

!    let $x000_termination_select_criteria = ' AND J.ACTION in (''TER'',''TWB'',''TWP'') '     !GEX001-del
    let $x000_termination_select_criteria = ' AND J.EMPL_STATUS in (''Q'',''R'',''T'',''U'') ' !GEX001-add

    !selection criteria
    let #x000_col_selection_criteria_a 	= 02
    let #x000_col_selection_criteria_b 	= 05
    !heading and detail lines
    let #x000_col_name         = 01
    let #x000_col_emplid       = 30
    !ITG 57503 ISDVSXP 02/18/2009 added deptid,changed print column numbers of below fields -start
    let #x000_col_deptid       = 42  
    let #x000_col_service_dt   = 50
!    let #x000_col_status       = 52
    let #x000_col_action       = 60
    let #x000_col_effdt        = 71
    let #x000_col_action_dt    = 82
    let #x000_col_jobcode      = 93
    let #x000_col_jobcode_desc = 100
    !ITG 57503 ISDVSXP 02/18/2009 added deptid -- end
!    let #x000_col_termination_dt = 52
!    let #x000_col_status       = 62
!    let #x000_col_effdt        = 73
!    let #x000_col_action_dt    = 84
!    let #x000_col_jobcode      = 95
!    let #x000_col_jobcode_desc = 102
    !total lines
    let #x000_col_totals_literal  = 4
    let #x000_col_totals_literal2 = 20
    let #x000_col_totals_literal3 = 30
    let #x000_col_union_cd_total  = 40
    use-report GEXPL020D
  When-Other
    Show '**Error**GEXPL020 SQR. Invalid report type indicated. Report type=' $x000_Report_Type 
    Show 'Stopping SQR.'
    Print '**Error**GEXPL020 SQR. Invalid report type indicated. Report type=' (+5,10)
    print $x000_Report_Type (0,+1)
    Print 'Stopping SQR.' (+1,10)
    STOP
  End-Evaluate

  show '$ReportID=' $ReportID 
  show '$ReportTitle=' $ReportTitle
  show '$ReportDate=' $ReportDate
  show '$ReportTime=' $ReportTime
  show '$x000_report_type=' $x000_report_type

 show '$GEXXX907_INCLUDE_DIV_CD_CRITERIA        =' $GEXXX907_INCLUDE_DIV_CD_CRITERIA
 show '$GEXXX913_INCLUDE_UNION_CD_CRITERIA      =' $GEXXX913_INCLUDE_UNION_CD_CRITERIA
 show '$FromDate=' $FromDate  
 show '$ThruDate =' $ThruDate 
 show '$x000_report_type_display_criteria       =' $x000_report_type 
 show '$x000_effdt_select_criteria              =' $x000_effdt_select_criteria 
 show '$x000_effseq_select_criteria             =' $x000_effseq_select_criteria 
 show '$x000_moonlighter_select_criteria        =' $x000_moonlighter_select_criteria  
 show '$x000_termination_select_criteria        =' $x000_termination_select_criteria 

 print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,#x000_col_selection_criteria_a)
 print '$Prcs_OPRID          ='                              (+1,#x000_col_selection_criteria_b)
 print $Prcs_OPRID                                           (0,+2)
 print '$PRCS_RUN_CNTL_ID    ='                              (+1,#x000_col_selection_criteria_b)
 print $PRCS_RUN_CNTL_ID                                     (0,+2)

 print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,#x000_col_selection_criteria_a)
 print '$FromDate                                           =' (+1,#x000_col_selection_criteria_b)
 print $FromDate                                            (0,+2)
 print '$ThruDate                                           =' (+1,#x000_col_selection_criteria_b)
 print $ThruDate                                            (0,+2)
 print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+1,#x000_col_selection_criteria_b)
 print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA 		(0,+2)
 print '$GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA         ='   (+1,#x000_col_selection_criteria_b)
 print $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA		(0,+2)
 print '$x000_report_type_display_criteria                  ='   (+1,#x000_col_selection_criteria_b)
 print $x000_report_type_display_criteria 		      (0,+2)
 print '$x000_moonlighter_select_criteria                   ='   (+1,#x000_col_selection_criteria_b)
 print $x000_moonlighter_select_criteria		            (0,+2)
 print '$x000_termination_select_criteria                   ='   (+1,#x000_col_selection_criteria_b)
 print $x000_termination_select_criteria		            (0,+2)
    

 print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,#x000_col_selection_criteria_a)
 print '$X000_ORDER_BY                                      ='   (+1,#x000_col_selection_criteria_b)
 print $X000_ORDER_BY 	(0,+2)

 let #page-count = 0
 NEW-PAGE
 LET #PAGE-COUNT = 1

end-procedure


!***************************************************
begin-procedure P015-get-input
!***************************************************
#debug9 show 'P015-get-input'

P015-Reask-Union-Code-Input: 
  input $x000_Union_Cd maxlen=3 type=char status=#x000_input_status 'Enter Union Code'
  if #x000_input_status <> 0
    goto P015-Reask-Union-Code-Input
  end-if
  let $x000_Union_Cd = upper($x000_Union_Cd)
  if rtrim($x000_Union_Cd,' ') = '' 
!SXK 10/21/99 Changed from E.UNION_CD- Begin
    let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = ' J.UNION_CD  = J.UNION_CD  '
    let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'J.UNION_CD = ALL '
  else
    let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = ' J.UNION_CD ' || ' In ('''  || $x000_Union_Cd  || ''')'
    let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA 
!SXK 10/21/99 End
  end-if

P015-Reask-Div-Code-Input: 
  input $x000_Div_Cd maxlen=3 type=char status=#x000_input_status 'Enter Company Division Code'
  if #x000_input_status <> 0
    goto P015-Reask-Div-Code-Input
  end-if
  let $x000_Div_Cd = upper($x000_Div_Cd)
  if rtrim($x000_Div_Cd,' ') = '' 
    let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = ' GDT.GEX_COMPANY_DIV_CD = GDT.GEX_COMPANY_DIV_CD '  
    let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = 'GDT.GEX_COMPANY_DIV_CD = ALL '  
  else
    let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = ' GDT.GEX_COMPANY_DIV_CD' || ' In ('''     || $x000_Div_Cd || ''')'  
    let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA 
  end-if

 do Ask-From-Thru-Date
 show '$FromDate=' $FromDate
 show '$ThruDate=' $ThruDate
 let $X000_REPORT_HEADING_DATE = 'From ' || $out1 || ' Thru ' || $out2
 show '$X000_REPORT_HEADING_DATE=' $X000_REPORT_HEADING_DATE 

P015-Reask-Report-Type-Input: 
  input $x000_Report_Type maxlen=1 type=char status=#x000_input_status 'Enter Report Type. Enter A B C or D)'
  if #x000_input_status <> 0
    goto P015-Reask-Report-Type-Input 
  end-if
  let $x000_Report_Type = upper($x000_Report_Type)

end-procedure


!***********************************************************************
begin-procedure P200-Get-Current-Job-Data
!***********************************************************************
#debug9 show 'P200-Get-Current-Job-Data'

  let $x200_Get_Current_Job_Data_Flag = 'N'

begin-SELECT 
!SXK 10/21/99 Modified from E.UNION_CD
J.UNION_CD   () on-break
                print=never
                level=01 
                before=Union-CD-Before-Change
                after=Union-CD-After-Change
                                    	
J.EMPLID
J.EMPL_RCD
J.EFFDT
J.EFFSEQ
J.JOBCODE
J.REG_TEMP
J.EMPL_STATUS
J.DEPTID
J.ACTION
J.ACTION_DT 
J.GEX_MOON_IND       
J.POSITION_NBR		!ISDVMXD added for ITG 56799 on 02/02/2009       
PD.NAME
 
E.TERMINATION_DT
E.SERVICE_DT

GDT.DEPTID
GDT.EFFDT
GDT.SEQUENCE_NUMBER
GDT.GEX_COMPANY_DIV_CD

  #debug7 show '**** Get Current JOB Row *****'
  #debug7 show '&J.EMPLID        =' &J.EMPLID
  #debug7 show '&J.EMPL_RCD     =' &J.EMPL_RCD
  #debug7 show '&J.EFFDT         =' &J.EFFDT
  #debug7 show '&J.EFFSEQ        =' &J.EFFSEQ
  #debug7 show '&J.JOBCODE       =' &J.JOBCODE
  #debug7 show '&J.REG_TEMP      =' &J.REG_TEMP
  #debug7 show '&J.EMPL_STATUS   =' &J.EMPL_STATUS
  #debug7 show '&J.DEPTID        =' &J.DEPTID 
  #debug7 show '&J.ACTION_DT     =' &J.ACTION_DT
  #debug7 show '&J.ACTION        =' &J.ACTION 
  #debug7 show '&J.GEX_MOON_IND  =' &J.GEX_MOON_IND
  #debug7 show '&PD.NAME         =' &PD.NAME
  #debug7 show '&E.TERMINATION_DT=' &E.TERMINATION_DT
  #debug7 show '&J.UNION_CD      =' &J.UNION_CD  !SXK 10/21/99

  if $x000_Report_Type = 'A'
!when there are more than 1 action per date, adjust the selection
!accordingly so rows in the same effdt will be tested as priors
    if &J.EFFSEQ > 0
     let #X000_WHERE_SELECT_CURRENT_JOB_EFFSEQ = &J.EFFSEQ - 1
     let $X000_WHERE_SELECT_CURRENT_JOB_EFFDT = &J.EFFDT
!     show '***EFFSEQ > 0****'
!     show '#X000_WHERE_SELECT_CURRENT_JOB_EFFSEQ=' #X000_WHERE_SELECT_CURRENT_JOB_EFFSEQ 
!     show '$X000_WHERE_SELECT_CURRENT_JOB_EFFDT=' $X000_WHERE_SELECT_CURRENT_JOB_EFFDT 
    else
!     show '***EFFSEQ = 0****'
     let $dtu_date = &J.EFFDT
!    convert the native date to yyyy-mm-dd format for the dtu routine
     do convert-to-dtu-date($dtu_date, $dtu_date_out)
!     show '$dtu_date_out=' $dtu_date_out     
     let $dtu_date = $dtu_date_out
     do dtu-subtract-days($dtu_date,1,$dtu_date_out)
!     show '$dtu_date_out=' $dtu_date_out  
     let $dtu_date = $dtu_date_out
!    convert back to native date from yyyy-mm-dd format 
     do convert-from-dtu-date($dtu_date, $dtu_date_out) 
     let $X000_WHERE_SELECT_CURRENT_JOB_EFFDT = $dtu_date_out 
     let #X000_WHERE_SELECT_CURRENT_JOB_EFFSEQ = 999
!     show '#X000_WHERE_SELECT_CURRENT_JOB_EFFSEQ=' #X000_WHERE_SELECT_CURRENT_JOB_EFFSEQ 
!     show '$X000_WHERE_SELECT_CURRENT_JOB_EFFDT=' $X000_WHERE_SELECT_CURRENT_JOB_EFFDT     
    end-if
   ! only report type A requires the prior job row
   do P210-Get-Prior-Job-Data
  else
   if $x000_Report_Type = 'C'
     Do P235-Get-Status-Date
   end-if
   do P800-Print-Current-Job-Detail-Line
  end-if

  Let $Position_Nbr = Ltrim(RTrim(&J.POSITION_NBR,' '),' ')		!isdvmxd added for ITG 56799 on 02/02/2009
  let $x200_Get_Current_Job_Data_Flag = 'Y'

FROM   PS_GEX_DEPT_TBL      GDT,
       PS_PERSONAL_DATA     PD,
       PS_EMPLOYMENT        E,
       PS_JOB               J

WHERE  PD.EMPLID     = J.EMPLID


AND    J.EMPLID      = E.EMPLID
AND    J.EMPL_RCD   = E.EMPL_RCD

AND    J.ACTION_DT BETWEEN $FromDate And $ThruDate
!sxk 10/21/99 
AND    J.UNION_CD NOT IN ('NON','SEC')                

AND    GDT.DEPTID    = J.DEPTID 

AND    GDT.EFFDT     =
      (SELECT MAX(GDT1.EFFDT)
       FROM   PS_GEX_DEPT_TBL GDT1
       WHERE  GDT1.DEPTID = GDT.DEPTID
       AND    GDT1.EFFDT <= $ThruDate) 

AND    GDT.SEQUENCE_NUMBER =
      (SELECT MAX(GDT2.SEQUENCE_NUMBER)
       FROM   PS_GEX_DEPT_TBL GDT2
       WHERE  GDT2.DEPTID = GDT.DEPTID
       AND    GDT2.EFFDT  = GDT.EFFDT)

AND   [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
AND   [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]

[$x000_effdt_select_criteria]
[$x000_effseq_select_criteria]
[$x000_moonlighter_select_criteria]
[$x000_termination_select_criteria]
 
ORDER BY [$X000_ORDER_BY]
end-SELECT

  if $x200_Get_Current_Job_Data_Flag = 'N'
    print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) CENTER
  end-if
end-procedure


!***********************************************************************
begin-procedure P210-Get-Prior-Job-Data
!***********************************************************************
#debug9 show 'P210-Get-Prior-Job-Data'

 let $x200_Get_Prior_Job_Data_Flag = 'N'

begin-SELECT 

PJ.EMPLID
PJ.EMPL_RCD
PJ.EFFDT
PJ.EFFSEQ
PJ.JOBCODE
PJ.REG_TEMP
PJ.ACTION
PJ.EMPL_STATUS
PJ.DEPTID

  #debug7 show '**** Get Prior JOB Row *****'
  #debug7 show '&PJ.EMPLID     =' &PJ.EMPLID
  #debug7 show '&PJ.EMPL_RCD  =' &PJ.EMPL_RCD
  #debug7 show '&PJ.EFFDT      =' &PJ.EFFDT
  #debug7 show '&PJ.EFFSEQ     =' &PJ.EFFSEQ
  #debug7 show '&PJ.JOBCODE    =' &PJ.JOBCODE
  #debug7 show '&PJ.REG_TEMP   =' &PJ.REG_TEMP
  #debug7 show '&PJ.EMPL_STATUS=' &PJ.EMPL_STATUS
  #debug7 show '&PJ.DEPTID     =' &PJ.DEPTID 

  let $x200_Get_Prior_Job_Data_Flag = 'Y'

  if  &PJ.REG_TEMP <> &J.REG_TEMP
     do P800-Print-Current-Job-Detail-Line
  end-if

FROM   PS_JOB PJ
 
WHERE  PJ.EMPLID    = &J.EMPLID
AND    PJ.EMPL_RCD = &J.EMPL_RCD

AND    PJ.EFFDT     =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID    = PJ.EMPLID
       AND    EMPL_RCD = PJ.EMPL_RCD
       AND    EFFDT    <= $X000_WHERE_SELECT_CURRENT_JOB_EFFDT)

AND    PJ.EFFSEQ =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB
       WHERE  EMPLID    = PJ.EMPLID
       AND    EMPL_RCD = PJ.EMPL_RCD
       AND    EFFDT     = PJ.EFFDT
       AND    EFFSEQ   <= #X000_WHERE_SELECT_CURRENT_JOB_EFFSEQ)

ORDER BY PJ.EMPLID, PJ.EMPL_RCD, PJ.EFFDT, PJ.EFFSEQ
					
end-SELECT
end-procedure


Begin-Procedure P235-Get-Status-Date
#debug9 Show 'P235-Get-Status-Date'
  Let $Reg-Temp-Change = 'N'
Begin-Select
J1.REG_TEMP
J1.EFFDT
J1.ACTION_DT

  If &J1.REG_TEMP = &J.REG_TEMP And
      $Reg-Temp-Change = 'N'
    Let $Effective-Date = &J1.EFFDT
    Let $Action-Date = &J1.ACTION_DT
    Let $Reg-Temp-Change = 'N'
  Else
    Let $Reg-Temp-Change = 'Y'
  End-If

FROM   PS_JOB J1

WHERE  EMPLID     = &J.EMPLID
AND    EMPL_RCD  = &J.EMPL_RCD
AND    EFFDT     <= &J.EFFDT
ORDER BY J1.EFFDT DESC
End-Select
End-Procedure


!***********************************************************************
begin-procedure P800-Print-Current-Job-Detail-Line
!***********************************************************************
#debug9 show 'P800-Print-Current-Job-Detail-Line'
  do Get-Emp-Ssn (&J.EMPLID, $Ssn_Out) !GEX_SEC_REENG 2015-04-29 Vahini Katta  
  Evaluate $x000_Report_Type
  When='A'             !GEXPL020A
  When='B'             !GEXPL020B
  When='C'             !GEXPL020C
    print &PD.NAME             (+1,#x000_col_name,30)
    !print &J.EMPLID            (0,#x000_col_emplid) !GEX_SEC_REENG 2015-04-29 Vahini Katta 
    print $Ssn_Out             (0,#x000_col_emplid)  !GEX_SEC_REENG 2015-04-29 Vahini Katta 
    !ITG 57503 ISDVSXP 02/18/2009 added deptid -- start
    IF ($x000_Report_Type = 'A' or $x000_Report_Type = 'C')
    print &J.DEPTID            (0,#x000_col_deptid)
    END-IF
    !ITG 57503 ISDVSXP 02/18/2009 added deptid -- end 

    do Format-DateTime(&E.SERVICE_DT, $out1, {DEFDATE}, '', '')
    Let $out1=edit(&E.SERVICE_DT,'mm/dd/yy') 
    print $out1 (0,#x000_col_service_dt)

    let $FieldName = 'REG_TEMP'
    let $FieldValue = &J.REG_TEMP
    let $AsOfDate = $ThruDate
    do Read-Translate-Table
    print $XlatShortName       (0,#x000_col_status)

    If $x000_Report_Type = 'C'
      do Format-DateTime($Effective-Date, $out1, {DEFDATE}, '', '')
    Else
      do Format-DateTime(&J.EFFDT, $out1, {DEFDATE}, '', '')
      Let $out1=edit(&J.EFFDT,'mm/dd/yy') !VENDRBC
    End-If
    print $out1 (0,#x000_col_effdt)

    If $x000_Report_Type = 'C'
      do Format-DateTime($Action-Date, $out1, {DEFDATE}, '', '')
    Else
      do Format-DateTime(&J.ACTION_DT, $out1, {DEFDATE}, '', '')
      Let $out1=edit(&J.ACTION_DT,'mm/dd/yy') !VENDRBC
    End-If
    print $out1 (0,#x000_col_action_dt)

    print &J.JOBCODE           (0,#x000_col_jobcode)
    let $x000_JobCode = &J.JOBCODE
    !do Get-Job-Code-Info
     !ISDVMXD added for ITG 56799 on 02/02/2009 -- Begin
    	
    Do Get-Position-Title		!isdvmxd added for ITG 56799 on 02/02/2009
    If $PosName = ' '
    	do Get-Job-Code-Info
    	Show 'Jobtitle from jobcode table = ' $x000_JobTitle
    Else
    	Let $x000_JobTitle = $PosName
    	Show 'Jobtitle from position-data table = ' $x000_JobTitle
    End-If
    !ISDVMXD added for ITG 56799 on 02/02/2009 -- End
    
    print $x000_JobTitle     (0,#x000_col_jobcode_desc)
    add 1 to #x000_union_cd_total_count round=0

  When='D'             !GEXPL020D
    print &PD.NAME              (+1,#x000_col_name,30)
   !print &J.EMPLID            (0,#x000_col_emplid) !GEX_SEC_REENG 2015-04-29 Vahini Katta 
    print $Ssn_Out             (0,#x000_col_emplid)  !GEX_SEC_REENG 2015-04-29 Vahini Katta 
    print &J.DEPTID             (0,#x000_col_deptid)  !ITG 57503 ISDVSXP 02/18/2009 added deptid 

    do Format-DateTime(&E.SERVICE_DT, $out1, {DEFDATE}, '', '')
    Let $out1=edit(&E.SERVICE_DT,'mm/dd/yy') !VENDRBC
    print $out1                (0,#x000_col_service_dt)

!    do Format-DateTime(&E.TERMINATION_DT , $out1, {DEFDATE}, '', '')
!    print $out1 	             (0,#x000_col_termination_dt)

!    let $FieldName = 'REG_TEMP'
!    let $FieldValue = &J.REG_TEMP
!    let $AsOfDate = $ThruDate
!    do Read-Translate-Table
!    print $XlatShortName       (0,#x000_col_status)
    print &J.ACTION             (0,#x000_col_action)

    do Format-DateTime(&J.EFFDT, $out1, {DEFDATE}, '', '')
    Let $out1=edit(&J.EFFDT,'mm/dd/yy') !VENDRBC
    print $out1                (0,#x000_col_effdt)

    do Format-DateTime(&J.ACTION_DT, $out1, {DEFDATE}, '', '')
    Let $out1=edit(&J.ACTION_DT,'mm/dd/yy') !VENDRBC
    print $out1 (0,#x000_col_action_dt)

    print &J.JOBCODE           (0,#x000_col_jobcode)
    let $x000_JobCode = &J.JOBCODE
    !do Get-Job-Code-Info
    
    !ISDVMXD added for ITG 56799 on 02/02/2009 -- Begin
    	
    Do Get-Position-Title
    If $PosName = ' '
    	do Get-Job-Code-Info
    	Show 'Jobtitle from jobcode table = ' $x000_JobTitle
    Else
    	Let $x000_JobTitle = $PosName
    	Show 'Jobtitle from position-data table = ' $x000_JobTitle
    End-If
    !ISDVMXD added for ITG 56799 on 02/02/2009 -- End
    
    print $x000_JobTitle     (0,#x000_col_jobcode_desc)
    add 1 to #x000_union_cd_total_count round=0

  When-Other
    Show '**Error**GEXPL020 SQR. Invalid report type indicated in P800.'
    Show 'Stopping SQR.'
    Print '**Error**GEXPL020 SQR. Invalid report type indicated in P800.' (+5,10)
    Print 'Stopping SQR.' (+1,10)
    STOP

  End-Evaluate  

end-procedure


!*****************************************
begin-procedure Union-CD-Before-Change
!*****************************************
#debug9 show 'Union-CD-Before-Change'			
!SXK 10/21/99 
 move &J.UNION_CD  to $x000_Union_CD 
 move 0 to #x000_union_cd_total_count 

end-procedure


!*****************************************
begin-procedure Union-CD-After-Change
!*****************************************
#debug9 show 'Union-CD-After-Change'

 if #x000_union_cd_total_count > 0
   print 'Union Cd  ' 	(+2,#x000_col_totals_literal)
   print $x000_Union_CD 	(0,#x000_col_totals_literal2)
   print 'Total'    	(0,#x000_col_totals_literal3)		
   let #x000_temp_col = #x000_col_union_cd_total - 0
   print #x000_union_cd_total_count (0,#x000_temp_col) EDIT 999,999  
   new-page
 end-if
 
end-procedure


!*****************************************
begin-procedure Get-Job-Code-Info
!*****************************************
 #debug9 show 'Get-Job-Code-Info'
 #debug8 show '$x000_JobCode=' $x000_JobCode

    move '' to $x000_JobTitle  
    move '' to $x000_JobTitleAbbrv  
!    move '' to $x000_Union_Cd

  if rtrim($x000_JobCode,' ') = ''
    show '$x000_JobCode is null, no row returned'
    goto Get-Job-Code-Info-Exit
  end-if   

begin-SELECT
JT.DESCR
JT.DESCRSHORT
JT.UNION_CD

  let $x000_JobTitle  = rtrim(&JT.Descr,' ')
  let $x000_JobTitleAbbrv  = rtrim(&JT.DescrShort,' ')
  #debug6 show '$x000_JobTitle=' $x000_JobTitle    
  #debug6 show '$x000_JobTitleAbbrv=' $x000_JobTitleAbbrv  

!  let $x000_Union_Cd = rtrim(&JT.UNION_CD,' ')

FROM   PS_JOBCODE_TBL JT

WHERE  JT.JOBCODE = $x000_JobCode
AND    JT.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_JOBCODE_TBL
       WHERE  JOBCODE = JT.JOBCODE 
       AND    EFFDT  <= $ThruDate)
end-SELECT

Get-Job-Code-Info-Exit:
end-procedure


Begin-Procedure Get-From-Date
#debug9 Show 'Get-From-Date'
Begin-Select
pc.pay_begin_dt &from_date
From   ps_pay_calendar pc
Where  To_Char(pc.check_dt,'YYYYMM') =
      (Select To_Char(Add_Months(check_dt,-1),'YYYYMM')
       From   ps_pay_calendar
       Where  $AsOfToday Between pay_begin_dt And pay_end_dt
       And    company  = pc.company
       And    paygroup = pc.paygroup
       And    pay_off_cycle_cal = 'N')	!AXL 2/28/2000 - Add
And    pc.pay_period = 1
And    pc.company  = 'GEI'
And    pc.paygroup = 'RET'
End-Select
End-Procedure


Begin-Procedure Get-Thru-Date
#debug9 Show 'Get-Thru-Date'
Begin-Select
pc.pay_end_dt &thru_date
From   ps_pay_calendar pc
Where  To_Char(pc.check_dt,'YYYYMM') =
      (Select To_Char(Add_Months(check_dt,-1),'YYYYMM')
       From   ps_pay_calendar
       Where  $AsOfToday Between pay_begin_dt And pay_end_dt
       And    company  = pc.company
       And    paygroup = pc.paygroup
       And    pay_off_cycle_cal = 'N')	!AXL 2/28/2000 - Add
And    pc.pay_period =
      (Select Max(pay_period)
       From   ps_pay_calendar
       Where  To_Char(check_dt,'YYYYMM') =
             (Select To_Char(Add_Months(check_dt,-1),'YYYYMM')
              From   ps_pay_calendar
              Where  $AsOfToday Between pay_begin_dt And pay_end_dt
              And    company  = pc.company
              And    paygroup = pc.paygroup
              And    pay_off_cycle_cal = 'N')	!AXL 2/28/2000 - Add
       And    company  = pc.company
       And    paygroup = pc.paygroup)
And    pc.company  = 'GEI'
And    pc.paygroup = 'RET'
End-Select
End-Procedure


!***********************************  GEX001-added entire paragraph
begin-procedure Get-Union-Cd-Info
!*****************************************
 #debug9 show 'Get-Union-Cd-Info'
 #debug8 show '$x000_Union_CD=' $x000_Union_CD

  if $x000_Union_CD <> $x000_Prior_Union_CD 
    let $x000_Prior_Union_CD = $x000_Union_CD 
    move ' ' to $x000_Union_Cd_Descr
  else
    goto Get-Union-CD-Exit
  end-if

begin-SELECT
UN.UNION_CD
UN.EFFDT
UN.DESCR
 
  let $x000_Union_Cd_Descr = rtrim(&UN.DESCR,' ')
 
  #debug6 show '&UN.UNION_CD=' &UN.UNION_CD 
  #debug6 show '&UN.EFFDT=' &UN.EFFDT       
  #debug6 show '$x000_Union_Cd_Descr=' $x000_Union_Cd_Descr 

FROM   PS_UNION_TBL UN   

WHERE  UN.UNION_CD = $x000_Union_CD
AND    UN.EFFDT = (SELECT MAX(UN1.EFFDT)
                     FROM   PS_UNION_TBL UN1
                     WHERE  UN1.UNION_CD = UN.UNION_CD 
                     AND    UN1.EFFDT  <= $ThruDate)
end-SELECT

Get-Union-CD-Exit:

end-procedure




#include 'askftd.sqc'    !From Thru Date input
#include 'getempnm.sqc'  !Get-Employee-Name procedure
#include 'datemath.sqc'  !date routines
#include 'setenv.sqc'    !set database environment
#include 'getcodta.sqc'  !Get-Company_Data procedure
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'datetime.sqc'  !general date and time formatting procedures
#include 'number.sqc'    !general number formatting procedures
#include 'stdapi.sqc'    !StdAPI-Init procedure
#include 'reset.sqc'     !Reset printer procedure
#include 'gexregtp.sqc'	 !Get full-part time code
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx913.sqc'  !Get union code multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'getposnm.sqc'  !Get-Position-Title procedure		!isdvmxd added for ITG 56799 on 02/02/2009
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta