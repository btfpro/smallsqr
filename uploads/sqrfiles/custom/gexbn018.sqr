!***********************************************************************
! GEXBN018:  Medicare Reimbursement Letter and Report                  *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXBN018 :            Medicare Reimbursement Letter and Report       *
!                                                                      *        
! Narrative:            This report generates a letter to all the      *
!                       retired employees asking for SSA-1099, so that *
!                       the medicare premiums the retirees paid during *
!                       the previous year can be reimbursed.           *
!                                                                      *
!                       Also, the report generates a list of retirees  *
!                       and their group insurance code.                *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *  
! SQL Tables:           gex_retired_def                                *
!                                                                      *
! Written by:           Jim Hutchison                                  *       
!                                                                      *
! Normally Run:         On Demand                                      *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Multiple group insurance codes               *
!                         Letter Date                                  *
!                         Premium Year                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INIT  CSR     DATE            DESCRIPTION OF THE CHANGE              *
!***********************************************************************
! JDH           06/04/98        Initial Creation                       *
!                                                                      *
! GXD           12/14/98        Added Flag to Run Control Panel to     *
!                               create either the Report or the Letter *
!                                                                      *
! CWB           01/07/00        Changed name of Benefits person from   *
!                               Renaye Lee to Dawn Edwards. Changed    *
!                               P.O. Box number and ZIP4. Changed      *
!                               the field used in the greeting.        *
!                                                                      *
! AXL           01/29/02        Changed name of Benefits person from   *
!                               Dawn Edwards to Gloria Glaze. Changed  *
!                               ZIP to POSTAL.                         *
!                                                                      *
! CJH           12/19/2003      Change verbage in letter.              *
!                                                                      *
! CJH           01/08/2004      Correct letter printing - fonct size   *
!                               and lines per inch - the 8.3 upgrade   *
!                               set the font incorrectly.              *
!                                                                      *
! AXL  10854    01/26/2005      Change verbage in letter.              *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
! CJH - 1/8/2004
#Include 'setup33.sqc'   !Printer and Page-Size Initialization (Portrait)
!#Include 'setup31.sqc'   !Printer and Page-Size Initialization (Portrait)


#define #column1 001
#define #column2 020
#define #column3 078
#define #column4 106

#define #d_column1 001
#define #d_column2 020
#define #d_column3 087
#define #d_column4 112


Begin-Setup
#debug9 Show 'Begin-Setup'
     Declare-Report Report
Printer-Type=HPLASERJET
     End-Declare
     Declare-Report Letter
Printer-Type=HPLASERJET

     End-Declare
End-Setup


Begin-Report

!alter-printer
!  point-size=12
!  symbol-set=0U                          ! ASCII symbol set
!  pitch=10                             ! Characters per inch
!  font=4                                 ! Courier font

#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 6

!GXD 12/14/98 Begin

!For-Reports=(Report)

if $gex_report_flag = 'R'

#debug9 Show 'Begin-Heading'
!sxk 09/22/98 Begin
  let $ReportID = 'BN018RPT'
!sxk 09/22/98 End
  #Include 'stdhdg01.sqc'

  Let $X000_temp_out = 'Sorted by ' || $X000_Order_By
  Print $X000_temp_out         (3,)  Center

  Let $X000_temp_out = 'Report as of ' || $Letter_Date
  Print $X000_temp_out         (4,)  Center

  Print 'EMPLID'               (+2,{#column1})
  Print 'NAME'                 (+0,{#column2})
  Print 'GROUP INSURANCE CODE' (+0,{#column3})
  Print 'GROUP INSURANCE DATE' (+0,{#column4})

end-if

! GXD 12/14/98 End

End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXBN018'
  Let $ReportTitle   = 'Medicare Reimbursement Letter and Report'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show '$ReportDate  = ' $ReportDate
  Show '$ReportTime  = ' $ReportTime

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  Let $X000_Order_By = 'Name'

  Show '$GEXXX914_Include_Gex_Grpins_Cd_Display_Criteria = ' $GEXXX914_Include_Gex_Grpins_Cd_Display_Criteria
  Show 'gex_report_flag = ' $gex_report_flag
  Show '$Letter_Date   = ' $Letter_Date
  Show '$Premium_Year  = ' $Premium_Year
  Show '$X000_Order_By = ' $X000_Order_By

! GXD 12/14/98 Begin
   if $gex_report_flag = 'R'
      Do P130-Print-Cover-Page
   end-if
! GXD 12/14/98 End

  Do P140-Format-Date


End-Procedure


Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Let $AsOfDate = $AsOfToday
  Let $Year4 = '1'
  Do Format-DateTime($AsOfDate, $Letter_Date, {DEFDATE}, '', '')
  Let #Premium_Year = Substr($AsOfDate,8,4)
  Let #Premium_Year = #Premium_Year - 1
  Do Format-Number(#Premium_Year, $Premium_Year, '9999')
  Let $GEXXX914_Include_Gex_Grpins_Cd_Criteria         = '1=1'
  Let $GEXXX914_Include_Gex_Grpins_Cd_Display_Criteria = 'All'
End-Procedure


Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXRCBN1-SELECT-PARAMETERS

  If Rtrim(&GEX_RC_BEN.ASOFDATE, ' ') = ''
    Let $AsOfDate = $AsOfToday
  Else
    Let $AsOfDate = &GEX_RC_BEN.ASOFDATE
  End-If

! GXD 12/14/98 Begin
  Move &GEX_RC_BEN.GEX_REPORT_FLAG to $gex_report_flag
! GXD 12/14/98 End

  Let $Year4 = '1'
  Do Format-DateTime($AsOfDate, $Letter_Date, {DEFDATE}, '', '')

  If &GEX_RC_BEN.CALENDAR_YEAR = 0
    Let #Premium_Year = Substr($AsOfDate,8,4)
    Let #Premium_Year = #Premium_Year - 1
    Do Format-Number(#Premium_Year, $Premium_Year, '9999')
  Else
    Let #Premium_Year = &GEX_RC_BEN.CALENDAR_YEAR
    Do Format-Number(#Premium_Year, $Premium_Year, '9999')
  End-If

  Let $GEXXX914_Gex_Grpins_Cd_Alias = 'gex_grpins_cd'                    
  Do GEXXX914-Select-Gex-Grpins-Cd-Parameters 
End-Procedure


begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX914_Include_Gex_Grpins_Cd_Display_Criteria    ='   (+1,5)
  Print $GEXXX914_Include_Gex_Grpins_Cd_Display_Criteria          (0,+2)

  Print 'Letter Date :'                     (+2,5)
  Print $Letter_Date                        (0,+2)

  Print 'Premium Year:'                     (+2,5)
  Print $Premium_Year                       (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'   (+5,2)
  Print $X000_Order_By                      (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


Begin-Procedure P140-Format-Date
#debug9 Show 'P140-Format-Date'

  Let $PY = $Premium_Year

  Let $Letter_Date_MM = Substr($Letter_Date,1,2)
  Evaluate $Letter_Date_MM
  When = '01'
    Let $Letter_Date_Month = 'JANUARY'
  When = '02'
    Let $Letter_Date_Month = 'FEBRUARY'
  When = '03'
    Let $Letter_Date_Month = 'MARCH'
  When = '04'
    Let $Letter_Date_Month = 'APRIL'
  When = '05'
    Let $Letter_Date_Month = 'MAY'
  When = '06'
    Let $Letter_Date_Month = 'JUNE'
  When = '07'
    Let $Letter_Date_Month = 'JULY'
  When = '08'
    Let $Letter_Date_Month = 'AUGUST'
  When = '09'
    Let $Letter_Date_Month = 'SEPTEMBER'
  When = '10'
    Let $Letter_Date_Month = 'OCTOBER'
  When = '11'
    Let $Letter_Date_Month = 'NOVEMBER'
  When = '12'
    Let $Letter_Date_Month = 'DECEMBER'
  End-Evaluate
  Let $Letter_Date_DD   = Substr($Letter_Date,4,2)
  Let $Letter_Date_YYYY = Substr($Letter_Date,7,4)
  Let $Full_Letter_Date = $Letter_Date_Month || '  ' ||
                          $Letter_Date_DD    || ', ' ||
                          $Letter_Date_YYYY
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'

Begin-Select
emplid
UPPER(name)              &name
UPPER(address1)          &address1
UPPER(address2)          &address2
UPPER(city)              &city
UPPER(state)             &state
UPPER(postal)            &zip	!AXL 01/29/2002 Changed from ZIP
UPPER(gex_grpins_cd)     &gex_grpins_cd
UPPER(gex_grpins_ret_dt) &gex_grpins_ret_dt
  Let $Data-Found-Flag = 'Y'
  Do Format-DateTime(&gex_grpins_ret_dt, $gex_grpins_ret_dt, {DEFDATE}, '', '')

! GXD 12/14/98 Begin
  if $gex_report_flag = 'R'
     Use-Report Report
     Do P210-Print-Record
  else
     Use-Report Letter
     Do P220-Print-Letter
!     Use-Report Report
  end-if
! GXD 12/14/98 End

From ps_gex_retired_def
Where [$GEXXX914_Include_Gex_Grpins_Cd_Criteria]
Order By name
End-Select
End-Procedure


Begin-Procedure P210-Print-Record
#debug9 Show 'P210-Print-Record'
  Print &emplid             (+1,{#d_column1})
  Print &name               (+0,{#d_column2})
  Print &gex_grpins_cd      (+0,{#d_column3})
  Print $gex_grpins_ret_dt  (+0,{#d_column4})
end-procedure


Begin-Procedure P220-Print-Letter
#debug9 Show 'P220-Print-Letter'

  Let $address3 = &city || ', ' || &state || '  ' || &zip
  If Rtrim(&address2, ' ') = ''
    Let $address2 = $address3
    Let $address3 = ''
  Else
    Let $address2 = &address2
  End-If

  Unstring &name By ',' Into $lname $fname
  Let $name = $fname || ' ' || $lname 
  Unstring $fname By ' ' Into $first_name $mid_initial

! CWB 01/07/2000 Begin

  Let $first_name_trim = rtrim($first_name, ' ')
  String $first_name_trim ':' by '' Into $first_name_colon

! CWB 01/07/2000 End

! CJH - 12/19/2003
  let $dollar = '$350.00'
! CJH - end

!Begin-Document (11,1)
Begin-Document (7,1)
$Full_Letter_Date
.b
.b
.b
$name
&address1
$address2
$address3
.b
.b
RE:  MEDICARE PREMIUMS FOR $PY
.b
.b
DEAR $first_name_colon 
.b
.b
IN ORDER FOR OK GROCERY COMPANY TO REIMBURSE YOU FOR THE
MEDICARE PREMIUMS YOU PAID DURING THE YEAR, YOU MUST SEND
A COPY OF FORM SSA-1099 SOCIAL SECURITY BENEFIT
STATEMENT THAT YOU RECEIVE FROM THE SOCIAL SECURITY
ADMINISTRATION.  THE FORM INDICATES HOW MUCH YOU HAVE
PAID IN PREMIUMS TOWARD MEDICARE FOR $PY .
.b
PLEASE SEND A COPY OF YOUR STATEMENT TO THE ADDRESS BELOW:
.b
          ATTN:  CLEM WILLIAMS - BENEFITS
          GIANT EAGLE, INC.
          P.O. BOX 11591
          PITTSBURGH,  PA  15238-1591
.b
!AXL 1/26/2005 New text - Begin
!UPON RECEIPT OF YOUR STATEMENT, A REIMBURSEMENT NOT TO EXCEED $dollar
!WILL OCCUR.
UPON RECEIPT OF YOUR STATEMENT, A REIMBURSEMENT WILL OCCUR
BASED ON THE AGREEMENT IN YOUR CONTRACT.
!AXL 1/26/2005 New text - End
.b
IF YOU HAVE ANY QUESTIONS REGARDING THIS MATTER, PLEASE DO
NOT HESITATE TO CALL ME AT (412) 963-2547.
.b
SINCERELY,
.b
.b
.b
.b
.b
CLEM WILLIAMS
BENEFITS SUPERVISOR
GIANT EAGLE, INC.
End-Document
New-Page
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'

! GXD 12/14/98 Begin
  If $gex_report_flag = 'R'

     If $Data-Found-Flag <> 'Y'
       Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
     Else
       Do P310-Totals
     End-If

  End-If
! GXD 12/14/98 End

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
End-Procedure


Begin-Procedure P310-Totals
#debug9 Show 'P310-Totals'
  New-Page
  Print 'TOTALS FOR REPORT'     (+3,8)
  Print 'GROUP INSURANCE CODE'  (+2,1)
  Print '  COUNT'               (+0,26)
  Print '--------------------'  (+1,1)
  Print '-------'               (+0,26)
Begin-Select
gex_grpins_cd &total_gex_grpins_cd
count(gex_grpins_cd) &total_gex_grpins_cd_count
  Move &total_gex_grpins_cd_count To $total_gex_grpins_cd_count 999,999
  Print &total_gex_grpins_cd       (+1,10)
  Print $total_gex_grpins_cd_count (+0,26)
From ps_gex_retired_def
Where [$GEXXX914_Include_Gex_Grpins_Cd_Criteria]
Group By gex_grpins_cd
Order By gex_grpins_cd
End-Select
End-Procedure


#include 'gexxx914.sqc'  !Get gex_grpins_cd multiple row table
#include 'gexxx920.sqc'  !Get ben single row run control
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
