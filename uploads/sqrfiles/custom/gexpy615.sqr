!***********************************************************************
! GEXPY615:  SERVICE AWARD TAPE                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY615 :		Service Award Tape                             *
!                                                                      *
! Narrative:		Tape will be sent to O C Tanner.  The tape     *
!                       contains the employee detail information who   *
!                       have completed at least 5 years of service and *
!                       their anniversary falls between the quarter    *
!                       begin date and the quarter end date. The tape  *
!                       is created in advance to allow O C Tanner time *
!                       to produce the award pins. A report will also  *
!                       be created for HR use.                         *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:		location_tbl                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Gene Davis                                     *
!                                                                      *
! Normally Run:	        On Demand                                      *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Company(s)		   	               *
!                         Deptid(s)			   	       *
!                         From Date                                    *
!                         Thru Date                                    *
!                         As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS      DATE            DESCRIPTION OF THE CHANGE              *
!***********************************************************************
! GXD		03/03/98	Initial Creation                       *
! SXK		09/08/98	Added OUTFILE variable                 *
! JDH		10/14/98	Corrected logic that determines if     *
!                               service date falls within date range.  *
!                               Added deptid exclusion parameter.      *
! SXK           02/04/99        Corrected file format according to O C *
!				Tanner specifications. Also included   *
!				new customer codes for 5 year lapel    *
!				pin awards. Added stdhdg01 sqc, will   *
!                               be printing hired date in the report   *
!                               unless seniority dt is populated.      *
! JDH           05/27/99        Corrected logic to get location info.  *
!                               Modified contact name logic.           *
! JNB           07/06/1999      changed $st_address1 and $st_address2  *
!                               to $store_address1 and $store_address2 *
!                               as these fields were blank when the tape
!                               was created. Also when the program was *
!                               run thru sqrw, changed the source so   *
!                               that the program will not exclude stores
!                               0081 and 0082.                         * 
!                               Changed source so that $l3_unique_cd   *
!                               will be unique for each location       *
!                                                                      *
!               07/19/1999      For TMK RE2 the customer code was      *
!                               changed from GI017 to GI057            *
!                               if acct_cd = '008' changed the location*
!                               to 'KAPPA' so that Randy Heiser and    *
!                               his address will be used. And l3 unique*
!                               code of '9960' was used for acct_cd 008* 
!                                                                      *
!              10/20/1999       Changes after the conference call      *
!                               Reduced customer codes to two          *
!                               GI086 = 5 yrs and GI083 10+ yrs        *
!                               l3_unique_code set to blanks           *
!                               contact person changed to Terri Clouse *      
!                               for BUTLER                             *
!                               Changed reports to two py615a & py615b *
!                               Changed files to two gexpy615.001 and  *
!                               gexpy615.002                           *
!              12/09/1999       Changed $company_seniority_dt          *
!                               to      $cmpny_seniority_dt            *  
!  JNB         01/26/2000       Changed zip to postal                  *      
!                                                                      *
!  AXL         08/07/2000       M & O stores being incorrectly         *
!                               defaulted to location RIDC.  Added list*
!                               of stores to be excluded in assignment *
!                                                                      *
!  CWB         09/18/2000       Added logic to allow the entering of   *
!                               as-of-dates, and from and thru dates,  *
!                               but does not require them.             * 
!                                                                      *
!  CWB         01/04/2001       Change Peggy Schmitt and Skip Adams to *
!                               Jill Tilford.                          * 
!                                                                      *
!  CWB         12/05/2001       Added company RFI. Added Stormy Sammon *
!                               and Kelly McCormick. Changed Joanne    *
!                               Brownlee to Melissa Anthony.           *
!                               Changed Terri Clouse to Nathan Book.   * 
!                               Removed BOLD from report. Since this   * 
!                               file is no longer sent to tape, but to *
!                               diskette, a shell script is used to    *
!                               rename the file to O.C. Tanner specs.  *
!                                                                      *
!  GBD         09/09/2002       v8.3 Upgrade                           *
!              10/01/2002       Corrected date formats for comparisons.*
!                                                                      *
!  GBD         09/17/2002       Modified the layout of the data files  *
!                               according to O.C. Tanner spreadsheet.  *
!  GBD         10/18/2002       Additional modifications per Patricia P*
!                                                                      *
!  GBD         12/18/2002       Remove 5 year report from processing.  *
!                                                                      *
!  GBD         04/07/2003       Added code to exclude all termination, *
!                               retired, and deceased status codes from*
!                               report file.                           *
!                                                                      *
!  GBD         08/07/2003       Replace hard coded names with generic  *
!                               titles.                                *
!                               Added temp table to be populated and   *
!                               used by gexpy616 to reference terms    *
!                               within each quarter. This table will be*
!                               reloaded each time this program is run.*
!                                                                      *
!  CWB         06/10/2004       Added 'Employee Comunications - Marketing
!                               Dept' to contacts.                     *  
!                                                                      *
!  NPK         10/03/2006       The vendor Changed  from OC tanner to  *
!                               CA short & changed the file layout as  *  
!                               per the new vendor.                    *
!                               Changed the SQR to send the Employees  *  
!                               data to contact names from GEX_DEPT_TBL*
!                               Stand alones are sent to General Manager  
!                               Pharmacies within Independent stores   *
!                               are sent to Pharmacy Manager           *
!                               Modified to pick up the company address*  
!                               from ESTAB_TBL instead of LOCATION TBL.*
!                               Modified to send EMPLID instead of SSN.*  
!                               Included 1 yr and 5 Yrs service also.  *  
!                               Excluded JOB CODE 90000.               *
!                               Added new Company MCC & Paygroup RE5.  *  
!                               Cleaned up the SQR by removing all the *  
!                               unwanted code & changed the header     *
!                               of the report.                         *  
!                                                                      *
! BXS         11/29/2006        Removed Name field from the output csv *
!                               as well as from the report. Replace it *
!                               with last name, first name and middle  *
!                               name. The format of report has been    *
!                               slightly changed to move the EMPLID to *
!                               a new line.                            *
!GEX-AXG     08/16/2007        Upgrade 9.0			       *
!***********************************************************************


#include 'setenv.sqc' !Set environment
#include 'setup32.sqc'
!#Define OUTFILE /apps/hr/hrms90/hrdev90/outgoing/	!For Testing
! JNB 10/20/1999  Begin
Begin-Setup

Declare-Report TenplusYr-Report
Printer-Type=LINEPRINTER
    Layout=Default
  End-Declare
End-Setup
! JNB 10/20/1999  End 


begin-report
  !Do Alter-Session		!Commented for Upgrade 9.0 GEX-AXG
  do Init-DateTime
  do Init-Number
  do stdapi-init
  do Get-Calendar-Year-Id

  do Get-Current-DateTime

  let $delimiter = ','             ! ISDVNPK 10/03/2006 Added this
  let $sev-error = 'N'
  let $firstcomp = 'Y'
  let $firstrec = 'Y'
  let $firstacctcd = 'Y'
  let $firstemplid = 'Y'
  let #pass1a = 0
  let #pass1 = 0
  let $company-changed = 'N'
  let $deptid-changed = 'N'
  let $OK_Process = 'N'

  let #empl-count = 0

  do Report
  do Commit-Transaction
  date-time () hh:mi:ss &timeEnded
  display 'Report Ended: ' noline
  display &timeEnded
  do stdapi-term
  !*do Reset
end-report


begin-procedure Report
 date-time () hh:mi:ss &timeBegan
 display 'Report Began: ' noline
 display &timeBegan

 move 'N' to $Errorfound

 if $prcs_process_instance = ''

  let $GEXXX900_INCLUDE_COMPANY_CRITERIA = 'B.COMPANY IN (''GEI'',''TMK'',''BRM'',''RTP'',''RFI'')'
  let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA
!  JNB  07/06/1999
!  let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = 'B.DEPTID NOT LIKE ''008%'''
  let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = '1=1'  

  let $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_EXCLUDE_DEPTID_CRITERIA
  Let $X000_Where_Select_AsOfDate = $AsOfToday

!****************** Accept Input from user for information ************************
 display ' '
 input $qtr_begindate maxlen=10 type=char 'Enter the Quarter Begin Date (mm/dd/yyyy) '
 if rtrim($qtr_begindate, '') = ''
    let $qtr_begindate = ' '
    display ' '
    display 'Quarter Begin Date Required - Execution Halted.'
    goto Report-exit
 else
    let $qtr_begindate = rtrim($qtr_begindate,' ')
 end-if

 display ' '                                                                                    
 input $qtr_enddate maxlen=10  type=char 'Enter the Quarter End Date (mm/dd/yyyy) '
 if rtrim($qtr_enddate, ' ') = ''
    display ' '
    display 'Quarter End Date Required - Execution Halted.'
    goto Report-exit
 else
    let $qtr_enddate = rtrim($qtr_enddate, ' ')
 end-if

  Let $Year4 = '1'
  Do Format-DateTime ($qtr_begindate,$FromDate,{DEFMDY}, '', 'native')
  Let $Year4 = '1'
  Do Format-DateTime ($qtr_enddate, $ThruDate,{DEFMDY}, '', 'native')


else

  Let $GEXXX900_Company_Alias = 'B.COMPANY'
  Do GEXXX900-Select-Company-Parameters

  Let $GEXXX902_Deptid_Alias = 'B.DEPTID'
  Do GEXXX902-Select-Deptid-Parameters
  Do GEXXX922-Select-Parameters
  Let $X000_Where_Select_AsOfDate = $GEX_RC_PAY.AsOfDate
  If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
    Let $X000_Where_Select_AsOfDate = $AsOfToday
  End-If

! CWB 09/18/2000 Begin

  Let $FromDate = $GEX_RC_PAY.FromDate
  Let $ThruDate = $GEX_RC_PAY.ThruDate

end-if

  Show '$FromDate ' $FromDate
  Show '$ThruDate ' $ThruDate

  If Rtrim($FromDate,' ') = '' and 
     Rtrim($ThruDate,' ') <> ''
     display ' '
     display 'Provide both FromDate and Thrudate or leave both blank'
     goto Report-exit
  end-if

  If Rtrim($FromDate,' ') <> '' and 
     Rtrim($ThruDate,' ') =  ''
     display ' '
     display 'Provide both FromDate and Thrudate or leave both blank'
     goto Report-exit
  end-if

  If Rtrim($FromDate,' ') = '' and
     Rtrim($ThruDate,' ') = ''

        LET $selectdate_MM = SUBSTR($X000_Where_Select_AsOfDate,4,3)                               
	LET $Selectdate_DD = SUBSTR($X000_Where_Select_AsOfDate,1,2)                                                      
	LET $Selectdate_YY = SUBSTR($X000_Where_Select_AsOfDate,8,4)                                                      
							    
   
! Set the from and thru dates to be the dates of the next quarter.

      Evaluate $selectdate_mm
      when = 'JAN'
      when = 'FEB'
      when = 'MAR'
         Let $FromDate = '01-APR-' || $Selectdate_yy
         Let $ThruDate = '30-JUN-' || $Selectdate_yy
      break
      when = 'APR'
      when = 'MAY'
      when = 'JUN'
         Let $FromDate = '01-JUL-' || $Selectdate_yy
         Let $ThruDate = '30-SEP-' || $Selectdate_yy
      break
      when = 'JUL'
      when = 'AUG'
      when = 'SEP'
         Let $FromDate = '01-OCT-' || $Selectdate_yy
         Let $ThruDate = '31-DEC-' || $Selectdate_yy
      break
      when = 'OCT'
      when = 'NOV'
      when = 'DEC'
         Let #rptyear = to_number($Selectdate_yy) + 1
         Let $selectdate_yy = to_char(#rptyear) 
         Let $FromDate = '01-JAN-' || $Selectdate_yy
         Let $ThruDate = '31-MAR-' || $Selectdate_yy
      break
      End-evaluate

  End-If

!GBD 10/18/2002
  show 'SELECTION CRITERIA FOR THIS REPORT RUN: '
  show '$GEXXX900_Include_Company_Display_Criteria = ' $GEXXX900_Include_Company_Display_Criteria
  show '$GEXXX902_Exclude_Deptid_Display_Criteria  = ' $GEXXX902_Exclude_Deptid_Display_Criteria

  let $Year4 = '1'
  do Format-DateTime ($X000_Where_Select_AsOfDate, $X000_Report_Heading_AsOfDate, {DEFMDY}, '', '')
  show 'As Of Date : ' $X000_Report_Heading_AsOfDate
!GBD 10/18/2002

  Let $Year4 = '1'
  Do Format-DateTime ($FromDate, $begindate, {DEFMDY}, '', '')
  Let $Year4 = '1'
  Do Format-DateTime ($ThruDate, $enddate, {DEFMDY}, '', '')

  show 'Begin Date: ' $begindate
  show 'End Date:   ' $enddate

! CWB 09/18/2000 End

!**********************************
! Setup Date Check
!**********************************
 date-time () MM/DD/YYYY &curr_date

  move &curr_date to $curr_date

  let $from_yy = substr($begindate,9,2)
  let $from_mmdd = substr($begindate,1,2) || substr($begindate,4,2)

  do Format-DateTime($begindate,$out1,{DEFMDY},'','native')
  display $out1

  let $thru_yy = substr($enddate,9,2)
  let $thru_mmdd = substr($enddate,1,2) || substr($enddate,4,2)

  do Format-DateTime($enddate,$out2,{DEFMDY},'','native')
  display $out2

  do Format-DateTime($begindate,$begin_dt,{DEFMDY},'','native')
  do Format-DateTime($enddate,$end_dt,{DEFMDY},'','native')

  do Format-DateTime($begin_dt,$date1,{DEFCMP},'','')
  do Format-DateTime($end_dt,$date2,{DEFCMP},'','')

  if $date1 > $date2
     display ' '
     display 'The From Date can not be greater than the Thru Date'
     goto Report-exit
  else

!     do Diff-Date($enddate,$begindate,#years,#months,#days)
     do Diff-Date($end_dt,$begin_dt,#years,#months,#days)     !GBD 09/09/2002 

     if #years > 0
	display ' '
	display 'The Hire Date Range cannot exceed one year.'
	goto Report-exit
     end-if
  end-if

  Use-Report TenplusYr-Report   ! JNB 10/20/99
  Do P130-Print-Cover-Page

  do Delete-Temp                !GBD 08/07/2003

  do process-data

Report-Exit:


  date-time () hh:mi:ss &timeProcess

  
  display #InputTran10 99999 noline           ! JNB 10/20/99
  display ' Service Award Transactions Processed: ' ! JNB 10/20/99
  display &timeProcess

end-procedure

begin-procedure Get-Values
   let $language_cd = $PRCS_LANGUAGE_CD
end-procedure


!  JNB 10/20/1999 Commented the following procedure
!*********************************************************************
!Prints the header information in the report.
!*********************************************************************

Begin-Heading 8 For-Reports=(TenplusYr-Report)
#debug9 Show 'Begin-Heading (TenplusYr-Report)'
  Let $ReportID      =   'GEXPY615'             ! ISDVNPK 10/03/2006 Modified this
  Let $ReportTitle   =   'Service Award Tape'
  Let $ReportTitle2  =   'Report'               ! ISDVNPK 10/03/2006 Modified this

  #Include 'stdhdg01.sqc'
  Print $ReportTitle2      (3,) Center

  let $Subheading = $begindate||' - '||$enddate
  print $Subheading   (4,) center

  print '-' (+3,1,174) fill
  position (+1)

End-Heading




begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX900_Include_Company_Display_Criteria          ='   (+1,5)
  Print $GEXXX900_Include_Company_Display_Criteria                (0,+2)
  Print '$GEXXX902_Exclude_Deptid_Display_Criteria           ='   (+1,5)
  Print $GEXXX902_Exclude_Deptid_Display_Criteria                 (0,+2)

  Print 'From Date  :'                      (+2,5)
  Print $begindate                          (0,+2)
  Print 'Thru Date  :'                      (+2,5)
  Print $enddate                            (0,+2)

  let $Year4 = '1'
  do Format-DateTime ($X000_Where_Select_AsOfDate, $X000_Report_Heading_AsOfDate, {DEFMDY}, '', '')
  Print 'As Of Date :'                      (+2,5)
  Print $X000_Report_Heading_AsOfDate       (0,+2)

  Let $X000_Order_By = 'Company, Paygroup, Deptid, Emplid'

  Print 'SORT ORDER FOR THIS REPORT RUN:'   (+5,2)
  Print $X000_Order_By                      (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


!GBD 08/07/2003 Begin
!*********************************************************************
! Remove old values from temp table
!*********************************************************************
Begin-Procedure Delete-Temp
Begin-Sql ON-ERROR=SQL-Error-Found('Delete-Temp')
  DELETE FROM PS_GEX_R_GEXPY615
End-Sql

Begin-Sql
  COMMIT
End-Sql

End-Procedure
!GBD 08/07/2003 End


!*********************************************************************
! Select all valid Employee Types for a specific company
!*********************************************************************

begin-procedure process-data

let $filename2 = '{OUTFILE}'||'gexpy615.csv'     ! ISDVNPK 10/03/2006 Modified this
open $filename2 as 2
      for-writing record=1250

if #writestat != 0
   display 'Error Opening output file.  Program terminating.'
   stop
end-if

move 'N' to $rowfound10    ! JNB 10/20/99

let #inputtran = 0

begin-select ON-ERROR=SQL-Error-Found('Process-data')
B.COMPANY             &company  
B.PAYGROUP            &paygroup  
B.DEPTID              &deptid  
B.EMPLID              &emplid
!A.NAME                &name           !ISDVBXS 11/29/2006 - Commented 
A.LAST_NAME           &last_name       !ISDVBXS 11/29/2006 - Added
A.FIRST_NAME          &first_name      !ISDVBXS 11/29/2006 - Added
A.MIDDLE_NAME         &middle_name     !ISDVBXS 11/29/2006 - Added
B.LOCATION            &location
B.ACCT_CD             &acct_cd
A.SEX                 &sex
A.ADDRESS1            &address1
A.ADDRESS2            &address2
A.CITY                &per_city
A.STATE               &per_state
A.POSTAL              &per_zip
A.COUNTRY             &country_code                      !GBD 09/17/2002
!Modified for Upgrade 9.0 GEX-AXG - Begin
!A.ORIG_HIRE_DT        &orig_hire_dt
D.ORIG_HIRE_DT        &orig_hire_dt
!Modified for Upgrade 9.0 GEX-AXG - End
C.SERVICE_DT          &service_dt
C.CMPNY_SENIORITY_DT  &cmpny_seniority_dt
B.JOBCODE             &jobcode                       ! ISDVNPK 10/03/2006 Added this

 If &jobcode <> '90000'                ! ISDVNPK 10/03/2006 Added this

  move &company            to $company
  move &paygroup           to $paygroup
  move &deptid             to $deptid
  move &emplid             to $emplid
 !ISDVBXS - 11/29/2006 - Change-Begin
  !move &name               to $name
  move &last_name          to $last_name
  move &first_name         to $first_name
  move &middle_name        to $middle_name
 !ISDVBXS - 11/29/2006 - Change-End
  move &location           to $location
  move &acct_cd            to $acct_cd
  move &sex                to $sex
  move &address1           to $address1
  move &address2           to $address2
  move &per_city           to $per_city
  move &per_state          to $per_state
  move &per_zip            to $per_zip
  move &country_code       to $country_code              !GBD 09/17/2002
  move &orig_hire_dt       to $orig_hire_dt
  move &service_dt         to $service_dt
  move &cmpny_seniority_dt to $cmpny_seniority_dt

  if rtrim($cmpny_seniority_dt,' ') <> ''
     move $cmpny_seniority_dt to $service_dt
  end-if

  show '$out2....' $out2
  show '$service_dt....' $service_dt

  do Diff-Date($out2,$service_dt,#service_years,#service_months, #service_days)

    show '&emplid....' &emplid
  show '#service_years....' #service_years

  if #service_years =  1 or                ! ISDVNPK 10/03/2006 Added this
     #service_years =  5 or                ! ISDVNPK 10/03/2006 Added this            !GBD 12/18/2002
     #service_years = 10 or
     #service_years = 15 or
     #service_years = 20 or
     #service_years = 25 or
     #service_years = 30 or
     #service_years = 35 or
     #service_years = 40 or
     #service_years = 45 or
     #service_years = 50

     let $year4 = '0'                                   !GBD 09/17/2002
     do Format-DateTime($service_dt, $serv_dt,{DEFYMD}, '', '')

     let $service_yy = substr($serv_dt,1,2)
     let $service_mmdd = substr($serv_dt,4,2) || substr($serv_dt,7,2)

     if $from_yy = $thru_yy
	if $service_mmdd >= $from_mmdd and
	   $service_mmdd <= $thru_mmdd
	   move 'Y' to $OK_Process
	end-if
     else
	if $service_mmdd <= $thru_mmdd
	   move 'Y' to $OK_Process
	end-if
     end-if
  end-if

  if $OK_Process = 'Y'
         move 'Y' to $rowfound10
         add 1 to #inputtran10
    do Get-Store-Info

    if RTRIM($cmpny_seniority_dt, ' ') = ''
       let $wk_hire_dt = $orig_hire_dt
    else
       let $wk_hire_dt = $cmpny_seniority_dt
    end-if


    do Get-Contact-Person
     
    do Get-Mapp-Emplid                   ! ISDVNPK 10/03/2006 Added this
    do Emplid-Change
    do Write-Record

    do Insert-Temp                                     !GBD 08/07/2003

    move 'N' to $OK_Process

  end-if

 end-if               ! ISDVNPK 10/03/2006 Added this

!  show '$X000_Where_Select_AsOfDate: ' $X000_Where_Select_AsOfDate
!Modified for Upgrade 9.0 GEX-AXG - Begin
!FROM PS_PERSONAL_DATA A, PS_EMPLOYMENT C, PS_JOB B
FROM PS_PERSONAL_DATA A, PS_EMPLOYMENT C, PS_JOB B, PS_PER_ORG_INST D
!Modified for Upgrade 9.0 GEX-AXG - End
WHERE A.EMPLID = B.EMPLID
AND   C.EMPLID = B.EMPLID
AND   A.EMPLID = D.EMPLID	!Added for Upgrade 9.0 GEX-AXG
AND   B.EMPL_RCD = D.ORG_INSTANCE_ERN		!gex-mxt added for upgrade during isuue tracker issue 364
AND   C.EMPL_RCD = B.EMPL_RCD
AND   B.EMPL_STATUS NOT IN ('D','Q','R','T','U','V','X')
AND  [$GEXXX900_INCLUDE_COMPANY_CRITERIA] 
AND  [$GEXXX902_EXCLUDE_DEPTID_CRITERIA]
AND   B.EFFDT = (SELECT MAX(EFFDT)
		 FROM PS_JOB
		 WHERE EMPLID = B.EMPLID
		 AND   EMPL_RCD = B.EMPL_RCD
		 AND   EFFDT <= $X000_Where_Select_AsOfDate)
AND   B.EFFSEQ = (SELECT MAX(D.EFFSEQ)
		  FROM PS_JOB D
		  WHERE D.EMPLID = B.EMPLID
		  AND   D.EMPL_RCD = B.EMPL_RCD
		  AND   D.EFFDT = B.EFFDT)

ORDER BY B.COMPANY, B.PAYGROUP, B.DEPTID, B.EMPLID

end-select


if $rowfound10 = 'N'
   display 'No Employees found for the Service Award Tape.' 
end-if
close 2

end-procedure


!*********************************************************************
! Store Info
!*********************************************************************
begin-procedure Get-Store-Info

  let $location2=$location

evaluate $company
   when = 'RTP'
      let $location2 = 'RIDC'
      break
   when = 'KAP'
      let $location2 = 'RIDC'
      break
   when = 'GEI'
      if (($paygroup='RET' or $paygroup='PHM') and
          ($deptid > '0400' and $deptid < '3000'))
	   let $location2 = 'RIDC'
           break
      end-if
      if ($location > 'A0001' and $location < 'A0035') or
	  $location = 'RIDC' or
!          $location = 'ALPHA' or
          $location = 'RTPRM' or
	  $location = 'OKGRCKAPP'
	  let $location2 = 'RIDC'
          break
      end-if
      if ($paygroup='RET' or $paygroup = 'PHM') and
          $deptid < '0400' and $deptid > '3000'
!        location remains the same
         break
      end-if
      if ($paygroup='W02' or $paygroup='W07') or 
          $location='OKGRCTHRN' or
          $location='OK35TH'
	 let $location2 = 'OKGRCTHRN'
         break
      end-if
      break
   when = 'TMK'
      if $paygroup='RE2' or $paygroup='PH2'
	 if $deptid='0049'
          !AXL - 8/7/2000 These stores need to default to normal locn - Begin 
          or $deptid ='1605' or $deptid ='1611' or $deptid ='1616' 
          or $deptid ='1618' or $deptid ='1620'
          !AXL - 8/7/2000 These stores need to default to normal locn - End
	  or $deptid>'3999'
	    !Location remains from the Main Select
	 else
	    let $location2 = 'RIDC'
	 end-if
      end-if
      if $paygroup='W05'
	 let $location2 = 'SHWHS'
      end-if
      if $paygroup='W04'
	 let $location2 = 'TAMARKIN'
      end-if
      if $paygroup='W06'
	 let $location2 = 'PETAL'
      end-if
      break
   when = 'BRM'
      if $paygroup = 'W01'
	 let $location2='BUTLR'
      end-if
      break
 ! ISDVNPK 10/03/2006 Added the below 
  when = 'MCC'
      if $paygroup = 'RE5'
	 let $location2='M3841'
      end-if
      break
 ! ISDVNPK 10/03/2006 Added the above
! CWB 12/05/2001 BEGIN
   when = 'RFI'
      break
! CWB 12/05/2001 END
   when-other
      display $emplid noline
      display ' has an invalid location code of ' noline
      display $location
      break

end-evaluate

   If $acct_cd = '008'
      let $location2 = 'RIDC'      ! JNB 07/19/1999
   End-If
 
   do Get-Location

end-procedure



!*********************************************************************
! Contact Person
!*********************************************************************

! ISDVNPK 10/03/2006 Added the below to get CONTACT NAME from GEX_DEPT_TBL & Send stand alones to General Manager & Pharamacies within Independent stores to Pharamacy Manager
begin-procedure Get-Contact-Person

let $contact_person = ' '
begin-select
DPT.DEPTID
GEX_DEPT.CONTACT_NAME
DPT.DESCR
GEX_DEPT.GEX_COMPANY_DIV_CD

  move &GEX_DEPT.CONTACT_NAME to  $contact_person
 
 If Ltrim(rtrim($contact_person,' '),' ') = ''
  If Ltrim(rtrim(&GEX_DEPT.GEX_COMPANY_DIV_CD,' '),' ') = 'GGO'
    let $contact_person = 'General Manager'
   Else
    If substr(Ltrim(rtrim(&DPT.DESCR,' '),' '),1,2) = 'Rx' 
     let $contact_person = 'Pharmacy Manager'
     let $location2 = $location
     do Get-Location 
    End-If
  End-If
 End-If

FROM PS_DEPT_TBL DPT,
     PS_GEX_DEPT_TBL GEX_DEPT
WHERE DPT.DEPTID = $deptid
AND GEX_DEPT.DEPTID = DPT.DEPTID 
AND GEX_DEPT.EFFDT = (SELECT MAX(GEX_DEPT1.EFFDT) FROM PS_GEX_DEPT_TBL GEX_DEPT1
			WHERE GEX_DEPT1.DEPTID = GEX_DEPT.DEPTID AND
				GEX_DEPT1.EFFDT <= $X000_WHERE_SELECT_ASOFDATE) 
	AND GEX_DEPT.SEQUENCE_NUMBER = (SELECT MAX(GEX_DEPT2.SEQUENCE_NUMBER) FROM PS_GEX_DEPT_TBL GEX_DEPT2
			WHERE GEX_DEPT2.DEPTID = GEX_DEPT.DEPTID AND
				GEX_DEPT2.EFFDT = GEX_DEPT.EFFDT)
AND DPT.EFFDT = GEX_DEPT.EFFDT

end-select

! ISDVNPK 10/03/2006 Added the above to get CONTACT NAME from GEX_DEPT_TBL & Send stand alones to General Manager & Pharamacies within Independent stores to Pharamacy Manager

 IF Ltrim(rtrim($contact_person,' '),' ') = ''     ! ISDVNPK 10/03/2006 Added this

!  let $contact_person = 'Personnel Manager'        GBD 08/07/2003
  let $contact_person = 'HR Manager'

  evaluate $location
!    when = 'OKGRCKAPPA' 
!    when = 'ALPHA'
!    when = 'KAPPA'
     when = 'RIDC'  
!     let $contact_person = 'Joanne Brownlee'  ! CWB 12/05/2001
!     let $contact_person = 'Melissa Anthony'       GBD 08/07/2003
!     let $contact_person = 'Communications Administrator, POD'  ! CWB 06/10/2004
     let $contact_person = 'Employee Communications - Marketing Dept'  ! CWB 06/10/2004
      break
    when = 'HBCSWASH'
!      let $contact_person = 'Michele Carlisle'     GBD 08/07/2003
      let $contact_person = 'HR Manager'
      break
    when = 'OKGRCTHRN'
!      let $contact_person = 'Jill Tilford'         GBD 08/07/2003
      let $contact_person = 'HR Manager'
      break
    when = 'OK35TH'
!      let $contact_person = 'Jill Tilford'         GBD 08/07/2003
      let $contact_person = 'HR Manager'
      break
    when = 'TAMARKIN'
    when = 'TMWHS'
!      let $contact_person = 'Jeannie Keiffer'      GBD 08/07/2003
      let $contact_person = 'HR Manager'
      break
!GBD 08/07/2003 - Begin - Remove
!    when = 'SHWHS'
!      let $contact_person = 'Ed Sikos'
!      break
!    when = 'PETAL'
!      let $contact_person = 'Phyllis Pascarella'   JNB  07/06/1999
!       let $contact_person = 'Karen Hudran'
!      break
!GBD 08/07/2003 - End
    when = 'BUTLR' 
    when = 'BUTLER'
! CWB 12/05/2001 BEGIN
!      let $contact_person = 'Terri Clouse'         JNB 10/20/1999
!       let $contact_person = 'Nathan Book'         GBD 08/07/2003
       let $contact_person = 'HR Manager'
! CWB 12/05/2001 END
      break
!GBD 08/07/2003 - Begin - Remove
!    when = 'RTPRM'
! CWB 12/05/2001 BEGIN
!      let $contact_person = 'Alberta Ziolkowski'
!      let $contact_person = 'Sarah Peters'
!      break
!GBD 08/07/2003 - End
    when = 'CODCORP'
       evaluate $Deptid
         when = 'C490'
         when = 'C510'
         when = 'C550'
         when = 'C811'
!           let $contact_person = 'Kelly McCormick' GBD 08/07/2003
           let $contact_person = 'HR Manager'
         break
         when-other
!           let $contact_person = 'Stormy Sammon'   GBD 08/07/2003
!           let $contact_person = 'Communications Administrator, COD' ! CWB 06/10/2004
            let $contact_person = 'Employee Communications - Marketing Dept' ! CWB 06/10/2004
         break
       end-evaluate
    when = 'RRWHS' 
!      let $contact_person = 'Kelly McCormick'      GBD 08/07/2003
      let $contact_person = 'HR Manager'
      break
! CWB 12/05/2001 END
    when-other
!      let $contact_person = 'Personnel Manager'    GBD 08/07/2003
       let $contact_person = 'HR Manager'
      break
  end-evaluate

  if $acct_cd = '008'
!    let $contact_person = 'Randy Heiser'           GBD 08/07/2003
    let $contact_person = 'Vice President, Pharmacy'
  end-if

 End-If            ! ISDVNPK 10/03/2006 Added this

end-procedure



! ISDVNPK 10/03/2006 Modifed this to get the Location from ESTAB_TBL
!*********************************************************************
! Location Info
!*********************************************************************
begin-procedure Get-Location

move 'N' to $loc_found

begin-select

L.DESCR
L.ADDRESS1
L.ADDRESS2
L.CITY
L.STATE
L.POSTAL

  move 'Y'           to $loc_found
  move &L.DESCR      to $store_name
  move &L.ADDRESS1   to $store_address1
  move &L.ADDRESS2   to $store_address2
  move &L.CITY       to $store_city
  move &L.STATE      to $store_state
  move &L.POSTAL     to $store_zip

FROM PS_ESTAB_LOC_US E,
     PS_ESTAB_TBL L
WHERE E.LOCATION = $location2
and E.ESTABID = L.ESTABID
and L.EFFDT = (SELECT MAX(EFFDT)
		 FROM  PS_ESTAB_TBL
		 WHERE ESTABID = L.ESTABID
		 AND   EFFDT <=  $X000_Where_Select_AsOfDate)

end-select

if $loc_found = 'N'
   move ' ' to $location2
   display 'Location not found on PS_LOCATION_TBL.'
   move ' ' to $store_name
   move ' ' to $store_address1
   move ' ' to $store_address2
   move ' ' to $store_city
   move ' ' to $store_state
   move ' ' to $store_zip
end-if

end-procedure
! ISDVNPK 10/03/2006 Modifed the above to get the Location from ESTAB_TBL

! ISDVNPK 10/03/2006 Added to get the GEX_MAPP_EMPLID
!*********************************
Begin-Procedure Get-Mapp-Emplid
!*********************************

let $mapp_emplid = ' '
Begin-SELECT
MAPP.EMPLID
 let $mapp_emplid = RTRIM(&MAPP.EMPLID,' ')

FROM PS_GEX_EMPLID_MAPP MAPP
WHERE MAPP.SSN = $emplid

End-SELECT

End-Procedure
! ISDVNPK 10/03/2006 Added to get the GEX_MAPP_EMPLID

!*********************************************************************
! Emplid Change
!*********************************************************************
begin-procedure Emplid-Change



  do Format-Number(#service_years, $serv_years, 'B9')

  let $Year4 = '0'
  if rtrim($cmpny_seniority_dt,' ') <> ''
     do Format-DateTime($cmpny_seniority_dt,$orig_dt,{DEFMDY},'','')
  else
     do Format-DateTime($orig_hire_dt,$orig_dt,{DEFMDY},'','')
  end-if
  let $orig_mm = substr($orig_dt,1,2)
  let $orig_dd = substr($orig_dt,4,2)
  let $orig_yy = substr($orig_dt,7,2)

  let $orig_all = $orig_mm||'/'||$orig_dd||'/'||$orig_yy

     Use-Report TenplusYr-Report 
  if #current-line > 48
     new-page
  end-if
!ISDVBXS  11/29/2006 Change-Begin- Removed name and added last, first, middle names to the report
!                                  moved EMPLID,SEX,SERVICE YEARS to the next line
!  print 'NAME:'              (+1,1) 
!  print $name                (0,10)
  print 'LAST NAME:'         (+1,1)
  print $last_name           (0,12)
  print 'FIRST NAME:'        (0,43)
  print $first_name          (0,55)
  print 'MIDDLE NAME:'       (0,90)
  print $middle_name         (0,103)
  print 'EMPLID:'            (+1,1) 
  print  $mapp_emplid        (0,10)    ! ISDVNPK 10/03/2006 Modified this
  print 'SEX:'               (0,50) 
  print $sex                 (0,55)
  print 'SERVICE YEARS:'     (0,88) 
  print $serv_years          (0,102)
!ISDVBXS  11/29/2006 Change-End
  print 'ADDRESS:'           (+1,1) 
  print $address1            (0,10)
  print 'HIRE DATE:'         (0,44) 
  print $orig_all            (0,56)
  print 'DEPTID:'            (0,72) 
  print $deptid              (0,80)
 ! print 'L3 UNIQUE CD:'      (0,89)   ! ISDVNPK 10/03/2006 commented this
 ! print $l3_unique_cd       (0,103)   ! ISDVNPK 10/03/2006 commented this
  print $address2           (+1,10)
  print 'COMPANY:'           (0,71) 
  print $company             (0,80)
  print 'LOCATION:'          (0,93) 
  print $location           (0,103)

  let $address3 = Rtrim($per_city,' ') || ', ' || $per_state || '  ' || $per_zip

  if Rtrim($address2,' ') = ''
    print $address3          (0,10)
  else
    print $address3         (+1,10)
  end-if
 
 ! print 'CUST CD:'           (+1,1)  ! ISDVNPK 10/03/2006 commented this
 ! print $wk_customer_code    (0,10)  ! ISDVNPK 10/03/2006 commented this
  print 'CONTACT:'           (+1,1)  
  print $contact_person      (0,10)
  print 'STORE:'             (0,48) 
  print $store_name          (0,55)
  print 'ADDRESS:'          (+1,46) 
  print $store_address1      (0,55)

  if Rtrim($store_address2,' ') <> ''
    print $store_address2   (+1,55)
  end-if

  let $store_address3 = Rtrim($store_city,' ') || ', ' || $store_state || '  ' || $store_zip

  print $store_address3     (+1,55)
  print ' '                  (+2,1)

end-procedure

!*********************************************************************
! Write Record
!*********************************************************************
begin-procedure Write-Record

  if length($store_zip) = 10
      let $st_zip1 = rtrim($store_zip,' ')
  else
     let $st_zip1 = rtrim($store_zip,' ')
  end-if
  let $country_code = 'USA'

!Checks for PO BOX in Address1
  let $big_address1 = rtrim($address1,' ')
  let #max_length = length($big_address1)
  let #ba_start = 1
  let #ba_end = 6
  UPPERCASE $big_address1
  while #max_length > #ba_end
     let $sub_address1 = substr($big_address1,#ba_start,#ba_end)
     if $sub_address1 = 'PO BOX'
        add 30 to #ba_end
        let $address1 = $store_address1
        let $address2 = $store_address2
        let $per_city = $store_city
        let $per_state = $store_state
	let $per_zip = $st_zip1
     else
        add 1 to #ba_start
        add 1 to #ba_end
     end-if
  end-while

!Checks for PO BOX in Address2
  let $big_address2 = rtrim($address2,' ')
  let #max_length = length($big_address2)
  let #ba_start = 1
  let #ba_end = 6
  UPPERCASE $big_address2
  while #max_length > #ba_end
     let $sub_address2 = substr($big_address2,#ba_start,#ba_end)
     if $sub_address2 = 'PO BOX'
        add 30 to #ba_end
        let $address1 = $store_address1
        let $address2 = $store_address2
        let $per_city = $store_city
        let $per_state = $store_state
	let $per_zip = $st_zip1
     else
        add 1 to #ba_start
        add 1 to #ba_end
     end-if
  end-while

!*****
  let $store_addl_address2 = ' '

  let $store_address1 = rtrim($store_address1,' ')
  let $store_address2 = rtrim($store_address2,' ')
  let $store_address1 = $store_address1 || ' ' || $store_address2
  
!*****
  let $addl_address2 = ' '

  let $address1 = rtrim($address1,' ')
  let $address2 = rtrim($address2,' ')
  let $address1 = $address1 || ' ' || $address2
  
!*****  

 
  do Format-Number(#service_years,$serv_yrs,'B9')

  Let $Year4 = '1'
  do Format-DateTime($wk_hire_dt, $wk_hire,{DEFMDY},'','')
  let $wk_hire_mm = substr($wk_hire,1,2)
  let $wk_hire_dd = substr($wk_hire,4,2)
  let $wk_hire_yy = substr($wk_hire,7,4)
!  do makeyear4digits($wk_hire_yy)
  let $wk_hire_all = $wk_hire_mm||'/'||$wk_hire_dd||'/'||$wk_hire_yy


 let $store_name     = translate ($store_name,',',' ')
 let $contact_person = translate ($contact_person,',',' ')
 let $name           = translate ($name,',',' ')

 let $store_address1 = translate ($store_address1,',',' ')
 let $store_city = translate ($store_city,',',' ')
 let $store_state = translate ($store_state,',',' ')

 let $address1 = translate ($address1,',',' ')
 let $per_city = translate ($per_city,',',' ')
 let $per_state = translate ($per_state,',',' ')


! ISDVNPK 10/03/2006 Modified the filelayout
! ISDVBXS 11/29/2006 Modified the file layout to remove 'name' and add 'last,first and middle names'
 Let #file_number = 2

  write #file_number from 
               $deptid:15                   !Work Location-Code
               $delimiter
	       $store_name:35               !Work Location-Company Name
               $delimiter
	       $contact_person:35           !Work Location-Attention Line
               $delimiter
	       $store_address1:60           !Work Location-Street Address
	       $delimiter
	       $store_city:35               !Work Location-City
               $delimiter
	       $store_state:3               !Work Location-State
               $delimiter
	       $st_zip1:10                  !Work Location-Zip Code
               $delimiter
              ! $country_code:3              !Work Location-Country
              ! $delimiter
               $mapp_emplid:15              !Employee ID#
               $delimiter
	      ! $name:60                     !Employee Name
              ! $delimiter
	       $last_name:30                 !Employee Last name                    
	       $delimiter
	       $first_name:30                !Employee First name
	       $delimiter
	       $middle_name:30               !Employee Middle name
               $delimiter
	       $wk_hire_all:10              !Employee Hire Date
               $delimiter
	       $sex:1                       !Employee Gender
               $delimiter
	       $serv_yrs:22                 !Employee Award Level
               $delimiter
	       $address1:35                 !Home-Address1
               $delimiter
	       $per_city:35                 !Home-City
               $delimiter
	       $per_state:3                 !Home-State
               $delimiter
	       $per_zip:10                  !Home-Zip Code
              ! $delimiter
	      ! $country_code:3              !Home-Country
!** Clear Process Variables ***
! ISDVNPK 10/03/2006 Modified the filelayout

end-procedure


!GBD 08/07/2003 Begin
!*********************************************************************
!Insert values into Temp table
!*********************************************************************
Begin-Procedure Insert-Temp
Begin-Sql ON-ERROR=SQL-Error-Found('Insert-Temp')
INSERT INTO PS_GEX_R_GEXPY615
VALUES ($emplid,
        $company,
        $paygroup,
        $deptid,
        $location)
End-Sql

End-Procedure
!GBD 08/07/2003 End

!*********************************************************************
!Displays the error messages and stops execution
!*********************************************************************
 
begin-procedure SQL-Error-Found($Proc_Name) 
!  do error-found
  display 'SQL Error in ' 
  display $Proc_Name 
  display $_sql-error
  display 'sql-status:' 
  display #_sql-status 

  STOP

end-procedure

#Include 'gexxx951.sqc'  !Get Oracle Instance 
#Include 'gexxx900.sqc'  !Multiple companies run control
#Include 'gexxx902.sqc'  !Multiple deptid run control
!#include 'gexaltse.sqc'  !Alter session SQC	!Commented for Upgrade 9.0 GEX-AXG
#Include 'gexxx922.sqc'  !Payroll run control
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'hrrnctl1.sqc'  !Get_Run_Control Procedure
#Include 'hrgetval.sqc'  !Get values mask routines
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'sqrtrans.sqc'
