!***********************************************************************
! GEXBN817:  Reset ELIG_CONFIG3                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXBN817 :            Reset ELIG_CONFIG3                             *
!                                                                      *
! Narrative:            Benefit programs G03 and G04 use elig config 3 *
!                       to represent a 'promoted from date'.  This is  *
!                       the citizenship code converted from DBS.  When *
!                       an EE is rehired, unless they are rehired due  *
!                       to reinstated from discipline, this field      *
!                       needs to be reset to all zeros.                *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           ben_prog_partic                                *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         None                                         *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! JDH       09/01/98   Initial Creation                                *
! SXK	      09/11/98   Commented out Ben Prog Partic access; No need   *
!			     to select employees by benefit program          *
! GEX_SEC_REENG 2015-06-24 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


#define #column1  001
#define #column2  022
#define #column3  060
#define #column4  078
#define #column5  096
#define #column6  116


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 8
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'

  Print 'As Of Date: '       (3,1)
  Do Format-DateTime($AsOfDate, $out, {DEFDATE}, '', '')
  Print $out ()

  Print $ReportTitle2        (4,) Center
  Print $ReportTitle3        (5,) Center

  Print 'EMPLOYEE'           (+2,{#column1})
  Print 'SERVICE'            (+0,{#column3})
  Print 'ELIG'               (+0,{#column5})
!  Print 'BENEFIT'            (+0,{#column6})

  Print 'ID'                 (+1,{#column1})
  Print 'NAME'               (+0,{#column2})
  Print 'DATE'               (+0,{#column3})
  Print 'EFFDT'              (+0,{#column4})
  Print 'CONFIG3'            (+0,{#column5})
!  Print 'PROGRAM'            (+0,{#column6})
End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXBN817'
  Let $ReportTitle   = ' '
  Let $ReportTitle2  = 'Employees who had ELIG_CONFIG3 (Promoted from Date) Value Reset'
  Let $ReportTitle3  = 'Examine the corresponding Benefits Event for possible reprocessing'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show ' '
  Show 'Report Began:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime

  Let $AsOfDate = $AsOfToday
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select On-Error=P260-SQL-Error-Found('P200-Main-Process')
j.emplid
j.empl_rcd
j.effdt
j.effseq
pd.name
e.service_dt
!bpp.benefit_program

  Let $Year4 = '1'
  Do Format-DateTime(&j.effdt, $j.effdt, {DEFYMD}, '', '')
  Do P210-Select

From   ps_personal_data      pd,
       ps_employment         e,
       ps_job                j
!      ps_ben_prog_partic    bpp

!Where bpp.benefit_program   In ('G03','G04')

!And   bpp.effdt              =
!     (Select Max(effdt)
!      From   ps_ben_prog_partic
!      Where  emplid          = bpp.emplid
!      And    empl_rcd       = bpp.empl_rcd
!      And    cobra_event_id  = bpp.cobra_event_id
!      And    effdt          <= $AsOfDate)

!And   e.emplid               = bpp.emplid
!And   e.empl_rcd            = bpp.empl_rcd

Where  e.service_dt          >= To_Date('03-JUN-1991','DD-MON-YYYY')

And    j.emplid               = e.emplid
And    j.empl_rcd            = e.empl_rcd

And    j.effdt                =
      (Select Max(effdt)
       From   ps_job
       Where  emplid          = j.emplid
       And    empl_rcd       = j.empl_rcd
       And    effdt          <= $AsOfDate)

And    j.effseq               =
      (Select Max(effseq)
       From   ps_job
       Where  emplid          = j.emplid
       And    empl_rcd       = j.empl_rcd
       And    effdt           = j.effdt)

And    j.elig_config3        <> '0000000000'

And    pd.emplid              = j.emplid

Order By pd.name
End-Select
End-Procedure


Begin-Procedure P210-Select
#debug9 Show 'P210-Select'
Begin-Select On-Error=P260-SQL-Error-Found('P210-Select')
j2.emplid
j2.empl_rcd
j2.effdt
j2.effseq
j2.elig_config3

  Let $Year4 = '1'
  Do Format-DateTime(&j2.effdt, $j2.effdt, {DEFYMD}, '', '')

  If ($j2.effdt   > $j.effdt) Or
     ($j2.effdt   = $j.effdt  And
      &j2.effseq >= &j.effseq)
      Let $Data-Found-Flag = 'Y'
      Do P220-Update
      Do P230-Print-Record
  End-If

From   ps_job          j2

Where  j2.emplid        = &j.emplid
And    j2.empl_rcd     = &j.empl_rcd
And    j2.effdt        >= &j.effdt
And    j2.elig_config3 <> '0000000000'
End-Select
End-Procedure


Begin-Procedure P220-Update
#debug9 Show 'P220-Update'
Begin-SQL On-Error=P260-SQL-Error-Found('P220-Update')
  Update ps_job
     Set elig_config3 = '0000000000'
   Where emplid       = &j2.emplid
     And empl_rcd    = &j2.empl_rcd
     And effdt        = &j2.effdt
     And effseq       = &j2.effseq
End-SQL
End-Procedure


Begin-Procedure P230-Print-Record
#debug9 Show 'P230-Print-Record'
  Do Format-DateTime(&j2.effdt, $effdt, {DEFMDY}, '', '')
  Do Format-DateTime(&e.service_dt, $service_date, {DEFMDY}, '', '')

  Print &j2.emplid           (+1,{#column1})      !Edit xxx-xx-xxxx ! GEX_SEC_REENG 2015-06-24 Vahini Katta  
  Print &pd.name             (+0,{#column2},030)
  Print $service_date        (+0,{#column3})
  Print $effdt               (+0,{#column4})
  Print &j2.elig_config3     (+0,{#column5})
End-Procedure


Begin-Procedure P260-SQL-Error-Found($Proc_Name) 
#debug9 Show 'P260-SQL-Error-Found'
  Display 'SQL Error in ' 
  Display $Proc_Name 
  Display $_sql-error
  Display 'sql-status:' 
  Display #_sql-status 
  Do P270-Emplid-in-Error
End-Procedure


Begin-Procedure P270-Emplid-in-Error
#debug9 Show 'P270-Emplid-in-Error'
  Display ''
  Display 'Error occurred while processing emplid ' Noline
  Display &j.emplid
  Display ''
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Do Get-Current-DateTime    !curdttim.sqc

  Show ' '
  Show 'Report Ended:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime
  Show ' '
  Show 'Successful end of report'
End-Procedure


#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
