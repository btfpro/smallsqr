!***********************************************************************
! GEXPY045:  Pension Codes for Franchise Employees                     *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY045:             Pension Codes for Franchise Employees          *
!                                                                      *
! Narrative:            This program reports pension codes for         *
!                        franchise employees.                          *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           gex_dept_tbl                                   *
!                       fed_tax_data                                   *
!                       personal_data                                  *
!                       job                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        Company Division Code(s)                       *
!                       Company(s)                                     *
!                       Pension Plan Flag                              *
!                       As Of Date                                     *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS      DATE            DESCRIPTION OF THE CHANGE              *
!***********************************************************************
! JDH           12/15/99        Initial Creation                       *
!***********************************************************************


#Include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


#define #column1     1
#define #column2    20
#define #column3    60
#define #column4    80


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 7
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg02.sqc'

  Let $X000_temp_out = 'Report as of ' || $X000_Report_Heading_AsOfDate
  Print $X000_temp_out     (3,)  Center

  Print 'EMPLID'           (+3,{#column1})
  Print 'NAME'             (  ,{#column2})
  Print 'DEPTID'           (  ,{#column3})
  Print 'PENSION FLAG'     (  ,{#column4})

  Print '-'                (+1,{#column1},9)   Fill
  Print '-'                (  ,{#column2},30)  Fill
  Print '-'                (  ,{#column3},10)  Fill
  Print '-'                (  ,{#column4},12)  Fill
End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID    =  'GEXPY045'
  Let $ReportTitle =  'Pension Codes for Franchise Employees'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  Let $GEXXX900_Company_Alias  = 'j.company'
  Let $GEXXX907_Div_CD_Alias   = 'gdt.gex_company_div_cd'

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan

  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '

  If Rtrim($pension_flag, ' ') = ''  Or
     Rtrim($pension_flag, ' ') = 'B'
    Let $pension_plan_criteria = '1=1'
    Let $pension_plan_display_criteria = 'ALL'
  Else
    Let $pension_plan_criteria = 'ftd.pension_plan = ''' || $pension_flag || ''''
    Let $pension_plan_display_criteria = $pension_plan_criteria
  End-If

  Do Format-DateTime($X000_Where_Select_AsOfDate, $X000_Report_Heading_AsOfDate, {DEFDATE}, '', '')
  Let $X000_Order_By = 'Company, Pension Plan, Name'

  Show '$GEXXX907_Include_Div_Cd_Display_Criteria           = '   $GEXXX907_Include_Div_Cd_Display_Criteria
  Show '$GEXXX900_Include_Company_Display_Criteria          = '   $GEXXX900_Include_Company_Display_Criteria
  Show '$Pension_Plan_Display_Criteria                      = '   $Pension_Plan_Display_Criteria
  Show '$X000_Where_Select_AsOfDate                         = '   $X000_Where_Select_AsOfDate

  Do P130-Print-Cover-Page
End-Procedure


Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Do Ask-As-Of-Date
  Let $X000_Where_Select_AsOfDate = $AsOfDate

  Let $DivCdString = ''''
  Display 'Enter Company Division Code or leave blank to exit.'
  While 1=1
    Input $In-DivCd Maxlen=3 'Company Division Code'
    Uppercase $In-DivCd
    If Rtrim($In-DivCd, ' ') = ''
      Concat '''' With $DivCdString
      Break
    End-If
    If $DivCdString <> ''''
      Concat ''',''' With $DivCdString
    End-If
    Concat $In-DivCd With $DivCdString
  End-While

  If $DivCdString = ''''''
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = '1=1'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = $GEXXX907_Div_CD_Alias || ' In (' || $DivCdString || ')'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA
  End-If

  Let $CompanyString = ''''
  Display 'Enter Company or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Concat '''' With $CompanyString
      Break
    End-If
    If $CompanyString <> ''''
      Concat ''',''' With $CompanyString
    End-If
    Concat $In-Company With $CompanyString
  End-While

  If $CompanyString = ''''''
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '1=1'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_Company_Alias || ' In (' || $CompanyString || ')'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA
  End-If

  Input $pension_flag 'Pension Plan Flag' Maxlen=1
End-Procedure


Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXXX922-Select-Parameters

  Let $X000_Where_Select_AsOfDate = $GEX_RC_PAY.AsOfDate
  If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
    Let $X000_Where_Select_AsOfDate = $AsOfToday
  End-If

  Do GEXXX900-Select-Company-Parameters
  Do GEXXX907-Select-Div-CD-Parameters 

  Let $pension_flag = $GEX_RC_PAY.GEXPY045_YES_NO
End-Procedure


begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_Include_Div_Cd_Display_Criteria           ='   (+2,5)
  Print $GEXXX907_Include_Div_Cd_Display_Criteria                 (0,+2)
  Print '$GEXXX900_Include_Company_Display_Criteria          ='   (+1,5)
  Print $GEXXX900_Include_Company_Display_Criteria                (0,+2)
  Print '$Pension_Plan_Display_Criteria                      ='   (+1,5)
  Print $Pension_Plan_Display_Criteria                            (0,+2)

  Print 'As Of Date :'                      (+2,5)
  Print $X000_Report_Heading_AsOfDate       (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'   (+5,2)
  Print $X000_Order_By                      (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select
j.company  ()  On-Break  Level=1
                         Print=Never
                         After=New-Page

ftd.pension_plan
j.emplid
pd.name
j.deptid

  Let $Data-Found-Flag = 'Y'

  Let $company = &j.company

  Do P210-Print-Record

From   ps_gex_dept_tbl     gdt,
       ps_fed_tax_data     ftd,
       ps_personal_data    pd,
       ps_job              j

Where  pd.emplid           = j.emplid

And    j.effdt             =
      (Select Max(effdt)
       From   ps_job
       Where  emplid       = j.emplid
       And    empl_rcd    = j.empl_rcd
       And    effdt       <= $X000_Where_Select_AsOfDate)

And    j.effseq            =
      (Select Max(effseq)
       From   ps_job
       Where  emplid       = j.emplid
       And    empl_rcd    = j.empl_rcd
       And    effdt        = j.effdt)

And    gdt.deptid          = j.deptid

And    gdt.effdt           =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid       = gdt.deptid
       And    effdt       <= $X000_Where_Select_AsOfDate)

And    gdt.sequence_number =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid       = gdt.deptid
       And    effdt        = gdt.effdt)

And    ftd.emplid          = j.emplid
And    ftd.company         = j.company

And    ftd.effdt           =
      (Select Max(effdt)
       From   ps_fed_tax_data
       Where  emplid       = ftd.emplid
       And    company      = ftd.company
       And    effdt       <= $X000_Where_Select_AsOfDate)

And   [$Pension_Plan_Criteria]
And   [$GEXXX900_Include_Company_Criteria]
And   [$GEXXX907_Include_Div_Cd_Criteria]

Order By j.company, ftd.pension_plan, pd.name
End-Select
End-Procedure


Begin-Procedure P210-Print-Record
#debug9 Show 'P210-Print-Record'
  Print &j.emplid           (+1,{#column1})
  Print &pd.name            (  ,{#column2},30)
  Print &j.deptid           (  ,{#column3})
  Print &ftd.pension_plan   (  ,{#column4})
End-Procedure


Begin-Procedure New-Page
#debug9 Show 'New-Page'
  New-Page
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded

  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  show ' '

  Show 'Successful end of report'
End-Procedure


#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'gexxx900.sqc'  !Get company multiple row table
#Include 'gexxx907.sqc'  !Get company/division code multiple row table
#Include 'gexxx922.sqc'  !Get pay single row run control
