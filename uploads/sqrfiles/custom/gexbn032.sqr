!***
!  this is in my working directory
!***


!***********************************************************************
! GEXBN032:  ENVELOPE INSERTS AND REPORT FOR RETIRED/DEFERRED EMPLOYEES*
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXBN032: ENVELOPE INSERTS AND REPORT FOR RETIRED/DEFERRED EMPLOYEES *
!                                                                      *        
! Narrative:	    This report produces two outputs:mailing inserts *
!                     with employee name and address information       *
!                     able to fit inside an 8.5 X 11 envelop and a     *
!                     report listing of the employees for which a label*
!                     was created.                                     * 
!                                                                      *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values                    *
!			      #debug7 data returned from sql calls           * 
!                                                                      *  
! SQL Tables:	      gex_retired_def                                *
!                                                                      *
!                                                                      *
!                                                                      *
! Written by:		Joe Besagar 07/01/1998                         *       
!                                                                      *
! Normally Run:		On demand                                      *
!                                                                      *
! Control Parms:		FOR WHERE CLAUSE:                              *
!				                                               *
!			      GEX_GRPINS_CD OR GEX_PENSION_CD                *
!			      FROM BIRTHDATE                                 *
!                       THRU BIRTHDATE                                 *
!                       FROM GRPINS_RET_DT                             *
!                       THRU GRPINS_RET_DT                             *
!                       FROM ZIP                                       *
!                       THRU ZIP                                       *
!                       FROM PENSION RET DT                            *
!                       THRU PENSION RET DT                            *
!                       QDRO                                           *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE                *
!***********************************************************************
!	JNB		07/01/98	Initial Creation                         *
!       SXK             09/22/1998      Added New Mobius ReportID                                                               *
!       SXK             11/23/1999      Changed from zip to postal     *
!       CJH             02/26/2003      Alter printer defn
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


begin-setup


declare-report Report-log
Printer-Type=HPLASERJET
   layout=DEFAULT
end-declare
declare-report mail-list
Printer-Type=HPLASERJET
   layout=DEFAULT
end-declare



end-setup

begin-report
#debug9 Show 'begin-report'

!CJH - 02/26/2003 - alter printer defn
alter-printer 
  font = 5
  POINT-SIZE = 11
! CJH -2/26/2003 - end

move 'N' to $rpt-log-printed
move 'N' to $mail-list-printed

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P000-Init-Report
  do P190-Decide-by-sort
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc

  if $rpt-log-printed = 'N'
     use-report report-log
     print 'No log information to report' (+1,1) bold
  end-if
 
  if $mail-list-printed= 'N'
     use-report mail-list
     print 'No log information to report' (+1,1) bold
  end-if

  Show 'Successful end of report'
end-report


begin-heading 6  for-reports=(report-log)
#debug9 Show 'begin-heading'
  !SXK 09/22/1998 Begin
  let $ReportID = 'BN032LOG'
  !SXK 09/22/1998 End
  #Include 'stdhdg01.sqc'

  Let $X000_temp_out = 'Sorted by ' || $X000_ORDER_BY
  Print $X000_temp_out         (3,)  center
  Let $X000_temp_out = 'Report as of ' || $X000_REPORT_HEADING_ASOFDATE
  Print $X000_temp_out         (4,)  center

  PRINT 'PENSION'        (+3,2)
  PRINT 'GRP INS'        (,11)
  PRINT 'NAME'           (,24)
  PRINT 'SOC SEC NUM'    (,54)
  PRINT 'ADDRESS LINE 1' (,74)
  PRINT 'ADDRESS LINE 2' (,104)
  PRINT 'CITY'           (,128)
  PRINT 'STATE'          (,153)
  PRINT 'ZIP'            (,163)
  PRINT '   CD   '       (+1,2) 
end-heading


begin-procedure P000-Init-Report
#debug9 Show 'P000-Init-Report'

  move 'GEXBN032'         to $ReportID
  move 'RETIRED/DEFERRED VESTED MAILING INSERTS AUDIT REPORT' to $ReportTitle

  do Stdapi-Init

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate=' $ReportDate
  Show '$ReportTime=' $ReportTime

  if $prcs_process_instance = ''
    Do Ask-From-Thru-Date
    Let $BirthDate_Range_Criteria = 'A.Birthdate Between ''' || $FromDate ||
                                                 ''' And ''' || $ThruDate || ''''
  
    let $GEXXX914_INCLUDE_GEX_GRPINS_CD_CRITERIA   = '1=1'
    let $GEXXX926_INCLUDE_GEX_PENSION_CD_CRITERIA  = '1=1'
    let $X000_WHERE_SELECT_ASOFDATE                = $AsOfToday
    let $pensnRetDate_Range_Criteria               = '1=1'  
    let $GrpinsRetDate_Range_Criteria              = '1=1'
    let $Zip_Range_Criteria                        = '1=1' 
    let $gex_QDRO_Criteria                         = '1=1'
    let $X000_ORDER_BY                             = ' '
  else
    Do GEXRCBN1-SELECT-PARAMETERS

    Let $FromBirthDate = &GEX_RC_BEN.From_Birthdate
    Let $ThruBirthDate = &GEX_RC_BEN.Thru_Birthdate

    If Rtrim($FromBirthDate, ' ') = '' or
       Rtrim($ThruBirthDate, ' ') = '' 
         Let $BirthDate_Range_criteria = '1=1'
    Else
         Let $BirthDate_Range_Criteria = 'A.Birthdate Between ''' || $FromBirthDate ||
                                                 ''' And ''' || $ThruBirthDate || ''''
    End-if

    Do Format-DateTime($FromBirthDate, $FromBirthDate_MDY, {DEFMDY}, '', '')
    Do Format-DateTime($ThruBirthDate, $ThruBirthDate_MDY, {DEFMDY}, '', '')


    Let $FromGrpinsRetDate = &GEX_RC_BEN.From_Grpins_ret_dt
    Let $ThruGrpinsRetDate = &GEX_RC_BEN.Thru_Grpins_ret_dt

    If Rtrim($FromGrpinsRetDate, ' ') = '' or
       Rtrim($ThruGrpinsRetDate, ' ') = '' 
         Let $GrpinsRetDate_Range_criteria = '1=1'
    Else
         Let $GrpinsRetDate_Range_Criteria = 'A.Gex_Grpins_ret_dt Between ''' || $FromGrpinsRetDate ||
                                                 ''' And ''' || $ThruGrpinsRetDate || ''''
    End-if

    Do Format-DateTime($FromGrpinsRetDate, $FromGrpinsRetDate_MDY, {DEFMDY}, '', '')
    Do Format-DateTime($ThruGrpinsRetDate, $ThruGrpinsRetDate_MDY, {DEFMDY}, '', '')


    Let $FrompensnRetDate = &GEX_RC_BEN.From_pensn_ret_dt
    Let $ThrupensnRetDate = &GEX_RC_BEN.Thru_pensn_ret_dt

    If Rtrim($FrompensnRetDate, ' ') = '' or
       Rtrim($ThrupensnRetDate, ' ') = '' 
         Let $pensnRetDate_Range_criteria = '1=1'
    Else
         Let $pensnRetDate_Range_Criteria = 'A.Gex_pension_ret_dt Between ''' || $FrompensnRetDate ||
                                                 ''' And ''' || $ThrupensnRetDate || ''''
    End-if

    Do Format-DateTime($FrompensnRetDate, $FrompensnRetDate_MDY, {DEFMDY}, '', '')
    Do Format-DateTime($ThrupensnRetDate, $ThrupensnRetDate_MDY, {DEFMDY}, '', '')

    Let $gexQDRO = &GEX_RC_BEN.GEX_QDRO
    Display '$gexQDRO ' noline
    display $gexQDRO
    If Rtrim($gexQDRO, ' ') = ''
       Let $gex_QDRO_Criteria = '1=1'
    Else
       Let $gex_QDRO_Criteria = 'A.GEX_QDRO = '''|| $gexqdro || ''''
        display ' $gex_QDRO_Criteria ' noline
        display $gex_qdro_criteria
    end-if
   
    Let $FromZip = &GEX_RC_BEN.From_Zip
    Let $ThruZip = &GEX_RC_BEN.Thru_zip

    If Rtrim($FromZip, ' ') = '' or
       Rtrim($ThruZip, ' ') = '' 
         Let $Zip_Range_Criteria = '1=1'
    Else
!SXK 11/23/1999 Changed from zip to postal
         Let $Zip_Range_Criteria = 'A.Postal Between ''' || $FromZip || 
                                            ''' And ''' || $ThruZip || ''''
    End-if

    Let $GEXXX911_Plan_Type_Alias       = '1'
    Let $GEXXX911_Benefit_Plan_Alias    = '1'
    lET $GEXXX914_GEX_GRPINS_CD_ALIAS = 'GEX_GRPINS_CD'
    Do  GEXXX914-Select-GEX-GRPINS-CD-PARAMETERS

    Let $GEXXX926_GEX_PENSION_CD_Alias   =  'GEX_PENSION_CD'
    Do  GEXXX926-Select-GEX-Pension-CD-Parameters

    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate
    If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  End-If

  If $GEXXX914_INCLUDE_GEX_GRPINS_CD_DISPLAY_CRITERIA = 'ALL'
      Let $X000_ORDER_BY = 'A.GEX_PENSION_CD, A.NAME'
      Let $sort_by_pension = 'Y'
  Else
      Let $X000_ORDER_BY = 'A.GEX_GRPINS_CD, A.NAME'
      Let $sort_by_grpins = 'Y'
  End-If

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE
  show '$GEXXX914_INCLUDE_GEX_GRPINS_CD_CRITERIA       =' $GEXXX914_INCLUDE_GEX_GRPINS_CD_CRITERIA
  show '$GEXXX926_INCLUDE_GEX_PENSION_CD_CRITERIA      =' $GEXXX926_INCLUDE_GEX_PENSION_CD_CRITERIA         

	
  Do P050-Print-Cover-Page

end-procedure


begin-procedure P050-Print-Cover-Page
#debug9 Show 'P050-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)

  Print 'As Of Date :'                                            (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                             (0,+2)

  Print 'From BirthDate :'                                        (+2,5)
  Print $FromBirthDate_MDY                                        (0,+2)
  Print 'Thru BirthDate :'                                        (+1,5)
  Print $ThruBirthDate_MDY                                        (0,+2)   

  Print 'From Grp Ins Ret Date :'                                 (+2,5)
  Print $FromGrpinsRetDate_MDY                                    (0,+2)
  Print 'Thru Grp Ins Ret Date :'                                 (+1,5)
  Print $ThruGrpinsRetDate_MDY                                    (0,+2)

  Print 'From Pension Ret Date :'                                 (+2,5)
  Print $FrompensnRetDate_MDY                                     (0,+2)
  Print 'Thru Pension Ret Date :'                                 (+1,5)
  Print $ThrupensnRetDate_MDY                                     (0,+2)

  Print 'From Zip Code :'                                         (+2,5)
  Print $FromZip                                                  (0,+2)
  Print 'Thru Zip Code :'                                         (+1,5)
  Print $ThruZip                                                  (0,+2)

  Print 'QDRO :'                                                  (+2,5)
  Print $gex_QDRO_Criteria                                        (0,+2)

  print '$GEXXX914_INCLUDE_GEX_GRPINS_CD_DISPLAY_CRITERIA  ='   (+2,5)
  print $GEXXX914_INCLUDE_GEX_GRPINS_CD_DISPLAY_CRITERIA 	    (0,+2)

  print '$GEXXX926_INCLUDE_GEX_PENSION_CD_DISPLAY_CRITERIA  ='   (+2,5)
  print $GEXXX926_INCLUDE_GEX_PENSION_CD_DISPLAY_CRITERIA 	     (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,2)
  Print $X000_ORDER_BY                                            (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


begin-procedure P190-Decide-by-sort

If $sort_by_pension = 'Y'
   Do P200-Process-Main
Else
   Do P210-Process-Main
end-if

End-Procedure

begin-procedure P200-Process-Main
#debug9 Show 'P200-Process-Main'

  Let $Data-Found-Flag = 'N'
  use-report report-log
  move 'Y' to $rpt-log-printed

 
begin-SELECT
A.GEX_PENSION_CD  &gex_pension_cd      (+3,2)  ON-BREAK PRINT=ALWAYS LEVEL=1
                                       SAVE=$PREV_PENSIONCODE AFTER=P200-PENSIONCODE-CHANGE 
   move &gex_pension_cd to $gex_pension_cd
A.GEX_GRPINS_CD   &gex_grpins_cd       (,11)
   move &gex_grpins_cd  to $gex_grpins_cd 
A.NAME            &name                (,24) 
   move &name           to $name
A.EMPLID          &emplid              (,54)   EDIT XXX-XX-XXXX
   move &emplid         to $emplid
A.ADDRESS1        &address1            (,74)   
   move &address1       to $address1 
A.ADDRESS2        &address2           (,104)
   move &address2       to $address2 
A.CITY            &city               (,128) 
   move &city           to $city
A.STATE           &state              (,153)
   move &state          to $state
!sxk 11/23/99 Changed from zip to postal
A.postal             &zip                (,163)
   move &zip            to $zip
 
  Let $Data-Found-Flag = 'Y'
  Let #emp_count = #emp_count + 1 
  Do mail-list-printing


FROM   PS_gex_retired_def   A 
     
Where  [$GEXXX914_INCLUDE_GEX_GRPINS_CD_CRITERIA]
AND    [$GEXXX926_INCLUDE_GEX_PENSION_CD_CRITERIA]
AND    [$BirthDate_Range_Criteria]
AND    [$pensnRetDate_Range_Criteria]
AND    [$GrpinsRetDate_Range_Criteria]
AND    [$Zip_Range_Criteria] 
AND    [$gex_QDRO_Criteria]
ORDER  BY [ $X000_ORDER_BY ] 
end-SELECT

  use-report report-log

  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Print 'Total Employees for Report: '     (+4,8)
  Print #report_count        (,65)   edit 9,999

end-procedure

begin-procedure P210-Process-Main
#debug9 Show 'P210-Process-Main'

  Let $Data-Found-Flag = 'N'
  use-report report-log
  move 'Y' to $rpt-log-printed

 
begin-SELECT
A.GEX_GRPINS_CD   &gex_grpins_cd_b          (+3,11)   ON-BREAK PRINT=ALWAYS LEVEL=1
                                            SAVE=$PREV_GRPINSCODE AFTER=P200-GRPINSCODE-CHANGE
    move &gex_grpins_cd_b  to  $gex_grpins_cd
A.GEX_PENSION_CD  &gex_pension_cd_b         (,2)
    move &gex_pension_cd_b to  $gex_pension_cd   
A.NAME            &name_b         (,24) 
    move &name_b           to  $name
A.EMPLID          &emplid_b       (,54)   EDIT XXX-XX-XXXX
    move &emplid_b         to  $emplid 
A.ADDRESS1        &address1_b     (,74)
    move &address1_b       to  $address1   
A.ADDRESS2        &address2_b     (,104)
    move &address2_b       to  $address2
A.CITY            &city_b         (,128) 
    move &city_b           to  $city 
A.STATE           &state_b        (,153)
    move &state_b          to  $state      
!SXK 11/23/99 Changed from zip
A.POSTAL             &zip_b          (,163)
    move &zip_b            to  $zip

  Let $Data-Found-Flag = 'Y'
  Let #emp_count = #emp_count + 1 
  Do mail-list-printing


FROM   PS_gex_retired_def   A 
     
Where  [$GEXXX914_INCLUDE_GEX_GRPINS_CD_CRITERIA]
AND    [$GEXXX926_INCLUDE_GEX_PENSION_CD_CRITERIA]
AND    [$BirthDate_Range_Criteria]
AND    [$pensnRetDate_Range_Criteria]
AND    [$GrpinsRetDate_Range_Criteria]
AND    [$Zip_Range_Criteria] 
AND    [$gex_QDRO_Criteria]
ORDER  BY [ $X000_ORDER_BY ] 
end-SELECT

  use-report report-log

  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Print 'Total Employees for Report: '     (+4,8)
  Print #report_count        (,65)   edit 9,999

end-procedure



!*********************************************************************
begin-procedure P200-PENSIONCODE-CHANGE 
!*********************************************************************
  use-report report-log

  Print 'Total Employees for Pension Code: '   (+4,8)
  Print  $prev_Pensioncode                   (+0,42)                       
  Print #emp_count   (,65)  edit 9,999

  Let #report_count = #report_count + #emp_count 
  Let #emp_count = 0
  Print ' '                            (+2,5)
 if #current-line >= 50
    new-page
 end-if 

end-procedure

!*********************************************************************
begin-procedure P200-GRPINSCODE-CHANGE 
!*********************************************************************

  use-report report-log

  Print 'Total Employees for GRPINS CODE: '   (+4,8)
  Print  $prev_grpinscode                     (+0,42)                       
  Print #emp_count   (,65)  edit 9,999

  Let #report_count = #report_count + #emp_count 
  Let #emp_count = 0
  Print ' '                           (+2,5)
  if #current-line >= 50
    new-page
  end-if

end-procedure

!*********************************************************************
begin-procedure mail-list-printing 
!*********************************************************************
   use-report mail-list

!   Do Declare-Printer-maillist

!   graphic () font 5  11
   Let $Name = RTRIM($name, ' ')
   Do Rotate-Name    !convert to First name Last name
   
  if rtrim($address2,' ') <> ''
   Print $name           (+22,42)
   print $gex_pension_cd (,73)
   Print $address1       (+1,42)
   Print $address2       (+1,42)
   Print $city           (+1,42)
   Print $state          (,62)
   Print $zip            (,65)
  else
   Print $name           (+22,42)
   print $gex_pension_cd (,73)
   Print $address1       (+1,42)
   Print $city           (+1,42)
   Print $state          (,62)
   Print $zip            (,65)
  end-if
   new-page 

  move 'Y' to $mail-list-printed
  use-report report-log

end-procedure

#include 'gexxx914.sqc'  !Get GEX_GRPINS_CD multiple row run control
#include 'gexxx926.sqc'  !Get GEX_PENSION_CD multiple row run control
#include 'gexxx920.sqc'  !Get ben single row run control 

#include 'askftd.sqc'    !Ask-From-Thru-Date procedure
#include 'rotname1.sqc'  !Rotate-Name procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
