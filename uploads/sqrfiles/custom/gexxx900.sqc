!**************************************************************************
!  GEXXX900:  Retrieve "Multiple" Company Run Controls and                *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX900_Company_Alias                                              *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX900_INCLUDE_COMPANY_CRITERIA                                   *
!    $GEXXX900_EXCLUDE_COMPANY_CRITERIA                                   *
!    $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                           *
!    $GEXXX900_EXCLUDE_COMPANY_DISPLAY_CRITERIA                           *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX900_Company_Alias = 'J.COMPANY'                            *
!    Do GEXXX900-Select-Company-Parameters                                *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX900_EXCLUDE_COMPANY_CRITERIA]                             *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX900-Select-Company-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_COMPANY_ROWS = 0
  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = ''
   
  Let $GEXXX900_EXCLUDE_COMPANY_CRITERIA = ''
  Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX900_EXCLUDE_COMPANY_DISPLAY_CRITERIA = 'NONE'
  
  
  show 'oprid=' $OprID !me
  show 'run id=' $Run_Cntl_ID  !me
  show 'company criteria' $GEXXX900_INCLUDE_COMPANY_CRITERIA !me
  show 'company alias' $GEXXX900_Company_Alias !me
  show 'column var company ' &GEX_RC_COMPANY.COMPANY !me
  show 'b4 select'
Begin-Select

GEX_RC_COMPANY.COMPANY
  
  Add 1 To #GEX_RC_COMPANY_ROWS
 
  If #GEX_RC_COMPANY_ROWS = 1
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '(' || $GEXXX900_Company_Alias || ' In ('''     || &GEX_RC_COMPANY.COMPANY || ''''
    show 'in if'$GEXXX900_INCLUDE_COMPANY_CRITERIA !me
    Let $GEXXX900_EXCLUDE_COMPANY_CRITERIA = '(' || $GEXXX900_Company_Alias || ' Not In (''' || &GEX_RC_COMPANY.COMPANY || ''''
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA || ',''' || &GEX_RC_COMPANY.COMPANY  || ''''
    show 'in else'$GEXXX900_INCLUDE_COMPANY_CRITERIA !me
    Let $GEXXX900_EXCLUDE_COMPANY_CRITERIA = $GEXXX900_EXCLUDE_COMPANY_CRITERIA || ',''' || &GEX_RC_COMPANY.COMPANY  || ''''
  End-If

From  PS_GEX_RC_COMPANY GEX_RC_COMPANY

Where GEX_RC_COMPANY.OPRID                = [$OprID]
And   GEX_RC_COMPANY.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_COMPANY.COMPANY, ' ') != ' '

    
End-Select

  If #GEX_RC_COMPANY_ROWS = 0
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_Company_Alias || ' = ' || $GEXXX900_Company_Alias
    Let $GEXXX900_EXCLUDE_COMPANY_CRITERIA = $GEXXX900_Company_Alias || ' = ' || $GEXXX900_Company_Alias
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA || '))'
    Let $GEXXX900_EXCLUDE_COMPANY_CRITERIA = $GEXXX900_EXCLUDE_COMPANY_CRITERIA || '))'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA
    Let $GEXXX900_EXCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_EXCLUDE_COMPANY_CRITERIA
  End-If

End-Procedure
