!**************************************************************************
!  GEXXX940:  Retrieve "Multiple" Coverage Elect Run Controls and         *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX940_Coverage_Elect_Alias                                       *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA                            *
!    $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA                            *
!    $GEXXX940_INCLUDE_COVERAGE_ELECT_DISPLAY_CRITERIA                    *
!    $GEXXX940_EXCLUDE_COVERAGE_ELECT_DISPLAY_CRITERIA                    *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX940_Coverage_Elect_Alias = 'GDT.COVERAGE_ELECT'            *
!    Do GEXXX940-Select-Coverage-Electt-Parameters                        *
!    Begin-Select                                                         *
!    COVERAGE_ELECT                                                       *
!    FROM PS_GEX_RC_CVG_ELEC GDT                                          *
!    WHERE [$GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA]                    *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!  INITIALS     DATE            DESCRIPTION OF THE CHANGE                 *
!**************************************************************************
!  AXL          01/18/2006       INITIAL CREATION                         *
!**************************************************************************


Begin-Procedure GEXXX940-Select-Coverage-Elect-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_Cvrg_Elect_ROWS = 0
  Let $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA = ''
  Let $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA = ''
  Let $GEXXX940_INCLUDE_COVERAGE_ELECT_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX940_EXCLUDE_COVERAGE_ELECT_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_Cvrg_Elect.COVERAGE_ELECT

  Add 1 To #RC_Cvrg_Elect_ROWS

  If #RC_Cvrg_Elect_ROWS = 1
    Let $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA = '(' || $GEXXX940_Coverage_Elect_Alias || ' In ('''     || &Gex_RC_Cvrg_Elect.COVERAGE_ELECT || ''''
    Let $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA = '(' || $GEXXX940_Coverage_Elect_Alias || ' Not In (''' || &Gex_RC_Cvrg_Elect.COVERAGE_ELECT || ''''
  Else
    Let $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA = $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA || ',''' || &Gex_RC_Cvrg_Elect.COVERAGE_ELECT  || ''''
    Let $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA = $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA || ',''' || &Gex_RC_Cvrg_Elect.COVERAGE_ELECT  || ''''
  End-If

From  PS_GEX_RC_CVG_ELEC GEX_RC_Cvrg_Elect

Where GEX_RC_Cvrg_Elect.OPRID                = [$OprID]
And   GEX_RC_Cvrg_Elect.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_Cvrg_Elect.COVERAGE_ELECT, ' ') != ' '

End-Select

  If #RC_Cvrg_Elect_ROWS = 0
    Let $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA = '1=1'
    Let $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA = '1=1'
  Else
    Let $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA = $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA || '))'
    Let $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA = $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA || '))'
    Let $GEXXX940_INCLUDE_COVERAGE_ELECT_DISPLAY_CRITERIA = $GEXXX940_INCLUDE_COVERAGE_ELECT_CRITERIA
    Let $GEXXX940_EXCLUDE_COVERAGE_ELECT_DISPLAY_CRITERIA = $GEXXX940_EXCLUDE_COVERAGE_ELECT_CRITERIA
  End-If

End-Procedure
