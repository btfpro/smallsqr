!**************************************************************************
!  GEXXX977:  Retrieve "Multiple" Coverage Code Run Controls and          *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX977_COVRG_CD_Alias                                                *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX977_INCLUDE_COVRG_CD_CRITERIA                                     *
!    $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA                                     *
!    $GEXXX977_INCLUDE_COVRG_CD_DISPLAY_CRITERIA                             *
!    $GEXXX977_EXCLUDE_COVRG_CD_DISPLAY_CRITERIA                             *
!                                                                         *                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX977_COVRG_CD_Alias = 'HB.COVRG_CD'                                *
!    Do GEXXX977-Select-COVRG_CD-Parameters                                  *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_HEALTH_BENEFIT HB                                                       *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX977_EXCLUDE_COVRG_CD_CRITERIA]                               *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	SXP		10/07/2009 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX977-Select-COVRG-CD-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_COVRG_CD_ROWS = 0
  Let $GEXXX977_INCLUDE_COVRG_CD_CRITERIA = ''
  Let $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA = ''
  Let $GEXXX977_INCLUDE_COVRG_CD_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX977_EXCLUDE_COVRG_CD_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_COVRGCD.COVRG_CD

  Add 1 To #GEX_RC_COVRG_CD_ROWS

  If #GEX_RC_COVRG_CD_ROWS = 1
    Let $GEXXX977_INCLUDE_COVRG_CD_CRITERIA = '(' || $GEXXX977_COVRG_CD_Alias || ' In ('''     || &GEX_RC_COVRGCD.COVRG_CD || ''''
    Let $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA = '(' || $GEXXX977_COVRG_CD_Alias || ' Not In (''' || &GEX_RC_COVRGCD.COVRG_CD || ''''
  Else
    Let $GEXXX977_INCLUDE_COVRG_CD_CRITERIA = $GEXXX977_INCLUDE_COVRG_CD_CRITERIA || ',''' || &GEX_RC_COVRGCD.COVRG_CD  || ''''
    Let $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA = $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA || ',''' || &GEX_RC_COVRGCD.COVRG_CD  || ''''
  End-If

From  PS_GEX_RC_COVRGCD GEX_RC_COVRGCD

Where GEX_RC_COVRGCD.OPRID                = [$OprID]
And   GEX_RC_COVRGCD.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_COVRGCD.COVRG_CD, ' ') != ' '

End-Select

  If #GEX_RC_COVRG_CD_ROWS = 0
    Let $GEXXX977_INCLUDE_COVRG_CD_CRITERIA = $GEXXX977_COVRG_CD_Alias || ' = ' || $GEXXX977_COVRG_CD_Alias
    Let $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA = $GEXXX977_COVRG_CD_Alias || ' = ' || $GEXXX977_COVRG_CD_Alias
  Else
    Let $GEXXX977_INCLUDE_COVRG_CD_CRITERIA = $GEXXX977_INCLUDE_COVRG_CD_CRITERIA || '))'
    Let $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA = $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA || '))'
    Let $GEXXX977_INCLUDE_COVRG_CD_DISPLAY_CRITERIA = $GEXXX977_INCLUDE_COVRG_CD_CRITERIA
    Let $GEXXX977_EXCLUDE_COVRG_CD_DISPLAY_CRITERIA = $GEXXX977_EXCLUDE_COVRG_CD_CRITERIA
  End-If

End-Procedure
