!***********************************************************************
! GEXPY092:  Update Bonus Table                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY092 :            Update Bonus Table                             *
!                                                                      *
! Narrative:            This SQR updates GEX_EMPL_BONUS for all        *
!                         employees who were paid a bonus.             *
!                                                                      *
! #Debugx Used:          paragraph trace                        *
!                        key variable values                    *
!                        data returned from sql calls           *
!                                                                      *
! SQL Tables:           job                                            *
!                       earnings_bal                                   *
!                       gex_empl_bonus                                 *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Annually                                       *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Bonus Award Date                             *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! JDH       07/14/99    Initial Creation                               *
!                                                                      *
! AXL       12/05/2000  Modified to satisfy new SQR 4 processing       *
!                       requirements.  Edit masks no longer valid when *
!                       converting from text to numeric using MOVE.    *
!                       Corrected a bug where pgm was using wrong date *
!                       from run ctl                                   *
!                                                                      *
! AXL       10/26/2001  Modified to update Bonus Table from            *
!                       PS_PAY_OTH_EARNS                               * 
!                                                                      *
! AXL       08/16/2002  Allow negative amts to come thru process       *
!                                                                      *
! AXL       01/28/2004  Modify the logic to determine year of bonus.   *
!                       Running after year-end was causing no rows to  *
!                       be processed.                                  *
!                                                                      *
! AXL       07/15/2005  Added a step to insert a row into Bonus Tbl    *
!                       when a 510 bonus has been paid out outside of  *
!                       of the normal bonus run.                       *
! ISDVNPK   08/15/2007  Commented OPTIMIZER_GOAL as part of 9.0 upgrade 
! GEX-TLL  07/01/2008   Inserted the GEX_BONUS_IND Field value ='X'    *
! ISDVDZK  08/10/2009   Added the code to change the insert logic to   *
!                       GEX_EMPL_BONUS as two new fields were added    * 
!                        in GEX_EMPL_BONUS                             *
! ISDVRKK  11/01/2010   Cleaned up code, added new fields to insert    *
!GEX-PY-ITG108847 ISDVAKG  10/21/2011	  Changed the logic to capture *
!			the bonus amounts from pay otheer earnings     *
!ISDVVXK  07/16/2014  GEXHR_848_E161362_BONUS2014 -Add effdt logic to  *
!                     gex_jobcode_tbl and added new columns to the     *
!                     insert bonus table proc			       *									 											 												 *	
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Landscape)

#define #column1    20		!Emplid
#define #column2    50		!Empl_Rcd#
#define #column3    80		!Deptid
#define #column4    100		!Bonus Amt

!***********************************************************************
Begin-Setup
!***********************************************************************
  Declare-Report Exception-Report
Printer-Type=HPLASERJET
    Layout=Default
  End-Declare

  Declare-Report Update-Report
Printer-Type=HPLASERJET
    Layout=Default
  End-Declare
End-Setup

!***********************************************************************
Begin-Report
!***********************************************************************
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report

!***********************************************************************
Begin-Heading 7 For-Reports=(Exception-Report)
!***********************************************************************
#debug9 Show 'Begin-Heading'
  Let $ReportID      =   'PY092E'
  Let $ReportTitle   =   'Update Bonus Table'
  Let $ReportTitle2  =   'Exception Report'
  Let $ReportTitle3  =   '(no corresponding bonus record found in gex_empl_bonus)'

  #Include 'stdhdg01.sqc'
  Print $ReportTitle2          (3,) Center
  Print $ReportTitle3          (4,) Center

  Print 'EMPLID'               (+2,{#column1})
  Print 'EMPL RCD#'            (+0,{#column2})
  Print 'DEPTID'	       (+0,{#column3})	
  Print '  BONUS AMOUNT'       (+0,{#column4})	

  Print '-'                    (+1,{#column1},9)  Fill
  Print '-'                    (+0,{#column2},9)  Fill
  Print '-'                    (+0,{#column3},10) Fill	
  Print '-'                    (+0,{#column4},14) Fill	
End-Heading

!***********************************************************************
Begin-Heading 7 For-Reports=(Update-Report)
!***********************************************************************
#debug9 Show 'Begin-Heading'
  Let $ReportID      =   'PY092U'
  Let $ReportTitle   =   'Update Bonus Table'
  Let $ReportTitle2  =   ''

  #Include 'stdhdg01.sqc'
  Print $ReportTitle2          (3,) Center

  Print 'EMPLID'               (+2,{#column1})
  Print 'EMPL RCD#'            (+0,{#column2})
  Print 'DEPTID'	       (+0,{#column3})		
  Print '  BONUS AMOUNT'       (+0,{#column4})		

  Print '-'                    (+1,{#column1},9)  Fill
  Print '-'                    (+0,{#column2},9)  Fill
  Print '-'                    (+0,{#column3},10) Fill	
  Print '-'                    (+0,{#column4},14) Fill	
End-Heading

!***********************************************************************
Begin-Procedure P100-Initialization
!***********************************************************************
#debug9 Show 'P100-Initialization'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPY092'
  Let $ReportTitle   =   'Update Bonus Table'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If
  
  Let $Bonus_Year = Substr($Bonus_Award_Dt,8,4)
  Let #Bonus_Year = trunc(to_number($Bonus_Year),0)
  
  Let $bonus_start = '01-SEP-'||$Bonus_Year	
  Let $bonus_end   = '31-DEC-'||$Bonus_Year	

  Show '$bonus_start: ' $bonus_start 
  Show '$bonus_end:   ' $bonus_end

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '

  Show '$Bonus_Award_Dt   = ' $Bonus_Award_Dt

End-Procedure

!***********************************************************************
Begin-Procedure P110-SQRW-Run-Controls
!***********************************************************************
#debug9 Show 'P110-SQRW-Run-Controls'
  Let $DateOK = 'N'
  While $DateOK <> 'Y'
    Do Ask-Bonus-Award-Date
  End-While
End-Procedure

!***********************************************************************
Begin-Procedure P120-Process-Scheduler-Run-Controls
!***********************************************************************
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXXX922-Select-Parameters
  Let $Bonus_Award_Dt   = $GEX_RC_PAY.ASOFDATE

  If Rtrim($Bonus_Award_Dt, ' ') = ''
    Let $Bonus_Award_Dt = $AsOfToday
  End-If
End-Procedure

!***********************************************************************
Begin-Procedure Ask-Bonus-Award-Date
!***********************************************************************
#debug9 Show 'Ask-Bonus-Award-Date'
  move 'Y' to $DateOK
  display ''
  display 'Enter the Bonus Award Date ({Prompt-Date}) for this report or press'
  input $Bonus_Award_Dt 'enter to default today''s date as the Bonus Award Date'
  if $Bonus_Award_Dt = ''
    move $AsOfToday to $Bonus_Award_Dt
  else
    move {PDelimiter} to $DDelimiter
    do Format-DateTime($Bonus_Award_Dt,$Bonus_Award_Dt,{Prompt-Mask},'','native')
  end-if
  do Validate-Native-Date($Bonus_Award_Dt,$DateOK)
  if $DateOK = 'N'
    display '**** This is an invalid date, please reenter ****'
  end-if
End-Procedure

!***********************************************************************
Begin-Procedure P200-Main-Process
!***********************************************************************
#debug9 Show 'P200-Main-Process'

Begin-Select
A.emplid
A.empl_rcd
B.deptid
A.deptid
b.jobcode
b.position_nbr									! GEX-PY-ITG108847  10/21/2011
!sum(C.oth_pay) &bonus_amt      ! ISDVRKK  11/01/2010
C.oth_pay

!  let #bonus_amt = &bonus_amt  ! ISDVRKK  11/01/2010
  let #bonus_amt = &C.oth_pay
  Do Get-Misc-Info

  Do Select-Bonus-Table         ! Check for existing row, with matching Bonus Amt
  if $found-bonus = 'N'
     IF #bonus_amt > 0
        Do P210-Update-Bonus-Table
        Do P220-Print-Record
     else
        do Select-Bonus-Table-Amt
        let #bonus_amt = #bonus_amt + #orig_bonus_amt
        Do P210-Update-Bonus-Table
        Do P220-Print-Record
     end-if
  else
     Do P220-Update-Bonus-Table ! Update Bonus Award Date
  end-if

FROM PS_PAY_CHECK A,
     PS_PAY_EARNINGS B,
     PS_PAY_OTH_EARNS C
WHERE B.COMPANY = A.COMPANY
AND B.PAYGROUP = A.PAYGROUP
AND B.PAY_END_DT = A.PAY_END_DT
AND B.OFF_CYCLE = A.OFF_CYCLE
AND B.PAGE_NUM = A.PAGE_NUM
AND B.LINE_NUM = A.LINE_NUM
AND B.SEPCHK = A.SEPCHK
AND C.COMPANY = B.COMPANY
AND C.PAYGROUP = B.PAYGROUP
AND C.PAY_END_DT = B.PAY_END_DT
AND C.OFF_CYCLE = B.OFF_CYCLE
AND C.PAGE_NUM = B.PAGE_NUM
AND C.LINE_NUM = B.LINE_NUM
AND C.SEPCHK = B.SEPCHK
AND C.ADDL_NBR = B.ADDL_NBR
AND A.PAY_END_DT > $bonus_start
AND A.PAY_END_DT < $bonus_end
AND C.ERNCD = '510' 
!Group By a.emplid, a.empl_rcd, b.deptid, a.deptid,b.jobcode  ! ISDVRKK  11/01/2010
Order By a.emplid, a.empl_rcd,C.oth_pay							! GEX-PY-ITG108847  10/21/2011

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Get-Misc-Info
!***********************************************************************
#debug9 Show 'Get-Misc-Info'
#debug8 Show 'Get-Misc-Info: '&a.emplid
#debug8 Show 'Get-Misc-Info: '&a.empl_rcd
#debug8 Show 'Get-Misc-Info: '$Bonus_Award_Dt

Begin-Select
ET.empl_type
N.NAME
ET.REPORTS_TO
ET.JOBCODE
ET.GRADE								! GEX-PY-ITG108847  10/21/2011

FROM PS_JOB ET, PS_PERSONAL_DATA N
  WHERE ET.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_JOB
        WHERE ET.EMPLID = EMPLID
          AND ET.EMPL_RCD = EMPL_RCD
          AND EFFDT <= $Bonus_Award_Dt)
    AND ET.EFFSEQ =
        (SELECT MAX(EFFSEQ) FROM PS_JOB
        WHERE ET.EMPLID = EMPLID
          AND ET.EMPL_RCD = EMPL_RCD
          AND ET.EFFDT = EFFDT)
     AND ET.EMPLID   = &a.emplid
     AND ET.EMPL_RCD = &a.empl_rcd
     AND ET.EMPLID   = N.emplid

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Select-Bonus-Table  ! ISDVRKK  11/01/2010
!***********************************************************************
#debug9 Show 'Select-Bonus-Table'
#debug8 Show 'Select-Bonus-Table: '&a.emplid
#debug8 Show 'Select-Bonus-Table: '&a.empl_rcd
#debug8 Show 'Select-Bonus-Table: '&a.deptid
#debug8 Show 'Select-Bonus-Table: '&b.deptid
#debug8 Show 'Select-Bonus-Table: '&b.jobcode
#debug8 Show 'Select-Bonus-Table: '#Bonus_Year
#debug8 Show 'Select-Bonus-Table: '#bonus_amt

let $found-bonus = 'N'

Begin-Select
'X'

   let $found-bonus = 'Y'

FROM ps_gex_empl_bonus
    Where  emplid             = &a.emplid	
    And    empl_rcd           = &a.empl_rcd	
    !And    deptid	          = &a.deptid							! GEX-PY-ITG108847  10/21/2011
    !And    budget_deptid	    = &b.deptid							! GEX-PY-ITG108847  10/21/2011
    !And    jobcode      	    = &b.jobcode						! GEX-PY-ITG108847  10/21/2011
    And    gex_bonus_year     = #Bonus_Year	
    And    gex_fnal_bonus_amt = #bonus_amt
ORDER BY GEX_FNAL_BONUS_AMT    	

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Select-Bonus-Table-Amt  ! ISDVRKK  11/01/2010
!***********************************************************************
#debug9 Show 'Select-Bonus-Table-Amt'
#debug8 Show 'Select-Bonus-Table-Amt: '&a.emplid
#debug8 Show 'Select-Bonus-Table-Amt: '&a.empl_rcd
#debug8 Show 'Select-Bonus-Table-Amt: '&a.deptid
#debug8 Show 'Select-Bonus-Table-Amt: '&b.deptid
#debug8 Show 'Select-Bonus-Table-Amt: '&b.jobcode
#debug8 Show 'Select-Bonus-Table-Amt: '#Bonus_Year
#debug8 Show 'Select-Bonus-Table-Amt: '#bonus_amt

Begin-Select
gex_bonus_amt

  let #orig_bonus_amt = &gex_bonus_amt

FROM ps_gex_empl_bonus
    Where  emplid             = &a.emplid	
    And    empl_rcd           = &a.empl_rcd	
    And    deptid	            = &a.deptid	
    And    budget_deptid	    = &b.deptid	
    And    jobcode      	    = &b.jobcode	
    And    gex_bonus_year     = #Bonus_Year	

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure P210-Update-Bonus-Table
!***********************************************************************
#debug9 Show 'P210-Update-Bonus-Table'
#debug8 Show 'P210-Update-Bonus-Table: '&a.emplid
#debug8 Show 'P210-Update-Bonus-Table: '&a.empl_rcd
#debug8 Show 'P210-Update-Bonus-Table: '&a.deptid
#debug8 Show 'P210-Update-Bonus-Table: '&b.deptid
#debug8 Show 'P210-Update-Bonus-Table: '&b.jobcode
#debug8 Show 'P210-Update-Bonus-Table: '#Bonus_Year

  Begin-SQL On-Error=P290-SQL-Error('Update ps_gex_empl_bonus')
    Update ps_gex_empl_bonus
    Set    gex_fnal_bonus_amt = #bonus_amt,	
           gex_bonus_award_dt = $Bonus_Award_Dt,
           COMMENTS 		  		= 'Adjustments Made through GEXPY092'             ! GEX-PY-ITG108847  10/21/2011
    Where  emplid             = &a.emplid	
    And    empl_rcd           = &a.empl_rcd	
    And    deptid	            = &a.deptid	  ! ISDVRKK  11/01/2010
    And    budget_deptid	    = &b.deptid	  ! ISDVRKK  11/01/2010
    And    jobcode      	    = &b.jobcode	! ISDVRKK  11/01/2010
    And    gex_bonus_year     = #Bonus_Year	
  End-SQL

  Add #sql-count To #rows-updated

  If #sql-count = 0
    Use-Report Exception-Report
    do Insert-Into-Bonus-Table
  Else
    Let $Data-Found-Flag = 'Y'
    Use-Report Update-Report
  End-If
End-Procedure

!***********************************************************************
Begin-Procedure P220-Update-Bonus-Table
!***********************************************************************
#debug9 Show 'P220-Update-Bonus-Table'
#debug8 Show 'P220-Update-Bonus-Table: '&a.emplid
#debug8 Show 'P220-Update-Bonus-Table: '&a.empl_rcd
#debug8 Show 'P220-Update-Bonus-Table: '&a.deptid
#debug8 Show 'P220-Update-Bonus-Table: '&b.deptid
#debug8 Show 'P220-Update-Bonus-Table: '&b.jobcode
#debug8 Show 'P220-Update-Bonus-Table: '#Bonus_Year

  Begin-SQL On-Error=P290-SQL-Error('Update ps_gex_empl_bonus award date')
    Update ps_gex_empl_bonus
    Set gex_bonus_award_dt = $Bonus_Award_Dt
    Where  emplid             = &a.emplid	
    And    empl_rcd           = &a.empl_rcd	
    And    deptid	            = &a.deptid	  
    And    budget_deptid	    = &b.deptid	  
    And    jobcode      	    = &b.jobcode	
    And    gex_bonus_year     = #Bonus_Year	
    And    gex_fnal_bonus_amt = #bonus_amt	
  End-SQL

End-Procedure

!***********************************************************************
Begin-Procedure Insert-Into-Bonus-Table
!***********************************************************************
#debug9 Show 'Insert-Into-Bonus-Table'
! ISDVRKK  11/01/2010   Cleaned up code, added new fields to insert    *

Begin-SQL On-Error=P290-SQL-Error('Insert-Into-Bonus-Table')
insert into ps_gex_empl_bonus
       (emplid,
        empl_rcd,
        gex_bonus_year,
        deptid,
        empl_type,
        budget_deptid,
        jobcode,
        POSITION_NBR,											! GEX-PY-ITG108847  10/21/2011
        name,
        annual_rt,
        gex_min_bonus_pct,
        gex_tar_bonus_pct,
        gex_max_bonus_pct,
        gex_empl_override,
        gex_csi_adj_pct,
        GEX_BONUS_PCT_USED,								! GEX-PY-ITG108847  10/21/2011
        GEX_BONUS_PCT2_USE,  !ISDVVXK  GEXHR_848_E161362_BONUS2014
        gex_bonus_status,
        gex_bonus_amt,
        gex_bonus_amt2, !ISDVVXK  GEXHR_848_E161362_BONUS2014
        gex_fnal_bonus_amt,
        GEX_BONUS_GROUP,									! GEX-PY-ITG108847  10/21/2011
        gex_bonus_award_dt,
        gex_bonus_ind,
        reports_to,
        comments_by_name,
        STD_HOURS,												! GEX-PY-ITG108847  10/21/2011	
        GRADE,														! GEX-PY-ITG108847  10/21/2011
        hourly_rt,
        gex_weekly_rt,
        gex_weekly_rt_1_5,
        gex_weekly_rt_2,
        submitted,
        submitted_dt,
        approver_id,
        approved,
        approvedttm,
        processed,
        process_dt,
        complete_flag,
        process_name,
        comments)
values (&a.emplid,                           !   EMPLID
        &a.empl_rcd,                         !   EMPL_RCD
        #Bonus_Year,                         !   GEX_BONUS_YEAR
        &a.deptid,                           !   DEPTID
        &et.empl_type,                       !   EMPL_TYPE
        &b.deptid,                           !   BUDGET_DEPTID
        &b.jobcode,                          !   JOBCODE
        &b.position_nbr,										 !	POSITION_NBR,
        &n.name,                             !   NAME
        0,                                   !   ANNUAL_RT
        0,                                   !   GEX_MIN_BONUS_PCT
        0,                                   !   GEX_TAR_BONUS_PCT
        0,                                   !   GEX_MAX_BONUS_PCT
        'N',                                 !   GEX_EMPL_OVERRIDE
        0,                                   !   GEX_CSI_ADJ_PCT
        0,																	 !	GEX_BONUS_PCT_USED
        '4',                                 !   GEX_BONUS_STATUS
        0,                                   !   GEX_BONUS_AMT
        #bonus_amt,                          !   GEX_FNAL_BONUS_AMT
        ' ',																 !	GEX_BONUS_GROUP,
        $Bonus_Award_Dt,                     !   GEX_BONUS_AWARD_DT
        ' ',                                 !   GEX_BONUS_IND
        &et.reports_to,                      !   REPORTS_TO
        ' ',                                 !   COMMENTS_BY_NAME
        0,																	 !	STD_HOURS
        &et.grade,													 !	GRADE
        0,                                   !   HOURLY_RT
        0,                                   !   GEX_WEEKLY_RT
        0,                                   !   GEX_WEEKLY_RT_1_5
        0,                                   !   GEX_WEEKLY_RT_2
        'N',                                 !   SUBMITTED
        '',                                  !   SUBMITTED_DT
        ' ',                                 !   APPROVER_ID
        'N',                                 !   APPROVED
        '',                                  !   APPROVEDTTM
        'N',                                 !   PROCESSED
        '',                                  !   PROCESS_DT
        'X',                                 !   COMPLETE_FLAG
        'GEXPY092',                          !   PROCESS_NAME !ISDVVXK  GEXHR_848_E161362_BONUS2014.
        'Inserted by GEXPY092')              !   COMMENTS
 End-Sql
 
End-Procedure
! ISDVRKK  11/01/2010   Cleaned up code, added new fields to insert    *

!***********************************************************************
Begin-Procedure P220-Print-Record
!***********************************************************************
#debug9 Show 'P220-Print-Record'
  Print &a.emplid        (+1,{#column1})
  Print &a.empl_rcd     (+0,{#column2})
  Print &b.deptid        (+0,{#column3})  
  Print #bonus_amt       (+0,{#column4})  Edit 999,999,990.00
End-Procedure

!***********************************************************************
Begin-Procedure P290-SQL-Error($Action)
!***********************************************************************
#debug9 Show 'P290-SQL-Error'
  Show $Action ' failed'
  Show $_sql-error
  Stop
End-Procedure

!***********************************************************************
Begin-Procedure P300-Finalization
!***********************************************************************
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Use-Report Update-Report
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  Else
    Move #rows-updated To $rows-updated 888,888,888
    Show $rows-updated ' rows updated in ps_gex_empl_bonus table'
  End-If

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Procedure


#include 'gexxx922.sqc'  !Get pay single row run control
#include 'validdt.sqc'   !Validate date routine
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
