!***********************************************************************
! GEXPY206:  COD - Eligibility Changes for Union Seniority Date        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY201 :            COD - Eligibility Chgs for Union Seniority Dt  *
!                                                                      *
! Narrative:            This SQR will list employees who have changed  *
!                       to a different union cvde after 90 days of the *
!                       change.  The Union Seniority date will be      *
!                       manually set back to  the original entry date  *
!                       after the employee has served their 90-day     *
!                       waiting period.  This report will identify     *
!                       those employees who are eligible to have the   *
!                       union seniority date changed back.  Personnel  *
!                       managers at each store will manually make the  *
!                       date changes, when applicable.                 *
!                                                                      *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           personal_data                                  *
!                       jobcode_tbl                                    *
!                       job                                            *
!                       job_labor                                      *
!                                                                      *
! Written by:           C. Haver                                       *
!                                                                      *
! Normally Run:         Weekly - Sundays with the GEXPYSAD job         *
!                                                                      *
!                                                                      *
! Control Parms:        COMPANY                                        *
!                       PAYGROUP                                       *
!                       UNION CODE                                     *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE       DESCRIPTION OF THE CHANGE                       *
!***********************************************************************
! CJH       11/19/2000 Initial Creation                                *
! ISDVNPK   08/17/2007 Upgrade 8.3 - 9.0 Added PS_PER_ORG_INST for ORIG_HIRE_DT
! VENDAXG   05/06/2009 Modified for ITG#60288 Position Number Changes  *
! GEXPY_SEC_REENG Vahini Katta 09/18/2016
! Changes realted to SSN conversion
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup32.sqc'

  #define #x000_col_selection_criteria_a   2
  #define #x000_col_selection_criteria_b   5

  #define #col_emplid			1
  #define #col_name			13
  #define #col_deptid			40
  #define #col_acct_cd			51
  #define #col_jobtitle			64
  #define #col_orig_hire_dt		96
  #define #col_union_cd_c   		115
  #define #col_sen_dt_c			127
  #define #col_union_cd_p		148
  #define #col_sen_dt_p			158

begin-heading 8
#debug9 show 'begin-heading  for-reports=(Seniority)'
  move 'COD - Eligibility for Changes to Union Seniority Date' to $ReportTitle
  #Include 'stdhdg01.sqc'
  
  Print 'Current'              (6,{#col_union_cd_c})    
  PRint 'Current'	       (,{#col_sen_dt_c})
  Print 'Previous'             (,{#col_union_cd_p})
  Print 'Previous'             (,{#col_sen_dt_p})

  Print 'Emplid'               (7,{#col_emplid})
  Print 'Name'                 (,{#col_name})
  Print 'Deptid'               (,{#col_deptid})
  Print 'Acct Cd'	       (,{#col_acct_cd})
  Print 'Job Title'            (,{#col_jobtitle})
  Print 'Orig Hire Dt'         (,{#col_orig_hire_dt})
  Print 'Union Cd'             (,{#col_union_cd_c})     
  Print 'Seniority Dt'         (,{#col_sen_dt_c})
  Print 'Union Cd'             (,{#col_union_cd_p})
  Print 'Seniority Dt'         (,{#col_sen_dt_p})

  Print '-'                    (8,1,176) fill  

end-heading


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPY206'
  Let $ReportTitle   =   'COD - Eligibility for Changes to Union Seniority Date'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '


  Let $GEXXX901_Company_Alias  = 'D.COMPANY' 
  Let $GEXXX901_Paygroup_Alias = 'D.PAYGROUP'    
  Let $GEXXX913_Union_cd_Alias = 'C.UNION_CD'

  If $prcs_process_instance = ''

    let $Prcs_OPRID = 'SQRW/pssqr'
    let $PRCS_RUN_CNTL_ID = 'manual'

  Display 'Enter Company/Paygroup or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''')'
      Else
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || $In-Paygroup || ''')'
      End-If
      Add 1 To #GEX_RC_PAYGROUP_ROWS
      If #GEX_RC_PAYGROUP_ROWS = 1
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While 

 If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If


 Display 'Enter Union Code or leave blank to exit.'
  While 1=1
    Input $In-Union-Cd Maxlen=3 'Union Code'
    Uppercase $In-Union-Cd
    If Rtrim($In-Union-Cd, ' ') = ''
      Break
    Else
        Let $Include_Condition = '(' || $GEXXX913_Union_cd_Alias || ' = '''  || $In-Union-cd || ''')'
      Add 1 To #GEX_RC_UNION_CD_ROWS
      If #GEX_RC_UNION_CD_ROWS = 1
        Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While 

 If #GEX_RC_UNION_CD_ROWS = 0
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA || ')'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  End-If



  Else
   Do GEXXX922-Select-Parameters
   Do GEXXX901-Select-Company-Paygroup-Parameters    
   Do GEXXX913-Select-UNION-CD-Parameters
   
  End-If


  let $X000_ORDER_BY = 'D.DEPTID, D.ACCT_CD, PER.NAME'


  show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA      = ' $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA   
  show '$GEXXX913_INCLUDE_UNION_CD_CRITERIA              = ' $GEXXX913_INCLUDE_UNION_CD_CRITERIA


  print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,{#x000_col_selection_criteria_a})
  print '$Prcs_OPRID          ='                                  (+1,{#x000_col_selection_criteria_b})
  print $Prcs_OPRID                                               (0,+2)
  print '$PRCS_RUN_CNTL_ID    ='                                  (+1,{#x000_col_selection_criteria_b})
  print $PRCS_RUN_CNTL_ID                                         (0,+2)

  print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,{#x000_col_selection_criteria_a})
  print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  print '$GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA         ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA	          (0,+2)

  print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,{#x000_col_selection_criteria_a})
  print '$X000_ORDER_BY                                      ='   (+1,{#x000_col_selection_criteria_b})
  print $X000_ORDER_BY 	(0,+2)

  let #page-count = 0
  NEW-PAGE

End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process

 
 do get-pay-end

! display $pay_begin_dt
! display $paY_end_dt

! display $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
! display $GEXXX913_INCLUDE_UNION_CD_CRITERIA
! display $X000_ORDER_BY

 let #ee_count = 0
 let #ee_total = 0

Begin-SELECT On-Error=P290-SQL-Error('P200-Main-process')
D.EMPLID
D.DEPTID  ()  On-Break  Level=1
                        Print=Never
                        After=Print-Deptid-Totals
D.ACCT_CD
INST.ORIG_HIRE_DT   ! PER.ORIG_HIRE_DT  !ISDVNPK 8/17/2007 Modified for Upgrade 9.0 
PER.NAME &name
C.UNION_CD
C.UNION_SENIORITY_DT
P.UNION_CD
P.UNION_SENIORITY_DT
D.JOBCODE
D.POSITION_NBR !Vendaxg Added for ITG#60288 on 05/06/2009

 display &D.EMPLID
  Let $Position_Nbr = Ltrim(Rtrim(&D.POSITION_NBR,' '),' ') !Vendaxg Added for ITG#60288 on 05/06/2009
  !Vendaxg Added for ITG#60288 on 05/06/2009  - Begin
  Let $PosName = ''
  Do Get-Position-Title	
  If $PosName = ''
  	do get-jobcode-descr
  End-If
  !Vendaxg Added for ITG#60288 on 05/06/2009  - End
! print the report rows
  do print-report-row

! count the emplid
  let #ee_count = #ee_count + 1
  let #ee_total = #ee_total + 1

FROM PS_JOB_LABOR C,
     PS_JOB_LABOR P,
     PS_JOB D,
     PS_PERSONAL_DATA PER,
     PS_PER_ORG_INST INST  !ISDVNPK 8/17/2007 Modified for Upgrade 9.0 

WHERE D.EMPLID = PER.EMPLID

  AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
  AND [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]

  AND C.EMPLID = D.EMPLID
  AND C.EMPL_RCD = D.EMPL_RCD

  AND P.EMPLID = D.EMPLID
  AND P.EMPL_RCD = D.EMPL_RCD
  AND INST.EMPLID = D.EMPLID  !ISDVNPK 8/17/2007 Modified for Upgrade 9.0 
  AND INST.ORG_INSTANCE_ERN  = D.EMPL_RCD	!GEX-MXT added for PER_ORG_INST join
  AND D.EFFDT = (select max(dd.effdt) from ps_job dd
                 where dd.emplid = d.emplid
                   and dd.empl_rcd = d.empl_rcd
                   and dd.effdt <= $pay_end_dt)
  AND D.EFFSEQ = (select max(ddd.effseq) from ps_job ddd
                  where ddd.emplid = d.emplid
                    and ddd.empl_rcd = d.empl_rcd
                    and ddd.effdt = d.effdt)

  AND C.EFFDT = (select max(cc.effdt) from ps_job_labor cc
                 where cc.emplid = c.emplid
                   and cc.empl_rcd = c.empl_rcd
                   and cc.eff_status = 'A'
                   and cc.effdt <= $pay_end_dt)
  AND C.EFFSEQ = (select max(ccc.effseq) from ps_job_labor ccc
                  where ccc.emplid = c.emplid
                    and ccc.empl_rcd = c.empl_rcd
                    and ccc.eff_status = 'A'
                    and ccc.effdt = c.effdt)

  AND C.EFFDT between $pay_begin_dt and $pay_end_dt   ! find new job_labor rows in the week
 
  AND P.EFFDT = (select max(pp.effdt) from ps_job_labor pp
                 where pp.emplid = p.emplid
                   and pp.empl_rcd = p.empl_rcd
                   and pp.eff_status = 'A'
                   and pp.union_cd <> c.union_cd      ! find row for previous union cd
                   and pp.effdt <= c.effdt)

  AND P.EFFSEQ = (select max(ppp.effseq) from ps_job_labor ppp
                  where ppp.emplid = p.emplid
                    and ppp.empl_rcd = p.empl_rcd
                    and ppp.eff_status = 'A'
                    and ppp.effdt = p.effdt)

  ORDER BY [$X000_ORDER_BY]

End-SELECT

  print 'Grand Total' 		(+1,{#col_emplid})
  print #ee_total		(,{#col_name}) edit 9999999


End-Procedure

!*********************************************************************
!Gets the Pay End Date to process for
!*********************************************************************
begin-procedure Get-Pay-End
 let $Found = 'N'
 
  
  do convert-to-dtu-date($AsOfToday,$today_dtu)
  do dtu-subtract-days($today_dtu,90,$begin_dt_dtu)
  do convert-from-dtu-date($begin_dt_dtu,$begin_Dt)
 display $begin_dt
 

 
begin-select loops=1 On-Error=P290-SQL-Error('get-pay-end')

Y1.PAY_END_DT 
Y1.pay_begin_dt 

  move 'Y' to $Found
  move &Y1.PAY_END_DT to $pay_end_dt
  move &Y1.PAY_BEGIN_DT TO $pay_begin_dt


FROM PS_PAY_CALENDAR Y1
WHERE Y1.PAY_END_DT = (SELECT MAX(PAY_END_DT)
                         FROM PS_PAY_CALENDAR
                        WHERE PAY_END_DT >= $begin_dt
                          AND PAY_BEGIN_DT <= $begin_dt)
  AND Y1.RUN_ID > ' '

end-select

  if $Found = 'N'
    display 'Error --  Pay Calendar not found or not setup for the Run ID.'
    print 'Error --  Pay Calendar not found or not setup for the Run ID.' (+1,1,60)
  else
    display 'Processing for Pay Begin Dt/Pay End Date: ' noline
    display $pay_begin_dt noline
    display '/' noline
    display $pay_end_dt
  end-if



end-procedure
!**********************************************************************
begin-procedure get-jobcode-descr
begin-select
JT.DESCR

	Let $PosName = Ltrim(Rtrim(&JT.DESCR,' '),' ')	!Vendaxg Added for ITG#60288 on 05/06/2009

from PS_JOBCODE_TBL JT
where JT.JOBCODE = &D.JOBCODE
  and JT.EFFDT = (select max(effdt) from ps_jobcode_tbl
         where jobcode = JT.jobcode
           and eff_status = 'A'
           and effdt <= SYSDATE)
end-select
end-procedure
!**********************************************************************
begin-procedure Print-Report-row
#debug9 Show 'Print-Report-row'

  print &D.EMPLID		(+1,{#col_emplid}) !edit xxx-xx-xxxx !GEXPY_SEC_REENG Vahini Katta 09/18/2016
  print &name			(,{#col_name})
  print &D.DEPTID               (,{#col_deptid})
  print &D.ACCT_CD              (,{#col_acct_cd})
  !Vendaxg Modified for ITG#60288 on 05/06/2009 - Begin
  !print &JT.DESCR               (,{#col_jobtitle})
  print $PosName             (,{#col_jobtitle})
  !Vendaxg Modified for ITG#60288 on 05/06/2009 - Begin
  print &INST.ORIG_HIRE_DT      (,{#col_orig_hire_dt})  ! &PER.ORIG_HIRE_DT  !ISDVNPK 8/17/2007 Modified for Upgrade 9.0 
  print &C.UNION_CD             (,{#col_union_cd_c})
  print &C.UNION_SENIORITY_DT   (,{#col_sen_dt_c})
  print &P.UNION_CD             (,{#col_union_cd_p})
  print &P.UNION_SENIORITY_DT   (,{#col_sen_dt_p})


end-procedure
!**********************************************************************
begin-procedure Print-Deptid-Totals

  print 'Dept Total' 		(+1,{#col_emplid})
  print #ee_count		(,{#col_name}) edit 9999999
  let #ee_count = 0

end-procedure
!**********************************************************************
Begin-Procedure P290-SQL-Error($Action)
#debug9 Show 'P290-SQL-Error'
  Show $Action ' failed'
  Show 'SQL Error:  '  $_sql-error
  Stop
End-Procedure

!*********************************************************************
Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Procedure

!********************************************************************
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx901.sqc'  !
#include 'gexxx950.sqc'  !
#include 'gexxx913.sqc'  !
#include 'gexxx924.sqc'  !
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
#include 'getposnm.sqc' !Vendaxg added for ITG#60288 on  05/06/2009