!**************************************************************************
!  GEXXX970:  Retrieve "Multiple" Deptid/Jobcode Run Controls and         *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX970_Deptid_Alias                                               *
!    $GEXXX970_Jobcode_Alias                                              *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA                            *
!    $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA                            *
!    $GEXXX970_INCLUDE_DEPTID_JOBCODE_DISPLAY_CRITERIA                    *
!    $GEXXX970_EXCLUDE_DEPTID_JOBCODE_DISPLAY_CRITERIA                    *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX970_Deptid_Alias  = 'J.DEPTID'                             *
!    Let $GEXXX970_Paygroup_Alias = 'J.JOBCODE'                           *
!    Do GEXXX970-Select-Deptid-Jobcode-Parameters                         *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA]                    *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE         *
!**************************************************************************
!	AXL 		12/02/2002 	Initial creation.                 *
!                                       New run control to select certain *
!                                       Deptid/Jobcode combinations for   *
!                                       PTY & PTW processing.             *
!**************************************************************************

Begin-Procedure GEXXX970-Select-Deptid-Jobcode-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DPTJC_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA = ''
  Let $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA = ''
  Let $GEXXX970_INCLUDE_DEPTID_JOBCODE_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX970_EXCLUDE_DEPTID_JOBCODE_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_DPTJC.Deptid
GEX_RC_DPTJC.JOBCODE

  Add 1 To #GEX_RC_DPTJC_ROWS

  Let $Include_Condition = '(' || $GEXXX970_Deptid_Alias  ||
                           '||'                           ||
                            $GEXXX970_Jobcode_Alias       ||
                            ' = '''  ||  &GEX_RC_DPTJC.Deptid  ||
                            &GEX_RC_DPTJC.JOBCODE || ''')' 
  
  Let $Exclude_Condition = '(' || $GEXXX970_Deptid_Alias  ||
                           '||'                           ||
                            $GEXXX970_Jobcode_Alias       ||
                            ' != '''  ||  &GEX_RC_DPTJC.Deptid  ||
                            &GEX_RC_DPTJC.JOBCODE || ''')' 

  If #GEX_RC_DPTJC_ROWS = 1
    Let $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA = '(' || $Include_Condition
    Let $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA = $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA = $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_DPTJC GEX_RC_DPTJC

Where GEX_RC_DPTJC.OPRID                 = [$OprID]
And   GEX_RC_DPTJC.RUN_CNTL_ID           = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DPTJC.DEPTID,  ' ') != ' '
And   Rtrim(GEX_RC_DPTJC.JOBCODE, ' ') != ' '

End-Select

  If #GEX_RC_DPTJC_ROWS = 0

    Let $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA = $GEXXX970_Deptid_Alias  || '||' ||
                                                    $GEXXX970_Jobcode_Alias ||
                                                    ' = ' ||
                                                    $GEXXX970_Deptid_Alias  || '||' ||
                                                    $GEXXX970_Jobcode_Alias 

    Let $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA = '1=1'
    
  Else
    Let $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA = $GEXXX970_INCLUDE_Deptid_JOBCODE_CRITERIA || ')'
    Let $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA = $GEXXX970_EXCLUDE_Deptid_JOBCODE_CRITERIA || ')'
    Let $GEXXX970_INCLUDE_DEPTID_JOBCODE_DISPLAY_CRITERIA = $GEXXX970_INCLUDE_DEPTID_JOBCODE_CRITERIA
    Let $GEXXX970_EXCLUDE_DEPTID_JOBCODE_DISPLAY_CRITERIA = $GEXXX970_EXCLUDE_DEPTID_JOBCODE_CRITERIA
  End-If

End-Procedure
