!**************************************************************************
!  GEXXX907:  Retrieve "Multiple" Company Division Codes Run Control and * 
!             Return a Formatted Where-Clause                             *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX907_DIV_CD_Alias                                               *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX907_INCLUDE_DIV_CD_CRITERIA                                    *
!    $GEXXX907_EXCLUDE_DIV_CD_CRITERIA                                    *
!    $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                            *
!    $GEXXX907_EXCLUDE_DIV_CD_DISPLAY_CRITERIA                            *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX907_DIV_CD_Alias = 'J.GEX_COMPANY_DIV_CD'                  *
!    Do GEXXX907-Select-DIV-CD-Parameters                                 *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]                            *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX907-Select-DIV-CD-Parameters

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DIV_CD_ROWS = 0
  Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = ''
  Let $GEXXX907_EXCLUDE_DIV_CD_CRITERIA = ''
  Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX907_EXCLUDE_DIV_CD_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_DIV_CD.GEX_COMPANY_DIV_CD

  Add 1 To #GEX_RC_DIV_CD_ROWS

  If #GEX_RC_DIV_CD_ROWS = 1
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = '(' || $GEXXX907_DIV_CD_Alias || ' In ('''     || &GEX_RC_DIV_CD.GEX_COMPANY_DIV_CD || ''''
    Let $GEXXX907_EXCLUDE_DIV_CD_CRITERIA = '(' || $GEXXX907_DIV_CD_Alias || ' Not In (''' || &GEX_RC_DIV_CD.GEX_COMPANY_DIV_CD || ''''
  Else
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA || ',''' || &GEX_RC_DIV_CD.GEX_COMPANY_DIV_CD  || ''''
    Let $GEXXX907_EXCLUDE_DIV_CD_CRITERIA = $GEXXX907_EXCLUDE_DIV_CD_CRITERIA || ',''' || &GEX_RC_DIV_CD.GEX_COMPANY_DIV_CD  || ''''
  End-If

From  PS_GEX_RC_DIV_CD GEX_RC_DIV_CD

Where GEX_RC_DIV_CD.OPRID               = [$OprID]
And   GEX_RC_DIV_CD.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DIV_CD.GEX_COMPANY_DIV_CD, ' ') != ' ' 
End-Select

  If #GEX_RC_DIV_CD_ROWS = 0
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = $GEXXX907_DIV_CD_Alias || ' = ' || $GEXXX907_DIV_CD_Alias
    Let $GEXXX907_EXCLUDE_DIV_CD_CRITERIA = $GEXXX907_DIV_CD_Alias || ' = ' || $GEXXX907_DIV_CD_Alias
  Else
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA || '))'
    Let $GEXXX907_EXCLUDE_DIV_CD_CRITERIA = $GEXXX907_EXCLUDE_DIV_CD_CRITERIA || '))'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA
    Let $GEXXX907_EXCLUDE_DIV_CD_DISPLAY_CRITERIA = $GEXXX907_EXCLUDE_DIV_CD_CRITERIA
  End-If

End-Procedure
