!***********************************************************************
!  GEXHR015:  GENDER AND ETHNIC GROUP DISTRIBUTION                     *
!             BY COMPANY AND DEPARTMENT                                *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXHR015:             Gender and Ethnic Group Distribution           *
!                         by Company and Department                    *
!                                                                      *        
! Narrative:		This program will generate a report that       *
!                       prints gender and ethnic distribution by       *
!                       company and department.  The report will list  *
!                       the number of males and females in each        *
!                       department of each company.  The report will   *
!                       also show the distribution of both male and    *
!                       female under five ethnic groups, namely Black, *
!                       White, Hisp, Asian, and Native.                *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values              *
!			      #debug7 data returned from sql calls     *
!                                                                      *  
! SQL Tables:		gex_dept_tbl                                   *
!                       personal_data                                  *
!                       job                                            *
!                                                                      *
! Written by:		Jim Hutchison  02/11/98                        *       
!                                                                      *
! Normally Run:		On Demand                                      *
!                                                                      *
! Control Parms:		FOR WHERE CLAUSE:                      *
!				  Company Division Code                *
!				  Company  	                       *
!				  Paygroup  	                       *
!				  Deptid	                       *
!				  Employee Status	               *
!				  Account Code                         *
!				  As Of Date                           *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE      *
!***********************************************************************
!	JDH		02/11/98	Initial Creation               *
!                                                                      *
!	AXL		09/05/2000      Added PS_DIVERSITY to main     *
!                                       join logic. ETHNIC_GROUP no    *
!                                       longer on PS_PERSONAL_DATA.    * 
!                                                                      *
!       AXL             07/25/2001      Address a run control problem  *
!                                       with Manager_Level. Hard-code  *
!                                       active EMPL_STATUS' into logic.*
!       VENDVXR         08/15/2007      Upgrade 9.0 Changes            *
!                                       PS_DIVERSITY.ETHINIC_GROUP changed    *
!                                       stored in PS_DIVERS_ETHNIC.ETHNIC_GRP_CD *
!                                       Mapping Values: WHITE = 1      *
!                                                       BLACK = 2      *
!       GEX-MXT         03/31/2008      Ethnicity changes 
!       ISDVMXD         05/05/2009      Update sqr to retrieve Job Title 
!                                       from Position Data ITG#60309   *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
!#Include 'setup31a.sqc'   !Printer and page-size initialization
#Include 'setup32a.sqc'   !Printer and page-size initialization
!#Include 'setup01.sqc'   !Printer and page-size initialization

 declare-report report1
Printer-Type=HPLASERJET
     layout=DEFAULT
   end-declare

   declare-report report2
Printer-Type=HPLASERJET
     layout=DEFAULT
   end-declare

end-setup

!***********************************************************************
begin-report
!***********************************************************************
#debug9 Show 'begin-report'

  do Init-DateTime           
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Init-Report
  do Process-Main
  do Process-Main2

  do EOJ
  do Stdapi-Term             !stdapi.sqc

end-report

!***********************************************************************
begin-heading 9 for-reports=(report1)
!***********************************************************************
#debug9 Show 'begin-heading'
  
  LET $REPORTID = 'HR015DIV'

  #Include 'stdhdg01.sqc'

  Let $temp_out = 'Report as of ' || $WHERE_SELECT_ASOFDATE 
  Print $temp_out         (3,)  center

  position (+1)
  
  print 'Division'		(+1,1)
  print	$div_cd			(,+2)

  PRINT 'WHITE'                 (+2,50)
  PRINT 'BLACK'                 (,70)
  PRINT 'OTHER'                 (,90)
  
  PRINT 'JOB TITLE'          	(+1,16)
  PRINT 'FEMALE'             	(,45)
  PRINT 'MALE'               	(,55)
  PRINT 'FEMALE'             	(,65)
  PRINT 'MALE'               	(,75)
  PRINT 'FEMALE'             	(,85)
  PRINT 'MALE'               	(,95)
  PRINT 'TOTAL'              	(,105)
  print '-'                  	(+1,1,110)  Fill
     
end-heading

!***********************************************************************
begin-heading 9 for-reports=(report2)
!***********************************************************************
#debug9 Show 'begin-heading'

  LET $REPORTID = 'HR015DPT'

  #Include 'stdhdg01.sqc'

  Let $temp_out = 'Report as of ' || $WHERE_SELECT_ASOFDATE 
  Print $temp_out         (3,)  center

  position (+1)
  
  print 'Division'		(+1,1)
  print	$div_cd			(,+2)
  print	$deptid			(+1,1)

  PRINT 'WHITE'                	(+2,50)
  PRINT 'BLACK'                 (,70)
  PRINT 'OTHER'                 (,90)
  
  PRINT 'JOB TITLE'             (+1,16)
  PRINT 'FEMALE'                (,45)
  PRINT 'MALE'                 	(,55)
  PRINT 'FEMALE'                (,65)
  PRINT 'MALE'                  (,75)
  PRINT 'FEMALE'                (,85)
  PRINT 'MALE'                  (,95)
  PRINT 'TOTAL'                 (,105)
  print '-'                     (+1,1,110)  Fill
     
end-heading

!***********************************************************************
begin-procedure Init-Report
!***********************************************************************
#debug9 Show 'Init-Report'

  do Initialize-Div_Cd-Totals
  do Initialize-Deptid-Totals
  do Initialize-Descr-Totals

  let $first  = 'Y'
  Let $first2 = 'Y'
   
  Let $GEXXX902_Deptid_Alias   		= 'J.DEPTID'
  Let $GEXXX907_Div_CD_Alias   		= 'GDT.GEX_COMPANY_DIV_CD'     
  Let $GEXXX916_Jobcode_Alias  		= 'J.JOBCODE'    
  Let $GEXXX938_Manager_Level_Alias  	= 'JT.MANAGER_LEVEL'              
  
  If $prcs_process_instance = ''
    Do SQRW-Run-Controls  
  Else
    Do Process-Scheduler-Run-Controls
  End-if

  let $ReportID		= 'GEXHR015'
  let $ReportTitle 	= 'GENDER AND ETHNIC GROUP DISTRIBUTION'

  display $ReportID noline
  display ' - '     noline
  display $ReportTitle 

  date-time () hh:mi:ss &TimeBegan
  date-time () MM/DD/YYYY  &DateBegan
  show 'Report Began at ' &TimeBegan ' on '  &DateBegan

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
 
  Show '$GEXXX902_Include_Deptid_Criteria           = ' $GEXXX902_Include_Deptid_Display_Criteria
  Show '$GEXXX907_INCLUDE_DIV_CD_CRITERIA           = ' $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA
  Show '$GEXXX916_INCLUDE_JOBCODE_CRITERIA          = ' $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA
  Show '$GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA    = ' $GEXXX938_INCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA
  Show '$WHERE_SELECT_ASOFDATE                      = ' $WHERE_SELECT_ASOFDATE 
    
  Do Print-Cover-Page
 
End-Procedure

!***********************************************************************
Begin-Procedure SQRW-Run-Controls 
!***********************************************************************
#debug9 show 'SQRW-Run-Controls'

  Let $DivCdString = ''''
  Display 'Enter Company Division Code or leave blank to exit.'
  While 1=1
    Input $In-DivCd Maxlen=3 'Company Division Code'
    Uppercase $In-DivCd
    If Rtrim($In-DivCd, ' ') = ''
      Concat '''' With $DivCdString
      Break
    End-If
    If $DivCdString <> ''''
      Concat ''',''' With $DivCdString
    End-If
    Concat $In-DivCd With $DivCdString
  End-While

  If $DivCdString = ''''''
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = '1=1'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = $GEXXX907_Div_CD_Alias || ' In (' || $DivCdString || ')'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA
  End-If  

  Let $DeptIDString = ''''
  Display 'Enter DeptID or leave blank to exit.'
  While 1=1
    Input $In-DeptID Maxlen=10 'DeptID'
    Uppercase $In-DeptID
    If Rtrim($In-DeptID, ' ') = ''
      Concat '''' With $DeptIDString
      Break
    End-If
    If $DeptIDString <> ''''
      Concat ''',''' With $DeptIDString
    End-If
    Concat $In-DeptID With $DeptIDString
  End-While

  If $DeptIDString = ''''''
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
  End-If

  Let $JobcodeString = ''''
  Display 'Enter Jobcode or leave blank to exit.'
  While 1=1
    Input $In-Jobcode Maxlen=6 'Jobcode'
    Uppercase $In-Jobcode
    If Rtrim($In-Jobcode, ' ') = ''
      Concat '''' With $JobcodeString
      Break
    End-If
    If $JobcodeString <> ''''
      Concat ''',''' With $JobcodeString
    End-If
    Concat $In-Jobcode With $JobcodeString
  End-While

  If $JobcodeString = ''''''
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = '1=1'
    Let $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA = 'ALL'
  Else 
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = $GEXXX916_Jobcode_Alias || ' In (' || $JobcodeString || ')'
    Let $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA = $GEXXX916_INCLUDE_JOBCODE_CRITERIA
  End-If

  Let $MgrLvlString = ''''
  Display 'Enter Manager Level or leave blank to exit.'
  While 1=1
    Input $In-MgrLvl Maxlen=2 'Manager Level'
    Uppercase $In-MgrLvl
    If Rtrim($In-MgrLvl, ' ') = ''
      Concat '''' With $MgrLvlString
      Break
    End-If
    If $MgrLvlString <> ''''
      Concat ''',''' With $MgrLvlString
    End-If
    Concat $In-MgrLvl With $MgrLvlString
  End-While

  If $MgrLvlString = ''''''
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA = '1=1'
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA = 'ALL'
  Else
    !AXL 07/26/2001 - Include 99 in analysis always - Begin
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA = $GEXXX938_Manager_Level_Alias || ' In (' || '''99''' || ',' || $MgrLvlString || ')'
    !AXL 07/26/2001 - Include 99 in analysis always - End
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA = $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA
  End-If

  Do Ask-As-Of-Date
  Let $WHERE_SELECT_ASOFDATE = $AsOfDate
 
End-Procedure

!***********************************************************************
Begin-Procedure Process-Scheduler-Run-Controls 
!***********************************************************************
#debug9 show 'Process-Scheduler-Run-Controls'

  Do GEXRCHR1-SELECT-PARAMETERS
  Do GEXXX902-Select-Deptid-Parameters
  Do GEXXX907-Select-Div-CD-Parameters 
  Do GEXXX916-Select-Jobcode-Parameters
  Do GEXXX938-Select-Manager_Level-Parameters

  Let $WHERE_SELECT_ASOFDATE = $GEX_RC_HR.AsOfDate
  If Rtrim ($GEX_RC_HR.AsOfDate, ' ') = ''
    Let $WHERE_SELECT_ASOFDATE = $asoftoday
  End-If

End-Procedure

!***********************************************************************
begin-procedure Print-Cover-Page
!***********************************************************************
#debug9 show 'Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)

  Print '$GEXXX902_Include_Deptid_Criteria                    ='   (+1,5)
  Print $GEXXX902_Include_Deptid_Display_Criteria                  (0,+2)

  Print '$GEXXX907_INCLUDE_DIV_CD_CRITERIA                    ='   (+1,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                  (0,+2)

  Print '$GEXXX916_INCLUDE_JOBCODE_CRITERIA                   ='   (+1,5)
  Print $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA                 (0,+2)

  Print '$GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA             ='   (+1,5)
  Print $GEXXX938_INCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA           (0,+2)

  Print '$WHERE_SELECT_ASOFDATE                               ='   (+1,5)
  Print  $WHERE_SELECT_ASOFDATE                                    (0,+2)

  Let #PAGE-COUNT = 0
  new-page
  let #page-count = 1
 
End-Procedure

!***********************************************************************
begin-procedure Process-Main
!***********************************************************************
#debug9 Show 'Process-Main'

begin-SELECT

/*+ RULE */

GDT.GEX_COMPANY_DIV_CD ()	on-break print=never
   				level=1
                                before=before-div_cd-change
				after=after-div_cd-change
				save=$prev_div_cd


 
				
jt.descr ()  			on-break print=never
				level=2
				after=after-descr-change
                                save=$prev_descr
jt.MANAGER_LEVEL                            
pd.sex			
!div.ethnic_group	        !Commented out for upgrade 9.0
div.ethnic_grp_cd               !upgrade 9.0 changes

j.POSITION_NBR !ISDVMXD added for ITG#60309 on 05/05/2009

  let $manager_level = &jt.manager_level
  let $sex	     = &pd.sex	
  !let $ethnic_group  = &div.ethnic_group   !commented out for upgrade 9.0
  let $ethnic_group  = &div.ethnic_grp_cd   !upgrade 9.0 changes
  let $div_cd        = &GDT.GEX_COMPANY_DIV_CD
  
  
  !ISDVMXD added for ITG#60309 on 05/05/2009 begins
       Let $Position_Nbr = &j.POSITION_NBR       
       Do Get-Position-Title       
       
       if $PosName = '' 
       Let $prev_descr = $prev_descr       
       else
       Let $prev_descr = $PosName       
       end-if
  !ISDVMXD added for ITG#60309 on 05/05/2009 ends
  
  
  Add 1 to #record_count
  do Accumulate-Totals

FROM ps_gex_dept_tbl  gdt,
     ps_jobcode_tbl   jt,
     ps_personal_data pd,
   !  ps_diversity     div,		!AXL 9/5/2000 - 7.5 Change and commented out for upgrade 9.0 
     ps_divers_ethnic  div,             !Upgrade 9.0 changes
     ps_job           j

WHERE  [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
   AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
   AND [$GEXXX916_INCLUDE_JOBCODE_CRITERIA]
   AND [$GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA]

   and j.empl_status in ('A','L','P','S') !AXL 07/26/2001 - Add

   AND j.emplid         = pd.emplid
   AND pd.emplid        = div.emplid
   AND DIV.PRIMARY_INDICATOR = 'Y' 	!GEX-MXT ADDED FOR ETHNICITY CHANGES
   AND j.effdt          =
      (SELECT MAX(effdt)
         FROM ps_job
        WHERE emplid    = j.emplid
          AND empl_rcd = j.empl_rcd
          AND effdt    <= $WHERE_SELECT_ASOFDATE)
   AND j.effseq         =
      (SELECT MAX(effseq)
         FROM ps_job
        WHERE emplid    = j.emplid
          AND empl_rcd = j.empl_rcd
          AND effdt     = j.effdt)

   AND j.JOBCODE = jt.JOBCODE
   AND jt.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL
        WHERE jt.SETID   = SETID
          AND jt.JOBCODE = JOBCODE
          AND EFFDT      <= $WHERE_SELECT_ASOFDATE) !AXL 07/26/2001 - Mod

   And j.deptid          = gdt.deptid
   And gdt.effdt         =
      (Select Max(effdt) From   ps_gex_dept_tbl
       Where  gdt.setid  = setid
       And    gdt.deptid = deptid
       And    effdt      <= $WHERE_SELECT_ASOFDATE) !AXL 07/26/2001 - Mod

order by GDT.GEX_COMPANY_DIV_CD, JT.DESCR

end-SELECT
 
end-procedure

!***********************************************************************
begin-procedure Before-Div_Cd-Change
!***********************************************************************
#debug9 Show 'Before-Div_Cd-Change'

  use-report Report1

  if $first='Y'
     let $first='N'
  else
     new-page
  end-if

end-procedure

!***********************************************************************
begin-procedure After-Descr-Change
!***********************************************************************
#debug9 Show 'After-Descr-Change'

  do Print-Descr-Totals
  do Initialize-Descr-Totals

end-procedure

!***********************************************************************
begin-procedure After-Div_Cd-Change
!***********************************************************************
#debug9 Show 'After-Div_Cd-Change'

  do Print-Div_Cd-Totals
  do Initialize-Div_Cd-Totals
  do Initialize-Descr-Totals

end-procedure

!***********************************************************************
begin-procedure Process-Main2
!***********************************************************************
#debug9 Show 'Process-Main2'

begin-SELECT

/*+ RULE */

GDT2.GEX_COMPANY_DIV_CD ()	on-break print=never
   				level=1
				save=$prev_div_cd

dpt2.descr () 			on-break print=never
   				level=2
                                before=before-deptid-change
				after=after-deptid-change
				save=$prev_deptid

jt2.descr ()  			on-break print=never
				level=3
				after=after-descr-change2
                                save=$prev_descr
jt2.MANAGER_LEVEL                            
pd2.sex		
!div2.ethnic_group	        !Commented out for upgrade 9.0
div2.ethnic_grp_cd              !upgrade 9.0 changes
j2.deptid	

j2.POSITION_NBR !ISDVMXD added for ITG#60309 on 05/05/2009
      
  let $manager_level = &jt2.manager_level
  let $sex	     = &pd2.sex	
 !let $ethnic_group  = &div2.ethnic_group   !commented out for upgrade 9.0
  let $ethnic_group  = &div2.ethnic_grp_cd   !upgrade 9.0 changes
  let $div_cd        = &GDT2.GEX_COMPANY_DIV_CD
  let $deptid	     = &dpt2.descr
  
  !ISDVMXD added for ITG#60309 on 05/05/2009 begins
       Let $Position_Nbr = &j2.POSITION_NBR       
       Do Get-Position-Title 
       
       if $PosName = '' 
       Let $prev_descr = $prev_descr     
       else
       Let $prev_descr = $PosName       
       end-if
  !ISDVMXD added for ITG#60309 on 05/05/2009 ends
  
  Add 1 to #record_count2
  do Accumulate-Totals

  FROM ps_gex_dept_tbl  gdt2,
       PS_DEPT_TBL      DPT2,
       ps_jobcode_tbl   jt2,
       ps_personal_data pd2,
     ! ps_diversity     div2,		!AXL 9/5/2000 - 7.5 Change and commented out for upgrade 9.0 
       ps_divers_ethnic  div2,              !Upgrade 9.0 changes
       ps_job           j2

 WHERE j2.deptid IN (select distinct j.deptid from ps_dept_tbl j
                     where [$GEXXX902_INCLUDE_DEPTID_CRITERIA])
  
   AND gdt2.GEX_COMPANY_DIV_CD in 
      (select distinct gdt.GEX_COMPANY_DIV_CD from ps_gex_dept_tbl gdt
       where [$GEXXX907_INCLUDE_DIV_CD_CRITERIA])

   AND j2.jobcode in (select distinct j.jobcode from ps_jobcode_tbl j
                      where [$GEXXX916_INCLUDE_JOBCODE_CRITERIA])

   AND jt2.manager_level in (select distinct jt.manager_level from ps_jobcode_tbl jt
                            where [$GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA]) 

   and j2.empl_status in ('A','L','P','S') !AXL 07/26/2001 - Add

   AND j2.emplid         = pd2.emplid
   AND pd2.emplid        = div2.emplid
   AND DIV2.PRIMARY_INDICATOR = 'Y' 	!GEX-MXT ADDED FOR ETHNICITY CHANGES
   AND j2.effdt          =
      (SELECT MAX(effdt)
         FROM ps_job
        WHERE emplid    = j2.emplid
          AND empl_rcd = j2.empl_rcd
          AND effdt    <= $WHERE_SELECT_ASOFDATE)
   AND j2.effseq         =
      (SELECT MAX(effseq)
         FROM ps_job
        WHERE emplid    = j2.emplid
          AND empl_rcd = j2.empl_rcd
          AND effdt     = j2.effdt)

   AND j2.JOBCODE = jt2.JOBCODE
   AND jt2.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL
        WHERE jt2.SETID   = SETID
          AND jt2.JOBCODE = JOBCODE
          AND EFFDT      <= $WHERE_SELECT_ASOFDATE)	!AXL 07/26/2001 - Mod

   And j2.deptid          = gdt2.deptid
   And gdt2.effdt         =
      (Select Max(effdt) From   ps_gex_dept_tbl
       Where  setid              = gdt2.setid
       And    deptid             = gdt2.deptid
       And    effdt             <= $WHERE_SELECT_ASOFDATE) !AXL 07/26/2001 - Mod
  
   And j2.deptid          = dpt2.deptid
   AND DPT2.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_DEPT_TBL
        WHERE DPT2.SETID   = SETID
          AND DPT2.DEPTID  = DEPTID
          AND EFFDT <= $WHERE_SELECT_ASOFDATE)	!AXL 07/26/2001 - Mod

order by GDT2.GEX_COMPANY_DIV_CD, DPT2.descr, JT2.DESCR

end-SELECT
 
end-procedure

!***********************************************************************
begin-procedure Accumulate-Totals
!***********************************************************************
#debug9 Show 'Accumulate-Totals'

 evaluate $manager_level
 when <> '99'
  evaluate $sex
    when = 'M'
      add 1 to #md
      evaluate $ethnic_group
        when = 'WHITE'                 !   '1'  Upgrade 9.0 changes
          add 1 to #wmd
          add 1 to #wmjt
          break
        when = 'BLACK'                 !    '2' UPgrade 9.0 changes
          add 1 to #bmd
          add 1 to #bmjt
          break
        when-other
          add 1 to #omd
          add 1 to #omjt
          break
      end-evaluate
      break 
    when = 'F'
      add 1 to #fd
      evaluate $ethnic_group
        when = 'WHITE'                 !   '1'  Upgrade 9.0 changes
          add 1 to #wfd
          add 1 to #wfjt
          break
        when = 'BLACK'                 !    '2' UPgrade 9.0 changes
          add 1 to #bfd
          add 1 to #bfjt
          break
        when-other
          add 1 to #ofd
          add 1 to #ofjt
          break
      end-evaluate
  end-evaluate
  add 1 to #totd
  add 1 to #totjt

 when-other

  evaluate $sex
    when = 'M'
      
      evaluate $ethnic_group
        when = 'WHITE'                 !   '1'  Upgrade 9.0 changes
          add 1 to #wmd_pop
          break
        when = 'BLACK'                 !    '2' UPgrade 9.0 changes
          add 1 to #bmd_pop
          break
        when-other
          add 1 to #omd_pop
          break
      end-evaluate
      break 
    when = 'F'
      add 1 to #fd
      evaluate $ethnic_group
        when = 'WHITE'                 !   '1'  Upgrade 9.0 changes
          add 1 to #wfd_pop
          break
        when = 'BLACK'                 !    '2' UPgrade 9.0 changes
          add 1 to #bfd_pop
          break
        when-other
          add 1 to #ofd_pop
          break
      end-evaluate
  end-evaluate
  add 1 to #totd_pop

 end-evaluate  

end-procedure

!***********************************************************************
begin-procedure Before-Deptid-Change
!***********************************************************************
#debug9 Show 'Before-Deptid-Change'

  use-report Report2

  if $first2='Y'
     let $first2='N'
  else
     new-page
  end-if

end-procedure

!***********************************************************************
begin-procedure After-Descr-Change2
!***********************************************************************
#debug9 Show 'After-Descr-Change2'

  do Print-Descr-Totals2
  do Initialize-Descr-Totals

end-procedure

!***********************************************************************
begin-procedure After-Deptid-Change
!***********************************************************************
#debug9 Show 'After-Deptid-Change'

  do Print-Deptid-Totals
  do Initialize-Deptid-Totals
  do Initialize-Descr-Totals

end-procedure

!***********************************************************************
begin-procedure Print-Descr-Totals
!***********************************************************************
#debug9 Show 'Print-Descr-Totals'
   
  use-report Report1
  if #totjt > 0
  
   print $prev_descr (+1,16)
   print #wfjt              (,45)   edit 99,999
   print #wmjt              (,55)   edit 99,999
   print #bfjt              (,65)   edit 99,999
   print #bmjt              (,75)   edit 99,999
   print #ofjt              (,85)   edit 99,999
   print #omjt              (,95)   edit 99,999
   print #totjt             (,105)  edit 99,999  
  
  end-if
  
end-procedure

!***********************************************************************
begin-procedure Print-Descr-Totals2
!***********************************************************************
#debug9 Show 'Print-Descr-Totals2'

  use-report Report2

  if #totjt > 0

   !print $previous-div_cd  (+1,1)
   print $prev_descr (+1,16)
   print #wfjt              (,45)   edit 99,999
   print #wmjt              (,55)   edit 99,999
   print #bfjt              (,65)   edit 99,999
   print #bmjt              (,75)   edit 99,999
   print #ofjt              (,85)   edit 99,999
   print #omjt              (,95)   edit 99,999
   print #totjt             (,105)  edit 99,999  
  
  end-if
  
end-procedure

!***********************************************************************
begin-procedure Print-Div_Cd-Totals
!***********************************************************************
#debug9 Show 'Print-Div_Cd-Totals'

  use-report Report1

  let #wfd_tot_pop = #wfd + #wfd_pop
  let #wmd_tot_pop = #wmd + #wmd_pop
  let #bfd_tot_pop = #bfd + #bfd_pop
  let #bmd_tot_pop = #bmd + #bmd_pop
  let #ofd_tot_pop = #ofd + #ofd_pop
  let #omd_tot_pop = #omd + #omd_pop
  let #totd_tot_pop = #totd + #totd_pop

   if #totd > 0
     let #wfd_perc    = (#wfd / #totd) * 100 
     let #wmd_perc    = (#wmd / #totd) * 100
     let #bfd_perc    = (#bfd / #totd) * 100 
     let #bmd_perc    = (#bmd / #totd) * 100
     let #ofd_perc    = (#ofd / #totd) * 100 
     let #omd_perc    = (#omd / #totd) * 100
     let #totd_perc   = (#totd / #totd) * 100
  else
     let #wfd_perc    = 0 
     let #wmd_perc    = 0
     let #bfd_perc    = 0 
     let #bmd_perc    = 0
     let #ofd_perc    = 0 
     let #omd_perc    = 0
     let #totd_perc   = 0
  end-if

  let $wfd_perc = edit(#wfd_perc,'999.9') || '%'
  let $wmd_perc = edit(#wmd_perc,'999.9') || '%'
  let $bfd_perc = edit(#bfd_perc,'999.9') || '%'
  let $bmd_perc = edit(#bmd_perc,'999.9') || '%'
  let $ofd_perc = edit(#ofd_perc,'999.9') || '%'
  let $omd_perc = edit(#omd_perc,'999.9') || '%'
  let $totd_perc = edit(#totd_perc,'999.9') || '%'

  print '-'               (+1,1,110)  Fill
  !print $prev_div_cd  (+1,1)
  print '* TOTALS *'      (+1,16)
  print #wfd              (,45)   edit 99,999
  print #wmd              (,55)   edit 99,999
  print #bfd              (,65)   edit 99,999
  print #bmd              (,75)   edit 99,999
  print #ofd              (,85)   edit 99,999
  print #omd              (,95)   edit 99,999
  print #totd             (,105)   edit 99,999
  print '* % of Total Mgmt *'  (+1,16)
  print $wfd_perc         (,45)   
  print $wmd_perc         (,55)
  print $bfd_perc         (,65)
  print $bmd_perc         (,75)
  print $ofd_perc         (,85)
  print $omd_perc         (,95)
  print $totd_perc        (,105)
  print ''                (+1,)

  if #totd_tot_pop > 0
     let #wfd_tot_perc    = (#wfd_tot_pop / #totd_tot_pop) * 100 
     let #wmd_tot_perc    = (#wmd_tot_pop / #totd_tot_pop) * 100
     let #bfd_tot_perc    = (#bfd_tot_pop / #totd_tot_pop) * 100 
     let #bmd_tot_perc    = (#bmd_tot_pop / #totd_tot_pop) * 100
     let #ofd_tot_perc    = (#ofd_tot_pop / #totd_tot_pop) * 100 
     let #omd_tot_perc    = (#omd_tot_pop / #totd_tot_pop) * 100
     let #totd_tot_perc   = (#totd_tot_pop / #totd_tot_pop) * 100
  else
     let #wfd_tot_perc    = 0
     let #wmd_tot_perc    = 0
     let #bfd_tot_perc    = 0
     let #bmd_tot_perc    = 0
     let #ofd_tot_perc    = 0
     let #omd_tot_perc    = 0
     let #totd_tot_perc   = 0
  end-if

  let $wfd_tot_perc = edit(#wfd_tot_perc,'999.9') || '%'
  let $wmd_tot_perc = edit(#wmd_tot_perc,'999.9') || '%'
  let $bfd_tot_perc = edit(#bfd_tot_perc,'999.9') || '%'
  let $bmd_tot_perc = edit(#bmd_tot_perc,'999.9') || '%'
  let $ofd_tot_perc = edit(#ofd_tot_perc,'999.9') || '%'
  let $omd_tot_perc = edit(#omd_tot_perc,'999.9') || '%'
  let $totd_tot_perc = edit(#totd_tot_perc,'999.9') || '%'

  print '* TOTAL POP *'   (,16)
  print #wfd_tot_pop      (,45)   edit 99,999
  print #wmd_tot_pop      (,55)   edit 99,999
  print #bfd_tot_pop      (,65)   edit 99,999
  print #bmd_tot_pop      (,75)   edit 99,999
  print #ofd_tot_pop      (,85)   edit 99,999
  print #omd_tot_pop      (,95)   edit 99,999
  print #totd_tot_pop     (,105)   edit 99,999
  print '* % of Total Population *'  (+1,16)
  print $wfd_tot_perc         (,45)   
  print $wmd_tot_perc         (,55)
  print $bfd_tot_perc         (,65)
  print $bmd_tot_perc         (,75)
  print $ofd_tot_perc         (,85)
  print $omd_tot_perc         (,95)
  print $totd_tot_perc        (,105)
  print ''                (+1,)
  
end-procedure

!***********************************************************************
begin-procedure Print-Deptid-Totals
!***********************************************************************
#debug9 Show 'Print-Deptid-Totals'

  use-report Report2

  let #wfd_tot_pop = #wfd + #wfd_pop
  let #wmd_tot_pop = #wmd + #wmd_pop
  let #bfd_tot_pop = #bfd + #bfd_pop
  let #bmd_tot_pop = #bmd + #bmd_pop
  let #ofd_tot_pop = #ofd + #ofd_pop
  let #omd_tot_pop = #omd + #omd_pop
  let #totd_tot_pop = #totd + #totd_pop

  if #totd > 0
     let #wfd_perc    = (#wfd / #totd) * 100 
     let #wmd_perc    = (#wmd / #totd) * 100
     let #bfd_perc    = (#bfd / #totd) * 100 
     let #bmd_perc    = (#bmd / #totd) * 100
     let #ofd_perc    = (#ofd / #totd) * 100 
     let #omd_perc    = (#omd / #totd) * 100
     let #totd_perc   = (#totd / #totd) * 100
  else
     let #wfd_perc    = 0 
     let #wmd_perc    = 0
     let #bfd_perc    = 0 
     let #bmd_perc    = 0
     let #ofd_perc    = 0 
     let #omd_perc    = 0
     let #totd_perc   = 0
  end-if

  let $wfd_perc = edit(#wfd_perc,'999.9') || '%'
  let $wmd_perc = edit(#wmd_perc,'999.9') || '%'
  let $bfd_perc = edit(#bfd_perc,'999.9') || '%'
  let $bmd_perc = edit(#bmd_perc,'999.9') || '%'
  let $ofd_perc = edit(#ofd_perc,'999.9') || '%'
  let $omd_perc = edit(#omd_perc,'999.9') || '%'
  let $totd_perc = edit(#totd_perc,'999.9') || '%'

  print '-'               (+1,1,110)  Fill
  !print $prev_deptid  (+1,1)
  print '* TOTALS *'      (+1,16)
  print #wfd              (,45)   edit 99,999
  print #wmd              (,55)   edit 99,999
  print #bfd              (,65)   edit 99,999
  print #bmd              (,75)   edit 99,999
  print #ofd              (,85)   edit 99,999
  print #omd              (,95)   edit 99,999
  print #totd             (,105)   edit 99,999
  print '* % of Total Mgmt *'  (+1,16)
  print $wfd_perc         (,45)   
  print $wmd_perc         (,55)
  print $bfd_perc         (,65)
  print $bmd_perc         (,75)
  print $ofd_perc         (,85)
  print $omd_perc         (,95)
  print $totd_perc        (,105)
  print ''                (+1,)

  if #totd_tot_pop > 0
     let #wfd_tot_perc    = (#wfd_tot_pop / #totd_tot_pop) * 100 
     let #wmd_tot_perc    = (#wmd_tot_pop / #totd_tot_pop) * 100
     let #bfd_tot_perc    = (#bfd_tot_pop / #totd_tot_pop) * 100 
     let #bmd_tot_perc    = (#bmd_tot_pop / #totd_tot_pop) * 100
     let #ofd_tot_perc    = (#ofd_tot_pop / #totd_tot_pop) * 100 
     let #omd_tot_perc    = (#omd_tot_pop / #totd_tot_pop) * 100
     let #totd_tot_perc   = (#totd_tot_pop / #totd_tot_pop) * 100
  else
     let #wfd_tot_perc    = 0
     let #wmd_tot_perc    = 0
     let #bfd_tot_perc    = 0
     let #bmd_tot_perc    = 0
     let #ofd_tot_perc    = 0
     let #omd_tot_perc    = 0
     let #totd_tot_perc   = 0
  end-if
     

  let $wfd_tot_perc = edit(#wfd_tot_perc,'999.9') || '%'
  let $wmd_tot_perc = edit(#wmd_tot_perc,'999.9') || '%'
  let $bfd_tot_perc = edit(#bfd_tot_perc,'999.9') || '%'
  let $bmd_tot_perc = edit(#bmd_tot_perc,'999.9') || '%'
  let $ofd_tot_perc = edit(#ofd_tot_perc,'999.9') || '%'
  let $omd_tot_perc = edit(#omd_tot_perc,'999.9') || '%'
  let $totd_tot_perc = edit(#totd_tot_perc,'999.9') || '%'

  print '* TOTAL POP *'   (,16)
  print #wfd_tot_pop      (,45)   edit 99,999
  print #wmd_tot_pop      (,55)   edit 99,999
  print #bfd_tot_pop      (,65)   edit 99,999
  print #bmd_tot_pop      (,75)   edit 99,999
  print #ofd_tot_pop      (,85)   edit 99,999
  print #omd_tot_pop      (,95)   edit 99,999
  print #totd_tot_pop     (,105)   edit 99,999
  print '* % of Total Population *'  (+1,16)
  print $wfd_tot_perc         (,45)   
  print $wmd_tot_perc         (,55)
  print $bfd_tot_perc         (,65)
  print $bmd_tot_perc         (,75)
  print $ofd_tot_perc         (,85)
  print $omd_tot_perc         (,95)
  print $totd_tot_perc        (,105)
  print ''                (+1,)
  
end-procedure

!***********************************************************************
begin-procedure Initialize-Div_Cd-Totals
!***********************************************************************
#debug9 Show 'Initialize-Div_Cd-Totals'

  Let #md  = 0
  Let #fd  = 0
  Let #bmd = 0
  Let #bfd = 0
  Let #wmd = 0
  Let #wfd = 0
  Let #omd = 0
  Let #ofd = 0
  Let #totd= 0

  Let #bmd_pop = 0
  Let #bfd_pop = 0
  Let #wmd_pop = 0
  Let #wfd_pop = 0
  Let #omd_pop = 0
  Let #ofd_pop = 0
  Let #totd_pop= 0

end-procedure

!***********************************************************************
begin-procedure Initialize-Deptid-Totals
!***********************************************************************
#debug9 Show 'Initialize-Deptidd-Totals'

  Let #md  = 0
  Let #fd  = 0
  Let #bmd = 0
  Let #bfd = 0
  Let #wmd = 0
  Let #wfd = 0
  Let #omd = 0
  Let #ofd = 0
  Let #totd= 0

  Let #bmd_pop = 0
  Let #bfd_pop = 0
  Let #wmd_pop = 0
  Let #wfd_pop = 0
  Let #omd_pop = 0
  Let #ofd_pop = 0
  Let #totd_pop= 0

end-procedure

!***********************************************************************
begin-procedure Initialize-Descr-Totals
!***********************************************************************
#debug9 Show 'Initialize-Descr-Totals'

  Let #bmjt = 0
  Let #bfjt = 0
  Let #wmjt = 0
  Let #wfjt = 0
  Let #omjt = 0
  Let #ofjt = 0
  Let #totjt= 0

end-procedure


!***********************************************************************
Begin-Procedure EOJ
!***********************************************************************

  use-report Report1
  if #record_count = 0
    print 'No Data Selected This Run'   (25,) Center
  end-if
 
  use-report Report2
  if #record_count2 = 0
     print 'No Data Selected This Run'   (25,) Center
  end-if

  Do Reset			

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'

End-Procedure



#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx916.sqc'  !Get jobcode multiple row table
#include 'gexxx921.sqc'  !Get hr single row run control
#include 'gexxx938.sqc'  !Get mgr_lvl multiple row table

#include 'askaod.sqc'    !Ask As Of Date input
#include 'getcodta.sqc'  !Get company data
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'getposnm.sqc'   !get position title  !ISDVMXD added for ITG#60309 on 05/05/2009