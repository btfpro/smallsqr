!**************************************************************************
!  GEXXX938:  Retrieve "Multiple" Manger Level Run Controls and           *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX938_Manager_Level_Alias                                        *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA                             *
!    $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA                             *
!    $GEXXX938_INCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA                     *
!    $GEXXX938_EXCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA                     *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX938_Manager_Level_Alias = 'JC.MANAGER_LEVEL                *
!    Do GEXXX938-Select-Manager_Level-Parameters                          *
!    Begin-Select                                                         *
!    JOBCODE                                                              *
!    FROM PS_JOBCODE_TBL JC                                               *
!    WHERE [$GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA]                     *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!  INITIALS     DATE            DESCRIPTION OF THE CHANGE                 *
!**************************************************************************
!  AXL          06/11/2001       INITIAL CREATION                         *
!                                                                         *
!  AXL          07/26/2001       Always include 99 mgr lvl.               *
!**************************************************************************


Begin-Procedure GEXXX938-Select-Manager_Level-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_MANAGER_LEVEL_ROWS = 0
  Let $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA = ''
  Let $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA = ''
  Let $GEXXX938_INCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX938_EXCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_MGR_LVL.MANAGER_LEVEL

  Add 1 To #GEX_RC_MANAGER_LEVEL_ROWS

  If #GEX_RC_MANAGER_LEVEL_ROWS = 1
    !AXL - 07/26/2001 - Begin
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA = '(' || $GEXXX938_MANAGER_LEVEL_Alias || ' In (''' || '99''' || ',''' || &GEX_RC_MGR_LVL.MANAGER_LEVEL || ''''
    !AXL - 07/26/2001 - End
    Let $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA = '(' || $GEXXX938_MANAGER_LEVEL_Alias || ' Not In (''' || &GEX_RC_MGR_LVL.MANAGER_LEVEL || ''''
  Else
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA = $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA || ',''' || &GEX_RC_MGR_LVL.MANAGER_LEVEL  || ''''
    Let $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA = $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA || ',''' || &GEX_RC_MGR_LVL.MANAGER_LEVEL  || ''''
  End-If

From  PS_GEX_RC_MGR_LVL GEX_RC_MGR_LVL

Where GEX_RC_MGR_LVL.OPRID                = [$OprID]
And   GEX_RC_MGR_LVL.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_MGR_LVL.MANAGER_LEVEL, ' ') != ' '

End-Select

  If #GEX_RC_MANAGER_LEVEL_ROWS = 0
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA = '1=1'
    Let $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA = '1=1'
  Else
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA = $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA || '))'
    Let $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA = $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA || '))'
    Let $GEXXX938_INCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA = $GEXXX938_INCLUDE_MANAGER_LEVEL_CRITERIA
    Let $GEXXX938_EXCLUDE_MANAGER_LEVEL_DISPLAY_CRITERIA = $GEXXX938_EXCLUDE_MANAGER_LEVEL_CRITERIA
  End-If

End-Procedure
