!**************************************************************************
!  GEXXX908:  Retrieve "Multiple" SAL_ADMIN_PLAN Run Controls and         *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX908_SAL_ADMIN_PLAN_Alias                                       *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA                            *
!    $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA                            *
!    $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA                    *
!    $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA                    *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX908_SAL_ADMIN_PLAN_Alias = 'J.SAL_ADMIN_PLAN'              *
!    Do GEXXX908-Select-SAL_ADMIN_PLAN-Parameters                         *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA]                    *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX908-Select-Sal-Admin-Plan-Parameters
                

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #PS_GEX_RC_SAL_ADM_ROWS = 0
  Let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA = ''
  Let $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA = ''
  Let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA = 'NONE'

Begin-Select

PS_GEX_RC_SAL_ADM.SAL_ADMIN_PLAN

  Add 1 To #PS_GEX_RC_SAL_ADM_ROWS

  If #PS_GEX_RC_SAL_ADM_ROWS = 1
    Let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA = '(' || $GEXXX908_SAL_ADMIN_PLAN_Alias || ' In ('''     || &PS_GEX_RC_SAL_ADM.SAL_ADMIN_PLAN || ''''
    Let $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA = '(' || $GEXXX908_SAL_ADMIN_PLAN_Alias || ' Not In (''' || &PS_GEX_RC_SAL_ADM.SAL_ADMIN_PLAN || ''''
  Else
    Let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA = $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA || ',''' || &PS_GEX_RC_SAL_ADM.SAL_ADMIN_PLAN  || ''''
    Let $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA = $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA || ',''' || &PS_GEX_RC_SAL_ADM.SAL_ADMIN_PLAN  || ''''
  End-If

From  PS_GEX_RC_SAL_ADM PS_GEX_RC_SAL_ADM

Where PS_GEX_RC_SAL_ADM.OPRID               = [$OprID]
And   PS_GEX_RC_SAL_ADM.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(PS_GEX_RC_SAL_ADM.SAL_ADMIN_PLAN, ' ') != ' ' 
End-Select

  If #PS_GEX_RC_SAL_ADM_ROWS = 0
    Let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA = $GEXXX908_SAL_ADMIN_PLAN_Alias || ' = ' || $GEXXX908_SAL_ADMIN_PLAN_Alias
    Let $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA = $GEXXX908_SAL_ADMIN_PLAN_Alias || ' = ' || $GEXXX908_SAL_ADMIN_PLAN_Alias
  Else
    Let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA = $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA || '))'
    Let $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA = $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA || '))'
    Let $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA = $GEXXX908_INCLUDE_SAL_ADMIN_PLAN_CRITERIA
    Let $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_DISPLAY_CRITERIA = $GEXXX908_EXCLUDE_SAL_ADMIN_PLAN_CRITERIA
  End-If

End-Procedure
