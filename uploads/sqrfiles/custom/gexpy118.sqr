!***********************************************************************
!  GEXPY118: Empl Local Tax Audit Report                               *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! PeopleSoft, Inc.; it is not to be copied, reproduced, or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of PeopleSoft.                        *
!                                                                      *
! Copyright (c) 1988-1998 PeopleSoft, Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
!      $Date:: 8/28/98 3:08p    $                                      *
!  $Revision:: 1                $                                      *
!  $Workfile:: GEXBN058.SQR     $                                      *
!                                                                      *
!***********************************************************************
!    Initials             Date                 Modifications           *
!                                                                      *
!      SXC              06/20/2000             Initial creation        *
!      GEX-MXT          11/07/2007             HCM Upgrade 9.0 changes *
!      VENDTKK          07/02/2008             ITG#51230 Added code to *
!					       Exclude employees with  *
!                                              locality code'I022612M1'*
!					       from reporting.         *
!      ISDVMXD           06/04/2009             S3 #12875175 - Added 
!					       logic to exclude locality
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#Include 'setup02.sqc'  !Printer and page-size initialization

#define #x000_col_emplid       10
#define #x000_col_company      25
#define #x000_col_state        35
#define #x000_col_locality     45
#define #x000_col_local_link   62

!*****************************************************************************
Begin-Heading 5
  #Include 'stdhdg01.sqc'

  print 'Emplid'            (+1,{#x000_col_emplid})
  print 'Company'           (0,{#x000_col_company})
  print 'State'             (0,{#x000_col_state})
  print 'Empl.Locality'     (0,{#x000_col_locality})
  print 'Missing Locality'  (0,{#x000_col_local_link})
End-Heading

!*****************************************************************************
Begin-report
#debug9 show 'begin-report' 
  !Do Set-Optimizer-Goal
  Do Init-DateTime
  Do Init-Number
  Do Get-Current-DateTime

  Do stdapi-init
  Do P010-Init-Variables
  Do Process-Main

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  do stdapi-term
  do Reset
End-report

!*****************************************************************************
begin-procedure Set-Optimizer-Goal
  Begin-SQL
    !Alter Session Set OPTIMIZER_GOAL=RULE;	!GEX-MXT commented for Upgrade
    Alter Session Set OPTIMIZER_MODE=RULE;	!GEX-MXT added for Upgrade
  End-SQL
end-procedure

!*****************************************************************************
Begin-procedure P010-Init-Variables
  let $ReportID    = 'GEXPY118'
  let $ReportTitle = 'Employee Local Tax Audit Report'
  let $Country     = 'USA'

  show '$ReportID='    $ReportID 
  show '$ReportTitle=' $ReportTitle

  Let $GEXXX937_State_Alias  = 'ltd.state'
  Let $GEXXX915_Locality_Alias = 'ltd.locality'		!isdvmxd added for S3# 12875175
  
  if $prcs_process_instance = ''      ! Must have been submitted via SQRW
      Do P110-SQRW-Run-Controls
  else
      Do GEXXX937-Select-State-Parameters
      Do GEXXX915-Select-Locality-Parameters		!isdvmxd added for S3#12875175 
!     Do P120-Process-Scheduler-Run-Controls
  end-if

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ''
  show 'As of Date :- ' $asoftoday
  Show '$GEXXX937_INCLUDE_STATE_CRITERIA = ' $GEXXX937_INCLUDE_STATE_CRITERIA
  Show '$GEXXX915_EXCLUDE_LOCALITY_CRITERIA= ' $GEXXX915_EXCLUDE_LOCALITY_CRITERIA	!isdvmxd added for S3#12875175
  
  print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,10)
  print '$Prcs_OPRID          ='                              (+1,15)
  print $Prcs_OPRID            	                               (0,+2)
  print '$PRCS_RUN_CNTL_ID    ='                              (+1,15)
  print $PRCS_RUN_CNTL_ID                                     (0,+2)

  print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,10)
  print '$GEXXX937_INCLUDE_STATE_CRITERIA   ='                (+1,15)
  print $GEXXX937_INCLUDE_STATE_CRITERIA                      (0,+2)
  print '$GEXXX915_EXCLUDE_LOCALITY_CRITERIA ='		      (+1,15)		!isdvmxd added for S3#12875175
  print $GEXXX915_EXCLUDE_LOCALITY_CRITERIA		      (0,+2)		!isdvmxd added for S3#12875175
  print 'As Of Date                         ='                (+1,15)
  print $asoftoday                                            (0,+2)

  LET #PAGE-COUNT = 0
  NEW-PAGE
  LET #PAGE-COUNT = 1
End-Procedure

!*****************************************************************
Begin-Procedure P110-SQRW-Run-Controls
Let $statestring = ''''
  While 1=1
      Display 'Enter State or leave blank to exit.'
      Input $in-state Maxlen=6 'State'
      Uppercase $In-state
      If Rtrim($in-state, ' ') = ''
	Concat '''' With $statestring
	Break
      End-If
      If $statestring <> ''''
	Concat ''',''' With $statestring
      End-If
      Concat $in-state With $statestring
  End-While

  if $statestring = ''''''
      Let $GEXXX937_INCLUDE_STATE_CRITERIA = '1=1'
  else
      Let $GEXXX937_INCLUDE_STATE_CRITERIA = $GEXXX937_State_Alias || ' In (' || $statestring || ')'
  end-if
End-Procedure

!************************************************************************************
Begin-Procedure Process-Main
Begin-Select
ltd.emplid
ltd.company
ltd.effdt
ltd.state
ltd.locality
j.empl_status
j.empl_rcd

!    show 'In Process main'
    Do get_match_locality

From ps_local_tax_data ltd, ps_job j

Where j.emplid = ltd.emplid
AND   j.effdt  = (select max(effdt) from ps_job
                  where emplid    = ltd.emplid  AND
                        empl_rcd = j.empl_rcd AND
                        effdt    <= $asoftoday)
AND   j.empl_status IN ('A','L','P')
AND   ltd.effdt = (select max(effdt) from ps_local_tax_data
                   where emplid   = ltd.emplid   AND
                         company  = ltd.company  AND
                         state    = ltd.state    AND
!                         locality = ltd.locality AND
                         effdt   <= $asoftoday)
AND  [$GEXXX937_INCLUDE_STATE_CRITERIA]
!AND ltd.locality <> 'I022612M1'			!VENDTKK    ITG#51230     2-JUL-2008
AND  [$GEXXX915_EXCLUDE_LOCALITY_CRITERIA]		!isdvmxd added for S3#12875175
Order By ltd.emplid
End-Select
End-Procedure

!*****************************************************************
Begin-Procedure get_match_locality
Begin-Select
gexlt.state
gexlt.locality
gexlt.effdt
gexlt.locality_link

!  show 'In match locality...'
!  show 'Locality : ' &gexlt.locality
!  show 'Locality Link : ' &gexlt.locality_link

  Do check_match_locality

From ps_gex_local_tax gexlt
Where gexlt.state    = &ltd.state
AND   gexlt.locality = &ltd.locality
AND   gexlt.effdt <= (select max(effdt) from ps_gex_local_tax
                      where state    = &ltd.state 
                        AND locality = &ltd.locality
                        AND effdt   <= &ltd.effdt)
End-Select
End-Procedure

!*****************************************************************
Begin-Procedure check_match_locality
!show 'In check match locality...'
Let $rec_found = 'N'

Begin-Select
ltd1.emplid
ltd1.state
ltd1.locality

   Let $rec_found = 'Y'

From ps_local_tax_data ltd1
Where emplid   = &ltd.emplid
AND   state    = &gexlt.state
AND   locality = &gexlt.locality_link
AND   effdt    = (select max(effdt) from ps_local_tax_data
                  where emplid   = &ltd.emplid          AND
                        state    = &gexlt.state         AND
                        locality = &gexlt.locality_link AND
                        effdt   <= &ltd.effdt)
End-Select
  if $rec_found = 'N'
     Do print_error_rpt
  end-if
End-Procedure

!*****************************************************************
Begin-Procedure print_error_rpt

!    show 'In print report...'
   
    if &ltd.emplid <> $old_emplid
       print '' (+1)
       print &ltd.emplid          (+1,{#x000_col_emplid})
       print &ltd.company         (0,{#x000_col_company})
    else
       print &ltd.company         (+1,{#x000_col_company})
    end-if     
    print &gexlt.state         (0,{#x000_col_state})       
    print &gexlt.locality      (0,{#x000_col_locality})
    print &gexlt.locality_link (0,{#x000_col_local_link})

    Let $old_emplid        =  &ltd.emplid
End-Procedure

!*****************************************************************
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'gexxx937.sqc'  !Get state list
#Include 'gexxx915.sqc'	 !Get locality list		!isdvmxd added for S3#12875175 
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status

#Include 'payrnctl.sqc'  !Get-Run-Control procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'reset.sqc'     !Reset 
#include 'askaod.sqc'    !Ask-As-Of-Date procedure
!*****************************************************************
