!***********************************************************************
! GEXPY074:  Post-Confirm Pay Confirmation Audit                       *
!                                                                      *
!            Veridy that no pay messages were generated from           *
!            the specified pay confirmation.  If there are pay messages*
!            abort the SQR which will stop the batch schedule until    *
!            the confirm is re-reun.                                   *
!                                                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! CJH       11/05/1998  Initial Creation                               *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


Begin-Report
  Do P100-Initialization
  Do P150-Verify-Runid
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Procedure P100-Initialization
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXPY074'
  Let $ReportTitle   = 'Post-Pay Confirm Pay Messages Audit'

  Show ' '
  Show ' '
  Show 'Report ID:    ' $ReportID
  Show 'Report Title: ' $ReportTitle
  Show ' '

  Do Get-Current-DateTime    !curdttim.sqc
  Show 'Report Started on ' $ReportDate ' at ' $ReportTime
  Show ' '

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  Show ' '

End-Procedure

!*************************************************************

Begin-Procedure P110-SQRW-Run-Controls
  While 1=1
    Input $Run_ID maxlen=3 type=char 'Enter the Pay Run ID'
    If $Run_ID <> ''
      Uppercase $Run_Id
      Show $Run_ID
      Break
    End-If
  End-While
End-Procedure

!*************************************************************

Begin-Procedure P120-Process-Scheduler-Run-Controls
  Do GEXXX922-Select-Parameters

  Let $Run_ID     = &GEX_RC_PAY.RUN_ID           

  If $Run_ID = ''
    Show 'Pay Run ID is required for this process'
    Stop
  End-If

  Show 'Pay Run ID         :  ' $Run_ID
End-Procedure

!**************************************************************
Begin-Procedure P150-Verify-Runid

 Let $id_found = 'N'
Begin-Select On-Error=P250-SQL-Error-Found('P200-Main-Process') LOOPS=1
run_id

  move 'Y' to $id_found

From  ps_pay_calendar
Where run_id = $Run_ID
End-Select

 if $id_found = 'N'
  Show 'ERROR:  Run ID not found on Pay Calendar'
  Show '  '
  Show 'Pay Run ID         :  ' $Run_ID
  Show '  '
  Show 'NOTE: Ignore the ORACLE error....it is used to abort the SQR'
  do end-sqr    ! Abort the SQR
 end-if         

End-Procedure

!**************************************************************

Begin-Procedure P200-Main-Process

 Let $found = 'N'

Begin-Select On-Error=P250-SQL-Error-Found('P200-Main-Process') LOOPS=1
company
paygroup

  move 'Y' to $found

From  ps_pay_message
Where run_id = $Run_ID
End-Select

 if $found = 'Y'
  Show 'ERROR:  Pay Messages found for Pay Confirmation'
  Show '  '
  Show 'Pay Run ID         :  ' $Run_ID
  Show '  '
  Show 'Re-run Pay Confirmation before continuing with '
  Show 'post-confirmation jobs'
  Show 'NOTE:  Ignore the ORACLE error..it is used to abort the SQR'
  do end-sqr    ! Abort the SQR
 end-if    
End-Procedure

!***********************************************************

Begin-Procedure P250-SQL-Error-Found($Proc_Name) 
  Show 'SQL Error in ' $Proc_Name
  Show $_sql-error
  Stop
End-Procedure

!***********************************************************
begin-procedure end-sqr 
  let $tbl_name = 'ge_error_table'
begin-select on-error=P250-SQL-Error-Found('end-sqr')
aa
from [$tbl_name]
end-select
end-procedure
!***********************************************************

Begin-Procedure P300-Finalization
  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Show ' '
  Show 'Report Ended on ' $ReportDate ' at ' $ReportTime
End-Procedure


#include 'gexxx922.sqc'  !Get pay single row run control
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'validdt.sqc'   ! Validate date routine
#Include 'number.sqc'    !Routines to format numbers
