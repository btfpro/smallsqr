!**************************************************************************
!  GEXXX909:  Retrieve "Multiple" Acct_CD Run Controls and                *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX909_ACCT_CD_Alias                                              *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX909_INCLUDE_ACCT_CD_CRITERIA                                   *
!    $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA                                   *
!    $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                           *
!    $GEXXX909_EXCLUDE_ACCT_CD_DISPLAY_CRITERIA                           *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX909_ACCT_CD_Alias = 'J.ACCT_CD'                            *
!    Do GEXXX909-Select-ACCT_CD-Parameters                                *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]                           *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX909-Select-ACCT-CD-Parameters

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_ACCT_CD_ROWS = 0
  Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = ''
  Let $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA = ''
  Let $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX909_EXCLUDE_ACCT_CD_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_ACCT_CD.ACCT_CD

  Add 1 To #GEX_RC_ACCT_CD_ROWS

  If #GEX_RC_ACCT_CD_ROWS = 1
    Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = '(' || $GEXXX909_ACCT_CD_Alias || ' In ('''     || &GEX_RC_ACCT_CD.ACCT_CD || ''''
    Let $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA = '(' || $GEXXX909_ACCT_CD_Alias || ' Not In (''' || &GEX_RC_ACCT_CD.ACCT_CD || ''''
  Else
    Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = $GEXXX909_INCLUDE_ACCT_CD_CRITERIA || ',''' || &GEX_RC_ACCT_CD.ACCT_CD  || ''''
    Let $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA = $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA || ',''' || &GEX_RC_ACCT_CD.ACCT_CD  || ''''
  End-If

From  PS_GEX_RC_ACCT_CD GEX_RC_ACCT_CD

Where GEX_RC_ACCT_CD.OPRID               = [$OprID]
And   GEX_RC_ACCT_CD.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_ACCT_CD.ACCT_CD, ' ') != ' ' 
End-Select

  If #GEX_RC_ACCT_CD_ROWS = 0
    Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = $GEXXX909_ACCT_CD_Alias || ' = ' || $GEXXX909_ACCT_CD_Alias
    Let $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA = $GEXXX909_ACCT_CD_Alias || ' = ' || $GEXXX909_ACCT_CD_Alias
  Else
    Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = $GEXXX909_INCLUDE_ACCT_CD_CRITERIA || '))'
    Let $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA = $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA || '))'
    Let $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA = $GEXXX909_INCLUDE_ACCT_CD_CRITERIA
    Let $GEXXX909_EXCLUDE_ACCT_CD_DISPLAY_CRITERIA = $GEXXX909_EXCLUDE_ACCT_CD_CRITERIA
  End-If

End-Procedure
