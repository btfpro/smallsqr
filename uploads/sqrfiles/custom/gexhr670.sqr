! PROGRAM:  Extract data from PeopleSoft PS_DEPT_TBL table to text file
!           in variable length column delimited format.

! HISTORY:  2009/10/01   1.00   MDC   Standard Infohrm Script.
!
!-------------------------------------------------------------------------------
! USAGE  :  1. Review the names assigned to commonly included modules. The names
!              used at your site may differ. Either the names can be changed or
!              the references removed. References are initially included to
!              ensure consistency in program structure and to support future
!              modifications. They are not required to support the operation of
!              the standard program.
!           2. Replace the literal <CLIENT NAME> assigned to the Client constant
!              in the Module Declaratives section, with your organisation name.
!           3. Review that the file name and path assigned to the output file,
!              for each possible operating system environment, is appropriate.
!              In general we encourage the use of the names released with the
!              standard program.
!           4. Insert code in user exit procedure Init_Report to perform any
!              site specific report initialisation.
!           5. Finally review the columns being extracted and edit accordingly.
!              If modifications are made then updates must be applied
!              consistently to the Output_Column_Label_Record, Extract_Data and
!              Output_Data_Record procedures. References to columns that do not
!              exist in your implementation can be removed. This can easily be
!              achieved by inserting a comment character '!' at the start of the
!              line.
!           6. In some situations extra confidentiality is required when
!              providing employee data (eg. SSN is used as employee identifier).
!              In these cases the employee identifier can be encrypted. Some
!              form of unique employee identification must be provided to
!              support the linking of data from multiple tables. When encrypting
!              employee identifiers it is important that they can be easily
!              decrypted so as to support reconciliation.

!----------------------------------------------------------- Common Declaratives
#Include 'setenv.sqc'             ! Set environment.
#Include 'setup02.sqc'            ! Declare printer.

!----------------------------------------------------------- Module Declaratives
#Define Client 'GiantEagle'        ! Set client constant. !Giant Eagle Modified 

!------------------------------------------------------------------------- Setup
Begin-Program
  Do Init-DateTime                ! Initializes date and time variables.
  Do Get-Current-DateTime         ! Gets the current date and time.
  Do Init-Number                  ! Initializes number variables.
  Do Init_Report                  ! User exit to perform report initialisation.
                                  ! Display extract table confirmation message.
  Show 'Extract table: PS_DEPT_TBL'
                                  ! Set output file name based on platform.
                                  ! Other platforms are DOS, WINDOWS, VM, VMS,
                                  ! and MVS.
  do stdapi-init			!Giant Eagle Added to Run the SQR thru Process Schedular
  Evaluate $SQR-Platform
    When = 'WINDOWS-NT'
      Let $File_Name = '/apps/hr/hrms90/hrdev90/outgoing/ps_dept_tbl.dat'	!Giant Eagle Modified the Path 
      Break
    When = 'UNIX'
      Let $File_Name = '/apps/hr/hrms90/hrdev90/outgoing/ps_dept_tbl.dat' !Giant Eagle Modified the Path
      Break
    When-Other
      Let $File_Name = '/apps/hr/hrms90/hrdev90/outgoing/ps_dept_tbl.dat' !Giant Eagle Modified the Path
      Break
  End-Evaluate
                                  ! Open output file and check success.
  Open $File_Name As 1 For-Writing Record=150000 Status=#Open_Status
  If #Open_Status != 0
    Show 'Output file: ' $File_Name ' could not be opened, extract terminated'
  Else
                                  ! Display output file processing message.
    Show 'Output file: ' $File_Name ' extracting data...'
    Do Output_Header_Record       ! Output header record.
    Let $Delimiter = chr(9)       ! Set delimiter to tab character.
    Do Output_Column_Label_Record ! Output column label record.
    Let #Record_Count = 0         ! Set record count to zero.
    Do Extract_Data               ! Extract data.
    Close 1                       ! Close output file.
                                  ! Display record count message.
    Show 'Total records extracted: ' #Record_Count
    
  End-If
  do stdapi-term 	!Giant Eagle Added to update the SQR Status in Process Schedular
End-Program

!--------------------------------------------------------- Init_Report Procedure
Begin-Procedure Init_Report
End-Procedure

!------------------------------------------------ Output_Header_Record Procedure
Begin-Procedure Output_Header_Record
                                  ! Output record.
  Write 1 From -
    'PeopleSoft PS_DEPT_TBL table extract for '
    {Client} 
    ' as at ' -
    $Current-Date
End-Procedure

!------------------------------------------ Output_Column_Label_Record Procedure
Begin-Procedure Output_Column_Label_Record
                                  ! Output record.
  Write 1 From -
    'SETID'      $Delimiter -
    'DEPTID'     $Delimiter -
    'EFFDT'      $Delimiter -
    'EFF_STATUS' $Delimiter -
    'COMPANY'    $Delimiter -
    'DESCR'      $Delimiter -
    'DESCRSHORT'
End-Procedure

!-------------------------------------------------------- Extract_Data Procedure
Begin-Procedure Extract_Data
                                  ! Retrieve data and output to file.
Begin-Select
SETID
DEPTID
EFFDT
EFF_STATUS
COMPANY
DESCR
DESCRSHORT
  Do Output_Data_Record
FROM PS_DEPT_TBL
End-Select
End-Procedure

!-------------------------------------------------- Output_Data_Record Procedure
Begin-Procedure Output_Data_Record
                                  ! Output record.
  Write 1 From -
    &SETID      $Delimiter -
    &DEPTID     $Delimiter -
    &EFFDT      $Delimiter -
    &EFF_STATUS $Delimiter -
    &COMPANY    $Delimiter -
    &DESCR      $Delimiter -
    &DESCRSHORT
  Add 1 To #Record_Count          ! Increment record count.
End-Procedure

!---------------------------------------------------------------- Common Modules


#include 'curdttim.sqc'           ! Routine to get current date and time.
#include 'datetime.sqc'           ! Routine to format dates and times.
#include 'number.sqc'             ! Routine to format numbers.
#Include 'stdapi.sqc'    !Update Process API !Giant Eagle Added 