!**************************************************************************
!  GEXXX906:  Retrieve "Multiple" Weekly Sal Range Run Controls and       *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX906_Wkly_Sal_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA                                  *
!    $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA                                  *
!    $GEXXX906_INCLUDE_WKLY_SAL_DISPLAY_CRITERIA                          *
!    $GEXXX906_EXCLUDE_WKLY_SAL_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX906_Wkly_Sal_Alias = 'J.ANNUAL_RT/52'                      *
!    Do GEXXX906-Select-Wkly-Sal-Parameters                               *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX906_INCLUDE_WKLY_SAL_CRITERIA]                            *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX906-Select-Wkly-Sal-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_WKLY_SAL_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA = ''
  Let $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA = ''
  Let $GEXXX906_INCLUDE_WKLY_SAL_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX906_EXCLUDE_WKLY_SAL_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_WKLY_SAL.GEX_WKLY_SAL_MIN
GEX_RC_WKLY_SAL.GEX_WKLY_SAL_MAX

  Move &GEX_RC_WKLY_SAL.GEX_WKLY_SAL_MIN To $Wkly_Sal_Min 888888888888.888888
  Let $Wkly_Sal_Min = Rtrim($Wkly_Sal_Min, '0')
  Let $Wkly_Sal_Min = Rtrim($Wkly_Sal_Min, '.')

  Move &GEX_RC_WKLY_SAL.GEX_WKLY_SAL_MAX To $Wkly_Sal_Max 888888888888.888888
  Let $Wkly_Sal_Max = Rtrim($Wkly_Sal_Max, '0')
  Let $Wkly_Sal_Max = Rtrim($Wkly_Sal_Max, '.')

  Add 1 To #GEX_RC_WKLY_SAL_ROWS

  Let $Include_Condition = '(' || $GEXXX906_Wkly_Sal_Alias || ' Between '     || $Wkly_Sal_Min || ' And ' || $Wkly_Sal_Max || ')'

  Let $Exclude_Condition = '(' || $GEXXX906_Wkly_Sal_Alias || ' Not Between ' || $Wkly_Sal_Min || ' And ' || $Wkly_Sal_Max || ')'

  If #GEX_RC_WKLY_SAL_ROWS = 1
    Let $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA = '(' || $Include_Condition
    Let $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA =
        $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA =
        $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_WKLY_SAL GEX_RC_WKLY_SAL

Where GEX_RC_WKLY_SAL.OPRID             = [$OprID]
And   GEX_RC_WKLY_SAL.RUN_CNTL_ID       = [$Run_Cntl_ID]
And  (GEX_RC_WKLY_SAL.GEX_WKLY_SAL_MIN != 0
Or    GEX_RC_WKLY_SAL.GEX_WKLY_SAL_MAX != 0)

End-Select

  If #GEX_RC_WKLY_SAL_ROWS = 0
    Let $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA = $GEXXX906_Wkly_Sal_Alias || ' = ' || $GEXXX906_Wkly_Sal_Alias
    Let $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA = $GEXXX906_Wkly_Sal_Alias || ' = ' || $GEXXX906_Wkly_Sal_Alias
  Else
    Let $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA = $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA || ')'
    Let $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA = $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA || ')'
    Let $GEXXX906_INCLUDE_WKLY_SAL_DISPLAY_CRITERIA = $GEXXX906_INCLUDE_WKLY_SAL_CRITERIA
    Let $GEXXX906_EXCLUDE_WKLY_SAL_DISPLAY_CRITERIA = $GEXXX906_EXCLUDE_WKLY_SAL_CRITERIA
  End-If

End-Procedure
