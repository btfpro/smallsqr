!**************************************************************************
!  GEXXX971:  Retrieve Email/Ftp run control values and return a          *
!              Formatted Where-Clause                                     *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX971_Active_Flag_Alias                                          *
!    $GEXXX971_Gex_Trans_Type_Alias                                       *
!    $GEXXX971_Email_Addr_Alias                                           *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX971_TRANS_TYPE_CRITERIA                                        *
!    $GEXXX971_TRANS_TYPE_DISPLAY_CRITERIA                                *
!    $GEXXX971_EMAIL_ADDR_CRITERIA                                        *
!    $GEXXX971_EMAIL_ADDR_DISPLAY_CRITERIA                                *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX971_Active_Flag_Alias  = 'GIR.ACTIVE_FLAG'                 *
!    Let $GEXXX971_Gex_Trans_Type_Alias = 'GIR.GEX_TRANS_TYPE'            *
!    Let $GEXXX971_Email_Addr_Alias = 'GIR.EMAIL_ADDR'                    *
!    Do GEXXX971-Select-Trans-Type-Parameters                             *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_GEX_INTFC_RULE GIR                                           *
!    WHERE [$GEXXX971_TRANS_TYPE_CRITERIA]                               *
!    AND   [$GEXXX971_EMAIL_ADDR_CRITERIA]
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE         *
!**************************************************************************
!	GBD 		01/14/2003 	Initial creation.                 *
!                                                                         *
!       GBD             07/09/2003      Added code to pull value for      *
!                                       GEX_INTFC_DATA.INTERNAL_EXTERNAL. *
!**************************************************************************

Begin-Procedure GEXXX971-Select-Trans-Type-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let $Prcs_Name = upper($ReportID)
  Let $PrcsName             = '''' || $Prcs_Name        || ''''
  Let $Prcs_DBName = upper($db_name)
  Let $DBName               = '''' || $Prcs_DBName      || ''''

!  Show 'Oprid:       ' $Oprid
!  Show 'Run_Cntl_ID: ' $Run_Cntl_ID
!  Show 'PrcsName:    ' $PrcsName
!  Show 'DBName:      ' $DBName

  Let #GEX_INTFC_ROWS = 0
  Let $GEXXX971_TRANS_TYPE_CRITERIA = ''
  Let $GEXXX971_TRANS_TYPE_DISPLAY_CRITERIA = ''
  Let $GEXXX971_EMAIL_ADDR_CRITERIA = ''
  Let $GEXXX971_EMAIL_ADDR_DISPLAY_CRITERIA = ''
  Let $GEXXX971_FTP_ADDR_CRITERIA = ''
  Let $GEXXX971_FTP_ADDR_DISPLAY_CRITERIA = ''
  Let $first_email = 'Y'
!  Let $first_ftp = 'Y'

Begin-Select

GEX_INTFC_DATA.DBNAME
GEX_INTFC_DATA.EMAIL_ADDR
GEX_INTFC_DATA.ACTIVE_FLAG
GEX_INTFC_DATA.GEX_TRANS_TYPE
GEX_INTFC_DATA.INTERNAL_EXTERNAL
GEX_INTFC_DATA.GEX_N_DIRECTORY

  Add 1 To #GEX_INTFC_ROWS
!  Show 'In SQC'

  Let $GEXXX971_ACTIVE_FLAG = &GEX_INTFC_DATA.ACTIVE_FLAG
  Let $GEXXX971_GEX_TRANS_TYPE = &GEX_INTFC_DATA.GEX_TRANS_TYPE
  Evaluate $GEXXX971_GEX_TRANS_TYPE
  When = 'N'
     Let $GEXXX971_TRANS_TYPE_CRITERIA = '1=1'
     Break
  When = 'E'
     Let $GEXXX971_EMAIL_ADDR = &GEX_INTFC_DATA.EMAIL_ADDR
     If Rtrim($GEXXX971_EMAIL_ADDR,' ') != ''
        If $first_email = 'Y'
            Let $first_email = 'N'
            Let $GEXXX971_EMAIL_ADDR_CRITERIA = $GEXXX971_EMAIL_ADDR
        Else
            Let $GEXXX971_EMAIL_ADDR_CRITERIA = $GEXXX971_EMAIL_ADDR_CRITERIA||', '||$GEXXX971_EMAIL_ADDR
        End-If
     End-If
     Let $GEXXX971_INT_EXT = &GEX_INTFC_DATA.INTERNAL_EXTERNAL      !GBD 07/09/2003
     Break
  When = 'F'
     Let $GEXXX971_FTP_ADDR = &GEX_INTFC_DATA.EMAIL_ADDR
     Let $GEXXX971_INT_EXT = &GEX_INTFC_DATA.INTERNAL_EXTERNAL
     Let $GEXXX971_DIR_PATH = &GEX_INTFC_DATA.GEX_N_DIRECTORY
     If Rtrim($GEXXX971_FTP_ADDR,' ') != ''
!        If $first_ftp = 'Y'
!            Let $first_ftp = 'N'
            Let $GEXXX971_FTP_ADDR_CRITERIA = $GEXXX971_FTP_ADDR
!        Else
!            Let $GEXXX971_FTP_ADDR_CRITERIA = $GEXXX971_FTP_ADDR_CRITERIA||', '||$GEXXX971_FTP_ADDR
!        End-If
     End-If
     Break
  End-Evaluate

From  PS_GEX_INTFC_RULE GEX_INTFC_RULE,
      PS_GEX_INTFC_DATA GEX_INTFC_DATA

Where GEX_INTFC_RULE.OPRID               = [$OprID]
And   GEX_INTFC_RULE.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   GEX_INTFC_RULE.OPRID               = GEX_INTFC_DATA.OPRID
And   GEX_INTFC_RULE.RUN_CNTL_ID         = GEX_INTFC_DATA.RUN_CNTL_ID
And   GEX_INTFC_RULE.PRCSNAME            = [$PrcsName]
And   GEX_INTFC_RULE.DBNAME              = [$DBName]
And   GEX_INTFC_RULE.PRCSNAME            = GEX_INTFC_DATA.PRCSNAME
And   GEX_INTFC_RULE.DBNAME              = GEX_INTFC_DATA.DBNAME
And   GEX_INTFC_RULE.EFFDT               = GEX_INTFC_DATA.EFFDT
And   GEX_INTFC_DATA.EFFDT = (select Max(EFFDT)
                              from PS_GEX_INTFC_DATA
                              Where OPRID       = GEX_INTFC_DATA.OPRID
                              And   RUN_CNTL_ID = GEX_INTFC_DATA.RUN_CNTL_ID
                              And   PRCSNAME    = GEX_INTFC_DATA.PRCSNAME
                              And   DBNAME      = GEX_INTFC_DATA.DBNAME
                              And   EFFDT      <= SYSDATE)

And   GEX_INTFC_DATA.ACTIVE_FLAG         = 'A'
And   GEX_INTFC_DATA.GEX_TRANS_TYPE     != 'N'

Order By GEX_INTFC_DATA.GEX_TRANS_TYPE, GEX_INTFC_DATA.EMAIL_ADDR

End-Select


  If #GEX_INTFC_ROWS = 0

    Let $GEXXX971_EMAIL_ADDR = ''
    Let $GEXXX971_EMAIL_ADDR_CRITERIA = ''
    Let $GEXXX971_FTP_ADDR = ''
    Let $GEXXX971_FTP_ADDR_CRITERIA = ''

!  Else
!    Show 'GEXXX971_EMAIL_ADDR_CRITERIA: ' $GEXXX971_EMAIL_ADDR_CRITERIA
!    Show 'GEXXX971_FTP_ADDR_CRITERIA:   ' $GEXXX971_FTP_ADDR_CRITERIA
    
  End-If

End-Procedure
