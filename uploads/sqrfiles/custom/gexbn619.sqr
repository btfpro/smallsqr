!***********************************************************************
! Interface Name: GEXBN619                                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
! GEXBN619:     UCCI Interface File                                    *
!                                                                      *
! Narrative:    This program creates a file and report of              *
!               employees enrolled in UCCI Dental coverage.            *
!***********************************************************************
!                                                                      *
! SQL Tables:   PS_PERSONAL_DATA                                       *
!               PS_JOB                                                 *
!               PS_HEALTH_BENEFIT                                      *
!               PS_BENEF_PLAN_TBL                                      *
!               PS_DEPENDENT_BENEF                                     *
!               PS_HEALTH_DEPENDNT                                     *
!                                                                      *
! Written by:   Carl Buckrop                                           *
!                                                                      *
! Normally Run: Weekly                                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#         INITIALS    DATE           DESCRIPTION OF THE CHANGE    *
!***********************************************************************
!CSR HR-10816  CWB         11/15/2004     INITIAL CREATION             *
!
!              VENDTKK     10/10/2007     Alter program to use SSN      *
!					  instead of EMPLID in preparation*
!					  for 7 character GE EMPLID.   *
!
!              VENDTKK     10/10/2007    Modified GEXBN619 (UCCI Interface)*
!                                        to accommodate values for     *
!				         Domestic Partner coverage     *
!					                               *
!              VENDTKK     11/15/2007    Added PLANE_TYPE '16'         *
!		                                                       *
!              DXS         12/03/2007     get-relationship, made       *
!		                          relationship value changes,  *
!                                         as a result of upgrade to 9.0*
!              MXR         02/06/2009     S3 12396213 Bug fix          *
!              KXY         03/31/2009     Added the code to pick Correct
!																					Dependent coverage date and 
!																					correct cancel dates
!							KXY          04/08/2009	    Changed the code for s3 # 12656317
! GEX ISDVNPK 12/25/2010 Modifed to remove the Student criteria & age limit is changed from 19 to 26
!GEX_SEC_REENG 2015-04-29 Vahini Katta                                 *
!Changes related to gex_emplid_mapp/7 digit emplid
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup02.sqc'

!*********************************************************************
!Initial processing, retrieving current dates and calls the main 
!procedure to do the processing.                                     
!*********************************************************************
begin-program

  do Init-DateTime
  do Init-Number
  do stdapi-init
  do initialization
  move 'GEXBN619' to $ReportID
  move 'UCCI Interface' to $ReportTitle
  show $ReportTitle 
  show '  ' 
  do Get-Current-DateTime

  date-time () mm/dd/yyyy &daybegan
  date-time () hh:mi:ss &timebegan
  show ' '
  show 'Report began on ' &daybegan ' at ' &timebegan
  show ' '

  date-time () yyyymmdd &createdate
  date-time () hhmiss   &createtime
  let $create_date = &createdate
  let $create_time = &createtime
 
  do Report
  do finalization
  do stdapi-term
end-program

!*********************************************************************
begin-procedure initialization
!*********************************************************************
#debug9 Show 'initialization'

  do delete-ucci-prior
  do insert-ucci-prior
  do delete-ucci-curr

  move 0 to #seq_nbr
  move 0 to #covrg_count
  move 0 to #elig_count

  let $first_ee = 'Y'
  let $3_spaces   = rpad($3_spaces,3,' ')
  let $5_spaces   = rpad($5_spaces,5,' ')
  let $7_spaces   = rpad($7_spaces,7,' ')
  let $8_spaces  = rpad($8_spaces,8,' ')
  let $9_spaces   = rpad($9_spaces,9,' ')
  let $10_spaces  = rpad($10_spaces,10,' ')
  let $18_spaces  = rpad($18_spaces,18,' ')
  let $35_spaces  = rpad($35_spaces,35,' ')
  let $37_spaces  = rpad($37_spaces,37,' ')
  let $40_spaces  = rpad($40_spaces,40,' ')
  let $55_spaces  = rpad($55_spaces,55,' ')
  let $139_spaces = rpad($139_spaces,139,' ')
  let $100_spaces = rpad($100_spaces,100,' ') 
  let $153_spaces = rpad($153_spaces,153,' ')
  let $158_spaces = rpad($158_spaces,158,' ')
  let $246_spaces = rpad($246_spaces,246,' ')
  let $251_spaces = rpad($251_spaces,251,' ')
  let $938_spaces = rpad($938_spaces,938,' ')
  let $stud_over_age = 'N' 
  let $non_stud_over_age = 'N'
  
  do open-file 

  date-time () mm/dd/yyyy &rptdt
  date-time () hh:mi:ss &rpttime
  move &rptdt to $report_date 
  move &rpttime to $report_time 
  show '$report_date: ' $report_date
  show '$report_time: ' $report_time
  let $first_ee = 'Y'
end-procedure

!**********************************************************
!  Delete the prior week's data from PS_GEX_UCCI_PRIOR
!**********************************************************
begin-procedure Delete-ucci-prior 
#debug9 Show 'Delete-ucci-prior'
  begin-SQL ON-ERROR=SQL-Error-Found('Delete-ucci-prior')
    delete from PS_GEX_UCCI_PRIOR
  end-SQL
end-procedure

!*******************************************************************
!  Insert the data from PS_GEX_UCCI_CURR into PS_GEX_UCCI_PRIOR
!*******************************************************************
begin-procedure Insert-ucci-prior 
#debug9 Show 'Insert-ucci-prior'
  begin-SQL ON-ERROR=SQL-Error-Found('Insert-ucci-prior')
    Insert into PS_GEX_UCCI_PRIOR
      (select *
       from PS_GEX_UCCI_CURR)
  end-SQL
end-procedure

!**********************************************************
!  Delete the prior week's data from PS_GEX_UCCI_CURR
!**********************************************************
begin-procedure Delete-ucci-curr 
#debug9 Show 'Delete-ucci-curr'
  begin-SQL ON-ERROR=SQL-Error-Found('Delete-ucci-curr')
    delete from PS_GEX_UCCI_CURR
  end-SQL
end-procedure

!*********************************************************************
begin-procedure Report
!*********************************************************************

 move 'N' to $Errorfound

  date-time () MM/DD/YYYY &curr_date
  date-time () hh:mi:ss &curr_time

 move &curr_date to $curr_date

  do Format-datetime($curr_date,$curr_Date_dbf_1,{DEFMDY},'','native')
  do Convert-To-DTU-Date($curr_Date_dbf_1,$curr_Date_1)

  let $curr_yy = substr($curr_Date_1,1,4)
  let $curr_mm = substr($curr_Date_1,6,2)
  let $curr_dd = substr($curr_Date_1,9,2)

  if $prcs_process_instance = ''
!    do GEXXX950-Input-Parameters
     do Ask-As-Of-Date
     
  else 
  
     	
     Do GEXRCBN1-SELECT-PARAMETERS				!VENDTKK   ITG#45098     20-NOV-2007
     Let $AsOfDate = &GEX_RC_BEN.AsOfDate   			!VENDTKK   ITG#45098     20-NOV-2007
        
  end-if
  if rtrim($asofdate,' ') = ''
    let $run_dt = $curr_yy||$curr_mm||$curr_dd
    do Format-datetime($run_dt, $run_date, {DEFCMP},'','native')
    do Format-datetime($run_dt, $asofdate, {DEFCMP},'','native')
  end-if
  show 'As of date: ' $asofdate
  show ' '
  do convert-to-dtu-date($asofdate,$asofdate_dtu)
  let $hdg_yy = substr($asofdate_dtu,1,4)
  let $hdg_mm = substr($asofdate_dtu,6,2)
  let $hdg_dd = substr($asofdate_dtu,9,2)
  let $hdg_asofdate = $hdg_mm||'/'||$hdg_dd||'/'||$hdg_yy
  do dtu-add-months($asofdate_dtu,1,$elig_date_dtu)
  let $elig_yy = substr($elig_date_dtu,1,4)
  let $elig_mm = substr($elig_date_dtu,6,2)
  let $elig_dd = '01'
  let $hdr_elig_date = $elig_yy||$elig_mm||$elig_dd

  show '$hdr_elig_date = ' $hdr_elig_date
  show ' '

  do dtu-add-days($asofdate_dtu,30,$plus30_date_dtu)
  do convert-from-dtu-date($plus30_date_dtu,$plus30_date)
  show '$asofdate: ' $asofdate
  show '$plus30_date: ' $plus30_date
  show ' '

 if $sev-error = 'Y'
    goto Report-exit
 end-if

 do process-data

Report-Exit:

end-procedure

!*********************************************************************
!Prints the header information in the report.
!*********************************************************************

begin-heading 5

  #Include 'stdhdg01.sqc'
  print 'EXCEPTION REPORT' (+0) center 
  let $hdg_tmp = 'As of: ' || $hdg_asofdate 
  print $hdg_tmp           (+1) center 
  print '-'                (+1,1,174) FILL

end-heading

!*********************************************************************
! Select all valid Employee Types for a specific company
!*********************************************************************

begin-procedure process-data
#debug9 Show 'process-data'
display 'Begin process'


move 'N' to $rowfound
let $end_process = 'N'
let #inputtran = 0

begin-select ON-ERROR=SQL-Error-Found('Process-data')

BP.GROUP_NBR               () on-break print=never level=03
                              save=$save_group_nbr  
                              before=before-group-nbr  

BP.VENDOR_ID
   move &BP.VENDOR_ID        to $provider 

VP.POLICY_NBR
   move &VP.POLICY_NBR       to $policy

PD.EMPLID                   () on-break print=never level=01
                               before=before-emplid  
                               after=after-emplid 
                             
HB.EMPL_RCD
   move &HB.EMPL_RCD        to #empl_rcd

PN.NATIONAL_ID
   move &PN.NATIONAL_ID     to $national_id

PD.FIRST_NAME
   move &PD.FIRST_NAME      to $first_name

PD.MIDDLE_NAME
   move &PD.MIDDLE_NAME     to $middle_name
   
PD.LAST_NAME
   move &PD.LAST_NAME      to $last_name
PD.NAME_PREFIX
   move &PD.NAME_PREFIX    to $prefix
PD.NAME_SUFFIX
   move &PD.NAME_SUFFIX    to $suffix
PD.ADDRESS1
   move &PD.ADDRESS1       to $address1
PD.ADDRESS2
   move &PD.ADDRESS2       to $address2
PD.ADDRESS3
   move &PD.ADDRESS3       to $address3
PD.CITY
   move &PD.CITY             to $city
PD.STATE
   move &PD.STATE            to $state
PD.POSTAL 
   move &PD.POSTAL           to $postal
PD.COUNTRY
   move &PD.COUNTRY          to $country
PD.BIRTHDATE
   move &PD.BIRTHDATE        to $birthdate
PD.SEX
   move &PD.SEX              to $sex
PD.MAR_STATUS
   move &PD.MAR_STATUS       to $mar_status
JB.FULL_PART_TIME
   move &JB.FULL_PART_TIME   to $full_part_time
JB.EMPL_TYPE
   move &JB.EMPL_TYPE        to $empl_type
JB.DEPTID
   move &JB.DEPTID           to $deptid
JB.EFFDT
   move &JB.EFFDT            to $job_effdt
EM.HIRE_DT
   move &EM.HIRE_DT          to $hire_dt
HB.COVRG_CD
   move &HB.COVRG_CD         to $covrg_cd
HB.EFFDT
   move &HB.EFFDT            to $hb_effdt
HB.COVERAGE_BEGIN_DT
   move &HB.COVERAGE_BEGIN_DT to $hb_coverage_begin_dt

HB.BENEFIT_PLAN              () on-break print=never level=02
                                before=before-benefit-plan   
                                after=after-benefit-plan

HB.COVERAGE_ELECT  
PP.BENEFIT_PROGRAM 
HB.PLAN_TYPE																	!Vendkxy added the code to pick correct cancel date
		move &HB.PLAN_TYPE					to $plan_type			!Vendkxy added the code to pick correct cancel date

   do format-group-nbr 
   move &BP.GROUP_NBR        to $group_nbr
   move &PD.EMPLID           to $emplid
   move $national_id         to $contract_id
   move &HB.BENEFIT_PLAN     to $benefit_plan
   move &PP.BENEFIT_PROGRAM  to $benefit_program

!################################################
!################################################

    if &hb.coverage_elect = 'E'
    move 'Y' to $rowfound
  end-if

FROM PS_JOB JB,
     PS_EMPLOYMENT EM,
     PS_PERS_NID PN, 
     PS_PERSONAL_DATA PD,
     PS_VENDOR_POLICY VP,
     PS_HEALTH_BENEFIT HB,
     PS_BENEF_PLAN_TBL BP,
     PS_BEN_PROG_PARTIC PP
WHERE JB.EMPLID       = PD.EMPLID
!######################################################################
!and   jb.emplid in ('004389892','189380702','264570489','302526010','296782950','357622098','177685028')
!and jb.emplid in ('199666122','177627116','194580927','187604403','159643190','210461482','551430465','167484611','120628829','302727163','172684240','188346745','272902806') ! VENDKXY for testing

!######################################################################
AND   JB.EMPLID       = HB.EMPLID
AND   JB.EMPL_RCD     = HB.EMPL_RCD
AND   EM.EMPLID       = JB.EMPLID
AND   PD.EMPLID       = EM.EMPLID
AND   PN.EMPLID       = PD.EMPLID
AND   PP.EMPLID       = HB.EMPLID
AND   PP.EMPL_RCD     = HB.EMPL_RCD
!#############################################################
AND   HB.PLAN_TYPE  IN ( '11' , '16' )			!VENDTKK ITG#45098 added PLAY_TYPE '16'
!#############################################################
AND   HB.PLAN_TYPE    = BP.PLAN_TYPE
AND   HB.BENEFIT_PLAN = BP.BENEFIT_PLAN
AND   BP.VENDOR_ID    = '0017'
!#############################################################
and   BP.GROUP_NBR <> ' '
!#############################################################
AND   BP.VENDOR_ID    = VP.VENDOR_ID
AND   BP.GROUP_NBR    = VP.GROUP_NBR
AND   VP.SETID        = 'COMMN'  

AND   JB.EFFDT = (SELECT MAX(EFFDT)
		  FROM PS_JOB
		  WHERE EMPLID    = JB.EMPLID
		  AND   EMPL_RCD = JB.EMPL_RCD
		  AND   EFFDT    <= $asofdate)

AND   JB.EFFSEQ = (SELECT MAX(EFFSEQ)
		   FROM PS_JOB
		   WHERE EMPLID    = JB.EMPLID
		   AND   EMPL_RCD = JB.EMPL_RCD
		   AND   EFFDT    = JB.EFFDT)

AND   HB.COVERAGE_BEGIN_DT =  (SELECT MAX(COVERAGE_BEGIN_DT) 
                   FROM PS_HEALTH_BENEFIT
                   WHERE HB.EMPLID = EMPLID
                   AND HB.EMPL_RCD = EMPL_RCD
                   AND HB.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND HB.PLAN_TYPE = PLAN_TYPE
                   AND HB.BENEFIT_NBR = BENEFIT_NBR
                   AND COVERAGE_BEGIN_DT  <= $asofdate)

AND   BP.EFFDT =  (SELECT MAX(EFFDT)
                   FROM PS_BENEF_PLAN_TBL
                   WHERE BP.PLAN_TYPE = PLAN_TYPE
                   AND BP.BENEFIT_PLAN = BENEFIT_PLAN
                   AND EFFDT <= $asofdate)  

AND   PP.EFFDT =  (SELECT MAX(EFFDT) 
                   FROM PS_BEN_PROG_PARTIC
                   WHERE PP.EMPLID = EMPLID
                   AND PP.EMPL_RCD = EMPL_RCD
                   AND PP.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND   EFFDT    <= $asofdate)
!                   AND EFFDT <= $plus30_date)  

ORDER BY  PD.EMPLID,HB.EFFDT DESC ,HB.BENEFIT_PLAN

end-select
do before-emplid   
if $rowfound = 'N'
   display 'No Employees found for the UCCI File.' 
else
  do create-header-rec
  do check-for-cancels
  do select-from-curr
end-if
end-procedure

begin-procedure before-group-nbr
#debug9 Show 'before-group-nbr ' 
end-procedure

begin-procedure format-group-nbr
#debug9 Show 'format-group-nbr, $save_group_nbr: ' $save_group_nbr
  if rtrim($save_group_nbr,' ') <> ''
    if substr($save_group_nbr,7,1) = '-'
      let $tmp_group_nbr   = substr($save_group_nbr,1,6) || substr($save_group_nbr,8,3)
   else 
      let $tmp_group_nbr = $save_group_nbr
    end-if
   let $rec_group_nbr = '005'||$tmp_group_nbr||'000'
  end-if

end-procedure

begin-procedure before-emplid
#debug9 Show 'before-emplid ' $emplid

#debug9 show 'emplid=' $emplid '#empl_rcd;;'  #empl_rcd '$HB_EFFDT==' $HB_EFFDT  '$hb_coverage_begin_dt:' $hb_coverage_begin_dt '$plan_type:' $plan_type
    if $emplid <> ''
      if &hb.coverage_elect = 'E'
        Do create-applicant-rec
        move 0 to #seq_nbr
        do insert-rec
        do get-dependent
      end-if
    end-if

end-procedure

begin-procedure after-emplid
#debug9 Show 'after-emplid, $emplid : ' $emplid 
  add 1 to #contract_ctr
#debug9 Show 'contract_ctr: ' #contract_ctr
end-procedure

begin-procedure before-benefit-plan
#debug9 Show 'before-benefit-plan ' $benefit_plan
end-procedure

begin-procedure after-benefit-plan
#debug9 Show 'after-benefit-plan ' $benefit_plan
end-procedure

begin-procedure get-zip-code 
  let $zip = substr($POSTAL,1,5)
  if rtrim($zip,' ') = ''
    let $zip = $5_spaces
  end-if 
  let $zip_suff = '    '
  let $zip_6    = '      '
end-procedure

!*********************************************************************
! Create record variables to be inserted into PS_GEXBN619
!*********************************************************************
begin-procedure Create-applicant-rec
#debug9 Show 'create-applicant-rec'

  let $Rec_type = '1'
  let $ssn = $national_id
  let $last_name = rpad($last_name,35,' ')
  let $first_name = rpad($first_name,25,' ')
  let $middle_name = rpad($middle_name,25,' ')
  let $prefix = rpad($prefix,10,' ')
  let $suffix = rpad($suffix,10,' ')
  let $year4 = '1'
  do Format-datetime($birthdate,$birth_Date,{DEFCMP},'','')
  let $address1 = rpad($address1,55,' ')
    if rtrim($address1,' ') = ''
       show 'EE missing address1 in PERSONAL_DATA: ' $emplid
    end-if
  let $address2 = rpad($address2,55,' ')
  let $city     = rpad($city,30,' ')
    if rtrim($city,' ') = ''
       show 'EE missing city in PERSONAL_DATA: ' $emplid
    end-if
  do get-zip-code 
  let $product_line = 'D'
  let $year4 = '1'
  do Convert-To-DTU-Date($hb_coverage_begin_dt,$covrg_Dt_dtu)
  let $covrg_yy = substr($covrg_Dt_dtu,1,4)
  let $covrg_mm = substr($covrg_Dt_dtu,6,2)
  let $covrg_dd = substr($covrg_Dt_dtu,9,2)
  let $applic_effdt = $covrg_yy||$covrg_mm||$covrg_dd
    let $applic_cancel_dt = '00000000'
  let $HIPAA_resp_ind = 'N'
  let $relation_cd    = '18'
  if $applic_cancel_dt = '00000000'
    let $cancel_reason_cd = '  '
  else
    let $cancel_reason_cd = 'AI'
  end-if
  let $Part_A_end_dt = '00000000'
!  let $year4 = '1'
!  do Format-datetime($hire_dt,$Hire_Date,{DEFCMP},'','')

	 !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
   !do Get-Gex-Mapp-EmpIid($emplid , $EmplId_Out)		!VENDTKK    ITG#44219    10/10/2007
   let $EmplId_Out	= $emplid
   !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
  
  let $out1 = $contract_id||$rec_type||$last_name||$first_name||$middle_name||
	$prefix||$suffix||$sex||$birth_date||$address1||$address2||$city||
	$state||$zip||$zip_suff||$zip_6||$rec_group_nbr||$product_line||$applic_effdt||
	$applic_cancel_dt||$HIPAA_resp_ind||$relation_cd||$cancel_reason_cd||
	$Part_A_end_dt||$ssn||$EmplId_Out 				!VENDTKK    ITG#44219    10/10/2007
  let $out1 = rpad($out1,1000,' ')

  add 1 to #total_recs

end-procedure

!*********************************************************************
begin-procedure Get-Dependent
#debug9 Show 'get-dependent'
begin-select

HD.EMPLID

DB.DEPENDENT_BENEF          () on-break print=never level=01
                               after=after-dep-benef    
   move &DB.DEPENDENT_BENEF to $dependent_benef

DB.FIRST_NAME
   move &DB.FIRST_NAME      to $dep_first_name

DB.MIDDLE_NAME
   move &DB.MIDDLE_NAME     to $dep_mid_name

DB.LAST_NAME
   move &DB.LAST_NAME       to $dep_last_name

DB.NAME_PREFIX
   move &DB.NAME_PREFIX     to $dep_prefix

DB.NAME_SUFFIX
   move &DB.NAME_SUFFIX     to $dep_suffix

DB.ADDRESS1
   move &DB.ADDRESS1        to $dep_address1
 
DB.ADDRESS2
   move &DB.ADDRESS2        to $dep_address2

DB.CITY
   move &DB.CITY            to $dep_city

DB.STATE 
   move &DB.STATE           to $dep_state

DB.POSTAL
   move &DB.POSTAL          to $dep_postal

DB.SAME_ADDRESS_EMPL
   move &DB.SAME_ADDRESS_EMPL to $same_address
 
DB.RELATIONSHIP
   move &DB.RELATIONSHIP    to $dep_relationship

DB.MAR_STATUS
   move &DB.MAR_STATUS      to $dep_mar_status

DB.STUDENT
   move &DB.STUDENT         to $dep_student
				 
DB.DISABLED
   move &DB.DISABLED        to $dep_disabled

DB.BIRTHDATE
   move &DB.BIRTHDATE       to $dep_birthdate

DB.SEX
   move &DB.SEX             to $dep_sex

NVL(DNID.NATIONAL_ID,' ')   &DNID.NATIONAL_ID
   move &DNID.NATIONAL_ID   to $dep_ssn

HD.EFFDT
   move &HD.EFFDT           to $hd_effdt
HD.PLAN_TYPE
   move  &HD.PLAN_TYPE      to $dep_plan_type
   
 
        
FROM PS_DEPENDENT_BENEF DB,
     PS_HEALTH_DEPENDNT HD,
     PS_DEP_BENEF_NID DNID,
		 PS_HEALTH_BENEFIT  CD			!Vendkxy added the code
WHERE DB.EMPLID          = HD.EMPLID
AND   DB.EMPLID          = $emplid
AND   HD.EMPL_RCD        = #empl_rcd
AND   DB.DEPENDENT_BENEF = HD.DEPENDENT_BENEF
AND   DB.EMPLID          = DNID.EMPLID(+)
AND   DB.DEPENDENT_BENEF = DNID.DEPENDENT_BENEF(+)
AND   HD.PLAN_TYPE  IN ( '11' , '16' )			!VENDTKK ITG#45098 added PLAY_TYPE '16' ! vendkxy commented the code
!AND    HD.PLAN_TYPE      = $plan_type        !Vendkxy added the code to pick correct cancel date
!AND   HD.EFFDT           = $HB_EFFDT					! vendkxy commented the code
AND   HD.EFFDT           >= $HB_EFFDT				! vendkxy uncommented the code
!Vendkxy added the code
and   cd.plan_type  =  HD.PLAN_TYPE
and   HD.emplid   = cd.emplid
and  cd.coverage_elect <> 'W'
AND   CD.COVERAGE_BEGIN_DT =  (SELECT MAX(COVERAGE_BEGIN_DT) 
                   FROM PS_HEALTH_BENEFIT
                   WHERE CD.EMPLID = EMPLID
                   AND CD.EMPL_RCD = EMPL_RCD
                   AND CD.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND CD.PLAN_TYPE = PLAN_TYPE
                   AND CD.BENEFIT_NBR = BENEFIT_NBR
                   AND COVERAGE_BEGIN_DT  <= $asofdate)
!Vendkxy added the code                   
ORDER BY HD.EMPLID, DB.DEPENDENT_BENEF

end-select
end-procedure

!*********************************************************************
begin-procedure after-dep-benef
#debug9 Show 'after-dep-benef'
  Do create-dependent-rec
  if ($stud_over_age = 'N' and $non_stud_over_age = 'N')
    do insert-rec
  end-if
end-procedure

!vendkxy added the code
!*************************************************************************
begin-procedure get-dependent-coverage-date
#debug9 Show 'get-dependent-coverage-date'

begin-select ON-ERROR=SQL-Error-Found('Get-cancel-date')
CD.COVERAGE_BEGIN_DT		&COVERAGE_BEGIN_DT
   move &COVERAGE_BEGIN_DT to $dep_coverage_begin_dt
FROM  PS_HEALTH_BENEFIT CD
WHERE CD.EMPLID       = $EMPLID
AND   CD.PLAN_TYPE = $dep_plan_type
AND   CD.COVERAGE_BEGIN_DT =  (SELECT MAX(COVERAGE_BEGIN_DT) 
                   FROM PS_HEALTH_BENEFIT
                   WHERE CD.EMPLID = EMPLID
                   AND CD.EMPL_RCD = EMPL_RCD
                   AND CD.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND CD.PLAN_TYPE = PLAN_TYPE
                   AND CD.BENEFIT_NBR = BENEFIT_NBR
                   AND COVERAGE_BEGIN_DT  <= $asofdate)
end-select
end-procedure
!*************************************************************************

begin-procedure Create-dependent-rec
#debug9 Show 'create-dependent-rec'
!  add 1 to #seq_nbr
  let $stud_over_age = 'N' 
  let $non_stud_over_age = 'N'
  move $dependent_benef to #seq_nbr 
  let $marital_status = ' '
  let $sex = $dep_sex
  do get-relationship
  if rtrim($dep_ssn,' ') = ''
    let $dep_ssn = '000000000'
  end-if
  let $dep_first_name = rpad($dep_first_name,25,' ')
  let $dep_middle_name = rpad($dep_mid_name,25,' ')
  let $dep_last_name = rpad($dep_last_name,35,' ')
  let $dep_prefix = rpad($dep_prefix,10,' ')
  let $dep_suffix = rpad($dep_suffix,10,' ')
  let $year4 = '1'
  do Format-datetime($dep_birthdate,$dep_birth_Date,{DEFCMP},'','')

  
  if $same_address <> 'Y'
    let $dep_address1 = rpad($dep_address1,55,' ')
    let $dep_address2 = rpad($dep_address2,55,' ')
    let $dep_city     = rpad($dep_city,30,' ')
    do get-dep-zip-code 
      if (rtrim($dep_address1,' ') = '' and rtrim($dep_city,' ') = '')
	    let $dep_address1 = $address1
	    let $dep_address2 = $address2
	    let $dep_city     = $city
	    let $dep_state    = $state
	    let $dep_zip      = $zip
	    let $dep_zip_suff = $zip_suff
	    let $dep_zip_6    = $zip_6
      end-if
  else
    let $dep_address1 = $address1
    let $dep_address2 = $address2
    let $dep_city     = $city
    let $dep_state    = $state
    let $dep_zip      = $zip
    let $dep_zip_suff = $zip_suff
    let $dep_zip_6    = $zip_6
  end-if
  let $product_line = 'D'
  

 ! let $dep_effdt = $applic_effdt  !vendkxy commented the code
 
 !vendkxy added the code
 do get-dependent-coverage-date				
 do Convert-To-DTU-Date($dep_coverage_begin_dt,$dep_covrg_Dt_dtu)
 let $covrg_yy = substr($dep_covrg_Dt_dtu,1,4)
 let $covrg_mm = substr($dep_covrg_Dt_dtu,6,2)
 let $covrg_dd = substr($dep_covrg_Dt_dtu,9,2)
 let $dep_coverage_begin_dt = $covrg_yy||$covrg_mm||$covrg_dd
 let $dep_effdt =$dep_coverage_begin_dt	
 !vendkxy added the code
 
  let $dep_cancel_dt = '00000000'
  !if $relation_cd <> '01'			!VENDTKK   ITG#45098     20-NOV-2007  commented 
  
 ! GEX ISDVNPK 12/25/2010 Commenetd below to remove the Student criteria for OE11 changes
 ! if ($relation_cd <> '01' AND $relation_cd <>  '53' )		!VENDTKK   ITG#45098     20-NOV-2007
 !  if $dep_student = 'Y'
 !     if $dep_disabled <> 'Y'
 !       let $stud_over_age = 'N' 
 !       do calc-stud-age
 !     end-if
 !   end-if
 ! end-if
 ! GEX ISDVNPK 12/25/2010 Commenetd above to remove the Student criteria for OE11 changes
 
  if $dep_disabled = 'Y'
    let $HIPAA_resp_ind = 'Y'
    let $rec_type = '4'
  else
    let $HIPAA_resp_ind = 'N'
  end-if
  if $dep_cancel_dt = '00000000'
    let $cancel_reason_cd = '  '
  else
    let $cancel_reason_cd = 'AI'
  end-if
  let $Part_A_end_dt = '00000000'
  
   !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
   !do Get-Gex-Mapp-EmpIid($emplid , $EmplId_Out)		!VENDTKK     ITG#44219    10/10/2007
   let $EmplId_Out=$emplid
   !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
   ! MXR 02/06/2009  Bug Fix  zero fill if the $dep_birth_date is blank
   if $dep_birth_date = ''
      let $dep_birth_date = '00000000'
   end-if
   	

  let $out1 = $contract_id||$rec_type||$dep_last_name||$dep_first_name||
	$dep_middle_name||$dep_prefix||$dep_suffix||$dep_sex||$dep_birth_date||
	$dep_address1||$dep_address2||$dep_city||$dep_state||$dep_zip||
	$dep_zip_suff||$dep_zip_6||$rec_group_nbr||$product_line||$dep_effdt||
	$dep_cancel_dt||$HIPAA_resp_ind||$relation_cd||
	$cancel_reason_cd||$Part_A_end_dt||$dep_ssn||$EmplId_Out                  !VENDTKK    ITG#44219    10/10/2007
  let $out1 = rpad($out1,1000,' ')
  
   
  add 1 to #total_recs

  !if $relation_cd <> '01'			!VENDTKK   ITG#45098     20-NOV-2007  commented 
  if ($relation_cd <> '01' AND $relation_cd <>  '53' )		!VENDTKK   ITG#45098     20-NOV-2007
    if $dep_disabled <> 'Y'
   ! GEX ISDVNPK 12/25/2010 Commenetd below remove the Student criteria for OE11 changes
     ! if $dep_student <> 'Y'  
   ! GEX ISDVNPK 12/25/2010 Commenetd above to remove the Student criteria for OE11 changes
        let $non_stud_over_age = 'N' 
        do calc-non-stud-age
     ! end-if    ! GEX ISDVNPK 12/25/2010 Commenetd to remove the Student criteria for OE11 changes
    end-if
  end-if
end-procedure

begin-procedure get-dep-zip-code 
  let $dep_zip = substr($dep_postal,1,5)
  if rtrim($dep_zip,' ') = ''
    let $dep_zip = $5_spaces
  end-if 
  let $dep_zip_suff = '    '
  let $dep_zip_6    = '      '
end-procedure
!*********************************************************************
begin-procedure get-relationship
!*********************************************************************
! Deepak, FC, 12/03/2007, Made changes due to 9.0 upgrade (XLAT values)

  let $rec_type = '3'
  evaluate $dep_relationship
    when = 'SP'
     let $relation_cd   = '01'
     let $rec_type = '2'
     break
    when = 'P'			! FA and M = P
     let $relation_cd   = '03'
     break
    when = 'GP'			! GF and GM = GP
     let $relation_cd   = '04'
     break
    when = 'GC'			! GS and GD = GC 
     let $relation_cd   = '05'
     break
    when = 'R'			! A and U = R
     let $relation_cd   = '06'  ! NI and NE became R and we changed oddball NC to R
     break			! no NN data in 8.3, We changed the oddball C to R
    when = 'FC'			! XD and XS = FC
     let $relation_cd   = '10'
     break
    ! Per Adeja/Molly 12/14/07, not using BL, SL, MI, ML, FI, FL
    !when = 'BL'			! not sure
    !when = 'SL'			! not sure
    ! let $relation_cd   = '12'
    ! break
    !when = 'PI'			! MI, ML, FI, FL = PI 
    ! let $relation_cd   = '13'
    ! break
    when = 'SB'			! B and SI = SB
     let $relation_cd   = '14'
     break
    when = 'C'			! D and S = C
    when = 'NC'						!VENDTKK   ITG#45098     20-NOV-2007
     let $relation_cd   = '19'
     break
    when = 'SC'			! SS and SD = SC
     let $relation_cd   = '17'
     break
    when = 'NA'					        !VENDTKK   ITG#45098     20-NOV-2007
     let $relation_cd   = '53'
     let $rec_type = '2'
     break
    when-other 
     show 'Dep Relationship unknown: ' $dep_relationship
     show 'For EMPLID: ' $emplid
     break
  end-evaluate
end-procedure
!*********************************************************************
Begin-procedure calc-stud-age
#debug9 show 'calc-stud-age'

!  do Format-datetime($report_date,$report_Date_dbf,{DEFMDY},'','native')
!  do Convert-To-DTU-Date($report_Date_dbf,$report_Date_dtu)
  do Convert-To-DTU-Date($dep_birthDate,$dep_birthDate_dtu)!
  do dtu-month-begin($asofdate_dtu,$month_begin_dtu)
  do Dtu-Diff-Years($dep_birthdate_dtu,$month_begin_dtu,#Age_Yrs)
  let $stud_over_age = 'N'
  do get-stud-age-limit

  do dtu-add-years($dep_birthdate_dtu,19,$19th_birthday_dtu)  
  do dtu-add-months($19th_birthday_dtu,1,$elig_effdt_dtu)  
  let $elig_effdt_dtu = substr($elig_effdt_dtu,1,8)||'01'
  do convert-from-dtu-date($elig_effdt_dtu,$elig_effdt_1)
  let $year4 = '1'
  do Format-datetime($elig_effdt_1,$elig_effdt,{DEFMDY},'','')
  do Convert-To-DTU-Date($hb_coverage_begin_dt,$covrg_begin_Date_dtu)
  let $year4 = '1'
  do Format-datetime($hb_coverage_begin_dt,$coverage_begin_Date,{DEFCMP},'','')

  if $elig_effdt_dtu > $covrg_begin_Date_dtu
     let $elig_effdt = $elig_effdt
  else
     let $elig_effdt = $coverage_begin_Date
  end-if

  if #Age_Yrs >= #stud_age_limit
    let $stud_over_age = 'Y'
    let $dep_name = rtrim($dep_first_name,' ')||' '||rtrim($dep_mid_name,' ')||
    		' '||rtrim($dep_last_name,' ')
    let $year4 = '1'
    do Format-datetime($dep_birthdate,$dep_birth_Dt,{DEFMDY},'','')
    #debug9 show '-------------------------------------------------------------------'  
    #debug9 show 'Student age greater than student age limit for program ' $benefit_program ' - enrollment not sent.'
    !show 'EE SSN: ' $emplid   '   Group nbr: ' $group_nbr		!vendtkK
    
    do Get-Gex-Mapp-EmpIid($contract_ID,$EmplId_Out)
    #debug9 show 'EE SSN: ' $contract_ID   '  EMPLID: ' $EmplId_Out   '   Group nbr: ' $group_nbr	!VENDTKK ITG#44219  
    #debug9 show 'Dependent name: ' $dep_name ', Dep SSN: ' $dep_ssn 
    #debug9 show 'Dependent birthdate: ' $dep_birth_Dt 
    if #current-line > 55
      new-page
    end-if
    let $prt1 = 'Student age greater than student age limit for program '||$benefit_program|| ' - enrollment not sent.'
    print $prt1    (+1,5)
    !let $prt1 = 'EE SSN: '||$emplid||', Group nbr: '||$group_nbr		!VENDTKK ITG#44219 
    let $prt1 = 'EE SSN: '||$contract_ID ||  ' EMPLID: ' || $EmplId_Out ||' Group nbr: '||$group_nbr    !VENDTKK ITG#44219 
    print $prt1    (+1,5)
    let $prt1 = 'Dep name: '||$dep_name||', Dep SSN: '||$dep_ssn||', Birthdate: '||$dep_birth_Dt
    print $prt1    (+1,5)
    let $prt1 = ' '
    print $prt1    (+1,5)
    add 1 to #over_stud_age_ctr
  end-if
      if #Age_Yrs < 19
        let $dep_name = rtrim($dep_first_name,' ')||
  		' '||rtrim($dep_mid_name,' ')||' '||rtrim($dep_last_name,' ')
        let $year4 = '1'
        do Format-datetime($dep_birthdate,$dep_birth_Dt,{DEFMDY},'','')
        let $dep_ssn = substr($dep_ssn,1,9)
          #debug9 show '-------------------------------------------------------------'  
          #debug9 show ' Dep coded as student in database, but under age 19 - transmitted as non-student.'
          !show 'EE SSN: ' $emplid   '   Group nbr: ' $group_nbr           !Vendtkk
          #debug9 show 'EE SSN: ' $contract_ID   '  EMPLID: ' $EmplId_Out   '   Group nbr: ' $group_nbr	!VENDTKK ITG#44219 
          #debug9 show 'Dependent name: ' $dep_name ', Dep SSN: ' $dep_ssn 
          #debug9 show 'Dependent birthdate: ' $dep_birth_Dt 
          add 1 to #under_stud_age_ctr
          if #current-line > 55
            new-page
          end-if
          let $prt1 = 'Dep coded as student in database, but under age 19 - transmitted as non-student.'
          print $prt1 (+1,5)
         ! let $prt1 = 'EE SSN: '||$emplid||', Group nbr: '||$group_nbr
         let $prt1 = 'EE SSN: '||$contract_ID ||  ' EMPLID: ' || $EmplId_Out ||' Group nbr: '||$group_nbr    !VENDTKK ITG#44219 
          print $prt1 (+1,5)
          let $prt1 = 'Dep name: '||$dep_name||', Dep SSN: '||$dep_ssn||', Birthdate: '||$dep_birth_Dt 
          print $prt1 (+1,5)
          let $prt1 = ' '
          print $prt1 (+1,5)
      end-if
end-procedure

begin-procedure get-stud-age-limit
begin-select 
BDP.STUDENT_AGE_LIMIT 
   move &BDP.STUDENT_AGE_LIMIT to #stud_age_limit
from PS_BEN_DEFN_PGM BDP
  where BDP.BENEFIT_PROGRAM = $benefit_program
    and BDP.EFF_STATUS = 'A'
    and BDP.EFFDT =
     (select max(effdt)
      from ps_ben_defn_pgm   
      where BDP.benefit_program = benefit_program
        and BDP.EFF_STATUS = eff_status 
        and effdt <=$asofdate)
end-select
end-procedure
!*********************************************************************
Begin-procedure calc-non-stud-age
#debug9 show 'calc-non-stud-age'

!  do Format-datetime($report_date,$report_Date_dbf,{DEFMDY},'','native')
!  do Convert-To-DTU-Date($report_Date_dbf,$report_Date_dtu)
  do Convert-To-DTU-Date($dep_birthDate,$dep_birthDate_dtu)!
  do dtu-month-begin($asofdate_dtu,$month_begin_dtu)
  do Dtu-Diff-Years($dep_birthdate_dtu,$month_begin_dtu,#dep_age_Yrs)
  do get-dep-age-limit

  if #dep_Age_Yrs >= #dep_age_limit
    let $non_stud_over_age = 'Y' 
    let $dep_name = rtrim($dep_first_name,' ')||' '||rtrim($dep_mid_name,' ')||
		' '||rtrim($dep_last_name,' ')
      let $year4 = '1'
      do Format-datetime($dep_birthdate,$dep_birth_Dt,{DEFMDY},'','')
      do Get-Gex-Mapp-EmpIid ($national_id, $EmplId_Out)
       #debug9 show $dashes  
       #debug9 show 'Dependent age greater than age limit for program ' $benefit_program ' - enrollment not sent.'
       !show 'EE SSN: ' $national_id  '  Group nbr: ' $group_nbr  vendtkk
       #debug9 show 'EE SSN: ' $national_id   '  EMPLID: '$EmplId_Out   ' Group nbr: ' $group_nbr	!VENDTKK ITG#44219 
       #debug9 show 'Dependent Name: ' $dep_name ', Dep SSN: ' $dep_ssn
       #debug9 show '$dep_birthdate: ' $dep_birth_Dt
       #debug9 show $dashes  
       if #current-line > 55
         new-page
       end-if
       let $prt1 = 'Dependent age greater than age limit for program '||$benefit_program|| ' - enrollment not sent.' 
       print $prt1 (+1,5)
       !let $prt1 = 'EE SSN: ' ||$national_id||  '  Group nbr: ' ||$group_nbr		!vendtkk
       let $prt1 = 'EE SSN: ' ||$national_id|| 'EMPLID: '||$EmplId_Out  ||  '  Group nbr: ' ||$group_nbr		!VENDTKK ITG#44219 
       print $prt1 (+1,5)
       let $prt1 = 'Dependent Name: ' ||$dep_name|| ', Dep SSN: ' ||$dep_ssn||', Birthdate: '||$dep_birth_Dt
       print $prt1 (+1,5)
       let $prt1 = ' '
       print $prt1 (+1,5)
  end-if
end-procedure

begin-procedure get-dep-age-limit
begin-select 
BDP1.DEP_AGE_LIMIT 
   move &BDP1.DEP_AGE_LIMIT to #dep_age_limit
from PS_BEN_DEFN_PGM BDP1
  where BDP1.BENEFIT_PROGRAM = $benefit_program
    and BDP1.EFF_STATUS = 'A'
    and BDP1.EFFDT =
     (select max(effdt)
      from ps_ben_defn_pgm   
      where BDP1.benefit_program = benefit_program
        and BDP1.EFF_STATUS = eff_status 
        and effdt <=$asofdate)
end-select
end-procedure

!*********************************************************************
begin-procedure select-from-curr
#debug9 Show 'select-from-curr'
begin-select
CURR.PORTAL_PATH

  let $temp1 = &CURR.PORTAL_PATH

  do write-record

from PS_GEX_UCCI_CURR CURR
ORDER BY CURR.EMPLID,CURR.SEQ_NBR
end-select
end-procedure

!*********************************************************************
! Write Record for UCCI File
!*********************************************************************
begin-procedure Write-Record
#debug9 Show 'write-record'
write 1 from 

  $temp1:1000

end-procedure

!*********************************************************************
! Open the file for writing              
!*********************************************************************
Begin-procedure Open-file
#debug9 Show 'open-file'

let $filename = '{OUTFILE}'||'gexbn619.dat'
open $filename as 1

     for-writing record=1000

if #writestat != 0
   display 'Error Opening output file.  Program terminating.'
   stop
end-if

end-procedure

!*********************************************************************
! Close the file               
!*********************************************************************
Begin-procedure Close-file
#debug9 Show 'close-file'
  close 1
end-procedure

!*********************************************************************
!Displays the error messages and stops execution
!*********************************************************************
 
begin-procedure SQL-Error-Found($Proc_Name) 
!  do error-found
  show 'SQL Error in ' $Proc_Name 
  show '$_sql-error: ' $_sql-error
  show 'sql-status:  '  #_sql-status 

  STOP

end-procedure

!*********************************************************************
!  HEADER RECORD
!*********************************************************************
begin-procedure create-header-rec
#debug9 Show 'create-header-rec'
  move 0 to #seq_nbr
  add 1 to #total_recs
  move #contract_ctr to $contract_count 09999999
  move #total_recs to $total_recs 09999999
  let $type_code = 'FULL '
  let $customer_id = '000013116'  

  write 1 from  $9_spaces:9
		'0'
		$customer_id:9
		$create_date:8
		$create_time:6
		$contract_count:8
		$total_recs:8
		$type_code:5
		$hdr_elig_date:8
		$938_spaces:938
end-procedure


!*********************************************************************
!  Insert rows into PS_GEX_UCCI_CURR
!*********************************************************************
Begin-procedure Insert-rec
#debug9 Show 'insert-rec'
 
!  do display-variables
!    move #seq_nbr to $seq_nbr 0999 
!    let $short_string = substr($out1,1,50)
!    let $tmp1 = 'Row: ' ||$contract_id|| ' ' ||$seq_nbr || ' ' ||$short_string
!    show $tmp1
Begin-sql
  insert into PS_GEX_UCCI_CURR 
    values($contract_id,#seq_nbr,$out1)
end-sql

end-procedure  
!*********************************************************************
begin-procedure check-for-cancels
begin-select
gup.seq_nbr
gup.portal_path

  let #rev_seq_nbr = &gup.seq_nbr
  let $rev_record = &gup.portal_path
  if substr($rev_record,306,8) = '00000000'
    do create-revised-record
  end-if

  from PS_GEX_UCCI_PRIOR GUP
  where not exists
    (select 'X'
     from PS_GEX_UCCI_CURR GUC
     where guc.emplid = gup.emplid
      and guc.seq_nbr = gup.seq_nbr)
   order by gup.emplid,gup.seq_nbr
end-select
end-procedure

!VENDKXY added the code
begin-procedure cancel-dep-coverage-dt
show 'cancel-dep-coverage-dt'
begin-select distinct
hd.plan_type  &cancel_plan_type

   Let $plan_type= &cancel_plan_type

from PS_HEALTH_DEPENDNT hd 
 where hd.emplid=$cancel_EMPLID
 and hd.dependent_benef=#rev_seq_nbr		
 and hd.plan_type in ('11','16')
 AND   hd.effdt =  (SELECT MAX(effdt) 
                   FROM PS_HEALTH_DEPENDNT
                   WHERE hD.EMPLID = EMPLID
                   AND hD.EMPL_RCD = EMPL_RCD
                   AND hD.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND hD.PLAN_TYPE = PLAN_TYPE
                   AND hD.BENEFIT_NBR = BENEFIT_NBR
                   AND effdt  <= $asofdate )	
end-select                   
end-procedure
!VENDKXY added the code


Begin-procedure Create-revised-record
#debug9 show 'Create-revised-record'
!#####################################################
  let $cancel_EMPLID = substr($rev_record,1,9)
  let $cancel_substr = substr($rev_record,1,124)
  !VENDKXY added the code
  if #rev_seq_nbr >= 1 			!vendkxy Changed the code for production fix s3 # 12656317
  do cancel-dep-coverage-dt 					
  end-if 
  !VENDKXY added the code
  do get-cancel-date
  let $year4 = '1'
  do Format-datetime($cd_coverage_begin_dt,$rev_cancel_dt,{DEFCMP},'','')
!#####################################################
  let $rev_contract_id = substr($rev_record,1,9)
  let $rev_out1 = substr($rev_record,1,305)||$rev_cancel_dt||
		substr($rev_record,314,3)||'AI'||substr($rev_record,319,682)
  do insert-revised-rec
  show ' '
  show 'Record with cancel date written for: ' $cancel_emplid
  show $cancel_substr
  add 1 to #cancel_ctr
  add 1 to #total_recs
end-procedure

!*************************************************************************
begin-procedure get-cancel-date
#debug9 Show 'get-cancel-date'

begin-select ON-ERROR=SQL-Error-Found('Get-cancel-date')
CD.EFFDT
CD.COVERAGE_BEGIN_DT
CD.COVERAGE_ELECT
   move &CD.COVERAGE_BEGIN_DT to $CD_coverage_begin_dt
   move &CD.COVERAGE_ELECT    to $CD_coverage_elect
FROM  PS_HEALTH_BENEFIT CD
WHERE CD.EMPLID       = $cancel_EMPLID
!AND   CD.PLAN_TYPE    IN ( '11' , '16' )			!VENDTKK ITG#45098 added PLAY_TYPE '16'
AND   CD.PLAN_TYPE = $plan_type        !Vendkxy added the code to pick correct cancel date
AND   CD.COVERAGE_BEGIN_DT =  (SELECT MAX(COVERAGE_BEGIN_DT) 
                   FROM PS_HEALTH_BENEFIT
                   WHERE CD.EMPLID = EMPLID
                   AND CD.EMPL_RCD = EMPL_RCD
                   AND CD.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND CD.PLAN_TYPE = PLAN_TYPE
                   AND CD.BENEFIT_NBR = BENEFIT_NBR
                   AND COVERAGE_BEGIN_DT  <= $asofdate)
!                   AND COVERAGE_BEGIN_DT  between $asofdate and $plus30_date)
!                   AND COVERAGE_BEGIN_DT  <= $plus30_date)

end-select
end-procedure
!*********************************************************************
!  Insert revised rows into PS_GEX_UCCI_CURR
!*********************************************************************
Begin-procedure Insert-revised-rec
#debug9 Show 'insert-revised-rec'
	
Begin-sql
  insert into PS_GEX_UCCI_CURR 
    values($rev_contract_id,#rev_seq_nbr,$rev_out1)
end-sql
end-procedure  

!########################################
begin-procedure display-variables
    show '########################################'
    show '$contract_id: ' $contract_id
    show '$emplid: ' $emplid
    show '$rec_type: ' $rec_type
    show '$benefit_plan: ' $benefit_plan
    show '$first_name: ' $first_name
    show '$last_name: ' $last_name
    show '$prefix: ' $prefix
    show '$suffix: ' $suffix
    show '$sex: ' $sex
    show '$relation_cd: ' $relation_cd
    show '########################################'
end-procedure
!########################################
begin-procedure finalization
  do close-file

  date-time () MM/DD/YYYY &dayEnded
  date-time () hh:mi:ss &timeEnded
  move #contract_ctr to $contract_ctr_disp 999,999
  move #cancel_ctr to $cancel_ctr_disp 999,999
  move #total_recs to $total_recs_disp 999,999
  show ' '
  show 'Total number of contracts written:        ' $contract_ctr_disp
  show 'Records written to file,including header: ' $total_recs_disp
  show ' '
  show 'Total number of cancel recs written:      ' $cancel_ctr_disp
  show ' '
  show 'Report Ended on ' &dayEnded ' at ' &timeEnded

end-procedure

#include 'askaod.sqc'    !Ask as of date
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'rotname3.sqc'
#include 'getgeid.sqc'		!VENDTKK       ITG#44219    10/10/2007
#include 'gexxx920.sqc'  !Get ben single row run control			!VENDTKK ITG#45098 20-NOV-2007 
