!**************************************************************************************************
! GEXPY218 : 		Local 880 Remaining Vac HRS		                          *
!**************************************************************************************************
!                                                                                                 *
!               	Confidentiality Information:                                              *
!                                                                                                 *
! 	This module is the confidential and proprietary information of                            *
! 	Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted                      *
! 	in any form, by any means, in whole or in part, nor is it to be used                      *
! 	for any purpose other than that for which it is expressly provided                        *
! 	without the written permission of Giant Eagle.                                            *
!                                                                                                 *
! 	Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved                              *
!                                                                                                 *
!**************************************************************************************************
!                                                                                                 *        
! GEXPY218 :    	Local 880 Remaining Vac HRS		                          *
!                                                                                                 *        
! Narrative:            This program creates a report to show the remaining vacation time	  *
!			 available with each TMs.			                          *
!                                                                                                 *
!                                                                                                 *  
! SQL Tables:           PS_JOB                                                                    *
!                       PS_PERSONAL_DATA                                                          *
!                       PS_EMPLOYMENT                                                             *
!                       PS_ADDL_PAY_DATA                                                          *
!                       PS_EARNINGS_BAL                                                           *
!                                                                                                 *
! Written by:           Manjiri Deo                                                               *       
!                                                                                                 *
! Normally Run:         Weekly                                                                    *
!                                                                                                 *
!**************************************************************************************************
#include 'setenv.sqc' !Set environment

Begin-Report
#debug9 Show 'Begin-Report'

  Do P100-Initialization
  do delete-prior-entries
  Do P200-Main-Process
  Do Stdapi-Term             !stdapi.sqc
  
End-Report
!****************************************************************************
Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Let $AsOfDate = $AsOfToday
  Let #Current_Year  = To_Number(Substr($AsOfDate,8,4))
  Let #Current_Year  =#Current_Year 
  Do Stdapi-Init             !stdapi.sqc
  
  Do P110-Process-Scheduler-Run-Controls
  
End-Procedure P100-Initialization
!****************************************************************************
Begin-Procedure P110-Process-Scheduler-Run-Controls
#debug9 Show 'P110-Process-Scheduler-Run-Controls'

  Let $GEXXX924_ERNCD_Alias = 'APD.ERNCD'
  Do GEXXX924-Select-ERNCD-Parameters 
  #debug8 show '$GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA=' $GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA

End-Procedure P110-Process-Scheduler-Run-Controls  
!*******************************************************************************************
Begin-Procedure P200-Main-Process
#debug9 show 'P200-Main-Process'


BEGIN-SELECT
J.DEPTID
J.EMPLID
J.EMPL_RCD
J.UNION_CD
APD.ERNCD

	Let $DEPTID  = Ltrim(RTrim(&J.DEPTID,' '),' ')
	Let $Emplid  = Ltrim(RTrim(&J.EMPLID,' '),' ')
	Let $EMPL_RCD  = &J.EMPL_RCD
	
	Let $Union_Cd = LTrim(RTrim(&J.UNION_CD,' '),' ')
	Let $Erncd = LTrim(RTrim(&APD.ERNCD,' '),' ') 
	
	If $Erncd = '515'
	  do process-515-erncd
	  do process-519-erncd
	  do process-524-erncd
	  do Insert-gex_vacation_hr
	end-if
	
	If $Erncd = '513'
	  do process-513-erncd
	  do process-519-erncd
	  do process-524-erncd
	  do Insert-gex_vacation_hr
	end-if

FROM PS_JOB J,
     PS_ADDL_PAY_DATA APD

WHERE J.EFFDT = (SELECT MAX(J_ED.EFFDT) FROM PS_JOB J_ED
		 WHERE J_ED.EMPLID = J.EMPLID
		 AND J_ED.EMPL_RCD = J.EMPL_RCD
		 AND J_ED.EFFDT <= SYSDATE)
AND J.EFFSEQ = (SELECT MAX(J_ES.EFFSEQ) FROM PS_JOB J_ES
		WHERE J_ES.EMPLID = J.EMPLID
		AND J_ES.EMPL_RCD = J.EMPL_RCD
		AND J_ES.EFFDT = J.EFFDT)
AND J.EMPLID = APD.EMPLID
AND J.EMPL_RCD = APD.EMPL_RCD
AND APD.EFFDT = (SELECT MAX(APD_ED.EFFDT) FROM PS_ADDL_PAY_DATA APD_ED
		  WHERE APD_ED.EMPLID = APD.EMPLID
		  AND APD_ED.EMPL_RCD = APD.EMPL_RCD
		  AND APD_ED.ERNCD = APD.ERNCD
		  AND APD_ED.ADDL_SEQ = APD.ADDL_SEQ
		  AND APD_ED.EFFDT <= SYSDATE)
AND APD.ADDL_SEQ = (SELECT MAX(APD_AS.ADDL_SEQ) FROM PS_ADDL_PAY_DATA APD_AS
		     WHERE APD_AS.EMPLID = APD.EMPLID
		     AND APD_AS.EMPL_RCD = APD.EMPL_RCD
		     AND APD_AS.ERNCD = APD.ERNCD
		     AND APD_AS.EFFDT = APD.EFFDT)		  
AND [$GEXXX924_INCLUDE_ERNCD_CRITERIA]
and J.EMPL_STATUS IN ('A','L','P','S') 
order by J.DEPTID
END-SELECT
         
End-Procedure P200-Main-Process
         
!**************************************************************************** 
!**************************************************************************** 
begin-procedure Insert-gex_vacation_hr
#debug9 show 'Insert-gex_vacation_hr'

if $union_cd = ''
   let $union_cd = ' '
end-if

Let #VAC_gex_entitle_hours = #HOURS_ENTITLED
Let #VAC_gex_taken_hours   = #HOURS_TAKEN
Let #VAC_gex_remain_hours  = #HOURS_REMAINING
Let #VAC_gex_pers_hours    = #PERS_HOURS
Let #VAC_gex_prev_hours    = #PREV_HOURS
Let #VAC_gex_buysell_hours = #BUYSELL_HOURS
 
begin-sql
 
insert into ps_gex_vacation_hr
       (emplid,
        empl_rcd,
        deptid,
        union_cd,
        gex_entitle_hours,
        gex_taken_hours,
        gex_remain_hours,
        gex_pers_hours,
        gex_prev_hours,
        gex_buysell_hours)
values ($emplid,                             !   EMPLID
        $empl_rcd,                           !   EMPL_RCD
        $deptid,                             !   DEPTID
        $union_cd,                           !   UNION_CD
        #VAC_gex_entitle_hours,              !   GEX_ENTITLE_HOURS
        #VAC_gex_taken_hours,                !   GEX_TAKEN_HOURS
        #VAC_gex_remain_hours,               !   GEX_REMAIN_HOURS
        #VAC_gex_pers_hours,                 !   GEX_PERS_HOURS
        #VAC_gex_prev_hours,                 !   GEX_PREV_HOURS
        #VAC_gex_buysell_hours);             !   GEX_BUYSELL_HOURS
 
end-sql
 
end-procedure
!**********************************************************************
begin-procedure delete-prior-entries
!**********************************************************************
#debug9 show 'delete-prior-entries'

begin-sql
delete from sysadm.PS_GEX_VACATION_HR 
end-sql

end-procedure

!*********************************************************************
!This displays all the sql errors occurred in the program
!*********************************************************************
begin-procedure SQL-Error-Found($Proc_Name) 

  show 'SQL Error in '$Proc_Name
  show 'sql-status:'#_sql-status

end-procedure

!****************************************************************************    
#include 'gexxx924.sqc'  !Get erncd multiple row table (for reg earnings codes)
#include 'gexpy513.sqc'  !Processing for 513 Earning Code
#include 'gexpy515.sqc'  !Processing for 515 Earning Code
#include 'gexpy519.sqc'  !Processing for 519/522/523 Earning Code
#include 'gexpy524.sqc'  !Processing for 524 Earning Code
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'askaod.sqc'    !Ask-As-Of-Date routine
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'getgeid.sqc'  

