!***********************************************************************
! GEXHR601:  Load Warehouse Region Code to Department Table            *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXHR601 :    Warehouse Region Code Load to PS Dept Tbl and Audit Rpt*
!                                                                      *
! Narrative:    This SQR is run nightly after the warehouse customer   *     
!               table is updated.  It selects all depts & their region *
!               codes from the customer table where dsply_str_sls_fg   *
!               = 'Y'.   Since the dept# is 4 numeric char, (but with  *
!               no leading zeros, the sqr then converts the dept# to a *
!               format with leading zeros.  It then looks up that dept#*
!               on the P.Soft department table and determines if the   *
!               region codes match.  If not, it updates the P.Soft     *
!               PS_GEX_DEPT_TBL region code with the value from the    *
!               customer table and writes a report line.               * 
!                                                                      *
! #Debugx Used: #debug9 paragraph trace                                *
!               #debug8 key variable values                            *
!               #debug7 data returned from sql calls                   *
!                                                                      *
! SQL Tables:   ps_gex_dept_tbl                                        *   
! DBS Tables:   DS001.Customer@DSSPROD5  *** Retired 12/2003***        *
!               DS001.Region@DSSPROD5    *** Retired 12/2003***        *
! DBS Tables:   OD001.Customer@ODPROD1                                 *
!               OD001.Region@ODPROD1                                   *
! Report    :   Audit Report                                           *
! Written by:   Susan Mahady                                           *
!                                                                      *
! Normally Run: Daily, runs < 1 minute.                                *
!                                                                      *
! Control Parms FOR WHERE CLAUSE: none                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS      DATE       DESCRIPTION OF THE CHANGE                   *
!***********************************************************************
! SCM           5/15/2000  Initial Creation                            *
!                                                                      *
! SCM           1/7/2002   Modified sqr to update the record when the  *
!                          regional manager name changes also.  Added  *
!                          old record detail to the audit report and   *
!                          added totals.                               *
!                                                                      *
! AXL HR-10592  12/8/2003  Changed the DB link to the DW tables.       *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)

#define #column1     1
#define #column2    10
#define #column3    25
#define #column4    50               !SCM 1/7/2002  Added column4
#define #column5    65               !SCM 1/7/2002  Added column5

Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report

Begin-Heading 7
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'
!   SCM  1/7/2001     Added 2 more columns for the Old region code and Old region mgr.
  
  Print 'Updated'            (+2,{#column2})
  Print 'Updated'            (+0,{#column3})
  Print 'Old'                (+0,{#column4})
  Print 'Old'                (+0,{#column5})
  Print 'DEPTID'             (+1,{#column1})
  Print 'Region Code'        (+0,{#column2})
  Print 'Region Manager'     (+0,{#column3})
  Print 'Region Code'        (+0,{#column4})
  Print 'Region Manager'     (+0,{#column5})
  
  Print '-'                  (+1,{#column1},6)  Fill
  Print '-'                  (+0,{#column2},11) Fill
  Print '-'                  (+0,{#column3},20) Fill
  Print '-'                  (+0,{#column4},11) Fill
  Print '-'                  (+0,{#column5},20) Fill
End-Heading

!***************************************
Begin-Procedure P100-Initialization
!***************************************
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXHR601'
  Let $ReportTitle   =   'Department Table Audit - Region Code Update'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '

End-Procedure

!***************************************
Begin-Procedure P200-Main-Process
!***************************************
#debug9 Show 'P200-Main-Process'
do P230-Dept-Select                 !select all departments from customer file
  
End-Procedure

!***************************************
Begin-Procedure P230-Dept-Select
!***************************************
#debug9 Show 'P230-Dept-Select'
   
  Let #recs-read = 0
  Let #recs-printed = 0
  Let #rows-updated = 0
  Let $Data-updated-flag = 'N'

Begin-Select
cus_act_no                                  !This is the deptid, it is 4 numeric
rgn_cd
rmgr_lst_nme

        Add 1 to #recs-read
        
        move &rgn_cd to $rgn_cd
        move &cus_act_no to $cus_act_no 9999
        !Do P235-Get-Region-Mgr             Get the region manager last name 
        move &rmgr_lst_nme to $rmgr_lst_nme 
        
        Do P240-Convert-Deptid              !convert deptid to put zeros in front of dept#              
        Let $Dept-Found = 'N'
        Do P250-Get-PS-Dept                 !lookup peoplesoft deptid to update region
        if $Dept-Found = 'Y'
 
! SCM  1/7/2002 rearranged this code logic and added logic if the regional mgr name changed.
! SCM  1/7/2002 ********   BEGIN       
!           If $rgn_cd = $depttbl_rgn_cd     !if these are = no need to update
!           else
!              do p270-update-record
!              do p250-print-audit            
!           end-if
            if $rgn_cd <> $depttbl_rgn_cd
                 do p270-update-record
                 do p250-print-audit                
            else
                 if $rmgr_lst_nme <> $depttbl_rmgr_lst_nme
                     do p270-update-record
                     do p250-print-audit
                 end-if
            end-if
                
! SCM 1/7/2002 *********   END              
        end-if

!From   DS001.Customer@dssprod5 ****AXL 12/8/2003
from customer			!AXL 12/8/2003 DBA set-up of a synonym pointing to odprod1				
                    
Where  dsply_str_sls_fg = 'Y' 
   and rmgr_lst_nme is not null !AXL 12/8/2003 Prevent sqr from aborting when mgr is blank
order by rgn_cd, cus_act_no 
 
End-Select
End-Procedure

!***************************************
!Begin-Procedure P235-Get-Region-Mgr
!***************************************
!begin-select
!rmgr_lst_nme
!   move &rmgr_lst_nme to $rmgr_lst_nme    
!From   DS001.Region@dssprod5
!where rgn_cd = $rgn_cd
!End-Select
!End-Procedure
  
!***************************************
Begin-Procedure P240-Convert-Deptid
!***************************************
!  cus_act_no coming in from the customer table is 4 numeric characters, but it is not
!  preceded with zeros.  In order to find the deptid on the peoplesoft gex_dept_tbl, it
!  must have zeros preceding the deptid.  This is a conversion to precede with zeros.

  Extract $1stchar from $cus_act_no 0 1       !extract 1st position of cus_act_no
  Extract $2ndchar from $cus_act_no 1 1       !extract 2nd position of cus_act_no
  Extract $3rdchar from $cus_act_no 2 1       !extract 3rd position of cus_act_no
  Extract $4thchar from $cus_act_no 3 1       !extract 4th position of cus_act_no
  
  if $3rdchar = ' '
     let $convert_deptid = '0' || '0' || '0' || $4thchar
  else
     if $2ndchar = ' '
        let $convert_deptid = '0' || '0' || $3rdchar || $4thchar
     else
        if $1stchar = ' '
            let $convert_deptid = '0' || $2ndchar || $3rdchar || $4thchar
        else
            let $convert_deptid = $cus_act_no
        end-if
     end-if
  end-if
  
End-Procedure

!***************************************
Begin-Procedure P250-Get-PS-Dept
!***************************************
#debug9 Show 'P250-Get-PS-Dept'
Begin-Select
GDT.DEPTID                            !this is the store number
GDT.EFFDT
GDT.SEQUENCE_NUMBER
GDT.GEX_RGN_CD
GDT.gex_rmgr_lst_nme

   Let $Dept-Found = 'Y'
   Move &GDT.GEX_RGN_CD to $depttbl_rgn_cd
   Move &GDT.DEPTID to $deptid
   Move &GDT.gex_rmgr_lst_nme to $depttbl_rmgr_lst_nme
      
FROM PS_GEX_DEPT_TBL GDT
 where gdt.deptid = $convert_deptid

 And gdt.effdt = 
      (Select max(d1.effdt) from
       ps_gex_dept_tbl d1
       Where d1.deptid = gdt.deptid
       and   d1.effdt <= $AsOfToday)

 And    gdt.sequence_number =
      (Select Max(d2.sequence_number)
       From   ps_gex_dept_tbl d2
       Where  d2.deptid       = gdt.deptid
       And    d2.effdt        = gdt.effdt)
End-Select

End-Procedure

!***************************************
Begin-Procedure P250-Print-Audit
!***************************************
#debug9 Show 'P250-Print-Audit'

  Print $deptid               (+1,{#column1})
  Print $rgn_cd               (+0,{#column2},30)
  print $rmgr_lst_nme         (+0,{#column3},20)     !SCM 1/7/2002  Changed from 38 to #column3
  Print $depttbl_rgn_cd       (+0,{#column4},30)     !SCM  1/7/2002  Added
  print $depttbl_rmgr_lst_nme (+0,{#column5},20)     !SCM  1/7/2002  Added
  
  Add 1 To #recs-printed
  
End-Procedure

!***************************************
Begin-Procedure P270-Update-Record
!***************************************
#debug9 Show 'P270-Update-Record'

  Begin-SQL On-Error=P290-SQL-Error('Update PS_GEX_DEPT_TBL')
    
     UPDATE PS_GEX_DEPT_TBL
        SET   GEX_RGN_CD = $Rgn_cd,
              gex_rmgr_lst_nme = $rmgr_lst_nme
     WHERE DEPTID = $deptid
      
  end-SQL
  Let $Data-Updated-Flag = 'Y'
  Add 1 to #rows-updated
    
End-Procedure

!***************************************
Begin-Procedure P290-SQL-Error($Action)
!***************************************
#debug9 Show 'P290-SQL-Error'
  Show $Action ' failed'
  Show 'SQL Error:  '  $_sql-error
  Show 'Deptid:  ' $DEPTID
  
  Stop
End-Procedure

!***************************************
Begin-Procedure P300-Finalization
!***************************************
#debug9 Show 'P300-Finalization'

  If $Data-updated-flag = 'N'  
    Print 'NO REGION CODES UPDATED FOR THIS REPORT RUN'	(25,) Center
  End-IF

  Move #recs-read To $recs-read 888,888,888
  Show $recs-read ' records read from input file'
  
  Move #recs-printed To $recs-printed 888,888,888
  Show $recs-printed ' records printed to exception report'

  Move #rows-updated To $rows-updated 888,888,888
  Show $rows-updated ' rows updated in ps_gex_deptid_tbl'
 
  Print '************* T O T A L S ******************'  (+2,1)                         !SCM 1/7/2002
  Print '#records read: '                      (+1,1)                         !SCM 1/7/2002
  print #recs-read                             (+0,40)   edit 999,999         !SCM 1/7/2002 
  Print '#records on audit report: '           (+1,1)                         !SCM 1/7/2002
  print #recs-printed                          (+0,40)   edit 999,999         !SCM 1/7/2002 
  Print '#rows updated in ps_gex_deptid_tbl: ' (+1,1)                         !SCM 1/7/2002
  print #rows-updated                          (+0,40)   edit 999,999         !SCM 1/7/2002 

! Rollback

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Procedure


#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
