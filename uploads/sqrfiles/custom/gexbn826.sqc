!**************************************************************************
!  GEXBN826:  Retrieve "Multiple" GEXBN826 Run Controls and               *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXBN826_Num_Months_Alias                                           *
!    $GEXBN826_Action_Alias                                               *
!    $GEXBN826_Action_Reason_Alias                                        *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXBN826_INCLUDE_CRITERIA                                           *
!    $GEXBN826_INCLUDE_DISPLAY_CRITERIA                                   *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $AsOfDate = $AsOfToday                                           *
!    Let $GEXBN826_Action_Alias = 'J.ACTION'                              *
!    Let $GEXBN826_Action_Reason_Alias = 'J.ACTION_REASON'                *
!    Let $GEXBN826_Num_Months_Alias = 'JOB.EFFDT'                         *
!    Do GEXBN826-Select-GEXBN826-Parameters                               *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXBN826_INCLUDE_CRITERIA]                                   *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		12/31/98 	INITIAL CREATION          *
!**************************************************************************

Begin-Procedure Get-GEXBN826-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_GEXBN826_ROWS = 0
  Let $Include_Condition    = ''
  Let $GEXBN826_INCLUDE_CRITERIA = ''
  Let $GEXBN826_INCLUDE_DISPLAY_CRITERIA = 'ALL'

Begin-Select

GEX_RC_GEXBN826.NUM_MONTHS
GEX_RC_GEXBN826.ACTION
GEX_RC_GEXBN826.ACTION_REASON

  Add 1 To #GEX_RC_GEXBN826_ROWS

  Let #Leave-Months = &GEX_RC_GEXBN826.NUM_MONTHS
  Let #Leave-Months = #Leave-Months * -1 
  Move #Leave-Months To $Leave-Months 8880

  If Rtrim(&GEX_RC_GEXBN826.ACTION, ' ') = ''
    Let $Include_Condition = '(' || $GEXBN826_Num_Months_Alias || ' <= ADD_MONTHS(''' || $AsOfDate || ''',' || $Leave-Months  || '))'
  Else
    If Rtrim(&GEX_RC_GEXBN826.ACTION_REASON, ' ') = ''
      Let $Include_Condition = '(' || $GEXBN826_Action_Alias || ' = ''' || &GEX_RC_GEXBN826.ACTION || ''' And '
                                   || $GEXBN826_Num_Months_Alias || ' <= ADD_MONTHS(''' || $AsOfDate || ''',' || $Leave-Months  || '))'
    Else
      Let $Include_Condition = '(' || $GEXBN826_Action_Alias || ' = ''' || &GEX_RC_GEXBN826.ACTION || ''' And '
                                   || $GEXBN826_Action_Reason_Alias || ' = ''' || &GEX_RC_GEXBN826.ACTION_REASON || ''' And '
                                   || $GEXBN826_Num_Months_Alias || ' <= ADD_MONTHS(''' || $AsOfDate || ''',' || $Leave-Months  || '))'
    End-If
  End-If

  If #GEX_RC_GEXBN826_ROWS = 1
    Let $GEXBN826_INCLUDE_CRITERIA = '(' || $Include_Condition
  Else
    Let $GEXBN826_INCLUDE_CRITERIA = $GEXBN826_INCLUDE_CRITERIA || ' Or '  || $Include_Condition
  End-If

From  PS_GEX_RC_GEXBN826 GEX_RC_GEXBN826

Where GEX_RC_GEXBN826.OPRID                 = [$OprID]
And   GEX_RC_GEXBN826.RUN_CNTL_ID           = [$Run_Cntl_ID]

End-Select

  If #GEX_RC_GEXBN826_ROWS = 0
    Let $GEXBN826_INCLUDE_CRITERIA = '1=1'
    Let $GEXBN826_INCLUDE_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXBN826_INCLUDE_CRITERIA = $GEXBN826_INCLUDE_CRITERIA || ')'
    Let $GEXBN826_INCLUDE_DISPLAY_CRITERIA = $GEXBN826_INCLUDE_CRITERIA
  End-If

End-Procedure
