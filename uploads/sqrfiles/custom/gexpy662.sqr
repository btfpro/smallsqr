!***********************************************************************
! GEXPY662.SQR:                                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-2006 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY662.SQR :                                                       *
!                                                                      *
! Narrative:            Create a report for active employees in        *
!                       American Seaway Foods (Cleveland RSC) showing  *
!                       both hours and earnings for REG, OTP, and 002  *
!                       (Double Time).  The report should list employee*
!                       level detail and subtotal all hours and        *
!                       earnings columns by jobcode and by department, *
!                       with a grand total for the entire report.      *
!			ITG-35724.				       *
!								       *
! #Debug Used:          x                                              *
!								       *
! SQL Tables:           PS_JOB, PS_JOBCODE_TBL, PS_PERSONAL_DATA,      *
!                       PS_PAY_EARNINGS, PS_PAY_OTH_EARNS              *
!								       *
! Written by:           Rapidigm, Deepak. S, 04/14/06                  *
!                                                                      *
! Normally Run:         This process will run weekly after the Tuesday *
!                       ('B' run) payroll cycle.                       *
!                                                                      *
! Control Parms:        Reads the run control table PS_GEX_RUN_CNTL    *
!								       *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! ISDVSXR  07/08/2008  ISDVSXR added the code for earn code 173        *                                                                         *
! ISDVMXD  03/18/2009  ITG 56990 - JObcode description changes         *                                                                   *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup02.sqc'   !Printer and Page-Size Initialization (Landscape)

#define #column1  001
#define #column2  008
#define #column3  019
#define #column4  038
#define #column5  050
#define #column6  075
#define #column7  088
#define #column8  103
#define #column9  116
!VENDAXG Added on 08/05/2008 for ITG 51358 - Begin		
!#define #column10  130
!#define #column11  150
#define #column10  126
#define #column11  140
!VENDAXG Added on 08/05/2008 for ITG 51358 - End
#define #column12  155	!VENDAXG Added on 08/05/2008 for ITG 51358		
#define #column13  170  !VENDAXG Added on 08/05/2008 for ITG 51358	

!***********************************************************************
! Start of Program
!***********************************************************************
Begin-Report

  print 'GEXPY662' (1,1)
  Print 'ASF Weekly Dept Pay'     (,10) center

  Print 'DEPTID'		(+1,{#column1})
  Print 'JOBCODE'		(+0,{#column2})
  Print 'JOB TITLE'		(+0,{#column3})
  print 'EMPLID'		(+0,{#column4})
  print 'NAME'			(+0,{#column5}) 
  print 'REG HOURS'		(+0,{#column6})
  print 'REG EARNINGS'		(+0,{#column7})	
  print 'OT HOURS'		(+0,{#column8})
  print 'OT EARNINGS'		(+0,{#column9})
  !VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
  print 'UOT HOURS'		(+0,{#column10})
  print 'UOT EARNINGS'		(+0,{#column11})
  !print 'DOUBLE TIME HOURS'	(+0,{#column10})	
  !print 'DOUBLE TIME EARNINGS'	(+0,{#column11})
  print 'DOUBLE TIME HOURS'	(+0,{#column12})	
  print 'DOUBLE TIME EARNINGS'	(+0,{#column13})
  !VENDAXG Modified on 08/05/2008 for ITG 51358 - End
  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Stdapi-Init             !stdapi.sqc
  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = 'gexpy662'
  end-if

  do Get-Run-Control	     !gexrnctl.sqc  

 if ltrim(rtrim(&thrudate,' '),' ') = ''
	do Select-Pay-End-Date
	let $thrudate = $xxx_pay_end_dt
  else
	let $thrudate = &thrudate
  end-if

  #debugx show '<' $thrudate '>'

  let $print-deptid-total = 'Y'
  let $print-report-total = 'Y'
  let $first = 'Y'
  do Initialize-Totals
  do Build-Dynamic-Where
  do Select-Data
  do End-of-Report-Printing
  do stdapi-term

End-Report


!********************************************************
! Procedure used to select current pay_end_dt 
! if none specified on the run control page
!********************************************************
begin-procedure Select-Pay-End-Date
begin-select distinct
max(xxx.pay_end_dt) &xxx.pay_end_dt
  
   move &xxx.pay_end_dt to $xxx_pay_end_dt
  
from ps_pay_calendar xxx
where xxx.pay_confirm_run = 'Y'
and xxx.company <= '999'
and xxx.pay_end_dt <= sysdate

end-select
end-procedure 



!***********************************************************************
! Main Procedure which selects the data.
!***********************************************************************
Begin-Procedure Select-Data
	#debugx show $GEXXX902_INCLUDE_DEPTID_CRITERIA
	#debugx show $GEXXX909_INCLUDE_ACCT_CD_CRITERIA
Begin-Select						
SUBSTR(A.DEPTID,1,4)			&deptid		
A.JOBCODE				&jobcode	
B.DESCR					&jobtitle
POS.POSITION_NBR			&position_nbr		!isdvmxd added for ITG 56990 on 03/18/2009
POS.DESCR				&position_title		!isdvmxd modified for ITG 56990 on 03/18/2009	
SUBSTR(A.EMPLID,1,9)			&emplid		
SUBSTR(C.NAME,1,20)			&name		
sum(SUBSTR(TO_CHAR(E.OTH_HRS),1,5))	&reghrs		
sum(SUBSTR(TO_CHAR(E.OTH_EARNS),1,7))	&regern		
sum(SUBSTR(TO_CHAR(F.OTH_HRS),1,5))	&othrs		
sum(SUBSTR(TO_CHAR(F.OTH_EARNS),1,7))	&otern		
sum(SUBSTR(TO_CHAR(G.OTH_HRS),1,5))	&dbltimehrs	
sum(SUBSTR(TO_CHAR(G.OTH_EARNS),1,7))	&dbltimeern
sum(SUBSTR(TO_CHAR(OTH.OTH_HRS),1,5))	&UOT_Hrs		!VENDAXG Added on 08/05/2008 for ITG 51358	
sum(SUBSTR(TO_CHAR(OTH.OTH_EARNS),1,7))	&UOT_Ern		!VENDAXG Added on 08/05/2008 for ITG 51358	



	move &deptid to $deptid
	move &jobcode to $jobcode

	if $first = 'Y'
		let $jobcode_old = $jobcode
		let $deptid_old = $deptid
		let $first = 'N'
	end-if

	if $jobcode <> $jobcode_old
		do Print-Jobcode-Total
		let $jobcode_old = $jobcode
	end-if
	
	if $deptid <> $deptid_old
		do Print-Deptid-Total
		let $deptid_old = $deptid
	end-if	

	move &reghrs to #reghrs
	move &regern to #regern
	move &othrs to $othrs 
	move &otern to $otern
	move &dbltimehrs to $dbltimehrs 
	move &dbltimeern to $dbltimeern
	move &UOT_Hrs to $UOT_Hrs 		!VENDAXG Added on 08/05/2008 for ITG 51358	
	move &UOT_Ern to $UOT_Ern		!VENDAXG Added on 08/05/2008 for ITG 51358	
	if $othrs = '' 
	   let #othrs = 0
	else 
	   move &othrs to #othrs 
	end-if

	if $otern = ''
	   let #otern = 0
	else
	   move &otern to #otern
	end-if
	
	if $dbltimehrs = ''
	   let #dbltimehrs = 0
	else
	   move &dbltimehrs to #dbltimehrs	
	end-if

	if $dbltimeern = ''
	   let #dbltimeern = 0	
	else
	   move &dbltimeern to #dbltimeern	
	end-if
	!VENDAXG Added on 08/05/2008 for ITG 51358 - Begin
	if $UOT_Hrs = '' 
	   let #UOT_Hrs = 0
	else 
	   move &UOT_Hrs to #UOT_Hrs 
	end-if

	if $UOT_Ern = ''
	   let #UOT_Ern = 0
	else
	   move &UOT_Ern to #UOT_Ern
	end-if
	!VENDAXG Added on 08/05/2008 for ITG 51358 - End
	do Print-Row

	let #jtot_reghrs = #jtot_reghrs + #reghrs
	let #jtot_regern = #jtot_regern + #regern
	let #jtot_othrs = #jtot_othrs + #othrs
	let #jtot_otern = #jtot_otern + #otern
	!VENDAXG Added on 08/05/2008 for ITG 51358 - Begin
	let #jtot_UOT_Hrs = #jtot_UOT_Hrs + #UOT_Hrs
	let #jtot_UOT_Ern= #jtot_UOT_Ern + #UOT_Ern
	!VENDAXG Added on 08/05/2008 for ITG 51358 - End
	let #jtot_dbltimehrs = #jtot_dbltimehrs + #dbltimehrs
	let #jtot_dbltimeern = #jtot_dbltimeern + #dbltimeern

FROM PS_JOB A, 
            PS_JOBCODE_TBL B, 
            PS_PERSONAL_DATA C, 
            PS_PAY_EARNINGS D,
            PS_PAY_OTH_EARNS E, 
            PS_PAY_OTH_EARNS F, 
            PS_PAY_OTH_EARNS G,
            PS_PAY_OTH_EARNS OTH, !VENDAXG Added on 08/05/2008 for ITG 51358
		! , PS_PAY_OTH_EARNS H
	    PS_POSITION_DATA POS	!isdvmxd added for ITG 56990 on 03/18/2009
	    	
  WHERE A.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_JOB A_ED
        WHERE A.EMPLID = A_ED.EMPLID
          AND A.EMPL_RCD = A_ED.EMPL_RCD
          AND A_ED.EFFDT <= SYSDATE)
    AND A.EFFSEQ =
        (SELECT MAX(A_ES.EFFSEQ) FROM PS_JOB A_ES
        WHERE A.EMPLID = A_ES.EMPLID
          AND A.EMPL_RCD = A_ES.EMPL_RCD
          AND A.EFFDT = A_ES.EFFDT)
     AND B.SETID = A.SETID_JOBCODE
     AND B.JOBCODE = A.JOBCODE
     AND B.EFFDT =
        (SELECT MAX(B_ED.EFFDT) FROM PS_JOBCODE_TBL B_ED
        WHERE B.SETID = B_ED.SETID
          AND B.JOBCODE = B_ED.JOBCODE
          AND B_ED.EFFDT <= A.EFFDT)
     AND C.EMPLID = A.EMPLID
     AND D.COMPANY = A.COMPANY
     AND D.PAYGROUP = A.PAYGROUP
     AND D.EMPLID = A.EMPLID
     AND D.EMPL_RCD = A.EMPL_RCD
     AND D.COMPANY =  E.COMPANY 		!(+)
     AND D.PAYGROUP =  E.PAYGROUP 		!(+)
     AND D.PAY_END_DT =  E.PAY_END_DT 		!(+)
     AND D.OFF_CYCLE =  E.OFF_CYCLE 		!(+)
     AND D.PAGE_NUM =  E.PAGE_NUM 		!(+)
     AND D.LINE_NUM =  E.LINE_NUM 		!(+)
     AND D.ADDL_NBR =  E.ADDL_NBR 		!(+)
     AND E.ERNCD = 'REG'			!(+) = 'REG'
     AND D.COMPANY =  F.COMPANY (+)
     AND D.PAYGROUP =  F.PAYGROUP (+)
     AND D.PAY_END_DT =  F.PAY_END_DT (+)
     AND D.OFF_CYCLE =  F.OFF_CYCLE (+)
     AND D.PAGE_NUM =  F.PAGE_NUM (+)
     AND D.LINE_NUM =  F.LINE_NUM (+)
     AND D.ADDL_NBR =  F.ADDL_NBR (+)
    AND F.ERNCD (+) = 'OTP' !ISDVSXR commented 07/08/2008 ITG-51358 !VENDAXG Uncommented on 08/05/2008 for ITG 51358
    ! AND F.ERNCD (+) IN ( 'OTP','173')!ISDVSXR added 07/08/2008 ITG-51358   !VENDAXG Commented on 08/05/2008 for ITG 51358
    !VENDAXG Added on 08/05/2008 for ITG 51358 - Begin
     AND D.COMPANY =  OTH.COMPANY (+)
     AND D.PAYGROUP =  OTH.PAYGROUP (+)
     AND D.PAY_END_DT =  OTH.PAY_END_DT (+)
     AND D.OFF_CYCLE =  OTH.OFF_CYCLE (+)
     AND D.PAGE_NUM =  OTH.PAGE_NUM (+)
     AND D.LINE_NUM =  OTH.LINE_NUM (+)
     AND D.ADDL_NBR =  OTH.ADDL_NBR (+)
     AND OTH.ERNCD (+) = '173'
     !VENDAXG Added on 08/05/2008 for ITG 51358 - End
       
     AND D.COMPANY =  G.COMPANY (+)
     AND D.PAYGROUP =  G.PAYGROUP (+)
     AND D.PAY_END_DT =  G.PAY_END_DT (+)
     AND D.OFF_CYCLE =  G.OFF_CYCLE (+)
     AND D.PAGE_NUM =  G.PAGE_NUM (+)
     AND D.LINE_NUM =  G.LINE_NUM (+)
     AND D.ADDL_NBR =  G.ADDL_NBR (+)
     AND G.ERNCD (+) = '002'
!     AND A.DEPTID IN ('W009', 'W010')
	AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
!      AND A.ACCT_CD IN ('802', '803', '804', '805', '806')
	AND [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
     AND D.PAY_END_DT = $thrudate !  '01-JAN-2005' 
!     AND D.COMPANY =  H.COMPANY
!     AND D.PAYGROUP =  H.PAYGROUP
!     AND D.PAY_END_DT =  H.PAY_END_DT
!     AND D.OFF_CYCLE =  H.OFF_CYCLE
!     AND D.PAGE_NUM =  H.PAGE_NUM
!     AND D.LINE_NUM =  H.LINE_NUM
!     AND D.ADDL_NBR =  H.ADDL_NBR
!     AND H.ERNCD in ('OTP','002','REG') 
    AND POS.POSITION_NBR = A.POSITION_NBR	!isdvmxd added for ITG 56990 on 03/18/2009
     AND POS.EFF_STATUS = 'A'			!isdvmxd added for ITG 56990 on 03/18/2009
     AND POS.EFFDT = (SELECT MAX(P_ED.EFFDT) from PS_POSITION_DATA P_ED 	!ISDVMXD added for ITG 56990 on 03/18s/2009
     			WHERE P_ED.POSITION_NBR = POS.POSITION_NBR
     			AND P_ED.EFF_STATUS = 'A'
     			AND P_ED.EFFDT <= SYSDATE)	 
     
!
!GROUP BY SUBSTR(A.DEPTID,1,4), A.JOBCODE, B.DESCR,SUBSTR(A.EMPLID,1,9), SUBSTR(C.NAME,1,20)			
GROUP BY SUBSTR(A.DEPTID,1,4), A.JOBCODE, B.DESCR,POS.POSITION_NBR,POS.DESCR,SUBSTR(A.EMPLID,1,9), SUBSTR(C.NAME,1,20)	!isdvmxd modified for ITG 56990 on 03/18/2009
ORDER BY SUBSTR(A.DEPTID,1,4),A.JOBCODE,SUBSTR(C.NAME,1,20)

End-Select
End-Procedure


!***********************************************************************
! Initializes the variables which hold the totals
!***********************************************************************
Begin-Procedure Initialize-Totals

	let #jtot_reghrs = 0
	let #jtot_regern = 0
	let #jtot_othrs = 0
	let #jtot_otern = 0
	!VENDAXG Added on 08/05/2008 for ITG 51358 - End
	let #jtot_UOT_Ern = 0
	let #jtot_UOT_Hrs = 0
	!VENDAXG Added on 08/05/2008 for ITG 51358 - End
	let #jtot_dbltimehrs = 0
	let #jtot_dbltimeern = 0

	if $print-deptid-total = 'Y'
		let #dtot_reghrs = 0
		let #dtot_regern = 0
		let #dtot_othrs = 0
		let #dtot_otern = 0
		!VENDAXG Added on 08/05/2008 for ITG 51358 - End
		let #dtot_UOT_Ern = 0
		let #dtot_UOT_Hrs = 0
		!VENDAXG Added on 08/05/2008 for ITG 51358 - End
		let #dtot_dbltimehrs = 0
		let #dtot_dbltimeern = 0
		let $print-deptid-total = 'N'
	end-if

	if $print-report-total = 'Y'
		let #rtot_reghrs = 0
		let #rtot_regern = 0
		let #rtot_othrs = 0
		let #rtot_otern = 0
		!VENDAXG Added on 08/05/2008 for ITG 51358 - End
		let #rtot_UOT_Ern = 0
		let #rtot_UOT_Hrs = 0
		!VENDAXG Added on 08/05/2008 for ITG 51358 - End
		let #rtot_dbltimehrs = 0
		let #rtot_dbltimeern = 0
		let $print-report-total = 'N'
	end-if

End-Procedure


!***********************************************************************
! Builds the Where clause for DEPTID and ACCT_CD
!***********************************************************************
Begin-Procedure Build-Dynamic-Where

	Let $GEXXX902_Deptid_Alias = 'A.DEPTID' 
	Let $GEXXX909_ACCT_CD_Alias = 'A.ACCT_CD'
	do GEXXX902-Select-Deptid-Parameters
	do GEXXX909-Select-ACCT-CD-Parameters

End-Procedure 


!***********************************************************************
! Prints a Data Row
!***********************************************************************
Begin-Procedure Print-Row

	let $deptid = '="' || &deptid || '"'
	let $emplid = '="' || &emplid || '"'

	print $deptid  (+1,{#column1}) 
	print &jobcode	(0,{#column2})
	!isdvmxd added for ITG 56990 on 03/18/2009 - Begin
	!Show 'jobtitle = ' &jobtitle
	!Show 'position title = ' &position_title
	If isblank(&position_title) = 0
		print	      &position_title	 	(+0,{#column3})
		!Show 'printed position title'
	Else 
		print	      &jobtitle 	(+0,{#column3})
		!Show 'printed jobtitle'
	End-If
	!isdvmxd added for ITG 56990 on 03/18/2009 - End
	
	print $emplid	(0,{#column4})
	print &name	(0,{#column5})

	print #reghrs	(0,{#column6}) edit 999.99
	print #regern	(0,{#column7}) edit 9999.99
	print #othrs	(0,{#column8}) edit 999.99 
        print #otern	(0,{#column9}) edit 9999.99
        !VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
        print #UOT_Hrs	(0,{#column10}) edit 999.99 
        print #UOT_Ern	(0,{#column11}) edit 9999.99
	
	!print #dbltimehrs (0,{#column10}) edit 999.99
	!print #dbltimeern (0,{#column11}) edit 9999.99
	print #dbltimehrs (0,{#column12}) edit 999.99
	print #dbltimeern (0,{#column13}) edit 9999.99
	!VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
End-Procedure 


!***********************************************************************
! Prints Totals on Jobcode Change
!***********************************************************************
Begin-Procedure Print-Jobcode-Total

	! print 'deepak ' (0,{#column1})
	print 'Total for Jobcode : '	(+2,{#column1})
	print #jtot_reghrs	(0,{#column6}) edit 9999.99
	print #jtot_regern	(0,{#column7}) edit 99999.99
	print #jtot_othrs	(0,{#column8}) edit 9999.99 
        print #jtot_otern	(0,{#column9}) edit 99999.99
        !VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
        print #jtot_UOT_Hrs	(0,{#column10}) edit 999.99 
        print #jtot_UOT_Ern	(0,{#column11}) edit 9999.99
        !print #jtot_dbltimehrs	(0,{#column10}) edit 9999.99
	!print #jtot_dbltimeern	(0,{#column11}) edit 99999.99
	print #jtot_dbltimehrs	(0,{#column12}) edit 9999.99
	print #jtot_dbltimeern	(0,{#column13}) edit 99999.99
	!VENDAXG Modified on 08/05/2008 for ITG 51358 - End
	
	print ' '			(+1,0)
        ! print &deptid		(+1,{#column1})

	let #dtot_reghrs = #dtot_reghrs + #jtot_reghrs
	let #dtot_regern = #dtot_regern + #jtot_regern
	let #dtot_othrs = #dtot_othrs + #jtot_othrs
	let #dtot_otern = #dtot_otern + #jtot_otern
	!VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
	let #dtot_UOT_Hrs = #dtot_UOT_Hrs + #jtot_UOT_Hrs
	let #dtot_UOT_Ern = #dtot_UOT_Ern + #jtot_UOT_Ern
	!VENDAXG Modified on 08/05/2008 for ITG 51358 - End
	let #dtot_dbltimehrs = #dtot_dbltimehrs + #jtot_dbltimehrs
	let #dtot_dbltimeern = #dtot_dbltimeern + #jtot_dbltimeern
 
	do Initialize-Totals

End-Procedure 


!***********************************************************************
! Prints Totals on Deptid Change
!***********************************************************************
Begin-Procedure Print-Deptid-Total

	print 'Total for Department : '	(+1,{#column1})
	print #dtot_reghrs	(0,{#column6}) edit 99999.99
	print #dtot_regern	(0,{#column7}) edit 999999.99
	print #dtot_othrs	(0,{#column8}) edit 99999.99 
        print #dtot_otern	(0,{#column9}) edit 999999.99
        !VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
        print #dtot_UOT_Hrs	(0,{#column10}) edit 999.99 
        print #dtot_UOT_Ern	(0,{#column11}) edit 9999.99
        !print #dtot_dbltimehrs	(0,{#column10}) edit 99999.99
	!print #dtot_dbltimeern	(0,{#column11}) edit 999999.99
	print #dtot_dbltimehrs	(0,{#column12}) edit 99999.99
	print #dtot_dbltimeern	(0,{#column13}) edit 999999.99
	!VENDAXG Modified on 08/05/2008 for ITG 51358 - End
	print ' '			(+1,0)
        let $print-deptid-total = 'Y'

	let #rtot_reghrs = #rtot_reghrs	+ #dtot_reghrs	
	let #rtot_regern = #rtot_regern + #dtot_regern	
	let #rtot_othrs = #rtot_othrs + #dtot_othrs	
        let #rtot_otern = #rtot_otern + #dtot_otern
        !VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
	let #rtot_UOT_Hrs = #rtot_UOT_Hrs + #dtot_UOT_Hrs
	let #rtot_UOT_Ern = #rtot_UOT_Ern + #dtot_UOT_Ern	
	!VENDAXG Modified on 08/05/2008 for ITG 51358 - End
	let #rtot_dbltimehrs = #rtot_dbltimehrs + #dtot_dbltimehrs	
	let #rtot_dbltimeern = #rtot_dbltimeern	+ #dtot_dbltimeern	
	
	do Initialize-Totals

End-Procedure 


!***********************************************************************
! Prints Totals on the end of the Report
!***********************************************************************
Begin-Procedure Print-Report-Total

	print 'Total for Report : '	(+1,{#column1})
	print #rtot_reghrs	(0,{#column6}) edit 99999.99
	print #rtot_regern	(0,{#column7}) edit 999999.99
	print #rtot_othrs	(0,{#column8}) edit 99999.99 
        print #rtot_otern	(0,{#column9}) edit 999999.99
        !VENDAXG Modified on 08/05/2008 for ITG 51358 - Begin
        print #rtot_UOT_Hrs	(0,{#column10}) edit 999.99 
        print #rtot_UOT_Ern	(0,{#column11}) edit 9999.99
	
	!print #rtot_dbltimehrs	(0,{#column10}) edit 99999.99
	!print #rtot_dbltimeern	(0,{#column11}) edit 999999.99
	print #rtot_dbltimehrs	(0,{#column12}) edit 99999.99
	print #rtot_dbltimeern	(0,{#column13}) edit 999999.9
	!VENDAXG Modified on 08/05/2008 for ITG 51358 - End
	
	
	print ' '			(+1,0)
        let $print-report-total = 'Y'
	
	do Initialize-Totals

End-Procedure 


!***********************************************************************
! Prints Totals after the last row is selected and Report Totals.
!***********************************************************************
Begin-Procedure End-of-Report-Printing

	do Print-Jobcode-Total
	let $jobcode_old = $jobcode
	do Print-Deptid-Total
	let $deptid_old = $deptid
	do Print-Report-Total

End-Procedure 

#include 'stdapi.sqc'    !Process Scheduler Interface
#include 'reset.sqc'     !Reset Printer Procedure
#include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date math
#include 'gexrnctl.sqc'  !Routine for Get-Run-Control
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#include 'gexxx902.sqc'  !Get DEPTID from Run Control Page
#include 'gexxx909.sqc'  !Get ACCT_CD from Run Control Page