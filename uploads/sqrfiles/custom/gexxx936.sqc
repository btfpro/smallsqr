!**************************************************************************
!  GEXXX936:  Retrieve "Multiple" Business Unit Run Controls and          *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX936_Business_Unit_Alias                                        *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA                             *
!    $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA                             *
!    $GEXXX936_INCLUDE_BUSINESS_UNIT_DISPLAY_CRITERIA                     *
!    $GEXXX936_EXCLUDE_BUSINESS_UNIT_DISPLAY_CRITERIA                     *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX936_Business_Unit_Alias = 'GDT.BUSINESS_UNIT'              *
!    Do GEXXX936-Select-Business-Unit-Parameters                          *
!    Begin-Select                                                         *
!    DEPTID                                                               *
!    FROM PS_GEX_DEPT_TBL GDT                                             *
!    WHERE [$GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA]                     *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE         *
!**************************************************************************
!	JDH 		10/13/99 	INITIAL CREATION                  *
!       JDH             10/29/99        When no parms found, let 1=1      *
!**************************************************************************


Begin-Procedure GEXXX936-Select-Business-Unit-Parameters

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_BUSINESS_UNIT_ROWS = 0
  Let $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA = ''
  Let $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA = ''
  Let $GEXXX936_INCLUDE_BUSINESS_UNIT_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX936_EXCLUDE_BUSINESS_UNIT_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_BUSINESS_UNIT.BUSINESS_UNIT

  Add 1 To #GEX_RC_BUSINESS_UNIT_ROWS

  If #GEX_RC_BUSINESS_UNIT_ROWS = 1
    Let $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA = '(' || $GEXXX936_Business_Unit_Alias || ' In ('''     || &GEX_RC_BUSINESS_UNIT.BUSINESS_UNIT || ''''
    Let $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA = '(' || $GEXXX936_Business_Unit_Alias || ' Not In (''' || &GEX_RC_BUSINESS_UNIT.BUSINESS_UNIT || ''''
  Else
    Let $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA = $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA || ',''' || &GEX_RC_BUSINESS_UNIT.BUSINESS_UNIT  || ''''
    Let $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA = $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA || ',''' || &GEX_RC_BUSINESS_UNIT.BUSINESS_UNIT  || ''''
  End-If

From  PS_GEX_RC_BUS_UNIT GEX_RC_BUSINESS_UNIT

Where GEX_RC_BUSINESS_UNIT.OPRID               = [$OprID]
And   GEX_RC_BUSINESS_UNIT.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_BUSINESS_UNIT.BUSINESS_UNIT, ' ') != ' ' 
End-Select

  If #GEX_RC_BUSINESS_UNIT_ROWS = 0
    Let $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA = '1=1'
    Let $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA = '1=1'
  Else
    Let $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA = $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA || '))'
    Let $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA = $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA || '))'
    Let $GEXXX936_INCLUDE_BUSINESS_UNIT_DISPLAY_CRITERIA = $GEXXX936_INCLUDE_BUSINESS_UNIT_CRITERIA
    Let $GEXXX936_EXCLUDE_BUSINESS_UNIT_DISPLAY_CRITERIA = $GEXXX936_EXCLUDE_BUSINESS_UNIT_CRITERIA
  End-If

End-Procedure
