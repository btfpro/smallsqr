!***********************************************************************
!  GEXPY014:  Active Employee Summary                                  *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY014              Active Employee Summary                        *
!                                                                      *
! Narrative:            Produce a summary totals of active full and    *
!                         part time EE's by company, deptid, acct_cd   *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           PS_JOB                                         *
!			PS_GEX_DEPT_TBL                                *
!                                                                      *
! Written by:           Dave Herzog-SoftLink Inc.                      *
!                                                                      *
! Prerequisite:         None                                           *
!                                                                      *
! Control Parms:	FOR WHERE CLAUSE:                              *
!                         Company Division Code                        *
!                         Company                                      *
!                         Paygroup                                     *
!                         Deptid                                       *
!                         Employee Status                              *
!                         Account Code                                 *
!                         As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  DAH          01/15/97     Initial Creation.                         *
!  DAH          02/02/98     Changed selection criteria from           *
!                              full_part_time to reg_temp and added    *
!                              paygroup. (GEX001)                      *
!  JDH          05/19/98     Changed reg_temp to full_part_time.       *
!  JDH          04/21/99     Added company and paygroup subtotal.      *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup01.sqc'   !Init printer and page size (Portrait)


  #define #col_selection_criteria_a 	 2
  #define #col_selection_criteria_b 	 5

  #define #col_company			20
  #define #col_paygroup			32
  #define #col_deptid			45
  #define #col_acct_cd			56
  #define #col_full_time_total		68
  #define #col_part_time_total		82
  #define #col_full_part_time_total	95


begin-report
#debug9 show 'Begin-Report'
  !Do Set-Optimizer-Goal  ! ISDVNPK 08/17/2007 Upgrade 8.3 to 9.0

  do Init-DateTime
  do Init-Number

  do Get-Current-DateTime
 
  do Init-Report

  do Process-Main
  do Reset
  do Stdapi-Term

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  show 'Successful End of Report'
end-report


begin-heading 6
#debug9 show 'Begin-Heading'
  #include 'stdhdg01.sqc' 

  let $temp_out = 'Sorted by ' || $Order_By
  print $temp_out       (3,) center 

  let $temp_out = 'Report as of ' || $REPORT_HEADING_ASOFDATE
  print $temp_out       (4,) center 

  PRINT 'COMPANY'       (+2,{#col_company})
  PRINT 'PAYGROUP'      (0,{#col_paygroup})
  PRINT 'DEPTID'        (0,{#col_deptid})
  PRINT 'ACCT CD'       (0,{#col_acct_cd})
  PRINT 'FULL TIME'     (0,{#col_full_time_total})
  PRINT 'PART TIME'     (0,{#col_part_time_total})
  PRINT ' SUBTOTAL'     (0,{#col_full_part_time_total})

  PRINT '-'             (+1,{#col_company},7)               Fill
  PRINT '-'             (+0,{#col_paygroup},8)              Fill
  PRINT '-'             (+0,{#col_deptid},6)                Fill
  PRINT '-'             (+0,{#col_acct_cd},9)               Fill
  PRINT '-'             (+0,{#col_full_time_total},9)       Fill
  PRINT '-'             (+0,{#col_part_time_total},9)       Fill
  PRINT '-'             (+0,{#col_full_part_time_total},9)  Fill
end-heading


begin-procedure Init-Report
#debug9 show 'Init-Report'
  move 'GEXPY014' to $ReportID
  move 'Active Employees Report' to $ReportTitle

  show '$ReportID=' $ReportID 
  show '$ReportTitle=' $ReportTitle

  do Stdapi-Init

  Let $GEXXX900_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
  Let $GEXXX902_Deptid_Alias   = 'J.DEPTID'
  Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'
  Let $GEXXX907_Div_CD_Alias   = 'GDT.GEX_COMPANY_DIV_CD'
  Let $GEXXX909_Acct_CD_Alias  = 'J.ACCT_CD'

  if $prcs_process_instance = ''
    do GEXXX950-Input-Parameters
  else
    Do GEXXX922-Select-Parameters
    let $AsOfDate = $GEX_RC_PAY.ASOFDATE
    if rtrim($GEX_RC_PAY.ASOFDATE,' ') = ''
      let $AsOfDate = $asoftoday
    end-if
    Do GEXXX900-Select-Company-Parameters
    Do GEXXX901-Select-Company-Paygroup-Parameters
    Do GEXXX902-Select-Deptid-Parameters
    Do GEXXX903-Select-Emp-Stat-Parameters
    Do GEXXX907-Select-Div-CD-Parameters
    Do GEXXX909-Select-Acct-CD-Parameters
  end-if

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  do Format-DateTime($AsOfDate, $REPORT_HEADING_ASOFDATE ,{DEFDATE}, '', '')
  Let $Order_By = 'COMPANY, PAYGROUP, DEPTID, ACCT_CD'

  do P130-Print-Cover-Page
end-procedure

! ISDVNPK 08/17/2007 Upgrade 8.3 to 9.0
!begin-procedure Set-Optimizer-Goal
!  Begin-SQL
!    Alter Session Set OPTIMIZER_GOAL=RULE;
!  End-SQL
!end-procedure


begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+2,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                 (0,+2)
  Print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	          (0,+2)
  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)

  Print 'As Of Date :'                                            (+2,5)
  Print $REPORT_HEADING_ASOFDATE                                  (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,2)
  Print $Order_By                                                 (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure Process-Main
#debug9 Show 'Process-Main'
Begin-Select
J.COMPANY 	() on-break
                   level=1
                   print=never
                   after=After-Company-Change

J.PAYGROUP 	() on-break
                   level=2
                   print=never
                   after=After-Paygroup-Change

J.DEPTID  	() on-break
                   level=3
                   print=never
                   before=Before-Deptid-Change
                   after=After-Deptid-Change

J.ACCT_CD 	() on-break
                   level=4
                   print=never
                   after=After-AcctCD-Change

J.FULL_PART_TIME

Count(*)           &ee_count

  Let $Company  = &J.COMPANY
  Let $Paygroup = &J.PAYGROUP
  Let $Deptid   = &J.DEPTID
  Let $Acct_Cd  = &J.ACCT_CD

  If &J.FULL_PART_TIME = 'F'
    Let #ft_subtotal = &ee_count
  Else
    If &J.FULL_PART_TIME = 'P'
      Let #pt_subtotal = &ee_count
    End-If
  End-If

From   PS_GEX_DEPT_TBL  GDT,
       PS_JOB           J
 
Where  J.EFFDT             =
      (Select Max(EFFDT)
       From   PS_JOB
       Where  EMPLID       = J.EMPLID
       And    EMPL_RCD    = J.EMPL_RCD
       And    EFFDT       <= $AsOfDate)
                        
And    J.EFFSEQ            =
      (Select Max(EFFSEQ)
       From   PS_JOB
       Where  EMPLID       = J.EMPLID
       And    EMPL_RCD    = J.EMPL_RCD
       And    EFFDT        = J.EFFDT)

And    J.DEPTID            = GDT.DEPTID

And    GDT.EFFDT           =
      (Select Max(EFFDT)
       From  PS_GEX_DEPT_TBL
       Where  DEPTID       = GDT.DEPTID
       And    EFFDT       <= $AsOfDate)

And    GDT.SEQUENCE_NUMBER =
      (Select Max(SEQUENCE_NUMBER)
       From   PS_GEX_DEPT_TBL
       Where  DEPTID       = GDT.DEPTID
       And    EFFDT        = GDT.EFFDT)

And   [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
And   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
And   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
And   [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
And   [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
And   [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]

Group By J.COMPANY, J.PAYGROUP, J.DEPTID, J.ACCT_CD, J.FULL_PART_TIME
Order By J.COMPANY, J.PAYGROUP, J.DEPTID, J.ACCT_CD
End-Select

  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) CENTER
  Else
    Print 'Grand Totals'         (+2,{#col_paygroup})
    Print #ft_grand_subtotal     (0,{#col_full_time_total})       Edit 9,999,999
    Print #pt_grand_subtotal     (0,{#col_part_time_total})       Edit 9,999,999
    Print #fpt_grand_subtotal    (0,{#col_full_part_time_total})  Edit 9,999,999
  End-If
End-Procedure


Begin-Procedure Before-Deptid-Change
#debug9 Show 'Before-Deptid-Change'
  Position (+1)
End-Procedure


Begin-Procedure After-Company-Change
#debug9 Show 'After-Company-Change'
  Print 'Company'                (+2,{#col_paygroup})
  Print $Company                 (0,{#col_deptid})

  Print #ft_company_subtotal     (0,{#col_full_time_total})       Edit 9,999,999
  Print #pt_company_subtotal     (0,{#col_part_time_total})       Edit 9,999,999
  Print #fpt_company_subtotal    (0,{#col_full_part_time_total})  Edit 9,999,999

  Add #ft_company_subtotal  To #ft_grand_subtotal
  Add #pt_company_subtotal  To #pt_grand_subtotal
  Add #fpt_company_subtotal To #fpt_grand_subtotal

  Let #ft_company_subtotal  = 0
  Let #pt_company_subtotal  = 0
  Let #fpt_company_subtotal = 0
End-Procedure


Begin-Procedure After-Paygroup-Change
#debug9 Show 'After-Paygroup-Change'
  Print 'Paygroup'               (+2,{#col_paygroup})
  Print $Paygroup                (0,{#col_deptid})

  Print #ft_paygroup_subtotal    (0,{#col_full_time_total})       Edit 9,999,999
  Print #pt_paygroup_subtotal    (0,{#col_part_time_total})       Edit 9,999,999
  Print #fpt_paygroup_subtotal   (0,{#col_full_part_time_total})  Edit 9,999,999

  Add #ft_paygroup_subtotal  To #ft_company_subtotal
  Add #pt_paygroup_subtotal  To #pt_company_subtotal
  Add #fpt_paygroup_subtotal To #fpt_company_subtotal

  Let #ft_paygroup_subtotal  = 0
  Let #pt_paygroup_subtotal  = 0
  Let #fpt_paygroup_subtotal = 0
End-Procedure


Begin-Procedure After-Deptid-Change
#debug9 Show 'After-Deptid-Change'
  Print 'Deptid'                 (+2,{#col_paygroup})
  Print $Deptid                  (0,{#col_deptid})

  Print #ft_deptid_subtotal      (0,{#col_full_time_total})       Edit 9,999,999
  Print #pt_deptid_subtotal      (0,{#col_part_time_total})       Edit 9,999,999
  Print #fpt_deptid_subtotal     (0,{#col_full_part_time_total})  Edit 9,999,999

  Add #ft_deptid_subtotal  To #ft_paygroup_subtotal
  Add #pt_deptid_subtotal  To #pt_paygroup_subtotal
  Add #fpt_deptid_subtotal To #fpt_paygroup_subtotal

  Let #ft_deptid_subtotal  = 0
  Let #pt_deptid_subtotal  = 0
  Let #fpt_deptid_subtotal = 0
End-Procedure


Begin-Procedure After-AcctCd-Change
#debug9 Show 'After-AcctCd-Change'
  Let $Data-Found-Flag = 'Y'

  Let #fpt_subtotal = #pt_subtotal + #ft_subtotal

  Print $Company               (+1,{#col_company})
  Print $Paygroup              (,{#col_paygroup})
  Print $Deptid                (,{#col_deptid})
  Print $Acct_Cd               (,{#col_acct_cd},10)
  Print #ft_subtotal           (,{#col_full_time_total})       Edit 9,999,999
  Print #pt_subtotal           (,{#col_part_time_total})       Edit 9,999,999
  Print #fpt_subtotal          (,{#col_full_part_time_total})  Edit 9,999,999

  Add #ft_subtotal  To #ft_deptid_subtotal
  Add #pt_subtotal  To #pt_deptid_subtotal
  Add #fpt_subtotal To #fpt_deptid_subtotal
	
  Let #ft_subtotal  = 0
  Let #pt_subtotal  = 0
  Let #fpt_subtotal = 0
End-Procedure


#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'gexregtp.sqc'	 !Translates REG_TEMP code into full/part time code
 
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx908.sqc'  !Get sal_admin_plan multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx950.sqc'  !Get SQRW run controls
#include 'askaod.sqc'    !Ask-As-Of-Date procedure

