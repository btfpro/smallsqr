!***********************************************************************
! GEXBN824:  Employees that are active without any hours or earnings   *
!            reported for a specific pay period.                       *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
! Narrative:            This report will generate a list of employees, *
!                       that are active but do not have any hours or   *
!                       earnings reported for a specific pay period.   *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_calendar                                   *
!                       earnings_bal                                   *
!                       pay_oth_earns                                  *
!                       pay_earnings                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Sreeni Kutam                                   *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         As Of Date                                   *
!                         Company/Paygroup(s)                          *
!                         Employee Status(s) to Exclude                *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! SXK       12/21/98    Initial Creation                               *
! JDH       01/18/99    Changed $ReportID from GEXBN823 to GEXBN824    *
! JDH       01/25/99    Added erncd 515 to vacation hours select       *
! JDH       04/15/99    Modified report to sort by company, paygroup,  *
!                         deptid, name.                                *
! SXK       10/20/99    Modified to have union_cd from job_labor       *
! MRP       01/11/01    COD Modifications:                             *
!                         Added Earn Codes: 519, 522, & 523            *
!  PSR      08/12/2002  Modified to get UNION_CD FROM JOB              * 
!                       - Upgrade 8.3                                  * 
! PSR       08/27/2002   EMPL_RCD is now part of the key for           *
!                           PS_EARNINGS_BAL - Upgrade 8.3              *
! GBD       07/15/2002  Added Fieldvalue to effective dated selection  *
!                       of the Load-Lookup procedure.                  *
!                                                                      *
! CXB       10/12/2007  Added Field JOBTITLE FROM TABLE PS_EMPLOYEES   *
!			Removed the '-' from EMPLID WHICH IS showing   *
!			preveiously on Print                           *
! MXR       02/25/2009  ITG # 56875  Jobtitle Change                   *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Landscape)


#define #column1      1
#define #column2     16
!GEX-CXB 10/12/2007  HCM 9.0 Upgrade Begin 
#define #column9     40	!Added GEX-CXB 10/12/2007  HCM 9.0 Upgrade
#define #column3     75 
#define #column4     87 
#define #column5     99 
#define #column6     112 
#define #column7     127 
#define #column8     142 

!#define #column3     50
!#define #column4     61
!#define #column5     78
!#define #column6     89
!#define #column7    104
!#define #column8    120
!GEX-CXB 10/12/2007  HCM 9.0 Upgrade End

#define #column1_d    1
#define #column2_d   16
!GEX-CXB 10/12/2007  HCM 9.0 Upgrade Begin 
#define #column9_d   40	!Added  GEX-CXB
!#define #column3_d  51
!#define #column4_d  63
!#define #column5_d  75
!#define #column6_d  88
!#define #column7_d  103
!#define #column8_d  118
#define #column3_d   75 
#define #column4_d   87 
#define #column5_d   96 
#define #column6_d   111 
#define #column7_d   126 
#define #column8_d   142 
!GEX-CXB 10/12/2007  HCM 9.0 Upgrade End




Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 10
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg06.sqc'

  If $Deptid <> $priorDeptid
    Do Get-Department-Name
    Let $priorDeptid = $Deptid
  End-If

  Print 'Deptid'                 (6,1)
  Print $Deptid                  (6,13)
  Print $DeptName                (6,+1)

  Print $ReportTitle2            (3,)  Center

  Print 'EMPLOYEE ID'            (+7,{#column1})
  Print 'NAME'                   (+0,{#column2})
  Print 'DESCRIPTION'	         (+0,{#column9})  !Added GEX-CXB 10/12/2007  HCM 9.0 Upgrade  
  Print 'JOBCODE'                (+0,{#column3})
  Print 'UNION CD'               (+0,{#column4})
  Print 'HOURS'                  (+0,{#column5})
  Print 'EARNINGS'               (+0,{#column6})
  Print '     MTD'               (-2,{#column7})
  Print 'VACATION'               (+1,{#column7})
  Print 'EARNINGS'               (+1,{#column7})
  Print 'STATUS'                 (+0,{#column8})

End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
 !Do Set-Optimizer-Goal	! Commented GEX-CXB  10/12/2007  HCM 9.0 Upgrade

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXBN824'
  Let $ReportTitle   = 'EMPLOYEES THAT ARE ACTIVE '
  Let $ReportTitle2  = 'WITHOUT ANY HOURS/EARNINGS REPORTED'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle $ReportTitle2
  Show ' '
  Show 'Report Began:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime

  Let $GEXXX901_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
  Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  do get-pay-end-dt
  Do Format-DateTime($pay_end_dt, $X000_Pay_End_Dt, {DEFDATE}, '', '')

  Do Format-DateTime($AsOfDate, $X000_Report_Heading_AsOfDate, {DEFDATE}, '', '')
  Let $X000_Order_By = 'Company, Paygroup, Deptid, Name'

  Do P130-Get-Xlat-Lookup
  Do P140-Print-Cover-Page
End-Procedure


begin-procedure Set-Optimizer-Goal
  Begin-SQL
    Alter Session Set OPTIMIZER_GOAL=RULE;
  End-SQL
end-procedure


Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Do Ask-As-Of-Date

  Display 'Enter Company/Paygroup or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''')'
      Else
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || $In-Paygroup || ''')'
      End-If
      Add 1 To #GEX_RC_PAYGROUP_ROWS
      If #GEX_RC_PAYGROUP_ROWS = 1
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While

  If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If

  Let $EmplStatusString = ''''
  Display 'Enter Employee Status or leave blank to exit.'
  While 1=1
    Input $In-EmplStatus Maxlen=1 'Employee Status'
    Uppercase $In-EmplStatus
    If Rtrim($In-EmplStatus, ' ') = ''
      Concat '''' With $EmplStatusString
      Break
    End-If
    If $EmplStatusString <> ''''
      Concat ''',''' With $EmplStatusString
    End-If
    Concat $In-EmplStatus With $EmplStatusString
  End-While

  If $EmplStatusString = ''''''
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = '1=1'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = $GEXXX903_Emp_Stat_Alias || ' In (' || $EmplStatusString || ')'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
  End-If
End-Procedure


Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXRCBN1-SELECT-PARAMETERS      !gexxx920.sqc

  Let $AsOfDate = &GEX_RC_BEN.AsOfDate
  If Rtrim (&GEX_RC_BEN.AsOfDate, ' ') = ''
    Let $AsOfDate = $AsOfToday
  End-If

  Do GEXXX901-Select-Company-Paygroup-Parameters
  Do GEXXX903-Select-Emp-Stat-Parameters
End-Procedure


!*********************************************************************
!Gets the pay end date of the previous pay cycle.
!*********************************************************************
Begin-Procedure get-pay-end-dt

move 'N' to $found

begin-select on-error=P260-sql-error-found('Get-Pay-End-Dt')
MAX(PAY.PAY_END_DT) &PAY.PAY_END_DT

  move &PAY.PAY_END_DT to $pay_end_Dt
  move $pay_end_dt To $PayEndDate
  move 'Y' to $found
  exit-select

FROM   PS_PAY_CALENDAR PAY

WHERE  PAY.PAY_END_DT     <= $AsOfDate
AND    PAY.PAY_SHEETS_RUN  = 'Y'
end-select

if $found = 'N'
   display 'No Previous Pay Cycle as of:' noline
   display $AsOfDate
   stop
end-if
end-procedure


begin-procedure P130-Get-Xlat-Lookup
#debug9 Show 'P130-Get-Xlat-Lookup'

  Let $Where = '     x.fieldname   = ''EMPL_STATUS''' ||
               ' And x.eff_status  = ''A''' ||
               !' And x.language_cd = ''ENG''' ||  !Commented GEX-CXB 10/12/2007 HCM9.0  Upgrade
               ' And x.effdt       = (Select Max(effdt)' ||
!               '                      From   xlattable x1' ||  !Commented GEX-CXB 10/12/2007 HCM9.0  Upgrade
	       '                      From   psxlatitem x1' ||	!Added GEX-CXB 10/12/2007 HCM9.0  Upgrade
               '                      Where  x1.fieldname    = x.fieldname' ||
!GBD 07/15/2002 Begin
!               '                      And    x1.language_cd  = ''ENG''' ||
!           '                         And    x1.language_cd  = x.language_cd' ||  !Commented GEX-CXB 10/12/2007 HCM9.0  Upgrade
               '                      And    x1.fieldvalue   = x.fieldvalue' ||
!GBD 07/15/2002 End
               '                      And    x1.effdt       <= ''' || $pay_end_dt || '''' ||
               '                      And    x1.eff_status   = ''A'')'

  Load-Lookup
    Name=XlatLookup
    Rows=11
    !Table='XLATTABLE X'  !Commented GEX-CXB 10/12/2007 HCM9.0  Upgrade
    Table='PSXLATITEM X'  !Added GEX-CXB 10/12/2007 HCM9.0  Upgrade
    Key='X.FIELDVALUE'
    Return_Value='X.XLATSHORTNAME'
    Where=$Where
    Quiet
End-Procedure


begin-procedure P140-Print-Cover-Page
#debug9 Show 'P140-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = '          (+2,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = '  (+2,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  Print 'As Of Date :'                                            (+2,5)
  Print $X000_Report_Heading_AsOfDate                             (0,+2)
  Print 'Pay End Date:'                                           (+2,5)
  Print $X000_Pay_End_Dt                                          (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,2)
  Print $X000_Order_By                                            (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'

show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA ' $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
show '$GEXXX903_INCLUDE_EMP_STAT_CRITERIA  ' $GEXXX903_INCLUDE_EMP_STAT_CRITERIA

  Let $Data-Found-flag = 'N'

Begin-Select On-Error=P260-SQL-Error-Found('P200-Main-Process')
j.company    ()  On-Break  Level=1
                           Print=Never

j.paygroup   ()  On-Break  Level=2
                           Print=Never

j.deptid     ()  On-Break  Level=3
                           Print=Never
                           After=P210-New-Page

pd.name      ()  On-Break  Level=4
                           Print=Never

j.emplid     ()  On-Break  Level=5
                           Print=Never
                           After=P220-EE-Break

j.empl_status
j.empl_rcd
j.jobcode
!SXK 10/20/99 Begin
!em.union_cd
J.UNION_CD
!SXK 10/20/1999 End
jc.descr	!Added GEX-CXB  10/12/2007 HCM 9.0 Upgrade
PSD.DESCR        !MXR  - 02/25/2009  ITG # 56875 
pe.off_cycle
pe.page_num
pe.line_num
pe.addl_nbr
pe.reg_hrs + pe.reg_earn_hrs + pe.ot_hrs &hours
pe.reg_earns + pe.reg_hrly_earns + pe.ot_hrly_earns &earns

  Let $company     = &j.company
  Let $paygroup    = &j.paygroup
  Let $deptid      = &j.deptid
  Let $name        = &pd.name
  Let $emplid      = &j.emplid
  Let #empl_rcd    = &j.empl_rcd
  Let $empl_status = &j.empl_status
  Let $jobcode     = &j.jobcode
   SHOW ' &PSD.DESCR =====    ' &PSD.DESCR   ! testing
  IF &PSD.DESCR <> ''  !MXR  - 02/25/2009  ITG # 56875  START
      LET  $jobtitle	   = &PSD.DESCR
   ELSE 
      Let $jobtitle	   = &jc.descr !Added GEX-CXB 10/12/2007 HCM9.0  Upgrade
   END-IF              !MXR  - 02/25/2009  ITG # 56875  END
  !SXK 10/20/1999 Begin
  !Let $union_cd    = &em.union_cd
  Let $union_cd = &J.UNION_CD
  !SXK 10/20/99 End

  Add &hours To #hours
  Add &earns To #earns

  Do Pay-Oth-Earns

From   ps_personal_data  pd,
!SXK 10/20/99 Begin
!       ps_employment     em,
!SXK 10/20/99 End
       ps_job            j,
       ps_pay_earnings   pe,
       ps_jobcode_tbl    jc,	!Added GEX-CXB 10/12/2007 HCM9.0  Upgrade
       PS_POSITION_DATA  PSD     !MXR - 02/25/2009  ITG # 56875

Where  j.effdt           =
      (Select Max(effdt)
       From   ps_job
       Where  emplid     = j.emplid
       And    empl_rcd  = j.empl_rcd
       And    effdt     <= $pay_end_dt)

And    j.effseq          =
      (Select Max(effseq)
       From   ps_job
       Where  emplid     = j.emplid
       And    empl_rcd  = j.empl_rcd
       And    effdt      = j.effdt)
!AND J.EMPLID = '007827080'  !testing
And    pd.emplid         = j.emplid
And    pe.company        = j.company
And    pe.paygroup       = j.paygroup
And    pe.pay_end_dt     = $pay_end_dt
And    pe.emplid         = j.emplid
And    pe.empl_rcd      = j.empl_rcd
And    jc.jobcode	= j.jobcode	!Added GEX-CXB 10/12/2007 HCM9.0 Upgrade Begin 
And    jc.effdt          =     
      (select max(jc1.effdt)
      from ps_jobcode_tbl jc1
      where jc1.jobcode=jc.jobcode) 		
AND j.JOBCODE = PSD.JOBCODE    !MXR - 02/25/2009 start
and j.position_nbr = PSD.POSITION_NBR
     AND PSD.EFFDT =
        (SELECT MAX(P_PSD.EFFDT) FROM PS_POSITION_DATA P_PSD
        WHERE PSD.POSITION_NBR = P_PSD.POSITION_NBR
          AND P_PSD.EFFDT <= j.EFFDT) !MXR - 02/25/2009  ITG # 56875 end 

And   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
And   [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]

Order By j.company, j.paygroup, j.deptid, pd.name, j.emplid
End-Select
End-Procedure


Begin-Procedure Pay-Oth-Earns
#debug9 Show 'Pay-Oth-Earns'
Begin-Select
poe.oth_hrs
poe.oth_earns

  Add &poe.oth_hrs   To #hours
  Add &poe.oth_earns To #earns

From   ps_pay_oth_earns   poe

Where  poe.company     = &j.company
And    poe.paygroup    = &j.paygroup
And    poe.pay_end_dt  = $pay_end_dt
And    poe.off_cycle   = &pe.off_cycle
And    poe.page_num       = &pe.page_num
And    poe.line_num       = &pe.line_num
And    poe.addl_nbr       = &pe.addl_nbr
End-Select
End-Procedure


Begin-Procedure P210-New-Page
#debug9 Show 'P210-New-Page'
  If $OK-To-Advance-Page = 'Y'
    Print 'Deptid'       (+2,1)
    Print $Deptid        (+0,+1)
    Print 'Count'        (+0,+1)
    Print #deptid-count  (+0,+1)  Edit 999,999,990

    New-Page
    Let $OK-To-Advance-Page = 'N'
    Let #deptid-count = 0
  End-If
End-Procedure


Begin-Procedure P220-EE-Break
#debug9 Show 'P220-EE-Break'
  If #earns = 0 And #hours = 0
    Let $Data-Found-Flag = 'Y'
    Let $OK-To-Advance-Page = 'Y'
    Add 1 To #deptid-count
    Do P230-Get-Vacation-Hours
    Do P240-Print-Record
  End-If

  Let #hours = 0
  Let #earns = 0
End-Procedure


Begin-Procedure P230-Get-Vacation-Hours
#debug9 Show 'P230-Get-Vacation-Hours'
  Let #Vac_Earns = 0

Begin-Select
eb.grs_mtd &Vac_Earns

  Let #Vac_Earns = &Vac_Earns

From   ps_pay_cal_bal_id  pcbi,
       ps_earnings_bal    eb

WHERE  PCBI.COMPANY    = $company
AND    PCBI.PAYGROUP   = $paygroup
AND    PCBI.PAY_END_DT = $pay_end_dt
AND    PCBI.BALANCE_ID = 'CY'

And    eb.emplid           = $emplid
And    eb.empl_rcd         = #empl_rcd
And    eb.company          = $company
And    eb.balance_id       = 'CY'
And    eb.balance_year     = pcbi.balance_year
And    eb.balance_qtr      = pcbi.balance_qtr
And    eb.balance_period   = pcbi.balance_period
And    eb.spcl_balance     = 'N'
!And    eb.erncd         In ('513','515')                  !COD mod, MRP, 01/11/2001
And    eb.erncd         In ('513','515','519','522','523') !COD mod, MRP, 01/11/2001
End-Select
End-Procedure


Begin-Procedure P240-Print-Record
#debug9 Show 'P240-Print-Record'
  Lookup XlatLookup $empl_status $status

  Print $emplid          (+1,{#column1_d})   !Edit xxx-xx-xxxx  !Commented Edit GEX-CXB 10/12/2007 HCM9.0  Upgrade
  Print $name            (+0,{#column2_d},30)
  Print $jobtitle	 (+0,{#column9_d})   !Added GEX-CXB 10/12/2007 HCM9.0  Upgrade
  Print $jobcode         (+0,{#column3_d})
  Print $union_cd        (+0,{#column4_d})
  Print #Hours           (+0,{#column5_d})   Edit 9,990.99
  Print #Earns           (+0,{#column6_d})   Edit 99,990.99
  Print #Vac_Earns       (+0,{#column7_d})   Edit 99,990.99
  Print $status          (+0,{#column8_d})

  Add 1 To #recs-printed
End-Procedure


Begin-Procedure P260-SQL-Error-Found($Proc_Name) 
#debug9 Show 'P260-SQL-Error-Found'
  Display 'SQL Error in ' 
  Display $Proc_Name 
  Display $_sql-error
  Display 'sql-status:' 
  Display #_sql-status 
  stop
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Do Get-Current-DateTime    !curdttim.sqc

  Show ' '
  Show 'Report Ended:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime
  Show ' '
  Show 'Successful end of report'
End-Procedure


#include 'askaod.sqc'    !Ask user for $AsOfDate
#include 'getcodta.sqc'  !Get-Company-Data procedure
#include 'getpgdta.sqc'  !Get-Paygroup-Data procedure
#include 'getdptnm.sqc'  !Get-Department-Name procedure
#include 'gexxx920.sqc'  !Get ben single row run control
#include 'gexxx901.sqc'  
#include 'gexxx903.sqc'  
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
