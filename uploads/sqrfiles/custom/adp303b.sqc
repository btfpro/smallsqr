!********************************************************************** 
! SQR Name:           adp303b.sqc  
! SQR Descr:          ADP Tax Service Transporter 
! Created by:         drmonroe 
! Create date:        10/25/2010 
! Last modified by:   drmonroe
! Last modified date: 02/17/2015 
!
! ------------------ v4Q2010 release -----------
! Nov 19, 2010  fix to Box01_03_05 summary, other earnings not displayed on W-2 for "Add to gross / non-taxable" types
! Nov 22, 2010  fix to Box01_03_05 summary, bypass not taxform_id = 'W' companies
!               BOX_01_03_05_SUMMARY
! Dec 06, 2010  ITE Box 14L / Legal in summary
! Dec 29, 2010  move 'zero' of variables after 'skip_box_01_03_05' label to keep bleeding from prior company out
!   ********** V3Q2011 ***************************
! Sep 28, 2011 dynamically build the Fica Limit
!              QUARTERLY_HISTORY
! Oct 13, 2011 incorporate MCK1 logic from 2010 year-end
! Dec 20, 2011 FULL_BOX_SUMMARY_W2 - increase Box 02,04,14,17,19 field widths to accomodate >1M in tax values
! Dec 28, 2011 USE_12C_in_Net_01_03_05
! Jan 05, 2012 added back in 'BOX_01_03_05_Warning_Details'
!   ********** V3Q2012 ***************************
! Sep 14, 2012 BYPASS_BOX99_LOGIC, shrink code base
! Oct  7, 2013 #debugd
! Jan  6, 2014 ITE1 - Dedcd 'F1' handling
! Oct 16, 2014 BOX_01_03_05_Warning_Details into report / not trace
! Dec  2, 2014 debugging in write-misc-box-records 
! Dec 17, 2014 change ' increment-box-warnings-details' to handle the overflow better
! Dec 17, 2014 MTN1 Verbage 1-9, if first 3 chars of verbagEIN
! Dec 24, 2014 force a blank line if the condition warrants skipping the summary
! Feb 17, 2015 USE_INT_CRITERIA
! May 12, 2015 SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN SKIP or GENERATE
! Jan  8, 2016 add show for MAX_BOX_WARNINGS, and up default to 20000
!********************************************************************************************************************* 

#define        adp303b_release     2016.01.08          ! yyyy.mm.dd.n  (sync this with pbzqtr.sqr's release version
#define        adp303b_version     adp303b.Jan 8, 2016

#if {SITE_ID} = 'ITE1'
  #define USE_12C_in_Net_01_03_05
#endif

#ifdef BOX_01_03_05_SUMMARY
  #define SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN     GENERATE                 !default to on for 01/03/05
  #define BOX_01_03_05_Warning_Details
  #ifndef MAX_BOX_WARNINGS
    #define MAX_BOX_WARNINGS 20000
  #endif
#endif



!#define debug_adp303b

!---------- HERE'S THE MAIN HOOK to get the Box (B) recs....FROM pbzqtr.sqr included by adp303q.sqc ------

begin-procedure write-misc-box-records  !ALL W-2 boxes extracted and output

 encode <39> into $tick

 if #write-misc-box-records = 0
   add 1 to #write-misc-box-records
   show 'write-misc-box-records , verson: {adp303b_version}'
   #ifdef BYPASS_BOX99_LOGIC
      show 'write-misc-box-records: BYPASS_BOX99_LOGIC enabled'
   #endif
   #ifdef DEDUCTS_AND_EARNINGS_MODE
      show 'write-misc-box-records: DEDUCTS_AND_EARNINGS_MODE = {DEDUCTS_AND_EARNINGS_MODE}'
   #endif
   #ifdef LIMITED_BOX_SUMMARY_W2
      show 'write-misc-box-records: LIMITED_BOX_SUMMARY_W2 enabled.'
   #endif
   #ifdef FULL_BOX_SUMMARY_W2
      show 'write-misc-box-records: FULL_BOX_SUMMARY_W2 enabled.'
   #endif
   #ifdef W2_VERBAGE_LINE1
      show 'write-misc-box-records: W2_VERBAGE_LINE1 = {W2_VERBAGE_LINE1}'
   #endif
   #ifdef W2_VERBAGE_LINE2
      show 'write-misc-box-records: W2_VERBAGE_LINE2 = {W2_VERBAGE_LINE2}'
   #endif
   #ifdef W2_VERBAGE_LINE3
      show 'write-misc-box-records: W2_VERBAGE_LINE3 = {W2_VERBAGE_LINE3}'
   #endif
   #ifdef W2_VERBAGE_LINE4
      show 'write-misc-box-records: W2_VERBAGE_LINE4 = {W2_VERBAGE_LINE4}'
   #endif
   #ifdef W2_VERBAGE_LINE5
      show 'write-misc-box-records: W2_VERBAGE_LINE5 = {W2_VERBAGE_LINE5}'
   #endif
   #ifdef W2_VERBAGE_LINE6
      show 'write-misc-box-records: W2_VERBAGE_LINE6 = {W2_VERBAGE_LINE6}'
   #endif
   #ifdef W2_VERBAGE_LINE7
      show 'write-misc-box-records: W2_VERBAGE_LINE7 = {W2_VERBAGE_LINE7}'
   #endif
   #ifdef W2_VERBAGE_LINE8
      show 'write-misc-box-records: W2_VERBAGE_LINE8 = {W2_VERBAGE_LINE8}'
   #endif
   #ifdef W2_VERBAGE_LINE9
      show 'write-misc-box-records: W2_VERBAGE_LINE9 = {W2_VERBAGE_LINE9}'
   #endif
 end-if

 #ifdef BOX_01_03_05_Warning_Details   !20141016 move to the report from the trace
    if #BOX_01_03_05_Warning_Details < 1
      show 'Initializing BOX_01_03_05_Warning_Details, verson: {adp303b_version}, Max warnings = {MAX_BOX_WARNINGS}'
     
      create-array name=BOX_Warning_Details size={MAX_BOX_WARNINGS}  !Summary earnings and deductions
      field=Company:char
      field=Emplid:char
      field=Box:char
      field=Descr:char
      
      add 1 to #BOX_01_03_05_Warning_Details
    end-if
    
 #endif

 if $SelectEmplid <> ''
    show 'write-misc-box-records: ' $Current_Company ' ' $Current_Emplid ' TaxForm_ID ' $TAXFORM_ID ' void ' $check-for-void-w2
 end-if


 #ifdef  enable_performance_monitor
    do Get-Current-Time
    let $display_dt0 = $display_dt
 #endif

if rtrim($check-for-void-w2,' ') <> 't'

  let $rectp = 'B'

  do Process-W-2-Box-Details
  do Process-Box14

  #ifdef FEIN_IN_BOX74
    if #RptYear >= 2008
      
      if rtrim($Current_Company,' ' ) <> rtrim($prior_Box74_Company,' ')
        do get-fein-for-Box74  
        let $Box74_fein = 'FEIN#: ' || $Box74_fein
        move $Current_Company to $prior_Box74_Company
      end-if

      let $rectp = 'B'

      #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US74' || rpad($blank,29,' ')  || rpad($Box74_fein,75,' ')
         do populate_ADP_QTR_HIST
      #endif
   
      #ifdef USE_TEMP_TABLE_FOR_OUTPUT
         let $qout_str = 'B US74' || rpad($blank,29,' ')  || rpad($Box74_fein,75,' ')
         do log-B-rec-data
      #else

       Write 1 FROM 'B US74':6
                    $blank:29
                    $Box74_fein:75

        add 1 to #reccnt
        add 1 to #company_trailer_reccnt
      #endif
      add 1 to #B_REC_Total_Count
  
    end-if
  #endif
  
  #ifdef custom_BUS74_private_sdi_acct
    do custom_BUS74_private_sdi_acct_procedure !probiz.sqc...... $private_sdi_acct
  #endif
  
  if rtrim($private_sdi_acct,' ') <> ''  !added 12/13/04
    let $rectp = 'B'

    let $SDI_description = 'DI P.P.# '
    move $private_sdi_acct to $private_sdi_acct_out
   #ifdef STRING_US74_DESC_AND_PLAN_NBR

    #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US74' || ' ' || rpad($blank,26,' ') || '00' || rpad($SDI_description,10,' ') || rpad($private_sdi_acct_out,75,' ')
         do populate_ADP_QTR_HIST
    #endif

    #ifdef USE_TEMP_TABLE_FOR_OUTPUT
       let $qout_str = 'B US74' || ' ' || rpad($blank,26,' ') || '00' || rpad($SDI_description,10,' ') || rpad($private_sdi_acct_out,75,' ')
       do log-B-rec-data
    #else

    Write 1 FROM    $rectp:1
                    $blank:1
                    'US':2
                    '74':2
                    $blank:1
                    $blank:26
                    '00':2
                    $SDI_description:10
                    $private_sdi_acct_out:75

      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
    #endif

   #else

    #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US74' || ' ' || rpad($SDI_description,10,' ') || rpad($blank,16,' ') || '00' || rpad($private_sdi_acct_out,75,' ')
         do populate_ADP_QTR_HIST
    #endif

    #ifdef USE_TEMP_TABLE_FOR_OUTPUT
       let $qout_str = 'B US74' || ' ' || rpad($SDI_description,10,' ') || rpad($blank,16,' ') || '00' || rpad($private_sdi_acct_out,75,' ')
       do log-B-rec-data
    #else

    Write 1 FROM    $rectp:1
                    $blank:1
                    'US':2
                    '74':2
                    $blank:1
                    $SDI_description:10
                    $blank:16
                    '00':2
                    $private_sdi_acct_out:75

      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
    #endif

   #endif
   add 1 to #B_REC_Total_Count

    
  end-if

  #ifdef custom_BNJ74_FLI_Plan_Text
    do custom_BNJ74_FLI_Plan_Text_procedure !probiz.sqc...... $FLI_Plan_Text
  #endif
  
  if rtrim($FLI_Plan_Text,' ') <> ''  !added 10/28/09
    let $rectp = 'B'

    let $FLI_description = 'FLI P.P.# '

   #ifdef STRING_NJ74_DESC_AND_PLAN_NBR
    #ifdef QUARTERLY_HISTORY
         let $box_str = 'B NJ74' || ' ' || rpad($blank,26,' ') || rpad($blank,2,' ') || rpad($FLI_description,10,' ') || rpad($FLI_Plan_Text,75,' ')
         do populate_ADP_QTR_HIST
    #endif

    #ifdef USE_TEMP_TABLE_FOR_OUTPUT
       let $qout_str = 'B NJ74' || ' ' || rpad($blank,26,' ') || rpad($blank,2,' ') || rpad($FLI_description,10,' ') || rpad($FLI_Plan_Text,75,' ')
       do log-B-rec-data
    #else

    Write 1 FROM    $rectp:1
                    $blank:1
                    'NJ':2
                    '74':2
                    $blank:1
                    $blank:26
                    $blank:2
                    $FLI_description:10
                    $FLI_Plan_Text:75

      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
    #endif
   
   #else
    #ifdef QUARTERLY_HISTORY
         let $box_str = 'B NJ74' || ' ' || rpad($FLI_description,10,' ') || rpad($blank,16,' ') || rpad($blank,2,' ') || rpad($FLI_Plan_Text,75,' ')
         do populate_ADP_QTR_HIST
    #endif
    #ifdef USE_TEMP_TABLE_FOR_OUTPUT
       let $qout_str = 'B NJ74' || ' ' || rpad($FLI_description,10,' ') || rpad($blank,16,' ') || rpad($blank,2,' ') || rpad($FLI_Plan_Text,75,' ')
       do log-B-rec-data
    #else

    Write 1 FROM    $rectp:1
                    $blank:1
                    'NJ':2
                    '74':2
                    $blank:1
                    $FLI_description:10
                    $blank:16
                    $blank:2
                    $FLI_Plan_Text:75

      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
    #endif
   #endif
   add 1 to #B_REC_Total_Count

  end-if
  
! box 99 records
! ----------------
  
  let $Summary_Company_Selection    = 'COMPANY IN ('  || $tick || $Current_Company || $tick || ')'

 #ifdef SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN
   let #Box99_count = 0
   let $rectp = 'B'
   let $summary_multi_comp_same_FEIN = 'f'
   do check-multi-comp-same-FEIN
   if $SelectEmplid <> ''
     show 'SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN {SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN}: ' $summary_multi_comp_same_FEIN
   end-if

   if $summary_multi_comp_same_FEIN = 't'
 
    if rtrim('{SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN}',' ') = 'SKIP'
      let $descr_warning = 'Skipped Mult W-2 Summary'
      put $Current_Company $Current_Emplid '' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $Desc = ' '
      do W2_Verbage     !force a blank line 12/23/14
      if $SelectEmplid <> ''
        show 'Message: ' $descr_warning
      end-if
      goto skip_w2_summary
    end-if
    if rtrim('{SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN}',' ') = 'GENERATE'
      let $descr_warning = 'Consolidated Mult W-2 Summary'
      put $Current_Company $Current_Emplid '' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $Desc = ' '
      do W2_Verbage     !force a blank line 12/23/14
      if $SelectEmplid <> ''
        show 'Message: ' $descr_warning
      end-if
    end-if
    
   end-if
 #endif
 
  if $SelectEmplid <> ''
   show 'Summary_Company_Selection ' $Summary_Company_Selection
  end-if
  
 
  !BOX 99 records
  !---------------
#ifndef BYPASS_BOX99_LOGIC
  let #max_Box99_in_spec = 30               !we can have 30 box 99 recs.. unless some of these switches gobble some of them
  #ifdef BALANCE_TO_BOX1_SUMMARY_LINE
   let #max_Box99_in_spec = #max_Box99_in_spec - 2
  #endif
  #ifdef W2_EMPLID_IN_SUMMARY                                                   ! KCTCS_NWC
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef LIMITED_BOX_SUMMARY_W2
   let #max_Box99_in_spec = #max_Box99_in_spec - 8
  #endif
  #ifdef FULL_BOX_SUMMARY_W2
   let #max_Box99_in_spec = #max_Box99_in_spec - 23
  #endif
  #ifdef BOX_01_03_05_SUMMARY
   let #max_Box99_in_spec = #max_Box99_in_spec - 4   !Total Earnings, Less, blank and Reported lines (4)
  #endif
  #ifdef W2_EXEMPTIONS_MARITAL
   let #max_Box99_in_spec = #max_Box99_in_spec - 2
  #endif
  #ifdef W2_VERBAGE_LINE1
   let #max_Box99_in_spec = #max_Box99_in_spec - 2    !blank and verbage line #1
  #endif
  #ifdef W2_VERBAGE_LINE2
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_VERBAGE_LINE3
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_VERBAGE_LINE4
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_VERBAGE_LINE5
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_VERBAGE_LINE6
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_VERBAGE_LINE7
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_VERBAGE_LINE8
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_VERBAGE_LINE9
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  #ifdef W2_COMPANY_INFO_IN_SUMMARY_SECTION
   let #max_Box99_in_spec = #max_Box99_in_spec - 3
  #endif
  #ifdef BOX99_TOTAL_GROSS
   let #max_Box99_in_spec = #max_Box99_in_spec - 1
  #endif
  
  move '$$$,$$$,$$9.99mi' to $FmtWage
  move '$$$,$$9.99mi'     to $FmtTax
  move '$$$,$$9.99mi'     to $FmtTax
  
  !----------------------------------------------------------------------------------------

  let #Box99_count = 0
  #if {DEDUCTS_AND_EARNINGS_MODE} = 'BALANCE_TO_BOX1'
    do Process-Box99
  #endif

  #ifdef W2_EMPLID_IN_SUMMARY                                                   ! KCTCS_NWC
      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')
      let $Desc = 'EMPLOYEE ID: ' || $Current_Emplid
      let $Desc = rpad($Desc,75,' ')
  
    #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || rpad($blank,27,' ') || rpad($Box99_count,2,' ') || rpad($Desc,76,' ')
         do populate_ADP_QTR_HIST
    #endif

    #ifdef USE_TEMP_TABLE_FOR_OUTPUT
       let $qout_str = 'B US99' || rpad($blank,27,' ') || rpad($Box99_count,2,' ') || rpad($Desc,76,' ')
       do log-B-rec-data
    #else

      Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2          
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:75
                       $blank:1

       add 1 to #reccnt
       add 1 to #company_trailer_reccnt
    
    #endif
  
    add 1 to #B_REC_Total_Count
  
  #endif
  
  #ifdef LIMITED_BOX_SUMMARY_W2

  !---------------------------------------------------------------------------
  !1.   GROSS WAGES              xxx,xxx,xxx.xx-
  !2.   Box 01: TAXABLE WAGES    xxx,xxx,xxx.xx-       
  !3.   Box 02: FED TAX WITHHELD xxx,xxx,xxx.xx-
  !4.   Box 04: SOCIAL SEC TAX   xxx,xxx,xxx.xx- 
  !5.   Box 06  MEDICARE TAX     xxx,xxx,xxx.xx-
  !6.   Box 14: SDI TAX          xxx,xxx,xxx.xx-
  !7.   Box 17: STATE INCOME TAX xxx,xxx,xxx.xx-   
  !8.   Box 19: LOCAL INCOME TAX xxx,xxx,xxx.xx-
  !---------------------------------------------------------------------------

   !Need to populate the amounts below.. then we're ready.
   !-----------------------------------------------------

   do Format-Number(#Box01_Current, $Box01_Current, $FmtWage)
   do Format-Number(#Box02_Current, $Box02_Current, $FmtWage)  
   do Format-Number(#Box04_Current, $Box04_Current, $FmtWage)
   do Format-Number(#Box06_Current, $Box06_Current, $FmtWage)
   do Format-Number(#Box17_Current, $Box17_Current, $FmtWage)
   do Format-Number(#Box19_Current, $Box19_Current, $FmtWage)
   do Format-Number(#Box14_Current, $Box14_Current, $FmtWage)

   do calc-gross99
   do Format-Number(#Total_Gross_ytd, $Total_Gross, $FmtWage)
   add #Total_Gross_ytd to #Total_Gross99_C
   add #Total_Gross_ytd to #Total_Gross99_Hash

   let $verbage = 'GROSS WAGES              ' || $Total_Gross   
   do write-limited-desc-w-2
   
   let $verbage = 'BOX 01: TAXABLE WAGES    ' || $Box01_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 02: FED TAX WITHHELD ' || $Box02_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 04: SOCIAL SEC TAX   ' || $Box04_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 06: MEDICARE TAX     ' || $Box06_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 14: SDI TAX          ' || $Box14_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 17: STATE INCOME TAX ' || $Box17_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 19: LOCAL INCOME TAX ' || $Box19_Current
   do write-limited-desc-w-2

   let #Box01_Current  = 0
   let #Box02_Current  = 0
   let #Box03_Current  = 0
   let #Box04_Current  = 0
   let #Box05_Current  = 0
   let #Box06_Current  = 0
   let #Box16_Current  = 0
   let #Box17_Current  = 0
   let #Box18_Current  = 0
   let #Box19_Current  = 0
   let #Box14_Current  = 0
   let #Total_Gross_ytd    = 0
   
  #endif
     
  #ifdef FULL_BOX_SUMMARY_W2

  !---------------------------------------------------------------------------
  !1.   GROSS WAGES              xxx,xxx,xxx.xx-
  !2.   Box 01: TAXABLE WAGES    xxx,xxx,xxx.xx-       
  !3.   Box 02: FED TAX WITHHELD xxx,xxx,xxx.xx-
  !4.   Box 04: SOCIAL SECURITY TAX  xxx,xxx.xx- 
  !5.   Box 06  MEDICARE TAX         xxx,xxx.xx-
  !6.   Box 14: SDI TAX              xxx,xxx.xx-
  !7.   Box 17: STATE INCOME TAX     xxx,xxx.xx-   
  !8.   Box 19: LOCAL INCOME TAX     xxx,xxx.xx-
  !9
  !10   Your Gross Pay was adjusted as follows to produce your W-2
  !11              Wages, Tips, other   Social Security   Medicare 
  !12              Compensation         Wages             Wages
  !13              Box 1                Box 3             Box 5
  !14
  !15   Gross Pay  xxx,xxx,xxx.xx-      xxx,xxx,xxx.xx-   xxx,xxx,xxx.xx-
  !16.  PLUS GTL       xxx,xxx.xx-          xxx,xxx.xx-       xxx,xxx.xx-
  !17.  PLUS LEGAL     xxx,xxx.xx-          xxx,xxx.xx-       xxx,xxx.xx-
  !18.  LESS 401K      xxx,xxx.xx-                 N/A               N/A  
  !19.  LESS n/tax compxxx,xxx.xx-          xxx,xxx.xx-       xxx,xxx.xx-
  !20   LESS FSA       xxx,xxx.xx-          xxx,xxx.xx-       xxx,xxx.xx-
  !21   LESS 125       xxx,xxx.xx-          xxx,xxx.xx-       xxx,xxx.xx-   
  !22   Wages over Limit      N/A       xxx,xxx,xxx.xx               N/A
  !23.  Reported   xxx,xxx,xxx.xx-      xxx,xxx,xxx.xx-   xxx,xxx,xxx.xx-
  !---------------------------------------------------------------------------
  !Need to populate the amounts below.. then we're ready.
  !-----------------------------------------------------

   do calc-gross99

   add #Total_Gross_ytd to #Total_Gross99_C
   add #Total_Gross_ytd to #Total_Gross99_Hash

   do Format-Number(#Total_Gross_ytd,      $Total_Gross,          $FmtWage)
   do Format-Number(#Total_Gross_ytd,      $Total_Gross_ytd,      $FmtWage)
   do Format-Number(#Total_Gross_ytd_fica, $Total_Gross_ytd_fica, $FmtWage)
   do Format-Number(#Total_Gross_ytd_med,  $Total_Gross_ytd_med,  $FmtWage)
   do Format-Number(#Box01_Current,        $Box01_Current, $FmtWage)
   do Format-Number(#Box02_Current,        $Box02_Current, $FmtWage)
   do Format-Number(#Box04_Current,        $Box04_Current, $FmtWage)
   do Format-Number(#Box06_Current,        $Box06_Current, $FmtWage)
   do Format-Number(#Box17_Current,        $Box17_Current, $FmtWage)
   do Format-Number(#Box19_Current,        $Box19_Current, $FmtWage)
   do Format-Number(#Box14_Current,        $Box14_Current, $FmtWage)

   let #FSA_Current     = 0
   let #Cafe125         = 0
   let #non_Cafe125_ss  = 0
   let #non_Cafe125_med = 0
   do get-earnings-bal-nontaxable-federal

   #if {SITE_ID} = 'ITE1'
    !Cafe 125    Cafe 125 = Box 14J + 14M + 10 amounts
    let #m = 0
    while #m < 37
        get #amt14  from Box14(#m) Namt
        if #amt14 <> 0
          get $Box14 from Box14(#m) Box
          if $Box14 = '14J'
            let #Current_14J = #amt14
          end-if
          if $Box14 = '14L'
            let #Current_14L = #amt14
          end-if
          if $Box14 = '14M'
            let #Current_14M = #amt14
          end-if
        end-if
        add 1 to #m
    end-while

    let #Cafe125 = #Current_10 + #Current_14J + #Current_14M
    let #FSA_Current = #Current_12R

   #endif
   
   #if {SITE_ID} = 'NFU1'       !Ivon 12/31/09
       !select before-tax records (non-401k) from the PS_DEDUCTION_BAL where DED_CLASS = 'B' and DEDCD NOT IN ('M401K', 401k, 401C). 
       !The results should be balances selected for the following existing deductions:
       !dedcd MFSA should be reflected on the summary section under 'LESS FSA', 
       !the sum of all others should be reflected under 'LESS 125'. 
       !The reason why I'm asking you to select non-401K codes is because if in the future we introduce 
       !a new before-tax deduction, we don't need to update the SQR to include it.
   
    ! get-earnings-bal-nontaxable-federal calcs these for NFU1

   #endif

   let #Cafe125_ss = #Cafe125
   let #Cafe125_med = #Cafe125

   #if {SITE_ID} = 'NFU1'       !Ivon 1/4/10
      let #Cafe125_ss  = #Cafe125 - #non_Cafe125_ss
      let #Cafe125_med = #Cafe125 - #non_Cafe125_med
   #endif
   
   do Format-Number(#Cafe125,     $Cafe125,     $FmtTax)
   do Format-Number(#Cafe125_ss,  $Cafe125_ss,  $FmtTax)
   do Format-Number(#Cafe125_med, $Cafe125_med, $FmtTax)
   do Format-Number(#FSA_Current, $FSA_Current, $FmtTax)
   do Format-Number(#Current_12C, $Box12C_Current, $FmtTax)
   do Format-Number(#Current_12D, $Box12D_Current, $FmtTax)
   do Format-Number(#Current_14L, $Box14L_Current, $FmtTax)
   do Format-Number(#non_taxable_fed, $non_taxable_fed, $FmtTax)
   do Format-Number(#non_taxable_ss , $non_taxable_ss , $FmtTax)
   do Format-Number(#non_taxable_med, $non_taxable_med, $FmtTax)
   
   #ifdef debug_adp303b
     #debugd show 'Full W-2 summary,                   12C = ' #Current_12C ' 12D = ' #Current_12D ' 14L = ' #Current_14L
     #debugd show '                       #non_taxable_fed = ' #non_taxable_fed ', #non_taxable_ss   = ' #non_taxable_ss       ', #non_taxable_med = ' #non_taxable_med
     #debugd show '                       #Cafe125         = ' #Cafe125         ', #Cafe125_ss       = ' #Cafe125_ss           ', #Cafe125_med     = ' #Cafe125_med    
     #debugd show '                       Total Gross Pay  = ' #Total_Gross_ytd ', Total Gross Fica  = ' #Total_Gross_ytd_fica ', Total Gross Med  = ' #Total_Gross_ytd_med
   #endif

   #ifndef USE_12C_in_Net_01_03_05
    let #Current_12C = 0
   #endif
   
   let #01_diff = #Total_Gross_ytd      + #Current_14L + #Current_12C - #Current_12D - #FSA_Current - #Cafe125 - #non_taxable_fed + #Total_other_ytd
   let #03_diff = #Total_Gross_ytd_fica + #Current_14L + #Current_12C - #FSA_Current - #Cafe125_ss - #non_taxable_ss + #Total_other_ytd_fica
   let #05_diff = #Total_Gross_ytd_med  + #Current_14L + #Current_12C - #FSA_Current - #Cafe125_med - #non_taxable_med + #Total_other_ytd_med

   do set-fica-wage-limit
   
   let #wages_over_limit = 0
   if #03_diff >= #fica_limit
      let #wages_over_limit = #03_diff - #fica_limit
      #ifdef debug_adp303b
        #debugd show 'Full W-2 summary: Adjusting Reported Fica down to limit of ' #fica_limit ', from ' #03_diff      
      #endif
      let #03_diff =  #fica_limit
   end-if
   
   do Format-Number(#Total_other_ytd     , $Total_other_ytd     , $FmtWage)
   do Format-Number(#Total_other_ytd_fica, $Total_other_ytd_fica, $FmtWage)
   do Format-Number(#Total_other_ytd_med,  $Total_other_ytd_med,  $FmtWage)
   do Format-Number(#wages_over_limit,     $wages_over_limit,     $FmtWage)


   do Format-Number(#01_diff, $01_diff, $FmtWage)
   do Format-Number(#03_diff, $03_diff, $FmtWage)
   do Format-Number(#05_diff, $05_diff, $FmtWage)
   
   
   let $verbage = 'GROSS WAGES              ' || $Total_Gross   
   do write-limited-desc-w-2
   
   let $verbage = 'BOX 01: TAXABLE WAGES    ' || $Box01_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 02: FED TAX WITHHELD ' || $Box02_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 04: SOCIAL SEC. TAX  ' || $Box04_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 06: MEDICARE TAX     ' || $Box06_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 14: SDI TAX          ' || $Box14_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 17: STATE INCOME TAX ' || $Box17_Current
   do write-limited-desc-w-2

   let $verbage = 'BOX 19: LOCAL INCOME TAX ' || $Box19_Current
   do write-limited-desc-w-2

   let $verbage = ' '
   do write-limited-desc-w-2

   let $verbage = 'Your Gross Pay was adjusted as follows to produce your W-2'
   do write-limited-desc-w-2

   let $verbage = ' '
   do write-limited-desc-w-2

   let $verbage = '           Wages, Tips, other   Social Security   Medicare '
   do write-limited-desc-w-2
   
   let $verbage = '           Compensation         Wages             Wages    '
   do write-limited-desc-w-2

   let $verbage = '                    Box 1                Box 3             Box 5'
   do write-limited-desc-w-2

   let $verbage = ' '
   do write-limited-desc-w-2

   let $verbage = 'Gross Pay  '     || $Total_Gross_ytd    || '      '     || $Total_Gross_ytd_fica   || '   '     || $Total_Gross_ytd_med
   do write-limited-desc-w-2

   let $verbage = 'PLUS GTL       ' || $Box12C_Current     || '          ' || $Box12C_Current      || '       ' || $Box12C_Current
   do write-limited-desc-w-2
#if {SITE_ID} = 'ITE1'
   let $verbage = 'PLUS LEGAL     ' || $Box14L_Current     || '          ' || $Box14L_Current      || '       ' || $Box14L_Current
   do write-limited-desc-w-2
#endif
   let $verbage = 'LESS 401K      ' || $Box12D_Current     || '                 N/A               N/A '
   do write-limited-desc-w-2

   let $verbage = 'LESS NON-TXBL  ' || $non_taxable_fed    || '          '  || $non_taxable_ss     || '       ' || $non_taxable_med       
   do write-limited-desc-w-2

   let $verbage = 'LESS FSA       ' || $FSA_Current        || '          '  || $FSA_Current        || '       ' || $FSA_Current
   do write-limited-desc-w-2

   let $verbage = 'LESS 125       ' || $Cafe125            || '          '  || $Cafe125_ss         || '       ' || $Cafe125_med
   do write-limited-desc-w-2

   let $verbage = 'Oth Comp   '     || $Total_other_ytd    || '      '      || $Total_other_ytd_fica  || '   '     || $Total_other_ytd_med
   do write-limited-desc-w-2

   let $verbage = 'Wages over '     || '           N/A'    || '       '     || $wages_over_limit      || '   '     || '           N/A'
   do write-limited-desc-w-2

   let $verbage = 'Reported   '     || $01_Diff            || '      '      || $03_Diff               || '   '     || $05_Diff       
   do write-limited-desc-w-2

 
   let #Box01_Current  = 0
   let #Box02_Current  = 0
   let #Box03_Current  = 0
   let #Box04_Current  = 0
   let #Box05_Current  = 0
   let #Box06_Current  = 0
   let #Box16_Current  = 0
   let #Box17_Current  = 0
   let #Box18_Current  = 0
   let #Box19_Current  = 0
   let #Box14_Current  = 0
   let #cafe125        = 0
   let #cafe125_ss     = 0
   let #cafe125_med    = 0
   let #Total_Gross_ytd = 0
   let #Total_Gross_ytd_fica = 0
   let #Total_Gross_ytd_med = 0
   let #Total_other_ytd = 0
   let #Total_other_ytd_fica = 0
   let #Total_other_ytd_med = 0
   let #Current_14J = 0
   let #Current_14M = 0
   let #Current_12C = 0
   let #Current_12D = 0
   let #Current_14L = 0
   let #Current_12R = 0
   let #Current_10  = 0
   let #FSA_Current = 0
  #endif

  #ifdef BOX_01_03_05_SUMMARY
    do Box01_03_05_Summary
  #endif

  #ifdef W2_EXEMPTIONS_MARITAL
  
      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')
      move  'Taxable Marital Status ' to $Desc

    #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || '00' || rpad($Desc,35,' ') || rpad($FWT_Mar_Status,16,' ')
         do populate_ADP_QTR_HIST
    #endif
    
    Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:35
                       $FWT_Mar_Status:16
                       $blank:25

       add 1 to #reccnt
       add 1 to #company_trailer_reccnt
       add 1 to #B_REC_Total_Count

      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')
      move  'Federal Exemptions ' to $Desc

    #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || 
                         rpad($Desc,35,' ') || rpad($FWT_ALLOWANCES,16,' ')
         do populate_ADP_QTR_HIST
    #endif
    
    Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:35
                       $FWT_ALLOWANCES:16
                       $blank:25

       add 1 to #reccnt
       add 1 to #company_trailer_reccnt
       add 1 to #B_REC_Total_Count

  #endif
  
  #ifdef BOX99_TOTAL_GROSS

      do calc-gross99
      do Format-Number(#Total_Gross_ytd, $Total_Gross, $FmtWage)

      add #Total_Gross_ytd to #Total_Gross99_C
      add #Total_Gross_ytd to #Total_Gross99_Hash

      let $Desc = 'Total Gross Wages = '
      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')

     #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || 
                         rpad($Desc,35,' ') || rpad($Total_Gross,16,' ')
         do populate_ADP_QTR_HIST
     #endif

      Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:35
                       $Total_Gross:16
                       $blank:25

      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
      add 1 to #B_REC_Total_Count

  #endif
  

  #ifdef W2_VERBAGE_LINE_CENTER
  
      let #blank_verbage = 0
      while #blank_verbage < {W2_VERBAGE_LINE_BLANKS}          ! blank lines of Box 99 recs to center it on the W-2
        do write-blank-verbage
        add 1 to #blank_verbage
      end-while
      
      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')

      let $verbage = '{W2_VERBAGE_LINE_CENTER}'

     #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || rpad($verbage,75,' ') 
         do populate_ADP_QTR_HIST
     #endif
     
     Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $verbage:75
                       $blank:1
      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
  
  #endif

  let $spaces = '                  '

  #ifdef W2_VERBAGE_LINE1
     let $Desc = ''
     do W2_Verbage
     let $Desc = '{W2_VERBAGE_LINE1}'
     #ifdef W2_VERBAGE_LINE1_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE1_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE2
     let $Desc = '{W2_VERBAGE_LINE2}'
     #ifdef W2_VERBAGE_LINE2_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE2_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE3
     let $Desc = '{W2_VERBAGE_LINE3}'
     #ifdef W2_VERBAGE_LINE3_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE3_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE4
     let $Desc = '{W2_VERBAGE_LINE4}'
     #ifdef W2_VERBAGE_LINE4_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE4_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE5
     let $Desc = '{W2_VERBAGE_LINE5}'
     #ifdef W2_VERBAGE_LINE5_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE5_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE6
     let $Desc = '{W2_VERBAGE_LINE6}'
     #ifdef W2_VERBAGE_LINE6_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE6_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE7
     let $Desc = '{W2_VERBAGE_LINE7}'
     #ifdef W2_VERBAGE_LINE7_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE7_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE8
     let $Desc = '{W2_VERBAGE_LINE8}'
     #ifdef W2_VERBAGE_LINE8_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE8_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  #ifdef W2_VERBAGE_LINE9
     let $Desc = '{W2_VERBAGE_LINE9}'
     #ifdef W2_VERBAGE_LINE9_IDENT_SPACES 
       let $Desc = substr($spaces,1,{W2_VERBAGE_LINE9_IDENT_SPACES}) || $Desc
     #endif
     do W2_Verbage
  #endif
  
  #ifdef W2_COMPANY_INFO_IN_SUMMARY_SECTION
   
     let $Company = $Current_Company
     do get-company-data

     !COMPANY NAME
     !------------
    
      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')
      move  $CompanyName to $Desc

     #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || rpad($Desc,35,' ') 
         do populate_ADP_QTR_HIST
     #endif

      Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:35
                       $blank:16
                       $blank:25

       add 1 to #reccnt
       add 1 to #company_trailer_reccnt
       add 1 to #B_REC_Total_Count


   !COMPANY ADDRESS
   !---------------
   
      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')
      move  $Compnyadd1 to $Desc

     #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || rpad($Desc,35,' ') 
         do populate_ADP_QTR_HIST
     #endif

      Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:35
                       $blank:16
                       $blank:25

       add 1 to #reccnt
       add 1 to #company_trailer_reccnt
       add 1 to #B_REC_Total_Count


   !COMPANY CITY, STATE, ZIPCODE CT.STATE,CT.POSTAL
   !-----------------------------------------------
   
      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')
      let $Desc = rtrim(&CT.CITY,' ') || ', ' || rtrim(&CT.STATE,' ') || ' ' || rtrim(&CT.POSTAL,' ')

     #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || rpad($Desc,35,' ') 
         do populate_ADP_QTR_HIST
     #endif
      Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:35
                       $blank:16
                       $blank:25

       add 1 to #reccnt
       add 1 to #company_trailer_reccnt
       add 1 to #B_REC_Total_Count
   #endif
  #endif
 end-if

skip_w2_summary:

 let #YE_AMOUNT_YTD_Emplid = 0
 let #TAX_BALANCE_YTD_Emplid = 0

 !added here to make sure we clear box totals
 !--------------------------------------------
 let #box_w2_count = 0
 clear-array name=Box_W2
 let #Box14_count = 0
 clear-array name=Box14
 let #Box_99_rec_count = 0
 clear-array name=Box99
 
  #ifdef  enable_performance_monitor
     do Get-Current-Time
     move 'write-misc-box-records' to $debug-proc1
     move ' '    to $debug-table1
     do log-delta-time
  #endif
end-procedure

#ifdef SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN
begin-procedure check-multi-comp-same-FEIN

  let $Company = $Current_Company
  do Company_FEIN
  let #orig_F = #F  
  let $Summary_Company_Selection    = 'COMPANY IN ('  || $tick || $Current_Company || $tick 


begin-SELECT
SKP1.COMPANY

  let $Company = &SKP1.COMPANY
  do Company_FEIN
  let #second_F = #F
  if #second_F = #orig_F
    let $summary_multi_comp_same_FEIN = 't'
    let $Summary_Company_Selection    = $Summary_Company_Selection  || ','  || $tick || &SKP1.COMPANY || $tick 
  end-if  
 
 FROM  PS_YE_EE SKP1 {NOLOCK_SQL}
   #ifdef USE_INT_CRITERIA
    WHERE SKP1.CALENDAR_YEAR   = int(\$RptYearZ\)
   #else
    WHERE SKP1.CALENDAR_YEAR   = #RptYear
   #endif
    AND SKP1.TAXFORM_ID      = 'W'
    AND SKP1.PROCESS_FLAG <> 'V'
    AND SKP1.COMPANY        <> $Current_Company
    AND SKP1.EMPLID          = $Current_Emplid
        
#ifdef SELECT_WITH_UR
 with ur
#endif
end-SELECT

  let $Summary_Company_Selection    =  $Summary_Company_Selection || ')'

end-procedure


begin-procedure Company_FEIN

  let #F = 0

begin-SELECT
SKP.FEDERAL_EIN

  let #F = &SKP.FEDERAL_EIN

FROM  PS_COMPANY_TBL SKP
WHERE SKP.COMPANY = $Company
  AND SKP.EFFDT = (SELECT MAX(EFFDT)
       FROM   PS_COMPANY_TBL
       WHERE  COMPANY = SKP.COMPANY
         AND  EFFDT  <= $Qtr_End_Native)
         
#ifdef SELECT_WITH_UR
 with ur
#endif
end-SELECT


end-procedure

#endif  !SUMMARY_WHEN_EMPLID_IN_MULT_COMPS_SAME_FEIN



begin-procedure W2_Verbage

    if $SelectEmplid <> ''
      show 'W2_Verbage. Desc ' $Desc
    end-if
    
    add 1 to #Box99_count
    do Format-Number(#Box99_count, $Box99_count, '99')

     #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || rpad($Desc,35,' ') 
         do populate_ADP_QTR_HIST
     #endif
     Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $Desc:35
                       $blank:16
                       $blank:25

      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
      add 1 to #B_REC_Total_Count

end-procedure


#ifdef W2_VERBAGE_LINE_CENTER
     
begin-procedure write-blank-verbage

      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')

     #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || ' ' || rpad($blank,10,' ') || rpad($blank,16,' ') || rpad($Box99_count,2,' ') || rpad($blank,75,' ') 
         do populate_ADP_QTR_HIST
     #endif
      Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $blank:75
                       $blank:1
      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
      
end-procedure
#endif


#ifdef FULL_BOX_SUMMARY_W2

begin-procedure get-earnings-bal-nontaxable-federal
   
   let #non_taxable_fed = 0
   let #non_taxable_ss  = 0
   let #non_taxable_med = 0
   let $Add_Gross = ''
   
 #ifdef  enable_performance_monitor
    do Get-Current-Time
    let $display_dt0 = $display_dt
 #endif

  let $EBNON_Summary_Company_Selection = 'AND EBNON.' || $Summary_Company_Selection
  
  
begin-SELECT
EBNON.GRS_YTD
EBNON.ERNCD
EBNON.COMPANY

 let #inx_company = INSTR($SUT_Exempt_state_str, $state_trimmed, 1)  !20130410
 if #inx_company > 0                                                 !20130410

   let $erncd = &EBNON.ERNCD
   do get-earnings-type
      
   if $ADD_GROSS = 'Y'

     if $Subject_FWT  <> 'Y'
       add  &EBNON.GRS_YTD to #non_taxable_fed  
     end-if
   
     if $Subject_FICA <> 'Y'
       add  &EBNON.GRS_YTD to #non_taxable_ss  
       add  &EBNON.GRS_YTD to #non_taxable_med   
     end-if

   end-if
 
 end-if
 

   FROM  PS_EARNINGS_BAL{WAREHOUSE_SUFFIX} EBNON {NOLOCK_SQL}
   WHERE EBNON.EMPLID           = $Current_Emplid

    #ifdef MVS                   !OS400 - /.../, NT - [...], MVS - \...\
      \$EBNON_Summary_Company_Selection\
    #else
      [$EBNON_Summary_Company_Selection]
    #endif
  
    AND EBNON.BALANCE_ID        = $Calendar_Year_Id
   #ifdef USE_INT_CRITERIA
    AND EBNON.BALANCE_YEAR      = int(\$RptYearZ\)
   #else
    AND EBNON.BALANCE_YEAR      = #RptYear
   #endif
    AND EBNON.SPCL_BALANCE      <> 'Y'
    AND EBNON.GRS_YTD           <> 0
    AND EBNON.BALANCE_PERIOD           =
       (SELECT MAX(BALANCE_PERIOD)
        FROM   PS_EARNINGS_BAL{WAREHOUSE_SUFFIX} {NOLOCK_SQL}
        WHERE  EMPLID            = EBNON.EMPLID
          AND  COMPANY           = EBNON.COMPANY
          AND  BALANCE_ID        = EBNON.BALANCE_ID
          #if {PeopleSoft_Version} >= '8'
           AND  {emp_rec}         = EBNON.{emp_rec}
          #endif
          AND  SPCL_BALANCE      = EBNON.SPCL_BALANCE
          AND  ERNCD             = EBNON.ERNCD
          AND  BALANCE_YEAR      = EBNON.BALANCE_YEAR

         #ifdef USE_INT_CRITERIA
            AND  BALANCE_PERIOD  <= int(\$RptMonthZ\))
         #else
            AND  BALANCE_PERIOD  <= #RptMonth)
         #endif


#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select

  let $DBNON_Summary_Company_Selection = 'AND DBNON.' || $Summary_Company_Selection


!add pre-tax deductions to the federal non-taxable 12/22/09
begin-SELECT
DBNON.DEDCD
DBNON.DED_YTD

   add  &DBNON.DED_YTD to #non_taxable_fed  

   #if {SITE_ID} = 'ITE1'
     if rtrim(&DBNON.DEDCD,' ') = 'F01' and #RptYear >= 2013
       add  &DBNON.DED_YTD to #non_taxable_med   
     end-if
   #endif


  FROM  PS_DEDUCTION_BAL{WAREHOUSE_SUFFIX} DBNON {NOLOCK_SQL}
  WHERE DBNON.EMPLID            = $Current_Emplid
    #ifdef MVS                   !OS400 - /.../, NT - [...], MVS - \...\
      \$DBNON_Summary_Company_Selection\
    #else
      [$DBNON_Summary_Company_Selection]
    #endif
    AND DBNON.BALANCE_ID        = $Calendar_Year_Id
    AND DBNON.DED_CLASS         = 'B'
    AND DBNON.PLAN_TYPE         = '00'             !general deductions, pre-tax only go into this catagory
   #ifdef USE_INT_CRITERIA
    AND DBNON.BALANCE_YEAR      = int(\$RptYearZ\)
   #else
    AND DBNON.BALANCE_YEAR      = #RptYear
   #endif
    AND DBNON.BALANCE_PERIOD           =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_DEDUCTION_BAL{WAREHOUSE_SUFFIX} {NOLOCK_SQL}
       WHERE  EMPLID            = DBNON.EMPLID
         AND  BALANCE_ID        = DBNON.BALANCE_ID
         AND  BALANCE_YEAR      = DBNON.BALANCE_YEAR
         #if {Peoplesoft_Version} >= '8'                 !12/3/04
         AND  BENEFIT_RCD_NBR   = DBNON.BENEFIT_RCD_NBR
         #endif
         AND  PLAN_TYPE         = DBNON.PLAN_TYPE
         AND  BENEFIT_PLAN      = DBNON.BENEFIT_PLAN
         AND  DED_CLASS         = DBNON.DED_CLASS
         AND  COMPANY           = DBNON.COMPANY
         AND  DEDCD             = DBNON.DEDCD
         #ifdef USE_INT_CRITERIA
            AND  BALANCE_PERIOD  <= int(\$RptMonthZ\))
         #else
            AND  BALANCE_PERIOD  <= #RptMonth)
         #endif

         AND DBNON.DED_YTD > 0

#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select


#if {SITE_ID} = 'NFU1'
       !select before-tax records (non-401k) from the PS_DEDUCTION_BAL where DED_CLASS = 'B' and DEDCD NOT IN ('M401K', 401k, 401C). 
       !The results should be balances selected for the following existing deductions:
       !dedcd MFSA should be reflected on the summary section under 'LESS FSA', 
       !the sum of all others should be reflected under 'LESS 125'. 
       !The reason why I'm asking you to select non-401K codes is because if in the future we introduce 
       !a new before-tax deduction, we don't need to update the SQR to include it.
   
    let $NFU1_Summary_Company_Selection = 'AND NFU1.' || $Summary_Company_Selection
   
begin-SELECT
NFU1.DEDCD
NFU1.DED_YTD


    let $dcd = rtrim(&NFU1.DEDCD,' ')
    uppercase $dcd
    
    if $dcd = 'MFSA'
     add  &NFU1.DED_YTD to #FSA_Current
    else
      if $dcd <> 'M401K' and $dcd <> '401K' and $dcd <> '401C'
        add  &NFU1.DED_YTD to #Cafe125
      end-if
    end-if
  
    if $dcd = '457'
      add &NFU1.DED_YTD to #non_Cafe125_ss
      add &NFU1.DED_YTD to #non_Cafe125_med
    end-if

  FROM  PS_DEDUCTION_BAL{WAREHOUSE_SUFFIX} NFU1 {NOLOCK_SQL}
  WHERE NFU1.EMPLID            = $Current_Emplid
    #ifdef MVS                   !OS400 - /.../, NT - [...], MVS - \...\
      \$NFU1_Summary_Company_Selection\
    #else
      [$NFU1_Summary_Company_Selection]
    #endif
    AND NFU1.BALANCE_ID        = $Calendar_Year_Id
    AND NFU1.DED_CLASS         = 'B'
   #ifdef USE_INT_CRITERIA
    AND NFU1.BALANCE_YEAR      = int(\$RptYearZ\)
   #else
    AND NFU1.BALANCE_YEAR      = #RptYear
   #endif
    AND NFU1.BALANCE_PERIOD           =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_DEDUCTION_BAL{WAREHOUSE_SUFFIX} {NOLOCK_SQL}
       WHERE  EMPLID            = NFU1.EMPLID
         AND  BALANCE_ID        = NFU1.BALANCE_ID
         AND  BALANCE_YEAR      = NFU1.BALANCE_YEAR
         #if {Peoplesoft_Version} >= '8'                 !12/3/04
         AND  BENEFIT_RCD_NBR   = NFU1.BENEFIT_RCD_NBR
         #endif
         AND  PLAN_TYPE         = NFU1.PLAN_TYPE
         AND  BENEFIT_PLAN      = NFU1.BENEFIT_PLAN
         AND  DED_CLASS         = NFU1.DED_CLASS
         AND  COMPANY           = NFU1.COMPANY
         AND  DEDCD             = NFU1.DEDCD
         #ifdef USE_INT_CRITERIA
            AND  BALANCE_PERIOD  <= int(\$RptMonthZ\))
         #else
            AND  BALANCE_PERIOD  <= #RptMonth)
         #endif

         AND NFU1.DED_YTD > 0

#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select

#endif

  #ifdef  enable_performance_monitor
     do Get-Current-Time
     move 'get-earnings-bal-federal' to $debug-proc1
     move 'PS_EARNINGS_BAL'    to $debug-table1
     do log-delta-time
  #endif

end-procedure
#endif


#ifdef FEIN_IN_BOX74
begin-procedure get-fein-for-Box74  

 let #Box74_FEIN = 0
 
begin-select
CO74.FEDERAL_EIN

   let #Box74_FEIN = &CO74.FEDERAL_EIN              

   FROM PS_COMPANY_TBL CO74 {NOLOCK_SQL}
   
       WHERE  CO74.COMPANY = $Current_Company
          AND CO74.EFFDT = (SELECT MAX(EFFDT)
           FROM   PS_COMPANY_TBL CO741 {NOLOCK_SQL}
           WHERE  CO741.COMPANY = CO74.COMPANY
             AND  CO741.EFFDT  <= $AsOfDate)

#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select

   move #Box74_FEIN to $Box74_FedEIN 099999999
   move $Box74_FedEIN to $Box74_fein xx-xxxxxxx

end-procedure
#endif

#ifdef BOX_01_03_05_SUMMARY
begin-procedure Box01_03_05_Summary

 if #Box01_03_05_Summary_cnt = 0
   show 'Box01_03_05_Summary: ' $Current_Company ' ' $Current_Emplid ' ' $TAXFORM_ID
   add 1 to #Box01_03_05_Summary_cnt
 end-if
 
   move '$$$,$$$,$$9.99mi' to $FmtWage

   #if {SITE_ID} = 'MCK1'
      !skip KPR and MPR and MCR companies for MCK1
      if $Current_Company = 'KPR' or $Current_Company = 'MPR' or $Current_Company = 'MCR'
       goto skip_box_01_03_05
      end-if
      move '$,$$$,$$$,$$9.99mi' to $FmtWage
   #endif

   #if {SITE_ID} = 'ADP1'
      !skip KPR and MPR and MCR companies for MCK1
      if $Current_Company = 'KPR' or $Current_Company = 'MPR' or $Current_Company = 'MCR'
       goto skip_box_01_03_05
      end-if
      move '$,$$$,$$$,$$9.99mi' to $FmtWage
   #endif


   !11/22/2010 added to bypass PR, 1099R companies...
   !--------------------------------------------------
   if $TAXFORM_ID <>  'W'
       goto skip_box_01_03_05
   end-if

   !Need to populate the amounts below.. then we're ready.
   !-----------------------------------------------------

   do calc-gross99 !returns #Total_earnings_ytd
 
   if $SelectEmplid <> ''
    show 'after calc-gross99 ' $Summary_Company_Selection ' YTD earnings = ' #Total_earnings_ytd
   end-if
   
   let $verbage = 'W-2 Summary: ' || '   ' || '     Fed:(Box 01        SocSec:(Box 03)       Med(Box 05)'
   do write-limited-desc-w-2

   let $verbage = '                    -------------       ---------------        -----------'
   do write-limited-desc-w-2

!Total Gross YTD
   do Format-Number(#Total_gross_ytd,      $Total_gross_ytd,      $FmtWage)
   do Format-Number(#Total_gross_ytd_fica, $Total_gross_ytd_fica, $FmtWage)
   do Format-Number(#Total_gross_ytd_med,  $Total_gross_ytd_med,  $FmtWage)
   let $verbage = 'Total Check YTD  '     || $Total_gross_ytd    || '     '     || $Total_gross_ytd_fica   || '  '     || $Total_gross_ytd_med
   do write-limited-desc-w-2

!Total Other earnings
   #ifdef TOTAL_OTHER_EARNINGS_SUMMARY
    do Format-Number(#Total_other_ytd,      $Total_other_ytd,      $FmtWage)
    do Format-Number(#Total_other_ytd_fica, $Total_other_ytd_fica, $FmtWage)
    do Format-Number(#Total_other_ytd_med,  $Total_other_ytd_med,  $FmtWage)
    let $verbage = 'Plus Other Earn  '     || $Total_other_ytd    || '     '     || $Total_other_ytd_fica   || '  '     || $Total_other_ytd_med
    do write-limited-desc-w-2
   #else  
    #ifdef TOTAL_OTHER_EARNINGS_DETAILS
     do write-other-earnings-details
    #endif
   #endif
   
!Total Taxable Earnings  (displayed only if they are different than Check YTD amounts)
   if (#Total_gross_ytd <> #Total_earnings_ytd) or (#Total_gross_ytd_fica <> #Total_earnings_ytd_fica) or (#Total_gross_ytd_med <> #Total_earnings_ytd_med)
    do Format-Number(#Total_earnings_ytd,      $Total_earnings_ytd,      $FmtWage)
    do Format-Number(#Total_earnings_ytd_fica, $Total_earnings_ytd_fica, $FmtWage)
    do Format-Number(#Total_earnings_ytd_med,  $Total_earnings_ytd_med,  $FmtWage)
    let $verbage = 'Taxable Earnings '     || $Total_earnings_ytd    || '     '     || $Total_earnings_ytd_fica   || '  '     || $Total_earnings_ytd_med
    do write-limited-desc-w-2
   end-if

!Less
   let $verbage = ''
   do write-limited-desc-w-2
   let $verbage = 'Less Pre-Tax Deductions'
   do write-limited-desc-w-2
   do get-pretax-deductions

!Reported    
   do get-01-03-05
   do Format-Number(#BOX_01_SUMMARY_Wages,      $Box01_Current,  $FmtWage)
   do Format-Number(#BOX_03_SUMMARY_Wages,      $Box03_Current,  $FmtWage)
   do Format-Number(#BOX_05_SUMMARY_Wages,      $Box05_Current,  $FmtWage)

   let $verbage = ''
   do write-limited-desc-w-2
   let $verbage = 'Reported on W-2  '     || $Box01_Current      || '     '     || $Box03_Current          || '  '     || $Box05_Current       
   do write-limited-desc-w-2

   if #Box99_count > 30
     
     #ifdef BOX_01_03_05_Warning_Details
      let $descr_warning = ' More than 30 Box 99 / IX records' 
      put $Current_Company $Current_Emplid '99' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
     #endif
     
     add 1 to #recon_earns_overrun

   end-if
  
   !Box 01
   do Get-Special-FWT-Status
   if $FWT_Status <> 'E'
    let #recon_earns = #BOX_01_SUMMARY_Wages - (#Total_earnings_ytd + #net_ded_ytd)
    if round (#recon_earns,2) <> 0

      do Format-Number(#BOX_01_SUMMARY_Wages, $BOX_01_SUMMARY_Wages,  $FmtWage)
      do Format-Number(#Total_other_ytd,      $Total_other_ytd     ,  $FmtWage)
      do Format-Number(#net_ern_ytd,          $net_ern_ytd         ,  $FmtWage)
      do Format-Number(#Total_gross_ytd,      $Total_gross_ytd     ,  $FmtWage)
      do Format-Number(#Total_earnings_ytd,   $Total_earnings_ytd  ,  $FmtWage)
      do Format-Number(#net_ded_ytd,          $net_ded_ytd         ,  $FmtWage)
      do Format-Number(#recon_earns,          $recon_earns         ,  $FmtWage)
      add 1 to #recon_total_warnings

     #ifdef BOX_01_03_05_Warning_Details
      let $descr_warning = ' Other Earnings      = ' || $Total_other_ytd 
      put $Current_Company $Current_Emplid '01' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Other Earnings  = ' || $net_ern_ytd
      put $Current_Company $Current_Emplid '01' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Gross wages         = ' ||  $Total_gross_ytd    
      put $Current_Company $Current_Emplid '01' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Box 01              = ' || $BOX_01_SUMMARY_Wages
      put $Current_Company $Current_Emplid '01' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Taxable Earnings    = ' || $Total_earnings_ytd 
      put $Current_Company $Current_Emplid '01' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Deducts & Benef = ' || $net_ded_ytd 
      put $Current_Company $Current_Emplid '01' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Earnings-Box 01 = ' || $recon_earns 
      put $Current_Company $Current_Emplid '01' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
     #endif

     add 1 to #recon_earns_errors

    else
      add 1 to #recon_earns_success
    end-if

   else
     show 'FWT Exempt associate: Company: ' $Current_Company ' Emplid: ' $Current_Emplid
   end-if

 ! A(Active),L(Leave),S(Suspend),P(Paid Leave).U(Un Paid Leave)
 if   $Empl_status = 'A' or $Empl_status = 'L' or $Empl_status = 'S' 
   or $Empl_status = 'P' or $Empl_status = 'U' or $Empl_status = 'T' 
   
  if $Job_FICA_Status = 'N' or $Job_FICA_Status = 'H'
   !Box 03
   do set-fica-wage-limit
   if #Box99_count <= 28 and (#BOX_03_SUMMARY_Wages >= #fica_limit)
     let $verbage = ''
     do write-limited-desc-w-2
     do Format-Number(#fica_limit, $fica_limit,  $FmtWage)
     let $verbage = 'SocSec (Box 03) wages Limited to Max: ' || $fica_limit
     do write-limited-desc-w-2
   end-if
   
    let #wages_over_limit = (#Total_earnings_ytd_fica + #net_ded_ytd_fica) - #fica_limit
    if  #wages_over_limit <= 0
      let #wages_over_limit = 0
    end-if
    let #recon_earns = (#BOX_03_SUMMARY_Wages + #wages_over_limit) - (#Total_earnings_ytd_fica + #net_ded_ytd_fica)
    if round (#recon_earns,2) <> 0

      do Format-Number(#BOX_03_SUMMARY_Wages,      $BOX_03_SUMMARY_Wages,  $FmtWage)
      do Format-Number(#wages_over_limit,          $wages_over_limit    ,  $FmtWage)
      do Format-Number(#Total_other_ytd_fica,      $Total_other_ytd     ,  $FmtWage)
      do Format-Number(#net_ern_ytd_fica,          $net_ern_ytd         ,  $FmtWage)
      do Format-Number(#Total_gross_ytd_fica,      $Total_gross_ytd     ,  $FmtWage)
      do Format-Number(#Total_earnings_ytd_fica,   $Total_earnings_ytd  ,  $FmtWage)
      do Format-Number(#net_ded_ytd_fica,          $net_ded_ytd         ,  $FmtWage)
      do Format-Number(#recon_earns,               $recon_earns         ,  $FmtWage)
      add 1 to #recon_total_warnings

     #ifdef BOX_01_03_05_Warning_Details
      let $descr_warning = ' Other Earnings      = ' || $Total_other_ytd 
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Other Earnings  = ' || $net_ern_ytd
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Gross wages         = ' || $Total_gross_ytd    
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Box 03              = ' || $BOX_03_SUMMARY_Wages
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Wages over Limit    = ' || $wages_over_limit
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Taxable Earnings    = ' || $Total_earnings_ytd 
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Deducts & Benef = ' || $net_ded_ytd 
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Earnings-Box 03 = ' || $recon_earns 
      put $Current_Company $Current_Emplid '03' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
     #endif

      add 1 to #recon_earns_errors_03

     else
      add 1 to #recon_earns_success_03
     end-if
   else
     add 1 to #Fica03_exempt_count
     #ifdef debug_adp303b
       #debugd show 'Message: No Box 03 recon  : Emplid ' $Current_Emplid ' is setup as FICA EXEMPT ' $Job_fica_status ' count = ' #Fica03_exempt_count
     #endif
   end-if
    
   !Box 05
   if $Job_FICA_Status = 'N' or $Job_FICA_Status = 'M' or $Job_FICA_Status = 'H'
    let #recon_earns = #BOX_05_SUMMARY_Wages - (#Total_earnings_ytd_med + #net_ded_ytd_med)
    if round (#recon_earns,2) <> 0

      do Format-Number(#BOX_05_SUMMARY_Wages,      $BOX_05_SUMMARY_Wages,  $FmtWage)
      do Format-Number(#Total_other_ytd_med,       $Total_other_ytd     ,  $FmtWage)
      do Format-Number(#net_ern_ytd_med,           $net_ern_ytd         ,  $FmtWage)
      do Format-Number(#Total_gross_ytd_med,       $Total_gross_ytd     ,  $FmtWage)
      do Format-Number(#Total_earnings_ytd_med,    $Total_earnings_ytd  ,  $FmtWage)
      do Format-Number(#net_ded_ytd_med,           $net_ded_ytd         ,  $FmtWage)
      do Format-Number(#recon_earns,               $recon_earns         ,  $FmtWage)
      add 1 to #recon_total_warnings

     #ifdef BOX_01_03_05_Warning_Details
      let $descr_warning = ' Other Earnings      = ' || $Total_other_ytd 
      put $Current_Company $Current_Emplid '05' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Other Earnings  = ' || $net_ern_ytd
      put $Current_Company $Current_Emplid '05' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Gross wages         = ' || $Total_gross_ytd    
      put $Current_Company $Current_Emplid '05' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Box 05              = ' || $BOX_01_SUMMARY_Wages
      put $Current_Company $Current_Emplid '05' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Taxable Earnings    = ' || $Total_earnings_ytd 
      put $Current_Company $Current_Emplid '05' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Deducts & Benef = ' || $net_ded_ytd 
      put $Current_Company $Current_Emplid '05' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
      let $descr_warning = ' Net Earnings-Box 05 = ' || $recon_earns 
      put $Current_Company $Current_Emplid '05' $descr_warning into BOX_Warning_Details(#BOX_Warning_Details) Company Emplid Box Descr
      do increment-box-warnings-details
     #endif

      add 1 to #recon_earns_errors_05

    else
      add 1 to #recon_earns_success_05
    end-if
  else
     add 1 to #Fica05_exempt_count
     #ifdef debug_adp303b
       #debugd show 'Message: No Box 05 recon  : Emplid ' $Current_Emplid ' is setup as FICA EXEMPT ' $Job_fica_status ' count = ' #Fica05_exempt_count
     #endif
  end-if
 else
   add 1 to #empl_status_exempt_count
   #ifdef debug_adp303b
     #debugd show 'Message: No Box 03/05 recon  : Emplid ' $Current_Emplid ' is setup as EMPL_STATUS ' $empl_status ' count = ' #empl_status_exempt_count
   #endif
 end-if
 !---------------------------------------------

    if #Box99_count <= 27 and #oth_earn_inx > 0
        let $verbage = ''
        do write-limited-desc-w-2
        let $verbage = '(Note: ''Other Earnings'' are Taxable Earnings not included in Check YTD,'
        do write-limited-desc-w-2
        let $verbage = '         OR Non-taxable Earnings added to Check YTD) '
        do write-limited-desc-w-2
    end-if
   
   !------------------------------------------------------

skip_box_01_03_05:

   let #BOX_01_SUMMARY_Wages  = 0
   let #BOX_03_SUMMARY_Wages  = 0
   let #BOX_05_SUMMARY_Wages  = 0
   let #Total_Gross_ytd    = 0
   let #Total_earnings_ytd = 0
   let #net_ded_ytd = 0
   let #net_ded_ytd_fica = 0
   let #net_ded_ytd_med = 0
   let #Total_earnings_ytd_med = 0
   let #Total_earnings_ytd_fica = 0
   let #wages_over_limit = 0
   let #Total_other_ytd_fica = 0
   let #Total_other_ytd_med = 0
   let #net_ern_ytd_fica = 0
   let #net_ern_ytd_med = 0
   let #Total_gross_ytd_fica = 0
   let #Total_gross_ytd_med = 0
   let #recon_earns = 0

end-procedure

Begin-Procedure increment-box-warnings-details

      add 1 to #BOX_Warning_Details

      if #BOX_Warning_Details < {MAX_BOX_WARNINGS}
       #ifdef debug_adp303b
         show 'increment-box-warnings-details ' #BOX_Warning_Details edit 99999 ', Emplid ' $Current_Emplid ', err count = ' #recon_earns_errors ', ok count = ' #recon_earns_success
       #endif
      else
       let #BOX_Warning_Details = {MAX_BOX_WARNINGS} - 1
       if #BOX_Warning_Details_overflow < 1
         show ' '
         show 'Warning:  set #define MAX_BOX_WARNINGS xxxxx in probiz.sqc, where xxxxx > {MAX_BOX_WARNINGS}, err count = ' #recon_earns_errors ', ok count = ' #recon_earns_success
         show '-------------------------------------------------------------------------------------------------------------------------------------------------------------- '
         show ' '
         add 1 to #BOX_Warning_Details_overflow
       end-if
      end-if      
end-procedure
      

Begin-Procedure Get-Special-FWT-Status

  let $FWT_Status = ''

BEGIN-SELECT
SFWT.SPECIAL_FWT_STATUS

  let $FWT_Status = rtrim(&SFWT.SPECIAL_FWT_STATUS,' ')

FROM PS_FED_TAX_DATA SFWT
WHERE SFWT.EMPLID  = $Current_Emplid
  AND SFWT.COMPANY = $Current_Company
  AND SFWT.EFFDT   = (SELECT MAX(SFWT1.EFFDT)
                     FROM PS_FED_TAX_DATA SFWT1
                     WHERE SFWT.EMPLID  = SFWT1.EMPLID
                       AND SFWT.COMPANY = SFWT1.COMPANY
                       AND SFWT1.EFFDT <= $Qtr_End_Native)
#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select
End-Procedure


begin-procedure write-other-earnings-details

 let #net_ern_ytd = 0
 let #oth_earn_inx = 0
 if #other_earn_count > 0

  let #ern = 0
  while #ern < #other_earn_count
    get $Erncd
        #GRS_YTD   
        $add_gross
        $Subject_FWT
        $Subject_FICA
        $EarningsDescr
      from other_earn(#ern)
        Erncd
        Amount
        ADD_GROSS
        SUBJECT_FWT
        SUBJECT_FICA
        DESCR
   

    !we need to deal with FICA Total other earnings for both N/Y ADD_GROSS (just like FWT)
    !---------------------------------------------------------------------------------------
    if   ($ADD_GROSS = 'N' and ($SUBJECT_FWT = 'Y' or $SUBJECT_FICA = 'Y') )
      or ($ADD_GROSS = 'Y' and ($SUBJECT_FWT = 'N' or $SUBJECT_FICA = 'N') )

      let $subject_string_fwt  = '             n/a '
      let $subject_string_fica = '             n/a '
      let $subject_string_med  = '             n/a '

      if #oth_earn_inx = 0
        let $verbage = ''
        do write-limited-desc-w-2
        let $verbage = 'Other Earnings'
        do write-limited-desc-w-2
        add 1 to #oth_earn_inx
      end-if

!11/19/2010 reworked this
!--------------------------
      if $ADD_GROSS = 'N'
        let #ern_ytd = #GRS_YTD
        if $SUBJECT_FWT = 'Y'
           do Format-Number(#ern_ytd,      $subject_string_fwt,  $FmtWage)
        end-if
        if $SUBJECT_FICA = 'Y'
           do Format-Number(#ern_ytd,      $subject_string_fica,  $FmtWage)
           move $subject_string_fica    to $subject_string_med
        end-if
      else
        let #ern_ytd = 0 - #GRS_YTD
        if $SUBJECT_FWT = 'N'
           do Format-Number(#ern_ytd,      $subject_string_fwt,  $FmtWage)
        end-if
        if $SUBJECT_FICA = 'N'
           do Format-Number(#ern_ytd,      $subject_string_fica,  $FmtWage)
           move $subject_string_fica    to $subject_string_med
        end-if
      end-if
!-----------------------------      
      
      let $EarningsDescr = rpad($EarningsDescr, 10, ' ')
      let $erncd = rpad($erncd,6,' ')
      let $verbage = ' ' || substr($erncd,1,6) || '-' || substr($EarningsDescr,1,8) 
      let $verbage = $verbage || ' ' || $subject_string_fwt || '     ' || $subject_string_fica  || '  ' || $subject_string_med

      add #ern_ytd to #net_ern_ytd
      
     if $SelectEmplid <> ''
       do Format-Number(#ern_ytd,  $ern_ytd,  $FmtWage)
       show 'write-other-earnings-details: ' $erncd ' ' $ADD_GROSS ' ' $SUBJECT_FWT ' ' $SUBJECT_FICA ' ' $EarningsDescr ' = ' $ern_ytd 
     end-if
     
     #if {SITE_ID} = 'MCK1'
       if rtrim($Erncd,' ') <> 'TGA' and rtrim($Erncd,' ') <> 'TGF'
         do write-limited-desc-w-2
       end-if
     #else
       do write-limited-desc-w-2
     #endif
    
    end-if    
    add 1 to #ern

  end-while
  
  let #other_earn_count = 0
  clear-array name=other_earn
  
 end-if
 
end-procedure


begin-procedure get-pretax-deductions

  let $first_benefit_ded = 't'
  let #net_ded_ytd = 0
  let #net_ded_ytd_fica = 0
  let #net_ded_ytd_med  = 0

  let $DBMCK1_Summary_Company_Selection = 'AND DBMCK1.' || $Summary_Company_Selection
  if $SelectEmplid <> ''
    show 'get-pretax-deductions:  for companies: ' $DBMCK1_Summary_Company_Selection
  end-if

  let $Dedcd = ''    
  let #Amount_Ded = 0
    
BEGIN-SELECT
DBMCK1.DEDCD
DBMCK1.DED_YTD
DBMCK1.DED_CLASS
DBMCK1.PLAN_TYPE

  let  $Cur_Dedcd = rtrim(&DBMCK1.DEDCD,' ')          
  let  $Cur_Plan_Type = rtrim(&DBMCK1.PLAN_TYPE,' ')   
  let  $Cur_ded_class = rtrim(&DBMCK1.DED_CLASS,' ')   

  if $SelectEmplid <> ''
      show 'get-pretax-deductions: ' $Cur_Plan_Type ' ' $Cur_Dedcd ' ' $Cur_Ded_Class ' = ' &DBMCK1.DED_YTD edit '999,999,999.99'
  end-if

  if $Dedcd = ''
    move $Cur_Dedcd     to $Dedcd
    move $Cur_Plan_Type to $Plan_Type
    move $Cur_Ded_Class to $Ded_Class
    if $Ded_Class = 'T'
      let #Amount_Ded = &DBMCK1.DED_YTD
    else
      let #Amount_Ded = 0 - &DBMCK1.DED_YTD  
    end-if
    goto get_next_ded
  else
    if $Cur_Dedcd <> $dedcd
      do Get-Deduction-Name         !Get the $DeductionName and $DeductionAbbrv (in adp303q.sqc)
      do Get-Deduction-FicaEffect   !Get the $DedFICA_EFFECT
      do Format-Number(#Amount_Ded,      $Amount_Ded,  $FmtWage)
      if $Ded_Class = 'T'
        let $verbage = 'Plus Taxable Benits'
        do write-limited-desc-w-2
      end-if
      let $DeductionName = rpad($DeductionAbbrv, 20, ' ')
      let $Dedcd = rpad($dedcd,6,' ')
      let $verbage = ' ' || substr($dedcd,1,6) || '-' || substr($DeductionName,1,8) || ' ' || $Amount_Ded
      if $DedFICA_EFFECT = 'N'
       let $subject_string_fica = '             n/a '
       let $subject_string_med  = '             n/a '
       let $verbage = $verbage || '     ' || $subject_string_fica || '  ' || $subject_string_med 
      else
       let $verbage = $verbage || '     ' || $Amount_Ded   !affects box 03
       let $verbage = $verbage || '  ' || $Amount_Ded   !affects box 05
       add #Amount_Ded to #net_ded_ytd_fica
       add #Amount_Ded to #net_ded_ytd_med 
      end-if
  
      add #Amount_Ded to #net_ded_ytd !always affects Box 1
      if $SelectEmplid <> ''
         show 'get-pretax-deductions:         writing record: ' $verbage
      end-if
      do write-limited-desc-w-2
      move $Cur_Dedcd to $Dedcd
      move $Cur_Plan_Type to $Plan_Type
      move $Cur_Ded_Class to $Ded_Class
      if $Ded_Class = 'T'
        let #Amount_Ded = &DBMCK1.DED_YTD
      else
        let #Amount_Ded = 0 - &DBMCK1.DED_YTD  
      end-if
    else
      if $Ded_Class = 'T'
        let #Amount_Ded = #Amount_Ded + &DBMCK1.DED_YTD
      else
        let #Amount_Ded = #Amount_Ded + (0 - &DBMCK1.DED_YTD  )
      end-if
    end-if
  
  
  end-if

get_next_ded:

  FROM  PS_DEDUCTION_BAL{WAREHOUSE_SUFFIX} DBMCK1 {NOLOCK_SQL}
  WHERE DBMCK1.EMPLID            = $Current_Emplid
    #ifdef MVS                   !OS400 - /.../, NT - [...], MVS - \...\
      \$DBMCK1_Summary_Company_Selection\
    #else
      [$DBMCK1_Summary_Company_Selection]
    #endif
    AND DBMCK1.BALANCE_ID        = $Calendar_Year_Id
    AND DBMCK1.DED_CLASS         in ('B','T')
   #ifdef USE_INT_CRITERIA
    AND DBMCK1.BALANCE_YEAR      = int(\$RptYearZ\)
   #else
    AND DBMCK1.BALANCE_YEAR      = #RptYear
   #endif
    AND DBMCK1.BALANCE_PERIOD           =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_DEDUCTION_BAL{WAREHOUSE_SUFFIX} {NOLOCK_SQL}
       WHERE  EMPLID            = DBMCK1.EMPLID
         AND  BALANCE_ID        = DBMCK1.BALANCE_ID
         AND  BALANCE_YEAR      = DBMCK1.BALANCE_YEAR
         AND  BENEFIT_RCD_NBR   = DBMCK1.BENEFIT_RCD_NBR
         AND  PLAN_TYPE         = DBMCK1.PLAN_TYPE
         AND  BENEFIT_PLAN      = DBMCK1.BENEFIT_PLAN
         AND  DED_CLASS         = DBMCK1.DED_CLASS
         AND  COMPANY           = DBMCK1.COMPANY
         AND  DEDCD             = DBMCK1.DEDCD
         #ifdef USE_INT_CRITERIA
            AND  BALANCE_PERIOD  <= int(\$RptMonthZ\))
         #else
            AND  BALANCE_PERIOD  <= #RptMonth)
         #endif

    AND DBMCK1.DED_YTD <> 0
    ORDER by DBMCK1.DED_CLASS, DBMCK1.PLAN_TYPE, DBMCK1.DEDCD

#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select

    if $dedcd <> ''
      do Get-Deduction-Name         !Get the $DeductionName and $DeductionAbbrv (in adp303q.sqc)
      do Get-Deduction-FicaEffect   !Get the $DedFICA_EFFECT
      if $Ded_Class = 'T'
        let $verbage = 'Plus Taxable Benits'
        do write-limited-desc-w-2
      end-if
      do Format-Number(#Amount_Ded,      $Amount_Ded,  $FmtWage)
      let $DeductionName = rpad($DeductionAbbrv, 20, ' ')
      let $Dedcd = rpad($dedcd,6,' ')
      let $verbage = ' ' || substr($dedcd,1,6) || '-' || substr($DeductionName,1,8) || ' ' || $Amount_Ded
      if $DedFICA_EFFECT = 'N'
       let $subject_string_fica = '             n/a '
       let $subject_string_med  = '             n/a '
       let $verbage = $verbage || '     ' || $subject_string_fica || '  ' || $subject_string_med 
      else
       let $verbage = $verbage || '     ' || $Amount_Ded   !affects box 03
       let $verbage = $verbage || '  ' || $Amount_Ded   !affects box 05
       add #Amount_Ded to #net_ded_ytd_fica
       add #Amount_Ded to #net_ded_ytd_med 
      end-if
  
      add #Amount_Ded to #net_ded_ytd !always affects Box 1
  
      if $SelectEmplid <> ''
         show 'get-pretax-deductions: (Final) writing record: ' $verbage
      end-if
      do write-limited-desc-w-2
   end-if

end-procedure

begin-procedure Get-Deduction-FicaEffect

  let $DedFICA_EFFECT = ''

begin-SELECT
DT5.FICA_EFFECT

  let $DedFICA_EFFECT = &DT5.FICA_EFFECT

  if $SelectEmplid <> ''
    show 'Get-Deduction-FicaEffect: ' $Dedcd ' ' $plan_type ' ' $Ded_Class ' ' $DedFICA_EFFECT  
  end-if
  
FROM  PS_DEDUCTION_CLASS DT5 {NOLOCK_SQL}
WHERE PLAN_TYPE = $Plan_Type
  AND DED_CLASS = $Ded_class 
  AND DEDCD     = $DedCd
  AND EFFDT     = (SELECT MAX(EFFDT)
    FROM   PS_DEDUCTION_CLASS {NOLOCK_SQL}
    WHERE  PLAN_TYPE        = DT5.PLAN_TYPE
      AND  DEDCD            = DT5.DEDCD
      AND  DED_CLASS        = DT5.DED_CLASS
      AND  EFFDT           <= $AsOfDate)

#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select

end-procedure 
 
begin-procedure Print-Box01-Box03-Box-05-Summary  !hook from pbzqtr.sqr


    let #Header_type = 75
    let $ReportTitle  = 'ADP Employment Tax, Box 01/03/05 Warnings'             


 #ifdef BOX_01_03_05_Warning_Details
 
  if #BOX_Warning_Details >  0
     
    let #Box_warn = 0
    while (#Box_warn < #BOX_Warning_Details)
       get $Com $Emp $B $descr from BOX_Warning_Details(#Box_warn)  Company Emplid Box Descr
       if #current-line > 56
          New-Page
          print ' ' (+1,1)
       end-if
       add 1 to #Box_warn
       
       if $Com <> $Prior_Com or $Emp <> $Prior_Emp or ($B <> $Prior_B)
         print ' ' (+1,1)
         let $Prior_Com = $com
         let $Prior_emp = $emp
         let $Prior_B   = $B
       end-if
       Print $Com         (+1,1) 
       print $Emp         (,11)
       print $B           (,29)
       print $descr       (,33)
     end-while

   
     New-Page
     
   end-if

 #endif

    print 'Count of BOX 01/03/05_SUMMARY Warnings                                                      =  ' (+2,1)
    print #BOX_Warning_Details                                                                              () edit 999,999
 
    print 'BOX_01_03_05_SUMMARY (W-2 Wage Reconciliation summary)' (+2,1)
    print 'Wage & Ded overrun count. More than 30 Box 99/IX records (too many earn/ded recs)           =  ' (+1,1)
    print #recon_earns_overrun () edit 999,999

    print 'Box 01: Wage & Ded match count.   Net Taxable ok. (Pre-Tax plus Taxable Benefits match)     =  ' (+2,1)
    print #recon_earns_success () edit 999,999
    print 'Box 01: Wage & Ded error count.   Net Taxable Difference (Net does not match)               =  ' (+1,1)
    print #recon_earns_errors  () edit 999,999

    print 'Box 03: Wage & Ded match count.   Net Taxable ok. (Pre-Tax plus Taxable Benefits match)     =  ' (+2,1)
    print #recon_earns_success_03 () edit 999,999
    print 'Box 03: Wage & Ded error count.   Net Taxable Difference (Net does not match)               =  ' (+1,1)
    print #recon_earns_errors_03  () edit 999,999

    print 'Box 05: Wage & Ded match count.   Net Taxable ok. (Pre-Tax plus Taxable Benefits match)     =  ' (+2,1)
    print #recon_earns_success_05 () edit 999,999
    print 'Box 05: Wage & Ded error count.   Net Taxable Difference (Net does not match)               =  ' (+1,1)
    print #recon_earns_errors_05  () edit 999,999

    New-Page
    
end-procedure

begin-procedure get-01-03-05

  let #BOX_01_SUMMARY_Wages = 0
  let #BOX_03_SUMMARY_Wages = 0
  let #BOX_05_SUMMARY_Wages = 0
  
  let $B135_Summary_Company_Selection = 'AND B135.' || $Summary_Company_Selection


begin-SELECT
B135.BOX
B135.W2_AMOUNT

  if $SelectEmplid <> ''
     show 'get-01-03-05: ' &B135.BOX ' = ' &B135.W2_AMOUNT
  end-if
  
  if rtrim(&B135.BOX,' ') = '01' 
    add &B135.W2_AMOUNT to #BOX_01_SUMMARY_Wages 
  end-if
  if rtrim(&B135.BOX,' ') = '03' 
    add &B135.W2_AMOUNT to #BOX_03_SUMMARY_Wages 
  end-if
  if rtrim(&B135.BOX,' ') = '05' 
    add &B135.W2_AMOUNT to #BOX_05_SUMMARY_Wages 
  end-if
  
  FROM  PS_YE_AMOUNTS B135 {NOLOCK_SQL}
   #ifdef USE_INT_CRITERIA
   WHERE B135.CALENDAR_YEAR      = int(\$RptYearZ\)
   #else
   WHERE B135.CALENDAR_YEAR   = #RptYear
   #endif
    AND B135.TAXFORM_ID      = 'W'
    #ifdef MVS                   !OS400 - /.../, NT - [...], MVS - \...\
     \$B135_Summary_Company_Selection\
    #else
     [$B135_Summary_Company_Selection]
    #endif
    AND B135.EMPLID          = $Current_Emplid
    AND B135.W2_AMOUNT       > 0
    AND B135.STATE           = '$U'
    AND B135.BOX             in ('01','03','05')
        
#ifdef SELECT_WITH_UR
 with ur
#endif
end-SELECT
end-procedure

#endif



begin-procedure write-limited-desc-w-2

      add 1 to #Box99_count
      do Format-Number(#Box99_count, $Box99_count, '99')

      let $verbage = rpad($verbage,75,' ')

   #ifdef BALANCE_TO_BOX1_PRINT
     if $SelectEmplid <> ''
         if (#Box_99_print_count < {MAX_BOX99_CODES})
          put $verbage into Box99_print(#Box_99_print_count) Desc
          add 1 to #Box_99_print_count
         end-if
     end-if  
   #endif

   #ifdef QUARTERLY_HISTORY
         let $box_str = 'B US99' || rpad($blank,27,' ') || rpad($Box99_count,2,' ') || rpad($verbage,76,' ')
         do populate_ADP_QTR_HIST
   #endif
   #ifdef USE_TEMP_TABLE_FOR_OUTPUT
       let $qout_str = 'B US99' || rpad($blank,27,' ') || rpad($Box99_count,2,' ') || rpad($verbage,76,' ')
       do Log-B-rec-data
   
   #else

      Write 1 FROM     $rectp:1
                       $blank:1
                       'US':2
                       '99':2           
                       $blank:1
                       $blank:10
                       $blank:16
                       $Box99_count:2
                       $verbage:75
                       $blank:1

      add 1 to #reccnt
      add 1 to #company_trailer_reccnt
    
    #endif
    
    add 1 to #B_REC_Total_Count

end-procedure
   
begin-procedure set-fica-wage-limit  !changed to dynamically find the limit 9/28/2011
  
 if #Fica_Limit <= 0
  
begin-SELECT
FICALM.MAX_GROSS

   let #fica_limit = &FICALM.MAX_GROSS

 FROM  PS_ST_OTH_TAX_TBL FICALM 
 WHERE FICALM.STATE = '$U'
   AND FICALM.TAX_TYPE = 'G'
   AND FICALM.TAXGR_BASE = 'D'
   AND FICALM.TAX_CLASS = 'D'
   AND FICALM.EFFDT = (SELECT MAX(EFFDT)
       FROM   PS_ST_OTH_TAX_TBL FICALM1 
       WHERE  FICALM1.STATE       = '$U'
         AND  FICALM1.TAX_CLASS   = FICALM.TAX_CLASS
         AND  FICALM1.EFFDT      <= $Qtr_end_native)

#ifdef SELECT_WITH_UR
 with ur
#end-if
end-select

  do format-number(#Fica_Limit, $Fica_Limit, '$$$,$$$,$$9.99')
  #ifdef debug_adp303b
     #debugd show 'Fica_Limit = ' $Fica_Limit
  #endif
  
 end-if
 
end-procedure

