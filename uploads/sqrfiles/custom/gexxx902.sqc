!**************************************************************************
!  GEXXX902:  Retrieve "Multiple" Deptid Run Controls and                 *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX902_Deptid_Alias                                               *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX902_INCLUDE_DEPTID_CRITERIA                                    *
!    $GEXXX902_EXCLUDE_DEPTID_CRITERIA                                    *
!    $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA                            *
!    $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA                            *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX902_Deptid_Alias = 'J.DEPTID'                              *
!    Do GEXXX902-Select-Deptid-Parameters                                 *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX902_INCLUDE_DEPTID_CRITERIA]                            *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!  GEXHR_E83371_KRONOS_INTGN                                              *
!               ISDVMZA		05/28/2014 	Add INCLUD_EXCLUDE_IND    *
!**************************************************************************


Begin-Procedure GEXXX902-Select-Deptid-Parameters

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DEPTID_ROWS = 0
  Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = ''
  Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = ''
  Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_DEPTID.DEPTID
GEX_RC_DEPTID.INCLUD_EXCLUDE_IND              !  GEXHR_E83371_KRONOS_INTGN  --  ISDVMZA	--  05/28/2014


  Add 1 To #GEX_RC_DEPTID_ROWS

  If #GEX_RC_DEPTID_ROWS = 1
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '(' || $GEXXX902_Deptid_Alias || ' In ('''     || &GEX_RC_DEPTID.DEPTID || ''''
    Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = '(' || $GEXXX902_Deptid_Alias || ' Not In (''' || &GEX_RC_DEPTID.DEPTID || ''''
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA || ',''' || &GEX_RC_DEPTID.DEPTID  || ''''
    Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = $GEXXX902_EXCLUDE_DEPTID_CRITERIA || ',''' || &GEX_RC_DEPTID.DEPTID  || ''''
  End-If

From  PS_GEX_RC_DEPTID GEX_RC_DEPTID

Where GEX_RC_DEPTID.OPRID               = [$OprID]
And   GEX_RC_DEPTID.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DEPTID.DEPTID, ' ') != ' '
End-Select

  If #GEX_RC_DEPTID_ROWS = 0
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' = ' || $GEXXX902_Deptid_Alias
    Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' = ' || $GEXXX902_Deptid_Alias
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA || '))'
    Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = $GEXXX902_EXCLUDE_DEPTID_CRITERIA || '))'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
    Let $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_EXCLUDE_DEPTID_CRITERIA
  End-If

End-Procedure
