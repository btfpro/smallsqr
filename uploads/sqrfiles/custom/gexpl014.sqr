!***********************************************************************
!  GEXPL014:  RMC Health & Welfare Report                              *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL014:             RMC Health & Welfare Report                    *
!                                                                      *
! Narrative:		This program creates a monthly report for      *
!                       RMC that contains employee information         *
!                       required by the union to administer the        *
!                       Health & Welfare benefit.                      *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_calendar                                   *
!                       ben_defn_optn                                  *
!                       ben_defn_pgm                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       benef_plan_tbl                                 *
!                       earnings_bal                                   *
!                       pay_line                                       *
!                       pay_check                                      *
!                       pay_deduction                                  *
!                       health_benefit                                 *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:	        Monthly                                        *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Plan Type(s)                                 *
!                         Vendor ID(s)                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS   DATE       DESCRIPTION OF THE CHANGE                     *
!***********************************************************************
!  JDH        02/10/99   Initial Creation                              *
!                                                                      *
!  JDH        02/26/99   Pull pay_check rows by the check date of the  *
!                          pay end rather than the check date of the   *
!                          pay_check row.                              *
!                        Add descending sort on effseq.                *
!                                                                      *
!  JDH        04/13/99   Get the effdt, action, action_reason from     *
!                          the job row that originally set the         *
!                          empl_status to its current value for        *
!                          empl_status in ('L','P','R','T').           *
!  JDH        05/19/99   Corrected invalid variable reference.         *
!  JDH        06/29/99   Changed logic to get action, action_reason,   *
!                          effdt.                                      *
!  SXK        11/23/99   Changed from ssn to emplid                    *
!                                                                      *
!  GBD        07/15/02   Added Fieldvalue to effective dated selection *
!                        of the Load-Lookup procedure.                 *
!                                                                      *
!  GBD        09/06/02   v8.3 Upgrade                                  *
!                                                                      *
!  GBD        10/16/02   Modify select statement to join BEN_DEFN_PGM  *
!                        to BEN_DEFN_OPTN by Benefit_Program and Effdt *
!                        only to avoid multiple row selection.         *
!                                                                      *
!  AXL        02/20/03   Add empl_rcd to Earnings Bal join.            *
!                                                                      *
!  SCM        10/30/03   Changed main select to ensure that multiple   *
!                        rows were not being selected because benefits *
!                        started using ben_defn_optn with the same     *
!                        benefit_program and plan_type, but with unique*
!                        covrg_cd.-See better description in Main select
!                        Added special code for Off Cycles.            *
!  CJH        11/7/2003  change sort order in Get-Status-Date procedure*
!                        to retrieve most current status.  Add last-dt-*
!                        worked to report.                             *
!  SCM       11/10/2003  Select last date worked from Job row when     *
!                        getting the leave begin date.                 *
!                        Commented some code that was not needed in the*
!                        status-date para.
!GEX-CXB 09/10/2007     Comment out the Procedure'Do Set-Optimizer-Goal'*
!			                  XLATTABLE table has been renamed to PSXLATITEM *
! VENDKXY     10/06/2009   Done Changes requested for ITG 68012        *
! VENDKXY     12/02/2009   Done the Changes for ITG 70317              *
!                           when store changes in month being processed*
!ISDVAWD     04/22/2014     ITG# 827  - Changed file path  '\\nt5\' *
!			       with '\\corp.gianteagle.com\'`	       *
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************


#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization
! CJH - 11/7/2003 - begin
#define #column1     1
#define #column2    33
#define #column3    44
#define #column4    56
#define #column5    66
#define #column6    74   !76
#define #column7    83   !85
#define #column8    92
#define #column9    99
#define #column10  110   !112
#define #column10b 120
#define #column11  130
#define #column12  144   !150
#define #column13  158   !166
#define #column14  174
! CJH - 11/7/2003 - end


Begin-Heading 8
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'

  Do Get-Provider-Name
  Do Get-Department-Name

  Print 'Provider:'            (3,1)
  Print $Provider              (3,13)
  Print $ProviderName          (3,+2)

  Print $Reporting_Period      (3,) Center 

  Print 'Deptid:'              (4,1)
  Print $Deptid                (4,13)
  Print $DeptName              (4,+2)

  Print 'IN STORE'             (+2,{#column3})
  Print 'SERVICE'              (+0,{#column4}) 
  Print 'REG'                  (+0,{#column5})
  Print 'EMPL'                 (+0,{#column6})
  Print 'ACTION'               (+0,{#column8})
  Print 'EFF'                  (+0,{#column9})
  Print 'BENEFIT'              (+0,{#column10})
! CJH - 11/7/2003 - begin
  Print 'LAST DT'              (+0,{#column10b})
  Print '    PREVIOUS'         (+0,{#column12})
  Print '     CURRENT'         (+0,{#column11})
  Print '   MTD BENEFIT'       (+0,{#column13})

  Print 'EMPLOYEE NAME'        (+1,{#column1})
  Print 'SSN'                  (+0,{#column2})
  Print 'DEPARTMENT'           (+0,{#column3})
  Print 'DATE'                 (+0,{#column4}) 
  Print 'TEMP'                 (+0,{#column5})
  Print 'STAT'                 (+0,{#column6})
  Print 'ACTION'               (+0,{#column7})
  Print 'REASON'               (+0,{#column8})
  Print 'DATE'                 (+0,{#column9})
  Print 'TERM DATE'            (+0,{#column10})
  Print 'WORKED'               (+0,{#column10b})
  Print '     MTD HRS'         (+0,{#column11})
  Print '     MTD HRS'         (+0,{#column12})
  Print '          PAID'       (+0,{#column13})
  Print 'VL'                   (+0,{#column14})

  Print '-'                    (+1,{#column1},30)  Fill
  Print '-'                    (+0,{#column2},09)  Fill
  Print '-'                    (+0,{#column3},8)  Fill
!GBD 07/15/2002 Begin
  Print '-'                    (+0,{#column4},8)  Fill
  Print '-'                    (+0,{#column5},7)  Fill
  Print '-'                    (+0,{#column6},04)  Fill
  Print '-'                    (+0,{#column7},06)  Fill
  Print '-'                    (+0,{#column8},06)  Fill
  Print '-'                    (+0,{#column9},9)  Fill
  Print '-'                    (+0,{#column10},8) Fill
  Print '-'                    (+0,{#column10b},8) Fill
  Print '-'                    (+0,{#column11},12) Fill
  Print '-'                    (+0,{#column12},12) Fill
  Print '-'                    (+0,{#column13},14) Fill
  Print '-'                    (+0,{#column14},2)  Fill
! CJH - 11/7/2003 end
!GBD 07/15/2002 End
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'
 MOVE $CURRENT-DATE TO $CURRDT 'MMDDYY'   !VENDKXY Done the Changes for ITG 70317     
     
  #ifdef NT		!VENDKXY Done the Changes for ITG 70317
  ! let $report-name = '\\corp.gianteagle.com\mobius\hrreports\' ||'PL014PDF.pdf'  !VENDKXY Done the Changes for ITG 68012  !VENDKXY Done the Changes for ITG 70317 
!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - - Changed Report name 
!*****************************************************************     
  !  Let $report-name = '\\nt5\ftproot\benefits\Local 19_GEXPL014\'|| 'PL014PDF_' || $CURRDT ||'.pdf'   !VENDKXY Done the Changes for ITG 70317 
    Let $report-name = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\Local 19_GEXPL014\'|| 'PL014PDF_' || $CURRDT ||'.pdf'   !VENDKXY Done the Changes for ITG 70317 
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************

  new-report  $report-name   !VENDKXY Done the Changes for ITG 68012
  #end-if			!VENDKXY Done the Changes for ITG 70317


!GEX-CXB FCIL Begin   09/10/07
  !Do Set-Optimizer-Goal	
!GEX-CXB FCIL END

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Do P100-Start
  
    #ifdef NT  !VENDKXY Done the Changes for ITG 70317
    Do Openfile     !VENDKXY Done the Changes for ITG 68012
  #end-if  !VENDKXY Done the Changes for ITG 70317
 
  Do P200-Main-Process
  Do P300-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report


Begin-Procedure Set-Optimizer-Goal
  Begin-SQL
    Alter Session Set OPTIMIZER_GOAL=RULE;
  End-SQL
End-Procedure


!VENDKXY Done the Changes for ITG 68012-- Begin
Begin-Procedure Openfile
  
    encode '<009>' into $delim

    ! Let $file014 = '\\corp.gianteagle.com\mobius\hrreports\' || 'PL014XLS' || '.xls'  !VENDKXY Done the Changes for ITG 70317 
!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - Updated File path   
!*****************************************************************    
    ! Let $file014 = '\\nt5\ftproot\benefits\Local 19_GEXPL014\'|| 'PL014XLS_' || $CURRDT ||'.xls'   !VENDKXY Done the Changes for ITG 70317 
     Let $file014 = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\Local 19_GEXPL014\'|| 'PL014XLS_' || $CURRDT ||'.xls'   !VENDKXY Done the Changes for ITG 70317 
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************
   	  		
    Open $file014 As 1 For-Writing Record=1000:Vary
    

   Move 'PL014XLS'			To $Report  
   encode '<012>' into $newline 
   String 'Report ID:' $Report  ' ' ' ' ' ' ' ' ' ' $ReportTitle by $delim into $gexpl014
   Write 1 from $gexpl014
   Write 1 from $newline 
   
    
    String 'VENDORID' 'DEPTID' 'EMPLOYEE NAME' 'SSN' 'JOB TITLE' 'SERVICE DATE' 'REG TEMP' 'EMPL STAT'    
   ! 'ACTION' 'ACTION REASON' 'EFF DATE' 'BENEFIT TERM DATE' 'LAST DATE WORKED' 'PREVIOUS MTD HRS' 'CURRENT MTD HRS' 'MTD BENEFIT PAID' 'VL' by $delim into $gexpl014 !VENDKXY Done the Changes for ITG 70317 
    'ACTION' 'ACTION REASON' 'EFF DATE' 'BENEFIT TERM DATE' 'LAST DATE WORKED' 'PREVIOUS MTD HRS' 'CURRENT MTD HRS' 'MTD BENEFIT PAID' 'VL' by $delim into $gexpl014   !VENDKXY Done the Changes for ITG 70317 
    Write 1 from $gexpl014
    
End-Procedure

!VENDKXY Done the Changes for ITG 68012-- End


Begin-Procedure P100-Start
#debug9 Show 'P100-Start'
   Move 'GEXPL014'                    To $ReportID    !VENDKXY Done the Changes for ITG 70317 
   #ifdef NT   														!VENDKXY Done the Changes for ITG 70317 
   !Move 'GEXPL014'                    To $ReportID  !VENDKXY Done the Changes for ITG 68012
   Move 'PL014PDF'			To $ReportID    !VENDKXY Done the Changes for ITG 68012
   #end-if  !VENDKXY Done the Changes for ITG 70317
   
  Move 'RMC HEALTH & WELFARE REPORT' To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  If $prcs_process_instance = ''

    Move '''' to $ProviderString
!    Display 'Enter Provider ID or leave blank to exit.'
    Display 'Enter Vendor ID or leave blank to exit.'
    While 1=1
      Input $Provider maxlen=6 'Provider ID'
      if RTRIM($Provider, ' ') = ''
        concat '''' with $ProviderString
        break
      end-if
      if $ProviderString <> ''''
        concat ''',''' with $ProviderString
      end-if
      concat $Provider with $ProviderString
    End-While

    Let $Vendor_ID = $Provider

    if $ProviderString = ''''''
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'ALL'
    else
!      let $GEXXX927_INCLUDE_PROVIDER_CRITERIA = 'bpt.provider In (' || $ProviderString || ')'
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = 'bpt.vendor_id In (' || $ProviderString || ')'   !GBD 09/06/2002
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
    end-if

    Move '''' to $PlanTypeString
    Display 'Enter Plan Type or leave blank to exit.'
    While 1=1
      Input $PlanType Maxlen=2 'Plan Type'
      Uppercase $PlanType
      if RTRIM($PlanType, ' ') = ''
        concat '''' with $PlanTypeString
        break
      end-if
      if $PlanTypeString <> ''''
        concat ''',''' with $PlanTypeString
      end-if
      concat $PlanType with $PlanTypeString
    End-While

    if $PlanTypeString = ''''''
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
    else
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = 'hb1.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = 'bpt.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    end-if

  Else

    Let $GEXXX912_Plan_Type_Alias = 'hb1.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'hb1.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2 = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA

    Let $GEXXX912_Plan_Type_Alias = 'bpt.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters

!    Let $GEXXX927_Provider_Alias = 'bpt.provider'
    Let $GEXXX927_Vendor_ID_Alias = 'bpt.vendor_id'        !GBD 09/06/2002
    Do GEXXX927-Select-Vendor-ID-Parameters

  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  If $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
    Display 'At least one plan type parameter must be specified'
    Stop
  End-If

  If $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
    Display 'At least one Vendor ID parameter must be specified'
    Stop
  End-If

  Let $X000_ORDER_BY = 'Vendor_ID, Deptid, Employee Name'

  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA  = '   $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA
  Show '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA      = '   $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA

  Do Get-Calendar-Year-Id    !getbalid.sqc

  Do P140-Get-Last-Confirmed-Pay
  Do P150-Print-Cover-Page
  Do P170-Get-Xlat-Lookup
End-Procedure


Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

Begin-Select Loops=1
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))                   &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))                 &balance_year
To_Char(Add_Months(pay_end_dt,1),'fmMonth YYYY')      &period
Last_Day(pay_end_dt)                                  &current_month_end
Last_Day(Add_Months(pay_end_dt,-1))                   &prev_month_end
Last_Day(Add_Months(pay_end_dt,1))                    &next_month_end
To_Number(To_Char(Add_Months(pay_end_dt,-1),'MM'))    &prev_month_balance_period
To_Number(To_Char(Add_Months(pay_end_dt,-1),'YYYY'))  &prev_month_balance_year

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period

  Let $current_month_begin = '01-'||Substr($pay_end_dt,4,8)
  Let $current_month_end   = &current_month_end
  Let $next_month_end      = &next_month_end
  Let $prev_month_begin    = '01-'||Substr(&prev_month_end,4,8)

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  Let #prev_month_balance_year   = &prev_month_balance_year
  Let #prev_month_balance_period = &prev_month_balance_period

  Show 'Report will contain balances for the month of ' $Reporting_Period
  Show 'Last confirmed pay end date:  ' $pay_end_dt

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_calendar

Where  pay_end_dt         =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt <= $AsOfToday   
       And    pay_confirm_run = 'Y')

And    pay_confirm_run = 'Y'
End-Select
End-Procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+2,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,2)
  Print '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = '  (+2,5)
  Print $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA    = '  (+2,5)
  Print $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA          (0,+2)

  Print 'Report will contain balances for the month of'       (+2,5)
  Print $Reporting_Period                                     (0,+1)
  Print 'Last confirmed pay end date:'                        (+2,5)
  Print $pay_end_dt                                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
End-Procedure


begin-procedure P170-Get-Xlat-Lookup
#debug9 Show 'P170-Get-Xlat-Lookup'

  Let $Where = '     x.fieldname   = ''REG_TEMP''' ||
	       ' And x.eff_status  = ''A''' ||
!GEX-CXB  FCIL  09/10/07  Begin
!	       ' And x.language_cd = ''ENG''' ||
!GEX-CXB FCIL  09/10/07  End
	       ' And x.effdt       = (Select Max(effdt)' ||
!GEX-CXB  FCIL  09/10/07  Begin
!	       '                      From   xlattable x1' ||      
	       '                      From   PSXLATITEM x1' ||     
!GEX-CXB  FCIL  09/10/07  End	       
	       '                      Where  x1.fieldname    = x.fieldname' ||
!GBD 07/15/2002 Begin
!	       '                      And    x1.language_cd  = ''ENG''' ||
!GEX-CXB FCIL  09/10/07  Begin
!	       '                      And    x1.language_cd  = x.language_cd' ||
!GEX-CXB FCIL  09/10/07  End
	       '                      And    x1.fieldvalue  = x.fieldvalue' ||
!GBD 07/15/2002 End
	       '                      And    x1.effdt       <= ''' || $pay_end_dt || '''' ||
	       '                      And    x1.eff_status   = ''A'')'

  Load-Lookup
    Name=XlatLookup
    Rows=10
!GEX-CXB  FCIL  09/10/07  Begin
!    Table='XLATTABLE X'
     Table='PSXLATITEM X'
!GEX-CXB FCIL  09/10/07  End
    Key='X.FIELDVALUE'
    Return_Value='X.XLATSHORTNAME'
    Where=$Where
    Quiet
End-Procedure
 
!******************************************************************
!  SCM  10/30/03  Since the benef dept began using multiple rows on
!       ben_defn_optn, this is how the rows were coming through...
!       with the covrg_cd uniquely identifying the benefit_plan row.
!BENEFI O C PAY_END_DT  DEPTID     EMPLID        DED_CUR
!------ - - ----------- ---------- ----------- ---------
!WRM31A N 4 27-SEP-2003 6414       279888462         130
!WRM31E N 1 04-OCT-2003 6414       279888462         130
!WRM31E N 4 04-OCT-2003 6414       279888462         130
!WRM31E N 1 11-OCT-2003 6414       279888462         130
!WRM31E N 4 11-OCT-2003 6414       279888462         130
!WRM31E N 1 18-OCT-2003 6414       279888462         130
!WRM31E N 4 18-OCT-2003 6414       279888462         130
!WRM31E Y 1 18-OCT-2003 6414       279888462        -130
!WRM31E Y 4 18-OCT-2003 6414       279888462        -130
!WRM31E Y 1 18-OCT-2003 6414       279888462         130
!WRM31E Y 4 18-OCT-2003 6414       279888462         130
!WRM31E N 1 25-OCT-2003 6414       279888462         130
!WRM31E N 4 25-OCT-2003 6414       279888462         130
!    If an employee had the same benefit_program, off cycle,
!    payend, deptid and multiple covrg codes, we only want to
!    process the deduction once.The Off-Cycles were giving lots
!    of grief because they could be multiple 1-4 covrg code 
!    combinations for the same pay end. (look above on Oct. 18)
!    so if the code seems messy, it is! But it works.
!***SCM End*************************************************
Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
 
  Let $Data-Found-Flag = 'N'
  Let #rpt_grand_tl_grs = 0

Begin-Select 
bpt.vendor_id        ()     On-Break   Level=1
                                       Print=Never
!                                       Save=$Save_Provider
!                                       After=P270-After-Provider-Break
                                       Save=$Save_Vendor                 !GBD 09/12/2002
                                       After=P270-After-Vendor-ID-Break     !GBD 09/12/2002

pl.deptid            ()     On-Break   Level=2
                                       Print=Never
                                       Save=$Save_Deptid
                                       After=P280-After-Deptid-Break
                                       Before=P285-Before-Deptid-Break

per.name             ()     On-Break   Level=3
                                       Print=Never

j.emplid             ()     On-Break   Level=4
                                       Print=Never

j.empl_rcd           ()     On-Break   Level=5
                                       Print=Never
                                       After=P290-Employee-Break

bpt.benefit_plan                        !SCM 10/30/03
bpt.plan_type                           !SCM 10/30/03
bdo.covrg_cd                            !SCM 10/30/03
pd.pay_end_dt                           !SCM 10/30/03
bdp.gex_spcl_accum_cd

e.service_dt

per.birthdate
per.emplid !SXK 11/23/1999 Changed ssn to emplid

j.company
j.effdt
j.acct_cd
j.empl_status
j.reg_temp
j.full_part_time
j.gex_volun_low_hrs
! 11/7/2003 - add last_date_worked
j.last_date_worked
! 11/7/2003 - end

pd.ded_cur
pd.off_cycle                
j.position_nbr  !VENDKXY Done the Changes for ITG 68012

!VENDKXY Done the Changes for ITG 68012-- Begin
  Let $Position_Nbr          = &j.position_nbr  
  Let $AsOfDate = $pay_end_dt
  do Get-Position-Title
  Let $jobtitle		     = $PosNameAbbrv 
  !VENDKXY Done the Changes for ITG 68012--End

  Let $Data-Found-Flag = 'Y'

  Let $Provider              = &bpt.vendor_id
  Let $Vendor_ID             = &bpt.vendor_id
  Let $Deptid                = &pl.deptid
  Let $Deptid1 = &pl.deptid   !VENDKXY Done the Changes for ITG 68012
  Let $per.name              = &per.name
  Let $j.company             = &j.company
  Let $j.emplid              = &j.emplid
  Let #j.empl_rcd            = &j.empl_rcd
  Let $bdp.gex_spcl_accum_cd = &bdp.gex_spcl_accum_cd
  Let $e.service_dt          = &e.service_dt
  Let $per.birthdate         = &per.birthdate
  Let $per.ssn               = &per.emplid !SXK 11/23/99 Changed to emplid from ssn
! 11/7/2003
  let $last_date_worked      = &j.last_date_worked
! end
  Let $j.effdt               = &j.effdt
  Let $j.acct_cd             = &j.acct_cd
  Let $j.empl_status         = &j.empl_status
  Let $j.reg_temp            = &j.reg_temp
  Let $j.full_part_time      = &j.full_part_time
  Let $j.gex_volun_low_hrs   = &j.gex_volun_low_hrs
  Let #ded_cur               = &pd.ded_cur 

  Let $Covrg_cd               = &bdo.covrg_cd          !SCM 10/30/03
  Let $pay_end_dt             = &pd.pay_end_dt         !SCM 10/30/03           

!  Add #ded_cur To #grs_mtd                            !SCM 10/30/03 commented old code

!SCM 10/30/03       begin       **************************    
  Let $bpt.plan_type          = &bpt.plan_type
  Let $bpt.benefit_plan       = &bpt.benefit_plan

!  show '---------------------------------------------'
!  show '&pd.off_cycle           = ' &pd.off_cycle
!  show '$Save_off_cycle         = ' $Save_off_cycle
!  show '$bpt.benefit_plan       = ' $bpt.benefit_plan
!  show '$Save_offc_benefit_plan = ' $Save_offc_benefit_plan
!  show '$Deptid                 = ' $Deptid
!  show '$Save_offc_Deptid)      = ' $Save_offc_Deptid
!  show '$j.emplid               = ' $j.emplid
!  show '$Save_offc_Emplid       = ' $Save_offc_Emplid
!  show '$pay_end_dt             = ' $pay_end_dt
!  show '$Save_offc_pay_end_dt   = ' $Save_offc_pay_end_dt
!  show '$Covrg_cd               = ' $Covrg_cd
!  show '$Save_offc_covrg_cd     = ' $Save_offc_covrg_cd
!  show '#ded_cur                = ' #ded_cur 
  
  if   &pd.off_cycle = 'Y' 
     if $Save_off_cycle = 'Y'                                              
       if    ($bpt.benefit_plan = $Save_offc_benefit_plan) and 
               ($bpt.plan_type = $Save_offc_plan_type) and
                 ($Deptid =  $Save_offc_Deptid) and
                   ($j.emplid = $Save_offc_Emplid) and
                      ($pay_end_dt = $Save_offc_pay_end_dt)  
            
                  if  ($Save_offc_covrg_cd = '1' and $Covrg_cd = '4')    !then you already added cur_ded
               
                      do P221-Save-OffCycle
                      goto Bypass-Check-for-Dup-BP
                  else     !saved must be a '4', so its a new record with a new deduction to add in
                  
                       Add #ded_cur To #grs_mtd                        
                       Add #ded_cur To #rpt_grand_tl_grs
                       do P221-Save-OffCycle
                       goto Bypass-Check-for-Dup-BP
                  end-if
       else     !last saved record WAS an offcycle but not the same benefit plan, deptid, emplid or payend     
            Add #ded_cur To #grs_mtd                        
            Add #ded_cur To #rpt_grand_tl_grs
            do P221-Save-OffCycle
            goto Bypass-Check-for-Dup-BP
       end-if        
     else       !last saved record was NOT an off cycle, so add in deduction for this offcycle
        
        Add #ded_cur To #grs_mtd                        
        Add #ded_cur To #rpt_grand_tl_grs
        do P221-Save-OffCycle
        goto Bypass-Check-for-Dup-BP
     end-if
      
  end-if          !continue with logic below since it is not an off-cycle 

!**********  process an On-Cycle 
                                                                    
  if    ($bpt.benefit_plan = $Save_benefit_plan) and 
      ($bpt.plan_type = $Save_plan_type) and
        ($Deptid =  $Save_Deptid) and
           ($j.emplid = $Save_Emplid) and
              ($pay_end_dt = $Save_pay_end_dt) and
                   ($Covrg_cd <> $Save_covrg_cd)            
  else
        Add #ded_cur To #grs_mtd                        
        Add #ded_cur To #rpt_grand_tl_grs                        
  end-if  

  let $Save_benefit_plan = $bpt.benefit_plan 
  let $Save_plan_type    = $bpt.plan_type
  let $Save_Deptid       = $Deptid
  let $Save_Emplid       = $j.emplid
  let $Save_pay_end_dt   = $pay_end_dt
  let $Save_covrg_cd     = $covrg_cd
  let $Save_off_cycle    = &pd.off_cycle

Bypass-Check-for-Dup-BP:
  
!  ********    SCM 10/30/03  end             ***************** 

From   ps_personal_data          per,
       ps_employment             e,
       ps_job                    j,
       ps_ben_defn_pgm           bdp,
       ps_ben_defn_optn          bdo,
       ps_benef_plan_tbl         bpt,
       ps_pay_cal_bal_id         pcbi,
       ps_pay_line               pl,
       ps_pay_check              pc,
       ps_pay_deduction          pd

Where  per.emplid                = j.emplid

And    e.emplid                  = j.emplid
And    e.empl_rcd               = j.empl_rcd
!And j.emplid in ('277562979', '292508134')
And    j.effdt                   =
      (Select Max(effdt)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt             <= $pay_end_dt)

And    j.effseq                  =
      (Select Max(effseq)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt              = j.effdt)

And    bpt.effdt                 =
      (Select Max(effdt)
       From   ps_benef_plan_tbl
       Where  plan_type          = bpt.plan_type
       And    benefit_plan       = bpt.benefit_plan
       And    effdt             <= $pay_end_dt)

!GBD 10/16/2002 Begin
And    bdo.benefit_program       = bdp.benefit_program
And    bdo.effdt                 = bdp.effdt
And    bdo.plan_type             = bpt.plan_type
And    bdo.benefit_plan          = bpt.benefit_plan

!And    bdo.effdt                 =
!      (Select Max(effdt)
!       From   ps_ben_defn_optn
!       Where  benefit_program    = bdo.benefit_program
!       And    effdt             <= $pay_end_dt
!       And    plan_type          = bdo.plan_type
!       And    option_id          = bdo.option_id)

!And    bdp.benefit_program       = bdo.benefit_program
!GBD 10/16/2002 End

And    bdp.effdt                 =
      (Select Max(effdt)
       From   ps_ben_defn_pgm
       Where  benefit_program    = bdp.benefit_program
       And    effdt             <= $pay_end_dt)

And    pcbi.company              = pc.company
And    pcbi.paygroup             = pc.paygroup
And    pcbi.balance_id           = 'CY'
And    pcbi.balance_year         = #balance_year
And    pcbi.balance_period       = #balance_period

And    pc.emplid                 = j.emplid
And    pc.empl_rcd              = j.empl_rcd

And    pc.pay_end_dt             = pcbi.pay_end_dt

And    pd.company                = pc.company
And    pd.paygroup               = pc.paygroup
And    pd.pay_end_dt             = pc.pay_end_dt
And    pd.off_cycle              = pc.off_cycle
And    pd.page_num               = pc.page_num
And    pd.line_num               = pc.line_num
And    pd.sepchk                 = pc.sepchk

And    pl.company                = pc.company
And    pl.paygroup               = pc.paygroup
And    pl.pay_end_dt             = pc.pay_end_dt
And    pl.off_cycle              = pc.off_cycle
And    pl.page_num               = pc.page_num
And    pl.line_num               = pc.line_num

And    pd.plan_type              = bpt.plan_type
And    pd.benefit_plan           = bpt.benefit_plan
And    pd.ded_class              = 'N'

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA]
And   [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA]

Order By bpt.vendor_id,
       pl.deptid,
       per.name,
       j.emplid,
       j.empl_rcd,
       pl.pay_end_dt,                       !SCM 10/30/03
       bpt.benefit_plan,                    !SCM 10/30/03                         
       pd.off_cycle,                        !SCM 10/30/03
       pd.ded_cur,                          !SCM 10/30/03       
       bdo.covrg_cd                         !SCM 10/30/03


End-Select
End-Procedure

!******** SCM 10/30/03  begin -Add new para.  Save off-cycle rcd ****

Begin-Procedure P221-Save-OffCycle
#debug9 Show 'P221-Save-OffCycle'
 let $Save_offc_benefit_plan = $bpt.benefit_plan 
 let $Save_offc_plan_type    = $bpt.plan_type
 let $Save_offc_Deptid       = $Deptid
 let $Save_offc_Emplid       = $j.emplid
 let $Save_offc_pay_end_dt   = $pay_end_dt
 let $Save_offc_covrg_cd     = $covrg_cd 
 let $Save_off_cycle         = &pd.off_cycle

end-procedure
!******** SCM 10/30/03  end *************


Begin-Procedure P220-Get-Benefit-Termination-Row
#debug9 Show 'P220-Get-Benefit-Termination-Row'
Begin-Select
Min(hb1.coverage_begin_dt) &hb1.coverage_begin_dt

From   ps_health_benefit hb1

Where  hb1.emplid         = $j.emplid
And    hb1.empl_rcd      = #j.empl_rcd

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA2]

And    hb1.effdt          =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid      = hb1.emplid
       And    empl_rcd   = hb1.empl_rcd
       And    plan_type   = hb1.plan_type)

And    hb1.coverage_begin_dt Between $prev_month_begin
                                 And $next_month_end

And    hb1.coverage_elect = 'T'
End-Select
End-Procedure


Begin-Procedure P230-Get-Leave-Begin-Date
#debug9 Show 'P230-Get-Leave-Begin-Date'
  Let $Status-Change = 'N'
Begin-Select
JOB.EFFDT
JOB.EMPL_STATUS
JOB.ACTION
JOB.ACTION_REASON
JOB.LAST_DATE_WORKED                                     !SCM 11/10/2003

  If &JOB.EMPL_STATUS = $J.EMPL_STATUS And
      $Status-Change = 'N'
    Let $Effective-Date = &JOB.EFFDT
    Let $Action         = &JOB.ACTION
    Let $Action_Reason  = &JOB.ACTION_REASON
    let $last_date_worked = &JOB.LAST_DATE_WORKED         !SCM 11/10/2003
    Let $Status-Change = 'N'
  Else
    Let $Status-Change = 'Y'
  End-If

FROM   PS_JOB JOB

WHERE  EMPLID     = $J.EMPLID
AND    EMPL_RCD  = #J.EMPL_RCD
AND    EFFDT     <= $J.EFFDT
ORDER BY JOB.EFFDT DESC, JOB.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P235-Get-Status-Date
#debug9 Show 'P235-Get-Status-Date'
  Let $Reg-Temp-Change = 'N'
Begin-Select
J1.EFFDT
J1.REG_TEMP
J1.ACTION
J1.ACTION_REASON
J1.EMPL_STATUS

!**  11/10/2003  SCM  commented this code so that it always
!**  gets the last action/actionreason code to print on the report.
!  If &J1.REG_TEMP = $J.REG_TEMP And
!      $Reg-Temp-Change = 'N'
!    Let $Reg-Temp-Change = 'N'
!    If &J1.EMPL_STATUS <> 'T'
      Let $Effective-Date = &J1.EFFDT
      Let $Action         = &J1.ACTION
      Let $Action_Reason  = &J1.ACTION_REASON
!    End-If
!  Else
!    Let $Reg-Temp-Change = 'Y'
!  End-If

FROM   PS_JOB J1

WHERE  EMPLID     = $J.EMPLID
AND    EMPL_RCD  = #J.EMPL_RCD
AND    EFFDT     <= $J.EFFDT
!CJH - 11/07/2003
! order ascending date so retrieve most current row out of select
! ORDER BY J1.EFFDT DESC, J1.EFFSEQ DESC
ORDER BY J1.EFFDT, J1.EFFSEQ    
End-Select
End-Procedure


Begin-Procedure P240-Get-Hours
#debug9 Show 'P240-Get-Hours'

  Let #hrs_mtd = 0

Begin-Select
eb.hrs_mtd

  Let #hrs_mtd = &eb.hrs_mtd

From   ps_earnings_bal eb

Where  eb.emplid              = $j.emplid
and    eb.empl_rcd            = #j.empl_rcd	!AXL 02/20/2003
And    eb.company             = $j.company
And    eb.balance_id          = 'CY'
And    eb.balance_year        = #year
And    eb.balance_period      = #period
And    eb.spcl_balance        = 'Y'
And    eb.erncd               = $bdp.gex_spcl_accum_cd
End-Select
End-Procedure


Begin-Procedure P250-Format-Record
#debug9 Show 'P250-Format-Record'

  Lookup XlatLookup $j.reg_temp $reg_temp

  Let $acctcd = $j.acct_cd
  Do Get-Acct-cd-descr

  If $j.gex_volun_low_hrs = 'Y'
    Let $gex_volun_low_hrs = 'Y'
  Else
    Let $gex_volun_low_hrs = ''
  End-If

  !Do Format-DateTime($e.service_dt, $Service_Dt, {DEFMDY}, '', '')
  !Do Format-DateTime($per.birthdate, $Birth_Date, {DEFMDY}, '', '')
 ! Do Format-DateTime($Effective-Date, $eff_dt, {DEFMDY}, '', '')
  !Do Format-DateTime(&hb1.coverage_begin_dt, $Term_Date, {DEFMDY}, '', '')
 ! Do Format-DateTime($last_date_worked, $last_date_worked_ref, {DEFMDY}, '', '')

!GEX-MXR Added for upgarde 9.0 on 15/10/2007  - Begin
Let $Service_Dt = Edit($e.service_dt,'mm/dd/yy')
Let $Birthdate = Edit($per.birthdate,'mm/dd/yy')
Let $eff_dt = Edit($Effective-Date,'mm/dd/yy')
Let $Term_Date = Edit(&hb1.coverage_begin_dt,'mm/dd/yy')
Let $last_date_worked_ref = Edit($last_date_worked,'mm/dd/yy')
!GEX-MXR Added for upgarde 9.0 on 15/10/2007  - End
End-Procedure


Begin-Procedure P260-Print-Record
#debug9 Show 'P260-Print-Record'
  Do Get-Emp-Ssn ($per.ssn, $Ssn_Out)
  Print $per.name                  (+1,{#column1},30)
  !Print $per.ssn                   (+0,{#column2}) !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $Ssn_Out                   (+0,{#column2})  !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $AcctNameAbbrv             (+0,{#column3})
  Print $Service_Dt                (+0,{#column4})   
  Print $reg_temp                  (+0,{#column5})
  Print $j.empl_status             (+0,{#column6})
  Print $Action                    (+0,{#column7})
  Print $Action_Reason             (+0,{#column8})
  Print $eff_dt                    (+0,{#column9})
  Print $Term_Date                 (+0,{#column10})
  Print $last_date_worked_ref      (+0,{#column10b})
  Print #prev_hrs_mtd              (+0,{#column11})  Edit 9,999,999.99
  Print #curr_hrs_mtd              (+0,{#column12})  Edit 9,999,999.99
  Print #grs_mtd                   (+0,{#column13})  Edit 999,999,999.99
  Print $gex_volun_low_hrs         (+0,{#column14})
  
  !VENDKXY Done the Changes for ITG 68012-- Begin 
  !  Let $Deptid = &pl.deptid
   #ifdef NT       !VENDKXY Done the Changes for ITG 70317      
   !STRING $Vendor_ID $Deptid1 $per.name $per.ssn  $jobtitle $e.service_dt $reg_temp  $j.empl_status  $Action  $Action_Reason  $Effective-Date &hb1.coverage_begin_dt $last_date_worked #prev_hrs_mtd #curr_hrs_mtd  #grs_mtd $gex_volun_low_hrs  BY $DELIM INTO $GEXPL014  !VENDKXY Done the Changes for ITG 70317
   !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins 
    !STRING $Vendor_ID $Deptid1 $per.name $per.ssn  $jobtitle $e.service_dt $reg_temp  $j.empl_status  $Action  $Action_Reason  $Effective-Date &hb1.coverage_begin_dt $last_date_worked  #curr_hrs_mtd #prev_hrs_mtd #grs_mtd $gex_volun_low_hrs  BY $DELIM INTO $GEXPL014  !VENDKXY Done the Changes for ITG 70317 
    STRING $Vendor_ID $Deptid1 $per.name $Ssn_Out  $jobtitle $e.service_dt $reg_temp  $j.empl_status  $Action  $Action_Reason  $Effective-Date &hb1.coverage_begin_dt $last_date_worked  #curr_hrs_mtd #prev_hrs_mtd #grs_mtd $gex_volun_low_hrs  BY $DELIM INTO $GEXPL014  !VENDKXY Done the Changes for ITG 70317
   !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
   WRITE 1 FROM $GEXPL014
    #end-if   !VENDKXY Done the Changes for ITG 70317
  !VENDKXY Done the Changes for ITG 68012-- End
  
End-Procedure


Begin-Procedure P270-After-Vendor-ID-Break
#debug9 Show 'P270-After-Vendor-ID-Break'

  Print '-'                        (+2,{#column11},014)   Fill
  Print '-'                        (,{#column12},014)     Fill
  Print '-'                        (,{#column13},014)     Fill
!  Print 'Provider'                 (+1,{#column8})
!  Print $Save_Provider             (0,+1)
  Print 'Vendor ID'                (+1,{#column8})        !GBD 09/12/2002
  Print $Save_Vendor               (0,+1)                 !GBD 09/12/2002
  Print 'Totals'                   (0,+1)
  Print #Vendorid-HW-Previous-Hrs  (0,{#column11})        Edit 9,999,999.99
  Print #Vendorid-HW-Current-Hrs   (0,{#column12})        Edit 9,999,999.99
  Print #Vendorid-HW-Amount        (0,{#column13})        Edit 999,999,999.99

  Let #Vendorid-HW-Previous-Hrs = 0
  Let #Vendorid-HW-Current-Hrs  = 0
  Let #Vendorid-HW-Amount       = 0
End-Procedure


Begin-Procedure P280-After-Deptid-Break
#debug9 Show 'P280-After-Deptid-Break'

  Print '-'                        (+2,{#column11},014)   Fill
  Print '-'                        (,{#column12},014)     Fill
  Print '-'                        (,{#column13},014)     Fill
  Print 'Deptid'                   (+1,{#column5})
  Print $Save_Deptid               (0,+1)
  Print 'Count'                    (0,+1)
  Print #Deptid-HW-Count           (0,+1)                 Edit 9,999,999
  Print #Deptid-HW-Previous-Hrs    (0,{#column11})        Edit 9,999,999.99
  Print #Deptid-HW-Current-Hrs     (0,{#column12})        Edit 9,999,999.99
  Print #Deptid-HW-Amount          (0,{#column13})        Edit 999,999,999.99

  Add #Deptid-HW-Previous-Hrs To #Vendorid-HW-Previous-Hrs
  Add #Deptid-HW-Current-Hrs  To #Vendorid-HW-Current-Hrs
  Add #Deptid-HW-Amount       To #Vendorid-HW-Amount

  Let #Deptid-HW-Count        = 0
  Let #Deptid-HW-Previous-Hrs = 0
  Let #Deptid-HW-Current-Hrs  = 0
  Let #Deptid-HW-Amount       = 0
End-Procedure


Begin-Procedure P285-Before-Deptid-Break
#debug9 Show 'P285-Before-Deptid-Break'

  New-Page
End-Procedure


Begin-Procedure P290-Employee-Break
#debug9 Show 'P290-Employee-Break'

  Do P220-Get-Benefit-Termination-Row

  Evaluate $j.empl_status
    When = 'L'
    When = 'P'
    When = 'R'
    When = 'T'
      Do P230-Get-Leave-Begin-Date
      Break
    When-Other
      Do P235-Get-Status-Date
  End-Evaluate

  Let #prev_hrs_mtd = 0
  Let #year   = #balance_year
  Let #period = #balance_period
  Do P240-Get-Hours
  Let #prev_hrs_mtd = #hrs_mtd

  Let #curr_hrs_mtd = 0
  Let #year   = #prev_month_balance_year
  Let #period = #prev_month_balance_period
  Do P240-Get-Hours
  Let #curr_hrs_mtd = #hrs_mtd

  Do P250-Format-Record
  Do P260-Print-Record

  Add 1             To #Deptid-HW-Count
  Add #prev_hrs_mtd To #Deptid-HW-Previous-Hrs
  Add #curr_hrs_mtd To #Deptid-HW-Current-Hrs
  Add #grs_mtd      To #Deptid-HW-Amount

  Let #prev_hrs_mtd = 0
  Let #curr_hrs_mtd = 0

  Let #grs_mtd      = 0
End-Procedure


Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'

  Do P310-Print-Report-Totals

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
End-Procedure

 
Begin-Procedure P310-Print-Report-Totals
#debug9 Show 'P310-Print-Report-Totals'

  If $Data-Found-Flag <> 'Y'
    New-Page
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  else                                !SCM   10/30/03 added report totals
     Print '*** Report Grand Total ***'        (+3,{#column7})  
     Print #rpt_grand_tl_grs                   (0,{#column11})      Edit 999,999,999.99
  End-If
End-Procedure
 

#include 'gexxx912.sqc'  !Get plan type multiple row table
#include 'gexxx927.sqc'  !Get vendor id multiple row table
#include 'getdptnm.sqc'  !Get-Department-Name procedure
#include 'getactnm.sqc'  !Get-Account-Name procedure
#include 'getprvnm.sqc'  !Get-Provider-Name procedure
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'getposnm.sqc'  !Get-Position-Title procedure !VENDKXY Done the Changes for ITG 68012
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta