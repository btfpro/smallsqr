!**************************************************************************
!  GEXXX967:  Retrieve "Multiple" Union Codes Run Control and             * 
!             Return a Formatted Where-Clause                             *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX967_UNION_CD_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX967_UNION_CD_CRITERIA                                  *
!    $GEXXX967_UNION_CD_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX967_UNION_CD_Alias = 'JL.UNION_CD'                          *
!    Do GEXXX967-Select-UNION-CD-Parameters                               *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB_LABOR JL                                                        *
!    WHERE [$GEXXX967_UNION_CD_CRITERIA]                          *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!  HR-10078	CWB 		04/18/2002 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX967-Select-UNION-CD-Parameters

  Let #GEX_RC_UNION_SL_ROWS = 0
  Let $GEXXX967_INCLUDE_UNION_CD_CRITERIA = ''
  Let $GEXXX967_UNION_CD_DISPLAY_CRITERIA = 'ALL'

Begin-Select

GEX_RC_UNION_SL.UNION_CD
GEX_RC_UNION_SL.INCLUD_EXCLUDE_IND

  Add 1 To #GEX_RC_UNION_SL_ROWS

  If #GEX_RC_UNION_SL_ROWS = 1
    If &GEX_RC_UNION_SL.INCLUD_EXCLUDE_IND = '+'
      Let $GEXXX967_UNION_CD_CRITERIA = '(' || $GEXXX967_UNION_CD_Alias || ' In ('''     || &GEX_RC_UNION_SL.UNION_CD || ''''
    else
      Let $GEXXX967_UNION_CD_CRITERIA = '(' || $GEXXX967_UNION_CD_Alias || ' Not In (''' || &GEX_RC_UNION_SL.UNION_CD || ''''
    end-if
  Else
    Let $GEXXX967_UNION_CD_CRITERIA = $GEXXX967_UNION_CD_CRITERIA || ',''' || &GEX_RC_UNION_SL.UNION_CD  || ''''
  End-If

From  PS_GEX_RC_UNION_SL GEX_RC_UNION_SL

Where GEX_RC_UNION_SL.GEX_SEQ              = #GEX_SEQ
And   Rtrim(GEX_RC_UNION_SL.UNION_CD, ' ') != ' ' 
End-Select

  If #GEX_RC_UNION_SL_ROWS = 0
    Let $GEXXX967_UNION_CD_CRITERIA = $GEXXX967_UNION_CD_Alias || ' = ' || $GEXXX967_UNION_CD_Alias
  Else
    Let $GEXXX967_UNION_CD_CRITERIA = $GEXXX967_UNION_CD_CRITERIA || '))'
    Let $GEXXX967_UNION_CD_DISPLAY_CRITERIA = $GEXXX967_UNION_CD_CRITERIA
  End-If

End-Procedure
