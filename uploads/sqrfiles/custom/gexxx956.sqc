!**************************************************************************
!  GEXXX956:  Retrieve "Multiple" ERNCD Run Controls for Holiday earnings *           
!               codes and Return a Formatted Where-Clause                 *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX956_ERNCD_Alias                                                *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX956_INCLUDE_ERNCD_CRITERIA                                     *
!    $GEXXX956_EXCLUDE_ERNCD_CRITERIA                                     *
!    $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA                             *
!    $GEXXX956_EXCLUDE_ERNCD_DISPLAY_CRITERIA                             *
!                                                                         *                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX956_ERNCD_Alias = 'J.ERNCD'                                *
!    Do GEXXX956-Select-ERNCD-Parameters                                  *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX956_EXCLUDE_ERNCD_CRITERIA]                               *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE          DESCRIPTION OF THE CHANGE   *
!**************************************************************************
!	?	SCM		11/14/2000    INITIAL CREATION for COD    *
!**************************************************************************


Begin-Procedure GEXXX956-Select-ERNCD-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_ERNCD_ROWS_HL = 0
  Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = ''
  Let $GEXXX956_EXCLUDE_ERNCD_CRITERIA = ''
  Let $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX956_EXCLUDE_ERNCD_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_ERNCD_HL.ERNCD

  Add 1 To #GEX_RC_ERNCD_ROWS_HL

  If #GEX_RC_ERNCD_ROWS_HL = 1
    Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = '(' || $GEXXX956_ERNCD_Alias || ' In ('''     || &GEX_RC_ERNCD_HL.ERNCD || ''''
    Let $GEXXX956_EXCLUDE_ERNCD_CRITERIA = '(' || $GEXXX956_ERNCD_Alias || ' Not In (''' || &GEX_RC_ERNCD_HL.ERNCD || ''''
  Else
    Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = $GEXXX956_INCLUDE_ERNCD_CRITERIA || ',''' || &GEX_RC_ERNCD_HL.ERNCD  || ''''
    Let $GEXXX956_EXCLUDE_ERNCD_CRITERIA = $GEXXX956_EXCLUDE_ERNCD_CRITERIA || ',''' || &GEX_RC_ERNCD_HL.ERNCD  || ''''
  End-If

From  PS_GEX_RC_ERNCD_HL GEX_RC_ERNCD_HL

Where GEX_RC_ERNCD_HL.OPRID                = [$OprID]
And   GEX_RC_ERNCD_HL.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_ERNCD_HL.ERNCD, ' ') != ' '

End-Select

  If #GEX_RC_ERNCD_ROWS_HL = 0
    Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = $GEXXX956_ERNCD_Alias || ' = ' || $GEXXX956_ERNCD_Alias
    Let $GEXXX956_EXCLUDE_ERNCD_CRITERIA = $GEXXX956_ERNCD_Alias || ' = ' || $GEXXX956_ERNCD_Alias
  Else
    Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = $GEXXX956_INCLUDE_ERNCD_CRITERIA || '))'
    Let $GEXXX956_EXCLUDE_ERNCD_CRITERIA = $GEXXX956_EXCLUDE_ERNCD_CRITERIA || '))'
    Let $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA = $GEXXX956_INCLUDE_ERNCD_CRITERIA
    Let $GEXXX956_EXCLUDE_ERNCD_DISPLAY_CRITERIA = $GEXXX956_EXCLUDE_ERNCD_CRITERIA
  End-If

End-Procedure
