!***********************************************************************
!  GEXPY038:  This is a summary report showing company, paygroup,      *
!             deptid and the number of employees and total earnings.   *
!             This is sorted by company,paygroup and deptid and        *
!             sub totals the number of employees and their total       *
!             earnings by deptid.                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY038:  		Census Report                                  *
!                                                                      *
! SQL Tables:		pay_calendar                                   *
!                       job                                            *
!                       earnings_bal                                   *
!                                                                      *
! Written by:		Sreeni Kutam                                   *
!                                                                      *
! Normally Run:		Quarterly                                      *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  SXK          10/28/98     Initial Creation                          *
!  SXK          11/12/98     Do not need paygroup in r/c               *
!  JDH          01/23/99     Added logic to get the maximum            *
!                              balance period per earnings code        *
!                              from the earnings balance table.        *
!                            Added SQRW run control logic.             *
!                            Added logic to pull balance for the month *
!                              of the last confirmed pay end date.     *
!  JDH          03/19/99     Added logic to exclude non-productive     *
!                              earnings codes.                         *
!  PSR         09/18/02      EMPL_RCD is now part of the key for       * 
!                            PS_EARNINGS_BAL - V8.3 Upgrade            *
!  SRK          02/25/2003   Modified pay_cal_bal_id select to retrieve*
!                            right balance period and year 
!***********************************************************************


#Include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and page-size initialization


Begin-Heading 6
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'

  Print 'Pay End Dt'            (3,1)
  Print $payenddt               (3,13)

  Print 'COMPANY'               (+3,001)
!  Print 'PAYGROUP'              (+0,012)
  Print 'DEPTID'                (+0,024)
  Print 'TOTAL EARNINGS'        (+0,038)
  Print 'TOTAL EMPLOYEES'       (+0,060)
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $GEXXX900_Company_Alias  = 'A.COMPANY'
! Let $GEXXX901_Paygroup_Alias = 'A.PAYGROUP'        
  Let $GEXXX902_Deptid_Alias = 'A.DEPTID'

  if $prcs_process_instance = ''
    Let $CompanyString = ''''
    Display 'Enter Company or leave blank to exit.'
    While 1=1
      Input $In-Company Maxlen=3 'Company'
      Uppercase $In-Company
      If Rtrim($In-Company, ' ') = ''
        Concat '''' With $CompanyString
        Break
      End-If
      If $CompanyString <> ''''
        Concat ''',''' With $CompanyString
      End-If
      Concat $In-Company With $CompanyString
    End-While

    If $CompanyString = ''''''
      Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '1=1'
      Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_Company_Alias || ' In (' || $CompanyString || ')'
      Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA
    End-If

    Let $DeptIDString = ''''
    Display 'Enter DeptID or leave blank to exit.'
    While 1=1
      Input $In-DeptID Maxlen=10 'DeptID'
      Uppercase $In-DeptID
      If Rtrim($In-DeptID, ' ') = ''
        Concat '''' With $DeptIDString
        Break
      End-If
      If $DeptIDString <> ''''
        Concat ''',''' With $DeptIDString
      End-If
      Concat $In-DeptID With $DeptIDString
    End-While

    If $DeptIDString = ''''''
      Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
      Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
      Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
    End-If

    Do Ask-As-Of-Date
    let $reportdate_ccyy = $AsOfDate
  else
!       get parameters from single row run control record 
	Do GEXXX922-Select-Parameters
      let $reportdate_ccyy = $GEX_RC_PAY.ASOFDATE
      if rtrim($GEX_RC_PAY.ASOFDATE,' ') = ''
        let $reportdate_ccyy = $asoftoday
      end-if

	Do GEXXX900-Select-Company-Parameters 
	Do GEXXX902-Select-Deptid-Parameters 
   end-if

  Do P140-Get-Last-Confirmed-Pay

  Do P100-Start
  Do P200-Main-Process
  Do P300-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report


Begin-Procedure P100-Start
#debug9 Show 'P100-Start'

  Let $ReportID    = 'GEXPY038'
  Let $ReportTitle = 'CENSUS REPORT'

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate='  $ReportDate
  Show '$ReportTime='  $ReportTime

  Let $AsOfDate = $reportdate_ccyy
  Let $X000_WHERE_SELECT_ASOFDATE = $AsofDate

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')

  Let $X000_ORDER_BY = 'Order by a.company, a.deptid'
  Let $Sort_Display = 'Company, Deptid'

  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  Do P150-Print-Cover-Page

  Let $GEXERNCD_Erncd_Alias = 'b.erncd'
  Do Get-Non-Productive-Erncds
End-Procedure


Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

Begin-Select Loops=1
pay_end_dt
!SRK 02/25/2003 commented out this logic since for some pay periods where check date falls into the next month, it should be pulling
!balances as of the check date not as of pay end date
!To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
!To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
!To_Char(pay_end_dt,'fmMonth YYYY')           &period
!SRK 02/25/2003 and added these two lines to get the right balance period and year
balance_period
balance_year

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
!SRK 02/25/2003 as this field is not being used any where
!  Let $Reporting_Period = &period

  Do Format-DateTime($pay_end_dt, $payenddt, {DEFDATE}, '', '')

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_cal_bal_id

Where  pay_end_dt         =
      (Select Max(pay_end_dt)
       From   ps_pay_cal_bal_id
       Where  pay_end_dt <= $reportdate_ccyy
       And    balance_id  = 'CY')
End-Select
End-Procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX900_Include_Company_Display_Criteria ='            (+1,5)
  Print $GEXXX900_Include_Company_Criteria                        (0,+2)
  Print '$GEXXX902_Include_Deptid_Display_Criteria  ='            (+1,5)
  Print $GEXXX902_Include_Deptid_Criteria                         (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)
 
  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'

  let #line_count = 0
  Let $Data-Found-Flag = 'N'
  do convert-to-dtu-date($reportdate_ccyy,$reportdate_dtu)
  let $reportdate_yyyy = substr($reportdate_dtu,1,4)
  let $reportdate_mm = substr($reportdate_dtu,6,2)

Begin-Select
A.COMPANY   () on-break print=never level=1 save=$prev_company  after=company-change
!A.PAYGROUP () on-break print=never level=2 save=$prev_paygroup after=paygroup-change
A.DEPTID    () on-break print=never level=2 save=$prev_deptid   after=dept-change
A.EMPLID
!PSR 09/18/2002 V8.3 Begin
A.EMPL_RCD
!PSR 09/18/2002 V8.3 End

  Let $Data-Found-Flag = 'Y'
  move &A.EMPLID to $emplid  
  do get-earnings-bal
  add 1 to #inputtran

From   PS_JOB A
Where [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]

AND    A.EFFDT =
      (select max(effdt)
       from   ps_job
       where  emplid     = A.EMPLID
       and    empl_rcd  = A.EMPL_RCD
       and    effdt     <= $pay_end_dt)
AND    A.EFFSEQ          =
      (select max(effseq)
       from   ps_job
       where  emplid     = A.EMPLID
       and    empl_Rcd   = A.EMPL_RCD
       and    effdt      = A.EFFDT)
[$X000_ORDER_BY]

End-Select

  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
End-Procedure


Begin-Procedure Get-Earnings-Bal
#debug9 Show 'Get-Earnings-Bal'
  move 'N' to $found

Begin-Select on-error=sql-error-found('Get-Earnings-Bal')
sum(b.grs_ytd) &empl_sum

  move 'Y' to $found
  add &empl_sum to #dept_sum

from   ps_earnings_bal b
where  b.emplid               = &A.emplid
and    b.company              = &A.COMPANY
and    b.balance_id           = 'CY'
and    b.balance_year         = #balance_year
!PSR 09/18/2002 V8.3 Begin
and    b.empl_rcd             = &A.EMPL_RCD
!PSR 09/18/2002 V8.3 End
and    b.balance_period       =
      (select max(balance_period)
       from   ps_earnings_bal
       where  emplid          = b.emplid
       and    company         = b.company
       and    balance_id      = b.balance_id
       and    balance_year    = b.balance_year
       and    balance_period <= #balance_period
       and    spcl_balance    = b.spcl_balance
       !PSR 09/18/2002 V8.3 Begin
       and    empl_rcd      = b.EMPL_RCD
       !PSR 09/18/2002 V8.3 End
       and    erncd           = b.erncd)
and    b.spcl_balance         = 'N'
and   [$GEXERNCD_EXCLUDE_ERNCD_CRITERIA]
end-select
end-procedure


Begin-Procedure Dept-Change
#debug9 Show 'Dept-Change'
  if rtrim($prev_deptid,' ') <> ''
    if #company_count = 0 

      print $prev_company (+1,3)
!     print &A.PAYGROUP (0,14)
      print $prev_deptid (0,25)
    else
      if #line_count > 45
        new-page
        let #line_count = 0
        print $prev_company (+1,3)
        print $prev_deptid (0,25)
      else
        print $prev_deptid (+1,25)
        add 1 to #line_count
      end-if
    end-if

    print #dept_sum (0,38) edit 999,999,990.99
    print #inputtran (0,69) edit 99,990
    add #dept_sum to #company_sum   !used to be paygroup_sum
    add #inputtran to #company_count !used to be paygroup_count
    let #dept_sum = 0
    let #inputtran = 0
  end-if
end-procedure


Begin-Procedure Paygroup-Change
#debug9 Show 'Paygroup-Change'
  print 'Totals for Paygroup' (+2,1)
  print $prev_paygroup (,+1)
  print #paygroup_sum (0,38) edit 999,999,990.99
  print #paygroup_count (0,69) edit 99,990
  add #paygroup_sum to #company_sum
  add #paygroup_count to #company_count
  let #paygroup_sum = 0
  let #paygroup_count = 0
  if &A.COMPANY = $prev_company
    new-page
  end-if
end-procedure


Begin-Procedure Company-Change
#debug9 Show 'Company-Change'
  print 'Totals for Company' (+2,1)
  print $prev_company (,+1)
  print #company_sum (0,38) edit 999,999,990.99
  print #company_count (0,69) edit 99,990
  let #company_sum = 0
  let #company_count = 0
  new-page
  let #line_count = 0
end-procedure


Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'
!
End-Procedure

 
begin-procedure SQL-Error-Found($Proc_Name) 
#debug9 Show 'SQL-Error-Found'
  do error-found
  display 'SQL Error in ' 
  display $Proc_Name 
  display $_sql-error
  display 'sql-status:' 
  display #_sql-status 
  STOP
end-procedure


begin-procedure Error-Found
#debug9 Show'Error-Found'
  move 'Y' to $ErrorFound
  do Format-Number(#InputTran, $out, '99999')
  display $out 

  let $EmplId_1st3 = substr($EmplId,1,3)
  let $EmplId_mid2 = substr($EmplId,4,2)
  let $EmplId_last4 = substr($EmplId,6,4)
 
  String $EmplId_1st3 $EmplID_mid2 $EmplId_last4 by '-' into $EmplId11

  display $EmplID11 
end-procedure


#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'askaod.sqc'    !Ask for As-Of-Date parameter
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'datemath.sqc'  !Performs Date Calculations
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get Calendar Year ID
#include 'gexbnmdt.sqc'  !Gets right run control date
#include 'getcodta.sqc'  !Gets company data
#include 'gexxx900.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexerncd.sqc'
