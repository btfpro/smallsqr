!***********************************************************************
!  GEXPY068:  Time Load Balance Report                                 *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY068:  		Time Load Balance Report                       * 
!                                                                      *        
! Narrative:		This program creates a weekly report of all    *
!                       hours and earnings for every earnings code.    *
!                       This report subtotals by account code and      * 
!                       grand total by department id.                  *
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values              *
!			      #debug7 data returned from sql calls     * 
!                                                                      *  
! SQL Tables:		pay_earnings                                   *
!                       pay_oth_earns                                  *
!                       pay_earnings                                   *
!                       gex_dept_tbl                                   *
!                                                                      *
! Written by:		Sreeni Kutam                                   *       
!                                                                      *
! Normally Run:		Weekly                                         *
!                                                                      *
! Control Parms:		FOR WHERE CLAUSE:                      *
!				  Company  	                       *
!				  Paygroup  	                       *
!				  As Of Date                           *
!                                                                      *
!				FOR ORDER BY CLAUSE:                   *
!				  1) by Company, Paygroup, Store       *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!	INITIALS	DATE		DESCRIPTION OF THE CHANGE      *
!***********************************************************************
!	SXK		07/29/98	Initial Creation               *
!                                                                      *
!       sxk             11/11/1998      Added paygroup/company totals  *
!                                                                      *
!       AXL             01/02/2001      Add criteria to get-pay-end-dt *
!                                       procedure to ignore off-cycle  *
!                                       calendar entries               *
!***********************************************************************


#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


Begin-Heading 8
#debug9 Show 'Begin-Heading'

#Include 'stdhdg02.sqc'

  If $PayGroup <> $priorPayGroup
    Do Get-PayGroup-Data
    Let $priorPayGroup = $PayGroup
  End-If

!  If $Company <> $priorCompany
!    Do Get-Company-Data
!    Let $priorCompany = $Company
!  End-If

  Let $X000_temp_out = 'Sorted by ' || $Sort_Display
  Print $X000_temp_out         (3,)  center

  Let $X000_temp_out = 'Pay End Dt: ' || $X000_REPORT_HEADING_ASOFDATE
  Print $X000_temp_out         (4,)  center

!  Print 'Company'              ( 4,001)
!  Print $Company               (+0,013)
!  Print $CompanyName           (+0,017)

  Print 'Pay Group'            ( 4,001)
  Print $PayGroup              (+0,013)
  Print $PayGroupName          (+0,017)

  Print 'STORE'                (+2,001)
  Print 'ACCT_CD'              (+0,015)
  Print 'ERNCD'                (+0,032)
  Print 'HOURS'                (+0,050)
  Print 'EARNINGS'             (+0,072)

  Print '-'                    (+1,1,150) Fill

End-Heading


Begin-Report
#debug9 Show 'Begin-Report'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Do P100-Start
  Do P200-Main-Process
  Do P300-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report


Begin-Procedure P100-Start
#debug9 Show 'P100-Start'

  Let $ReportID    = 'GEXPY068'
  Let $ReportTitle = 'TIME LOAD BALANCE REPORT'

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate='  $ReportDate
  Show '$ReportTime='  $ReportTime

  If $prcs_process_instance = ''
    do P110-Get-Parms
  Else
    Do GEXXX922-Select-Parameters

    Let $GEXXX901_Company_Alias  = 'J.COMPANY'
    Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
    Do GEXXX901-Select-Company-Paygroup-Parameters
    Let $GEXXX902_Deptid_Alias = 'J.DEPTID'
    Do GEXXX902-Select-Deptid-Parameters 

    Let $AsOfDate = $GEX_RC_PAY.AsOfDate
    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate
    If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
      do get-pay-end-dt
      Let $X000_WHERE_SELECT_ASOFDATE = $pay_end_Dt
    End-If
  End-If

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')

  Let $X000_ORDER_BY = 'Order by j.company, j.paygroup, j.deptid, j.acct_cd, o.erncd'
  Let $Sort_Display = 'Company, Paygroup, DeptID, AcctCD, ErnCD'

  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  Do P150-Print-Cover-Page

End-Procedure


Begin-Procedure P110-Get-Parms
#debug9 Show 'P110-Get-Parms'


  Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1' 
  Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1' 
  do get-pay-end-dt
  Let $X000_WHERE_SELECT_ASOFDATE                 = $pay_end_Dt

End-Procedure

!*********************************************************************
!Gets the pay end date of the previous pay cycle.
!*********************************************************************

Begin-Procedure get-pay-end-dt

move 'N' to $found

begin-select 
PAY.PAY_BEGIN_DT
PAY.PAY_END_DT

  move &PAY.PAY_END_DT to $pay_end_Dt
  move &PAY.PAY_BEGIN_DT to $pay_begin_dt
  display 'Pay End Date:' noline
  display $pay_end_Dt
  move 'Y' to $found
  exit-select

FROM PS_PAY_CALENDAR PAY

WHERE PAY.PAY_sheets_RUN = 'Y'
  AND PAY.PAY_END_DT = (SELECT MAX(PAY_END_DT)
		      FROM PS_PAY_CALENDAR
		     WHERE PAY_END_DT <= $asoftoday
		       AND PAY_sheets_RUN = 'Y'
! 01/02/2001 - add criteria to ignore off-cycle calendar entries
                       AND PAY_OFF_CYCLE_CAL = 'N')
  AND PAY.PAY_OFF_CYCLE_CAL = 'N'
! 01/02/2001 - end

end-select

if $found = 'N'
   display 'Pay Calendar where sheets ran is not found'
   stop
end-if

end-procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)


  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)

  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA       (0,+2)

  Print 'Pay End Dt :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)
 
  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
display $X000_WHERE_SELECT_ASOFDATE
display $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
display $GEXXX902_INCLUDE_DEPTID_CRITERIA
  Let $Data-Found-Flag = 'N'
 
Begin-Select
j.company  ()                          On-Break  Level=1  After=P216-After-Company-Break      Print=Never save=$prev_company
j.paygroup ()                          On-Break  Level=2  After=P217-After-Paygroup-Break     Print=Never save=$prev_paygroup
j.deptid   (,1)                        On-Break  Level=3  After=P218-After-Deptid-Break Save=$previous_deptid Print=Change/top-page
j.acct_cd  (,15)                       On-Break  Level=4  After=P219-After-Acctcd-Break Save=$previous_acctcd Print=Change/top-page
o.erncd    (,33)                       On-Break  Level=5  After=P220-After-Erncd-Break Save=$Previous_Erncd Print=Change/Top-Page
j.pay_end_Dt
o.oth_hrs
o.oth_pay

  Let $Company  = &j.company
  Let $Paygroup = &j.paygroup
  Let $Data-Found-Flag = 'Y'
  Let $Record-Printed = 'N'

  Do P212-Accumulate-Totals
!  Do P214-Print-Record

From   ps_pay_earnings           j,
       ps_pay_oth_earns          o

Where j.pay_end_dt = $X000_WHERE_SELECT_ASOFDATE 
And    o.company                = j.company
And    o.paygroup               = j.paygroup
And    o.pay_end_dt             = j.pay_end_dt
And    o.page_num                  = j.page_num
And    o.line_num                  = j.line_num
And    o.addl_nbr                  = j.addl_nbr
And    o.off_cycle              = j.off_cycle
And   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
And   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
!Group By j.company, j.paygroup, j.pay_end_Dt, j.deptid, j.acct_cd, o.erncd 
[$X000_ORDER_BY]
End-Select
  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
End-Procedure



Begin-Procedure P212-Accumulate-Totals
#debug9 Show 'P212-Accumulate-Totals'

  Add &o.oth_hrs      To #Erncd-Hours
  Add &o.oth_pay    To #Erncd-Amount

  Add &o.oth_hrs      To #Acctcd-Hours
  Add &o.oth_pay    To #Acctcd-Amount

  Add &o.oth_hrs      To #Deptid-Hours
  Add &o.oth_pay    To #Deptid-Amount

  Add &o.oth_hrs    to #paygroup-hours
  Add &o.oth_pay    to #paygroup-amount

  Add &o.oth_hrs    to #company-hours
  Add &o.oth_pay    to #company-amount
  
End-Procedure


!Begin-Procedure P214-Print-Record
!#debug9 Show 'P214-Print-Record'

!  Let $Record-Printed = 'Y'

!  if $Page_Break_AcctCD = 'Y' and #Counter_Dept = 0
!     Print $Previous_DeptID (,1)
!     Let #Counter_Dept = 1
!  else
!     if $Page_Break_AcctCD = 'Y' and #Counter_Dept = 1
!        Print ' ' (,1,4) Fill
!        Let $Page_Break_AcctCd = 'N'
!     end-if
!  end-if

!  Print &o.erncd                 (+0,033,003)
!  Print &o.oth_hrs               (+0,047,010)  Edit 999,999.99
!  Print &o.oth_earns             (+0,067,013)  Edit 99,999,999.99
!  position (+1)

!End-Procedure

Begin-Procedure P216-After-Company-Break
#debug9 Show 'P216-After-Company-Break'
 if $Record-Printed = 'Y'
      Let $company_Subtotal_Text = ' company ' || $Prev_company || ' Totals'
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print $company_Subtotal_Text    (+1,17)
      Print #company-Hours            (+0,047)      Edit 999,999.99
      Print #company-Amount           (+0,067)      Edit 99,999,999.99
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print ' '                      (+2,001)
      Let #company-Hours  = 0
      Let #company-Amount = 0

    New-Page
 end-if
 Let $Record-Printed = 'N'

End-Procedure

Begin-Procedure P217-After-Paygroup-Break
#debug9 Show 'P217-After-Paygroup-Break'
 if $Record-Printed = 'Y'
      Let $Paygroup_Subtotal_Text = ' Paygroup ' || $Prev_Paygroup || ' Totals'
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print $Paygroup_Subtotal_Text    (+1,17)
      Print #Paygroup-Hours            (+0,047)      Edit 999,999.99
      Print #Paygroup-Amount           (+0,067)      Edit 99,999,999.99
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print ' '                      (+2,001)
      Let #Paygroup-Hours  = 0
      Let #Paygroup-Amount = 0
      if rtrim($prev_company,' ') <> '' and rtrim(&j.company,' ') <> ''
         and rtrim(&j.paygroup,' ') <> ''
         and &j.paygroup <> $prev_paygroup
         if $prev_company <> &j.company
         else
            New-Page
         end-if
      end-if
 end-if

End-Procedure

Begin-Procedure P218-After-Deptid-Break
#debug9 Show 'P218-After-Deptid-Break'

  If $Record-Printed = 'Y'

      Let $Deptid_Subtotal_Text = ' Store ' || $Previous_Deptid || ' Totals'
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print $Deptid_Subtotal_Text    (+1,17)
      Print #Deptid-Hours            (+0,047)      Edit 999,999.99
      Print #Deptid-Amount           (+0,067)      Edit 99,999,999.99
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print ' '                      (+2,001)
      Let #Deptid-Hours  = 0
      Let #Deptid-Amount = 0
      if rtrim($prev_paygroup,' ') <> '' and rtrim(&j.paygroup,' ') <> ''
         and rtrim(&j.deptid,' ') <> '' 
         and &j.deptid <> $previous_deptid
         if &j.paygroup <> $prev_paygroup
         else    
            New-Page
         end-if
      end-if
  End-If

  Let $Deptid_Subtotal_Text = ''
!  Let $Record-Printed = 'N'
End-Procedure

Begin-Procedure P219-After-Acctcd-Break
#debug9 Show 'P219-After-Acctcd-Break'

Let $Page_Break_AcctCD = 'N'

  If $Record-Printed = 'Y'
      if #current-line >= 54
         new-page
         Let $Page_Break_AcctCD = 'Y'
         Let #counter_Dept = 0
      end-if

      Let $Acctcd_Subtotal_Text = ' AcctCd ' || $Previous_Acctcd || ' Totals'
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print $Acctcd_Subtotal_Text    (+1,17)
      Print #Acctcd-Hours            (+0,047)      Edit 999,999.99
      Print #Acctcd-Amount           (+0,067)      Edit 99,999,999.99
      Print '-'                      (+1,047,010)  Fill
      Print '-'                      (+0,067,013)  Fill
      Print ' '                      (+2,001)
      Let #Acctcd-Hours  = 0
      Let #Acctcd-Amount = 0

  End-If

  Let $Acctcd_Subtotal_Text = ''

End-Procedure

Begin-Procedure P220-After-Erncd-Break
#debug9 Show 'P220-After-Erncd-Break'

      Print $Previous_Erncd         (+0,033,003)
      Print #Erncd-Hours            (+0,047,010)      Edit 999,999.99
      Print #Erncd-Amount           (+0,067,013)      Edit 99,999,999.99
      Let #Erncd-Hours  = 0
      Let #Erncd-Amount = 0
      position (+1)
      Let $Record-Printed = 'Y'

End-Procedure

Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'
!
End-Procedure


#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'askaod.sqc'    !Ask for As-Of-Date parameter
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get Calendar Year ID
#Include 'getcodta.sqc'  !Get Company Data
#Include 'getpgdta.sqc'  !Get Paygroup Data

#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get Detptid multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
