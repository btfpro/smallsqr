!***********************************************************************
!  GEXPY097:  PHARMACY REPORT                                          *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY097:             Pharmacy Report                                *
!                                                                      *
! Narrative:            This program creates a weekly report of all    *
!                       pharmacy employees.                            *
!                                                                      *
! SQL Tables:           pay_oth_earns                                  *
!                       pay_earnings                                   *
!                       gex_dept_tbl                                   *
!                       personal_data                                  *
!                       job                                            *
!                                                                      *
! Written by:           Tony Labra                                     *
!                                                                      *
! Normally Run:	        Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Company Division Code                        *
!                         Company                                      *
!                         Paygroup                                     *
!                         Deptid                                       *
!                         Employee Status                              *
!                         Account Code                                 *
!                         As Of Date                                   *
!                                                                      *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         	DESCRIPTION OF THE CHANGE              *
!***********************************************************************
!  AXL          03/23/1998     	Initial Creation                       *
!                                                                      *
!  AXL          08/16/1999	Corrected earning code selection.      *
!                               Resorted by alpha. Added Job Title.    *
!                               Added grand totals by company.         *
!                                                                      *
!               08/04/2000	Overhauled the main logic to produce a *
!                               dat file rather than an lis report     *
!                                                                      *
!  AXL          11/15/2000	Added new training codes.              *
!                                                                      *
!  AXL          12/22/2000	Split SICK & DISABILITY into separate  *
!                               columns.                               *
!                                                                      *
!  MRP          01/08/2001	COD Modifications:                     *
!                                 Added Earn Codes: 519, 522, & 523    *
!                                                                      *
!  SCM          02/22/2001      Changed Mgr name for region 2 from     *
!                               Khorey to Kost                         *
!                                                                      *
!  AXL          02/19/2002      Changed Mgr name for region 6 from     *
!                               Weinstein to Rinovato.  Added Trng     *
!                               earn cd 14A.                           *
!                                                                      *
!  AXL          02/06/2003      Moved Mgr assignment routine to a new  *
!                               sqc for easier maintenance.            *
!                                                                      *
!  AXL          02/28/2003      Allow logic to p/u latest security     *  
!                               tree changes                           *
!                                                                      *
!  GBD          04/08/2003      Added Erncd 164 Pharmacy Night Premium *
!                               to earnings code selections for earning*
!                               premium amounts.                       *
!                                                                      *
!  AXL          10/01/2003      Transferred Treenode assignment to     *
!                               sqc for easier maintenance.            *
!                                                                      *
!  AXL          10/06/2004      Address a bug that was suppressing     *
!                               hours earned at away stores.           *
!                                                                      *
!  AXL          01/12/2005      Added PREM Hrs to output.  Added logic *
!                               to create Excel file instead of DAT    *
!                               file.                                  *
!                                                                      *
!  AXL  33063   07/20/2005      Retire hard-coded logic that determines*
!                               the Pharmacy Specialist. The info will *
!                               now come the RX Security tree.         *
!  MXR         07/27/2007  ITG # 42710 -new disability earnings code (526)*
!                          added to the hard coding                    *
!  SXP   51358  23/07/2008 ITG #51358 ADD ERNCD 173                    *
! VENDTKK       02/11/2009 Added INCLUDE GETPOSNM.SQC to pull	       *
!                  	   JOBTITLE from POSITION_DATA.DESCR   	       *
!			   field instead of JOBCODE_TBL.DESCR          *  
!***********************************************************************

#Include 'setenv.sqc'    !Set environment

!***********************************************************************
Begin-Report
!***********************************************************************

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc
  Do Initialization
  Do Open-File
  Do Write-file-header		!AXL 01/12/2005

  Do Truncate-Temp-Tbl
  Do Main
  Do Create-Out-File

  Do EOJ-Process
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report

!***********************************************************************
Begin-Procedure Initialization
!***********************************************************************
  !AXL 07/20/2005 Let cmpy/paygroup/dpt come from main select        
  Let $GEXXX900_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
  Let $GEXXX902_Deptid_Alias   = 'J.DEPTID'

  Let $GEXXX903_Emp_Stat_Alias = 'J2.EMPL_STATUS'
  Let $GEXXX907_Div_CD_Alias   = 'GDT.GEX_COMPANY_DIV_CD'                    
  Let $GEXXX909_Acct_CD_Alias  = 'J2.ACCT_CD'

  Do Get-Last-Confirmed-Pay

  If $prcs_process_instance = ''
    Do SQRW-Run-Controls  
  Else
    Do Process-Scheduler-Run-Controls
  End-if

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Let $ReportID    = 'GEXPY097'
  Let $ReportTitle = 'PHARMACY REPORT'

  encode '<009>' into $tab	!AXL 01/12/2005

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle

  Do Format-DateTime($WHERE_SELECT_ASOFDATE, $REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')

  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA
  Show '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA  
  Show '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA
  Show '$WHERE_SELECT_ASOFDATE                              ='   $WHERE_SELECT_ASOFDATE
 
End-Procedure

!***********************************************************************
Begin-Procedure SQRW-Run-Controls 
!***********************************************************************

  Do GEXXX950-Input-Parameters
  Let $WHERE_SELECT_ASOFDATE = $X000_WHERE_SELECT_ASOFDATE

End-Procedure

!***********************************************************************
Begin-Procedure Process-Scheduler-Run-Controls 
!***********************************************************************

  Do GEXXX922-Select-Parameters
  Do GEXXX900-Select-Company-Parameters
  Do GEXXX901-Select-Company-Paygroup-Parameters
  Do GEXXX902-Select-Deptid-Parameters
  Do GEXXX903-Select-Emp-Stat-Parameters
  Do GEXXX907-Select-Div-CD-Parameters 
  Do GEXXX909-Select-Acct-CD-Parameters

  Let $WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate

  If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
    Let $WHERE_SELECT_ASOFDATE = $pay_end_dt
  End-If
  
End-Procedure

!***********************************************************************
Begin-Procedure Get-Last-Confirmed-Pay
!***********************************************************************

Begin-Select
MAX(PAY_END_DT) &PAY_END_DT

  Let $pay_end_dt = &PAY_END_DT
  Show 'Last confirmed pay end date:  ' $pay_end_dt

FROM   PS_PAY_CALENDAR

WHERE  PAY_END_DT <= $AsOfToday
AND    PAY_CONFIRM_RUN = 'Y'
End-Select
End-Procedure

!***********************************************************************
begin-procedure Open-File
!***********************************************************************
  !AXL 01/12/2005 New extension
  !let $file1 = '{OUTFILE}' || lower($ReportID) || '.dat'
  let $file1 = '{OUTFILE}' || lower($ReportID) || '.xls'

  open $file1 as 1 for-writing
        record=218:vary	!AXL - 12/22/2000 Increase LREC
        status=#filestat
	
  if #filestat < 0
     display 'Open failed for file <' noline
     display $file1 noline
     display '>'
     stop
  end-if

  end-procedure

!AXL 1/12/2005 New - Begin
!***********************************************************************************
Begin-Procedure Write-file-header
!***********************************************************************************
#debug9 Show 'Write-File-Header'

  Move '' To $all_cols       

  string 'Mgr ' ' ' 'Home ' ' '
         'Oth ' ' ' ' ' ' '
	 'Wrk ' 'Std ' 'Prem ' 'Prem '
	 'Reg ' 'Otp ' 'UOtp' 'Trng ' 'Vacn ' !SXP FOR ITG #51358
	 'Hol ' 'Sick ' 'Dis ' 'Pay End '
	 'Run ' by $tab into $all_cols  

  write 1 from $all_cols Status=#filestat

  If #filestat <> 0
     Show 'Error writing file header.  Program terminating.'
     Stop
  End-If

  !write 1 from 'Mgr':20     
  !             ' ':10
  !             'Home':10
  !             ' ':11        
  !             'Oth':6
  !             ' ':4
  !             ' ':30           
  !             ' ':15     
  !             'Wrk':4      
  !             'Std':5      
  !             'Prem':8   
  !'Prem':8   
  !             'Reg':8    
  !             'Otp':8    
  !             'Trng':8   
  !             'Vacn':8   
  !             'Hol':8   
  !             'Sick':8		
  !             'Dis':8   	
  !             'Pay End':12
  !             'Run':12

  Move '' To $all_cols     
  string 'Name ' ' Region ' 'Store ' 'Emplid '
         'Store ' 'Ord ' 'Name ' 'Job Title ' 
	 'Sts ' 'Hrs ' 'Amt ' 'Hrs '
	 'Hrs ' 'Hrs ' 'Hrs ' 'Hrs ' 'Hrs '             !SXP ADDED FOR ITG#51358
	 'Hrs ' 'Hrs ' 'Hrs ' 'Date '
	 'Date ' by $tab into $all_cols   

     write 1 from $all_cols Status=#filestat2
    
     If #filestat2 <> 0
        Show 'Error writing file header2.  Program terminating.'
        Stop
     End-If

  !write 1 from 'Name':20      
  !             'Region':10
  !             'Store':10
  !             'Emplid':11        
  !             'Store':6
  !             'Ord':4
  !            'Name':30           
  !             'Job Title':15     
  !             'Sts':4      
  !             'Hrs':5      
  !             'Amt':8   
  !	       'Hrs':8
  !             'Hrs':8    
  !             'Hrs':8    
  !             'Hrs':8 
  !             'Hrs':8   
  !             'Hrs':8       
  !             'Hrs':8  	
  !             'Hrs':8		    
  !             'Date':12
  !             'Date':12
end-procedure

!*********************************************************************
begin-procedure Truncate-Temp-Tbl
!*********************************************************************

begin-sql 

truncate table ps_gex_r_gexpy097
end-sql

end-procedure

!*********************************************************************
begin-procedure Main
!*********************************************************************
  let $sql-statement = 'Main'

begin-select distinct

J.COMPANY		!AXL 07/20/2005 
J.PAYGROUP		!AXL 07/20/2005 
J.deptid

	let $deptid=&j.deptid
        let $process_rx = 'N'
        do Evaluate-TreeNodes !AXL 10/1/2003 - Done in SQC


  #ifdef debug8
    show 'deptid            = ' $deptid
    show '   $p_num         = ' $p_num
    show '   $region        = ' $region
    show '   $pharm_regions = ' $pharm_regions
    show '   $pos           = ' #pos
  #endif
 
        if $process_rx = 'Y'		!AXL 10/1/2003 From SQC
           !Do Evaluate-RX-Regions	AXL 07/20/2005 No longer needed
	   do pharmacy-groups
        end-if

FROM PS_JOB J
 WHERE J.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_JOB
        WHERE J.EMPLID = EMPLID
          AND J.EMPL_RCD = EMPL_RCD
          AND EFFDT <= $WHERE_SELECT_ASOFDATE)
    AND J.EFFSEQ =
        (SELECT MAX(EFFSEQ) FROM PS_JOB
        WHERE J.EMPLID = EMPLID
          AND J.EMPL_RCD = EMPL_RCD
          AND J.EFFDT = EFFDT)
    !AXL 07/20/2005 Let cmpy/paygroup/dpt come from main select - Begin
    AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
    AND [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
    AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
    !AXL 07/20/2005 Let cmpy/paygroup/dpt come from main select - End

end-select
  
end-procedure

!*********************************************************************
begin-procedure pharmacy-groups
!*********************************************************************
  let $sql-statement = 'pharmacy-groups'
  let $data_found_flag ='N'

begin-select

j2.company
j2.paygroup
j2.emplid
j2.empl_rcd
J2.deptid
pd.name
j2.jobcode
j2.full_part_time
j2.std_hours
j2.Position_nbr    !VENDTKK    ITG#56786   11-Feb-2009
        !show &j2.company ' ' &j2.paygroup ' ' &j2.emplid ' ' &j2.empl_rcd ' ' &J2.deptid

        let $data_found_flag ='Y'

        let $emplid 		= &j2.emplid
	let #empl_rcd_nbr 	= &j2.empl_rcd
        let $JobCode		= &j2.jobcode

        If &j2.full_part_time ='F'
           Let $Work_Sts ='FT'
        Else
           Let $Work_sts ='PT'
        End-If

        Do Get-Job-Title
        
        !VENDTKK Begin   ITG#56786   11-Feb-2009
  
         Let $Position_Nbr = &j2.Position_nbr
 	 Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosNameAbbrv = '' 
  		Let $JobTitleabbrv = $JobTitleabbrv
	 Else
 		Let $JobTitleabbrv = $PosNameAbbrv
	 End-if
         !VENDTKK End   ITG#56786   11-Feb-2009	

        Do Get-Prem-Amts
        Do Get-Home-Store-Amts
        do Insert-Home-Info
        Do Get-Other-Store-Amts

  FROM PS_JOB J2,
       ps_personal_data pd,
       ps_gex_dept_tbl gdt

  WHERE pd.emplid = j2.emplid
    AND J2.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_JOB
        WHERE J2.EMPLID = EMPLID
          AND J2.EMPL_RCD = EMPL_RCD
          AND EFFDT <= $WHERE_SELECT_ASOFDATE)
    AND J2.EFFSEQ =
        (SELECT MAX(EFFSEQ) FROM PS_JOB
        WHERE J2.EMPLID = EMPLID
          AND J2.EMPL_RCD = EMPL_RCD
          AND J2.EFFDT = EFFDT)

    AND j2.deptid 		  = $deptid

    AND gdt.deptid                = j2.deptid
    AND gdt.effdt                 =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid             = j2.deptid
       And    effdt             <= $WHERE_SELECT_ASOFDATE)
    AND gdt.sequence_number       =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid             = gdt.deptid
       And    effdt              = gdt.effdt)

    !AXL 07/20/2005 Let cmpy/paygroup/dpt come from main select - Begin
    AND   J2.COMPANY  = &j.company
    AND   J2.PAYGROUP = &j.paygroup
    AND   J2.DEPTID   = &j.deptid
    !AXL 07/20/2005 Let cmpy/paygroup/dpt come from main select - End

    And   [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
    And   [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
    And   [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
  
end-select
  
end-procedure

!***********************************************************************
Begin-Procedure Get-Prem-Amts
!***********************************************************************
    
Begin-Select
sum(poeprm.oth_earns) &poeprm.oth_earns
sum(poeprm.oth_hrs)   &poeprm.oth_hrs		!AXL 1/12/2005 New

  let #prem_amt = 	&poeprm.oth_earns
  let #prem_hrs = 	&poeprm.oth_hrs		!AXL 01/12/2005

From   ps_pay_earnings peprm, ps_pay_oth_earns poeprm
Where  poeprm.company               = peprm.company
And    poeprm.paygroup              = peprm.paygroup
And    poeprm.pay_end_dt            = peprm.pay_end_dt
And    poeprm.off_cycle             = peprm.off_cycle
And    poeprm.page_num              = peprm.page_num
And    poeprm.line_num              = peprm.line_num
And    poeprm.addl_nbr              = peprm.addl_nbr
And    peprm.pay_end_dt             = $WHERE_SELECT_ASOFDATE
And    peprm.emplid                 = $emplid
And    peprm.empl_rcd               = #empl_rcd_nbr
!And    poeprm.erncd                 = '167'                     !GBD 04/08/2003
And    poeprm.erncd                IN ('164','167')              !GBD 04/08/2003 Erncd Added
And    peprm.deptid	            = &j2.deptid

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Get-Home-Store-Amts
!***********************************************************************
  
Begin-Select
poe.erncd
sum(poe.oth_hrs)  &poe.oth_hrs   

  let $erncd = 		&poe.erncd

  Evaluate $Erncd
    When = 'REG'
	add &poe.oth_hrs to #RegHrs
        break
    When = 'OTP'
	add &poe.oth_hrs to #OTPHrs
        break
    !SXP ADDED FOR ITG #51358-BEGIN
    When = '173'
	add &poe.oth_hrs to #173Hrs
        break
    !SXP ADDED FOR ITG #51358-END
    !AXL 12/22/2000 Split SICK & DISABILITY into separate columns - Begin
    When = '501'
        add &poe.oth_hrs to #SickHrs
	break
    When = '516'
    when = '526'  !isdvmxr 07/27/2007  ITG # 42710 
  
        add &poe.oth_hrs to #DisHrs
	break
    !AXL 12/22/2000 Split SICK & DISABILITY into separate columns - End
    When = '140'
    When = '141'
    When = '142'
    When = '143'
    When = '144'
    When = '145'
    When = '146'
    !AXL 11/15/2000 New Trng Codes - Begin
    When = '147'
    When = '148'
    When = '149'
    !AXL 11/15/2000 New Trng Codes - End
    When = '14A'  	!AXL 02/19/2002 New Trng Code Add
    When = '150'
    When = '151'
    When = '152'
    When = '153'
    When = '154'
    When = '155'
    When = '156'
        add &poe.oth_hrs to #TrngHrs
	break
    When = '513'
    When = '519'                        !COD mod, MRP, 01/09/2001
    When = '522'                        !COD mod, MRP, 01/09/2001
    When = '523'                        !COD mod, MRP, 01/09/2001
        add &poe.oth_hrs to #VacHrs
	break
    When = 'HOL'
    When = '502'
        add &poe.oth_hrs to #HolHrs
        break
   End-Evaluate

From   ps_pay_earnings           pe,
       ps_pay_oth_earns          poe
Where  poe.company               = pe.company
And    poe.paygroup              = pe.paygroup
And    poe.pay_end_dt            = pe.pay_end_dt
And    poe.off_cycle             = pe.off_cycle
And    poe.page_num                 = pe.page_num
And    poe.line_num                 = pe.line_num
And    poe.addl_nbr                 = pe.addl_nbr
And    pe.pay_end_dt             = $WHERE_SELECT_ASOFDATE
And    pe.emplid                 = $emplid
And    pe.empl_rcd              = #empl_rcd_nbr
And    pe.deptid                 = &J2.deptid
Group By poe.erncd

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Get-Other-Store-Amts
!***********************************************************************
  let $hold= ' '
  let #dept_count = 1
Begin-Select
pe2.deptid  
pothe.erncd
sum(pothe.oth_hrs)  	&pothe.oth_hrs
 
  let $erncd = 		&pothe.erncd
  let $Oth_Deptid=	&pe2.deptid

  Do Get-Other-Prem-Amts
  
  ! AXL - Keep the home store #1 but increment all other 
  !       stores worked at by one

  if  $Oth_Deptid <> $hold
      add 1 to #dept_count
      let $hold =  $Oth_Deptid
  end-if
  
  Evaluate $Erncd
    When = 'REG'
	add &pothe.oth_hrs to #Oth_RegHrs
        break
    When = 'OTP'
	add &pothe.oth_hrs to #Oth_OTPHrs
        break
    !SXP ADDED FOR ITG #51358-BEGIN
    When = '173'
	add &pothe.oth_hrs to #Oth_173Hrs
        break
    !SXP ADDED FOR ITG #51358-END
    !AXL 12/22/2000 Split SICK & DISABILITY into separate columns - Begin
    When = '501'
        add &pothe.oth_hrs to #Oth_SickHrs
	break
    When = '516' 
    when = '526'  !isdvmxr 07/27/2007  ITG # 42710 
  
        add &pothe.oth_hrs to #Oth_DisHrs
	break
    !AXL 12/22/2000 Split SICK & DISABILITY into separate columns - End
    When = '140'
    When = '141'
    When = '142'
    When = '143'
    When = '144'
    When = '145'
    When = '146'
    !AXL 11/15/2000 New Trng Codes - Begin
    When = '147'
    When = '148'
    When = '149'
    !AXL 11/15/2000 New Trng Codes - End
    When = '14A'  	!AXL 02/19/2002 New Trng Code Add
    When = '150'
    When = '151'
    When = '152'
    When = '153'
    When = '154'
    When = '155'
    When = '156'
        add &pothe.oth_hrs to #Oth_TrngHrs
	break
    When = '513'
    When = '519'                        !COD mod, MRP, 01/09/2001
    When = '522'                        !COD mod, MRP, 01/09/2001
    When = '523'                        !COD mod, MRP, 01/09/2001
        add &pothe.oth_hrs to #Oth_VacHrs
	break
    When = 'HOL'
    When = '502'
        add &pothe.oth_hrs to #Oth_HolHrs
        break
   End-Evaluate
   
   Let #Other_Hold=(#Oth_RegHrs+#Oth_OTPHrs+#Oth_173Hrs+#Oth_SickHrs+#Oth_DisHrs+#Oth_TrngHrs+#prem_hrs2+#prem_amt2+#Oth_VacHrs+#Oth_HolHrs)
   If #Other_Hold <> 0
     do insert-Other-Info
   end-if
   
From   ps_pay_earnings  pe2,
       ps_pay_oth_earns pothe
Where  pothe.company              = pe2.company
And    pothe.paygroup             = pe2.paygroup
And    pothe.pay_end_dt           = pe2.pay_end_dt
And    pothe.off_cycle            = pe2.off_cycle
And    pothe.page_num             = pe2.page_num
And    pothe.line_num             = pe2.line_num
And    pothe.addl_nbr             = pe2.addl_nbr
And    pe2.pay_end_dt             = $WHERE_SELECT_ASOFDATE
And    pe2.emplid                 = $emplid
And    pe2.empl_rcd               = #empl_rcd_nbr
And    pe2.deptid                 <> &j2.deptid          
!and    pothe.erncd           NOT IN ('164','167')	AXL 10/6/2004 - Disable
Group By PE2.Deptid, POTHE.erncd
Order By PE2.Deptid   

End-Select

 let #dept_count = 0

End-Procedure

!***********************************************************************
Begin-Procedure Get-Other-Prem-Amts
!***********************************************************************
    
Begin-Select
sum(poeprm2.oth_earns) &poeprm2.oth_earns
sum(poeprm2.oth_hrs)   &poeprm2.oth_hrs		!AXL 01/12/2005

  let #prem_amt2 = 	&poeprm2.oth_earns
  let #prem_hrs2 =      &poeprm2.oth_hrs	!AXL 01/12/2005

From   ps_pay_earnings peprm2, ps_pay_oth_earns poeprm2
Where  poeprm2.company               = peprm2.company
And    poeprm2.paygroup              = peprm2.paygroup
And    poeprm2.pay_end_dt            = peprm2.pay_end_dt
And    poeprm2.off_cycle             = peprm2.off_cycle
And    poeprm2.page_num              = peprm2.page_num
And    poeprm2.line_num              = peprm2.line_num
And    poeprm2.addl_nbr              = peprm2.addl_nbr
And    peprm2.pay_end_dt             = $WHERE_SELECT_ASOFDATE
And    peprm2.emplid                 = $emplid
And    peprm2.empl_rcd               = #empl_rcd_nbr
!And    poeprm2.erncd                 = '167'                  !GBD 04/08/2003
And    poeprm2.erncd                IN ('164','167')           !GBD 04/08/2003 Erncd Added
And    peprm2.deptid	             = $oth_deptid

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Insert-Home-Info
!***********************************************************************
Begin-SQL  
  Insert Into ps_gex_r_gexpy097
    Values ($region,
	    $resp_mgr,
	    $deptid,
            $EMPLID,
            $deptid,
            1,
            &pd.name,
            $JobTitleAbbrv,
            $work_sts,
            &j2.std_hours,
            #prem_amt,
	    #prem_hrs,		!AXL 01/12/2005
	    #RegHrs,    
            #OTPHrs,   
	    #173Hrs,            !SXP ITG #51358 23/07/2008
            #TrngHrs,  
            #VacHrs,   
            #HolHrs,
           !AXL - 12/22/2000 New Columns - Begin
            #SickHrs,		
            #DisHrs)	
           !AXL - 12/22/2000 New Columns - End
End-SQL

  let #prem_amt = 0
  let #prem_hrs = 0		!AXL - 01/12/2004 New Columns
  Let #RegHrs  	= 0
  Let #OTPHrs  	= 0
  Let #173Hrs 	= 0             !SXP ITG #51358 23/07/2008
  Let #TrngHrs 	= 0
  Let #VacHrs 	= 0
  Let #HolHrs  	= 0
  Let #SickHrs 	= 0		!AXL - 12/22/2000 New Columns 
  Let #DisHrs  	= 0		!AXL - 12/22/2000 New Columns 

End-Procedure

!***********************************************************************
Begin-Procedure Insert-Other-Info
!***********************************************************************
            
Begin-SQL  
  Insert Into ps_gex_r_gexpy097
    Values ($region,
            $resp_mgr,
	    &j2.deptid,
            $EMPLID,
            $Oth_deptid,
            #dept_count, 
            &pd.name,
            ' ',
            ' ',
            0,
            #prem_amt2,
	    #prem_hrs2,		!AXL 01/12/2005
	    #Oth_RegHrs,    
            #Oth_OTPHrs,        !SXP 23/07/2008
	    #Oth_173Hrs,   
            #Oth_TrngHrs,  
            #Oth_VacHrs,   
            #Oth_HolHrs,
           !AXL - 12/22/2000 New Columns - Begin
            #Oth_SickHrs,		
            #Oth_DisHrs)	
           !AXL - 12/22/2000 New Columns - End
End-SQL

  Let #Oth_RegHrs  	= 0
  Let #Oth_OTPHrs  	= 0
  Let #Oth_173Hrs  	= 0      !SXP  23/07/2008
  Let #Oth_TrngHrs 	= 0
  Let #Oth_VacHrs 	= 0
  Let #Oth_HolHrs  	= 0
  Let #Oth_SickHrs 	= 0		!AXL - 12/22/2000 New Columns 
  Let #Oth_DisHrs 	= 0		!AXL - 12/22/2000 New Columns 
  Let #prem_amt2	= 0
  let #prem_hrs2        = 0		!AXL 01/12/2005

End-Procedure

!*********************************************************************
begin-procedure Create-Out-File
!*********************************************************************
  let $sql-statement = 'Create-Out-File'

begin-select

TMP.TREE_NODE  
TMP.LAST_NAME                  
TMP.DEPTID                 
TMP.EMPLID                 
TMP.GEX_DEPTID_OVR 
TMP.GEX_NUM_ROWS
TMP.NAME
TMP.DESCRSHORT     
TMP.WORKGROUP      
TMP.STD_HOURS      
sum(TMP.PREMIUM_RATE) 	&TMP_PREMIUM_RATE
sum(TMP.GEX_HRS_PREM)	&TMP_GEX_HRS_PREM	!AXL 01/12/2005
sum(TMP.GEX_REG_HRS)	&TMP_GEX_REG_HRS
sum(TMP.GEX_OTP_HRS)	&TMP_GEX_OTP_HRS
sum(TMP.GEX_173_HRS)	&TMP_GEX_173_HRS         !SXP 23/07/2008
sum(TMP.GEX_TRNG_HRS)  	&TMP_GEX_TRNG_HRS         
sum(TMP.GEX_VACN_HRS)	&TMP_GEX_VACN_HRS
sum(TMP.GEX_HOL_HRS)	&TMP_GEX_HOL_HRS
 !AXL - 12/22/2000 New Columns - Begin
sum(TMP.GEX_SICK_HRS)	&TMP_GEX_SICK_HRS
sum(TMP.GEX_DIS_HRS)	&TMP_GEX_DIS_HRS
 !AXL - 12/22/2000 New Columns - End

  	Do Write-Record

FROM PS_GEX_R_GEXPY097 TMP

group by TMP.LAST_NAME,TMP.TREE_NODE,TMP.DEPTID,TMP.EMPLID,
         GEX_NUM_ROWS,TMP.GEX_DEPTID_OVR,TMP.NAME,
         TMP.DESCRSHORT,TMP.WORKGROUP,TMP.STD_HOURS  

order by TMP.LAST_NAME,TMP.TREE_NODE,TMP.DEPTID,TMP.EMPLID,
         GEX_NUM_ROWS,TMP.GEX_DEPTID_OVR
   

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Write-Record
!***********************************************************************

  !AXL 1/12/2005 Format field for proper importing into excel
  let $deptid =  '="' || &TMP.DEPTID || '"'
  let $emplid =  '="' || &TMP.EMPLID || '"'
  let $gex_deptid_ovr =  '="' || &TMP.GEX_DEPTID_OVR || '"'
  
  let $prem_rate = edit(&TMP_PREMIUM_RATE,'9999.99')
  let $prem_hrs  = edit(&TMP_GEX_HRS_PREM,'99.99')   !AXL 01/12/2005
  let $reg_hrs   = edit(&TMP_GEX_REG_HRS,'99.99')   
  let $otp_hrs   = edit(&TMP_GEX_OTP_HRS,'99.99')  
  let $173_hrs  = edit(&TMP_GEX_173_HRS,'99.99')       !SXP ITG #51358 23/07/2008
  let $trng_hrs  = edit(&TMP_GEX_TRNG_HRS,'99.99')  
  let $vacn_hrs  = edit(&TMP_GEX_VACN_HRS,'99.99')   
  let $hol_hrs   = edit(&TMP_GEX_HOL_HRS,'99.99')   
 !AXL - 12/22/2000 New Columns - Begin
  let $sick_hrs  = edit(&TMP_GEX_SICK_HRS,'99.99') 
  let $dis_hrs   = edit(&TMP_GEX_DIS_HRS,'99.99') 
 !AXL - 12/22/2000 New Columns - End

  !AXL 01/12/2005 New Write methodology

  Move '' To $all_cols
   string &TMP.LAST_NAME &TMP.TREE_NODE $DEPTID $EMPLID
          $GEX_DEPTID_OVR &TMP.GEX_NUM_ROWS &TMP.NAME &TMP.DESCRSHORT
          &TMP.WORKGROUP &TMP.STD_HOURS $prem_rate $prem_hrs
          $REG_HRS $OTP_HRS $173_HRS $TRNG_HRS $VACN_HRS            !23/07/2008
          $HOL_HRS $SICK_HRS $DIS_HRS $WHERE_SELECT_ASOFDATE
          $AsOfToday by $tab into $all_cols

   write 1 from $all_cols Status=#filestat
               
   If #filestat <> 0
     Show 'Error writing output file1.  Program terminating.'
     Stop
   End-If

!       write 1 from &TMP.LAST_NAME:20
!               &TMP.TREE_NODE:10
!               &TMP.DEPTID:10
!               &TMP.EMPLID:11        
!               &TMP.GEX_DEPTID_OVR:6
!               &TMP.GEX_NUM_ROWS:4
!               &TMP.NAME:30           
!               &TMP.DESCRSHORT:15     
!               &TMP.WORKGROUP:4      
!               &TMP.STD_HOURS:5      
!               $prem_rate:8   
!	       $prem_hrs:8
!               $REG_HRS:8    
!               $OTP_HRS:8    
!               $TRNG_HRS:8   
!               $VACN_HRS:8   
!               $HOL_HRS:8  
              !AXL - 12/22/2000 New Columns - Begin
!               $SICK_HRS:8   
!               $DIS_HRS:8 
              !AXL - 12/22/2000 New Columns - End  
!               $WHERE_SELECT_ASOFDATE:12  
!               $AsOfToday:12
              
End-Procedure

!***********************************************************************
Begin-Procedure EOJ-Process
!***********************************************************************

  If $Data-Found-Flag = 'N'
    show 'NO DATA SELECTED FOR THIS REPORT RUN'	
  End-If

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  close 1

End-Procedure

#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'askaod.sqc'    !Ask for As-Of-Date parameter
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#include 'getjobtl.sqc'  !Get Job Title Descr 
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx950.sqc'  !Get SQRW run controls
#include 'gexxx939.sqc'  !Get RX Mgr names per RX Region
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   11-Feb-2009
