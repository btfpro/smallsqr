!***********************************************************************
! GEXPY146.SQR:                                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-2006 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY146.SQR :                                                       *
!                                                                      *
! Narrative:            Convert SQL written for Mark M. to SQR for more*
!			flexibility and in order to be scheduled       *
!			monthly.  This output needs to be delivered to *
!			him in excel format, so he can manipulate the  *
!			raw data as he pleases.                        *	 
!                       ITG-34999                                      *
!								       *
! #Debug Used:          x                                              *
!								       *
! SQL Tables:           PS_JOB, PS_GEX_DEPT_TBL, PS_PERSONAL_DATA,     *
!			PS_DEPT_TBL, PS_JOBCODE_TBL, PS_DEPARTMENT_TBL *
!			PS_DEPARTMENT_TBL is a view of the PS_DEPT_TBL *
!			in the finance db link by a dblink             *
!                                                                      *
!								       *
! Written by:           Rapidigm, Deepak, 04/28/06                     *
!                                                                      *
! Normally Run:         This report needs to be run monthly, scheduled *
!			to run on the 1st of every month, with the     *
!			output (FILE) being sent to Mark M.            *
!                                                                      *
! Control Parms:        Read run control values from PS_GEX_RUN_CNTL   *
!								       *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! ISDVNPK  09/25/2006  Commented out the Comprate field from printing  *
!                      in the report                                   *
!                                                                      *
! MXT      08/24/2007   Use GEX_GL_DEPT_VW in place of DEPARTMENT_TBL. *
!ISDVDZK   02/23/2010   Done the changes for ITG 73418--Begin          *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup02.sqc'   !Printer and Page-Size Initialization (Landscape)

#define 	#column1 	001
#define 	#column2 	015
#define 	#column3 	028
#define 	#column4 	040
#define 	#column5 	050
#define 	#column6 	060
#define 	#column7 	070
#define 	#column8 	080
#define 	#column9 	090
#define 	#column10 	100
#define 	#column11	110
!#define 	#column12	120  ! ISDVNPK  09/25/2006 Commented this

!***********************************************************************
! Defines Heading of the report
!***********************************************************************

Begin-Report

    print 'GEXPY146' (1,1)
    Print 'Compensation Report by GL Department'     (,10) center

    Print 'DEPTID'     		(+1,{#column1})
    Print 'DESCR'		(+0,{#column2})
    Print 'EMPLID'     		(+0,{#column3})
    Print 'NAME'    		(+0,{#column4})
    Print 'DESCR'    		(+0,{#column5})
    Print 'GRADE'    		(+0,{#column6})
    Print 'STD_HOURS'    	(+0,{#column7})
    Print 'BUSINESS_UNIT'    	(+0,{#column8})
    Print 'GL_DEPARTMENT'    	(+0,{#column9})
    Print 'GL_DEPT_DESCR'  	(+0,{#column10})
    Print 'GEX_DEPTID_OVR' 	(+0,{#column11})
   ! Print 'COMPRATE'  	 	(+0,{#column12})    ! ISDVNPK  09/25/2006 Commented this


  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Stdapi-Init             !stdapi.sqc
   
  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = 'gexpy146'
  end-if
  
  do Get-Run-Control	     !gexrnctl.sqc  

  if ltrim(rtrim(&asofdate,' '),' ') = ''
	let $asofdate = $current-date
  else
	let $asofdate = &asofdate
  end-if

  #debugx show '<' $asofdate '>'
  
  do Build-Dynamic-Where
  do Select-Data
  do stdapi-term

End-Report


!***********************************************************************
! Builds the where clause for DEPTID
!***********************************************************************
Begin-Procedure Build-Dynamic-Where

	Let $GEXXX902_Deptid_Alias = 'B.DEPARTMENT'
	Do GEXXX902-Select-Deptid-Parameters

	Let $Deptid_Alias = 'B.DEPARTMENT'
	Let $BusUnit_Alias = 'B.BUSINESS_UNIT'
	Do Select-Deptid-BusUnit-Parameters
	
	#debugx show $GEXXX902_INCLUDE_DEPTID_CRITERIA 
	#debugx show $Deptid_BU_Criteria 
End-Procedure


!***********************************************************************
! When business unit is specified on the run control page, need to select 
! only those business units within that department.
!***********************************************************************
Begin-Procedure Select-Deptid-BusUnit-Parameters
	let #GEX_ROW = 0 
	let $first = 'Y'

	Let $OprID         = '''' || $Prcs_OprID       || ''''
	Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''


begin-select distinct 
a.department		&department
a.business_unit		&business_unit
	
	if $first = 'Y'
		move &department to $old_department
		let $first = 'N'
	end-if

	if &department <> $old_department
		Let $Deptid_BU_Criteria = $Deptid_BU_Criteria || ')) OR '
		let #GEX_ROW = 0 
	end-if

	Add 1 To #GEX_ROW

	If #GEX_ROW = 1
		let $Deptid_BU_Criteria = $Deptid_BU_Criteria  || '(' || $Deptid_Alias || ' = ''' || &department || '''' || ' AND ' || $BusUnit_Alias || ' In ('''  || &business_unit || '''' 
	else
		let $Deptid_BU_Criteria = $Deptid_BU_Criteria || ',''' || &business_unit  || ''''
	end-if

from ps_gex_rc_dpt_unit a
where a.oprid = [$OprID]
and a.run_cntl_id = [$Run_Cntl_ID]
order by a.department
end-select

	If #GEX_ROW = 0
	    Let $Deptid_BU_Criteria = '1=0'
	else
	    Let $Deptid_BU_Criteria = $Deptid_BU_Criteria || '))'
	end-if

End-Procedure 

!***********************************************************************
! Main Procedure which selects the data.
!***********************************************************************
Begin-Procedure Select-Data
Begin-Select
A.DEPTID		&deptid
D.DESCR			&descr
A.EMPLID		&emplid
C.NAME			&name
E.DESCR			&jobdescr
A.GRADE			&grade
A.STD_HOURS		&std_hours
B.BUSINESS_UNIT		&bus_unit
B.DEPARTMENT		&gl_deptid
FS_D.DESCR		&gl_descr
A.GEX_DEPTID_OVR	&gex_deptid_ovr
a.COMPRATE		&comprate
A.Position_nbr              !ISDVDZK Done the changes for ITG 73418

	move &deptid to $deptid
	let $deptid = '="' || edit($deptid,'0000') || '"'
	move &bus_unit to $bus_unit
	let $bus_unit = '="' || edit($bus_unit,'00000') || '"'
	move &gl_deptid to $gl_deptid
	let $gl_deptid = '="' || edit($gl_deptid,'0000') || '"'
	
	 !ISDVDZK Done the changes for ITG 73418--Begin
     Let $Position_Nbr = &A.Position_nbr
 	 Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosName = '' 
  		Let $jobtitle = &jobdescr
	 Else
 		Let $jobtitle = $PosName
	 End-if
    !ISDVDZK Done the changes for ITG 73418--End

	
	do Print-Data

FROM PS_JOB A,  PS_GEX_DEPT_TBL B, PS_PERSONAL_DATA C, PS_DEPT_TBL D, PS_JOBCODE_TBL E,
!PS_DEPARTMENT_TBL FS_D  !Manish, PY214, Giant Eagle Modification
PS_GEX_GL_DEPT_VW  FS_D	 !Manish, PY214, Giant Eagle Modification
WHERE A.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_JOB A_ED
        WHERE A.EMPLID = A_ED.EMPLID
          AND A.EMPL_RCD = A_ED.EMPL_RCD
          AND A_ED.EFFDT <= $asofdate)
AND A.EFFSEQ =
        (SELECT MAX(A_ES.EFFSEQ) FROM PS_JOB A_ES
        WHERE A.EMPLID = A_ES.EMPLID
          AND A.EMPL_RCD = A_ES.EMPL_RCD
          AND A.EFFDT = A_ES.EFFDT)
AND B.EFFDT =
        (SELECT MAX(B_ED.EFFDT) FROM PS_GEX_DEPT_TBL B_ED
        WHERE B.SETID = B_ED.SETID
          AND B.DEPTID = B_ED.DEPTID
          AND B_ED.EFFDT <= $asofdate)
AND E.SETID = A.SETID_JOBCODE
AND E.JOBCODE = A.JOBCODE
AND E.EFFDT =
        (SELECT MAX(E_ED.EFFDT) FROM PS_JOBCODE_TBL E_ED
        WHERE E.SETID = E_ED.SETID
          AND E.JOBCODE = E_ED.JOBCODE
          AND E_ED.EFFDT <= $asofdate)
AND (
    (B.DEPTID = A.GEX_DEPTID_OVR  AND A.GEX_DEPTID_OVR <> ' ') 
 OR (B.DEPTID = A.DEPTID  AND A.GEX_DEPTID_OVR = ' ')
     )
AND A.EMPLID = C.EMPLID
AND A.DEPTID = D.DEPTID
AND D.EFFDT =
        (SELECT MAX(D_ED.EFFDT) FROM PS_DEPT_TBL D_ED
        WHERE D.SETID = D_ED.SETID
          AND D.DEPTID = D_ED.DEPTID
          AND D_ED.EFFDT <= $asofdate)
AND A.EMPL_STATUS IN ('A','L','P','S')
AND ( [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
  OR  [$Deptid_BU_Criteria]			! (B.DEPARTMENT = '0000' AND B.BUSINESS_UNIT IN ('05435', '05436'))
    )
!AND B.DEPARTMENT = FS_D.DEPTID 	!Manish, PY214, Giant Eagle Modification
AND B.DEPARTMENT = FS_D.DEPARTMENT 	!Manish, PY214, Giant Eagle Modification

AND FS_D.EFFDT = 
!Manish, PY214, Giant Eagle Modification-Begin
!	(SELECT MAX(FS_DD.EFFDT) FROM PS_DEPARTMENT_TBL FS_DD
!	WHERE FS_DD.DEPTID = FS_D.DEPTID
	(SELECT MAX(FS_DD.EFFDT) FROM PS_GEX_GL_DEPT_VW FS_DD
	WHERE FS_DD.DEPARTMENT = FS_D.DEPARTMENT
!Manish, PY214, Giant Eagle Modification-End
	AND FS_DD.EFFDT <= $asofdate)
AND FS_D.EFF_STATUS  = 'A'
End-Select
End-Procedure


!***********************************************************************
! Prints Data
!***********************************************************************
Begin-Procedure Print-Data	
	
    Print $deptid     		(+1,{#column1})
    Print &descr		(+0,{#column2})
    Print &emplid     		(+0,{#column3})
    Print &name   		(+0,{#column4})
    !Print &jobdescr    		(+0,{#column5})
    Print  $jobtitle    		(+0,{#column5}) !ISDVDZK Done the changes for ITG 73418
    Print &grade    		(+0,{#column6}) 
    Print &std_hours    	(+0,{#column7})
    Print $bus_unit    		(+0,{#column8})
    Print $gl_deptid    	(+0,{#column9})
    Print &gl_descr  		(+0,{#column10})
    Print &gex_deptid_ovr 	(+0,{#column11})
    ! Print &comprate  	 	(+0,{#column12})       ! ISDVNPK  09/25/2006 Commented this
 
End-Procedure 


#include 'stdapi.sqc'    !Process Scheduler Interface
#include 'reset.sqc'     !Reset Printer Procedure
#include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date math
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#include 'gexrnctl.sqc'  !Get Asofdate from Run Control Page
#include 'gexxx902.sqc'  !Get Deptid from Run Control Page
#include 'gexxx936.sqc'  !Get Business_Unit from Run Control Page
#include 'getposnm.sqc'  !Get postion title from PS_POSITION_DATA 
