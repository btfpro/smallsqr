!**************************************************************************
!  GEXXX912:  Retrieve "Multiple" Benefit Plan Run Controls and           *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX912_Plan_Type_Alias                                            *
!    $GEXXX912_Benefit_Plan_Alias                                         *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA                              *
!    $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA                              *
!    $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA                      *
!    $GEXXX912_EXCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA                      *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX912_Plan_Type_Alias        = 'hb.plan_type'                *
!    Let $GEXXX912_Benefit_Plan_Alias     = 'hb.benefit_plan'             *
!    Do GEXXX912-Select-Benefit-Plan-Parameters                           *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    From ps_health_benefit hb                                            *
!    Where [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA]                      *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		05/28/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX912-Select-Benefit-Plan-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_BEN_PLAN_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = ''
  Let $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA = ''
  Let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX912_EXCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_BEN_PLAN.PLAN_TYPE
GEX_RC_BEN_PLAN.BENEFIT_PLAN

  Add 1 To #GEX_RC_BEN_PLAN_ROWS

  If Rtrim(&GEX_RC_BEN_PLAN.BENEFIT_PLAN, ' ') = ''
    Let $Include_Condition = '(' || $GEXXX912_Plan_Type_Alias || ' = '''  || &GEX_RC_BEN_PLAN.PLAN_TYPE || ''')'
    Let $Exclude_Condition = '(' || $GEXXX912_Plan_Type_Alias || ' != ''' || &GEX_RC_BEN_PLAN.PLAN_TYPE || ''')'
  Else
    Let $Include_Condition = '(' || $GEXXX912_Plan_Type_Alias || ' = '''  || &GEX_RC_BEN_PLAN.PLAN_TYPE || ''' And ' || $GEXXX912_Benefit_Plan_Alias  || ' = '''  || &GEX_RC_BEN_PLAN.BENEFIT_PLAN || ''')'
    Let $Exclude_Condition = '(' || $GEXXX912_Plan_Type_Alias || ' != ''' || &GEX_RC_BEN_PLAN.PLAN_TYPE || ''' Or '  || $GEXXX912_Benefit_Plan_Alias  || ' != ''' || &GEX_RC_BEN_PLAN.BENEFIT_PLAN || ''')'
  End-If

  If #GEX_RC_BEN_PLAN_ROWS = 1
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '(' || $Include_Condition
    Let $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA = $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_BEN_PLAN GEX_RC_BEN_PLAN

Where GEX_RC_BEN_PLAN.OPRID                         = [$OprID]
And   GEX_RC_BEN_PLAN.RUN_CNTL_ID                   = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_BEN_PLAN.PLAN_TYPE, ' ')        != ' '
!And   Rtrim(GEX_RC_BEN_PLAN.BENEFIT_PLAN, ' ')     != ' '

End-Select

  If #GEX_RC_BEN_PLAN_ROWS = 0
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = $GEXXX912_Plan_Type_Alias     || ' = ' || $GEXXX912_Plan_Type_Alias    || ' And ' ||
                                                  $GEXXX912_Benefit_Plan_Alias  || ' = ' || $GEXXX912_Benefit_Plan_Alias
    Let $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA = $GEXXX912_Plan_Type_Alias     || ' = ' || $GEXXX912_Plan_Type_Alias    || ' And ' ||
                                                  $GEXXX912_Benefit_Plan_Alias  || ' = ' || $GEXXX912_Benefit_Plan_Alias
  Else
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA         = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA || ')'
    Let $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA         = $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA || ')'
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    Let $GEXXX912_EXCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_EXCLUDE_BENEFIT_PLAN_CRITERIA
  End-If

End-Procedure
