!***********************************************************************
! GEXBN645:  Cobra Administration Report                               *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!------------------------------------------------------------------------
! Report Name: GEXBN645.SQR - SQR Template
!-----------------------------------------------------------------------
! Description: Giant Eagle is changing the vendor used for COBRA 
! administration from HM Benefits to BCC effective January 1, 2012. The 
! new vendor will provide COBRA administrative services for Giant Eagle
! Team Members, including communicating COBRA eligibility, collecting 
! payment and carrier notifications. Giant Eagle is committed to 
! transmitting information for Team Members newly eligible for COBRA 
! to the new vendor electronically. This program will generate the file 
! of newly eligible COBRA participants for the vendor. This will run 
! weekly and generate a file which will be encrypted and then FTP’d to 
! the vendor. In addition to the file, the program will generate an 
! error/exception report. 
!-----------------------------------------------------------------------
! GEX Modifications
!-----------------------------------------------------------------------
! GEXBN_848_D103104_01 2011-05-17 Vahini Katta
! New Development
! GEXBN_848_D103104_02 2011-10-06 Vahini Katta
! Report coverage begin dt and not event dt on the file
! GEXBN_848_D103104_03 2011-10-11 Vahini Katta
! Join the coverage elect dt while checking the event class
! GEXBN_848_D103104_04 2011-10-27 Vahini Katta
! Assign coverage begin dt -1 to the event date
! GEXBN_848_D168867_01 2014-04-22 Vahini Katta
! Change the file Path
! GEX_SEC_REENG 2016-05-27 Vahini Katta                                 
! Changes related to gex_emplid_mapp/7 digit emplid                     
! Ujwal Dyasani		  26-Jan-2017		Updated "psmail" command to    *
!										Linux after PT852 upgrade	   *
!-----------------------------------------------------------------------

#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Portrait)

Begin-Report
#debug9 Show 'Begin-Report'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc
  
  show ' '
  display 'Start Run Time: ' noline
  show $AsofNow
  Do Initialization
  Do Open-File
  Do Write_Header
 
  Do Get-Pay-End-Dt
  Do Delete-Temp-Record
  Do Report-Lost-Covrg
  Do Report-Lost-Dep-Cov
  Do Report-Life-Ins-Terms
  Do Close-File
  Do Send-Email
  
  show ' '
  display 'End Run Time: ' noline
  show $AsofNow
  do Stdapi-Term
    
End-Report

!-----------------------------------------------------------------------
! Function:    Initialization                                          -
! Description: Initialization                                          -
! Called By:   Report-Lost-Covrg                                       -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Initialization
#debug9 Show 'Initialization'
   
  Let $ReportID      = 'GEXBN645'
  Let $ReportTitle   = 'Cobra Interface'
  Let $delim = ','

  MOVE $CURRENT-DATE TO $CURRDT 'YYYYMMDD'
  
  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show '$ReportDate  = ' $ReportDate
  Show '$ReportTime  = ' $ReportTime
    
  If $prcs_process_instance = ''
    Do SQRW-Run-Controls
  Else
    Do Process-Scheduler-Run-Controls
  End-if
 
  If isnull($AsofDate)
     Let $AsofDate = $AsOfToday
  End-If
  
  show '$SlctPlanCriteria   :' $SlctPlanCriteria
  show '$SlctPlanCriteria1  :' $SlctPlanCriteria1
  show '$SlctPlanCriteria2  :' $SlctPlanCriteria2
  show '$SlctBenCriteria    :' $SlctBenCriteria
  show '$SlctBenCriteria1   :' $SlctBenCriteria1
  show '$SlctBenCriteria2   :' $SlctBenCriteria2
  show '$AsofDate           :' $AsofDate
  
End-Procedure

!-----------------------------------------------------------------------
! Function:    SQRW-Run-Controls                                       -
! Description: SQRW-Run-Controls                                       -
! Called By:   Initialization                                          -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure SQRW-Run-Controls
#debug9 Show 'SQRW-Run-Controls'

  Do Ask-As-Of-Date
 
  input $BenCriteria maxlen=100 type=char 'Enter the Benefit Program enclosed in quotes. Multiple Ben Program should be separated by commas'
  
  input $PlanCriteria maxlen=100 type=char 'Enter the  Plan type enclosed in quotes. Multiple Ben Program should be separated by commas'
  
  show '$BenCriteria :' $BenCriteria
  show '$PlanCriteria:' $PlanCriteria
  
  if isnull($BenCriteria)
    move '' to $SlctBenCriteria
    move '' to $SlctBenCriteria1
    move '' to $SlctBenCriteria2
  else
    let $SlctBenCriteria  = 'AND BAS.BENEFIT_PROGRAM IN (' || $BenCriteria || ')' 
    let $SlctBenCriteria1 = 'AND BO.BENEFIT_PROGRAM IN (' || $BenCriteria || ')' 
    let $SlctBenCriteria2 = 'AND BO.BENEFIT_PROGRAM IN (' || $BenCriteria || ')'
  end-if 
  
  if isnull($PlanCriteria)
    move '' to $SlctPlanCriteria
    move '' to $SlctPlanCriteria1
    move '' to $SlctPlanCriteria2
  else
    let $SlctPlanCriteria  = 'AND HB1.PLAN_TYPE IN (' || $PlanCriteria || ')'
    let $SlctPlanCriteria1 = 'AND FB1.PLAN_TYPE IN (' || $PlanCriteria || ')' 
    let $SlctPlanCriteria2 = 'AND HB3.PLAN_TYPE IN (' || $PlanCriteria || ')'
  end-if 
  
End-Procedure

!-----------------------------------------------------------------------
! Function:    Process-Scheduler-Run-Controls                          -
! Description: Select Values from Run Control                          -
! Called By:   Initialization                                          -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Process-Scheduler-Run-Controls
#debugd show 'Process-Scheduler-Run-Controls'

 show '$prcs_run_cntl_id = ' $prcs_run_cntl_id
 show '$prcs_oprid       = ' $prcs_oprid
 Do GEXRCBN1-SELECT-PARAMETERS

 Let $AsOfDate = &GEX_RC_BEN.AsOfDate

 move '' to $BenCriteria
 move '' to $PlanCriteria
 
BEGIN-SELECT
RB.BENEFIT_PROGRAM

    if isnull($BenCriteria)
       let $BenCriteria = $BenCriteria ||''''|| &RB.BENEFIT_PROGRAM || ''''
    else
       let $BenCriteria = $BenCriteria ||','||''''|| &RB.BENEFIT_PROGRAM || ''''
    end-if

FROM PS_GEX_RUN_BENPROG RB
WHERE RB.oprid       = $prcs_oprid
  AND RB.run_cntl_id = $prcs_run_cntl_id
END-SELECT

BEGIN-SELECT
RP.PLAN_TYPE

    if isnull($PlanCriteria)
       let $PlanCriteria = $PlanCriteria ||''''|| &RP.PLAN_TYPE || ''''
    else
       let $PlanCriteria = $PlanCriteria ||','||''''|| &RP.PLAN_TYPE || ''''
    end-if

FROM PS_GEX_RUN_PLNTYPE RP
WHERE RP.oprid       = $prcs_oprid
  AND RP.run_cntl_id = $prcs_run_cntl_id
END-SELECT

  if isnull($BenCriteria)
    move '' to $SlctBenCriteria
    move '' to $SlctBenCriteria1
    move '' to $SlctBenCriteria2
  else
    let $SlctBenCriteria  = 'AND BAS.BENEFIT_PROGRAM IN (' || $BenCriteria || ')' 
    let $SlctBenCriteria1 = 'AND BO.BENEFIT_PROGRAM IN (' || $BenCriteria || ')' 
    let $SlctBenCriteria2 = 'AND BO.BENEFIT_PROGRAM IN (' || $BenCriteria || ')'
  end-if 
  
  if isnull($PlanCriteria)
    move '' to $SlctPlanCriteria
    move '' to $SlctPlanCriteria1
    move '' to $SlctPlanCriteria2
  else
    let $SlctPlanCriteria  = 'AND HB1.PLAN_TYPE IN (' || $PlanCriteria || ')'
    let $SlctPlanCriteria1 = 'AND FB1.PLAN_TYPE IN (' || $PlanCriteria || ')' 
    let $SlctPlanCriteria2 = 'AND HB3.PLAN_TYPE IN (' || $PlanCriteria || ')'
  end-if 

  show ' '
  show '*** Run Control Parameters ***'
 
end-procedure Process-Scheduler-Run-Controls

!-----------------------------------------------------------------------
! Function:    Open-File                                               -
! Description: Opens Files                                             -
! Called By:   Begin_report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Open-File

  #ifdef NT   
   show '$prcs_run_cntl_id :' $prcs_run_cntl_id
   IF lower($prcs_run_cntl_id)='gexbn645a'
    ! GEXBN_848_D168867_01 2014-04-22 Vahini Katta Begins
    !Let $File       = '\\nt5\ftproot\benefits\Cobra\in\'  || 'CobraBCC_A_'|| $CURRDT ||  '.csv'
    !LET $Error_File = '\\nt5\ftproot\benefits\Cobra\in\'  || 'CobraBCC_A_'|| $CURRDT || '_error.csv' 
    Let $File       = '\\corp.gianteagle.com\ftp\benefits\Cobra\in\'  || 'CobraBCC_A_'|| $CURRDT ||  '.csv'        !testing
    LET $Error_File = '\\corp.gianteagle.com\ftp\benefits\Cobra\in\'  || 'CobraBCC_A_'|| $CURRDT || '_error.csv' 
    ! GEXBN_848_D168867_01 2014-04-22 Vahini Katta Ends
   else
    ! GEXBN_848_D168867_01 2014-04-22 Vahini Katta Begins
    !Let $File       = '\\nt5\ftproot\benefits\Cobra\in\'  || 'CobraBCC_B_'|| $CURRDT ||  '.csv'
    !LET $Error_File = '\\nt5\ftproot\benefits\Cobra\in\'  || 'CobraBCC_B_'|| $CURRDT || '_error.csv' 
    Let $File       = '\\corp.gianteagle.com\ftp\benefits\Cobra\in\'  || 'CobraBCC_B_'|| $CURRDT ||  '.csv'        !testing
    LET $Error_File = '\\corp.gianteagle.com\ftp\benefits\Cobra\in\'  || 'CobraBCC_B_'|| $CURRDT || '_error.csv' 
    ! GEXBN_848_D168867_01 2014-04-22 Vahini Katta Ends
   End-if  
  #end-if 
   
   OPEN $File as 1 for-writing record=1600 status = #Open
     
   OPEN $Error_File as 2 for-writing record=1600 status = #Open1
    
   if (#Open <> 0)
      let $Error-Message = 'PROGRAM ABORTED - Problem opening file123 : ' || $File
      show $Error-Message
      STOP
   end-if
   
   if (#Open1 <> 0)
      let $Error-Message = 'PROGRAM ABORTED - Problem opening Error file345 : ' || $Error_File
      show $Error-Message
      STOP
   end-if
   
End-Procedure Open-File

!-----------------------------------------------------------------------
! Function:    Write_Header                                            -
! Description: Writes the header row for both the files                -
! Called By:   Begin_report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Write_Header
        
 Write 1 from 'First Name'		           $delim
             'Last Name'		             $delim
             'Employee SSN'		           $delim
             'SSN'			                 $delim
             'Birthdate'		             $delim
             'Relationship'		           $delim
             'Addr1'			               $delim
             'Addr2'			               $delim
             'City'			                 $delim
             'State'			               $delim
             'Zip Code'			             $delim
             'Phone'			               $delim
             'EE Hire Date'		           $delim
             'EE Term Date'		           $delim
             'Original Effective Date'	 $delim
             'Effective Date'		         $delim
             'Qualifying Event'		       $delim
             'Event Date'		             $delim
             'Severance Date'		         $delim
             'Coverage Type'		         $delim
             'Enrollment Level'		       $delim
             'Benefit Code'		           $delim
             'Comments'			             $delim
             'Gender'			               $delim
             'Division'			             $delim
             'Department'		             $delim
             'Location'			             $delim
             'Area'			                 $delim
             'Payroll Class'		         $delim
             'EE UDClass 1'		           $delim
             'EE UDClass 2'		           $delim
             'EE UDClass 3'		           $delim
             'EE UDClass 4'		           $delim
             'EE UDClass 5'		           $delim
                   
  Write 2 from 'Employee SSN'            $delim
               'First Name'              $delim
               'Last Name'               $delim
               'Addr1'			             $delim
               'Addr2'			             $delim
               'City'			               $delim
               'State'			             $delim
               'Zip Code'			           $delim 
               'Birthdate'		           $delim
               'Gender'			             $delim
               'EE Hire Date'		         $delim
               'Department'		           $delim
               'Division'			           $delim
               'GE Emplid'               $delim
               'Message'                 $delim


End-Procedure


!-----------------------------------------------------------------------
! Function:    Get-Pay-End-Dt                                          -
! Description: Get the current week pay end dt                         -
! Called By:   Report-Lost-Covrg                                       -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Pay-End-Dt
Begin-Select loops=1
PAY.PAY_BEGIN_DT
PAY.PAY_END_DT

  move &PAY.PAY_BEGIN_DT to $Pay_Begin_Dt
  move &PAY.PAY_END_DT   to $Pay_End_Dt
  display 'Pay End Date:' noline
  display $pay_end_Dt

FROM PS_PAY_CALENDAR PAY
WHERE PAY.PAY_END_DT = (SELECT MAX(PAY_END_DT) FROM PS_PAY_CALENDAR
		                    WHERE PAY_END_DT <= $AsofDate
                        AND PAY_OFF_CYCLE_CAL = 'N')
AND PAY.PAY_OFF_CYCLE_CAL = 'N'
End-Select
End-Procedure

!-----------------------------------------------------------------------
! Function:    Delete-Temp-Record                                      -
! Description: Get the current week pay end dt                         -
! Called By:   Report-Lost-Covrg                                       -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Delete-Temp-Record
Begin-SQL
DELETE FROM PS_GEX_COBRA_TEMP
End-SQL
End-Procedure Delete-Temp-Record

!-----------------------------------------------------------------------
! Function:    Report-Lost-Covrg                                       -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Report-Lost-Covrg

Begin-Select
HB1.EMPLID,
HB1.EMPL_RCD,
HB1.COBRA_EVENT_ID,
HB1.PLAN_TYPE, 
HB1.BENEFIT_NBR,
HB1.EFFDT,
HB1.DEDUCTION_END_DT,
HB1.COVERAGE_BEGIN_DT,
HB1.COVERAGE_ELECT_DT,
HB1.COVERAGE_ELECT,
HB1.BENEFIT_PLAN

    If Substr(&HB1.PLAN_TYPE,1,1) = '1'
 			do Select-Prev-Health-plan
 		Else
 		  If Substr(&HB1.PLAN_TYPE,1,1) = '6'
 		     Move '' to $Covrg_Cd
 	 		   do Select-Prev-FSA-plan                           
		  End-if
		End-if
		
FROM PS_HEALTH_BENEFIT  HB1
WHERE HB1.COVERAGE_ELECT = 'T'
AND ((HB1.COVERAGE_ELECT_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt
      AND HB1.COVERAGE_BEGIN_DT < $Pay_End_Dt)
      OR (HB1.COVERAGE_BEGIN_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt))
[$SlctPlanCriteria]
UNION SELECT                                            
FB1.EMPLID,
FB1.EMPL_RCD,
FB1.COBRA_EVENT_ID,
FB1.PLAN_TYPE, 
FB1.BENEFIT_NBR,
FB1.EFFDT,
FB1.DEDUCTION_END_DT,
FB1.COVERAGE_BEGIN_DT,
FB1.COVERAGE_ELECT_DT,
FB1.COVERAGE_ELECT,
FB1.BENEFIT_PLAN                                       
FROM PS_FSA_BENEFIT  FB1                                      
WHERE FB1.COVERAGE_ELECT = 'T'
AND ((FB1.COVERAGE_ELECT_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt
      AND FB1.COVERAGE_BEGIN_DT < $Pay_End_Dt)
      OR (FB1.COVERAGE_BEGIN_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt))
[$SlctPlanCriteria1]
ORDER BY EMPLID,PLAN_TYPE              
End-Select
End-Procedure Report-Lost-Covrg

!-----------------------------------------------------------------------
! Function:    Select-Prev-Health-plan                                 -
! Description: Select the Previous Health Row                          -
! Called By:   Report-Lost-Covrg                                       -
! Calls:       Match-Ben-Plan Get-TM-Details Check-Event-Class         -
!              Life-Add-Benefit,Get-Covg-Cd-Descr,Get-Dependents       -
!              Write-Report                                            - 
!-----------------------------------------------------------------------
Begin-Procedure Select-Prev-Health-plan

  Move 'N' to $Error_Found
  do Initialize-Vars
  Move ''  to $SlctDepCriteria  
  Move ''  to $Event_Dt
Begin-Select
HB2.EMPLID
HB2.EMPL_RCD
HB2.BENEFIT_PLAN
HB2.PLAN_TYPE
HB2.COVRG_CD
HB2.EFFDT
HB2.COVERAGE_ELECT
HB2.BENEFIT_NBR
     
   if &HB2.COVERAGE_ELECT='E'
     Move &HB2.EMPLID       to  $Emplid
     Move &HB2.EMPL_RCD     to  $Empl_Rcd 
     Move &HB1.EFFDT        to  $Effdt
     Move &HB1.COVERAGE_ELECT_DT to $Covrg_Elect_Dt
     Move &HB2.EFFDT        to  $Prev_Effdt
     Move &HB2.BENEFIT_PLAN to  $Ben_Plan
     Move &HB2.BENEFIT_NBR  to  $Ben_Nbr
     Move &HB2.COVRG_CD     to  $Covrg_cd 
     Move &HB2.PLAN_TYPE    to  $Pln_Type
     Move 'PLAN_TYPE'       to  $FieldName
	   Move &HB2.PLAN_TYPE    to  $FieldValue
     do Read-Translate-Table
	   Move $XlatShortName    to  $Coverage
	   
	   do Match-Ben-Plan
  
     if $Ben_Plan_Match='Y'
     
       do Get-TM-Details
       do Check-Event-Class
       
       if $Error_Found='N'
         do Insert-Temp-Record
         do Life-Add-Benefit
         do Get-Covg-Cd-Descr 
         Move &HB2.BENEFIT_PLAN to $Ben_Plan
         Move 'EMP' to $Relationship
         do Write-Report
         Move &HB2.BENEFIT_NBR  to  $Ben_Nbr
         Move &HB2.PLAN_TYPE    to  $Pln_Type
         do Get-Dependents   
       End-if  
     end-if   
   end-if  
      
FROM PS_HEALTH_BENEFIT HB2
WHERE HB2.EMPLID = &HB1.EMPLID
AND HB2.EMPL_RCD = &HB1.EMPL_RCD
AND HB2.PLAN_TYPE = &HB1.PLAN_TYPE
AND HB2.EFFDT = (SELECT MAX(EFFDT) FROM PS_HEALTH_BENEFIT
                  WHERE EMPLID = HB2.EMPLID
                    AND EMPL_RCD = HB2.EMPL_RCD
                    AND COBRA_EVENT_ID=HB2.COBRA_EVENT_ID
                    AND PLAN_TYPE = HB2.PLAN_TYPE
                    AND BENEFIT_NBR = HB2.BENEFIT_NBR
                    AND EFFDT < &HB1.EFFDT)             
End-Select
End-Procedure Select-Prev-Health-plan

!-----------------------------------------------------------------------
! Function:    Report-Lost-Dep-Cov                                       -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Report-Lost-Dep-Cov
 
Begin-Select
HB3.EMPLID,
HB3.EMPL_RCD,
HB3.PLAN_TYPE, 
HB3.BENEFIT_NBR,
HB3.EFFDT,
HB3.COVRG_CD,
HB3.COVERAGE_ELECT,
HB3.BENEFIT_PLAN,
HB3.COVERAGE_ELECT_DT

  do Select-Prev-Hlth-Plan

FROM PS_HEALTH_BENEFIT  HB3
WHERE HB3.COVERAGE_ELECT = 'E'
AND ((HB3.COVERAGE_ELECT_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt
     AND HB3.COVERAGE_BEGIN_DT < $Pay_End_Dt)
     OR (HB3.COVERAGE_BEGIN_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt))
[$SlctPlanCriteria2]
ORDER BY HB3.EMPLID,HB3.PLAN_TYPE
End-Select
End-Procedure Report-Lost-Dep-Cov

!-----------------------------------------------------------------------
! Function:    Select-Prev-Hlth-Plan                                   -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Select-Prev-Hlth-Plan
  Move 'N' to $Error_Found
  do Initialize-Vars
Begin-Select
HB4.EMPLID
HB4.EMPL_RCD
HB4.BENEFIT_PLAN
HB4.PLAN_TYPE
HB4.COVRG_CD
HB4.EFFDT
HB4.COVERAGE_ELECT
HB4.BENEFIT_NBR
     
   if &HB4.COVERAGE_ELECT='E'
   
     Move &HB4.EMPLID       to  $Emplid
     Move &HB4.EMPL_RCD     to  $Empl_Rcd 
     Move &HB3.EFFDT        to  $Effdt
     Move &HB3.COVERAGE_ELECT_DT to $Covrg_Elect_Dt
     Move &HB4.EFFDT        to  $Prev_Effdt
     Move &HB4.BENEFIT_PLAN to  $Ben_Plan
     Move &HB4.BENEFIT_NBR  to  $Ben_Nbr
     Move &HB4.COVRG_CD     to  $Covrg_cd
     Move &HB4.PLAN_TYPE    to  $Pln_Type
     Move 'PLAN_TYPE'       to  $FieldName
	   Move &HB4.PLAN_TYPE    to  $FieldValue
     do Read-Translate-Table
	   Move $XlatShortName    to  $Coverage
	   
     do Match-Ben-Plan
     if $Ben_Plan_Match='Y'
     
      do Get-Check-Dep-Count
      if #Count>0
       do Get-TM-Details
       do Check-Event-Class
       
       if $Error_Found='N'
         do Insert-Temp-Record
         do Life-Add-Benefit
         do Get-Covg-Cd-Descr 
         Move &HB4.BENEFIT_PLAN to  $Ben_Plan
         Move 'EMP' to $Relationship
         Move ''  to $Event_Dt
         do Write-Report
         Move &Event_Dt  to $Event_Dt
         Move &HB4.BENEFIT_NBR  to  $Ben_Nbr
         Move &HB4.PLAN_TYPE    to  $Pln_Type
         do Get-Dependents   
       End-if  
      end-if 
     end-if   
   end-if  
      
FROM PS_HEALTH_BENEFIT HB4
WHERE HB4.EMPLID = &HB3.EMPLID
AND HB4.EMPL_RCD = &HB3.EMPL_RCD
AND HB4.PLAN_TYPE = &HB3.PLAN_TYPE
AND HB4.EFFDT = (SELECT MAX(EFFDT) FROM PS_HEALTH_BENEFIT
                  WHERE EMPLID = HB4.EMPLID
                    AND EMPL_RCD = HB4.EMPL_RCD
                    AND COBRA_EVENT_ID=HB4.COBRA_EVENT_ID
                    AND PLAN_TYPE = HB4.PLAN_TYPE
                    AND BENEFIT_NBR = HB4.BENEFIT_NBR
                    AND EFFDT < &HB3.EFFDT)             
End-Select
End-Procedure Select-Prev-Health-plan

!-----------------------------------------------------------------------
! Function:    Get-Check-Dep-Count                                     -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Check-Dep-Count
 Move 0 to #Count
 Move '' to $DepCriteria
 
Begin-Select
EMPLID,
PLAN_TYPE,
DEPENDENT_BENEF

  if isnull($DepCriteria)
       let $DepCriteria = $DepCriteria ||''''|| &DEPENDENT_BENEF || ''''
  else
       let $DepCriteria = $DepCriteria ||','||''''|| &DEPENDENT_BENEF || ''''
  end-if

 add 1 to #Count
 
FROM
(
SELECT EMPLID,PLAN_TYPE,DEPENDENT_BENEF FROM PS_HEALTH_DEPENDNT WHERE PLAN_TYPE=&HB4.PLAN_TYPE AND EMPLID=&HB4.EMPLID AND EFFDT=&HB4.EFFDT
MINUS
SELECT EMPLID,PLAN_TYPE,DEPENDENT_BENEF FROM PS_HEALTH_DEPENDNT WHERE PLAN_TYPE=&HB4.PLAN_TYPE AND EMPLID=&HB4.EMPLID AND EFFDT=&HB3.EFFDT
)
End-Select
  if isnull($DepCriteria)
    move '' to $SlctDepCriteria
  else
    let $SlctDepCriteria  = 'AND HD.DEPENDENT_BENEF IN (' || $DepCriteria || ')'
  end-if 
End-Procedure Get-Check-Dep-Count

!-----------------------------------------------------------------------
! Function:    Select-Prev-FSA-Plan                                    -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Select-Prev-FSA-Plan
  
  Move 'N' to $Error_Found
  do Initialize-Vars
  
Begin-Select
FB2.EMPLID
FB2.EMPL_RCD
FB2.BENEFIT_PLAN
FB2.PLAN_TYPE
FB2.EFFDT
FB2.COVERAGE_ELECT
FB2.BENEFIT_NBR
     
   if &FB2.COVERAGE_ELECT='E'
     Move &FB2.EMPLID       to $Emplid
     Move &FB2.EMPL_RCD     to $Empl_Rcd 
     Move &HB1.EFFDT        to $Effdt
     Move &HB1.COVERAGE_ELECT_DT to $Covrg_Elect_Dt
     Move &FB2.EFFDT        to $Prev_Effdt
     Move &FB2.BENEFIT_PLAN to $Ben_Plan
     Move &FB2.BENEFIT_NBR  to $Ben_Nbr
     Move &FB2.PLAN_TYPE    to $Pln_Type
     Move 'PLAN_TYPE'       to $FieldName
	   Move &FB2.PLAN_TYPE    to $FieldValue
     do Read-Translate-Table
	   Move $XlatShortName    to $Coverage
	   move '' to $Covg_Cd_Descr
	  
     do Match-Ben-Plan
     if $Ben_Plan_Match='Y'
       do Get-TM-Details
       do Check-Event-Class
       
       if $Error_Found='N'
         do Insert-Temp-Record
         do Life-Add-Benefit
         Move &FB2.BENEFIT_PLAN to $Ben_Plan
         Move 'EMP' to $Relationship
         do Write-Report
       End-if  
     end-if   
   end-if
      
FROM PS_FSA_BENEFIT  FB2
WHERE FB2.EMPLID = &HB1.EMPLID
AND FB2.EMPL_RCD = &HB1.EMPL_RCD
AND FB2.PLAN_TYPE = &HB1.PLAN_TYPE
!AND FB2.COVERAGE_ELECT = 'E'
AND FB2.EFFDT = (SELECT MAX(EFFDT) FROM PS_FSA_BENEFIT
                 WHERE EMPLID = FB2.EMPLID
                 AND EMPL_RCD = FB2.EMPL_RCD
                 AND COBRA_EVENT_ID=FB2.COBRA_EVENT_ID
                 AND PLAN_TYPE = FB2.PLAN_TYPE
                 AND BENEFIT_NBR = FB2.BENEFIT_NBR
                 AND EFFDT < &HB1.EFFDT)                    
End-Select
End-Procedure Select-Prev-FSA-Plan

!-----------------------------------------------------------------------
! Function:    Life-Add-Benefit                                        -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Life-Add-Benefit
 Move 'N' to $Life_Ins_Flag
Begin-Select
LB1.EMPLID,
LB1.EMPL_RCD,
LB1.PLAN_TYPE, 
LB1.BENEFIT_NBR,
LB1.EFFDT,
LB1.DEDUCTION_END_DT,
LB1.COVERAGE_BEGIN_DT,
LB1.COVERAGE_ELECT_DT,
LB1.COVERAGE_ELECT,
LB1.BENEFIT_PLAN

 Move &LB1.EFFDT to $Life_Effdt
 do Select-Prev-Life-plan
 
FROM PS_LIFE_ADD_BEN LB1
WHERE LB1.EMPLID=$Emplid
AND LB1.EMPL_RCD=$Empl_Rcd 
AND LB1.BENEFIT_NBR=$Ben_Nbr
AND LB1.PLAN_TYPE='20'
AND LB1.COVERAGE_ELECT = 'T'
AND ((LB1.COVERAGE_ELECT_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt
      AND LB1.COVERAGE_BEGIN_DT < $Pay_End_Dt)
      OR (LB1.COVERAGE_BEGIN_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt))
End-Select
End-Procedure Life-Add-Benefit        

!-----------------------------------------------------------------------
! Function:    Select-Prev-Life-plan                                   -
! Description: Life-Add-Benefit                                        -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Select-Prev-Life-plan

Begin-Select
LB2.BENEFIT_PLAN
LB2.PLAN_TYPE
LB2.EFFDT
LB2.COVERAGE_ELECT
 
   if &LB2.COVERAGE_ELECT = 'E'
     Move &LB2.PLAN_TYPE    to  $Pln_Type
     Move &LB2.BENEFIT_PLAN to  $Ben_Plan
     do Match-Ben-Plan

     if $Ben_Plan_Match='Y'
        Move 'Y' to $Life_Ins_Flag
     end-if   
   end-if  
      
FROM PS_LIFE_ADD_BEN LB2
WHERE LB2.EMPLID = &LB1.EMPLID
AND LB2.EMPL_RCD = &LB1.EMPL_RCD
AND LB2.PLAN_TYPE = '20'
AND LB2.EFFDT = (SELECT MAX(EFFDT) FROM PS_LIFE_ADD_BEN
                  WHERE EMPLID = LB2.EMPLID
                    AND EMPL_RCD = LB2.EMPL_RCD
                    AND PLAN_TYPE = LB2.PLAN_TYPE
                    AND BENEFIT_NBR = LB2.BENEFIT_NBR
                    AND EFFDT < &LB1.EFFDT)
End-Select
End-Procedure Select-Prev-Life-plan

!-----------------------------------------------------------------------
! Function:    Match-Ben-Plan                                          -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Match-Ben-Plan

 Move 'N' to $Ben_Plan_Match
 
Begin-Select loops=1
BO.BENEFIT_PLAN
  
  Move 'Y' to $Ben_Plan_Match
  
FROM PS_BEN_DEFN_OPTN BO
WHERE BO.BENEFIT_PLAN=$Ben_Plan
AND BO.PLAN_TYPE=$Pln_Type
AND BO.EFFDT=(SELECT MAX(BO1.EFFDT) FROM PS_BEN_DEFN_OPTN BO1
               WHERE BO1.BENEFIT_PROGRAM=BO.BENEFIT_PROGRAM
               AND BO1.PLAN_TYPE=BO.PLAN_TYPE
               AND BO1.OPTION_ID=BO.OPTION_ID
               AND BO1.EFFDT<=$Asofdate)
[$SlctBenCriteria1]
End-Select
End-Procedure Match-Ben-Plan

!-----------------------------------------------------------------------
! Function:    Get-Covg-Cd-Descr                                       -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Covg-Cd-Descr
 move '' to $Covg_Cd_Descr
Begin-SELECT
C.DESCR 
  move &C.DESCR to $Covg_Cd_Descr
FROM PS_COVRG_CD_TBL C
WHERE C.COVRG_CD=$Covrg_cd
AND C.EFFDT=(SELECT MAX(C1.EFFDT) FROM PS_COVRG_CD_TBL C1
              WHERE C1.COVRG_CD=C.COVRG_CD
              AND C1.EFFDT<=$Asofdate)  
End-Select
End-Procedure
!-----------------------------------------------------------------------
! Function:    Check-Event-Class                                       -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Check-Event-Class
 Move 'N' to $Bas_Partic_Match
 
Begin-Select
BPL.SCHED_ID ,
BPL.BENEFIT_RCD_NBR,
BPL.EVENT_ID,
BAS.EVENT_CLASS,
!GEXBN_848_D103104_02 2011-10-06 Vahini Katta Begin
!TO_CHAR(BAS.EVENT_DT,'YYYYMMDD') &Event_Dt
!TO_CHAR(BPL.COVERAGE_BEGIN_DT,'YYYYMMDD') &Event_Dt
!GEXBN_848_D103104_02 2011-10-06 Vahini Katta End
!GEXBN_848_D103104_04 2011-10-06 Vahini Katta Begin
TO_CHAR(BPL.COVERAGE_BEGIN_DT-1,'YYYYMMDD') &Event_Dt
!GEXBN_848_D103104_04 2011-10-06 Vahini Katta End

  Move 'Y' to $Bas_Partic_Match
  Move &Event_Dt to $Event_Dt
  
  if &BPL.SCHED_ID='EM'
    do Get-Evt-Class
  else
    Move 'SCHED_ID not equal to Event Maintenance' to $Message
    do Write-Error-report
  end-if

FROM PS_BAS_PARTIC BAS, PS_BAS_PARTIC_PLAN BPL
WHERE BAS.EMPLID = $Emplid
  AND BAS.EMPL_RCD = $Empl_Rcd 
  AND BPL.SCHED_ID = BAS.SCHED_ID
  AND BPL.EMPLID = BAS.EMPLID
  AND BPL.BENEFIT_RCD_NBR = BAS.BENEFIT_RCD_NBR
  AND BPL.EVENT_ID = BAS.EVENT_ID
  AND BPL.PLAN_TYPE = $Pln_Type
  AND BPL.DEDUCTION_BEGIN_DT=$Effdt
  !GEXBN_848_D103104_03 2011-10-06 Vahini Katta Begin
  AND BPL.COVERAGE_ELECT_DT=$Covrg_Elect_Dt
  !GEXBN_848_D103104_03 2011-10-06 Vahini Katta End
  [$SlctBenCriteria]
End-Select
 if $Bas_Partic_Match='N'
   Move 'No row in BAS_PARTIC_PLAN' to $Message 
   do Write-Error-Report
 end-if
End-Procedure Check-Event-Class

!-----------------------------------------------------------------------
! Function:    Get-Evt-Class                                           -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Evt-Class
 Move 'N' to $Evt_Class_Found
Begin-Select
CBR.EVENT_CLASS
CBR.GEX_COBRA
CBR.DESCR

 Move 'Y' to $Evt_Class_Found
 
FROM PS_GEX_CBR_EVT_RSN CBR
WHERE CBR.EVENT_CLASS=&BAS.EVENT_CLASS
AND CBR.EFFDT=(SELECT MAX(CBR1.EFFDT) FROM PS_GEX_CBR_EVT_RSN CBR1
               WHERE CBR1.EVENT_CLASS=CBR.EVENT_CLASS
                 AND CBR1.EFFDT<=$Asofdate)
End-Select
  if $Evt_Class_Found='N'
   Move 'EVENT_CLASS not COBRA Event' to $Message
   do Write-Error-Report
  end-if
End-Procedure Get-Evt-Class

!-----------------------------------------------------------------------
! Function:    Get-Dependents                                          -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Dependents
Begin-Select
HD.EMPLID,
HD.PLAN_TYPE,
HD.DEPENDENT_BENEF,
HD.EFFDT,
     
  Do Get-Dep-Details
  Do Write-Report

FROM PS_HEALTH_DEPENDNT HD
WHERE HD.EMPLID = $Emplid
AND HD.EFFDT    = $Prev_Effdt
AND HD.PLAN_TYPE= $Pln_Type
AND HD.BENEFIT_NBR=$Ben_Nbr
[$SlctDepCriteria] 
End-Select
End-Procedure Get-Dependents

!-----------------------------------------------------------------------
! Function:    Get-TM-Details                                          -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-TM-Details

 Move ' ' to $First_Name
 Move ' ' to $Last_Name
 Move ' ' to $Sex
 Move ' ' to $SSN
 Move ''  to $Birth_Dt
 Move ' ' to $Division
 Move ' ' to $Deptid
 Move ' ' to $National_ID
 
Begin-Select
UPPER(P.FIRST_NAME) &P.FIRST_NAME,
UPPER(P.LAST_NAME) &P.LAST_NAME,
N.NATIONAL_ID,
TO_CHAR(P.BIRTHDATE,'YYYYMMDD') &Birth_Dt,
UPPER(P.ADDRESS1) &P.ADDRESS1,
UPPER(P.ADDRESS2) &P.ADDRESS2,
UPPER(P.CITY) &P.CITY,
UPPER(P.STATE) &P.STATE,
P.POSTAL,
P.PHONE,
TO_CHAR(J.LAST_HIRE_DT,'YYYYMMDD') &Last_Hire_Dt,
TO_CHAR(J.TERMINATION_DT,'YYYYMMDD') &Term_Dt,
P.SEX,
J.DEPTID,
J.EMPLID,
J.EMPL_RCD,
!M.EMPLID,
J.COMPANY,
J.PAYGROUP

  Move &P.FIRST_NAME  to $First_Name
  Move &P.LAST_NAME   to $Last_Name
  Move &P.SEX         to $Sex
  Move &N.NATIONAL_ID to $SSN
  Move &N.NATIONAL_ID to $National_ID
  Move &Birth_Dt      to $Birth_Dt
  Move &J.DEPTID      to $Deptid

  do Get-Check-Dt
  
FROM PS_PERSONAL_DATA P,
     PS_PERS_NID N,
     PS_JOB J
    ! ,PS_GEX_EMPLID_MAPP M
WHERE P.EMPLID=N.EMPLID
AND P.EMPLID=J.EMPLID
!AND P.EMPLID=M.SSN
!AND J.EMPL_RCD=M.EMPL_RCD
AND N.COUNTRY='USA'
AND N.NATIONAL_ID_TYPE='PR'
AND J.EFFDT=(SELECT MAX(J1.EFFDT) FROM PS_JOB J1
              WHERE J1.EMPLID=J.EMPLID
              AND J1.EMPL_RCD=J.EMPL_RCD
              AND J1.EFFDT<=$AsofDate)
AND J.EFFSEQ=(SELECT MAX(J2.EFFSEQ) FROM PS_JOB J2
               WHERE J2.EMPLID=J.EMPLID
               AND   J2.EMPL_RCD=J.EMPL_RCD
               AND J2.EFFDT=J.EFFDT) 
AND J.EMPLID=$Emplid
End-Select
End-Procedure Get-TM-Details

!-----------------------------------------------------------------------
! Function:    Get-Check-Dt                                            -
! Description: Get-TM-Details                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Check-Dt
Begin-Select
CHECK_DT

  
  Move &CHECK_DT to $Chk_dt
  Do convert-to-dtu-date($Chk_dt,$Chk_dt_dtu)
  Do dtu-dayofweek$($Chk_dt_dtu,$dayofweek)
  
  if $dayofweek='Thursday'
     Move '0002' to $Division
  end-if
  if $dayofweek='Tuesday' or $dayofweek='Wednesday'
    Move '0001' to $Division
  End-if  
  
FROM PS_PAY_CHECK CD
WHERE CD.EMPLID   = &J.EMPLID
  AND CD.EMPL_RCD = &J.EMPL_RCD
  AND CD.COMPANY  = &J.COMPANY
  AND CD.PAYGROUP = &J.PAYGROUP
  AND CD.OFF_CYCLE= 'N'
  AND CD.CHECK_DT = (SELECT MAX(check_dt) FROM PS_PAY_CHECK
                      WHERE CD.COMPANY  = COMPANY
                        AND CD.PAYGROUP = PAYGROUP
	                      AND emplid      = &J.EMPLID
                        AND EMPL_RCD    = &J.EMPL_RCD
                        and off_cycle   ='N'           
	                      AND CHECK_DT   <= $ASOFDATE)
End-Select	                      
End-Procedure Get-Check-Dt

!-----------------------------------------------------------------------
! Function:    Get-Dep-Details                                         -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Dep-Details
  Move ' ' to $First_Name
  Move ' ' to $Last_Name
  Move ' ' to $SSN
  Move ''  to $Birth_Dt
  Move ' ' to $Relationship
  Move ' ' to $Sex

Begin-SELECT
DP.DEPENDENT_BENEF,
UPPER(DP.FIRST_NAME) &DP.FIRST_NAME,
UPPER(DP.LAST_NAME) &DP.LAST_NAME,
TO_CHAR(DP.BIRTHDATE,'YYYYMMDD') &Dep_Birth_Dt,
DP.RELATIONSHIP,
DN.NATIONAL_ID,
DP.SEX

  Move &DP.FIRST_NAME  to $First_Name
  Move &DP.LAST_NAME   to $Last_Name
  Move &DN.NATIONAL_ID to $SSN
  Move &Dep_Birth_Dt   to $Birth_Dt
  Move &DP.SEX         to $Sex

  Evaluate &DP.RELATIONSHIP
  when='SP'
  when='X'
  when='XN'
      Move 'SPS' to $Relationship
      break            
  when='NA'
      Move 'DP'  to $Relationship
      break      
  when='C'
  when='SC'
  when='GC'
      Move 'DEP' to $Relationship
      break   
  when-other
      break
  End-Evaluate 

FROM PS_DEPENDENT_BENEF DP,
     PS_DEP_BENEF_NID DN
WHERE DP.EMPLID=DN.EMPLID(+)
  AND DP.DEPENDENT_BENEF=DN.DEPENDENT_BENEF(+)
  AND DN.COUNTRY(+)='USA'
  AND DN.NATIONAL_ID_TYPE(+)='PR'
  AND DP.EMPLID=$Emplid
  AND DP.DEPENDENT_BENEF=&HD.DEPENDENT_BENEF
End-Select  
End-Procedure Get-Dep-Details

!-----------------------------------------------------------------------
! Function:    Report-Life-Ins-Terms                                   -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Report-Life-Ins-Terms
 Move 'N' to $Life_Ins_Flag
 Move ''  to $Life_Effdt
 Move 'N' to $Ben_Plan_Match
Begin-Select DISTINCT
LB3.EMPLID,
LB3.EMPL_RCD,
LB3.PLAN_TYPE, 
LB3.BENEFIT_NBR,
LB3.EFFDT,
LB3.DEDUCTION_END_DT,
LB3.COVERAGE_BEGIN_DT,
LB3.COVERAGE_ELECT_DT,
LB3.COVERAGE_ELECT,
LB3.BENEFIT_PLAN

  do Select-Prev-Life-plan-Term
 
FROM PS_LIFE_ADD_BEN LB3
WHERE LB3.PLAN_TYPE='20'
AND LB3.COVERAGE_ELECT = 'T'
AND ((LB3.COVERAGE_ELECT_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt
      AND LB3.COVERAGE_BEGIN_DT < $Pay_End_Dt)
      OR (LB3.COVERAGE_BEGIN_DT BETWEEN $Pay_Begin_Dt AND $Pay_End_Dt))
AND NOT EXISTS(SELECT 'X' FROM PS_GEX_COBRA_TEMP 
               WHERE EMPLID=LB3.EMPLID
                 AND EMPL_RCD=LB3.EMPL_RCD)      
End-Select
End-Procedure

!-----------------------------------------------------------------------
! Function:    Select-Prev-Life-plan-Term                              -
! Description: Report-Life-Ins-Terms                                   -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Select-Prev-Life-plan-Term
  Move ' ' to $Coverage     
  Move ' ' to $Covg_Cd_Descr   
  Move ' ' to $Ben_Plan     
  Move 'N' to $Error_Found   
Begin-Select
LB4.EMPLID
LB4.EMPL_RCD
LB4.BENEFIT_PLAN
LB4.PLAN_TYPE
LB4.EFFDT
LB4.COVERAGE_ELECT
LB4.BENEFIT_NBR
 
   if &LB4.COVERAGE_ELECT='E'
     Move &LB4.EMPLID       to  $Emplid
     Move &LB4.EMPL_RCD     to  $Empl_Rcd 
     Move &LB3.EFFDT        to  $Effdt
     Move &LB3.COVERAGE_ELECT_DT to $Covrg_Elect_Dt
     Move &LB4.EFFDT        to  $Prev_Effdt
     Move &LB4.BENEFIT_PLAN to  $Ben_Plan
     Move &LB4.BENEFIT_NBR  to  $Ben_Nbr
     Move &LB4.PLAN_TYPE    to  $Pln_Type
     Move 'PLAN_TYPE'       to  $FieldName
	   Move &LB4.PLAN_TYPE    to  $FieldValue
	  
     do Read-Translate-Table
	   Move $XlatShortName    to  $Coverage
	   
     do Match-Ben-Plan
    
     if $Ben_Plan_Match='Y'
       Move 'Y' to $Life_Ins_Flag
       do Get-TM-Details
       do Check-Event-Class
    
       if $Error_Found='N'
         Move &LB4.BENEFIT_PLAN to  $Ben_Plan
         Move 'EMP' to $Relationship
         Move &Event_Dt  to $Event_Dt
         Move ' ' to $Coverage     
         Move ' ' to $Covg_Cd_Descr   
         Move ' ' to $Ben_Plan
         do Write-Report
       End-if  
     else  
       Move 'N' to $Life_Ins_Flag
     end-if   
   end-if  
      
FROM PS_LIFE_ADD_BEN LB4
WHERE LB4.EMPLID = &LB3.EMPLID
AND LB4.EMPL_RCD = &LB3.EMPL_RCD
AND LB4.PLAN_TYPE = '20'
AND LB4.EFFDT = (SELECT MAX(EFFDT) FROM PS_LIFE_ADD_BEN
                  WHERE EMPLID = LB4.EMPLID
                    AND EMPL_RCD = LB4.EMPL_RCD
                    AND PLAN_TYPE = LB4.PLAN_TYPE
                    AND BENEFIT_NBR = LB4.BENEFIT_NBR
                    AND EFFDT < &LB3.EFFDT)
End-Select
End-Procedure 

!-----------------------------------------------------------------------
! Function:    Initialize-Vars                                         -
! Description: Main Selection                                          -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Initialize-Vars
  Move ' ' to $Emplid
  Move ' ' to $Empl_Rcd
  Move ''  to $Effdt
  Move ''  to $Covrg_Elect_Dt
  Move ''  to $Prev_Effdt
  Move ' ' to $Ben_Plan
  Move ' ' to $Pln_Type
  Move ' ' to $FieldName
  Move ' ' to $FieldValue
End-Procedure

!-----------------------------------------------------------------------
! Function:    Write-Report                                            -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Write-Report

  Let $Coverage=upper($Coverage)
  Let $Covg_Cd_Descr=upper($Covg_Cd_Descr)
  Let $Phone=translate(&P.PHONE, '/','-') 
  Let $Addr1=translate(&P.ADDRESS1,',',' ')

  Write 1 from $First_Name       $delim
               $Last_Name        $delim
               $National_ID      $delim
               $SSN              $delim
               $Birth_Dt         $delim
               $Relationship     $delim
               $Addr1            $delim
               &P.ADDRESS2       $delim
               &P.CITY           $delim
               &P.STATE          $delim
               &P.POSTAL         $delim
               $Phone            $delim
               &Last_Hire_Dt     $delim
               &Term_Dt          $delim
               ' '               $delim
               ' '               $delim
               &CBR.GEX_COBRA    $delim
               $Event_Dt         $delim
               ' '               $delim
               $Coverage         $delim
               $Covg_Cd_Descr    $delim
               $Ben_Plan         $delim
               ' '               $delim
               !&P.SEX            $delim
               $Sex              $delim
               $Division         $delim
               $DEPTID           $delim
               ' '               $delim
               ' '               $delim
               ' '               $delim
               !&M.EMPLID         $delim !GEX_SEC_REENG 2015-04-29 Vahini Katta
               $EMPLID           $delim  !GEX_SEC_REENG 2015-04-29 Vahini Katta
               $Life_Ins_Flag    $delim
               ' '               $delim
               ' '               $delim
               ' '               
End-Procedure Write-Report

!-----------------------------------------------------------------------
! Function:    Write-Error-Report                                      -
! Description: Select-Prev-Health-plan                                 -
! Called By:   Begin Report                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Write-Error-Report
  Move 'Y' to $Error_Found
  
  Let $Message=upper($Message)
  Let $Addr1=translate(&P.ADDRESS1,',',' ')
  
  if $Emplid<>$Prev_Emplid
  write 2 from $National_ID      $delim
               $First_Name       $delim
               $Last_Name        $delim
               $Addr1            $delim
               &P.ADDRESS2       $delim
               &P.CITY           $delim
               &P.STATE          $delim
               &P.POSTAL         $delim
               $Birth_Dt         $delim
               $Sex              $delim
               &Last_Hire_Dt     $delim
               $DEPTID           $delim
               $Division         $delim
               !&M.EMPLID        $delim  !GEX_SEC_REENG 2015-04-29 Vahini Katta
               $EMPLID           $delim  !GEX_SEC_REENG 2015-04-29 Vahini Katta
               $Message 
  End-if             
  Move $Emplid to $Prev_Emplid               
End-Procedure Write-Error-Report

!-----------------------------------------------------------------------
! Function:    Close-File                                              -
! Description: Closes the File                                         -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Close-File
  Close 1
  Close 2
  If #filestat != 0
     Show 'Error closing output file.  Program terminating.'
     Stop 
  End-If
End-Procedure

!-----------------------------------------------------------------------
! Function:    Send-Email                                              -
! Description: Sends the email                                         -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Send-Email
let $to_list = ' '
Begin-Select
SE.EMAILID

   do Create-To-List

From  PS_GEX_EMAIL_ADDR SE
WHERE RTRIM(SE.GEX_EMAILTYPE, ' ') = 'CBRA'
End-Select
 do Send-It
end-procedure

!-----------------------------------------------------------------------
! Function:    Create-To-List                                          -
! Description: Creates the Email List                                  -
! Called By:   Send-Email                                              -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure  Create-To-List
       let $to_list = $to_list || &SE.EMAILID || ';'
End-Procedure

!-----------------------------------------------------------------------
! Function:    Send-It                                                 -
! Description: Creates the Email List                                  -
! Called By:   Send-Email                                              -
! Calls:       Get-Verbiage                                            -
!-----------------------------------------------------------------------
Begin-Procedure Send-It

let $TEXT_ID = $ReportID || '_SUBJ' 
Do Get-Verbiage
let $subject = &A.HR_SSTEXT_TEXT

let $TEXT_ID = $ReportID || '_BODY' 
Do Get-Verbiage

let $body_txt = &A.HR_SSTEXT_TEXT 

! Commented by Ujwal 20170126 - Begin
let $mail-cmd = 'psmail -TO"'||$to_list||'" -SUBJECT"'||$subject||'" -BODY"'||$body_txt||'" -FILE"'||$fileout
! Commented by Ujwal 20170126 - End
! Added by Ujwal 20170126 - Begin
let $mail-cmd = 'psmail -TO"'||$to_list||'" -SUBJECT"'||$subject||'" -BODY"'||$body_txt||'" -FILE"'||$fileout||'"'
! Added by Ujwal 20170126 - End


if $to_list <> ' '
  CALL SYSTEM USING $mail-cmd #Status
end-if

End-Procedure

!-----------------------------------------------------------------------
! Function:    Get-Verbiage                                            -
! Description: Creates the Email List                                  -
! Called By:   Send-It                                                 -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Verbiage
Begin-Select
A.HR_SSTEXT_TEXT

from ps_HR_SSTEXT_TEXT A 
WHERE A.OBJECTOWNERID = 'HHR' 
AND A.HR_SSTEXT_SUB_ID = 'GEX' 
AND A.TEXT_ID = $TEXT_ID 
AND A.EFFDT = (SELECT MAX(B.EFFDT) FROM PS_HR_SSTEXT_EFFDT B 
               WHERE B.OBJECTOWNERID = A.OBJECTOWNERID 
                 AND B.HR_SSTEXT_SUB_ID = A.HR_SSTEXT_SUB_ID 
                 AND B.TEXT_ID = A.TEXT_ID 
                 AND B.EFFDT <= $AsOfDate)
End-Select
End-Procedure

!-----------------------------------------------------------------------
! Function:    Insert-Temp-Record                                      -
! Description: Insert into temp record                                 -
! Called By:   Select-Prev-FSA-Plan,Select-Prev-Health-plan            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Insert-Temp-Record
Begin-Sql
INSERT INTO PS_GEX_COBRA_TEMP
VALUES
($Emplid
,$Empl_Rcd
,$Pln_Type
)
End-Sql
End-Procedure Insert-Temp-Record

#Include 'readxlat.sqc'  !Routines for the translate values
#Include 'gexxx911.sqc'  !Get Benefit Program Multiples Run Controls
#include 'gexxx920.sqc'  !Get ben single row run control
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math