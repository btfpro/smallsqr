!**************************************************************************
!  GEXXX916:  Retrieve "Multiple" Jobcode Run Controls and                *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX916_Jobcode_Alias                                              *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX916_INCLUDE_JOBCODE_CRITERIA                                   *
!    $GEXXX916_EXCLUDE_JOBCODE_CRITERIA                                   *
!    $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA                           *
!    $GEXXX916_EXCLUDE_JOBCODE_DISPLAY_CRITERIA                           *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX916_Jobcode_Alias = 'J.JOBCODE'                            *
!    Do GEXXX916-Select-Jobcode-Parameters                                *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX916_EXCLUDE_JOBCODE_CRITERIA]                             *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		06/08/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX916-Select-Jobcode-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_JOBCODE_ROWS = 0
  Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = ''
  Let $GEXXX916_EXCLUDE_JOBCODE_CRITERIA = ''
  Let $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX916_EXCLUDE_JOBCODE_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_JOBCODE.JOBCODE

  Add 1 To #GEX_RC_JOBCODE_ROWS

  If #GEX_RC_JOBCODE_ROWS = 1
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = '(' || $GEXXX916_Jobcode_Alias || ' In ('''     || &GEX_RC_JOBCODE.JOBCODE || ''''
    Let $GEXXX916_EXCLUDE_JOBCODE_CRITERIA = '(' || $GEXXX916_Jobcode_Alias || ' Not In (''' || &GEX_RC_JOBCODE.JOBCODE || ''''
  Else
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = $GEXXX916_INCLUDE_JOBCODE_CRITERIA || ',''' || &GEX_RC_JOBCODE.JOBCODE  || ''''
    Let $GEXXX916_EXCLUDE_JOBCODE_CRITERIA = $GEXXX916_EXCLUDE_JOBCODE_CRITERIA || ',''' || &GEX_RC_JOBCODE.JOBCODE  || ''''
  End-If

From  PS_GEX_RC_JOBCODE GEX_RC_JOBCODE

Where GEX_RC_JOBCODE.OPRID                = [$OprID]
And   GEX_RC_JOBCODE.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_JOBCODE.JOBCODE, ' ') != ' '

End-Select

  If #GEX_RC_JOBCODE_ROWS = 0
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = $GEXXX916_Jobcode_Alias || ' = ' || $GEXXX916_Jobcode_Alias
    Let $GEXXX916_EXCLUDE_JOBCODE_CRITERIA = $GEXXX916_Jobcode_Alias || ' = ' || $GEXXX916_Jobcode_Alias
  Else
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = $GEXXX916_INCLUDE_JOBCODE_CRITERIA || '))'
    Let $GEXXX916_EXCLUDE_JOBCODE_CRITERIA = $GEXXX916_EXCLUDE_JOBCODE_CRITERIA || '))'
    Let $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA = $GEXXX916_INCLUDE_JOBCODE_CRITERIA
    Let $GEXXX916_EXCLUDE_JOBCODE_DISPLAY_CRITERIA = $GEXXX916_EXCLUDE_JOBCODE_CRITERIA
  End-If

End-Procedure
