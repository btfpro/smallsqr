!**************************************************************************
!  GEXXX905:  Retrieve "Multiple" Hourly Rate Range Run Controls and      *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX905_Hrly_Rt_Alias                                              *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX905_INCLUDE_HRLY_RT_CRITERIA                                   *
!    $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA                                   *
!    $GEXXX905_INCLUDE_HRLY_RT_DISPLAY_CRITERIA                           *
!    $GEXXX905_EXCLUDE_HRLY_RT_DISPLAY_CRITERIA                           *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX905_Hrly_Rt_Alias = 'J.HOURLY_RT'                          *
!    Do GEXXX905-Select-Hrly-Rt-Parameters                                *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX905_INCLUDE_HRLY_RT_CRITERIA]                             *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX905-Select-Hrly-Rt-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_HRLY_RT_ROWS = 0
  Let $Include_Condition   = ''
  Let $Exclude_Condition   = ''
  Let $GEXXX905_INCLUDE_HRLY_RT_CRITERIA = ''
  Let $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA = ''
  Let $GEXXX905_INCLUDE_HRLY_RT_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX905_EXCLUDE_HRLY_RT_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_HRLY_RT.GEX_HRLY_RT_MIN
GEX_RC_HRLY_RT.GEX_HRLY_RT_MAX

  Move &GEX_RC_HRLY_RT.GEX_HRLY_RT_MIN To $Hrly_Rt_Min 888888888888.888888
  Let $Hrly_Rt_Min = Rtrim($Hrly_Rt_Min, '0')
  Let $Hrly_Rt_Min = Rtrim($Hrly_Rt_Min, '.')

  Move &GEX_RC_HRLY_RT.GEX_HRLY_RT_MAX To $Hrly_Rt_Max 888888888888.888888
  Let $Hrly_Rt_Max = Rtrim($Hrly_Rt_Max, '0')
  Let $Hrly_Rt_Max = Rtrim($Hrly_Rt_Max, '.')

  Add 1 To #GEX_RC_HRLY_RT_ROWS

  Let $Include_Condition = '(' || $GEXXX905_Hrly_Rt_Alias || ' Between '     || $Hrly_Rt_Min || ' And ' || $Hrly_Rt_Max || ')'

  Let $Exclude_Condition = '(' || $GEXXX905_Hrly_Rt_Alias || ' Not Between ' || $Hrly_Rt_Min || ' And ' || $Hrly_Rt_Max || ')'

  If #GEX_RC_HRLY_RT_ROWS = 1
    Let $GEXXX905_INCLUDE_HRLY_RT_CRITERIA = '(' || $Include_Condition
    Let $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX905_INCLUDE_HRLY_RT_CRITERIA =
        $GEXXX905_INCLUDE_HRLY_RT_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA =
        $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_HRLY_RT GEX_RC_HRLY_RT

Where GEX_RC_HRLY_RT.OPRID            = [$OprID]
And   GEX_RC_HRLY_RT.RUN_CNTL_ID      = [$Run_Cntl_ID]
And  (GEX_RC_HRLY_RT.GEX_HRLY_RT_MIN != 0
Or    GEX_RC_HRLY_RT.GEX_HRLY_RT_MAX != 0)

End-Select

  If #GEX_RC_HRLY_RT_ROWS = 0
    Let $GEXXX905_INCLUDE_HRLY_RT_CRITERIA = $GEXXX905_Hrly_Rt_Alias || ' = ' || $GEXXX905_Hrly_Rt_Alias
    Let $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA = $GEXXX905_Hrly_Rt_Alias || ' = ' || $GEXXX905_Hrly_Rt_Alias
  Else
    Let $GEXXX905_INCLUDE_HRLY_RT_CRITERIA = $GEXXX905_INCLUDE_HRLY_RT_CRITERIA || ')'
    Let $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA = $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA || ')'
    Let $GEXXX905_INCLUDE_HRLY_RT_DISPLAY_CRITERIA = $GEXXX905_INCLUDE_HRLY_RT_CRITERIA
    Let $GEXXX905_EXCLUDE_HRLY_RT_DISPLAY_CRITERIA = $GEXXX905_EXCLUDE_HRLY_RT_CRITERIA
  End-If

End-Procedure

