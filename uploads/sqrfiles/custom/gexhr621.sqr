!***********************************************************************
! GEXHR621: Non-Pharmacy Rolodex file to STARS                          *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXHR621: Non-Pharmacy Rolodex file to STARS                          *
!                                                                      *        
! Narrative:      This is a copy of the program GEXHR620.sqr           *
!                 for non-pharmacy                                     *
! #Debugx Used:         #debug9 paragraph trace                        *
!                                                                      *  
! SQL Tables:           PS_JOB                                         *
!                       PS_PERSONAL_DATA                               *
!                       PS_DEPT_TBL                                    *
!                       PS_GEX_STARS_TM_NP                             *
! Written by:           Jyotsna Negi                                   *       
!                                                                      *
! Normally Run:         Daily                                          *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS      DATE            DESCRIPTION OF THE CHANGE              *
!***********************************************************************
! GEXHR_848     07/06/2016      Initial Development                    *
! GEX_SEC_REENG 2015-10-03 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'


Begin-Report
#debug9 Show 'Begin-Report'

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc
  
  show ' '
  display 'Start Run Time: ' noline
  show $AsofNow
  Do P100-Initialization
  Do Open-File
  Do Main-Process
  Do Close-File
  show '#Rows_Updated :' #Rows_Updated
  show '#Rows_Inserted:' #Rows_Inserted
  show ' '
  display 'End Run Time: ' noline
  show $AsofNow
  do Stdapi-Term
    
End-Report


!****************************************************************************
Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
   
  Let $ReportID      = 'GEXHR621'
  Let $ReportTitle   = 'Non-Pharmacy Rolodex file to STARS'
  Let $delim = '|'
  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show '$ReportDate  = ' $ReportDate
  Show '$ReportTime  = ' $ReportTime
 
End-Procedure

!**************************************************************************
Begin-Procedure Open-File
#debug9 Show 'P140-Open-File'
  #ifdef UNIX
   Let $File = '{OUTFILE}' || 'STARSnonpharm.txt'
  #end-if
  #ifdef NT  
   LET $File = '\\moveitkdcfsrv01\fileserv$\Central\ToEncrypt\MRT STARS\' || 'STARSnonpharm.txt'

   show ' $File :' $File
  #end-if 
   OPEN $File as 1 
   for-writing
   record=1600    
   status = #Open_mem
   !show '$File  '   $File 
    
   if (#Open_mem <> 0)
      let $Error-Message = 'PROGRAM ABORTED - Problem opening file : ' || $File
      show $Error-Message
      STOP
   end-if
   
End-Procedure

!-----------------------------------------------------------------------
! Function:    Main-Process                                            -
! Description: Main-Process                                            -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Main-Process 
  Let $GEXXX916_Jobcode_Alias = 'J.JOBCODE'
  Do GEXXX916-Select-Jobcode-Parameters
Begin-Select 
!M.EMPLID,             !GEX_SEC_REENG 2016-11-14 Jyotsna
J.EMPLID,
J.EMPL_RCD,
P.FIRST_NAME,
P.LAST_NAME,
P.MIDDLE_NAME, 
P.ADDRESS1,       
P.ADDRESS2,       
P.CITY,       
P.STATE,       
P.POSTAL,   
P.COUNTY,   
DECODE(P.PHONE,' ','000/000-0000',P.PHONE) &P.PHONE,       
J.LAST_HIRE_DT,       
J.EMPL_STATUS,       
J.FULL_PART_TIME,
J.DEPTID,       
D.DESCR,
J.JOBCODE  
  show '&J.EMPLID :' &J.EMPLID
  
  do Get-Prior-Row
  
  if $Emplid_Found='Y'
     do Update-Temp-Table
  else
     do Insert-Temp-Table
  End-if   
  do Write-Report
  
FROM PS_PERSONAL_DATA P,
     PS_JOB J,
!GEX_SEC_REENG 2016-10-03 Vahini Katta Begin
     !PS_DEPT_TBL D,
     !PS_GEX_EMPLID_MAPP M
     PS_DEPT_TBL D
!GEX_SEC_REENG 2016-10-03 Vahini Katta End
WHERE P.EMPLID=J.EMPLID
!AND J.EMPLID=M.SSN                                 !GEX_SEC_REENG 2016-10-03 Vahini Katta
!AND J.EMPL_RCD=M.EMPL_RCD                          !GEX_SEC_REENG 2016-10-03 Vahini Katta 
AND J.EFFDT=(SELECT MAX(J1.EFFDT) FROM PS_JOB J1
             WHERE J.EMPLID=J1.EMPLID
             AND J.EMPL_RCD=J1.EMPL_RCD
             AND J1.EFFDT<=SYSDATE)
AND J.EFFSEQ=(SELECT MAX(J2.EFFSEQ) FROM PS_JOB J2
              WHERE J2.EMPLID=J.EMPLID
              AND J2.EMPL_RCD=J.EMPL_RCD
              AND J2.EFFDT=J.EFFDT)              
AND [$GEXXX916_INCLUDE_JOBCODE_CRITERIA]
AND D.EFFDT=(SELECT MAX(D1.EFFDT) FROM PS_DEPT_TBL D1
             WHERE D1.SETID=D.SETID
               AND D1.DEPTID=D.DEPTID
               AND D1.EFFDT<=SYSDATE)
AND J.DEPTID=D.DEPTID               
AND NOT EXISTS ( SELECT 'X' FROM PS_GEX_STARS_TM_NP H
                 WHERE H.EMPLID=J.EMPLID 
                   AND H.EMPL_RCD=J.EMPL_RCD
                   AND H.FIRST_NAME=P.FIRST_NAME 
                   AND H.LAST_NAME=P.LAST_NAME 
                   AND H.MIDDLE_NAME=P.MIDDLE_NAME
                   AND H.ADDRESS1=P.ADDRESS1
                   AND H.ADDRESS2=P.ADDRESS2
                   AND H.CITY=P.CITY
                   AND H.STATE=P.STATE
                   AND H.POSTAL=P.POSTAL
                   AND H.COUNTY=P.COUNTY
                   AND DECODE(H.PHONE,' ','000/000-0000',H.PHONE)=DECODE(P.PHONE,' ','000/000-0000',P.PHONE)
                   AND NVL(H.LAST_HIRE_DT,SYSDATE)=NVL(J.LAST_HIRE_DT,SYSDATE)
                   AND H.EMPL_STATUS=J.EMPL_STATUS
                   AND H.FULL_PART_TIME=J.FULL_PART_TIME
                   AND H.DEPTID=J.DEPTID
                )
ORDER BY P.LAST_NAME,P.FIRST_NAME                             	 
End-Select	 
End-Procedure Main-Process	

!-----------------------------------------------------------------------
! Function:    Get-Prior-Row                                           -
! Description: Compare the row with prior row to see if any changes    -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Get-Prior-Row
 Move 'N' to $Emplid_Found
Begin-Select
H.EMPLID
H.FIRST_NAME,
H.LAST_NAME,
H.MIDDLE_NAME, 
H.ADDRESS1,       
H.ADDRESS2,       
H.CITY,       
H.STATE,       
H.POSTAL,   
H.COUNTY,   
H.PHONE,       
H.LAST_HIRE_DT,       
H.EMPL_STATUS,       
H.FULL_PART_TIME
  
  Move 'Y' to $Emplid_Found
  show '$Emplid_Found :' $Emplid_Found
  
FROM PS_GEX_STARS_TM_NP H
WHERE H.EMPLID=&J.EMPLID
AND H.EMPL_RCD=&J.EMPL_RCD
End-Select
End-Procedure

!-----------------------------------------------------------------------
! Function:    Insert-Temp-Table                                       -
! Description: Inserts row to temp table                               -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Insert-Temp-Table
Begin-SQL
INSERT INTO PS_GEX_STARS_TM_NP
VALUES
(&J.EMPLID,
&J.EMPL_RCD,
&P.FIRST_NAME,
&P.LAST_NAME,
&P.MIDDLE_NAME, 
&P.ADDRESS1,       
&P.ADDRESS2,       
&P.CITY,       
&P.STATE,       
&P.POSTAL,   
&P.COUNTY,   
&P.PHONE,       
&J.LAST_HIRE_DT,       
&J.EMPL_STATUS,       
&J.FULL_PART_TIME,
&J.DEPTID,
&J.JOBCODE)
End-SQL
 if isnull($sql-error)
   add 1 to #Rows_Inserted
 end-if  
End-Procedure Insert-Temp-Table

!-----------------------------------------------------------------------
! Function:    Update-Temp-Table                                       -
! Description: Updates temp table                                      -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Update-Temp-Table
Begin-SQL
UPDATE PS_GEX_STARS_TM_NP 
SET EMPLID=&J.EMPLID,    
    EMPL_RCD=&J.EMPL_RCD,       
    FIRST_NAME=&P.FIRST_NAME,        
    LAST_NAME=&P.LAST_NAME,         
    MIDDLE_NAME=&P.MIDDLE_NAME,       
    ADDRESS1=&P.ADDRESS1,          
    ADDRESS2=&P.ADDRESS2,          
    CITY=&P.CITY,              
    STATE=&P.STATE,             
    POSTAL=&P.POSTAL,            
    COUNTY=&P.COUNTY,            
    PHONE=&P.PHONE,             
    LAST_HIRE_DT=&J.LAST_HIRE_DT,           
    EMPL_STATUS=&J.EMPL_STATUS,       
    FULL_PART_TIME=&J.FULL_PART_TIME,
    DEPTID=&J.DEPTID,
	JOBCODE=&J.JOBCODE
WHERE EMPLID=&J.EMPLID  
AND EMPL_RCD=&J.EMPL_RCD  
End-SQL
  if isnull($sql-error)
   add 1 to #Rows_Updated
   show 'update &J.EMPLID:' &J.EMPLID
 end-if  
End-Procedure

!-----------------------------------------------------------------------
! Function:    Write-Report                                            -
! Description: Write the Current Row                                   -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Write-Report

   !write 1 from &M.EMPLID                $delim !GEX_SEC_REENG 2016-10-03 Vahini Katta
   write 1 from &J.EMPLID                $delim  !GEX_SEC_REENG 2016-10-03 Vahini Katta 
                &J.EMPL_RCD              $delim
                &P.FIRST_NAME            $delim
                &P.LAST_NAME             $delim
                &P.MIDDLE_NAME           $delim
                &P.ADDRESS1              $delim
                &P.ADDRESS2              $delim
                &P.CITY                  $delim
                &P.STATE                 $delim
                &P.POSTAL                $delim
                &P.COUNTY                $delim
                &P.PHONE                 $delim
                &J.LAST_HIRE_DT          $delim
                &J.EMPL_STATUS           $delim
                &J.FULL_PART_TIME        $delim
                &J.DEPTID                $delim
                &D.DESCR
                
End-Procedure Write-Report

!-----------------------------------------------------------------------
! Function:    Close-File                                              -
! Description: Closes the File                                         -
! Called By:   Main-Process                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
Begin-Procedure Close-File
  Close 1

  If #filestat != 0
     Show 'Error closing output file.  Program terminating.'
     Stop 
  End-If
End-Procedure

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'timemath.sqc'
#Include 'gexxx916.sqc'  !Get RunControl JobCodes
