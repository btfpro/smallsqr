!***********************************************************************
! GEXPL030.SQR:                                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-2006 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL030.SQR :                                                       *
!                                                                      *
! Narrative:            This program was cloned from the Query         *
!					      . ITG # 35811            *
!			Create a report that lists any newly hired     *
!                       retail employee that is classified as 	       *
!                       full-time or any active retail employee that   *
!                       went from part-time to full-time status.       *
! #Debug Used:          x                                              *
!								       *
! SQL Tables:           PS_JOB					       *
!                       PS_PERSONAL_DATA                               *
!            		PS_EMPLOYMENT                                  *
!            		PS_JOBCODE_TBL                                 * 
!								       *
! Written by:           Rapidigm, Vinay, 04/24/06                      *
!                                                                      *
! Normally Run:         This process will run weekly after the Tuesday *
!                        (‘B’ run) payroll cycle.                      *
!                                                                      *
! Control Parms:        Reads the run control table PS_GEX_RUN_CNTL    *
!								       *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! GEX isdvnpk      03/25/2008 for rehire dt issue in 90                *
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup02.sqc'   !Printer and Page-Size Initialization (Landscape)

  #define 	#column1 	001
  #define 	#column2 	008
  #define 	#column3 	037
  #define 	#column4 	050
  #define 	#column5 	062
  #define 	#column6 	071
  #define 	#column7 	104
  #define 	#column8 	112
  #define 	#column9 	121
  #define 	#column10	128
  #define 	#column11	137
  #define 	#column12	171
  
 

!***********************************************************************
! Defines Heading of the report
!***********************************************************************
Begin-Heading 7

let $ReportID = 'GEXPL030'
#include 'stdhdg01.sqc'

	Print 'Retail FT Audit'     (3,1) center

Print 'DEPT'	(+3,{#column1})
Print 'NAME'	(+0,{#column2}) underline
Print 'HIRE'	(+0,{#column3})
Print 'RE-HIRE'	(+0,{#column4})
Print 'NEW'	(+0,{#column5})
Print 'NEW'	(+0,{#column6})
Print 'NEW'	(+0,{#column7})
Print 'HOURLY'	(+0,{#column8})
Print 'UNION'	(+0,{#column9})
Print 'OLD'	(+0,{#column10})
Print 'OLD'	(+0,{#column11})
Print 'OLD'	(+0,{#column12})

Print 'ID'		(+1,{#column1}) underline
Print 'DATE'		(+0,{#column3}) underline
Print 'DATE'		(+0,{#column4}) underline
Print 'JOBCODE'		(+0,{#column5}) underline
Print 'JOB TITLE'	(+0,{#column6}) underline
Print 'FT/PT'		(+0,{#column7}) underline
Print 'RATE'		(+0,{#column8})	underline
Print 'CODE'		(+0,{#column9})	underline
Print 'JOBCODE'		(+0,{#column10}) underline	
Print 'JOB TITLE'	(+0,{#column11}) underline	
Print 'FT/PT'		(+0,{#column12}) underline	
Print ' ' (+1,1)

!    Print 'DEPTID'          	(+3,{#column1})
!    Print 'NAME'          	(+0,{#column2})
!    Print 'HIRE_DT'            	(+0,{#column3})
!    Print 'REHIRE_DT'     	(+0,{#column4})
!    Print 'JOBCODE'        	(+0,{#column5})
!    print 'New Job Title'     	(+0,{#column6}) 
!    print 'FULL_PART_TIME'     	(+0,{#column7})
!    print 'HOURLY_RT'  	        (+0,{#column8})
!    print 'UNION_CD'    	(+0,{#column9})
!    print 'JOBCODE'	        (+0,{#column10})
!    print 'Old Job Title'  	(+0,{#column11})
!    print 'Old EE Status'	(+0,{#column12})
 
			   
End-Heading


!***********************************************************************
! Start of Program
!***********************************************************************
Begin-Report

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Stdapi-Init             !stdapi.sqc
  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = 'gexpl030'
  end-if
  
  do Get-Run-Control	     !gexrnctl.sqc  

  if isnull(&thrudate)
  	do Select-Pay-End-Date
	let $thrudate = $xxx_pay_end_dt
  else
        let $thrudate = &thrudate
  end-if

  if isnull(&fromdate)
    	do Select-Pay-End-Date
	let $fromdate = $xxx_pay_begin_dt
  else
	let $fromdate = &fromdate
  end-if

  #debugx show '<' $fromdate '><' $thrudate '>'

  do Select-Data
  do stdapi-term

End-Report


!********************************************************
! Procedure used to select current pay_begin_dt and 
! pay_end_dt if none specified on the run control page
!********************************************************
begin-procedure Select-Pay-End-Date
begin-select distinct
max(xxx.pay_end_dt)	&xxx.pay_end_dt
max(xxx.pay_begin_dt)   &xxx.pay_begin_dt

   move &xxx.pay_end_dt to $xxx_pay_end_dt
   move &xxx.pay_begin_dt to $xxx_pay_begin_dt
  
from ps_pay_calendar xxx
where xxx.pay_confirm_run = 'Y'
and xxx.company <= '999'
and xxx.pay_end_dt <= sysdate

end-select
end-procedure 



!***********************************************************************
! Main Procedure which selects the data.
!***********************************************************************
Begin-Procedure Select-Data
	Let $GEXXX903_Emp_Stat_Alias = 'A.EMPL_STATUS'                      
	    Do GEXXX903-Select-Emp-Stat-Parameters 
	Let $GEXXX901_Company_Alias  = 'A.COMPANY'                           
	Let $GEXXX901_Paygroup_Alias = 'A.PAYGROUP'                     
	    Do GEXXX901-Select-Company-Paygroup-Parameters
	
	Let $GEXXX913_UNION_CD_Alias = 'A.UNION_CD'                         
	    Do GEXXX913-Select-UNION-CD-Parameters  
	    
	if  &incl_excl='I'
	    Let $GEXXX913_INCL_EXCL_UNION_CD_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA 
	else
	    Let $GEXXX913_INCL_EXCL_UNION_CD_CRITERIA = $GEXXX913_EXCLUDE_UNION_CD_CRITERIA                                
	End-if

	#debugx show $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
        #debugx show $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
	#debugx show $GEXXX913_INCL_EXCL_UNION_CD_CRITERIA

Begin-Select distinct
A.DEPTID			        &deptid
C.NAME 					&name
TO_CHAR(D.HIRE_DT,'YYYY-MM-DD')		&hire_dt
! GEX isdvnpk      03/25/2008 for rehire dt issue in 90   
TO_CHAR((SELECT JREH.EFFDT FROM PS_JOB JREH WHERE JREH.EMPLID = A.EMPLID AND JREH.EMPL_RCD  = A.EMPL_RCD  AND JREH.EFFDT =
        (SELECT MAX(JREH1.EFFDT) FROM PS_JOB JREH1 WHERE JREH.EMPLID = JREH1.EMPLID AND JREH.EMPL_RCD  = JREH1.EMPL_RCD
          AND JREH1.EFFDT <= SYSDATE AND JREH1.ACTION = 'REH') 
    AND JREH.EFFSEQ = (SELECT MAX(JREH2.EFFSEQ) FROM PS_JOB JREH2 WHERE JREH.EMPLID = JREH2.EMPLID AND JREH.EMPL_RCD = JREH2.EMPL_RCD
          AND JREH.EFFDT     = JREH2.EFFDT AND JREH2.ACTION      = 'REH')
    AND JREH.EFFDT >=	NVL((SELECT JREHA.EFFDT FROM PS_JOB JREHA WHERE JREHA.EMPLID = JREH.EMPLID AND JREHA.EMPL_RCD  = JREH.EMPL_RCD
    	  AND JREHA.EFFDT = (SELECT MAX(JREHA1.EFFDT) FROM PS_JOB JREHA1 WHERE JREHA.EMPLID = JREHA1.EMPLID
          AND JREHA.EMPL_RCD  = JREHA1.EMPL_RCD AND JREHA1.EFFDT <= SYSDATE AND JREHA1.ACTION = 'HIR') 
    	  AND JREHA.EFFSEQ = (SELECT MAX(JREHA2.EFFSEQ) FROM PS_JOB JREHA2 WHERE JREHA.EMPLID = JREHA2.EMPLID
          AND JREHA.EMPL_RCD  = JREHA2.EMPL_RCD AND JREHA.EFFDT = JREHA2.EFFDT AND JREHA2.ACTION = 'HIR')), '01-jan-1900')),'YYYY-MM-DD')	&rehire_dt
! GEX isdvnpk      03/25/2008 for rehire dt issue in 90        
A.JOBCODE				&jobcode1
B.DESCR					&descr1
A.FULL_PART_TIME			&full_part_time1
A.HOURLY_RT				&hourly_rt
A.UNION_CD				&union_cd
E.JOBCODE				&jobcode2
F.DESCR					&descr2
E.FULL_PART_TIME			&full_part_time2
   
  
	  move &hourly_rt to $hourly_rt 999.99
          do Print-Data    


  FROM PS_JOB A, 
            PS_JOBCODE_TBL B,
            PS_PERSONAL_DATA C, 
            PS_EMPLOYMENT D,
            PS_JOB E, 
            PS_JOBCODE_TBL F
  WHERE ( A.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_JOB A_ED
        WHERE A.EMPLID = A_ED.EMPLID
          AND A.EMPL_RCD = A_ED.EMPL_RCD
          AND A_ED.EFFDT <= SYSDATE)
    AND A.EFFSEQ =
        (SELECT MAX(A_ES.EFFSEQ) FROM PS_JOB A_ES
        WHERE A.EMPLID = A_ES.EMPLID
          AND A.EMPL_RCD = A_ES.EMPL_RCD
          AND A.EFFDT = A_ES.EFFDT)
     AND B.JOBCODE = A.JOBCODE
     AND B.SETID = A.SETID_JOBCODE
     AND B.EFFDT =
        (SELECT MAX(B_ED.EFFDT) FROM PS_JOBCODE_TBL B_ED
        WHERE B.SETID = B_ED.SETID
          AND B.JOBCODE = B_ED.JOBCODE
          AND B_ED.EFFDT <= SYSDATE)
     AND A.EMPLID = C.EMPLID
     AND A.EMPLID = D.EMPLID
     AND A.EMPL_RCD = D.EMPL_RCD
     AND A.EMPLID = E.EMPLID
     AND A.EMPL_RCD = E.EMPL_RCD                      
     AND E.EFFDT BETWEEN $fromdate AND $thrudate      
     AND F.SETID = E.SETID_JOBCODE
     AND F.JOBCODE = E.JOBCODE
     AND F.EFFDT =
        (SELECT MAX(F_ED.EFFDT) FROM PS_JOBCODE_TBL F_ED
        WHERE F.SETID = F_ED.SETID
          AND F.JOBCODE = F_ED.JOBCODE
          AND F_ED.EFFDT <= SYSDATE)                     
     AND [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
     AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
     AND [$GEXXX913_INCL_EXCL_UNION_CD_CRITERIA]     
     AND A.FULL_PART_TIME = 'F'
     AND (( D.HIRE_DT BETWEEN $fromdate AND $thrudate
     ! GEX isdvnpk      03/25/2008 for rehire dt issue in 90   
     !OR D.REHIRE_DT BETWEEN $fromdate AND $thrudate)
     OR (SELECT
JREHX.EFFDT
FROM PS_JOB JREHX
  WHERE JREHX.EMPLID    = A.EMPLID
    AND JREHX.EMPL_RCD  = A.EMPL_RCD
    AND JREHX.EFFDT =
        (SELECT MAX(JREHX1.EFFDT) FROM PS_JOB JREHX1
        WHERE JREHX.EMPLID    = JREHX1.EMPLID
          AND JREHX.EMPL_RCD  = JREHX1.EMPL_RCD
          AND JREHX1.EFFDT      <= sysdate
          AND JREHX1.ACTION      = 'REH') 
    AND JREHX.EFFSEQ =
        (SELECT MAX(JREHX2.EFFSEQ) FROM PS_JOB JREHX2
        WHERE JREHX.EMPLID    = JREHX2.EMPLID
          AND JREHX.EMPL_RCD  = JREHX2.EMPL_RCD
          AND JREHX.EFFDT     = JREHX2.EFFDT
          AND JREHX2.ACTION      = 'REH')
    AND JREHX.EFFDT >=	NVL((SELECT JREHXA.EFFDT
		FROM PS_JOB JREHXA
  		WHERE JREHXA.EMPLID  = JREHX.EMPLID
    		AND JREHXA.EMPL_RCD  = JREHX.EMPL_RCD
    		AND JREHXA.EFFDT =
        		(SELECT MAX(JREHXA1.EFFDT) FROM PS_JOB JREHXA1
        		WHERE JREHXA.EMPLID    = JREHXA1.EMPLID
          		AND JREHXA.EMPL_RCD  = JREHXA1.EMPL_RCD
          		AND JREHXA1.EFFDT      <= sysdate
          		AND JREHXA1.ACTION      = 'HIR') 
    		AND JREHXA.EFFSEQ =
        		(SELECT MAX(JREHXA2.EFFSEQ) FROM PS_JOB JREHXA2
        		WHERE JREHXA.EMPLID    = JREHXA2.EMPLID
          		AND JREHXA.EMPL_RCD  = JREHXA2.EMPL_RCD
          		AND JREHXA.EFFDT     = JREHXA2.EFFDT
          		AND JREHXA2.ACTION      = 'HIR')), '01-jan-1900')) BETWEEN $fromdate AND $thrudate)
     ! GEX isdvnpk      03/25/2008 for rehire dt issue in 90   
     OR E.FULL_PART_TIME = 'P') )
  ORDER BY 1, 2   
     

End-Select
End-Procedure


!***********************************************************************
! Prints Data
!***********************************************************************
Begin-Procedure Print-Data
		
   	print	      &deptid	         (+2,{#column1})
    	print	      &name	         (+0,{#column2})
    	print	      &hire_dt	         (+0,{#column3})
    	print	      &rehire_dt	 (+0,{#column4})
    	print	      &jobcode1	         (+0,{#column5})
    	print	      &descr1	         (+0,{#column6}) 
    	print	      &full_part_time1	 (+0,{#column7})
    	print	      $hourly_rt	 (+0,{#column8})
    	print	      &union_cd	         (+0,{#column9})
    	print	      &jobcode2	         (+0,{#column10})
    	print	      &descr2	         (+0,{#column11})
    	print	      &full_part_time2	 (+0,{#column12})    	   	
	


End-Procedure 


#include 'stdapi.sqc'    !Process Scheduler Interface
#include 'reset.sqc'     !Reset Printer Procedure
#include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date math
#include 'gexrnctl.sqc'  !Routine for Get-Run-Control
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#include 'gexxx901.sqc'	 !INCLUDE_DIV_CD_CRITERIA
#include 'gexxx967.sqc'	 !INCLUDE_RGNCD_CRITERIA
#include 'gexxx903.sqc'	 !INCLUDE_EMPL_STATUS
#include 'gexxx913.sqc'	 !INCLUDE_RGNCD_CRITERIA

