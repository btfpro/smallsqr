!***********************************************************************
! GEXPY096  CURRENT PAY PERIOD EARNINGS                                *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY096 :            CURRENT PAY PERIOD EARNINGS                    *
!                                                                      *
! Narrative:            This program will produce a report of current  *
!                       earnings for the pay period.                   *
!                                                                      *
! SQL Tables:           pay_earnings                                   *
!                       pay_oth_earns                                  *
!                       pay_cal_bal_id                                 *
!                                                                      *
! Temp Tables:          gex_r_gexpy096                                 *
!                                                                      *
! Written by:           Tony Labra                                     *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Multiple Deptids                             *
!                         Multiple Cmpy/Paygroups                      *
!                         Multiple JobCodes                            *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! AXL       06/10/99    Initial Creation                               *
! JDH       10/22/1999  Changed delete table to truncate table         *
! GEX_PY258 08/22/2007  Sqr from 8.3 is moved to 9.0 as it is.         *
!                       No changes are made.                           *     
! ISDVSXP   23/07/2008  ITG #51358- ADD ERNCD 173. No changes are made.*  
!								       *
! VENDTKK   01/28/2009  Added INCLUDE GETPOSNM.SQC to pull             *
!                  	JOBTITLE from POSITION_DATA.DESCR   	       *
!			field instead of JOBCODE_TBL.DESCR             *
!			           	                               *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization

#define #col_jobcode	1
#define #col_erncode	41
#define #col_curr_earns 81

!***********************************************************************
Begin-Report
!***********************************************************************
 
  Do Initialization
  Do Main-Process
  Do Stdapi-Term
  Do EOJ
End-Report

!***********************************************************************
Begin-Heading 7
!***********************************************************************
  #Include 'stdhdg02.sqc'

  Print $pay_period         (3,)  Center

  if $Deptid <> $priorDeptid
    do Get-Department-Name
    move $Deptid to $priorDeptid
  end-if

  print 'Deptid'               (4,1)
  print $Deptid                (0,13)
  print $DeptName              (0,+2)

  Print 'JOB CODE - TITLE'   			(+2,{#col_jobcode})
  Print 'EARN CD - DESCRIPTION'	        	(+0,{#col_erncode})
  Print '     EARNINGS'			        (+0,{#col_curr_earns})

  Print '-'                    (+1,{#col_jobcode},95)  Fill
  
End-Heading

!***********************************************************************
Begin-Procedure Initialization
!***********************************************************************

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc
  Do Truncate-Temp-Table

  Let $GEXXX900_Company_Alias  = 'pe.company'
  Let $GEXXX901_Company_Alias  = 'pe.company'
  Let $GEXXX901_Paygroup_Alias = 'pe.paygroup'
  Let $GEXXX902_Deptid_Alias   = 'pe.deptid'
  Let $GEXXX916_Jobcode_Alias  = 'pe.jobcode'

  Let $GEXXX900_Company_Alias_OT  = 'peot.company'
  Let $GEXXX901_Company_Alias_OT  = 'peot.company'
  Let $GEXXX901_Paygroup_Alias_OT = 'peot.paygroup'
  Let $GEXXX902_Deptid_Alias_OT   = 'peot.deptid'
  Let $GEXXX916_Jobcode_Alias_OT  = 'peot.jobcode'

  Let $GEXXX900_Company_Alias_OTH  = 'peoth.company'
  Let $GEXXX901_Company_Alias_OTH  = 'peoth.company'
  Let $GEXXX901_Paygroup_Alias_OTH = 'peoth.paygroup'
  Let $GEXXX902_Deptid_Alias_OTH   = 'peoth.deptid'
  Let $GEXXX916_Jobcode_Alias_OTH  = 'peoth.jobcode'
   
  If $prcs_process_instance = ''
    Do SQRW-Run-Controls
  Else
    Do Process-Scheduler-Run-Controls
  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Let $ReportID      = 'GEXPY096'
  Let $ReportTitle   = 'CURRENT PAY PERIOD EARNINGS'
  Let $Order_By      = 'Company, DeptID, Jobcode, Earnings Amount'
   
  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  Show '$GEXXX900_Include_Company_Criteria 	    = ' $GEXXX900_Include_Company_Display_Criteria
  Show '$GEXXX901_Include_Company_Paygroup_Criteria = ' $GEXXX901_Include_Company_Paygroup_Display_Criteria
  Show '$GEXXX902_Include_Deptid_Criteria           = ' $GEXXX902_Include_Deptid_Display_Criteria
  Show '$GEXXX916_Include_Jobcode_Criteria          = ' $GEXXX916_Include_Jobcode_Display_Criteria
  Show '$WHERE_SELECT_ASOFDATE                      = ' $WHERE_SELECT_ASOFDATE 
  Show '$ORDER_BY                                   = ' $ORDER_BY
  
  Do Get-Last-Confirmed-Pay
  Do Print-Cover-Page

End-Procedure

!***********************************************************************
Begin-Procedure SQRW-Run-Controls
!***********************************************************************

  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA 	= '1=1'
  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT 	= '1=1'
  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH 	= '1=1'
  Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA= 'ALL'
  
  Display 'Enter Company/Paygroup or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''')'
        Let $Include_Condition_OT = '(' || $GEXXX901_Company_Alias_OT || ' = '''  || $In-Company || ''')'
	Let $Include_Condition_OTH = '(' || $GEXXX901_Company_Alias_OTH || ' = '''  || $In-Company || ''')'
      Else
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || $In-Paygroup || ''')'
        Let $Include_Condition_OT = '(' || $GEXXX901_Company_Alias_OT || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias_OT || ' = '''  || $In-Paygroup || ''')'
        Let $Include_Condition_OTH = '(' || $GEXXX901_Company_Alias_OTH || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias_OTH || ' = '''  || $In-Paygroup || ''')'
      End-If
      Add 1 To #GEX_RC_PAYGROUP_ROWS
      If #GEX_RC_PAYGROUP_ROWS = 1
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA   = '(' || $Include_Condition
	Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT= '(' || $Include_Condition_OT
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH= '(' || $Include_Condition_OTH
      Else
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT || ' Or '  || $Include_Condition_OT
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT || ' Or '  || $Include_Condition_OTH
      End-If
    End-If
  End-While

  If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If

  Let $DeptIDString = ''''
  Display 'Enter DeptID or leave blank to exit.'
  While 1=1
    Input $In-DeptID Maxlen=10 'DeptID'
    Uppercase $In-DeptID
    If Rtrim($In-DeptID, ' ') = ''
      Concat '''' With $DeptIDString
      Break
    End-If
    If $DeptIDString <> ''''
      Concat ''',''' With $DeptIDString
    End-If
    Concat $In-DeptID With $DeptIDString
  End-While

  If $DeptIDString = ''''''
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT = '1=1'
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH = '1=1'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT = $GEXXX902_Deptid_Alias_OT || ' In (' || $DeptIDString || ')'
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH = $GEXXX902_Deptid_Alias_OTH || ' In (' || $DeptIDString || ')'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
  End-If

  Let $JobcodeString = ''''
  Display 'Enter Jobcode or leave blank to exit.'
  While 1=1
    Input $In-Jobcode Maxlen=6 'Jobcode'
    Uppercase $In-Jobcode
    If Rtrim($In-Jobcode, ' ') = ''
      Concat '''' With $JobcodeString
      Break
    End-If
    If $JobcodeString <> ''''
      Concat ''',''' With $JobcodeString
    End-If
    Concat $In-Jobcode With $JobcodeString
  End-While

  If $JobcodeString = ''''''
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = '1=1'
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT = '1=1'
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH = '1=1'
    Let $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA = $GEXXX916_Jobcode_Alias || ' In (' || $JobcodeString || ')'
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT = $GEXXX916_Jobcode_Alias_OT || ' In (' || $JobcodeString || ')'
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH = $GEXXX916_Jobcode_Alias_OTH || ' In (' || $JobcodeString || ')'
    Let $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA = $GEXXX916_INCLUDE_JOBCODE_CRITERIA
  End-If

  Do Ask-As-Of-Date
  Let $WHERE_SELECT_ASOFDATE = $AsOfDate
  
End-Procedure

!***********************************************************************
Begin-Procedure Process-Scheduler-Run-Controls
!***********************************************************************
  
  Do GEXXX922-Select-Parameters
  Let $WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.ASOFDATE

  if rtrim($WHERE_SELECT_ASOFDATE, ' ' ) = ''
    let $WHERE_SELECT_ASOFDATE = $asoftoday
  end-if

  LET $AsOfDate = $WHERE_SELECT_ASOFDATE 

  Do GEXXX900-Select-Company-Parameters	
  Do GEXXX900-Select-Company-Parameters_X		! Modified copy of gexxx900.sqc
  
  Do GEXXX901-Select-Company-Paygroup-Parameters
  Do GEXXX901-Select-Company-Paygroup-Parameters_X	! Modified copy of gexxx901.sqc
  
  Do GEXXX902-Select-Deptid-Parameters
  Do GEXXX902-Select-Deptid-Parameters_X		! Modified copy of gexxx902.sqc
  
  Do GEXXX916-Select-Jobcode-Parameters
  Do GEXXX916-Select-Jobcode-Parameters_X   		! Modified copy of gexxx916.sqc
   
End-Procedure

!***********************************************************************
Begin-Procedure Get-Last-Confirmed-Pay
!***********************************************************************
Begin-Select
Max(pay_end_dt) &pay_end_dt

  Let $pay_end_dt = &pay_end_dt
  
  Do Format-DateTime($pay_end_dt, $payenddt, {DEFMDY}, '', '')
  Let $Pay_Period = 'FOR PAY PERIOD ENDING ' || $payenddt


  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  Else
    Show 'Last confirmed pay end date:  ' $pay_end_dt
  End-If

From   ps_pay_calendar

Where  pay_end_dt <= $AsOfToday
And    pay_confirm_run = 'Y'

End-Select
End-Procedure

!***********************************************************************
begin-procedure Print-Cover-Page
!***********************************************************************

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)

  Print '$GEXXX900_Include_Company_Criteria                   ='   (+1,5)
  Print  $GEXXX900_Include_Company_Display_Criteria                (0,+2)

  Print '$GEXXX901_Include_Company_Paygroup_Criteria          ='   (+1,5)
  Print  $GEXXX901_Include_Company_Paygroup_Display_Criteria       (0,+2)

  Print '$GEXXX902_Include_Deptid_Criteria                    ='   (+1,5)
  Print $GEXXX902_Include_Deptid_Display_Criteria                  (0,+2)

  Print '$GEXXX916_Include_Jobcode_Criteria                   ='   (+1,5)
  Print $GEXXX916_Include_Jobcode_Display_Criteria                 (0,+2)

  Print '$WHERE_SELECT_ASOFDATE                               ='   (+1,5)
  Print  $WHERE_SELECT_ASOFDATE                                    (0,+2)
  
  Print 'SORT ORDER FOR THIS REPORT RUN:'                          (+3,2)
  Print '$Order_By                                            ='   (+1,5)
  Print $Order_By                                                  (0,+2)

  Let #PAGE-COUNT = 0
 
End-Procedure

!*******************************************************************
Begin-procedure Truncate-Temp-Table
!*******************************************************************
 
begin-sql On-Error=SQL-Error-E('Truncate-Temp-Table')
Truncate Table PS_GEX_R_GEXPY096

end-sql

end-procedure

!***********************************************************************
Begin-Procedure Main-Process
!***********************************************************************
 
  Do Select-Reg-Earnings
  Do Select-OT-Earnings

  Do Select-OTH-Earnings
  Do Select-From-Temp

End-Procedure

!***********************************************************************
Begin-Procedure Select-Reg-Earnings
!***********************************************************************
Begin-Select On-Error=SQL-Error-E('Select-Reg-Earnings')
PE.COMPANY 
PE.DEPTID 
PE.JOBCODE 
PE.POSITION_NBR		!VENDTKK ITG#56786   28-Jan-2009
SUM(PE.REG_EARNS) + SUM(PE.REG_HRLY_EARNS) 	&reg_earns

    Let $Company 	=  &PE.COMPANY
    Let $DeptID  	=  &PE.Deptid
    Let $Jobcode        =  &PE.Jobcode
    Let $Erncd   	=  'REG'
    Let #Earns_Amt   	=  &reg_earns
    Let $Position_Nbr   =  &PE.Position_nbr  !VENDTKK ITG#56786   28-Jan-2009
     

    Do Insert-Into-Temp
 
FROM PS_PAY_EARNINGS PE
  WHERE  PE.PAY_END_DT =$pay_end_dt	
     AND [$GEXXX900_Include_Company_Criteria]
     AND [$GEXXX901_Include_Company_Paygroup_Criteria]
     AND [$GEXXX902_Include_Deptid_Criteria]
     AND [$GEXXX916_Include_Jobcode_Criteria]
  
GROUP BY PE.COMPANY, PE.DEPTID, PE.JOBCODE ,PE.POSITION_NBR		!VENDTKK ITG#56786   28-Jan-2009
HAVING SUM(PE.REG_EARNS) + SUM(PE.REG_HRLY_EARNS) <> 0

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Select-OT-Earnings
!***********************************************************************
Begin-Select On-Error=SQL-Error-E('Select-OT-Earnings')
PEOT.COMPANY 
PEOT.DEPTID 
PEOT.JOBCODE 
PEOT.POSITION_NBR		!VENDTKK ITG#56786   28-Jan-2009
SUM(PEOT.OT_HRLY_EARNS)	&ot_earns

    Let $Company 	=  &PEOT.COMPANY
    Let $DeptID  	=  &PEOT.Deptid
    Let $Jobcode        =  &PEOT.Jobcode
    Let $Erncd   	=  'OTP'
    Let #Earns_Amt   	=  &ot_earns
    Let $Position_Nbr   =  &PEOT.Position_nbr  !VENDTKK ITG#56786   28-Jan-2009

    Do Insert-Into-Temp
 
FROM PS_PAY_EARNINGS PEOT
  WHERE  PEOT.PAY_END_DT =$pay_end_dt	
     AND [$GEXXX900_Include_Company_Criteria_OT]
     AND [$GEXXX901_Include_Company_Paygroup_Criteria_OT]
     AND [$GEXXX902_Include_Deptid_Criteria_OT]
     AND [$GEXXX916_Include_Jobcode_Criteria_OT]
  
GROUP BY PEOT.COMPANY, PEOT.DEPTID,PEOT.JOBCODE,PEOT.POSITION_NBR		!VENDTKK ITG#56786   28-Jan-2009
HAVING SUM(PEOT.OT_HRLY_EARNS) <> 0

End-Select
End-Procedure

!***********************************************************************




!***********************************************************************
Begin-Procedure Select-OTH-Earnings
!***********************************************************************
Begin-Select On-Error=SQL-Error-E('Select-OTH-Earnings')
PEOTH.COMPANY 
PEOTH.DEPTID 
PEOTH.JOBCODE 
POE.ERNCD
PEOTH.POSITION_NBR		!VENDTKK ITG#56786   28-Jan-2009
SUM(POE.OTH_EARNS)   &POE.OTH_EARNS

    Let $Company 	=  &PEOTH.COMPANY
    Let $DeptID  	=  &PEOTH.Deptid
    Let $Jobcode        =  &PEOTH.Jobcode
    Let $Erncd   	=  &POE.ERNCD
    Let #Earns_Amt   	=  &POE.OTH_EARNS
    Let $Position_Nbr   =  &PEOTH.POSITION_NBR
    Let $Position_Nbr   =  &PEOTH.Position_nbr  !VENDTKK ITG#56786   28-Jan-2009

    Do Insert-Into-Temp
   
FROM PS_PAY_EARNINGS PEOTH, PS_PAY_OTH_EARNS POE
  WHERE  PEOTH.PAY_END_DT =$pay_end_dt	
     AND PEOTH.COMPANY = POE.COMPANY
     AND PEOTH.PAYGROUP = POE.PAYGROUP
     AND PEOTH.PAY_END_DT = POE.PAY_END_DT
     AND PEOTH.OFF_CYCLE = POE.OFF_CYCLE
     AND PEOTH.PAGE_NUM = POE.PAGE_NUM
     AND PEOTH.LINE_NUM = POE.LINE_NUM
     AND PEOTH.ADDL_NBR = POE.ADDL_NBR
  
     AND [$GEXXX900_Include_Company_Criteria_OTH]
     AND [$GEXXX901_Include_Company_Paygroup_Criteria_OTH]
     AND [$GEXXX902_Include_Deptid_Criteria_OTH]
     AND [$GEXXX916_Include_Jobcode_Criteria_OTH]
  
GROUP BY PEOTH.COMPANY, PEOTH.DEPTID, PEOTH.JOBCODE, POE.ERNCD, PEOTH.POSITION_NBR		!VENDTKK ITG#56786   28-Jan-2009
HAVING SUM(POE.OTH_EARNS) <> 0

End-Select
End-Procedure

!*********************************************************************
Begin-procedure Insert-Into-Temp
!*********************************************************************
begin-SQL On-Error=SQL-Error-E('Insert-Into-Temp')
Insert into PS_GEX_R_GEXPY096
     Values
       (nvl($company,' '),
	nvl($deptid,' ') ,
        nvl($jobcode,' '),
	nvl($erncd,' ')  ,
	nvl(#earns_amt,0))

end-sql
end-procedure

!***********************************************************************
Begin-Procedure Select-From-Temp
!***********************************************************************
Begin-Select On-Error=SQL-Error-E('Select-From-Temp')
T.COMPANY (,) On-Break
	       Print=Never
               Level=1
               After=Company-Totals

T.DEPTID  (,) On-Break
	       Print=Never
               Level=2
               Before=Before-DeptId
               After=DeptId-Totals

T.JOBCODE (,) On-Break
               Print=Never
               Level=3
               Before=Before-Jobcode
               After=Jobcode-Totals
T.ERNCD
SUM(T.EARNS_AMT) 	&earns

    Let $Company =  &T.COMPANY
    Let $DeptID  =  &T.Deptid
    Let $Erncd   =  &T.Erncd
    Let #EARNS   =  &EARNS 

    Add 1 to #record_count
    Do Print-Detail
   
FROM PS_GEX_R_GEXPY096 T
   
GROUP BY T.COMPANY, T.DEPTID, T.JOBCODE, T.ERNCD
ORDER BY T.COMPANY, T.DEPTID, T.JOBCODE, SUM(T.EARNS_AMT)

End-Select

    print 'Report Totals'	(+2,1)
    print #tot_earns		(,{#col_curr_earns})      edit 99,999,999.99

End-Procedure

!***********************************************************************
begin-procedure Company-Totals
!***********************************************************************

  print 'Company'	(+2,1)
  print $Company        (,+1)
  print 'Totals'	(,+1)
        
  print #cmpy_earns	(,{#col_curr_earns})      edit 99,999,999.99
  
  Add #cmpy_earns      To #tot_earns
  
  let #cmpy_earns      = 0
 
End-Procedure

!***********************************************************************
Begin-procedure Before-Deptid
!***********************************************************************

  New-Page
END-PROCEDURE

!***********************************************************************
begin-procedure DeptId-Totals
!***********************************************************************

  print 'Dept'		(+2,1)
  print $deptid	        (,+1)
  print 'Totals'	(,+1)
        
  print #dept_earns	(,{#col_curr_earns})      edit 99,999,999.99
  
  Add #dept_earns      To #cmpy_earns
  
  let #dept_earns      = 0
 
End-Procedure

!***********************************************************************
begin-procedure Before-Jobcode
!***********************************************************************
  Let $Jobcode  = &T.Jobcode
  Do Get-Job-Title
  !VENDTKK Begin   ITG#56786   28-Jan-2009

  Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosName = '' 
  		Let $jobtitle = $jobtitle
	 Else
 		Let $jobtitle = $PosName
	 End-if
!VENDTKK End   ITG#56786   28-Jan-2009	 
 
  Let $Jobcd_Print= rtrim($jobcode,' ')|| ' - ' || rtrim($Jobtitle,' ')

  print $Jobcd_Print         (+1,{#col_jobcode})
 
END-PROCEDURE

!***********************************************************************
begin-procedure Jobcode-Totals
!***********************************************************************

  print 'Jobcode'	(+2,1)
  print $jobcode        (,+1)
  print 'Totals'	(,+1)
        
  print #jc_earns	(,{#col_curr_earns})      edit 99,999,999.99
  position (+1)
 
  Add #jc_earns      To #dept_earns
  
  let #jc_earns      = 0
 
End-Procedure


!***********************************************************************
begin-procedure Print-Detail
!***********************************************************************

  Do Get-Earnings-Name

  Let $Erncd_Print= rtrim($Erncd,' ') || ' - ' || rtrim($EarningsName,' ')

  print $Erncd_Print        (+1,{#col_erncode})
  print #earns              (,{#col_curr_earns})    edit 99,999,999.99
 
  Add #earns  To #jc_earns
  
  let #earns  = 0
 
End-Procedure

!***********************************************************************
Begin-Procedure GEXXX900-Select-Company-Parameters_X
!***********************************************************************

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_COMPANY_ROWS = 0
  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT = ''
  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH = ''
  
Begin-Select

GEX_RC_COMPANY_X.COMPANY

  Add 1 To #GEX_RC_COMPANY_ROWS

  If #GEX_RC_COMPANY_ROWS = 1
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT = '(' || $GEXXX900_Company_Alias_OT || ' In ('''     || &GEX_RC_COMPANY_X.COMPANY || ''''
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH= '(' || $GEXXX900_Company_Alias_OTH || ' In ('''     || &GEX_RC_COMPANY_X.COMPANY || ''''
    
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT = $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT || ',''' || &GEX_RC_COMPANY_X.COMPANY  || ''''
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH= $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH || ',''' || &GEX_RC_COMPANY_X.COMPANY || ''''
  End-If

From  PS_GEX_RC_COMPANY GEX_RC_COMPANY_X

Where GEX_RC_COMPANY_X.OPRID                = [$OprID]
And   GEX_RC_COMPANY_X.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_COMPANY_X.COMPANY, ' ') != ' '

End-Select

  If #GEX_RC_COMPANY_ROWS = 0
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT = $GEXXX900_Company_Alias_OT || ' = ' || $GEXXX900_Company_Alias_OT
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH = $GEXXX900_Company_Alias_OTH || ' = ' || $GEXXX900_Company_Alias_OTH
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT = $GEXXX900_INCLUDE_COMPANY_CRITERIA_OT || '))'
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH = $GEXXX900_INCLUDE_COMPANY_CRITERIA_OTH || '))'
  End-If

End-Procedure

!*********************************************************************
Begin-Procedure GEXXX901-Select-Company-Paygroup-Parameters_X
!*********************************************************************

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_PAYGROUP_ROWS = 0
  Let $Include_Condition_OT    = ''
  Let $Include_Condition_OTH    = ''
  Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT  = ''
  Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH = ''
  
Begin-Select

GEX_RC_PAYGROUP_X.COMPANY
GEX_RC_PAYGROUP_X.PAYGROUP

  Add 1 To #GEX_RC_PAYGROUP_ROWS

  Let $Include_Condition_OT = '(' || $GEXXX901_Company_Alias_OT  || ' = '''  || &GEX_RC_PAYGROUP_X.COMPANY  || ''' And ' || $GEXXX901_Paygroup_Alias_OT || ' = '''  || &GEX_RC_PAYGROUP_X.PAYGROUP || ''')'
  Let $Include_Condition_OTH = '(' || $GEXXX901_Company_Alias_OTH  || ' = '''  || &GEX_RC_PAYGROUP_X.COMPANY  || ''' And ' || $GEXXX901_Paygroup_Alias_OTH || ' = '''  || &GEX_RC_PAYGROUP_X.PAYGROUP || ''')'
  
  If #GEX_RC_PAYGROUP_ROWS = 1
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT = '(' || $Include_Condition_OT
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH = '(' || $Include_Condition_OTH
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT || ' Or '  || $Include_Condition_OT
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH= $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH || ' Or '  || $Include_Condition_OTH
  End-If

From  PS_GEX_RC_PAYGROUP GEX_RC_PAYGROUP_X

Where GEX_RC_PAYGROUP_X.OPRID                 = [$OprID]
And   GEX_RC_PAYGROUP_X.RUN_CNTL_ID           = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_PAYGROUP_X.COMPANY,  ' ') != ' '
And   Rtrim(GEX_RC_PAYGROUP_X.PAYGROUP, ' ') != ' '

End-Select

  If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT = $GEXXX901_Company_Alias_OT  || ' = ' || $GEXXX901_Company_Alias_OT  || ' And ' ||
                                                         $GEXXX901_Paygroup_Alias_OT || ' = ' || $GEXXX901_Paygroup_Alias_OT
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH= $GEXXX901_Company_Alias_OTH  || ' = ' || $GEXXX901_Company_Alias_OTH  || ' And ' ||
                                                         $GEXXX901_Paygroup_Alias_OTH || ' = ' || $GEXXX901_Paygroup_Alias_OTH
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OT || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH= $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA_OTH || ')'
  End-If

End-Procedure

!*********************************************************************
Begin-Procedure GEXXX902-Select-Deptid-Parameters_X
!*********************************************************************

  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DEPTID_ROWS = 0
  Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT = ''
  Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH = ''

Begin-Select

GEX_RC_DEPTID_X.DEPTID

  Add 1 To #GEX_RC_DEPTID_ROWS

  If #GEX_RC_DEPTID_ROWS = 1
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT = '(' || $GEXXX902_Deptid_Alias_OT || ' In ('''     || &GEX_RC_DEPTID_X.DEPTID || ''''
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH = '(' || $GEXXX902_Deptid_Alias_OTH || ' In ('''     || &GEX_RC_DEPTID_X.DEPTID || ''''
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT = $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT || ',''' || &GEX_RC_DEPTID_X.DEPTID  || ''''
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH = $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH || ',''' || &GEX_RC_DEPTID_X.DEPTID  || ''''
  End-If

From  PS_GEX_RC_DEPTID GEX_RC_DEPTID_X

Where GEX_RC_DEPTID_X.OPRID               = [$OprID]
And   GEX_RC_DEPTID_X.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DEPTID_X.DEPTID, ' ') != ' ' 
End-Select

  If #GEX_RC_DEPTID_ROWS = 0
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT = $GEXXX902_Deptid_Alias_OT || ' = ' || $GEXXX902_Deptid_Alias_OT
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH = $GEXXX902_Deptid_Alias_OTH || ' = ' || $GEXXX902_Deptid_Alias_OTH   
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT = $GEXXX902_INCLUDE_DEPTID_CRITERIA_OT || '))'
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH = $GEXXX902_INCLUDE_DEPTID_CRITERIA_OTH || '))'    
  End-If

End-Procedure

!*********************************************************************
Begin-Procedure GEXXX916-Select-Jobcode-Parameters_X
!*********************************************************************

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_JOBCODE_ROWS = 0
  Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT = ''
  Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH = ''

Begin-Select

GEX_RC_JOBCODE_X.JOBCODE

  Add 1 To #GEX_RC_JOBCODE_ROWS

  If #GEX_RC_JOBCODE_ROWS = 1
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT = '(' || $GEXXX916_Jobcode_Alias_OT || ' In ('''     || &GEX_RC_JOBCODE_X.JOBCODE || ''''
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH = '(' || $GEXXX916_Jobcode_Alias_OTH || ' In ('''     || &GEX_RC_JOBCODE_X.JOBCODE || ''''   
  Else
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT = $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT || ',''' || &GEX_RC_JOBCODE_X.JOBCODE  || ''''
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH = $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH || ',''' || &GEX_RC_JOBCODE_X.JOBCODE  || ''''
  End-If

From  PS_GEX_RC_JOBCODE GEX_RC_JOBCODE_X

Where GEX_RC_JOBCODE_X.OPRID                = [$OprID]
And   GEX_RC_JOBCODE_X.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_JOBCODE_X.JOBCODE, ' ') != ' '

End-Select

  If #GEX_RC_JOBCODE_ROWS = 0
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT = $GEXXX916_Jobcode_Alias_OT || ' = ' || $GEXXX916_Jobcode_Alias_OT
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH = $GEXXX916_Jobcode_Alias_OTH || ' = ' || $GEXXX916_Jobcode_Alias_OTH
  Else
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT = $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OT || '))'
    Let $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH = $GEXXX916_INCLUDE_JOBCODE_CRITERIA_OTH || '))'
  End-If

End-Procedure

!*********************************************************************
Begin-Procedure SQL-Error-E($Action)
!*********************************************************************
  Show $Action ' failed'
  Show $_sql-error
  show 'sql-status: ' #_sql-status
  Stop
End-Procedure

!***********************************************************************
Begin-Procedure EOJ
!***********************************************************************
 
  if #record_count = 0
    print 'No Data Selected This Run'   (25,) Center
  end-if
 
  Do Reset			
  
  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'

End-Procedure

#include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datemath.sqc'  !Routines for date math
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'getcodta.sqc'  !Get-Company-Data procedure
#include 'getdptnm.sqc'  !Get-Department-Name procedure
#include 'geternnm.sqc'  !Get-Earn Code Name Procedure
#include 'getjobtl.sqc'  !Get Job Title
#include 'getpgdta.sqc'  !Get-Paygroup-Data procedure
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx916.sqc'  !Get job code multiple row table
#include 'gexxx922.sqc'  !Get GE payroll run controls
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   28-Jan-2009  	