!**************************************************************
!  GetActRs:  Get Action Reason from the Action Reason Table  *
!**************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2007 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2007/07/31:22:55:21                                 
!       $Release:  HR9                                                 
!    $Resolution:  689848                                              
!                                                                      *
!***********************************************************************
! Deepak, FC, 11/27/07, Changed alias from A. to GEX. in procedure 
! Get-Action as per044.sqr was failing.
! (SQR 3719) Columns names and expressions must be unique or be given 
! unique pseudonyms (&name). A.ACTION_DESCR,A.ACTION_DESCRSHORT,A.EFFDT.
! A.EFFDT is used in the sqr. This procedure is not used in the sqr
! and this is a compile time error.
!***********************************************************************

! 
begin-procedure Get-Action-Reason

  #ifndef ACTN_REASON_TBL
    #define ACTN_REASON_TBL
  #end-if

  move '' to $Action_Reason_Descr
  move '' to $Action_Reason_DescrShort
  move '' to $ROE_Reason

  if rtrim($Action,' ') = '' or rtrim($Action_Reason,' ') = ''
    goto GetActRsExit
  end-if
  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if
begin-SELECT
AR.DESCR,
AR.DESCRSHORT,
AR.EFFDT

 
   do Get_Related_ACTN_REASON_TBL($Action,$Action_Reason,&AR.EFFDT)
   let $Action_Reason_Descr = $ACTN_REASON_TBL-DESCR
   let $Action_Reason_DescrShort = $ACTN_REASON_TBL-DESCRSHORT
   
   
FROM  PS_ACTN_REASON_TBL AR
WHERE AR.ACTION = $Action
  AND AR.ACTION_REASON = $Action_Reason
  AND AR.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_ACTN_REASON_TBL
       WHERE  ACTION = $Action
         AND  ACTION_REASON = $Action_Reason
         AND  EFFDT <= $AsOfDate)
end-SELECT


begin-SELECT
ARN.ROE_REASON
FROM  PS_ACT_RSN_TBL_NA ARN
WHERE ARN.ACTION = $Action
  AND ARN.ACTION_REASON = $Action_Reason
  AND ARN.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_ACT_RSN_TBL_NA
       WHERE  ACTION = $Action
         AND  ACTION_REASON = $Action_Reason
         AND  EFFDT <= $AsOfDate)
end-SELECT

  if  rtrim($Action_Reason_Descr,' ') = '' and rtrim($Action_Reason_DescrShort,' ') = ''
    let $Action_Reason_Descr = rtrim(&AR.Descr,' ')
    let $Action_Reason_DescrShort = rtrim(&AR.DescrShort,' ')
  end-if

  let $ROE_Reason = rtrim(&ARN.ROE_Reason,' ')
GetActRsExit:
end-procedure

begin-procedure Get-Action

  
  #ifndef ACTION_TBL
    #define ACTION_TBL
  #end-if

  move '' to $Action_Descr
  move '' to $Action_DescrShort
  if rtrim($Action,' ') = ''
        goto GetActExit
  end-if
  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if
! Deepak, FC, 11/27/07
! Changed alias from A. to GEX. as per044.sqr was failing 
! 
begin-SELECT
GEX.ACTION_DESCR,GEX.ACTION_DESCRSHORT,GEX.EFFDT

  do Get_Related_ACTION_TBL($Action,&GEX.EFFDT)
  let $Action_Descr = $ACTION_TBL-ACTION_DESCR
  let $Action_DescrShort = $ACTION_TBL-ACTION_DESCRSHORT
  
FROM PS_ACTION_TBL GEX
WHERE GEX.ACTION = $Action
AND GEX.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_ACTION_TBL
       WHERE  ACTION = $Action
         AND  EFFDT <= $AsOfDate)
end-SELECT
  if  rtrim($Action_Descr,' ') = '' and rtrim($Action_DescrShort,' ') = ''
    let $Action_Descr = rtrim(&GEX.Action_Descr,' ')
    let $Action_DescrShort = rtrim(&GEX.Action_DescrShort,' ')
  end-if
GetActExit:
end-procedure

#include 'rellang.sqc'
