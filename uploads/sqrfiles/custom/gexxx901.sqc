!**************************************************************************
!  GEXXX901:  Retrieve "Multiple" Paygroup Run Controls and               *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX901_Company_Alias                                              *
!    $GEXXX901_Paygroup_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA                          *
!    $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA                          *
!    $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA                          *
!    $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX901_Company_Alias  = 'J.COMPANY'                           *
!    Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'                          *
!    Do GEXXX901-Select-Company-Paygroup-Parameters                       *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]                  *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX901-Select-Company-Paygroup-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_PAYGROUP_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = ''
  Let $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = ''
  Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_PAYGROUP.COMPANY
GEX_RC_PAYGROUP.PAYGROUP

  Add 1 To #GEX_RC_PAYGROUP_ROWS

  Let $Include_Condition = '(' || $GEXXX901_Company_Alias  || ' = '''  || &GEX_RC_PAYGROUP.COMPANY  || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || &GEX_RC_PAYGROUP.PAYGROUP || ''')'
  Let $Exclude_Condition = '(' || $GEXXX901_Company_Alias  || ' != ''' || &GEX_RC_PAYGROUP.COMPANY  || ''' Or '  || $GEXXX901_Paygroup_Alias || ' != ''' || &GEX_RC_PAYGROUP.PAYGROUP || ''')'

  If #GEX_RC_PAYGROUP_ROWS = 1
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
    Let $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_PAYGROUP GEX_RC_PAYGROUP

Where GEX_RC_PAYGROUP.OPRID                 = [$OprID]
And   GEX_RC_PAYGROUP.RUN_CNTL_ID           = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_PAYGROUP.COMPANY,  ' ') != ' '
And   Rtrim(GEX_RC_PAYGROUP.PAYGROUP, ' ') != ' '

End-Select

  If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_Company_Alias  || ' = ' || $GEXXX901_Company_Alias  || ' And ' ||
                                                      $GEXXX901_Paygroup_Alias || ' = ' || $GEXXX901_Paygroup_Alias
    Let $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_Company_Alias  || ' = ' || $GEXXX901_Company_Alias  || ' And ' ||
                                                      $GEXXX901_Paygroup_Alias || ' = ' || $GEXXX901_Paygroup_Alias
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
    Let $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_EXCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If

End-Procedure
