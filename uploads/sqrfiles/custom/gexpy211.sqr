!***********************************************************************
! GEXPY211:  This SQR process calculates vacation hours for employees  *
!            in the Akron-Canton Stores (Local 880) who have           *
!            attained one or more years of service.  Vacation          *
!            hours earned is calculated by multiplying the number of   *
!            eligible vacation weeks (as determined by service years)  *
!            times the average number of hours worked in the previous  *
!            calendar year. [Eligible weeks is hard-coded in this sqr.]*
!                                                                      *
!            If the average number of hours worked is => 37, then the  *
!            employee is given vacation hours = (eligible weeks * 40). *
!            otherwise,                                                *
!            If the average number of hours worked is < 37, then the   *
!            employee is given vacation hours = (eligible weeks *      *
!            average number of hours worked.                           *
!            *NOTE* There is hard-coding for specific deptids.         *
!            Employees are awarded vacation hours at the beginning of  *
!            the calendar year.  Therefore, this process will run      *
!            annually.                                                 *
!***********************************************************************
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#   INITIALS   DATE      DESCRIPTION OF THE CHANGE                *        
!***********************************************************************
!                                                                      *
!  HR      SCM   08/20/2003   Initial creation for Akron Vacation.     *
!                             Cloned from GEXPY210.sqr.                *
!                                                                      *
!  10604   AXL   04/05/2004   Automate vacation rules for BMC &        *
!                             Louisville/Alliance stors.               *
!                                                                      *
!  10617   AXL   12/20/2004   Cap vacation entitlement at 200 hrs      *
!                                                                      *      
!ITG 37761 CWB   11/28/006    New logic to calc elig weeks for         *
!                             Stores 6348, 4095, 4022, 4124, 4025      *
!                             & 4096.                                  *
!                                                                      *
!  45626   SXN   12/21/2007   Modified for Wadsworth 4677.             *                                                                   *  
!  45627   SXN   12/21/2007   Modified for Alliance 1605.              *    
!  58566   SXP	 08/12/2009   GEXPY211 new vacation schedule for 3 deptids*       
!13734086   MXR   01/11/2010   Changed deptid 4677 is hard coded and the*
!                             deptid was changed to 4601               *  
!  73860    RKK   12/01/2010  Correction to 4677/4601                  *
! ITG93005 ISDVNPK 02/21/2011 Modifed this to select based on          *
!                              VACATION SENIOR DT                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup32.sqc'

begin-setup
  declare-report Vacation-Annual-Report
Printer-Type=HPLASERJET                    !Comment temporarily for running in SQRW, but need for mobius
!Printer-Type=LINEPRINTER
    layout=DEFAULT
  end-declare

  declare-report Report-log
 Printer-Type=HPLASERJET                    !Comment temporarily for running in SQRW, but need for mobius
!Printer-Type=LINEPRINTER
    layout=DEFAULT
  end-declare
end-setup


begin-heading 6 for-reports=(report-log)
  LET $REPORTID = 'PY211LOG'
  move 'Vacation Calculation for Akron - Report Log' to $ReportTitle

 #Include 'stdhdg01.sqc'

  position          (+2)
  print 'Trans#'    (,1)
  print 'Emplid'    (,18)
  print 'Message'   (,50)
  print '-'         (+1,1,174) fill
end-heading


begin-heading 7 for-reports=(Vacation-Annual-Report)
  move 'GEXPY211' to $ReportID
  move 'Vacation Calculation for Akron - Annual Update' to $ReportTitle

 #Include 'stdhdg02.sqc'
  Let $X000_temp_out = 'Report as of ' || $X000_REPORT_HEADING_ASOFDATE

  Print $X000_temp_out            (3,)  center
  print 'Dept'                    (6,1)
  print 'Acct'                    (,9)
  print 'Pay'                     (,73)
  print 'Job'                     (,89)
  print 'Full/Part'               (,97)
  print 'Service'                 (,107)
  print 'Awarded'                 (,156)  
  print 'Hrs Worked'              (,137)

  print 'Code'                    (+1,1)
  print 'Description'             (,9)
  print 'Name'                    (,23)
  print 'Emplid'                  (,60)
  print 'Group'                   (,73)
  print 'Union'                   (,81)
  print 'Code'                    (,89)
  print 'Time Ind'                (,97)
  print 'Date'                    (,107)
  print 'Hire Date'               (,120)
  print '   YTD   '               (,137)
  print 'Vacation Hrs'            (,154)
  print 'Yrs Svc'                 (,168)
  
  print '-'                       (+1,1,174) fill
end-heading


begin-report
  use-report Vacation-Annual-Report

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
  Let $GEXXX902_Deptid_Alias   = 'a.deptid'
  Let $GEXXX913_UNION_CD_Alias = 'A.UNION_CD'
  Let $X000_Order_By =  'A.COMPANY, A.DEPTID, P.NAME'

  If $prcs_process_instance = ''
    Do Ask-As-Of-Date

    Display 'Enter DeptID or leave blank to exit.'
    Let #deptcount = 1                                    !GBD 09/06/2002
    While 1=1
      Input $In-DeptID Maxlen=10 'DeptID'
      Uppercase $In-DeptID

      If Rtrim($In-DeptID, ' ') = ''
        Concat '''' With $DeptIDString
        Break
      End-If
 
      If $DeptIDString <> ''''
        if #deptcount = 1
           Concat '''' With $DeptIDString
        else
           Concat ''',''' With $DeptIDString
        end-if
        add 1 to #deptcount
      End-If
      Concat $In-DeptID With $DeptIDString

    End-While

    If $DeptIDString = '''''' or $DeptIDString = ''''
      Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
      Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
      Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
    End-If
 
    Let $UnionCdString = ''''
    Display 'Enter Union Code or leave blank to exit.'
    While 1=1
      Input $In-UnionCd Maxlen=3 'Union Code'
      Uppercase $In-UnionCd
      If Rtrim($In-UnionCd, ' ') = ''
        Concat '''' With $UnionCdString
        Break
      End-If
      If $UnionCdString <> ''''
        Concat ''',''' With $UnionCdString
      End-If
      Concat $In-UnionCd With $UnionCdString
    End-While
    If $UnionCdString = ''''''
      Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
      Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_Union_CD_Alias || ' In (' || $UnionCdString || ')'
      Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
    End-If
  
  Else
    Do GEXXX922-Select-Parameters
    Let $AsOfDate = $GEX_RC_PAY.ASOFDATE
    If Rtrim($AsOfDate,' ') = ''
      Let $AsOfDate = $AsOfToday
    End-If

    Do GEXXX902-Select-Deptid-Parameters
    Do GEXXX913-Select-UNION-CD-Parameters

  End-If
  
  let $Year4   = '1'
  Do Format-DateTime($AsOfDate, $X000_REPORT_HEADING_ASOFDATE, {DEFMDY}, '', '')

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '

  Do Convert-To-DTU-Date($AsOfDate,$AsofDate_dtu)
  Do dtu-add-days($AsOfDate_dtu,-1, $AsOfDateMinus1_dtu)
  Do Convert-From-DTU-Date($AsOfDateMinus1_dtu,$AsOfDateMinus1)
 
  Show '$GEXXX902_Include_Deptid_Display_Criteria          = ' $GEXXX902_Include_Deptid_Display_Criteria
  Show '$X000_Where_Select_AsOfDate = ' $X000_Where_Select_AsOfDate
  show '$GEXXX913_INCLUDE_UNION_CD_CRITERIA ' $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  
  do Gexregtp_Init_Strings
  do Get-Date-Codes
  do Convert-To-DTU-Date($AsOfDate, $AsOfDate_dtu)

  move $AsOfYYYY to #AsOfYYYY
  let #prev_year = #AsOfYYYY - 1
  let #next_year = #AsOfYYYY + 1 		 
  
  let $next_year = to_char(floor(#next_year))	 
  
  Let $effdt      = '01-JAN-' || $next_year	

  Let $curr_effdt = '01-JAN-' || $asofyyyy    ! ITG 37761 CWB 12/2006

  move 'GEXPY211' to $ReportID
  move 'Vacation Calculation for Akron - Annual Update' to $ReportTitle 
  Do P130-Print-Cover-Page 

  show '$effdt for insert: ' $effdt
  show '$AsOfYYYY        : ' $AsOfYYYY		 

  move 'N' to $found
  let #inputtran = 0
  do P200-Select-Employees
  
Report-Exit:

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded

  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  show ' '

  display #InputTran 999,999 noline
  display ' Transactions Processed'

  do Commit-Transaction

  do Stdapi-Term
end-report

begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX902_Include_Deptid_Display_Criteria ='             (+1,5)
  Print $GEXXX902_Include_Deptid_Display_Criteria                 (0,+2)

  Print '$GEXXX913_INCLUDE_UNION_CD_CRITERIA       ='             (+1,5)
  Print $GEXXX913_INCLUDE_UNION_CD_CRITERIA                       (0,+2)

  Print 'As Of Date :'                      (+2,5)
  Print $X000_Report_Heading_AsOfDate       (0,+2)

  Print 'Effdt for Row Insert'             (+2,5)
  Print $effdt                             (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'   (+5,2)
  Print $X000_Order_By                      (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


begin-procedure P200-Select-Employees
#debug9 Show 'P200-Select-Employees'
  move 'N' to $found
  move 'N' to $Empl_found

  show '$AsOfDate ' $AsOfDate

begin-select ON-ERROR=P999-SQL-Error-Found
A.COMPANY   ()  ON-BREAK  LEVEL=1  PRINT=NEVER

A.DEPTID    ()  ON-BREAK  LEVEL=2  PRINT=NEVER
                                   SAVE=$previous_deptid
                                   BEFORE=P210-Before-Deptid-Change
                                   AFTER=P220-After-Deptid-Change
A.JOBCODE
A.PAYGROUP
A.EMPLID
A.EMPL_RCD
A.REG_TEMP
A.HOURLY_RT
A.ACCT_CD
A.UNION_CD
B.HIRE_DT
B.REHIRE_DT
B.SERVICE_DT
B.GEX_VCN_SENIOR_DT  ! ISDVNPK 02/21/2011 ITG93005 Modifed this to select based on VACATION SENIOR DT
P.NAME

  let $Company = &A.company
  move &A.EMPLID to $emplid
  move 'Y' to $Empl_found
  move 0 to #hrs_worked
  move 0 to #vac_hrs_awarded

  ! ISDVNPK 02/21/2011 ITG93005 Modifed this to select based on VACATION SENIOR DT
  If rtrim(datetostr(&B.GEX_VCN_SENIOR_DT,'dd-mon-yyyy'),' ') = ''
   move &B.SERVICE_DT to $service_dt
  else
   move &B.GEX_VCN_SENIOR_DT to $service_dt
  end-if
  ! ISDVNPK 02/21/2011 ITG93005 Modifed this to select based on VACATION SENIOR DT
  do Convert-To-DTU-Date($service_dt, $service_dt_dtu)
  do Dtu-Diff-Years($service_dt_dtu,$AsOfDate_dtu,#dtu_years)
  
  #ifdef debug8 
    show ' '
    show ' emplid              = ' $emplid
    show ' deptid              = ' &a.deptid
    show ' $service_dt         = ' $service_dt
    show ' $service_dt_dtu     = ' $service_dt_dtu
    show ' $AsOfDate_dtu       = ' $AsOfDate_dtu
    show ' #dtu_years          = ' #dtu_years
  #endif
  
  do P250-Hours-Worked

  if #hrs_worked > 0
    add 1 to #inputtran
    let #avg_weekly_hrs_worked = #hrs_worked / 52

    do P260-Calc-Eligible-Weeks

    if &A.DEPTID = '6378' or &A.DEPTID = '4060' or &A.DEPTID = '1611' !or &A.DEPTID = '1605'    !VENDSXN Commented for ITG-45627
        let #vac_hrs_awarded = round(#avg_weekly_hrs_worked * #eligible_weeks,2) 
      
    else             !for all other deptids, use 40 hours if avg hours worked is >= 37 hours
      evaluate #avg_weekly_hrs_worked
      when >= 37
        let #vac_hrs_awarded = round(40 * #eligible_weeks,2)
        break
      when < 37
        let #vac_hrs_awarded = round(#avg_weekly_hrs_worked * #eligible_weeks,2)
        break
      end-evaluate
     end-if

     if #vac_hrs_awarded > 199
        let #vac_hrs_awarded = 200
     end-if

     do P300-Check-Existing-Vac-Erncd
     do P340-Insert-Addl-Pay-Data          
     do P400-Print-Detail-Report
  end-if

  #ifdef debug7 
    show ' avg hours           = ' #avg_weekly_hrs_worked
    show ' hours worked        = ' #hrs_worked
    show ' eligible_weeks      = ' #eligible_weeks
    show ' vac_hrs_awarded     = ' #vac_hrs_awarded
    show '--------------------------------------' 
  #endif
  
FROM  PS_JOB A,
      PS_EMPLOYMENT B,
      PS_PERSONAL_DATA P 
WHERE  A.EMPLID           = B.EMPLID
   AND A.EMPL_RCD         = B.EMPL_RCD
   AND A.EMPLID           = P.EMPLID    
   And  [$GEXXX902_Include_Deptid_Criteria]
   AND  [$GEXXX913_INCLUDE_UNION_CD_CRITERIA] 
   AND A.EMPL_STATUS NOT IN ('T','R','U','Q')
   AND A.EFFDT            =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID     = A.EMPLID
          AND EMPL_RCD  = A.EMPL_RCD
          AND EFFDT     <= $AsOfDate)
   AND A.EFFSEQ           =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB 
       WHERE  EMPLID      = A.EMPLID
          AND EMPL_RCD   = A.EMPL_RCD
          AND EFFDT       = A.EFFDT)
   ! ISDVNPK 02/21/2011 ITG93005 Modifed this to select based on VACATION SENIOR DT
   AND ((ADD_MONTHS(B.SERVICE_DT,12) <= $AsOfDate and B.gex_vcn_senior_dt is Null) 
       or (ADD_MONTHS(B.gex_vcn_senior_dt,12) <= $AsOfDate and B.gex_vcn_senior_dt is not Null))
   ! AND ADD_MONTHS(B.SERVICE_DT,12) <= $AsOfDate    
   ! ISDVNPK 02/21/2011 ITG93005 Modifed this to select based on VACATION SENIOR DT

ORDER BY A.COMPANY, A.DEPTID, P.NAME
end-select

   if $Empl_found = 'N'
      Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
   end-if
end-procedure P200-Select-Employees


begin-procedure P210-Before-Deptid-Change
#debug9 Show 'P210-Before-Deptid-Change'
  let $DeptID = &A.DEPTID
  do Get-Department-Name
end-procedure P210-Before-Deptid-Change


begin-procedure P220-After-Deptid-Change
#debug9 Show 'P220-After-Deptid-Change'
  if #detail_count > 0
    new-page
    move 0 to #detail_count
  end-if
  let $DeptName = ''
end-procedure P220-After-Deptid-Change


begin-procedure P250-Hours-Worked
#debug9 Show 'P250-Hours-Worked'

begin-select on-error=P999-SQL-Error-Found
SUM(J.HRS_YTD) &J.HRS_YTD

  #debug8 show '&J.HRS_YTD  ' &J.HRS_YTD
  add &J.HRS_YTD To #hrs_worked

FROM   PS_EARNINGS_BAL J
WHERE  J.EMPLID            = &A.EMPLID
   AND (J.COMPANY   ='335' or J.COMPANY > '999')  !Ignore indys xcept for 335  
   AND J.BALANCE_ID        = 'CY'
   AND J.BALANCE_YEAR      =  #AsOfYYYY
   AND J.EMPL_RCD          = &A.EMPL_RCD
   AND J.BALANCE_PERIOD    =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_EARNINGS_BAL
       WHERE  EMPLID       = J.EMPLID
          AND COMPANY      = J.COMPANY
          AND BALANCE_ID   = J.BALANCE_ID
          AND BALANCE_YEAR = J.BALANCE_YEAR
          AND SPCL_BALANCE = J.SPCL_BALANCE
          AND EMPL_RCD     = J.EMPL_RCD
          AND ERNCD        = J.ERNCD)
   AND J.SPCL_BALANCE      = 'Y'
   AND J.ERNCD             = 'TOT'
end-select
end-procedure P250-Hours-Worked


!*********************************************************************
!This procedure determines the number of weeks allowed based on the 
!service years for the employee.
!*********************************************************************

begin-procedure P260-Calc-Eligible-Weeks
#debug9 Show 'P260-Calc-Eligible-Weeks'

   Do format-datetime (&B.Hire_dt,$Hire_dt_YYYYMMDD,{DEFCMP},'','')
 
   evaluate &A.DEPTID

    !when = '1605'	!VENDSXN Commented for ITG-45627
    when = '1611'
         evaluate #dtu_years 
           when >= 16
             move 4 to #eligible_weeks
             break
           when >= 15
              !If EE hired before 10/01/2000 w/15 yrs is elig for 4 wks 
              if #Hire_dt_YYYYMMDD < 20001001
                 move 4 to #eligible_weeks
              end-if
              break
           when >= 9
             move 3 to #eligible_weeks
             break
           when >= 3
             move 2 to #eligible_weeks
             break
           when >= 1
             move 1 to #eligible_weeks
             break
         end-evaluate
    when = '4022'
    when = '4024'
    when = '4025'
    when = '4095'
    when = '4096'
    when = '6348'
    when = '1605'	!VENDSXN Added for ITG-45627
    !when = '4677'	!ISDVRKK ITG-73860
    when = '4601'	  !ISDVRKK ITG-73860
    when = '4060'       !sxp ITG 58566 08/12/2009
    when = '4387'       !sxp ITG 58566 08/12/2009
    when = '6378'       !sxp ITG 58566 08/12/2009
      do get-curr-elig-weeks
         evaluate #dtu_years 
           when >= 20
           !If EE hired before 01/01/1984 is elig for 6 wks 
            if #Hire_dt_YYYYMMDD < 19840101
               move 6 to #eligible_weeks
             else
               if #curr_elig_weeks > 5
                 move #curr_elig_weeks to #eligible_weeks
               else
                 move 5 to #eligible_weeks
               end-if 
             end-if
             break
           when >= 15
                if #curr_elig_weeks > 4
                  move #curr_elig_weeks to #eligible_weeks
                else
                  move 4 to #eligible_weeks
                end-if
              break
           when >= 10
                if #curr_elig_weeks > 3
                  move #curr_elig_weeks to #eligible_weeks
                else
                  move 3 to #eligible_weeks
                end-if
             break
           when >= 3
                if #curr_elig_weeks > 2
                  move #curr_elig_weeks to #eligible_weeks
                else
                  move 2 to #eligible_weeks
                end-if
             break
           when >= 1
                if #curr_elig_weeks > 1
                  move #curr_elig_weeks to #eligible_weeks
                else
                  move 1 to #eligible_weeks
                end-if
             break
         end-evaluate
    when-other
         evaluate #dtu_years 
           when >= 23
             move 6 to #eligible_weeks
             break
           when >= 17
             move 5 to #eligible_weeks
             break
           when >= 12
             move 4 to #eligible_weeks
             break
           when >= 7
             move 3 to #eligible_weeks
             break
           when >= 3
             move 2 to #eligible_weeks
             break
           when >= 1
             move 1 to #eligible_weeks
             break
         end-evaluate
    end-evaluate
  
end-procedure P260-Calc-Eligible-Weeks

! Find the current year's eligible weeks to use in comparison with next year's eligible weeks.

Begin-procedure get-curr-elig-weeks

  move 0 to #curr_vac_hrs
  move 0 to #curr_elig_weeks

Begin-select on-error=P999-SQL-Error-Found
ad.oth_hrs
    move &ad.oth_hrs to #curr_vac_hrs
FROM ps_addl_pay_data ad
WHERE ad.emplid = $emplid
  AND ad.erncd = '513'
  and ad.effdt = $curr_effdt
end-select

  let #curr_elig_weeks = round(#curr_vac_hrs / 40,0)
 
End-procedure get-curr-elig-weeks


!*********************************************************************
!This procedure tries to find if the Vacation Earnings Code (513) is
!defined in ADDL_PAY_ERNCD table. If there is, inserts a row in 
!ADDL_PAY_EFFDT with AsofDate as the effective date. Otherwise,
!inserts the earning code 513 in the ADDL_PAY_ERNCD and also a row with
!effective date equal to AsofDate in ADDL_PAY_EFFDT.
!*********************************************************************
   
begin-procedure P300-Check-Existing-Vac-Erncd
#debug9 Show 'P300-Check-Existing-Vac-Erncd'
  move 'N' to $found

begin-select on-error=P999-SQL-Error-Found
'K'
  move 'Y' to $found
  do P330-Insert-New-Effdt

FROM   PS_ADDL_PAY_ERNCD K
WHERE  K.EMPLID    = &A.EMPLID
   AND K.EMPL_RCD = &A.EMPL_RCD
   AND K.ERNCD     = '513'
end-select

  if $found = 'N'
    do P320-Insert-Vac-Erncd
    do P330-Insert-New-Effdt
  end-if
end-procedure P300-Check-Existing-Vac-Erncd

begin-procedure P320-Insert-Vac-Erncd
#debug9 Show 'P320-Insert-Vac-Erncd for Emplid: ' &a.emplid
  begin-sql on-error=P999-SQL-Error-Found
    INSERT INTO PS_ADDL_PAY_ERNCD
               (EMPLID,
                EMPL_RCD,
                ERNCD,
                RECORD_SOURCE)                         
        VALUES (&A.EMPLID,
                &A.EMPL_RCD,
                '513',
                'O')                                   
  end-sql
end-procedure P320-Insert-Vac-Erncd


begin-procedure P330-Insert-New-Effdt
#debug9 Show 'P330-Insert-New-Effdt for Emplid: ' &a.emplid
 begin-sql on-error=P999-SQL-Error-Found
    INSERT INTO PS_ADDL_PAY_EFFDT
               (EMPLID,
                EMPL_RCD,
                ERNCD,
                EFFDT,
                RECORD_SOURCE)                        
        VALUES (&A.EMPLID,
                &A.EMPL_RCD,
                '513',
                $effdt,
                'O')                                   
  end-sql
end-procedure P330-Insert-New-Effdt
 
begin-procedure P340-Insert-Addl-Pay-Data
#debug9 Show 'P340-Insert-Addl-Pay-Data for Emplid: ' &a.emplid
 begin-sql on-error=P999-SQL-Error-Found
    INSERT INTO PS_ADDL_PAY_DATA
               (EMPLID,
                EMPL_RCD,
                ERNCD,
                EFFDT,
                ADDL_SEQ,
                DEPTID,
                JOBCODE,
                POSITION_NBR,
                ACCT_CD,
                GL_PAY_TYPE,
                ADDL_PAY_SHIFT,
                OTH_HRS,
                HOURLY_RT,
                OTH_PAY,
                ADDLPAY_REASON,
                SEPCHK,
                EARNINGS_END_DT,
                GOAL_AMT,
                GOAL_BAL,
                OK_TO_PAY,
                DISABLE_DIR_DEP,
                PRORATE_ADDL_PAY,
                PRORATE_CUI_WEEKS,
                PAY_PERIOD1,
                PAY_PERIOD2,
                PAY_PERIOD3,
                PAY_PERIOD4,
                PAY_PERIOD5,
                STATE,
                LOCALITY,
                TAX_PERIODS,
                TAX_METHOD,
                ADDL_PAY_FREQUENCY,
                DED_TAKEN,
                DED_SUBSET_ID,
                DED_TAKEN_GENL,
                DED_SUBSET_GENL,
                PLAN_TYPE,
                BUSINESS_UNIT,
                COMP_RATECD,
                RECORD_SOURCE)                         
        VALUES (&A.EMPLID,
                &A.EMPL_RCD,
                '513',
                $effdt,
                1,
                ' ',
                ' ',
                ' ',
                ' ',
                ' ',
                ' ',
                #vac_hrs_awarded,
                0,
                0,
                ' ',
                0,
                '',
                0,
                0,
                'N',
                ' ',
                ' ',
                ' ',
                'N',
                'N',
                'N',
                'N',
                'N',
                ' ',
                ' ',
                0,
                ' ',
                ' ',
                ' ',
                ' ',
                ' ',
                ' ',
                ' ',
                'STDBU',                              !default value
                ' ',
                'O')                                   
  end-sql
end-procedure P340-Insert-Addl-Pay-Data

begin-procedure P400-Print-Detail-Report
#debug9 Show 'P400-Print-Detail-Report'
use-report Vacation-Annual-Report

  add 1 to #detail_count

  let $Acctcd = &A.ACCT_CD
  Do Get-Acct-cd-descr
 
  print &A.DEPTID                   (+1,1)
  print $AcctNameAbbrv              (,9)
  print &P.NAME                     (,23)
  print &A.EMPLID                   (,60)
  print &A.PAYGROUP                 (,74)
  print &A.UNION_CD                 (,82)
  print &A.JOBCODE                  (,89)

  move &A.REG_TEMP to $GEXREGTP_ftpt_code
  do GEXREGTP_translate_code
  print $GEXREGTP_ftpt_code_return  (,100)

  let $Year4   = '1'
  ! ISDVNPK 02/21/2011 ITG93005 Modifed this to select based on VACATION SENIOR DT
  If rtrim(datetostr(&B.GEX_VCN_SENIOR_DT,'dd-mon-yyyy'),' ') = ''
   do Format-DateTime(&B.SERVICE_DT,$ServiceDate,{DEFMDY},'','')
  else
   do Format-DateTime(&B.GEX_VCN_SENIOR_DT,$ServiceDate,{DEFMDY},'','')
  end-if
  ! ISDVNPK 02/21/2011 ITG93005 Modifed this to select based on VACATION SENIOR DT
  print $ServiceDate                (,107) 

  let $Year4   = '1'
  do Format-DateTime(&B.HIRE_DT,$HireDate,{DEFMDY},'','')
  print $HireDate                   (,120)

  print #hrs_worked                 (,137)   edit 99,999.99
  print #vac_hrs_awarded            (,154)   edit 99,999.99
  print #dtu_years                  (,171)   edit 99
 
end-procedure P400-Print-Detail-Report


begin-procedure P999-SQL-Error-Found
#debug9 Show 'P999-SQL-Error-Found'
  move $EmplId to $Format_EmplId xxx-xx-xxxx
  move 'Y' to $ErrorFound
  do Format-Number(#InputTran, $out, '99999')
  
  use-report report-log

  print $out         (+1,1)
  print $EmplID11    (,18)
  print $sql-error   (,35) 

 
  display $out noline 
  display '    ' noline 
  display $EmplID11
  display $sql-error
  display 'Program stopped by user request.'
  STOP
 
end-procedure P999-SQL-Error-Found

#Include 'gexxx922.sqc'  !Get GEX_RC_PAY run control parameters
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx913.sqc'  !Get Union code multiple row table
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getdatcd.sqc'  !Retrieves the asofyear, asofmonth etc.,
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'askaod.sqc'    !Ask-As-Of-Date routine
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions
#include 'gexregtp.sqc'  !Translates the REG_TEMP to Full_Part_time
#include 'getdptnm.sqc'  !Get department name
#include 'getactnm.sqc'  !Get account name
#include 'getcodta.sqc'  !Get company data
#Include 'getempnm.sqc'  !Get-Employee-Name
