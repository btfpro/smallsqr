!***********************************************************************
!  GEXPY640:  COD Worker's Compensation Interface                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXPY640:             COD Worker's comp Interface Program            *
!                                                                      *        
! Narrative:		This program will generate an output file of   *
!                       the most recent Personal Data/Job changes.     *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           * 
!                                                                      *  
! SQL Tables:           personal_data                                  *
!                       job                                            *
!                                                                      *
! Written by:           C. Haver                                       *       
!                                                                      *
! Normally Run:	        Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Company / Paygroup                           *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE            DESCRIPTION OF THE CHANGE              *
!***********************************************************************
!  CJH          10/30/2000      Initial Creation                       *
!               04/03/2001      Modify Phone select                    *
!                                                                      *  
!  CWB          06/11/2001      Add DEPTID exclude so that TMK can be  *
!                               added to the file, but without those   *
!                               stores in Pennsylvania.                * 
!                                                                      *  
!  CWB          07/11/2001      Changed main select to use ACTION_DT   *
!                               instead of EFFDT.                      *
!  PSR          09/30/2002      Added NAMES and ADDRESSES tables to get*
!                               name and address data Upgrade V8.3     * 
!GEX_305  SXD   08/28/2007      Commented the alter session procedure  * 
!                               to remove Optimizer rule               * 
! ISDVMXD	01/29/2009	ITG 56799 - JT.Descr changes in SQR    *	         
!GEX_SEC_REENG 2015-04-29 Vahini Katta                                 *
!Changes related to gex_emplid_mapp/7 digit emplid     
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


#define #column1     1
#define #column2    12
#define #column3    44
#define #column4    56
#define #column5    65
#define #column6    77
#define #column7    90
#define #column8   103
#define #column9   119
#define #column10  135
#define #column11  151
#define #column12  164


begin-report
#debug9 Show 'begin-report'
!GEX_PY305  SXD  08/28/2007   Begin
  !do Alter-Session
!GEX_PY305  SXD  08/28/2007   End	     
  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P000-Init-Report

! CWB 06/11/2001 BEGIN
  let $X000_REPORT_HEADING_ASOFDATE = $reportdate
  do P050-Print-Cover-Page
! CWB 06/11/2001 BEGIN

  do P100-Open-File
  do P200-Process-Main
  do P300-Close-File
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
end-report



begin-procedure P000-Init-Report
#debug9 Show 'P000-Init-Report'

  move 'GEXPY640'                to $ReportID
  move 'COD Workers Compensation Interface File'    to $ReportTitle

  do Stdapi-Init

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate='  $ReportDate
  Show '$ReportTime='  $ReportTime

  Let $GEXXX900_Company_Alias  = 'J.COMPANY'

! CWB 06/11/2001 BEGIN
  Let $GEXXX902_Deptid_Alias  = 'J.DEPTID'
! CWB 06/11/2001 END

  if $prcs_process_instance = ''
    Do GEXXX950-Input-Parameters
  Else
    Do GEXXX922-Select-Parameters
    Do GEXXX900-Select-Company-Parameters
! CWB 06/11/2001 BEGIN
    Do GEXXX902-Select-Deptid-Parameters
! CWB 06/11/2001 END
  End-If


  Let $X000_ORDER_BY = 'Emplid'

  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
! CWB 06/11/2001 BEGIN
  Show '$GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA ='   $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA
! CWB 06/11/2001 END

end-procedure


Begin-Procedure GEXXX950-Input-Parameters

  Let #GEX_RC_COMPANY_ROWS = 0

  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '1=1'
  Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'

  Let $CompanyString = ''''
  Display 'Enter Company or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Concat '''' With $CompanyString
      Break
    End-If
    If $CompanyString <> ''''
      Concat ''',''' With $CompanyString
    End-If
    Concat $In-Company With $CompanyString
  End-While

  If $CompanyString = ''''''
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '1=1'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_Company_Alias || ' In (' || $CompanyString || ')'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA
  End-If

! CWB 06/11/2001 BEGIN
  Let #GEX_RC_DEPTID_ROWS = 0

  Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = '1=1'
  Let $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA = 'NONE'

  Let $DeptidString = ''''
  Display 'Enter Deptid to exclude or leave blank to exit.'
  While 1=1
    Input $In-deptid Maxlen=10 'deptid'
    Uppercase $In-deptid
    If Rtrim($In-deptid, ' ') = ''
      Concat '''' With $deptidString
      Break
    End-If
    If $deptidString <> ''''
      Concat ''',''' With $deptidString
    End-If
    Concat $In-deptid With $deptidString
  End-While

  If $deptidString = ''''''
    Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = '1=1'
    Let $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA = 'NONE'
  Else
    Let $GEXXX902_EXCLUDE_DEPTID_CRITERIA = $GEXXX902_DEPTID_Alias || ' Not In (' || $deptidString || ')'
    Let $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_EXCLUDE_DEPTID_CRITERIA
  End-If
! CWB 06/11/2001 END

End-Procedure


!*************************************************************************
begin-procedure P050-Print-Cover-Page
#debug9 Show 'P050-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+2,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                 (0,+2)
  Print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)

! CWB 06/11/2001 BEGIN
!  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
!  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	          (0,+2)
  Print '$GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
  Print $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA	          (0,+2)
! CWB 06/11/2001 END

  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


begin-procedure P100-Open-File
#debug9 Show 'P100-Open-File'

  let $file1 = '{OUTFILE}' || lower($ReportID) || '.dat'
 

  open $file1
        as 1
        for-writing
        record=306:fixed
        status=#filestat

  if #filestat != 0
     display 'Error Opening output file.  Program terminating.'
     stop
  end-if
end-procedure
!**************************************************************************
begin-procedure P200-Process-Main
#debug9 Show 'P200-Process-Main'

  do get-pay-dates
  Let $Data-Found-Flag = 'N'
  Let $pos_flag = 'N'	
begin-SELECT
J.EMPLID
J.DEPTID
J.JOBCODE
J.JOB_ENTRY_DT
J.COMPANY          !ISDVSRC : ITG:34012/CSR:10987
!PDE.FIRST_NAME
J.POSITION_NBR		!ISDVMXD added for ITG 56799 on 01/29/2009
NA.FIRST_NAME
!PDE.LAST_NAME
NA.LAST_NAME
!PDE.ADDRESS1
AD.ADDRESS1
!PDE.CITY
AD.CITY
!PDE.STATE
AD.STATE
!PDE.POSTAL
AD.POSTAL
PD.BIRTHDATE
PD.SEX
PD.MAR_STATUS
PD.PHONE &ph
E.HIRE_DT
E.TERMINATION_DT


  Let $Position_Nbr = LTrim(RTrim(&J.POSITION_NBR,' '),' ') !isdvmxd added for ITG 56799 on 01/29/2009
  !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
  !move &J.EMPLID to $emplid xxx-xx-xxxx
  do Get-Emp-Ssn (&J.EMPLID, $Ssn_Out)
  move $Ssn_Out to $emplid xxx-xx-xxxx
  !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
  Show 'Emplid = ' $emplid
  do get-dept-descr
  Do Get-Position-Title				!isdvmxd added for ITG 56799 on 01/29/2009
  Show 'in Main - after position-title, $posname = ' $PosName
  !If $PosName = '' 			!isdvmxd added for ITG 56799 on 01/29/2009
  If $PosName = ' ' 			!isdvmxd added for ITG 56799 on 01/29/2009
  	do get-jobtitle
  	Let $PosName = LTrim(RTrim(&JT.DESCR,' '),' ')	!isdvmxd added for ITG 56799 on 01/29/2009
  End-If				!isdvmxd added for ITG 56799 on 01/29/2009
  
  
  Do Format-DateTime(&E.HIRE_DT, $Hire_Dt, {DEFYMD}, '', '')
  Do Format-DateTime(&E.TERMINATION_DT, $termination_Dt, {DEFYMD}, '', '')
! 04/03/2001 - do not get phone from personal_phone, use personal_data.phone
!  do get-phone
  let $ph = ltrim(&ph,' ')
  if $ph = ''
    let $phone = '(000) 000-0000'
  else
    let $ph1 = translate($ph,'/','')
    let $ph1 = translate($ph1,'(','')
    let $ph1 = translate($ph1,')','')
    let $ph1 = translate($ph1,'-','')
    if length($ph1) < 10
       let $phone = edit($ph1,'xxx-xxxx')
    else
      let $phone = edit($ph1,'(xxx) xxx-xxxx')
    end-if
  end-if

  do P240-Write-Record

FROM   PS_PERS_DATA_EFFDT  PDE,
       PS_PERSONAL_DATA    PD,
       PS_JOB              J,
       PS_EMPLOYMENT       E,
!PSR 09/30/2002 Begin
       PS_NAMES            NA,
       PS_ADDRESSES        AD
!PSR 09/30/2002 End

WHERE  PDE.EMPLID          = PD.EMPLID
AND    PD.EMPLID           = J.EMPLID
AND    J.EMPLID            = E.EMPLID

!PSR  09/30/2002 Begin
AND    AD.EMPLID           = PD.EMPLID
AND    NA.EMPLID           = PD.EMPLID
AND    AD.ADDRESS_TYPE     = 'HOME'
AND    NA.NAME_TYPE        = 'PRI'
AND    (NA.EFFDT = (SELECT MAX(NA_ED.EFFDT) FROM PS_NAMES NA_ED
		   WHERE NA.EMPLID = NA_ED.EMPLID
		   AND NA.NAME_TYPE = NA_ED.NAME_TYPE
		   AND NA_ED.EFFDT <= SYSDATE) )
AND    AD.EFFDT =  (SELECT MAX(AD_ED.EFFDT) FROM PS_ADDRESSES AD_ED
                  WHERE AD.EMPLID = AD_ED.EMPLID
                  AND AD.ADDRESS_TYPE = AD_ED.ADDRESS_TYPE
                  AND AD_ED.EFFDT <= SYSDATE)

!PSR  09/30/2002 End
AND    J.EFFDT             =
      (SELECT MAX(jj.EFFDT) FROM   PS_JOB jj
       WHERE  jj.EMPLID       = J.EMPLID
       AND    jj.EMPL_RCD    = J.EMPL_RCD
       AND    jj.EFFDT       <= SYSDATE)
AND    J.EFFSEQ            =
      (SELECT MAX(jjj.EFFSEQ) FROM   PS_JOB jjj
       WHERE  jjj.EMPLID       = J.EMPLID
       AND    jjj.EMPL_RCD    = J.EMPL_RCD
       AND    jjj.EFFDT        = J.EFFDT)
AND    J.ACTION_DT             =
      (SELECT MAX(jj.ACTION_DT) FROM   PS_JOB jj
       WHERE  jj.EMPLID       = J.EMPLID
       AND    jj.EMPL_RCD    = J.EMPL_RCD
       AND    jj.ACTION_DT   <= SYSDATE)
AND    PDE.EFFDT             =
      (SELECT MAX(EFFDT)
       FROM   PS_PERS_DATA_EFFDT
       WHERE  EMPLID       = PDE.EMPLID
       AND    EFFDT       <= SYSDATE)


!CWB 07/11/2001 BEGIN
!AND   ((J.EFFDT between $pay_begin_dt and $pay_end_dt) OR 
AND   ((J.ACTION_DT between $pay_begin_dt and $pay_end_dt) OR 
       (PDE.EFFDT between $pay_begin_dt and $pay_end_dt) OR 
!PSR 09/30/2002 BEGIN
       (NA.EFFDT between $pay_begin_dt and $pay_end_dt) OR
       (AD.EFFDT between $pay_begin_dt and $pay_end_dt))
!PSR 09/30/2002 END
!CWB 07/11/2001 END


AND   [$GEXXX900_INCLUDE_COMPANY_CRITERIA]

! CWB 06/11/2001 BEGIN
AND   [$GEXXX902_EXCLUDE_DEPTID_CRITERIA]
! CWB 06/11/2001 END

ORDER  BY J.EMPLID
end-SELECT

  If $Data-Found-Flag = 'N'
    Print 'NO ERRORS FOUND FOR THIS INTERFACE RUN'	(25,) Center
  End-If

  do Format-Number(#recs-written,$recs-written,'888,888,888')
  display ''
  display $recs-written noline
  display ' records written to ' noline
  display $file1
  display ''
end-procedure

!***********************************************************************
! 04/03/2001 - comment out get-phone procedure
!begin-procedure get-phone
!  let $phone_found = 'N'
!begin-select
!PP.PHONE &ph
!
!
! move 'Y' to $phone_found
! let $phone = edit(&ph,'(xxx) xxx-xxxx')
!
!from ps_personal_phone pp
!where  PP.EMPLID           = &J.EMPLID
!AND    PP.PHONE_TYPE       = 'HOME'
!end-SELECT
!
! if $phone_found = 'N'
!   move '(000) 000-0000' to $phone 
! end-if
!
!end-procedure
!*********************************************************************

begin-procedure get-dept-descr
begin-select
dt.descr
	
from ps_dept_tbl dt
where dt.deptid = &J.DEPTID
 and  dt.effdt = (select max(effdt) from ps_dept_tbl
                  where deptid = dt.deptid
                    and eff_status = 'A'
                    and effdt <= SYSDATE)
end-select
end-procedure
!**********************************************************************
begin-procedure get-jobtitle
begin-select
jt.descr

	Show 'in jobtitle procedure... jt.descr =' &jt.descr
	
from ps_jobcode_tbl jt
where jt.jobcode = &J.JOBCODE
 and  jt.effdt = (select max(effdt) from ps_jobcode_tbl
                  where jobcode = jt.jobcode
                    and eff_status = 'A'
                    and effdt <= SYSDATE)
end-select
end-procedure
!**********************************************************************
begin-procedure Get-Pay-Dates
 let $Found = 'N'

begin-select loops=1 On-Error=P290-SQL-Error('get-pay-dates')

Y1.PAY_END_DT 
Y1.pay_begin_dt 

  move 'Y' to $Found
  move &Y1.PAY_END_DT to $pay_end_dt
  move &Y1.PAY_BEGIN_DT TO $pay_begin_dt


FROM PS_PAY_CALENDAR Y1
WHERE Y1.PAY_SHEETS_RUN = 'Y'
  AND Y1.PAY_CONFIRM_RUN = 'Y'
  AND Y1.PAY_END_DT = (SELECT MAX(PAY_END_DT)
                         FROM PS_PAY_CALENDAR
                        WHERE PAY_SHEETS_RUN = 'Y'
                          AND PAY_CONFIRM_RUN = 'Y'
                          AND PAY_END_DT <= $AsOfToday)
  AND Y1.RUN_ID > ' '

end-select

  if $Found = 'N'
    display 'Error --  Pay Calendar not found or not setup for the Run ID.'
    print 'Error --  Pay Calendar not found or not setup for the Run ID.' (+1,1,60)
  else
    display 'Processing for Pay End Date: ' noline
    display $pay_end_dt
  end-if
end-procedure

!***************************************************************************

begin-procedure P240-Write-Record
#debug9 Show 'P240-Write-Record'

  add 1 to #recs-written
  do convert-to-dtu-date(&PD.BIRTHDATE,$birthdate_dtu)
  let $birthdate = translate($birthdate_dtu,'-','')
  do convert-to-dtu-date(&E.HIRE_DT,$hire_dtu)
  let $hire_dt = translate($hire_dtu,'-','')
  do convert-to-dtu-date(&E.TERMINATION_DT,$term_dtu)
  let $term_dt = translate($term_dtu,'-','')
  do convert-to-dtu-date(&J.JOB_ENTRY_DT,$job_entry_dtu)
  let $job_entry_dt = translate($job_entry_dtu,'-','')

  write 1 from $emplid:11
               &NA.FIRST_NAME:15
               &NA.LAST_NAME:15           
               &AD.ADDRESS1:25
               &AD.CITY:19
               &AD.STATE:2
               &AD.POSTAL:10
               $phone:14
               $birthdate:8
               &PD.SEX:1
               &PD.MAR_STATUS
               $blank:2
               &J.EMPLID:11
               $hire_dt:8
               $term_dt:8
               !$blank:3	!ISDVSRC : ITG:34012/CSR:10987
               &J.COMPANY:3	!ISDVSRC : ITG:34012/CSR:10987
               &J.JOBCODE:6		
               &J.DEPTID:6
               &DT.DESCR:29
               $blank:40
               &J.JOBCODE:6
               !&JT.DESCR:29		!isdvmxd commented for ITG 56799 on 01/29/2009
               $PosName:29		!isdvmxd added for ITG 56799 on 01/29/2009
               $job_entry_dt:8

    status=#filestat

  if #filestat != 0
     display 'Error Writing output file.  Program terminating.'
     stop
  end-if
end-procedure


!*********************************************************************
begin-procedure P300-Close-File
#debug9 Show 'P300-Close-File'

  close 1
end-procedure
!**********************************************************************
Begin-Procedure P290-SQL-Error($Action)
#debug9 Show 'P290-SQL-Error'
  Show $Action ' failed'
  Show 'SQL Error:  '  $_sql-error
  Stop
End-Procedure
!***************************************************************************
#include 'gexxx900.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexaltse.sqc'  !Alter session SQC  
#Include 'askftd.sqc'    !Ask-From-Thru-Date procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#include 'getposnm.sqc'	 !Get-Position-Title Procedure		!isdvmxd added for ITG 56799
#include 'getgeid.sqc'   !GEX_SEC_REENG 2015-04-29 Vahini Katta

