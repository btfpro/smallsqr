!***********************************************************************
! GEXBN803:  MULTIPLE EVENT RULES FOR PLA AND BENEFIT PROGRAM G04      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXBN803: MULTIPLE EVENT RULES FOR PLA AND BENEFIT PROGRAM G04       *
!                                                                      *        
! Narrative:        Employees in Benefit Program G04 get their coverage*
!                     for 6 months. We need to adjust this to 4 months *
!                     only, if employees in G04 are hired >= 01/01/1981*
!                     and on leave >= 3 months. We cannot setup event  *
!                     rule for this for G04, because we already have   *
!                     one event rule for G04 and action=PLA to set 6   *
!                     months as the coverage.                          *
!                                                                      *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                             #debug8 key variable values              *
!                             #debug7 data returned from sql calls     * 
!                                                                      *  
! SQL Tables:           job                                            *
!                       personal_data                                  *
!                       ben_prog_partic                                *
!                       health_benefit                                 *
!                       life_add_ben                                   *
!                       disability_ben                                 *
!                       pay_calendar                                   *
!                                                                      *
! Written by:           Gene Davis  08/07/1998                         *       
!                                                                      *
! Normally Run:         Monthly                                        *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS        DATE            DESCRIPTION OF THE CHANGE   *
!***********************************************************************
!  GBNC056   GXD           08/07/98      Initial Creation              *
!                                                                      *
!            SXK 	   10/26/1998    Modified to process starting  *
!					 from base tables              *
!                                                                      *
!            AXL           01/26/2000    Added a PS_JOB sub-select to  *
!                                        Process-Base-Table procedure  *
!                                        to prevent termed ee's from   *
!                                        being selected.  Also comment-*
!                                        ed out line limiting Actions  *
!                                        picked to PLA                 *                                                                     *
!            AXL           02/08/2000    Added a procedure to          *
!                                        check for certain PLA records *
!                                        in PS_JOB                     *  
!            JNB           03/15/2000    Removed action_reason = 'FML' *
!            SXC           05/05/2000    Removed the logic where one   *
!                                        day is added to the pay end dt*
!                                        to obtain the next pay begindt*
!                                        Instead the pay_End_dt will be*
!                                        assigned to $range_end_dt     *
!            SXC           05/23/2000    Changed the criteria for      *
!                                        action & action reason        *
!            CJH           01/02/2001    Add criteria to get-pay-end-dt*
!                                        procedure to ignore off-cycle *
!                                        calendar entries              *
! #35547     CXA           06/19/2006    Add criteria to select additional
!                                        benefits programs (R01, R04)  *
!                                        get-pay-end-dt			*
! HR-11081   DXS           10/23/2006    get-pay-end-dt procedure had   *
!					 hard-coded date of 30-APR-2006.*
!					 Removed that and now using     *
!					 $Asofdate. Added criteria to   *
!					 select ben program R06. Mod in *
!					 the procedure			*
!					 process-base-table		*
!									*
! HR-11169   DXS           02/13/2007    More changes in the procedure  *
!                                        process-base-table, for BP     *
!                                        G03, G04                       *
!				         Action_reason list changed in  *
!                                        Find-PLA-Row.			*   
!  ISDVSXP S3 12815285     05/19/2009    Update Coverage Begin Date correctly
!                                         in Bas Tables                           
! VENDKXY                  01/12/2009     Done the changes for ITG 71800*     
!GEX-BN126321 Kundan kumar Yeedibilli 05/11/2012 Added G07 to main logic*
!GEXBN_848_E165715_01 Vahini Katta 02/12/2014  Add the action reason PER
! GEX_SEC_REENG 2015-06-24 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!            MVA           11/11/2015    Added 1Y to the list of plan   *
!                                        type for processing            *
!************************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

begin-report
#debug9 Show 'begin-report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P000-Init-Report
  do P200-Process-Main
  do Get-Current-DateTime
  do End-Report
  do Reset                   !reset.sqc

! DXS, comment this after testing
!  do Rollback-Transaction

 ! DXS, un-comment this after testing
  do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
  Show '$ReportDate=' $ReportDate
  Show '$ReportTime=' $ReportTime
  
end-report


begin-heading 9
#debug9 Show 'begin-heading'
!VENDKXY Done the changes for ITG 71800--Begin
! #Include 'stdhdg01.sqc'
 
  print '            '    (1,1)
  print 'PeopleSoft'      ()          center
  print 'Report ID:  '    (+1,1)
  print $ReportID         ()
  uppercase $ReportTitle
  print $ReportTitle      (,+5)         ! center
  let #RptCol = {ColR} - 2
  page-number             (0,#RptCol)  'Page No.  '
  print 'Run Date '       (+1,#RptCol)
  print $ReportDate       ()
  print 'Run Time '       (+1,#RptCol)
  print $ReportTime       ()
 !VENDKXY Done the changes for ITG 71800 --End
!  Do GEXXX951-Select-Oracle-Instance

!  Print 'Oracle DB: '             (1,1)
!  Print $GEXXX951_Oracle_Instance    ()

!  Print 'Report ID: '       (+1,1)
!  Print $ReportID               ()
!  Print $ReportTitle            () Center
  
!  Print 'SQR Begin Time '   (+1,1)
!  Print $ReportDate             ()
!  Print $ReportTime         (0,+2)
  
  PRINT 'Emplid'            (+2,1)
  PRINT 'Rcd'               (0,14)
  PRINT 'Company'           (0,19)
  PRINT 'Paygroup'          (0,28)
  PRINT 'Plan Type'         (0,38)
  PRINT 'Covrg Elect'       (0,49)
  PRINT 'Coverage Begin Dt'   (0,74)

end-heading

!**************************************************************************
begin-procedure P000-Init-Report
#debug9 Show 'P000-Init-Report'

  move 0 to #emp_count
  let $errorfound = 'N' 
  let #dis_count = 0
  let #life_count = 0
  let #health_count = 0

  move 'GEXBN803'         to $ReportID
  !move 'Multiple Event Rules for PLA and Benefit Program G04, R01, R04, R06' to $ReportTitle   !VENDKXY Done the changes for ITG 71800
  move 'Multiple Event Rules for PLA and Benefit Program G01, G02, G03, G04, R01, R04, R06' to $ReportTitle   !VENDKXY Done the changes for ITG 71800
  move 'Multiple Event Rules for PLA and Benefit Program G01, G02, G03, G04, G07, R01, R04, R06' to $ReportTitle   !GEX-BN126321 Kundan kumar Yeedibilli

  do Stdapi-Init

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate=' $ReportDate
  Show '$ReportTime=' $ReportTime

  let $X000_ORDER_BY                = 'EMPLID'  

  if $prcs_process_instance = ''
    let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
  else
    Let $X000_Where_Select_AsofDate = $AsOfToday
  End-If

!  Do GEXXX923-Select-Plan-Type-Parameters

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
!  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  do get-pay-end-dt

end-procedure

!*********************************************************************
!Gets the pay end date of the previous pay cycle.
!*********************************************************************

Begin-Procedure get-pay-end-dt

move 'N' to $found

begin-select on-error=sql-error-found('Get-Pay-End-Dt')
PAY.PAY_BEGIN_DT
PAY.PAY_END_DT

  move &PAY.PAY_END_DT to $pay_end_Dt
  move &PAY.PAY_END_DT to $range_end_Dt    ! SXC 05/05/2000
  move &PAY.PAY_BEGIN_DT to $pay_begin_dt
  display 'Pay End Date:' noline
  display $pay_end_Dt
  move 'Y' to $found
  exit-select

FROM PS_PAY_CALENDAR PAY

WHERE PAY.PAY_END_DT = (SELECT MAX(PAY_END_DT)
   		      FROM PS_PAY_CALENDAR
		     WHERE PAY_END_DT <= $AsOfToday	
		     !WHERE PAY_END_DT <= '30-JAN-2010'		! DXS, used for testing ! vendkxy testing
! 01/02/2001 - add criteria to ignore off-cycle calendar entries
                       AND PAY_OFF_CYCLE_CAL = 'N')
AND PAY.PAY_OFF_CYCLE_CAL = 'N'
! 01/02/2001 - end
end-select

if $found = 'N'
   display 'No Previous Pay Cycle as of:' (,34)
   display $AsOfToday ()
   stop
else
   ! SXC 05/05/2000. Commented the following lines since the range_end_dt is already set as the
   ! pay_end_dt within the above select.
   ! do convert-to-dtu-date($pay_end_Dt,$range_end_dt_dtu)
   ! do dtu-add-days($range_end_dt_dtu,1,$range_end_dt_dtu)
   ! do convert-from-dtu-date($range_end_dt_dtu,$range_end_dt)
   ! Display 'After adding one day to the pay end date'
   display $range_end_Dt
end-if

end-procedure

!***********************************************************************
begin-procedure P200-Process-Main
#debug9 Show 'P200-Process-Main'

 Let $Data-Found-Flag = 'N'


 let $cobra_event_id = 'HB.COBRA_EVENT_ID   = BPP.cobra_event_id'
 let $base_table = 'PS_HEALTH_BENEFIT HB'
 let $base_table_name = 'H'
 do process-base-table
 
 let $cobra_event_id = '1 = 1'
 let $base_table = 'PS_LIFE_ADD_BEN HB'
 let $base_table_name = 'L'
 do process-base-table
 
 let $base_table = 'PS_DISABILITY_BEN HB'
 let $base_table_name = 'D'
 do process-base-table

  If $Data-Found-Flag = 'N'
     Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
  Else
     do Format-Number(#health_count,$health_count,'9999')
     Print 'Total Employees Processed in Health Benefits: '  (+2,1)
     Print $health_count                     (0,+2)
     do Format-Number(#life_count,$life_count,'9999')
     Print 'Total Employees Processed in Life Benefits: '  (+2,1)
     Print $Life_count                     (0,+2)
     do Format-Number(#dis_count,$dis_count,'9999')
     Print 'Total Employees Processed in Disability Benefits: '  (+2,1)
     Print $dis_count                     (0,+2)

  End-If

end-procedure

!*********************************************************************
begin-procedure process-base-table
!*********************************************************************
	show '$cobra_event_id ' $cobra_event_id
	show '$base_table ' $base_table 
	show '$base_table_name ' $base_table_name
	show '$pay_begin_Dt ' $pay_begin_Dt
	show '$range_end_Dt ' $range_end_Dt

begin-SELECT
JB.EMPLID                   &emplid
   move &emplid          to $emplid

JB.EMPL_RCD                &empl_rcd
   move &empl_rcd        to #empl_rcd

JB.ACTION                   &action
   move &action          to $action

JB.ACTION_REASON            &action_reason
   move &action_reason   to $action_reason
HB.COVERAGE_BEGIN_DT
   move &HB.COVERAGE_BEGIN_DT to $coverage_begin_dt
HB.EFFDT
   move &HB.EFFDT to $effdt
HB.PLAN_TYPE 
   move &hb.plan_type to $plan_type
BPP.BENEFIT_PROGRAM
   move &bpp.benefit_program to $benefit_program    ! CXA 06/19/2006

to_char(EM.SERVICE_DT,'yyyymmdd') &service_dt	   !DXS, 02/13/07. New field defn.
	move &service_dt to $service_dt

  display ' ' noline
  display $emplid noline
!  display ' ' noline
!  display #empl_rcd noline
!  display $action noline
!  display ' ' noline
!  display $action_reason
  display 'Plan Type:' noline
  display $plan_type 
  display 'Coverage Begin Dt:' noline
  display $coverage_begin_dt

  Let $Data-Found-Flag = 'Y'

  show '$benefit_program ' $benefit_program
  show '$service_dt ' $service_dt

! CXA 06/19/2006 - Begin
  evaluate $benefit_program
      when = 'R01'
      when = 'R06'	! DXS, new addition
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-6,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
      when = 'R04'
      when = 'G03'	! DXS, 02/13/07. New addition
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-4,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
      When = 'G04'
       if $service_dt > '20040627'  ! DXS, 02/13/07, New If Condition.
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-6,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       else
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-2,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       end-if
       !VENDKXY Done the changes for ITG 71800   -- Begin
       When = 'G01'
           if $service_dt > '20040628' and  $action_reason = 'STD'
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-4,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       	   end-if
       		 if $service_dt > '20040628' and  $action_reason = 'WC2'
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-3,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       		 end-if
       		 if $service_dt > '20040628' and  $action_reason = 'LOF'
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-3,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       		 end-if
       	 !GEXBN_848_E165715_01 Vahini Katta 02/12/2014 - Begins
          if $service_dt > '20040628' and  $action_reason = 'PER'
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-1,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       		 end-if   
       	 !GEXBN_848_E165715_01 Vahini Katta 02/12/2014 - Ends	 
       When = 'G02'
           if $service_dt > '20040628' and  $action_reason = 'STD'
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-6,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       	   end-if
       		 if $service_dt > '20040628' and  $action_reason = 'WC2'
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-12,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       		 end-if
       		 if $service_dt > '20040628' and  $action_reason = 'LOF'
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-3,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       		 end-if
       !VENDKXY Done the changes for ITG 71800   -- End
       !GEX-BN126321 Kundan kumar Yeedibilli -- Begin
       When = 'G07'
       if $service_dt > '20081208' 
         do convert-to-dtu-date($coverage_begin_dt,$coverage_begin_dt_dtu)
         do dtu-add-months($coverage_begin_Dt_dtu,-6,$new_coverage_dt_dtu)
         do convert-from-dtu-date($new_coverage_dt_dtu,$new_coverage_begin_dt)
       end-if
       !GEX-BN126321 Kundan kumar Yeedibilli--End
  end-evaluate 
! CXA 06/19/2006 - End
  display 'New Coverage Begin Dt:' noline
  display $new_coverage_begin_dt
  
  show 'Doing Find-PLA-Row'
  do Find-PLA-Row
  if $PLA_Row_Found = 'Y'

    evaluate $base_table_name
        when = 'H'
	   show 'Doing update-health-benefit'
           do update-health-benefit
        when = 'L'
	 show 'Doing update-life-add-ben'
         do update-life-add-ben
        when = 'D'
	   show 'Doing update-disability-ben'
           do update-disability-ben
    end-evaluate

  end-if
 
FROM          [$base_table],PS_BEN_PROG_PARTIC BPP,
       PS_JOB JB,
       PS_EMPLOYMENT EM

WHERE  BPP.EMPLID          = JB.EMPLID
 AND   BPP.EMPLID          = EM.EMPLID
 AND   BPP.EMPL_RCD       = JB.EMPL_RCD
 AND   JB.EMPL_RCD        = EM.EMPL_RCD
 AND   JB.EMPL_STATUS      = 'P'
 !AND   JB.ACTION           = 'PLA'  AXL 1/26/2000 
 !CXA 06/19/2006 - Begin
 AND ((BPP.BENEFIT_PROGRAM = 'G04'
 AND   to_char(EM.SERVICE_DT,'yyyymmdd') >= '19810101')  
 OR   (BPP.BENEFIT_PROGRAM = 'R01'
 AND   to_char(EM.SERVICE_DT,'yyyymmdd') >= '20060406'
 AND  JB.ACTION_REASON <> 'LOF')              !VENDKXY Done the changes for ITG 71800
 OR   (BPP.BENEFIT_PROGRAM = 'R04'
 AND   to_char(EM.SERVICE_DT,'yyyymmdd') >= '20060405') 
 OR   (BPP.BENEFIT_PROGRAM = 'R06'
 AND   to_char(EM.SERVICE_DT,'yyyymmdd') >= '20060731'   ! DXS, new addition for R06
 AND  JB.ACTION_REASON <> 'LOF' ) 	!VENDKXY Done the changes for ITG 71800
 OR   (BPP.BENEFIT_PROGRAM = 'G03'
 AND   to_char(EM.SERVICE_DT,'yyyymmdd') > '20040627')		!DXS, 02/13/2007. Added this for G03
  !VENDKXY Done the changes for ITG 71800   -- Begin
  OR   (BPP.BENEFIT_PROGRAM = 'G01'
  AND   to_char(EM.SERVICE_DT,'yyyymmdd') > '20040628')
  OR   (BPP.BENEFIT_PROGRAM = 'G02'
  AND   to_char(EM.SERVICE_DT,'yyyymmdd') > '20040628')
 !VENDKXY Done the changes for ITG 71800   -- end
 !GEX-BN126321 Kundan kumar Yeedibilli -- Begin
 OR   (BPP.BENEFIT_PROGRAM = 'G07'
  AND   to_char(EM.SERVICE_DT,'yyyymmdd') > '20081208')
 !GEX-BN126321 Kundan kumar Yeedibilli-- End
 )
! CXA 06/19/2006 - End
 AND   HB.EMPLID = BPP.EMPLID
 AND   HB.EMPL_RCD = BPP.EMPL_RCD
 AND   [$cobra_event_id]  
 ! MVA 11/11/2015 -- Added 1Y to the plan type
 !AND   HB.PLAN_TYPE IN ('10','11','14','1S','1T','1W','1Z','20','22','30')
 AND   HB.PLAN_TYPE IN ('10','11','14','1S','1T','1W','1Y','1Z','20','22','30')
 AND   HB.COVERAGE_ELECT   = 'T'
 AND   HB.COVERAGE_ELECT_DT >= $pay_begin_Dt
 AND   HB.COVERAGE_ELECT_DT <= $range_end_Dt
 AND   BPP.EFFDT = (SELECT MAX(EFFDT)
		    FROM PS_BEN_PROG_PARTIC
		    WHERE EMPLID          = BPP.EMPLID
		    AND   EMPL_RCD       = BPP.EMPL_RCD
		    AND   COBRA_EVENT_ID  = BPP.COBRA_EVENT_ID)
! AXL 1/26/2000 - Begin
 AND   JB.EFFDT  = (SELECT MAX(EFFDT) FROM PS_JOB
		    WHERE EMPLID          = JB.EMPLID
		    AND   EMPL_RCD       = JB.EMPL_RCD
		    AND   EFFDT		  <= $range_end_Dt)
 AND   JB.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
		    WHERE EMPLID       = JB.EMPLID
		    AND   EMPL_RCD    = JB.EMPL_RCD
		    AND   EFFDT	       = JB.EFFDT)
! AXL 1/26/2000 - End

ORDER BY HB.EMPLID, HB.EMPL_RCD, HB.PLAN_TYPE

end-SELECT

end-procedure

! AXL 2/08/2000 - Begin
!*********************************************************************
! Find PLA row that meets criteria for processing
!*********************************************************************
begin-procedure Find-PLA-Row
! SXC - 05/23/2000 Changed the criteria for action & action reason in the select below
! per Pam North's e-mail dt May 08 2000.

   show '$emplid ' $emplid
   show '#empl_rcd ' #empl_rcd

  let $PLA_Row_Found = 'N'

begin-Select
'x'

  let $PLA_Row_Found = 'Y'

FROM PS_JOB JB2
  WHERE JB2.EMPLID   	= $emplid
   AND  JB2.EMPL_RCD   = #empl_rcd
   AND  JB2.EFFDT =  (SELECT MAX(EFFDT) FROM PS_JOB
                       WHERE EMPLID      = JB2.EMPLID
                          AND EMPL_RCD  = JB2.EMPL_RCD
                          AND ((ACTION     ='PLA' ) OR
                                   (ACTION = 'COR' AND ACTION_REASON IN ('LOF','ST2','ST3','WC1','WC2','STD')))  ! DXS, 02/13/07, 
                          AND EFFDT      <= $range_end_Dt)							 ! List of Action_Reason wasn't 
   AND  JB2.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB								 ! same for both the sub-selects.
		       WHERE EMPLID      = JB2.EMPLID
		          AND EMPL_RCD  = JB2.EMPL_RCD
		          AND EFFDT      = JB2.EFFDT)
		        ! AND ACTION     ='PLA')
   !GEXBN_848_E165715_01 Vahini Katta 02/12/2014 - Begins		        
   !AND  JB2.ACTION_REASON IN ('LOF','ST2','ST3','WC1','WC2','STD')  !JNB 03/15/00 'FML',
   AND  JB2.ACTION_REASON IN ('LOF','ST2','ST3','WC1','WC2','STD','PER')  !JNB 03/15/00 'FML',
   !GEXBN_848_E165715_01 Vahini Katta 02/12/2014 - Ends
end-Select

end-Procedure
! AXL 2/08/2000 - End

!*********************************************************************
! Update Health Benefit
!*********************************************************************
begin-procedure Update-Health-Benefit

begin-sql on-error=SQL-Error-Found('Update-Health-Benefit')
Update ps_health_benefit
set coverage_begin_dt  = $new_coverage_begin_dt
where emplid = $emplid
and   empl_rcd = #empl_rcd
and   plan_type = $plan_type
and   effdt = $effdt
and   coverage_elect = 'T'
end-sql
if #sql-count > 0
   add #sql-count to #health_count
   show 'Doing get-bp-plan-info'
   do get-bp-plan-info
   if $found = 'Y'
      show 'Doing update-bp-plan-info'
      do update-bp-plan-info
      show 'Doing print-record'
      do print-record
   end-if
end-if

end-procedure

!*********************************************************************
! Update Life Add Ben
!*********************************************************************
begin-procedure Update-Life-Add-Ben

!display 'Coverage End Dt: ' noline
!display $covrg_end_dt
!display 'Pay Begin Dt: ' noline
!display $pay_begin_dt

begin-sql on-error=SQL-Error-Found('Update-Life-Add-Ben')
Update ps_life_add_ben
set coverage_begin_dt  = $new_coverage_begin_dt
where emplid = $emplid
and   empl_rcd = #empl_rcd
and   plan_type = $plan_type
and   EFFDT = $effdt
and   coverage_elect = 'T'
end-sql
if #sql-count > 0
   add #sql-count to #life_count
   show 'Doing get-bp-plan-info'
   do get-bp-plan-info
   if $found = 'Y'
      show 'Doing update-bp-plan-info'
      do update-bp-plan-info
      show 'Doing print-record'
      do print-record
   end-if
end-if

end-procedure

!*********************************************************************
! Update Disability Ben
!*********************************************************************
begin-procedure Update-Disability-Ben 

begin-sql on-error=SQL-Error-Found('Update-Disability-Benefit')
Update ps_disability_ben
set coverage_begin_dt  = $new_coverage_begin_dt
where emplid = $emplid
and   empl_rcd = #empl_rcd
and   plan_type = $plan_type
and   EFFDT = $effdt
and   coverage_elect = 'T'
end-sql
if #sql-count > 0
   add #sql-count to #dis_count
   show 'Doing get-bp-plan-info'
   do get-bp-plan-info
   if $found = 'Y'
      show 'Doing update-bp-plan-info'
      show 'event_id in update' #event_id !ISDVSXP S3 12815285 05/19/2009
      do update-bp-plan-info
      show 'Doing print-record'
      do print-record
   end-if
end-if

end-procedure

!*********************************************************************
!Retrives Benefit Program and Event Class from PS_BAS_PARTIC
!*********************************************************************

Begin-Procedure get-bp-PLAN-INFO

move 'N' to $found

begin-select on-error=sql-error-found('Get-BP-Plan-Info')
BPL.SCHED_ID 
BPL.BENEFIT_RCD_NBR
BPL.EVENT_ID
    move &BPL.SCHED_ID    to $sched_id
    move &BPL.BENEFIT_RCD_NBR to #benefit_rcd_nbr
    move &BPL.EVENT_ID to #event_id
    show 'event_id' #event_id !ISDVSXP S3 12815285 05/19/2009
    move 'Y' to $found

FROM PS_BAS_PARTIC BAS, PS_BAS_PARTIC_PLAN BPL

WHERE BAS.EMPLID = $emplid
  AND BAS.EMPL_RCD = #empl_rcd
!  AND BAS.BENEFIT_PROGRAM = 'G04'
  AND BAS.BENEFIT_PROGRAM = $benefit_program	! CXA 06/19/2006
  AND BPL.SCHED_ID = BAS.SCHED_ID
  AND BPL.EMPLID = BAS.EMPLID
  AND BPL.BENEFIT_RCD_NBR = BAS.BENEFIT_RCD_NBR
  AND BPL.EVENT_ID = BAS.EVENT_ID
  AND BPL.PLAN_TYPE = $plan_type
  AND BPL.COVERAGE_ELECT = 'T'
  AND BPL.COVERAGE_BEGIN_DT = $coverage_begin_dt  !ISDVSXP S3 12815285 05/19/2009
end-select

end-procedure



!*********************************************************************
!Updates COVERAGE_BEGIN_DT on Bas Partic Plan Table
!*********************************************************************

Begin-Procedure Update-BP-Plan-Info

display 'Updating BAS_PARTIC_PLAN ' noline
display 'New Coverge Begin Date:' noline
display $new_coverage_begin_Dt

begin-sql on-error=sql-error-found('Update-Ded-Begin-Date')
UPDATE PS_BAS_PARTIC_PLAN
   SET COVERAGE_BEGIN_DT = $new_coverage_begin_dt
WHERE SCHED_ID = $sched_id
  AND EMPLID = $emplid
  AND BENEFIT_RCD_NBR = #benefit_rcd_nbr
  AND EVENT_ID = #event_id
  AND PLAN_TYPE = $plan_type
!  AND BENEFIT_PROGRAM = 'G04'
  AND BENEFIT_PROGRAM = $benefit_program	! CXA 06/19/2006
  AND COVERAGE_ELECT = 'T'
end-sql

end-procedure

!*********************************************************************
begin-procedure Print-Record
#debug9 Show 'Print-Record'

   if #current-line > 51
      new-page
   end-if

!   do Convert-to-dtu-date($cov_end_dt,$cov1)
!   let $c_yy = substr($cov1,1,4)
!   let $c_mm = substr($cov1,6,2)
!   let $c_dd = substr($cov1,9,2)
!   let $cov_all = $c_mm||'/'||$c_dd||'/'||$c_yy

!   do Convert-to-dtu-date($ded_end_dt,$ded1)
!   let $d_yy = substr($ded1,1,4)
!   let $d_mm = substr($ded1,6,2)
!   let $d_dd = substr($ded1,9,2)
!   let $ded_all = $d_mm||'/'||$d_dd||'/'||$d_yy

!   let $c2_yy = substr($coverage_end_dt,1,4)
!   let $c2_mm = substr($coverage_end_dt,6,2)
!   let $c2_dd = substr($coverage_end_dt,9,2)
!   let $c2_all = $c2_mm||'/'||$c2_dd||'/'||$c2_yy

!   do Convert-to-dtu-date($pay_begin_dt,$pay1)
!   let $p_yy = substr($pay1,1,4)
!   let $p_mm = substr($pay1,6,2)
!   let $p_dd = substr($pay1,9,2)
!   let $pay_all = $p_mm||'/'||$p_dd||'/'||$p_yy

print $emplid             (+1,1)  !edit xxx-xx-xxxx ! GEX_SEC_REENG 2015-06-24 Vahini Katta 
print #empl_rcd           (0,15)  edit 9
print $company            (0,21)
print $paygroup           (0,31)
print $plan_type          (0,42)
print 'T'                 (0,54)
print 'FROM'              (0,66)
print $coverage_begin_dt  (0,79)
print 'TO  '             (+1,66)
print $new_coverage_begin_dt (0,79)
print ' '                 (+1,1)

end-procedure

!*********************************************************************
begin-procedure End-Report
  
  Do Get-Current-DateTime
  Print 'SQR End Time '    (+2,1)
  Print $ReportDate            ()
  Print $ReportTime        (0,+2)

end-procedure

!*********************************************************************
begin-procedure SQL-Error-Found($Proc_Name)

let $errorfound = 'Y'
print 'SQL Error in ' (+1,21)
print $Proc_Name       (0,35)
print $_sql-error      (+1,1)
print 'sql-status:'    (+1,1)
print #_sql-status         () edit 99999

print $emplid                     (+1,1)
print #empl_rcd                   (0,12)
print $company                    (0,17)
print $paygroup                   (0,26)
print 'did not update correctly.'  (0,33)

end-procedure

!**************************************************************
#include 'askftd.sqc'    !Ask-From-Thru-Date procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'gexxx951.sqc'  !Get Oracle Database ID
#Include 'gexxx923.sqc'  !Multiple Plan Types
!**************************************************************
