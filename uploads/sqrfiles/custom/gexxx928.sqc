!**************************************************************************
!  GEXXX928:  Retrieve "Multiple" Benefit Program Run Controls and        *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX928_Benefit_Program_Alias                                      *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA                           *
!    $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA                           *
!    $GEXXX928_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA                   *
!    $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA                   *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX928_Benefit_Program_Alias  = 'bpp.benefit_program'         *
!    Do GEXXX928-Select-Benefit-Program-Parameters                        *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    From ps_health_benefit hb                                            *
!    Where [$GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA]                   *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!  INITIALS     DATE            DESCRIPTION OF THE CHANGE                 *
!**************************************************************************
!  SXK          12/08/98        INITIAL CREATION                          *
!  SXK          02/17/99        Added additional right parenthesis        *
!  JDH          10/29/99        When no parms found, let 1=1              *
!**************************************************************************


Begin-Procedure GEXXX928-Select-Benefit-Program-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_BEN_PRGM_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA = ''
  Let $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = ''
  Let $GEXXX928_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_BEN_PRGM.BENEFIT_PROGRAM

  Add 1 To #GEX_RC_BEN_PRGM_ROWS

  If #GEX_RC_BEN_PRGM_ROWS = 1
    Let $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA = '(' || $GEXXX928_benefit_program_Alias || ' In ('''     || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM || ''''
    Let $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = '(' || $GEXXX928_benefit_program_Alias || ' Not In ('''     || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM || ''''
  Else
    Let $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA || ',''' || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM  || ''''
    Let $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA || ',''' || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM  || ''''
  End-If

From  PS_GEX_RC_BEN_PRGM GEX_RC_BEN_PRGM

Where GEX_RC_BEN_PRGM.OPRID                         = [$OprID]
And   GEX_RC_BEN_PRGM.RUN_CNTL_ID                   = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_BEN_PRGM.BENEFIT_PROGRAM,  ' ') != ' '

End-Select

  If #GEX_RC_BEN_PRGM_ROWS = 0
    Let $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA = '1=1'
    Let $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = '1=1'
  Else
    Let $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA || '))'
    Let $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA || '))'
    Let $GEXXX928_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = $GEXXX928_INCLUDE_BENEFIT_PROGRAM_CRITERIA
    Let $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = $GEXXX928_EXCLUDE_BENEFIT_PROGRAM_CRITERIA
  End-If

End-Procedure
