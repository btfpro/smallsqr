!***********************************************************************
! GEXPL003:  Seniority and Union Information Report                    *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL003 :            Seniority and Union Information Report         *
!                                                                      *
! Narrative:            This program lists union information           *
!                       for employees.                                 *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_cal_bal_id                                 *
!                       jobcode_tbl                                    *
!                       acct_cd_tbl                                    *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Multiple Company/Paygroups                   *
!                         Multiple Union Codes                         *
!                         Multiple Employee Status(es)                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! JDH       12/18/98    Initial Creation                               *
! JDH       04/09/99    Added employee status parameter.               *
! JDH       04/15/99    Added logic to exclude salaried employees and  *
!                         independent store owners and families.       *
! SXK       10/21/99    Modified to have union from job_labor          *
! SXK       11/16/99    Modified to get ethnic group from ps_diversity *
! AXC       08/02/2002  Union Code now on Job. Remove Job Labor from
!			Join. 8.3 Upgrade
! GBD       07/15/02    Added Fieldvalue to effective dated selection  *
!                       of the Load-Lookup procedure.                  *
!                       Corrected spacing issue with report output.    
!
! AMS       09/04/07    Retrofitted for 8.3 to 9.0 changes              *
! AXG       09/26/2007  Modified to use GEX_ACCT_CD_TBL in 9.0 Upgrade.*
! GEX isdvnpk      03/25/2008 for rehire dt issue in 90   
!GEX-MXT    03/31/2008  Ethnicity changes                               *
! ISDVASS   04/22/2008  Ethnic Groups pulled from ps_divers_ethnic     *
! VENDAXG      09/24/2008    Modified Code for S3#11373179 	       *
! ISDVMXD   02/02/2009   ITG 56799 JT.Descr changes for SQRs	       * 
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!PT852         10/24/2016       Ujwal Dyasani                          *		
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Landscape)


#define #column1     1
#define #column2     7
#define #column3    38
#define #column4    44
#define #column5    75
#define #column6   106
#define #column7   116
#define #column8   127
#define #column9   136
#define #column10  146
#define #column11  151
#define #column12  162
#define #column13  172


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 10
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg02.sqc'

  if $PayGroup <> $priorPayGroup
    do Get-PayGroup-Data
    move $PayGroup to $priorPayGroup
  end-if
  print 'Pay Group'            (4,01)
  print $PayGroup              (0,13)
  print $PayGroupName          (0,17)

  if $UnionCd <> $priorUnionCd
    do Get-Union-Name
    move $UnionCd to $priorUnionCd
  end-if
  print 'Union'                (5,01)
  print $UnionCd               (0,13)
  print $UnionName             (0,17)

  Print 'UNION'                (+2,{#column9})

  Print 'JOB'                  (+1,{#column3})
  Print 'JOB'                  (+0,{#column4})
  !PT852 - Commented - Ujwal - Begin  
  !Print 'EMPLOYEE'             (+0,{#column6})
  !PT852 - Commented - Ujwal - End
  Print 'EMPLOYMENT'           (+0,{#column7})
  Print 'SERVICE'              (+0,{#column8})
  Print 'SENIORITY'            (+0,{#column9})
  Print 'EEO1'                 (+0,{#column10})
  Print 'EEO'                  (+0,{#column11})
  Print '     HRLY'            (+0,{#column12})

  Print 'STORE'                (+1,{#column1})
  Print 'DEPARTMENT'           (+0,{#column2})
  Print 'CODE'                 (+0,{#column3})
  Print 'DESCRIPTION'          (+0,{#column4})
  Print 'NAME'                 (+0,{#column5})
  !PT852 - Commented - Ujwal - Begin
  !Print 'NUMBER'               (+0,{#column6})
  !PT852 - Commented - Ujwal - End
  !PT852 - Added - Ujwal - Begin
  Print 'SSN'               (+0,{#column6})
  !PT852 - Added - Ujwal - End
  Print 'DATE'                 (+0,{#column7})
  Print 'DATE'                 (+0,{#column8})
  Print 'DATE'                 (+0,{#column9})
  Print 'CODE'                 (+0,{#column10})
  Print 'GROUP'                (+0,{#column11})
  Print '     RATE'            (+0,{#column12})
  Print 'F/P'                  (+0,{#column13})

  Print '-'                    (+1,{#column1},05)    Fill
  Print '-'                    (+0,{#column2},30)    Fill
  Print '-'                    (+0,{#column3},05)    Fill
  Print '-'                    (+0,{#column4},30)    Fill
  Print '-'                    (+0,{#column5},30)    Fill
  Print '-'                    (+0,{#column6},09)    Fill
  Print '-'                    (+0,{#column7},10)    Fill
  Print '-'                    (+0,{#column8},08)    Fill
  Print '-'                    (+0,{#column9},09)    Fill
  Print '-'                    (+0,{#column10},04)   Fill
  Print '-'                    (+0,{#column11},10)   Fill
  Print '-'                    (+0,{#column12},09)   Fill
  Print '-'                    (+0,{#column13},03)   Fill
End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do P150-Set-Optimizer-Goal

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPL003'
  Let $ReportTitle   =   'Seniority and Union Information Report'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  Let $GEXXX901_Company_Alias  = 'J.COMPANY'
  Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'
  Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
  Let $GEXXX913_Union_CD_Alias = 'J.UNION_CD' !SXK 10/21/99 !AXC Upgrade 8.3

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  Let $X000_Order_By = 'Company, Paygroup, Union Code, F/P Time, Employment Date'

  Show '$GEXXX901_Include_Company_Paygroup_Display_Criteria = ' $GEXXX901_Include_Company_Paygroup_Display_Criteria
  Show '$GEXXX913_Include_Union_Cd_Display_Criteria         = ' $GEXXX913_Include_Union_Cd_Display_Criteria
  Show '$GEXXX903_Include_Emp_Stat_Display_Criteria         = ' $GEXXX903_Include_Emp_Stat_Display_Criteria

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Do P130-Get-Last-Confirmed-Pay
  Do P140-Print-Cover-Page
  Do P160-Get-Xlat-Lookup
End-Procedure


Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Display 'Enter Company/Paygroup or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''')'
      Else
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || $In-Paygroup || ''')'
      End-If
      Add 1 To #GEX_RC_PAYGROUP_ROWS
      If #GEX_RC_PAYGROUP_ROWS = 1
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While

  If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If


  Let $UnionCdString = ''''
  Display 'Enter Union Code or leave blank to exit.'
  While 1=1
    Input $In-UnionCd Maxlen=3 'Union Code'
    Uppercase $In-UnionCd
    If Rtrim($In-UnionCd, ' ') = ''
      Concat '''' With $UnionCdString
      Break
    End-If
    If $UnionCdString <> ''''
      Concat ''',''' With $UnionCdString
    End-If
    Concat $In-UnionCd With $UnionCdString
  End-While

  If $UnionCdString = ''''''
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_Union_CD_Alias || ' In (' || $UnionCdString || ')'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  End-If


  Let $EmplStatusString = ''''
  Display 'Enter Employee Status or leave blank to exit.'
  While 1=1
    Input $In-EmplStatus Maxlen=1 'Employee Status'
    Uppercase $In-EmplStatus
    If Rtrim($In-EmplStatus, ' ') = ''
      Concat '''' With $EmplStatusString
      Break
    End-If
    If $EmplStatusString <> ''''
      Concat ''',''' With $EmplStatusString
    End-If
    Concat $In-EmplStatus With $EmplStatusString
  End-While

  If $EmplStatusString = ''''''
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = '1=1'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = $GEXXX903_Emp_Stat_Alias || ' In (' || $EmplStatusString || ')'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
  End-If
End-Procedure


Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXXX901-Select-Company-Paygroup-Parameters
  Do GEXXX913-Select-Union-Cd-Parameters
  Do GEXXX903-Select-Emp-Stat-Parameters
End-Procedure


Begin-Procedure P130-Get-Last-Confirmed-Pay
#debug9 Show 'P130-Get-Last-Confirmed-Pay'
Begin-Select
MAX(PAY_END_DT) &PAY_END_DT

  Let $pay_end_dt = &PAY_END_DT
  Show 'Last confirmed pay end date:  ' $pay_end_dt

FROM   PS_PAY_CALENDAR

WHERE  PAY_END_DT <= $AsOfToday
AND    PAY_CONFIRM_RUN = 'Y'
End-Select
End-Procedure


Begin-Procedure P140-Print-Cover-Page
#debug9 Show 'P140-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX901_Include_Company_paygroup_Display_Criteria ='   (+1,5)
  Print $GEXXX901_Include_Company_paygroup_Display_Criteria       (0,+2)
  Print '$GEXXX913_Include_Union_Cd_Display_Criteria         ='   (+1,5)
  Print $GEXXX913_Include_Union_Cd_Display_Criteria               (0,+2)
  Print '$GEXXX903_Include_Emp_Stat_Display_Criteria         ='   (+1,5)
  Print $GEXXX903_Include_Emp_Stat_Display_Criteria               (0,+2)

  Print 'Last confirmed pay end date:'                            (+2,5)
  Print $pay_end_dt                                               (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,2)
  Print $X000_Order_By                                            (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure P150-Set-Optimizer-Goal
#debug9 Show 'P150-Set-Optimizer-Goal'
  Begin-SQL
!    Alter Session Set OPTIMIZER_GOAL=RULE;   ! AMS 09/04/07
  End-SQL
End-Procedure


begin-procedure P160-Get-Xlat-Lookup
#debug9 Show 'P160-Get-Xlat-Lookup'

  Let $Where='     X.FIELDNAME   = ''ETHNIC_GROUP'''       ||
             ' AND X.EFF_STATUS  = ''A'''                  ||
!             ' AND X.LANGUAGE_CD = ''ENG'''                ||   !AMS 09/04/07
             ' AND X.EFFDT       = '                       ||
                 '(SELECT MAX(EFFDT)'                      ||
!                 ' FROM   XLATTABLE X1'                    ||    !AMS 09/04/07
                 ' FROM   PSXLATITEM X1'                    ||    !AMS 09/04/07
                 ' WHERE  X1.FIELDNAME   = X.FIELDNAME'    ||
!GBD 07/15/2002 Begin
!                 ' AND    X1.LANGUAGE_CD = ''ENG'''        ||
!                 ' AND    X1.LANGUAGE_CD = X.LANGUAGE_CD'  ||             !AMS 09/04/07
                 ' AND    X1.FIELDVALUE = X.FIELDVALUE'    ||
!GBD 07/15/2002 End
                 ' AND    X1.EFFDT      <= ''' || $AsOfToday || '''' ||
                 ' AND    X1.EFF_STATUS  = ''A'')'

  Load-Lookup
    Name=XlatLookup
    Rows=10
!    Table='XLATTABLE X'						!AMS 09/04/07
	  Table = 'PSXLATITEM X'					!AMS 09/04/07
    Key='X.FIELDVALUE'	
    Return_Value='X.XLATSHORTNAME'
    Where=$Where
    Quiet
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select
J.COMPANY   ()  ON-BREAK  LEVEL=1
                          PRINT=NEVER

J.PAYGROUP  ()  ON-BREAK  LEVEL=2
                          PRINT=NEVER
!SXK 10/21/99 Modified from E.UNION_cD
!AXC UPgrade 8.3. Union now on Job.
J.UNION_CD  ()  ON-BREAK  LEVEL=3
                          PRINT=NEVER
                          AFTER=P220-UNION-CHANGE

J.DEPTID
ACT.DESCR
J.JOBCODE
JT.DESCR
PD.NAME
J.EMPLID
! GEX isdvnpk      03/25/2008 for rehire dt issue in 90   
NVL((SELECT JREH.EFFDT FROM PS_JOB JREH WHERE JREH.EMPLID = J.EMPLID AND JREH.EMPL_RCD  = J.EMPL_RCD  AND JREH.EFFDT =
        (SELECT MAX(JREH1.EFFDT) FROM PS_JOB JREH1 WHERE JREH.EMPLID = JREH1.EMPLID AND JREH.EMPL_RCD  = JREH1.EMPL_RCD
          AND JREH1.EFFDT <= $pay_end_dt AND JREH1.ACTION = 'REH') 
    AND JREH.EFFSEQ = (SELECT MAX(JREH2.EFFSEQ) FROM PS_JOB JREH2 WHERE JREH.EMPLID = JREH2.EMPLID AND JREH.EMPL_RCD = JREH2.EMPL_RCD
          AND JREH.EFFDT     = JREH2.EFFDT AND JREH2.ACTION      = 'REH')
    AND JREH.EFFDT >=	NVL((SELECT JREHA.EFFDT FROM PS_JOB JREHA WHERE JREHA.EMPLID = JREH.EMPLID AND JREHA.EMPL_RCD  = JREH.EMPL_RCD
    	  AND JREHA.EFFDT = (SELECT MAX(JREHA1.EFFDT) FROM PS_JOB JREHA1 WHERE JREHA.EMPLID = JREHA1.EMPLID
          AND JREHA.EMPL_RCD  = JREHA1.EMPL_RCD AND JREHA1.EFFDT <= $pay_end_dt AND JREHA1.ACTION = 'HIR') 
    	  AND JREHA.EFFSEQ = (SELECT MAX(JREHA2.EFFSEQ) FROM PS_JOB JREHA2 WHERE JREHA.EMPLID = JREHA2.EMPLID
          AND JREHA.EMPL_RCD  = JREHA2.EMPL_RCD AND JREHA.EFFDT = JREHA2.EFFDT AND JREHA2.ACTION = 'HIR')), '01-jan-1900')), E.HIRE_DT) &EMPLOYMENT_DT
! GEX isdvnpk      03/25/2008 for rehire dt issue in 90   
E.SERVICE_DT
!SXK 10/21/99 - Changed from E.UNION_SENIORITY_DT
!AXC 8.3 Upgrade UNION_SENIORITY_DT now on Job
J.UNION_SENIORITY_DT
JT.EEO1CODE
!Pdd.ETHNIC_GROUP !SXK 11/16/1999 From PS_DIVERSITY table
PDD.ETHNIC_GROUP	!GEX-MXR Modified for Upgrade 9.0 on 09/26/2007 
!DE.ETHNIC_GRP_CD
PDD.DESCRSHORT					! ISDVASS 04/22/2008
J.HOURLY_RT
J.FULL_PART_TIME
J.POSITION_NBR			!ISDVMXD added for ITG 56799 on 02/02/2009


  Let $Data-Found-Flag = 'Y'

  Let $Company  = &J.COMPANY
  Let $Paygroup = &J.PAYGROUP
  Let $UnionCd  = &J.UNION_CD  !SXK 10/21/99 ! AXC 8.3 Upgrade Union_CD now on Job
  Let $Ethnic_Group = &PDD.DESCRSHORT
  Let $Position_Nbr = LTrim(RTrim(&J.POSITION_NBR,' '),' ')	!isdvmxd added for ITG 56799 on 02/02/2009
  show 'EMPLID --> ' &J.emplid
  show 'Ethnic Group --> ' $Ethnic_Group
  Do P210-Print-Record

FROM   PS_JOBCODE_TBL     JT,
      !GEX-AXG Modified for Upgrade 9.0 on 09/26/2007 - Begin
       !PS_ACCT_CD_TBL     ACT,
       PS_GEX_ACCT_CD_TBL     ACT,
       !GEX-AXG Modified for Upgrade 9.0 on 09/26/2007 - End
       PS_PERSONAL_DATA   PD,
       PS_GEX_PERSON GEX, !Vendaxg Added Code for S3#11373179
       PS_EMPLOYMENT      E,
       PS_JOB             J, 
!       PS_DIVERSITY PDD !sxk 10/21/99              AMS 09/04/07
       PS_DIVERS_ETHNIC   DE,                     !    AMS 09/04/07
       PS_ETHNIC_GRP_TBL  PDD
!AXC Remove Job Labor as it is now a view. Fields are on JOb. PS_JOB_LABOR JL,8.3 Upgrade 


WHERE  J.EFFDT            =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID      = J.EMPLID
       AND    EMPL_RCD   = J.EMPL_RCD
       AND    EFFDT      <= $pay_end_dt)

AND    J.EFFSEQ           =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB
       WHERE  EMPLID      = J.EMPLID
       AND    EMPL_RCD   = J.EMPL_RCD
       AND    EFFDT       = J.EFFDT)

AND    E.EMPLID           = J.EMPLID
AND    PD.EMPLID 	= GEX.EMPLID !Vendaxg Added Code for S3#11373179
AND    E.EMPL_RCD        = J.EMPL_RCD
AND    DE.ETHNIC_GRP_CD = PDD.ETHNIC_GRP_CD
AND PDD.EFFDT =
        (SELECT MAX(C_ED.EFFDT) FROM PS_ETHNIC_GRP_TBL C_ED
        WHERE DE.SETID = C_ED.SETID
          AND DE.ETHNIC_GRP_CD = C_ED.ETHNIC_GRP_CD
          AND C_ED.EFFDT <= $pay_end_dt)

!SXK 10/21/1999 begin
!AXC Job Labour is now a view. All fields will be on Job. 8.3 Upgrade
!AND    JL.EMPLID    = J.EMPLID
!AND    JL.EMPL_RCD = J.EMPL_RCD

!AND    JL.EFFDT                =
!      (SELECT MAX(EFFDT)
!       FROM   PS_JOB_LABOR
!       WHERE  EMPLID          = JL.EMPLID
!       AND    EMPL_RCD       = JL.EMPL_RCD
!       AND    EFFDT          <= $pay_end_dt)

!AND    Jl.EFFSEQ               =
!      (SELECT MAX(EFFSEQ)
!       FROM   PS_JOB_LABOR
!       WHERE  EMPLID          = JL.EMPLID
!       AND    EMPL_RCD       = JL.EMPL_RCD
!       AND    EFFDT           = JL.EFFDT)
!SXK 10/21/1999 end
!AXC End
AND    PD.EMPLID          = J.EMPLID

AND    JT.JOBCODE         = J.JOBCODE

AND    JT.EFFDT           =
      (SELECT MAX(EFFDT)
       FROM   PS_JOBCODE_TBL
       WHERE  JOBCODE     = J.JOBCODE
       AND    EFFDT      <= $pay_end_dt)

AND    ACT.ACCT_CD        = J.ACCT_CD
!GEX-AXG Added for Upgrade 9.0 on 09/26/2007 - End
AND    ACT.EFFDT          =														 ! AMS 09/04/07
      (SELECT MAX(GEXACCT.EFFDT)																	! AMS 09/04/07
       FROM   PS_GEX_ACCT_CD_TBL GEXACCT															! AMS 09/04/07
       WHERE  GEXACCT.ACCT_CD     = ACT.ACCT_CD										! AMS 09/04/07
       AND    GEXACCT.EFFDT      <= $pay_end_dt)
!GEX-AXG Added for Upgrade 9.0 on 09/26/2007 - End
!AND    ACT.EFFDT          =														 ! AMS 09/04/07
!      (SELECT MAX(EFFDT)																	! AMS 09/04/07
!       FROM   PS_ACCT_CD_TBL															! AMS 09/04/07
!       WHERE  ACCT_CD     = J.ACCT_CD										! AMS 09/04/07
!       AND    EFFDT      <= $pay_end_dt)									! AMS 09/04/07
AND    J.EMPL_TYPE <> 'S'
!Vendaxg Commented Code for S3#11373179 - Begin
!AND    PD.GEX_FAMILY_MBR NOT IN ('OWN','SPO','FAM')
AND    GEX.GEX_FAMILY_MBR NOT IN ('OWN','SPO','FAM')
!Vendaxg Commented Code for S3#11373179 - End

AND   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
AND   [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]
AND   [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
!SXK 11/16/1999 Begin
!AND PD.EMPLID = PDD.EMPLID							!AMS 09/04/07
AND PD.EMPLID = DE.EMPLID								!AMS 09/04/07
AND DE.PRIMARY_INDICATOR = 'Y' 	!GEX-MXT ADDED FOR ETHNICITY CHANGES
AND DE.REG_REGION = 'USA'								!AMS 09/04/07
!SXK 11/16/1999 End
!SXK 10/21/99 Modified e.union_cd
! GEX isdvnpk      03/25/2008 for rehire dt issue in 90   
!ORDER BY J.COMPANY, J.PAYGROUP, J.UNION_CD, J.FULL_PART_TIME, NVL(E.REHIRE_DT, E.HIRE_DT)
ORDER BY J.COMPANY, J.PAYGROUP, J.UNION_CD, J.FULL_PART_TIME, NVL((SELECT
JREHX.EFFDT
FROM PS_JOB JREHX
  WHERE JREHX.EMPLID    = J.EMPLID
    AND JREHX.EMPL_RCD  = J.EMPL_RCD
    AND JREHX.EFFDT =
        (SELECT MAX(JREHX1.EFFDT) FROM PS_JOB JREHX1
        WHERE JREHX.EMPLID    = JREHX1.EMPLID
          AND JREHX.EMPL_RCD  = JREHX1.EMPL_RCD
          AND JREHX1.EFFDT      <= $pay_end_dt
          AND JREHX1.ACTION      = 'REH') 
    AND JREHX.EFFSEQ =
        (SELECT MAX(JREHX2.EFFSEQ) FROM PS_JOB JREHX2
        WHERE JREHX.EMPLID    = JREHX2.EMPLID
          AND JREHX.EMPL_RCD  = JREHX2.EMPL_RCD
          AND JREHX.EFFDT     = JREHX2.EFFDT
          AND JREHX2.ACTION      = 'REH')
    AND JREHX.EFFDT >=	NVL((SELECT JREHXA.EFFDT
		FROM PS_JOB JREHXA
  		WHERE JREHXA.EMPLID  = JREHX.EMPLID
    		AND JREHXA.EMPL_RCD  = JREHX.EMPL_RCD
    		AND JREHXA.EFFDT =
        		(SELECT MAX(JREHXA1.EFFDT) FROM PS_JOB JREHXA1
        		WHERE JREHXA.EMPLID    = JREHXA1.EMPLID
          		AND JREHXA.EMPL_RCD  = JREHXA1.EMPL_RCD
          		AND JREHXA1.EFFDT      <= $pay_end_dt
          		AND JREHXA1.ACTION      = 'HIR') 
    		AND JREHXA.EFFSEQ =
        		(SELECT MAX(JREHXA2.EFFSEQ) FROM PS_JOB JREHXA2
        		WHERE JREHXA.EMPLID    = JREHXA2.EMPLID
          		AND JREHXA.EMPL_RCD  = JREHXA2.EMPL_RCD
          		AND JREHXA.EFFDT     = JREHXA2.EFFDT
          		AND JREHXA2.ACTION      = 'HIR')), '01-jan-1900')), E.HIRE_DT)
! isdvnpk      03/25/2008 for rehire dt issue in 90   
End-Select
End-Procedure


Begin-Procedure P210-Print-Record
#debug9 Show 'P210-Print-Record'
!  Lookup XlatLookup &pdd.ethnic_group $Ethnic_Group    !ISDVASS 04/22/2008

  Do Format-DateTime (&EMPLOYMENT_DT, $Employment_Date, {DEFMDY}, '', '')
  Do Format-DateTime (&E.SERVICE_DT, $Service_Date, {DEFMDY}, '', '')
!SXK 10/21/999 Changed from E.UNION_SENIORITY_DT
  Do Format-DateTime (&J.UNION_SENIORITY_DT, $Seniority_Date, {DEFMDY}, '', '')
  Do Get-Position-Title		!isdvmxd added for ITG 56799 on 02/02/2009
Let $Employment_Date = Edit(&EMPLOYMENT_DT,'mm/dd/yy')
Let $Service_Date = Edit(&E.SERVICE_DT,'mm/dd/yy')
Let $Seniority_Date = Edit(&J.UNION_SENIORITY_DT,'mm/dd/yy')


  Print &J.DEPTID             (+1,{#column1},05)
  Print &ACT.DESCR            (+0,{#column2},30)
  Print &J.JOBCODE            (+0,{#column3},05)
  !ISDVMXD added for ITG 56799 on 02/02/2009  -  Begin
  If $PosName = ' '
  	Show 'Posname is blank so jt.descr = ' &JT.DESCR
  	Print &JT.DESCR             (+0,{#column4},30)
  Else
  	Show 'position-data-descr = '$PosName
  	Print $PosName		    (+0,{#column4},30)
  End-If
  !ISDVMXD added for ITG 56799 on 02/02/2009  -  End
  Print &PD.NAME              (+0,{#column5},30)
  do Get-Emp-Ssn (&J.EMPLID, $Ssn_Out) !GEX_SEC_REENG 2015-04-29 Vahini Katta  
  !Print &J.EMPLID             (+0,{#column6},09) !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $Ssn_Out              (+0,{#column6},09)  !GEX_SEC_REENG 2015-04-29 Vahini Katta
  Print $Employment_Date      (+0,{#column7},08)
  Print $Service_Date         (+0,{#column8},08)
  Print $Seniority_Date       (+0,{#column9},08)
  Print &JT.EEO1CODE          (+0,{#column10},04)
  Print $Ethnic_Group         (+0,{#column11},10)
  Print &J.HOURLY_RT          (+0,{#column12},09)  Edit 99,990.00
  Print &J.FULL_PART_TIME     (+0,{#column13},01)
End-Procedure


Begin-Procedure P220-UNION-CHANGE
#Debug9 Show 'P220-UNION-CHANGE'
  New-Page
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Procedure


#include 'getcodta.sqc'  !Get-Company-Data Routine
#include 'getpgdta.sqc'  !Get-Paygroup-Data Routine
#include 'getunion.sqc'  !Get-Union-Name Routine
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx913.sqc'  !Get union code multiple row table
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
#include 'getposnm.sqc'	 !Get-Position-Title procedure		!isdvmxd added for ITG 56799 on 02/02/2009
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta