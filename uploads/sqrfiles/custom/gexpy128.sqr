!***********************************************************************
!   GEXPY128:   SET DED_TAKEN and TAX_METHOD on paysheets              *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!                                                                      *
!***********************************************************************
! GEXPY128:             SET DED_TAKEN and TAX_METHOD on paysheets      *
!                                                                      *
! Narrative:            Set DED_TAKEN and TAX_METHOD values for        *
!                       selected earnings codes on paysheet tables.    *
!                                                                      * 
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values              * 
!			      #debug7 data returned from sql calls     * 
!                                                                      *
! SQL Tables:           PAY_OTH_EARNS                                  *  
!                       PAY_EARNINGS                                   * 
!                                                                      *
! Written by:           Carl Buckrop                                   *                                                           
!                                                                      *
! Normally run:         Weekly                                         *
!                                                                      *
! Entered Run                                                          *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                                                                      *
!            		ERNCD     	                               *
!     		                                                       *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS    DATE           DESCRIPTION OF THE CHANGE        *        
!***********************************************************************
! GEXPY128     CWB   05/11/2001   INITIAL CREATION                     *
!                                 Also created GEXXX960.SQC            *
!                                                                      *
! CSR HR10232  SCM   08/06/2002   Modified pay_line_status = 'U' so    *
!                                 paysheets would get re-calced.       *
!                                                                      *
!  HR10381     GBD   11/22/2002   Add Off_Cycle='N' to select stmnt.   *
!                                                                      *
!  HR10306     GBD   02/28/2003   v8.3 Upgrade - added production chgs *
!              SRK   03/05/2003   Added DED_TAKEN_GENL                 *
!                                                                      *
!  HR10493     GBD   07/15/2003   Added Company and Paygroup to run    *
!                                 control selection.                   *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup02.sqc'

!***********************************************************************
begin-heading 7 
!***********************************************************************

#debug9 show 'begin-heading'

 #Include 'stdhdg01.sqc'

  print '-' (+2,1,160) fill

  print 'ERNCD'                (+2,1)
  print 'Company'              (0,7)
  print 'Paygroup'             (0,15)
  print 'Pay End Date'         (0,24)
  print 'Off Cycle'            (0,38)
  print 'Page#'                (0,48) 
  print 'Line#'                (0,54) 
  print 'Addl#'                (0,60)
  print 'Sepck'                (0,67) 
 
end-heading

!*********************************************************************
begin-program
!***********************************************************************

#debug9 show 'begin-report'

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  show 'asoftoday =' $asoftoday
  do stdapi-init
  do p010-Init-Variables 
  do process-report    
  do stdapi-term
  do Reset
end-program

!***********************************************************************
begin-procedure p010-Init-Variables
!***********************************************************************

#debug9 show 'P010-Init-Variables'

  let $x000_new_page_flag = 'N'
  
  let $ReportID           = 'GEXPY128'
  let $ReportTitle        = 'SET DED_TAKEN and TAX_METHOD Report'
  let $Country            = 'USA'

  show '$ReportID= '     $ReportID
  show '$ReportTitle = ' $ReportTitle   

  Do GEXXX951-Select-Oracle-Instance

  Let $GEXXX901_Company_Alias = 'poe.company'                 !GBD 07/15/2003
  Let $GEXXX901_Paygroup_Alias = 'poe.paygroup'               !GBD 07/15/2003
  Do GEXXX901-Select-Company-Paygroup-Parameters              !GBD 07/15/2003

  Let $GEXXX924_ERNCD_Alias = 'POE.ERNCD'                                
  Do GEXXX924-Select-ERNCD-Parameters  
  Do GEXXX960-Select-Parameters  

  Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_GEXPY128.AsOfDate

  if rtrim($X000_WHERE_SELECT_ASOFDATE, ' ' ) = ''
    let $X000_WHERE_SELECT_ASOFDATE = $asoftoday
    LET $AsOfDate = $X000_WHERE_SELECT_ASOFDATE 
  else
    LET $AsOfDate = $X000_WHERE_SELECT_ASOFDATE 
 end-if

  do Get-Date-Codes
  let #AsOfYYYY = to_number($AsOfYYYY)
  let #AsOfMM   = to_number($AsOfMonth)

  do convert-To-DTU-Date($X000_Where_Select_Asofdate,$curr_Date_1)

  move $X000_Where_Select_Asofdate   to  $Curr_Date_dbf_1

  let $curr_date_mm = substr($curr_date_1,6,2)
  let $curr_date_dd = substr($curr_date_1,9,2)
  let $curr_date_yyyy = substr($curr_date_1,1,4)

  show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA     = ' $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA   !GBD 07/15/2003

  show '$GEXXX924_INCLUDE_ERNCD_CRITERIA  = ' $GEXXX924_INCLUDE_ERNCD_CRITERIA   
  show '$X000_WHERE_SELECT_ASOFDATE       = ' $X000_WHERE_SELECT_ASOFDATE 

	print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'     (+5,5)
	print '$Prcs_OPRID          = '                          (+2,5)
	print $Prcs_OPRID                                        (0,+2)
	print '$PRCS_RUN_CNTL_ID    = '                          (+1,5)
	print $PRCS_RUN_CNTL_ID                                  (0,+2)

    	print 'SELECTION CRITERIA FOR THIS REPORT RUN:'          (+5,5)
 	print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = '   (+1,5)   !GBD 07/15/2003
 	print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA        (0,+2)   !GBD 07/15/2003
	print '$GEXXX924_INCLUDE_ERNCD_CRITERIA            = '   (+1,5)                 
	print $GEXXX924_INCLUDE_ERNCD_CRITERIA                   (0,+2)            
	print '$X000_WHERE_SELECT_ASOFDATE                 = '   (+1,5)
	print  $X000_WHERE_SELECT_ASOFDATE                       (0,+2)

 	let #page-count = 0
	NEW-PAGE
	LET #PAGE-COUNT = 1

end-procedure

!***********************************************************************
begin-procedure Process-Report
!***********************************************************************
         
date-time () hh:mi:ss &timeBegan
show 'Report Began: ' &timebegan

 move 'N' to $Errorfound

 do p100-select-for-update

 do p300-finish

end-procedure

!****************************************************************
begin-procedure P100-select-for-update
begin-select On-Error=P900-SQL-Error('P100-select-for-update')
poe.company
poe.paygroup
poe.pay_end_dt
poe.off_cycle
poe.page_num
poe.line_num
poe.addl_nbr
poe.sepchk
poe.erncd

  let $company = &poe.company
  let $paygroup = &poe.paygroup
  let $pay_end_dt = &poe.pay_end_dt
  let $off_cycle = &poe.off_cycle
  move &poe.page_num to $page_nbr 9999 
  move &poe.line_num to $line_nbr 9999 
  move &poe.addl_nbr to $addl_nbr 9999 
  move &poe.sepchk to $sepchk 9999 
  let $erncd = &poe.erncd

  do p200-update-pay-oth-earns
  do p210-update-pay-earnings
  do p400-print-record

from ps_pay_oth_earns poe
where [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]           !GBD 07/15/2003 - Added
and [$GEXXX924_INCLUDE_ERNCD_CRITERIA]
and poe.off_cycle = 'N'                                       !GBD 11/22/2002 - added
and poe.sepchk > 0
and poe.pay_end_dt = (select max(pc.pay_end_dt)
                      from ps_pay_calendar pc
                      where pc.pay_sheets_run = 'Y'
                      and pc.pay_end_dt <= $X000_WHERE_SELECT_ASOFDATE)
end-select

end-procedure

!****************************************************************
begin-procedure P200-update-pay-oth-earns
Begin-sql On-Error=P900-SQL-Error('P200-update-pay-oth-earns')
update ps_pay_oth_earns poe1
   set poe1.tax_method = $tax_method
where   poe1.company     = &poe.company
and	poe1.paygroup	 = &poe.paygroup
and	poe1.pay_end_dt  = &poe.pay_end_dt 
and	poe1.off_cycle   = &poe.off_cycle
and	poe1.page_num       = &poe.page_num
and	poe1.line_num       = &poe.line_num
and	poe1.addl_nbr       = &poe.addl_nbr
and	poe1.sepchk	 = &poe.sepchk 	

end-sql
add #sql-count to #update_poe_count 
end-procedure

!****************************************************************
begin-procedure P210-update-pay-earnings

begin-SQL On-Error=P900-SQL-Error('P210-update-pay-earnings')
update ps_pay_earnings pe
   set pe.ded_taken  = $ded_taken,
       pe.tax_method = $tax_method,
       pe.pay_line_status = 'U',             ! 08/06/2002     SCM  added
       pe.ded_taken_genl = $ded_taken_genl  ! 03/05/2003     SRK 

where   pe.company     = &poe.company
and	pe.paygroup    = &poe.paygroup
and	pe.pay_end_dt  = &poe.pay_end_dt 
and	pe.off_cycle   = &poe.off_cycle
and	pe.page_num       = &poe.page_num
and	pe.line_num       = &poe.line_num
and	pe.addl_nbr       = &poe.addl_nbr
and	pe.sepchk      = &poe.sepchk 	

end-sql
add #sql-count to #update_pe_count
end-procedure

!*****************************************************************
begin-procedure p400-print-record
  print $erncd              (+1,2)  
  print $company            (0,9)
  print $Paygroup           (0,17)
  print $Pay_End_Dt         (0,24)
  print $Off_Cycle          (0,42)
  print $Page_nbr           (0,47) 
  print $Line_nbr           (0,53) 
  print $Addl_nbr           (0,59)
  print $Sepchk             (0,66) 
 
end-procedure
!*****************************************************************
begin-procedure p300-finish

  let $pe_count  = #update_pe_count 
  let $poe_count = #update_poe_count 
  show ' '
  show 'Updates to PS_PAY_EARNINGS : ' $pe_count edit 99,999
  show 'Updates to PS_PAY_OTH_EARNS: ' $poe_count edit 99,999

  print 'Updates to PS_PAY_EARNINGS : ' (+2,10,30) 
  print  $pe_count                      (,40) edit 99,999
  print 'Updates to PS_PAY_OTH_EARNS: ' (+1,10,30)
  print  $poe_count                     (,40) edit 99,999

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

end-procedure
!*********************************************************************

!*********************************************************************
!Displays the error messages and stops execution
!*********************************************************************
 
begin-procedure P900-SQL-Error($Proc_Name) 
  display 'SQL Error in ' 
  display $Proc_Name 
  display $_sql-error
  display 'sql-status:' 
  display #_sql-status 

  STOP

end-procedure
!*********************************************************************

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Get_run_control procedure
 
#Include 'gexxx951.sqc'  !Select Oracle Instance

#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx922.sqc'  !Get multiple row table from GEX_RC_PAY
#include 'gexxx924.sqc'  !Get ERNCD multiple row table
#include 'gexxx960.sqc'  !Get single row values from GEX_RC_GEXPY128
#include 'getdptnm.sqc'  !Get Department Name
#include 'getactnm.sqc'  !Get Acct Cd Name
