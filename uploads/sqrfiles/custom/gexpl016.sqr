!***********************************************************************
!  GEXPL016:  Union Employee List (Baker's List)                       *
!***********************************************************************
!                                                                      *
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL016:  	Baker's List Local 19 Ohio                             *
!                                                                      *
! Narrative:	Creates a report of hours and gross earnings for       *
!                 Baker's Local 19.                                    *
!                                                                      *
! #Debugx Used:	#debug9 paragraph trace                                *
!			#debug8 key variable values                    *
!			#debug7 data returned from sql calls           *
!                                                                      *
!                                                                      * 
! SQL Tables:     PS_GEX_DEPT_TBL                                      *
!			PS_PAY_CAL_BAL_ID	                       *
!			PS_PERSONAL_DATA 	                       *
!			PS_EMPLOYMENT                                  *
!                       PS_JOB                                         *
!			PS_EARNINGS_BAL                                *
!                                                                      *
! Written by:	John Dorsey-SoftLink Inc.  5/29/98                     *
!                                                                      *
! Normally Run:	Between Jan 1 and Feb 28 for prior year                *
!                                                                      *
! Prerequisite: 	None                                           *
!                                                                      *
! Control Parms:	FOR WHERE CLAUSE:                              *
!			Co/Div Code	                               *
!                        Company Division code grouping - multiples    *
!                       Union_cd                                       *
!                        Union to include in report - multiples        *
!                       As Of Date	                               *
!                        Allow for entry or default to todays date.    *
!                                                                      *
!*********************************************************************************************
!                         MODIFICATION HISTORY                         			     *
!*********************************************************************************************
!ID#	INITIALS	DATE	    DESCRIPTION OF THE CHANGE          			     *
!*********************************************************************************************
!GEX001	JWD		5/29/98	    Initial Creation                   			     *
!                                                                      			     *
!           JDH         11/10/98    Modified SQRW run control logic    			     *
!                                                                      			     *
!	    SXK         10/21/99    Changed to have union from job_labor
!                                                                      			     *
!           JNB         12/15/99    Alter Session                      			     *
!                                                                      			     *
!           JNB         12/22/99    Added Jobcode and Jobcode descr    			     *
!                                                                      			     *
!           PSR         08/12/02    Modified UNION_CD FROM JOB_LABOR   			     *
!                                   TO JOB - Upgrade 8.3               			     *
!                                                                      			     *
!           PSR         08/27/02    EMPL_RCD is now part of the key for			     *
!                                   PS_EARNINGS_BAL - Upgrade 8.3      			     *
!                                                                      			     *
!           CXA         12/09/04    Produce File For tranmission Added 			     *
!                                   logic to create Excel file instead 			     *
!                                   of DAT file.                       			     *
!           SRC         03/02/05    Modified Report Title to 'Year End Union Hours and Wages'*
!                                   Deleted Sorted by in the title                           *
!                                   Added DEPT to Run control selection                      *
!                                   Sorted page break by DEPTID - so added 2 new procedures  *
!                                   According to new specs -                                 *
!                                   Added New procedure: P300-Get-Earnings-Bal-178           *
!				                            to calcualte the Actual earnings ($) under the earnings  *
!				                            code 178                                                 *
!				                                                                                     *
!                                   Added EARN.CD to the data file at the end                *
!				                            print EARN.CD on the .lis file		             *	
!
!           AMS	        09/10/2007  8.3 TO 9.0 UPGRADE RETROFIT CHANGES	
!	
!         VENDTKK       02/05/2009 Added INCLUDE GETPOSNM.SQC to pull JOBTITLE from          *
!				   POSITION_DATA.DESCR field instead of JOBCODE_TBL.DESCR    *
!			           	                              			     *										*
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!*********************************************************************************************

!selection criteria
#define #x000_col_selection_criteria_a 	2
#define #x000_col_selection_criteria_b 	5

!line 1
#define #x000_col_emplid		1 
#define #x000_col_name			13
#define #x000_col_jobcode 		44	!jnb 12/22/1999
#define #x000_col_jobcd_descr		56	!jnb 12/22/1999
#define #x000_col_servicedt		68
#define #x000_col_full_part		82	!CXA 12/09/2004
#define #x000_col_Empl_status		93	!CXA 12/09/2004
#define #x000_col_Termdt		106	!CXA 12/09/2004
#define #x000_col_hourly_rt		118	!CXA 12/09/2004
#define #x000_col_Gross			130	!CXA 12/09/2004
#define #x000_col_Hours			146	!CXA 12/09/2004
#define #x000_col_Earnings_cd           162     !ISDVSRC 

!line totals
#define #x000_col_totals_literal	4
#define #x000_col_totals_literal2	20
#define #x000_col_totals_literal3	30


#include 'setenv.sqc' !Set environment
#Include 'setup02.sqc'  !Printer and page-size initialization


begin-heading 7

 let $Company = rtrim($x000_Company,' ')

 #Include 'stdhdg01.sqc'

 !let $x000_temp_out =  'Sorted by Union Code, Name' 
 !print $x000_temp_out 			(3,) center 

let $x000_temp_out =  'For ' || $X000_REPORT_HEADING_ASOFDATE 
 !print $x000_temp_out 	            (4,) center 
 print $x000_temp_out 	            (3,) center 
 
 print 'Union Code:'			(0,1)
 print $x000_union_cd			(0,13)
 print $unionname			(0,21)

 print 'Dept ID:'			(+1,1)
 print $x000_Dept_Id			(0,13)

!line 1
!         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7 
!12345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
!EMPLID      NAME                           JOBCODE     DESCR       SERVICE DATE  Full/Part  Empl Status  Term Date   HOURLY RT      GROSS WAGE           HOURS
!xxx-xx-xxxx xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx xxxxxxxxxx  xxxxxxxxxx  xx/xx/xxxx    X          X            XX/XX/XXXX  xxx,xxx.xx  xxx,xxx,xxx.xx  xxx,xxx,xxx.xx
 print 'Emplid'	        (+2,{#x000_col_emplid})
 print 'Name'      	    (0,{#x000_col_name})
 Print 'JobCode'        (0,{#x000_col_jobcode})       !jnb 12/22/1999
 print 'Descr'          (0,{#x000_col_jobcd_descr})   !jnb 12/22/1999
 print 'Service Date'	  (0,{#x000_col_servicedt})	
 print 'Full/Part'	    (0,{#x000_col_full_part})	!CXA 12/09/2004
 print 'Empl Status'	  (0,{#x000_col_Empl_status})	!CXA 12/09/2004
 print 'Term Date'	    (0,{#x000_col_Termdt})		!CXA 12/09/2004
 print 'Hourly Rt'      (0,{#x000_col_hourly_rt})
 print '  Gross Wage'   (0,{#x000_col_Gross})
 print '    Hours'      (0,{#x000_col_Hours})
 print 'Premium Wages'  (0,{#x000_col_Earnings_cd})     !ISDVSRC 03/02/05
 print 'Earn code:178'  (+1,{#x000_col_Earnings_cd})    !ISDVSRC 03/02/05
 
end-heading


!***********************************************************************
begin-report
!***********************************************************************
#debug9 show 'begin-report'

!  do Alter-Session   ! Set Session to RULE Optimizer   JNB 12/16/1999							AMS  09/10/2007
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  show '$asoftoday=' $asoftoday 
 
  do StdAPI-Init
  do P010-Init-Variables

  let #write_rec1 = 0		!CXA 12/09/2004

  do P100-Get-Job-Data

  do StdAPI-Term
  do Reset

  show 'Successful End of Report'

end-report


!***********************************************************************
begin-procedure P010-Init-Variables
!***********************************************************************
#debug9 show 'P010-Init-Variables'

!  do P012-Array-Initialization

  do Get-Calendar-Year-Id	!returns designator for calendar year as stored
				!on the installation table
  show '$Calendar_Year_Id=' $Calendar_Year_Id

  let $x000_new_page_flag  = 'N'
  let $x000_new_page_flag2 = 'N'

  let $ReportID    = 'GEXPL016'
  let $ReportTitle = 'Year End Union Hours and Wages'
  let $Country     = 'USA'
 
   encode '<009>' into $tab
   
  show '$ReportID=' $ReportID 
  show '$ReportTitle=' $ReportTitle
  show '$ReportDate=' $ReportDate
  show '$ReportTime=' $ReportTime
 
  Let $GEXXX907_Div_CD_Alias   = 'GEX_DEPT.GEX_COMPANY_DIV_CD'
  Let $GEXXX913_UNION_CD_Alias = 'J.UNION_CD'                  !SXK 10/21/99
  Let $GEXXX902_Deptid_Alias   = 'GEX_DEPT.DEPTID'             !ISDVSRC 03/05/05

!RUN CONTROL LOGIC
  if $Prcs_Process_Instance = ''
	do P015-get-input
  else
 
!	get parameters from single row run control record 
	Do GEXXX922-Select-Parameters
      let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.ASOFDATE
      if rtrim($GEX_RC_PAY.ASOFDATE,' ') = ''
        let $X000_WHERE_SELECT_ASOFDATE = $asoftoday
      end-if

!	get parameters from multiples run control record
         
	Do GEXXX907-Select-DIV-CD-Parameters
	Do GEXXX913-Select-UNION-CD-Parameters
	Do GEXXX902-Select-Deptid-Parameters       !ISDVSRC
          
  end-if

  !let $X000_ORDER_BY = 'J.UNION_CD, P.NAME'  !sxk 10/21/99
  let $X000_ORDER_BY = 'J.DEPTID,J.UNION_CD, P.NAME'  !ISDVSRC 

  do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE ,{DEFDATE}, '', '')

  show '$X000_WHERE_SELECT_ASOFDATE                      =' $X000_WHERE_SELECT_ASOFDATE 
  show '$GEXXX907_INCLUDE_DIV_CD_CRITERIA                =' $GEXXX907_INCLUDE_DIV_CD_CRITERIA
  show '$GEXXX913_INCLUDE_UNION_CD_CRITERIA              =' $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  show '$GEXXX902_INCLUDE_DEPTID_CRITERIA                =' $GEXXX902_INCLUDE_DEPTID_CRITERIA
  show '$X000_ORDER_BY                                   =' $X000_ORDER_BY

  print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,{#x000_col_selection_criteria_a})
  print '$Prcs_OPRID          ='                              (+1,{#x000_col_selection_criteria_b})
  print $Prcs_OPRID                                           (0,+2)
  print '$PRCS_RUN_CNTL_ID    ='                              (+1,{#x000_col_selection_criteria_b})
  print $PRCS_RUN_CNTL_ID                                     (0,+2)

  print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,{#x000_col_selection_criteria_a})
  print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA 	    (0,+2)
  print '$GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA         ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA 	    (0,+2)
  
  print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA         ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA 	    (0,+2) 
  
  print '$X000_WHERE_SELECT_ASOFDATE                         ='   (+1,{#x000_col_selection_criteria_b})
  print $X000_WHERE_SELECT_ASOFDATE                           (0,+2)
   
  print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,{#x000_col_selection_criteria_a})
  print '$X000_ORDER_BY                                      ='   (+1,{#x000_col_selection_criteria_b})
  print $X000_ORDER_BY 	(0,+2)


  LET #PAGE-COUNT = 0
  NEW-PAGE
  LET #PAGE-COUNT = 1

end-procedure

!******************************************
begin-procedure P012-Array-Initialization
!******************************************
#debug9 show 'P012-Array-Initialization'

!TOTALS ARRAY *****************  Total Type *****************************
!                     (0)                  (1)                   (2)	
!Level
!(0) Grand Total	MTD/YTD 401K	MTD/YTD Gross Wage	MTD/YTD Hours
!(1) Company	MTD/YTD 401K	MTD/YTD Gross Wage	MTD/YTD Hours
!(2) Deptid		MTD/YTD 401K	MTD/YTD Gross Wage	MTD/YTD Hours
!(3) Union Cd	MTD/YTD 401K	MTD/YTD Gross Wage	MTD/YTD Hours
	 
  create-array name=Total-Array size=4 -
    field=TotalName:char 
    field=total:number:3

  put 'Grand Total'      into Total-Array(0) TotalName
  put 'Company Total'    into Total-Array(1) TotalName
  put 'Deptid'		 into Total-Array(2) TotalName
  put 'Union Cd'         into Total-Array(3) TotalName
 
end-procedure

!***********************************************************************
begin-procedure P015-get-input
!***********************************************************************
#debug9 show 'P015-get-input'

  Do Ask-As-Of-Date
  Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate
  !---------------------------------------------------------------------------
  Let $DivCdString = ''''
  Display 'Enter Company Division Code or leave blank to exit.'
  While 1=1
    Input $In-DivCd Maxlen=3 'Company Division Code'
    Uppercase $In-DivCd
    If Rtrim($In-DivCd, ' ') = ''
      Concat '''' With $DivCdString
      Break
    End-If
    If $DivCdString <> ''''
      Concat ''',''' With $DivCdString
    End-If
    Concat $In-DivCd With $DivCdString
  End-While

  If $DivCdString = ''''''
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = '1=1'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = $GEXXX907_Div_CD_Alias || ' In (' || $DivCdString || ')'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA
  End-If
  !---------------------------------------------------------------------------
  Let $UnionCdString = ''''
  Display 'Enter Union Code or leave blank to exit.'
  While 1=1
    Input $In-UnionCd Maxlen=3 'Union Code'
    Uppercase $In-UnionCd
    If Rtrim($In-UnionCd, ' ') = ''
      Concat '''' With $UnionCdString
      Break
    End-If
    If $UnionCdString <> ''''
      Concat ''',''' With $UnionCdString
    End-If
    Concat $In-UnionCd With $UnionCdString
  End-While

  If $UnionCdString = ''''''
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_Union_CD_Alias || ' In (' || $UnionCdString || ')'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  End-If
 !--------------------------------------------------------------------------- ! ISDVSRC 03/02/05
 
  	 Let $DeptIDString = ''''
  	 Display 'Enter DeptID or leave blank to exit.'
	  While 1=1
	    Input $In-DeptID Maxlen=10 'DeptID'
	    Uppercase $In-DeptID
	    If Rtrim($In-DeptID, ' ') = ''
	      Concat '''' With $DeptIDString
	      Break
	    End-If
	    If $DeptIDString <> ''''
	      Concat ''',''' With $DeptIDString
	    End-If
	    Concat $In-DeptID With $DeptIDString
	  End-While
	
	  If $DeptIDString = ''''''
	    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
	    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
	  Else
	    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
	    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
	  End-If
  !--------------------------------------------------------------------------- ! ISDVSRC 03/02/05
 
end-procedure


!******************************************
begin-procedure P100-Get-Job-Data
!******************************************
#debug9 show 'P100-Get-Job-Data'

!CXA 12/09/2004 - Begin
!display 'Begin process'

  !ISDVSRC 03/02/05
   Evaluate $Prcs_Run_Cntl_ID
     When = 'gexpl016_y' 
      Let $File_Name = 'gexpl016_y'
      Let $file1 = '{OUTFILE}' || Lower($File_Name) || '.xls'
     break
     When = 'gexpl016_ya' 
      Let $File_Name = 'gexpl016_ya'
      Let $file1 = '{OUTFILE}' || Lower($File_Name) || '.xls'
     break
     When = 'gexpl016_yb' 
      Let $File_Name = 'gexpl016_yb'
      Let $file1 = '{OUTFILE}' || Lower($File_Name) || '.xls'
     break
   End-Evaluate
   show '$Prcs_Run_Cntl_ID:' $Prcs_Run_Cntl_ID
   !ISDVSRC 03/02/05
 
 
  open $file1 as 1 for-writing
        record=218:vary	
        status=#filestat
  if #filestat < 0
     display 'Open failed for file <' noline
     display $file1 noline
     display '>'
     stop
  end-if

!open $filename as 1
!open 'c:\temp\gexbn603.rpt' as 1
!     for-writing record=500
!
!if #writestat != 0
!   display 'Error Opening output file.  Program terminating.'
!   stop
!end-if

move 'N' to $rowfound
let $end_process = 'N'
let #inputtran = 0
!CXA 12/09/2004 - End

  let $x100_Get_Job_Data_Flag = 'N'

begin-SELECT 
!sxk 10/21/99 CHANGED from emp.union_Cd
J.DEPTID        () on-break print=never after=P875-Dept-After-Change         ! ISDVSRC 03/02/05
				before=P775-Dept-Before-Change								
				level=01
J.UNION_CD	() on-break print=never after=P875-Union-Cd-After-Change 
				before=P775-Union-Cd-Before-Change								
				level=02
P.NAME
J.COMPANY
J.EMPLID
J.EMPL_RCD
J.EFFDT
J.EFFSEQ
J.EMPL_STATUS
J.HOURLY_RT
J.JOBCODE   		!jnb 12/22/1999
J.FULL_PART_TIME	!CXA 12/09/2004
J.Position_nbr 		!VENDTKK    ITG#56786   5-Feb-2009
EMP.SERVICE_DT
EMP.TERMINATION_DT	!CXA 12/09/2004

PCB.PAY_END_DT
PCB.BALANCE_YEAR
PCB.BALANCE_QTR
PCB.BALANCE_PERIOD

GEX_DEPT.DEPTID
GEX_DEPT.EFFDT
GEX_DEPT.SEQUENCE_NUMBER
GEX_DEPT.GEX_COMPANY_DIV_CD


  #debug7 show '******* JOB DATA **************'
  #debug7 show '&J.COMPANY=' &J.COMPANY
  #debug7 show '&J.DEPTID=' &J.DEPTID 
  #debug7 show '&J.PAYGROUP=' &J.PAYGROUP 
  #debug7 show '&J.EMPLID=' &J.EMPLID
  #debug7 show '&J.EMPL_RCD=' &J.EMPL_RCD
  #debug7 show '&J.EFFDT=' &J.EFFDT
  #debug7 show '&J.EFFSEQ=' &J.EFFSEQ
  #debug7 show '&J.HOURLY_RT=' &J.HOURLY_RT
  #debug7 show '&J.EMPL_STATUS=' &J.EMPL_STATUS
  #debug7 show '&P.NAME=' &P.NAME
  #debug7 show '&EMP.SERVICE_DT=' &EMP.SERVICE_DT
  #debug7 show '&EMP.TERMINATION_DT=' &EMP.TERMINATION_DT
  #debug7 show '&J.UNION_CD=' &J.UNION_CD  !SXK 10/21/99

!  &EMP.BENEFIT_RCD_NBR 

 let #x200_balance_year = &PCB.BALANCE_YEAR
 let #x200_balance_qtr = &PCB.BALANCE_QTR
 let #x200_balance_period = &PCB.BALANCE_PERIOD 
 let $x200_deptid = &J.DEPTID
 let $x200_Union_Cd = &J.UNION_CD  !SXK 10/21/99 
 
 let $x200_Dept_Id  = &J.DEPTID   		  !ISDVSRC
 SHOW '&PCB.BALANCE_PERIOD :' &PCB.BALANCE_PERIOD !ISDVSRC 
 
 let $x200_Name = &P.NAME
! let $x600_Service_Dt = &EMP.SERVICE_DT
! let $x600_Termination_Dt = &EMP.TERMINATION_DT	!CXA 12/09/2004
 let $x200_Emplid = &J.EMPLID
 let $x200_Full_Part = &J.FULL_PART_TIME	!CXA 12/09/2004

  do Format-DateTime(&EMP.service_dt, $x600_servicedt,{DEFDATE}, '', '')
  do Format-DateTime(&EMP.termination_dt, $x600_Termination_dt,{DEFDATE}, '', '')

  let $Jump_Record = 'N'
  evaluate &J.EMPL_STATUS		!CXA 12/09/2004
    when = 'A'
    when = 'L'
    when = 'P'
    when = 'S'
       let $x200_Empl_Status = ' '
       break
    when-other
       let $x200_Empl_Status = &J.EMPL_STATUS
       let $Termination_Dt = &EMP.TERMINATION_DT
       if substr($X000_WHERE_SELECT_ASOFDATE,10,2) <> substr($Termination_Dt,10,2)
         let $Jump_Record = 'Y'
       end-if
  end-evaluate

  if $Jump_Record = 'Y'
    Goto Jump-Record
  end-if

! let $x100_JobCode = &J.JOBCODE

		!when you find a savings plan record, whether waived or elected
		!if $x150_Get_Savings_Data_Flag = 'Y'
  let $setid='COMMN'                     !kmw 3/13/2000 correct missing title
  let $JobCode  = rtrim(&J.JobCode,' ')  !jnb 12/22/1999
  do Get-Job-Title                       !jnb 12/22/1999

  !VENDTKK  Begin  ITG#56786   5-Feb-2009
  Let $Position_Nbr = &J.Position_nbr
  Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosNameAbbrv  = '' 
  		Let $JobTitleAbbrv = $JobTitleAbbrv
	 Else
 		Let $JobTitleAbbrv = $PosNameAbbrv 
	 End-if
!VENDTKK End   ITG#56786  5-Feb-2009

  Do P300-Get-Earnings-Bal
  Do P300-Get-Earnings-Bal-178          !ISDVSRC 03/06/2005
  Do Get-Emp-Ssn (&J.EMPLID, $Ssn_Out) !GEX_SEC_REENG 2015-04-29 Vahini Katta
  do P600-Write-Detail-Report-Line
  do P650-Write-Detail-File-Line	!CXA 12/09/2004
  add 1 to #inputtran			!CXA 12/09/2004
		!end-if

  let $x100_Get_Job_Data_Flag = 'Y'

Jump-Record:

FROM  PS_GEX_DEPT_TBL   GEX_DEPT,
      PS_PAY_CAL_BAL_ID	PCB,
      PS_PERSONAL_DATA 	P,
      PS_EMPLOYMENT 	EMP,
      PS_JOB          	J

WHERE P.EMPLID = J.EMPLID
 AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
 AND [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]
 AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]  !ISDVSRC
 AND J.EFFDT  =
    		(SELECT MAX(EFFDT)
       		FROM PS_JOB
      		WHERE EMPLID  = J.EMPLID
        		AND EMPL_RCD = J.EMPL_RCD
        		AND EFFDT    <= $X000_WHERE_SELECT_ASOFDATE)
  	AND J.EFFSEQ =
    		(SELECT MAX(EFFSEQ)
       		FROM PS_JOB
      		WHERE EMPLID  = J.EMPLID
        		AND EMPL_RCD = J.EMPL_RCD
        		AND EFFDT     = J.EFFDT)
	AND J.COMPANY      = PCB.COMPANY 
	AND J.PAYGROUP     = PCB.PAYGROUP
	AND PCB.PAY_END_DT = (SELECT MAX(PCB1.PAY_END_DT) FROM PS_PAY_CAL_BAL_ID PCB1
					WHERE PCB1.COMPANY = PCB.COMPANY AND 
					PCB1.PAYGROUP = PCB.PAYGROUP AND
					PCB1.PAY_END_DT <= $X000_WHERE_SELECT_ASOFDATE)
	AND PCB.BALANCE_ID = $Calendar_Year_Id
	AND J.EMPLID       = EMP.EMPLID
	AND J.EMPL_RCD    = EMP.EMPL_RCD
	AND J.DEPTID       = GEX_DEPT.DEPTID 

	AND GEX_DEPT.EFFDT = (SELECT MAX(GEX_DEPT1.EFFDT) FROM PS_GEX_DEPT_TBL GEX_DEPT1
			WHERE GEX_DEPT1.DEPTID = GEX_DEPT.DEPTID AND
				GEX_DEPT1.EFFDT <= $X000_WHERE_SELECT_ASOFDATE) 
	AND GEX_DEPT.SEQUENCE_NUMBER = (SELECT MAX(GEX_DEPT2.SEQUENCE_NUMBER) FROM PS_GEX_DEPT_TBL GEX_DEPT2
			WHERE GEX_DEPT2.DEPTID = GEX_DEPT.DEPTID AND
				GEX_DEPT2.EFFDT = GEX_DEPT.EFFDT)

ORDER BY [$x000_order_by]
					
end-SELECT

  
 if $x100_Get_Job_Data_Flag = 'N'
	print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) CENTER 
 end-if

   close 1
   
   move #write_rec1 to $rec1  999,999
   

   display 'Employee Records:  ' noline
   display $rec1
   let $end_process = 'Y'
   print 'Total Employees processed:  '  (+2,29)
   print $rec1                                ()
  

end-procedure


!***************************************
begin-procedure P300-Get-Earnings-Bal
!***************************************
#debug9 show 'P300-Get-Earnings-Bal'

 
 let $P300_Get_Earnings_Bal_Flag = 'N'
 let #x300_hrs_ytd = 0
 let #x300_grs_ytd = 0

begin-SELECT
EB.EMPLID
EB.COMPANY
EB.BALANCE_ID 
EB.BALANCE_YEAR 
EB.BALANCE_QTR 
EB.BALANCE_PERIOD 
EB.SPCL_BALANCE
EB.ERNCD   
EB.HRS_MTD
EB.HRS_YTD 
EB.GRS_MTD
EB.GRS_YTD 

#DEBUG7 show '********** EARNINGS BALANCE DATA ****************'

 #DEBUG7 show 'EB.EMPLID=' &EB.EMPLID
 #DEBUG7 show 'EB.COMPANY=' &EB.COMPANY
 #DEBUG7 show 'EB.BALANCE_ID=' &EB.BALANCE_ID 
 #DEBUG7 show 'EB.BALANCE_YEAR=' &EB.BALANCE_YEAR 
 #DEBUG7 show 'EB.BALANCE_YEAR=' &EB.BALANCE_QTR  
 #DEBUG7 show 'EB.BALANCE_PERIOD=' &EB.BALANCE_PERIOD 
 #DEBUG7 show 'EB.SPCL_BALANCE=' &EB.SPCL_BALANCE
 #DEBUG7 show 'EB.ERNCD=' &EB.ERNCD   
 #DEBUG7 show 'EB.BALANCE_PERIOD=' &EB.BALANCE_PERIOD
 #DEBUG7 show 'EB.HRS_MTD=' &EB.HRS_MTD
 #DEBUG7 show 'EB.HRS_YTD=' &EB.HRS_YTD 
 #DEBUG7 show 'EB.GRS_MTD=' &EB.GRS_MTD   
 #DEBUG7 show 'EB.GRS_YTD =' &EB.GRS_YTD 
 
 let $EB.ERNCD     = &EB.ERNCD                    !ISDVSRC
 
 let #x300_hrs_ytd = #x300_hrs_ytd + &EB.HRS_YTD 
 let #x300_grs_ytd = #x300_grs_ytd + &EB.GRS_YTD 

 
 let $P300_Get_Earnings_Bal_Flag = 'Y'

FROM  PS_EARNINGS_BAL EB

WHERE EB.EMPLID         = &J.EmplID
  AND EB.COMPANY        = &J.Company
  AND EB.SPCL_BALANCE   = 'N'
  AND EB.BALANCE_ID     = $Calendar_Year_Id
  AND EB.BALANCE_YEAR   = &PCB.BALANCE_YEAR
  AND EB.EMPL_RCD       = &J.EMPL_RCD
  AND EB.BALANCE_PERIOD =
      (SELECT MAX(EB1.BALANCE_PERIOD)
       FROM   PS_EARNINGS_BAL EB1
       WHERE  EB1.EMPLID        = EB.EmplID
         AND  EB1.COMPANY       = EB.Company
         AND  EB1.ERNCD         = EB.ERNCD
         AND  EB1.SPCL_BALANCE  = EB.SPCL_BALANCE
         AND  EB1.BALANCE_ID    = EB.BALANCE_ID
         AND  EB1.BALANCE_YEAR  = EB.BALANCE_YEAR
         AND  EB1.EMPL_RCD      = EB.EMPL_RCD
         AND  EB1.BALANCE_PERIOD <= &PCB.BALANCE_PERIOD)
end-SELECT
end-procedure


!*****************************************
!BEGIN - ISDVSRC
!Actual Dollar amount earned for earnings code 178 
begin-procedure P300-Get-Earnings-Bal-178
!*****************************************
#debug9 show 'P300-Get-Earnings-Bal-178'

 let $P300_Get_Earnings_Bal_Flag_178 = 'N'
 let #x300_hrs_ytd_178 = 0
 let #x300_grs_ytd_178 = 0

begin-SELECT
EB2.EMPLID
EB2.COMPANY
EB2.BALANCE_ID 
EB2.BALANCE_YEAR 
EB2.BALANCE_QTR 
EB2.BALANCE_PERIOD 
EB2.SPCL_BALANCE
EB2.ERNCD   
EB2.HRS_MTD
EB2.HRS_YTD 
EB2.GRS_MTD
EB2.GRS_YTD 

  let $EB2.ERNCD     = &EB2.ERNCD                    
 
 let #x300_hrs_ytd_178 = #x300_hrs_ytd_178 + &EB2.HRS_YTD 
 let #x300_grs_ytd_178 = #x300_grs_ytd_178 + &EB2.GRS_YTD 

    
 let $P300_Get_Earnings_Bal_Flag_178 = 'Y'

FROM  PS_EARNINGS_BAL EB2

WHERE EB2.EMPLID         = &J.EmplID
  AND EB2.COMPANY        = &J.Company
  AND EB2.SPCL_BALANCE   = 'N'
  AND EB2.BALANCE_ID     = $Calendar_Year_Id
  AND EB2.BALANCE_YEAR   = &PCB.BALANCE_YEAR
  AND EB2.EMPL_RCD       = &J.EMPL_RCD
  AND EB2.ERNCD          = '178'         		
  AND EB2.BALANCE_PERIOD =
      (SELECT MAX(EB3.BALANCE_PERIOD)
       FROM   PS_EARNINGS_BAL EB3
       WHERE  EB3.EMPLID        = EB2.EmplID
         AND  EB3.COMPANY       = EB2.Company
         AND  EB3.ERNCD         = EB2.ERNCD
         AND  EB3.SPCL_BALANCE  = EB2.SPCL_BALANCE
         AND  EB3.BALANCE_ID    = EB2.BALANCE_ID
         AND  EB3.BALANCE_YEAR  = EB2.BALANCE_YEAR
         AND  EB3.EMPL_RCD      = EB2.EMPL_RCD
         AND  EB3.BALANCE_PERIOD <= &PCB.BALANCE_PERIOD)
end-SELECT
end-procedure  P300-Get-Earnings-Bal-178
!END - ISDVSRC


!*****************************************
begin-procedure P600-Write-Detail-Report-Line
!*****************************************
#debug9 show  'P600-Write-Detail-Report-Line'
  !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
  !print &J.EMPLID         	  (+1,{#x000_col_emplid})		edit xxx-xx-xxxx
  print $Ssn_Out              (+1,{#x000_col_emplid})		edit xxx-xx-xxxx
  !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
  print &P.NAME       		    (0,{#x000_col_name},30)
  print $Jobcode		          (0,{#x000_col_jobcode})         !jnb 12/22/1999
  print $JobTitleAbbrv		    (0,{#x000_col_jobcd_descr},10)  !jnb 12/22/1999
  print $x600_SERVICEDT		    (0,{#x000_col_servicedt})
  print $x200_Full_Part		    (0,{#x000_col_full_part})	!CXA 12/09/2004
  print $x200_Empl_Status	    (0,{#x000_col_Empl_status})	!CXA 12/09/2004
  print $x600_Termination_Dt	(0,{#x000_col_Termdt})		!CXA 12/09/2004
  print &J.HOURLY_RT		      (0,{#x000_col_hourly_rt})  EDIT 999,990.00
  print #x300_grs_ytd 		    (0,{#x000_col_Gross}) EDIT 999,999,990.00
  print #x300_hrs_ytd 		    (0,{#x000_col_Hours}) EDIT 999,999,990.00
  print #x300_grs_ytd_178     (0,{#x000_col_Earnings_cd})              ! ISDVSRC 03/02/05
  
 add 1 to #write_rec1
end-procedure

!*****************************************
begin-procedure P650-Write-Detail-File-Line
!*****************************************
#debug9 show  'P650-Write-Detail-File-Line'

  move &J.HOURLY_RT	  to $HOURLY_RT 999,990.00
  move #x300_grs_ytd	  to $x300_grs_ytd 999,999,990.00
  move #x300_hrs_ytd	  to $x300_hrs_ytd 999,999,990.00
  move #x300_grs_ytd_178  to $x300_grs_ytd_178 999,999,990.00    !ISDVSRC


!previously writing to a .dat file  !ISDVSRC


!  write 1 from &J.EMPLID:9
!	       ' ':1
!               &P.NAME:30
!	       ' ':1
!               $Jobcode:10
!	       ' ':1
!               $JobTitleAbbrv:10
!	       ' ':1
!               $x600_SERVICEDT:8
! 	       ' ':1
!               $x200_Union_Cd:3
!	       ' ':1
!               $x200_Full_Part:1
!	       ' ':1
!               $x200_Empl_Status:1
!	       ' ':1
!               $x600_Termination_Dt:8
!	       ' ':1
!               $HOURLY_RT:10
!	       ' ':1
!               $x300_grs_ytd:14
!	       ' ':1
!               $x300_hrs_ytd:14
!               ' ':1
!               $x300_grs_ytd_178:14               !ISDVSRC 03/02/05

! now to an .xls file

     ! If an EMPLID has leading zeros, excel will eliminates and writes the rest
     ! below logic writes everything to the file
      !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
        
     !let $emplid =  '="' || &J.EMPLID || '"'    
     let $emplid =  '="' || $Ssn_Out  || '"'   
     !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
     !show '$emplid   :' $emplid
     !show '&J.EMPLID :' &J.EMPLID 

     Move '' To $all_cols
     
    
     string  $emplid &P.NAME $Jobcode $JobTitleAbbrv $x600_SERVICEDT $x200_Union_Cd $x200_Full_Part $x200_Empl_Status $x600_Termination_Dt $HOURLY_RT $x300_grs_ytd $x300_hrs_ytd $x300_grs_ytd_178 by $tab into $all_cols
     write 1 from $all_cols Status=#filestat
     
     !Add 1 To #recs-written 
end-procedure

!*****************************************
begin-procedure P775-Union-Cd-Before-Change
!*****************************************
#debug9 show 'P775-Union-Cd-Before-Change'
				
 if  $x000_new_page_flag = 'Y'
	!new-page
	let $x000_new_page_flag = 'N'
 end-if
!SXK 10/21/99 changed from emp.union_cd
 move &J.UNION_CD to $x000_Union_Cd
 move &J.UNION_CD to $UnionCd
 do Get-Union-Name
 #debug8 show '$x000_Union_Cd=' $x000_Union_Cd

end-procedure


!*****************************************
begin-procedure P875-Union-Cd-After-Change
!*****************************************
#debug9 show 'P875-Union-Cd-After-Change'
#debug8 show '$x000_Union_Cd=' $x000_Union_Cd

 show 'P875-Union-Cd-After-Change'     
 show '$x000_Union_Cd=' $x000_Union_Cd
  
 let $x000_new_page_flag = 'Y'

end-procedure
!****************************************




!*****************************************
!ISDVSRC 03/02/05
begin-procedure P775-Dept-Before-Change
!*****************************************
#debug9 show 'P775-Dept-Before-Change'
				
 if  $x000_new_page_flag2 = 'Y'
	new-page
	let $x000_new_page_flag2 = 'N'
 end-if
 move &J.DEPTID  to $x000_Dept_Id
 !move &J.DEPTID  to $UnionCd
 !do Get-Union-Name
 #debug8 show '$x000_Dept_Id=' $x000_Dept_Id

end-procedure


!*****************************************
!ISDVSRC 03/02/05
begin-procedure P875-Dept-After-Change
!*****************************************
#debug9 show 'P875-Union-Cd-After-Change'
#debug8 show '$x000_Dept_Id=' $x000_Dept_Id

 show 'P875-Union-Cd-After-Change'     
 show '$x000_Dept_Id=' $x000_Dept_Id
  
 let $x000_new_page_flag2 = 'Y'

end-procedure
!*****************************************


#Include 'gexaltse.sqc'  !Alter session
#Include 'askaod.sqc'    !Get SQRW run controls
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'getdednm.sqc'  !Get-Deduction-Name procedure
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#include 'getunion.sqc'  !Get-Union-Name procedure
#include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'reset.sqc'     !Reset 
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx913.sqc'  !Get Union code multiple row table
#include 'gexxx902.sqc'  !Get-Deptid-Parameters
#include 'getjobtl.sqc'  !Get-Job-Title procedure        !jnb 12/22/1999
#Include 'getposnm.sqc'  !Get-Position-Title    VENDTKK ITG#56786   5-Feb-2009  
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta