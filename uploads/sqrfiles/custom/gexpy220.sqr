
!***************************************************************************************
! GEXPY220:            Smart Track load compare Report	                               * 
!                                                                                      *
!***************************************************************************************
!                                                                                      *
!               Confidentiality Information:                                           *
!                                                                                      *
! This module is the confidential and proprietary information of                       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted                 *
! in any form, by any means, in whole or in part, nor is it to be used                 *
! for any purpose other than that for which it is expressly provided                   *
! without the written permission of Giant Eagle.                                       *
!                                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved                         *
!                                                                                      *
!***************************************************************************************
!                                                                                      *        
! GEXPY220 :										*
!                                                                                      *        
! Narrative:    Smart Track load compare Report. This process compares hourly rate on  *
!payline that was passed from  SMARTTRACK to hourly rate in PS. If the ST rate is lower,*
!the payline hourly needs to be updated (via GEXPY221). Departments are picked from 
!PS_GEX_DEPT_TBL where Time collection type' is SmartTrack and excludes 0600, 0626, 
!W002 and W007 departments which are added in the run control of GEXPY220.
!Deptids 0600, 0626, W002 and W007 are in the run control for 659 but not for 220
! because we don’t want to update their paylines	                               * 
!                                                                                      *  
! SQL Tables:  PS_PAY_CALENDAR,PS_PAY_CHECK,PS_PAY_EARNINGS,PS_PAY_OTH_EARNS,PS_JOB    *
!                                                                                      *
! Written by:           Kundan Kumar Y	                                               *       
!                                                                                      *
! Normally Run:		Every Monday through Autosys after Gexpyta4.							       *
!                                                                                      *
! Control Parms:	pay end date,Dept id,erncd       				*
!											*
!***************************************************************************************
!                         MODIFICATION HISTORY                                          *
!****************************************************************************************
! CSR    INITIALS     DATE         DESCRIPTION OF THE CHANGE                            *
!****************************************************************************************
!                                                                                       *
!        VENDKXY      06/24/2009   New program created for ITG 62251                    *
!	 VENDKXY      04/09/2010   Done the changes for ITG 73755. Include departments in
! 				   run control is removed and departments are picked 
!				   from PS_GEX_DEPT_TBL where Time collection type' is 
!				   SmartTrack and excludes 0600, 0626, W002 and W007 
!				   departments which are added in the run control	*
!
! Modification Log
! GEX_SEC_REENG 09/18/2016 Vahini Katta                                                 *
! Changes related to SSN Conversion                                                     *
!****************************************************************************************

#include 'setenv.sqc'    !set environment
#include 'setup32.sqc'   !printer and page-size initialization




!**************************************************************************************************************************
!        REPORT
!**************************************************************************************************************************
begin-Report
! Processing RunControl Parameters

 Do Init-DateTime           !datetime.sqc
 Do Init-Number             !number.sqc
 Do Get-Current-DateTime    !curdttim.sqc
 Do Stdapi-Init             !stdapi.sqc


     do Get-Run-Control	     !gexrnctl.sqc  
    If Rtrim (&asofdate, ' ') = ''
    Let $AsOfDate = $AsOfToday
   else
    let $AsOfDate = &asofdate
   end-if


 Let $ReportID      =   'GEXPY220'
 Let $ReportTitle   =   'SMART TRACK TIME LOAD COMPARE REPORT'
 
 do Select-Pay-End-Dt
 Let $GEXXX902_Deptid_Alias = 'A.DEPTID'  
 Do GEXXX902-Select-Deptid-Parameters 	  
 Let $GEXXX924_ERNCD_Alias = 'C.ERNCD'
 Do GEXXX924-Select-ERNCD-Parameters 
 Do rangeoferncd

   
 Show '$ReportID    = ' $ReportID
 Show '$ReportTitle = ' $ReportTitle
 Show '$ReportDate  = ' $ReportDate
 Show '$ReportTime  = ' $ReportTime
 !show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA=' $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA !vendkxy
 show '$GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA=' $GEXXX902_EXCLUDE_DEPTID_DISPLAY_CRITERIA !vendkxy
 show '$GEXXX924_EXCLUDE_ERNCD_DISPLAY_CRITERIA=' $GEXXX924_EXCLUDE_ERNCD_DISPLAY_CRITERIA
 show '$ERNCDRANGE=' $ERNCDRANGE
   
   Do Openfile     
   do processdata
  
  Do Stdapi-Term             !stdapi.sqc
end-Report

!*********************************************************************
!Prints the header information in the report.
!*********************************************************************

begin-heading 6

#Include 'stdhdg01.sqc'
print 'Pay Period End  ' (3,1)
do Format-DateTime($Pay_End_Dt, $out, {DEFDATE}, '', '')
  print $out ()
  print 'COMPANY' (+2,1)
  print 'PAYGROUP' (,9)
  print 'PAYENDDT' (,18)
  print 'DEPTID'  (,29)
  ! GEX_SEC_REENG 09/18/2016 Vahini Katta
  !print 'SSN' (,36)
  print 'EMPLID' (,36)
  ! GEX_SEC_REENG 09/18/2016 Vahini Katta
  print 'NAME' (,46)
  print 'JOBCODE' (,78)
  print 'ERNCD' (,86)
  print 'OTHHRS' (,92)
  print 'EMPTYPE' (,99)
  print 'PAGE NUM' (,107)
  print 'LINE NUM' (,115
  print 'ADDL NBR' (,123)
  print 'ERN END DT' (,132)
  print 'PAYHYRT' (,145)
  print 'JOBHYRT' (,153)
  print  'DIFF'	(,161)
  print '-' (+1,1,168) fill
  
end-heading

!**************************************************************************************
!  Open File
!**************************************************************************************
Begin-Procedure Openfile
      
       encode '<009>' into $delim
       
      Let $file220 = '{OUTFILE}' || 'GEXPY220' || '.txt'  
      Open $file220 As 1 For-Writing Record=1000:Vary
      
        Let $file220_1 = '{OUTFILE}' || 'GEXPY220' || '.xls'  
      Open $file220_1 As 2 For-Writing Record=1000:Vary
      
      string 'COMPANY' 'PAYGROUP' 'DEPTID' 'SSN' 'NAME' 'JOBCODE' 'ERNCD' 'OTH HRS' 'EMPL TYPE' 'PAGE NUM' 'LINE NUM' 'ADDL NBR' 'EARNS END DT' 'EARNS HRLY RT' 'JOB HRLY RT' 'PAY END DT' BY $DELIM INTO $GEXPY220
      
      WRITE 2 FROM $GEXPY220
      
End-Procedure

  
!**************************************************************************************************************************
!		Earnning codes range
!**************************************************************************************************************************

Begin-Procedure rangeoferncd

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let $ERNCDRANGE_Alias = 'C.ERNCD'	
  
Begin-Select 

PS_GEX_RC_ERNCD_1.ERNCD2
PS_GEX_RC_ERNCD_1.ERNCD3
  

 Let $ERNCDRANGE =  $ERNCDRANGE_Alias || ' NOT BETWEEN '''     || &PS_GEX_RC_ERNCD_1.ERNCD2 || '''' || ' AND ''' || &PS_GEX_RC_ERNCD_1.ERNCD3 || ''''
   
  
From  PS_GEX_RC_ERNCD_1 PS_GEX_RC_ERNCD_1

Where PS_GEX_RC_ERNCD_1.OPRID                 = [$OprID]
And   PS_GEX_RC_ERNCD_1.RUN_CNTL_ID           = [$Run_Cntl_ID]
And   PS_GEX_RC_ERNCD_1.EFFDT = (SELECT MAX(EFFDT) FROM PS_GEX_RC_ERNCD_1 
WHERE OPRID = PS_GEX_RC_ERNCD_1.OPRID AND RUN_CNTL_ID = PS_GEX_RC_ERNCD_1.RUN_CNTL_ID)
End-Select
end-procedure
!*********************************************************************

begin-procedure Select-Pay-End-Dt
begin-select
MAX(PC.PAY_END_DT)      &pay_end_dt
  move &pay_end_dt   to $pay_end_dt
FROM   PS_PAY_CALENDAR  PC
WHERE  PC.PAY_END_DT              =
      (SELECT MAX(PAY_END_DT)
       FROM   PS_PAY_CALENDAR
       WHERE  PAY_END_DT      <= $ASOFDATE)

end-select

 SHOW '$pay_end_dt==' $pay_end_dt
end-procedure

!*********************************************************************

begin-procedure processdata
 show 'process data'
begin-select DISTINCT 
A.COMPANY (+1,1) 
A.PAYGROUP (,9)
TO_CHAR(A.PAY_END_DT,'YYYY-MM-DD') &pay_dt (,18)
A.DEPTID (,29)
A.EMPLID (,36)
A.NAME (,46)
B.JOBCODE (,78)
C.ERNCD (,86)
C.OTH_HRS (,92)
E.EMPL_TYPE (,99)
B.PAGE_NUM (,107)
B.LINE_NUM (,115)
B.ADDL_NBR (,123)
B.EARNS_END_DT (,132) 
B.HOURLY_RT (,145) 
E.HOURLY_RT (,153) 
(round(B.HOURLY_RT,4) - round(E.HOURLY_RT,4)) (,161)

  Let #job.hlyrt = round(&E.HOURLY_RT,4)
  Let #earn.hlyrt = round(&B.HOURLY_RT,4)

    
    STRING #earn.hlyrt $pay_end_dt #job.hlyrt &A.EMPLID &B.JOBCODE &B.EARNS_END_DT BY ';' INTO $GEXPY220
    
    WRITE 1 FROM $GEXPY220
    
    string &A.COMPANY  &A.PAYGROUP  &A.DEPTID  &A.EMPLID &A.NAME &B.JOBCODE &C.ERNCD &C.OTH_HRS &E.EMPL_TYPE &B.PAGE_NUM &B.LINE_NUM &B.ADDL_NBR &B.EARNS_END_DT  #earn.hlyrt  #job.hlyrt &pay_dt by $delim into $gexpy220
     
    write 2 from $GEXPY220


!FROM PS_PAY_CHECK A, PS_PAY_EARNINGS B, PS_PAY_OTH_EARNS C, PS_JOB E !vendkxy
FROM PS_PAY_CHECK A, PS_PAY_EARNINGS B, PS_PAY_OTH_EARNS C, PS_JOB E,ps_gex_dept_tbl gdt !vendkxy
WHERE A.COMPANY = B.COMPANY 
AND A.PAYGROUP = B.PAYGROUP 
AND A.PAY_END_DT = B.PAY_END_DT 
AND A.OFF_CYCLE = B.OFF_CYCLE 
AND A.PAGE_NUM = B.PAGE_NUM 
AND A.LINE_NUM = B.LINE_NUM 
AND A.SEPCHK = B.SEPCHK 
AND B.COMPANY = C.COMPANY 
AND B.PAYGROUP = C.PAYGROUP 
AND B.PAY_END_DT = C.PAY_END_DT 
AND B.OFF_CYCLE = C.OFF_CYCLE 
AND B.PAGE_NUM = C.PAGE_NUM 
AND B.LINE_NUM = C.LINE_NUM 
AND B.ADDL_NBR = C.ADDL_NBR 
AND A.PAY_END_DT = $pay_end_dt
and B.Earns_end_dt= $pay_end_dt
and a.emplid= e.emplid
and a.empl_rcd = e.empl_rcd
AND E.EFFDT          =
     (SELECT MAX(EFFDT)
	FROM   PS_JOB 
	WHERE  EMPLID    = E.EMPLID
	AND    EMPL_RCD = E.EMPL_RCD
	AND    EFFDT    <= $pay_end_dt)
AND   E.EFFSEQ         =
     (SELECT MAX(EFFSEQ)
	FROM   PS_JOB
	WHERE  EMPLID    = E.EMPLID
	AND    EMPL_RCD = E.EMPL_RCD
	AND    EFFDT     = E.EFFDT)
AND E.EMPL_TYPE <>'S'
AND (round(B.HOURLY_RT,4) - round(E.HOURLY_RT,4)) < 0
!VENDKXY done the changes for ITG 73755-- Begin
and gdt.gex_time_collect = 'SmartTrack'
and A.DEPTID = gdt.deptid
And   gdt.effdt        =
     (Select Max(effdt)
      From   ps_gex_dept_tbl
      Where  deptid    = a.deptid
      And    effdt    <= $pay_end_dt)
And   gdt.sequence_number =
     (Select Max(sequence_number)
      From   ps_gex_dept_tbl
      Where  deptid    = gdt.deptid
      And    effdt     = gdt.effdt)
!AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND [$GEXXX902_EXCLUDE_DEPTID_CRITERIA]
!VENDKXY done the changes for ITG 73755-- End
AND [$GEXXX924_EXCLUDE_ERNCD_CRITERIA]
AND [$ERNCDRANGE]
order by a.company,a.paygroup,a.emplid,a.deptid

end-select
end-procedure

!*********************************************************************
#include 'stdapi.sqc'
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#include 'gexrnctl.sqc'  !for Run-Cntrl-Parameters
#include 'gexxx902.sqc'  !dept id parameters 
#include 'gexxx924.sqc'  !EARN CODE parameters
