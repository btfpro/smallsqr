!***********************************************************************
!  GEXHR038:  GetGo Turnover Analysis by Account Code.                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
! GEXHR038:             GetGo Turnover Analysis by Account Code        *
!                                                                      *
! Narrative:            This program creates a monthly employee        *
!                       GetGo Turnover Analysis Report with a break-up *
!                       for each Account Code in a Department.         *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           PS_JOB                                         *
!                       PS_PERSON                                      *
!                       PS_R_ACTION_COUNTS                             *
!                                                                      *
! Written by:           Ramesh Solai (cloned from GEXHR036)            *
!                                                                      *
! Normally Run:         Monthly                                        *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS    DATE           DESCRIPTION OF THE CHANGE                *
!***********************************************************************
!  TCSXRAS     07/24/2014     Cloned from GEXHR036 and adapted for     *
!                             Account Code wise break-up.              *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

#define #column1    01
#define #column2    13
#define #column3    43
#define #column4    51
#define #column5    61
#define #column6    72
#define #column7    84
#define #column8    94
#define #column9   105
#define #column10  111

!-----------------------------------------------------------------------
begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
begin-report
!-----------------------------------------------------------------------
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
   
  if $prcs_oprid=''
     goto last2
  end-if
  
  do Process-Main
  do Reset
  do Stdapi-Term
  
last2:
end-report
!-----------------------------------------------------------------------

!-----------------------------------------------------------------------
begin-heading 3
!-----------------------------------------------------------------------
  #Include 'stdhtr08.sqc'
 
!  print 'Begin'                   (+2,{#column3})
!  print 'Hires/'                    (,{#column4})
!  print 'Termin-'                   (,{#column5})
!  print 'Retire-'                   (,{#column6})
!  print 'Transfers'                 (,{#column7})
!  print 'Transfers'                 (,{#column8})
!  print 'End'                       (,{#column9})
!  print 'Turnover'                 (,{#column10})
      
!  print 'Acct_cd'                 (+1,{#column1})          !RAS
!  print 'Descr     '                (,{#column2})          !RAS
!  print 'Count +'                   (,{#column3})
!  print 'Rehires -'                 (,{#column4})
!  print 'ations -'                  (,{#column5})
!  print 'ments +'                   (,{#column6})
!  print '     In +'                 (,{#column7})
!  print '    Out ='                 (,{#column8})
!  print 'Count'                     (,{#column9})
!  print '    Rate'                 (,{#column10})

end-heading

begin-procedure Init-Report

  Let $ReportTitle   = 'GetGo Turnover Analysis'
  do Delete-Worktable

  do Stdapi-Init

  if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT, PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto last1
  end-if

  if $prcs_process_instance = ''
     do Ask-From-Thru-Date
     do GEXXX950-Input-Parameters
  else
     do Select-Parameters
     Let $FromDate=&RUN_CNTL_HR.FROMDATE
     Let $ThruDate=&RUN_CNTL_HR.THRUDATE
  end-if
  
  move $Prcs_Run_Cntl_ID to $ReportID
  uppercase $ReportID
  
  show 'Before Calling Procedure'
  show '$FromDate:  ' $FromDate
  show '$ThruDate:  ' $ThruDate
  
  show '  $AsOfToday:' $AsOfToday
  if $FromDate='' and $ThruDate=''
     show 'Before SetDefault-Begin-End-Date'
     do Process-Scheduler-Run-Controls
  end-if
  
  show 'After Calling Procedure'
  show '$FromDate:  ' $FromDate
  show '$ThruDate:  ' $ThruDate
  
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')
  
last1:
end-procedure

begin-procedure Get-Values

   let $language_cd = $PRCS_LANGUAGE_CD
   do Get-From-Thru-Date
   
end-procedure

!-----------------------------------------------------------------------
begin-procedure Print-Header
!-----------------------------------------------------------------------
 
  print 'Begin'                   (+2,{#column3})
  print 'Hires/'                    (,{#column4})
  print 'Termin-'                   (,{#column5})
  print 'Retire-'                   (,{#column6})
  print 'Transfers'                 (,{#column7})
  print 'Transfers'                 (,{#column8})
  print 'End'                       (,{#column9})
  print 'Turnover'                 (,{#column10})
      
  print 'Acct_cd'                 (+1,{#column1})          !RAS
  print 'Descr     '                (,{#column2})          !RAS
  print 'Count +'                   (,{#column3})
  print 'Rehires -'                 (,{#column4})
  print 'ations -'                  (,{#column5})
  print 'ments +'                   (,{#column6})
  print '     In +'                 (,{#column7})
  print '    Out ='                 (,{#column8})
  print 'Count'                     (,{#column9})
  print '    Rate'                 (,{#column10})

end-procedure

!*************************************************
Begin-Procedure P140-Get-Last-Confirmed-Pay
!*************************************************
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

   Show '** Using this date for selecting last confirmed pay: ' $AsOfToday

Begin-Select Loops=1
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'fmMonth YYYY')           &period
Last_Day(pay_end_dt)                         &current_month_end
Last_Day(Add_Months(pay_end_dt,-1))          &prev_month_end
Last_Day(Add_Months(pay_end_dt,1))           &next_month_end

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period

  Let $current_month_begin = '01-'||Substr($pay_end_dt,4,8)
  Let $current_month_end   = &current_month_end
  Let $next_month_end      = &next_month_end
  
  Let $FromDate = '01-'||Substr(&prev_month_end,4,8)
  Let $ThruDate = &prev_month_end
  
  show '$FromDate:  ' $FromDate
  show '$ThruDate:  ' $ThruDate

  Let $current_year_begin = '01-JAN-'||Substr($pay_end_dt,8,4)
  Let $current_year_end   = '31-DEC-'||Substr($pay_end_dt,8,4)

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99')
  move $balance_qtr to $balance_qtr x
  let #balance_qtr = $balance_qtr

  show 'Balance period = ' #balance_period
  Show 'Report will contain balances for the month of ' $Reporting_Period
  Show 'Last confirmed pay end date:  ' $pay_end_dt

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_calendar

Where  pay_end_dt         =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt <= $AsOfToday
       And    pay_confirm_run = 'Y')

And    pay_confirm_run = 'Y'
End-Select
End-Procedure

!************************************************************************
Begin-Procedure Process-Scheduler-Run-Controls
!************************************************************************
#debug9 Show 'Process-Scheduler-Run-Controls'
  Show 'Process-Scheduler-Run-Controls'
Begin-Select DISTINCT
!add_months(add_months(last_Day(sysdate),-1),-1)+1 &From_Dt
!From PS_EMPLOYMENT
sysdate-8 &From_Dt
from dual
End-Select

Begin-Select DISTINCT
!add_months(last_DaY(sysdate) , -1) &Thru_Dt
!From PS_EMPLOYMENT
sysdate-2 &Thru_Dt
from dual
End-Select

  Let $FromDate = &From_Dt
  Let $ThruDate = &Thru_Dt

  !show '$FromDate:  ' $FromDate
  !show '$ThruDate:  ' $ThruDate 

End-Procedure Process-Scheduler-Run-Controls
!************************************************************************

!-----------------------------------------------------------------------
begin-procedure Process-Main

  !*** HP092200 Education and Government                        PUBSEC
  do Get-EandG                                                  !PUBSEC
                                                                !PUBSEC
  if $PublicSector = 'N'                                        !PUBSEC
     Let $StatusClause = '(''A'', ''L'', ''S'', ''P'')'         !PUBSEC
  else                                                          !PUBSEC
     Let $StatusClause = '(''A'', ''L'', ''S'', ''P'', ''W'')'  !PUBSEC
  end-if                                                        !PUBSEC
  !*** End HP092200                                             !PUBSEC
  
  do Get-Begin-and-End-Counts
  do Read-Job-Records

begin-SELECT Distinct 
  print 'Dept ID:'               (+3,1,10)
DPT.DEPARTMENT                   (0,9,10)
  move &DPT.Department to $EachDept  
  move &DPT.Department to $DeptID
  do Get-Department-Name
  print $DeptName                (0,23,37)
  
  do Print-Header
  do Create-Report

FROM PS_R_ACTION_COUNTS DPT
End-SELECT

begin-SELECT
  print 'Company Total'           (+2,13)                 !RAS
SUM(CMP.BEGINS)       &CMPBeginCountC
  do Format-Number(&CMPBeginCountC, $out, '999,999')
  print $out                      (0,43,9)
SUM(CMP.HIRES)        &CMPHireCountC
  do Format-Number(&CMPHireCountC, $out, '999,999')
  print $out                      (0,51,9)
SUM(CMP.TERMINATIONS) &CMPTermCountC
  do Format-Number(&CMPTermCountC, $out, '999,999')
  print $out                      (0,61,9)
SUM(CMP.RETIRES)      &CMPRetireCountC
  do Format-Number(&CMPRetireCountC, $out, '999,999')
  print $out                      (0,72,9)
SUM(CMP.XFERSIN)      &CMPXfersInCountC
  do Format-Number(&CMPXfersInCountC, $out, '999,999')
  print $out                      (0,84,9)
SUM(CMP.XFERSOUT)     &CMPXfersOutCountC
  do Format-Number(&CMPXfersOutCountC, $out, '999,999')
  print $out                      (0,94,9)
SUM(CMP.ENDS)         &CMPEndCountC

  move &CMPBeginCountC to #CMPBeginCountC
  move &CMPHireCountC to #CMPHireCountC
  move &CMPTermCountC to #CMPTermCountC
  move &CMPRetireCountC to #CMPRetireCountC
  move &CMPXfersInCountC to #CMPXfersInCountC
  move &CMPXfersOutCountC to #CMPXfersOutCountC
  add #CMPBeginCountC to #CMPHireCountC
  subtract #CMPTermCountC from #CMPHireCountC
  subtract #CMPRetireCountC from #CMPHireCountC
  add #CMPXfersInCountC to #CMPHireCountC
  subtract #CMPXfersOutCountC from #CMPHireCountC
    do Format-Number(#CMPHireCountC, $out, '999,999')
    
    print $out                     (0,104,9)   

  move &CMPBeginCountC to #BeginPlusEnd
  add  #CMPHireCountC to #BeginPlusEnd
  if #BeginPlusEnd <> 0
    move &CMPTermCountC to #Terminations
    multiply 200 times #Terminations
    divide #BeginPlusEnd into #Terminations
    do Format-Number(#Terminations, $out, '9999.9')
    
    print $out                   (0,114,4)
    print '%'                    ()
       
  else
     print 'NM'                   (0,114)
  end-if

FROM PS_R_ACTION_COUNTS CMP
end-SELECT

  do Delete-Worktable
  
end-procedure Process-Main

!-----------------------------------------------------------------------
begin-procedure Get-Begin-and-End-Counts

  move 'Y' to $EndCounts
  move $ThruDate to $CountDate

  show 'Count Date:  '  $CountDate
  do Get-Count

  move 'N' to $EndCounts
  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($FromDate, $From_YMD, {DEFYMD}, '', '')
  do dtu-subtract-days($From_YMD,1,$Count_YMD)

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($Count_YMD,$CountDate,{DEFYMD},'','native')

  show 'Count Date:  '  $CountDate
  do Get-Count

end-procedure Get-Begin-and-End-Counts

begin-procedure Delete-Worktable

let $sql-statement = 'PER010.SQR,Delete-Worktable,Delete,PS_R_Action_Counts'
begin-SQL On-Error=SQL-Error
DELETE FROM PS_R_ACTION_COUNTS;
end-SQL

end-procedure Delete-Worktable

!-----------------------------------------------------------------------
begin-procedure Get-Count    

Let $GEXXX902_Deptid_Alias   = 'A.DEPTID'  
do Select-Deptid-Parameters
show ' $GEXXX902_INCLUDE_DEPTID_CRITERIA: A is here' $GEXXX902_INCLUDE_DEPTID_CRITERIA

begin-SELECT
A.DEPTID,A.ACCT_CD,COUNT(*) &Count                              !RAS

  if $EndCounts = 'Y'
    move &Count to #Ends
  else
    move &Count to #Begins
  end-if
  LET $BU = RTRIM(&A.ACCT_CD,' ')                               !RAS
  move &A.DEPTID to $DeptID                                     !RAS
  
  do Department-Change

FROM  PS_JOB A,
      PS_PERSON PP,
      PS_PER_ORG_INST INST
WHERE A.EMPLID = PP.EMPLID
  AND INST.PER_ORG = 'EMP'
  AND A.EMPLID = INST.EMPLID
  AND INST.ORG_INSTANCE_ERN =  A.EMPL_RCD
  AND A.EMPL_STATUS IN [$StatusClause]                         !PUBSEC
  AND A.EFFDT =
      (SELECT MAX(H.EFFDT)
       FROM PS_JOB H
       WHERE H.EMPLID = A.EMPLID
         AND H.EMPL_RCD = A.EMPL_RCD
         AND H.EFFDT <= $CountDate)
  AND A.EFFSEQ =
      (SELECT MAX(I.EFFSEQ)
       FROM PS_JOB I
       WHERE I.EMPLID = A.EMPLID
         AND I.EMPL_RCD = A.EMPL_RCD
         AND I.EFFDT = A.EFFDT)
  AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
GROUP BY A.DEPTID,A.ACCT_CD                                    !RAS
end-SELECT

end-procedure Get-Count

!-----------------------------------------------------------------------
begin-procedure Read-Job-Records
!-----------------------------------------------------------------------
Let $GEXXX902_Deptid_Alias   = 'B.DEPTID'  
do Select-Deptid-Parameters
show ' $GEXXX902_INCLUDE_DEPTID_CRITERIA:B is here' $GEXXX902_INCLUDE_DEPTID_CRITERIA

show 'From Date  ' $FromDate 
show 'Thru Date  ' $ThruDate 

begin-SELECT
B.ACCT_CD

   LET $BU = RTRIM(&B.ACCT_CD,' ')                                    !RAS

B.DEPTID () on-break print=never procedure=Department-Change          !RAS
B.EMPLID () on-break print=never procedure=Clear-Duplicates
B.ACTION,B.ACTION_REASON,B.EMPL_STATUS,B.EMPL_RCD,B.EFFDT,B.EFFSEQ

  move &B.DEPTID to $DeptID                                           !RAS
  
  Let $GEXXX902_Deptid_Alias   = 'E.DEPTID'                           !RAS

  evaluate &B.Action
    when = 'HIR'              !Hire
    when = 'REH'              !Rehire
      add 1 to #Hires
    when = 'TER'              !Termination
    when = 'TWP'
    when = 'TWB'
      if (#dup_term = 0)
        if &B.EMPL_STATUS <> 'X' and &B.EMPL_STATUS <> 'V'
          add 1 to #Terminations
          let #dup_term = 1
        end-if
      end-if
    when = 'RET'              !Retirement
    when = 'RWP'
      if (#dup_retire = 0)
        add 1 to #Retires
        let #dup_retire = 1
      end-if
    when = 'XFR'              !Transfer
      add 1 to #XfersIn
      
      do Find-Transfer-Out
    when = 'POS'              !Transfer
      if (&B.action_reason = 'XFR')
        add 1 to #XfersIn
        do Find-Transfer-Out
      end-if
  end-evaluate
  
FROM  PS_JOB B,
      PS_PERSON PP2,
      PS_PER_ORG_INST INST
WHERE B.EMPLID = PP2.EMPLID
  AND INST.PER_ORG = 'EMP'
  AND B.EMPLID = INST.EMPLID
  AND INST.ORG_INSTANCE_ERN =  B.EMPL_RCD
  AND B.EFFDT BETWEEN $FromDate AND $ThruDate
  AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]   
ORDER BY B.DEPTID,B.ACCT_CD,B.EMPLID                               !RAS

end-SELECT

  do Department-Change

end-procedure Read-Job-Records

!-----------------------------------------------------------------------
begin-procedure Find-Transfer-Out

Let $GEXXX902_Deptid_Alias   = 'E.DEPTID'
do Select-Deptid-Parameters
show ' $GEXXX902_INCLUDE_DEPTID_CRITERIA:E is here' $GEXXX902_INCLUDE_DEPTID_CRITERIA

begin-SELECT Loops=1
E.BUSINESS_UNIT
E.DEPTID
E.ACCT_CD                                                           !RAS
E.EFFDT
E.EFFSEQ

  do Insert-XferOut

FROM  PS_JOB E,
      PS_PERSON PP3,
      PS_PER_ORG_INST INST
WHERE E.EMPLID    = &B.EmplID
  AND E.EMPLID    = PP3.EMPLID
  AND INST.PER_ORG = 'EMP'
  AND E.EMPLID = INST.EMPLID
  AND INST.ORG_INSTANCE_ERN =  E.EMPL_RCD
  AND E.EMPL_RCD = &B.EMPL_RCD
  AND (E.EFFDT < &B.EffDt OR E.EFFDT = &B.EffDt AND E.EFFSEQ < &B.EffSeq)
AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
ORDER BY E.EFFDT DESC, E.EFFSEQ DESC

end-SELECT
end-procedure Find-Transfer-Out

begin-procedure Insert-XferOut

let $sql-statement = 'PER010.SQR,Insert-XferOut,Insert,PS_R_Action_Counts'
begin-SQL On-Error=SQL-Error
INSERT INTO PS_R_ACTION_COUNTS VALUES(&E.DEPTID,&E.ACCT_CD,0,0,0,0,1,0,0)
!RAS
end-SQL

end-procedure  Insert-XferOut

!-----------------------------------------------------------------------
begin-procedure Department-Change

let $sql-statement = 'PER010.SQR,Department-Change,Insert,PS_R_Action_Counts'
begin-SQL On-Error=SQL-Error
INSERT INTO PS_R_ACTION_COUNTS VALUES
  ($DeptID,$BU,#Begins,#Hires,#Retires,#XfersIn,0,#Terminations,#Ends)
end-SQL

  move 0 to #Begins
  move 0 to #Hires
  move 0 to #Retires
  move 0 to #XfersIn
  move 0 to #Terminations
  move 0 to #Ends

end-procedure Department-Change

!-----------------------------------------------------------------------
begin-procedure Clear-Duplicates

  let #dup_term = 0
  let #dup_retire = 0

end-procedure Clear-Duplicates

!-----------------------------------------------------------------------
begin-procedure Create-Report

begin-SELECT 
!BSC.DEPARTMENT                       !RAS (+1,1,10)
!  move &BSC.Department to $DeptID
BSC.BUSINESS_UNIT                (+1,1,10)                        !RAS
  LET $BU = RTRIM(&BSC.BUSINESS_UNIT,' ')                         !RAS
  do Get-Acct-Code                                                !RAS
  print $AcctName                (0,13,27)                        !RAS
SUM(BSC.BEGINS)       &BSCBeginCount
  do Format-Number(&BSCBeginCount, $out, '999,999')
  print $out                     (0,43,9)
SUM(BSC.HIRES)        &BSCHireCount
  do Format-Number(&BSCHireCount, $out, '999,999')
  print $out                     (0,51,9)
SUM(BSC.TERMINATIONS) &BSCTermCount
  do Format-Number(&BSCTermCount, $out, '999,999')
  print $out                     (0,61,9)
SUM(BSC.RETIRES)      &BSCRetireCount
  do Format-Number(&BSCRetireCount, $out, '999,999')
  print $out                     (0,72,9)
SUM(BSC.XFERSIN)      &BSCXfersInCount
  do Format-Number(&BSCXfersInCount, $out, '999,999')
  print $out                     (0,84,9)
SUM(BSC.XFERSOUT)     &BSCXfersOutCount
  do Format-Number(&BSCXfersOutCount, $out, '999,999')
  print $out                     (0,94,9)

SUM(BSC.ENDS)         &BSCEndCount

  move &BSCBeginCount to #BSCBeginCount
  move &BSCHireCount to #BSCHireCount
  move &BSCTermCount to #BSCTermCount
  move &BSCRetireCount to #BSCRetireCount
  move &BSCXfersInCount to #BSCXfersInCount
  move &BSCXfersOutCount to #BSCXfersOutCount
  add #BSCBeginCount to #BSCHireCount
  subtract #BSCTermCount from #BSCHireCount
  subtract #BSCRetireCount from #BSCHireCount
  add #BSCXfersInCount to #BSCHireCount
  subtract #BSCXfersOutCount from #BSCHireCount
    do Format-Number(#BSCHireCount, $out, '999,999')
   
    print $out                     (0,104,9)
    

!***********************************************************
!   Turnover Rate =      Terminations * 100%
!                   -----------------------------
!                   (Begin Count + End Count) / 2
!***********************************************************

  move &BSCBeginCount to #BeginPlusEnd
  add #BSCHireCount to #BeginPlusEnd
  if #BeginPlusEnd <> 0
    move &BSCTermCount to #Terminations
    multiply 200 times #Terminations
    divide #BeginPlusEnd into #Terminations
    do Format-Number(#Terminations, $out, '9999.9')
    
    print $out                   (0,114,4)
    print '%'                    ()
  else    
    print 'NM'                   (0,114)
  end-if

FROM PS_R_ACTION_COUNTS BSC
Where BSC.DEPARTMENT = $EachDept                             !RAS
GROUP BY BSC.BUSINESS_UNIT                                   !RAS
ORDER BY BSC.BUSINESS_UNIT                                   !RAS

end-SELECT 

!***********
begin-SELECT
  print 'Department Total'           (+2,13)                 !RAS
SUM(BSC.BEGINS)       &BSCBeginCountC
  do Format-Number(&BSCBeginCountC, $out, '999,999')
  print $out                      (0,43,9)
SUM(BSC.HIRES)        &BSCHireCountC
  do Format-Number(&BSCHireCountC, $out, '999,999')
  print $out                      (0,51,9)
SUM(BSC.TERMINATIONS) &BSCTermCountC
  do Format-Number(&BSCTermCountC, $out, '999,999')
  print $out                      (0,61,9)
SUM(BSC.RETIRES)      &BSCRetireCountC
  do Format-Number(&BSCRetireCountC, $out, '999,999')
  print $out                      (0,72,9)
SUM(BSC.XFERSIN)      &BSCXfersInCountC
  do Format-Number(&BSCXfersInCountC, $out, '999,999')
  print $out                      (0,84,9)
SUM(BSC.XFERSOUT)     &BSCXfersOutCountC
  do Format-Number(&BSCXfersOutCountC, $out, '999,999')
  print $out                      (0,94,9)
SUM(BSC.ENDS)         &BSCEndCountC

  move &BSCBeginCountC to #BSCBeginCountC
  move &BSCHireCountC to #BSCHireCountC
  move &BSCTermCountC to #BSCTermCountC
  move &BSCRetireCountC to #BSCRetireCountC
  move &BSCXfersInCountC to #BSCXfersInCountC
  move &BSCXfersOutCountC to #BSCXfersOutCountC
  add #BSCBeginCountC to #BSCHireCountC
  subtract #BSCTermCountC from #BSCHireCountC
  subtract #BSCRetireCountC from #BSCHireCountC
  add #BSCXfersInCountC to #BSCHireCountC
  subtract #BSCXfersOutCountC from #BSCHireCountC
    do Format-Number(#BSCHireCountC, $out, '999,999')
    
    print $out                     (0,104,9)

  move &BSCBeginCountC to #BeginPlusEnd
  add  #BSCHireCountC to #BeginPlusEnd
  if #BeginPlusEnd <> 0
    move &BSCTermCountC to #Terminations
    multiply 200 times #Terminations
    divide #BeginPlusEnd into #Terminations
    do Format-Number(#Terminations, $out, '9999.9')
    
    print $out                   (0,114,4)
    print '%'                    ()
       
  else
     print 'NM'                   (0,114)
  end-if

FROM PS_R_ACTION_COUNTS BSC
Where BSC.DEPARTMENT = $EachDept
end-SELECT

end-procedure


!****************Definition of new Procedures *********************!
Begin-Procedure SetDefault-Begin-End-Date

BEGIN-SELECT 
TO_CHAR(LAST_DAY(SYSDATE),'YYYY-MM-DD')		&LastDate
FROM DUAL
END-SELECT 
do Convert-From-DTU-Date(&LastDate,$ThruDate)
Do dtu-month-begin(&LastDate,$FromDate1) 
do Convert-From-DTU-Date($FromDate1,$FromDate)

End-Procedure SetDefault-Begin-End-Date
!******************************************************************


!******************************************************************
Begin-Procedure GEXXX950-Input-Parameters
!******************************************************************

  Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '1=1'
  Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'

  Display 'Enter Company/Paygroup or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''')'
      Else
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || $In-Paygroup || ''')'
      End-If
      Add 1 To #GEX_RC_PAYGROUP_ROWS
      If #GEX_RC_PAYGROUP_ROWS = 1
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While

  If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If

  Let $DeptIDString = ''''
  Display 'Enter DeptID or leave blank to exit.'
  While 1=1
    Input $In-DeptID Maxlen=10 'DeptID'
    Uppercase $In-DeptID
    If Rtrim($In-DeptID, ' ') = ''
      Concat '''' With $DeptIDString
      Break
    End-If
    If $DeptIDString <> ''''
      Concat ''',''' With $DeptIDString
    End-If
    Concat $In-DeptID With $DeptIDString
  End-While

  If $DeptIDString = ''''''
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '1=1'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' In (' || $DeptIDString || ')'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
  End-If

  Let $EmplStatusString = ''''
  Display 'Enter Employee Status or leave blank to exit.'
  While 1=1
    Input $In-EmplStatus Maxlen=1 'Employee Status'
    Uppercase $In-EmplStatus
    If Rtrim($In-EmplStatus, ' ') = ''
      Concat '''' With $EmplStatusString
      Break
    End-If
    If $EmplStatusString <> ''''
      Concat ''',''' With $EmplStatusString
    End-If
    Concat $In-EmplStatus With $EmplStatusString
  End-While

  If $EmplStatusString = ''''''
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = '1=1'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX903_INCLUDE_EMP_STAT_CRITERIA = $GEXXX903_Emp_Stat_Alias || ' In (' || $EmplStatusString || ')'
    Let $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA = $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
  End-If


  Let $DivCdString = ''''
  Display 'Enter Company Division Code or leave blank to exit.'
  While 1=1
    Input $In-DivCd Maxlen=3 'Company Division Code'
    Uppercase $In-DivCd
    If Rtrim($In-DivCd, ' ') = ''
      Concat '''' With $DivCdString
      Break
    End-If
    If $DivCdString <> ''''
      Concat ''',''' With $DivCdString
    End-If
    Concat $In-DivCd With $DivCdString
  End-While

  If $DivCdString = ''''''
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = '1=1'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX907_INCLUDE_DIV_CD_CRITERIA = $GEXXX907_Div_CD_Alias || ' In (' || $DivCdString || ')'
    Let $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA = $GEXXX907_INCLUDE_DIV_CD_CRITERIA
  End-If


  Let $AcctCdString = ''''
  Display 'Enter Account Code or leave blank to exit.'
  While 1=1
    Input $In-AcctCd Maxlen=25 'Account Code'
    Uppercase $In-AcctCd
    If Rtrim($In-AcctCd, ' ') = ''
      Concat '''' With $AcctCdString
      Break
    End-If
    If $AcctCdString <> ''''
      Concat ''',''' With $AcctCdString
    End-If
    Concat $In-AcctCd With $AcctCdString
  End-While

  If $AcctCdString = ''''''
    Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = '1=1'
    Let $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = $GEXXX909_Acct_CD_Alias || ' In (' || $AcctCdString || ')'
    Let $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA = $GEXXX909_INCLUDE_ACCT_CD_CRITERIA
  End-If
 			
  Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate
  
End-Procedure 

!******************************************************************!
Begin-Procedure Select-Deptid-Parameters
!******************************************************************!
  Let $OprID         = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID   = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DEPTID_ROWS = 0
  Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = ''
  Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = 'ALL'
  
Begin-Select

GEX_RC_DEPTID.DEPTID

  Add 1 To #GEX_RC_DEPTID_ROWS

  If #GEX_RC_DEPTID_ROWS = 1
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = '(' || $GEXXX902_Deptid_Alias || ' In ('''     || &GEX_RC_DEPTID.DEPTID || ''''
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA || ',''' || &GEX_RC_DEPTID.DEPTID  || ''''
  End-If

From  PS_GEX_RC_DEPTID GEX_RC_DEPTID

Where GEX_RC_DEPTID.OPRID               = [$OprID]
And   GEX_RC_DEPTID.RUN_CNTL_ID         = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DEPTID.DEPTID, ' ') != ' ' 
End-Select

  If #GEX_RC_DEPTID_ROWS = 0
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_Deptid_Alias || ' = ' || $GEXXX902_Deptid_Alias
  Else
    Let $GEXXX902_INCLUDE_DEPTID_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA || '))'
    Let $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA = $GEXXX902_INCLUDE_DEPTID_CRITERIA
End-If

End-Procedure

!******************************************************************!
begin-procedure Get-Acct-Code
!******************************************************************!

  move '' to $AcctName
  move '' to $AcctNameAbbrv

  if Rtrim($BU,' ') = ''
    goto GetAcctCdExit
  end-if

begin-SELECT
AX.DESCR
AX.DESCRSHORT

  let $AcctName      = Rtrim(&AX.DESCR,' ')
  let $AcctNameAbbrv = Rtrim(&AX.DESCRSHORT,' ')

FROM  PS_ACCT_CD_TBL AX
WHERE AX.ACCT_CD = $BU  
end-SELECT

GetAcctCdExit:

end-procedure
!******************************************************************!

!-----------------------------------------------------------------------
#include 'stdapi.sqc'    !Routine to update run status
#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askftd.sqc'    !From Thru Date input
!#include 'hrsecty.sqc'   !Get SQR Security parameters           

#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'geteandg.sqc'  !Education Government Installed
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
