!***********************************************************************
!                 V I S I O N   E L E C T I O N                        *
!              Cole Vision Report and Interface                        *
! gexbn612.sqr                 Interface Name: GIANTEAGLE_ELIG.DAT     *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
! GEXBN612:     Vision                                                 *
!                                                                      *
! Narrative:    This program creates a file and report of employees    *
!               that elected Vision benefits.                          *
!               A script is executed w/in the sqr (gexbn612.sh) that   *
!               ftp's the file to the Vision administrator.            *
!***********************************************************************
! SQL Tables:   PS_PERSONAL_DATA                                       *
!               PS_JOB                                                 *
!               PS_HEALTH_BENEFIT                                      *
!               PS_BENEF_PLAN_TBL                                      *
!               PS_BEN_PROG_PARTIC                                     *
!               PS_DEPENDENT_BENEF                                     *
!               PS_HEALTH_DEPENDNT                                     *
!               PS_DEP_BENEF_NID                                       *
!               PS_PAY_CHECK        (for pay_end_dt)                   *
!                                                                      *
! Written by:   Susan Mahady                                           *
!                                                                      *
! Normally Run: Bi-Weekly on Wednesdays beginning 1/8/2003             *
!----------------------------------------------------------------------*
! Tables Referenced:						       *
!----------------------------------------------------------------------*
! TABLE                  SEL     UPD     INS     DEL                   *
!----------------------------------------------------------------------*
! PS_PERSONAL_DATA        Y       -       -       -                    *
! PS_JOB		  Y       -       -       -                    * 
! PS_HEALTH_BENEFIT       Y       -       -       -                    *
! PS_BENEF_PLAN_TBL       Y       -       -       -                    *
! PS_BEN_PROG_PARTIC      Y       -       -       -                    *
! PS_DEPENDENT_BENEF      Y       -       -       -                    * 
! PS_HEALTH_DEPENDNT      Y       -       -       -                    *
! PS_DEP_BENEF_NID        Y       -       -       -                    *
! PS_PAY_CHECK            Y       -       -       -                    *
!----------------------------------------------------------------------*
! Program Flow:                                                        *
!				                                       *
!   INIT-DATETIME                                                      *
!   INIT-NUMBER                                                        *
!   STDAPI-INIT                                                        *
!   GET-CALENDAR-YEAR-ID                                               *
!   A000-SQRW-RUN-CONTROLS                                             *
!   A001-PROCESS-SCHEDULER-RUN-CONTROLS                                *
!   GET-CURRENT-DATETIME                                               *
!   REPORT                                                             *
!	   Z100-SELECT-PAY-END-DT                                      *
!	   P050-PRINT-COVER-PAGE                                       *
!	   A010-OUTPUT-FILE-PROCESS                                    *
!	   W100-WRITE-HEADER-RECORD                                    *
!	   A100-PROCESS-DATA                                           *
!		   F100-FORMAT-EE-DETAIL-RECORD                        *
!			   G300-CONVERT-DATES-TO-MMDDCCYY              *
!			   EVALUATE-COVRG-CD                           *
!		   W200-WRITE-DETAIL-RECORD                            *
!		   P100-PRINT-EE-RECORD                                *
!			   P400-COVRG-CHANGE                           *
!			   P300-GROUP-CHANGE                           *
!		   A200-GET-DEPENDENT                                  *
!			   G200-GET-DEPEND-NID                         *
!			   F200-FORMAT-DEPEND-DETAIL-REC               *
!				   G300-CONVERT-DATES-TO-MMDDCCYY      *
!			   W200-WRITE-DETAIL-RECORD                    *
!			   P200-PRINT-DEPENDENT-RECORD                 *
!		   W300-WRITE-TRAILER-RECORD                           *
!		   W300-WRITE-TRAILER-RECORD                           *
!		   P400-COVRG-CHANGE                                   *
!		   P300-GROUP-CHANGE                                   *
!   COMMIT-TRANSACTION                                                 *
!   STDAPI-TERM                                                        *
!----------------------------------------------------------------------*
! Debugging:                                                           *
!       Syntax:   -Debug789   Place on command line after db name.     *
!       Options:        9       FLOW    Procedure names.               *
!       Options:        8       DATA    Selected information.          *
!----------------------------------------------------------------------*
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  CSR#    INITIALS  DATE        DESCRIPTION OF THE CHANGE             *
!***********************************************************************
! HR-10340   SCM    10/01/2002   INITIAL CREATION                      *
!                                                                      *
! HR-10340   GBD    01/02/2003   Added Health_Benefit.Effdt+5 day logic*
!                                to the max effdt lookup to keep       *
!                                employees from losing coverage before *
!                                they should (effdt's are sync'd every *
!                                Sunday using GEXBN805.SQR). Also put  *
!                                the same logic in the dependent lookup*
!                                                                      *
! HR-10306   GBD    02/28/2003   v8.3 Upgrade                          *
!                                                                      *
!            CJH    01/16/2004   add coverage code for EE + 1          *
!                                                                      *
! HR-10665   AXL    03/04/2004   Adjust sub-select logic for Dependants*
!                                join.                                 *
!                                                                      *
!            AXL    03/24/2004 - Suppress Dependents for Employee-Only *
!                                Coverage                              *
! HR-10815   CXA    11/09/2004 - New 4-Tier coverage codes             *      
!                                                                      *
! HR-10953   AXL    09/26/2005   Accommodate a new contract number     *
!                                Remove contact person Jeanne Philippi *
!                                and her GE phone number               * 
!                                Change the following:                 *
!                                Change from Cole Vision Interfc to    *
!                                Vision Interfc                        *
!                                lis report headers:                   *
!                                Change from Cole Vision Eligibility to*
!                                Vision Eligibility                    *
!                                column header:                        *
!                                Change from Cole Group # to Group #   *
! HR-45436 VENDKXY 12/04/2007    Add logic to report Domestic Partners *
!                                for vision (Plan Type 14/Plan Type 17)*
!                                to vision vendor. 
!				 Added logic for getting the correct   *
!				 Detail tier values and added a        *
!				 procedure for it.Also changed the     *
!				 sort order of the output layout       *
!ITG 70177 ISDVSXP 11/30/2009    New Cole specification                *
!ITG113102 VENDAXG 11/14/2011   Send 7 Digit ID as Subscriber memberID.*  
!GEX_SEC_REENG 2015-04-29 Vahini Katta                                 *
!Changes related to gex_emplid_mapp/7 digit emplid
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup02.sqc'

!*********************************************************************
!Initial processing, retrieving current dates and calls the main 
!procedure to do the processing.                                     
!*********************************************************************
begin-report

  do Init-DateTime
  do Init-Number
  do stdapi-init
  do Get-Calendar-Year-Id
  move 'GEXBN612' to $ReportID
  !move 'Cole Vision Eligibility' to $ReportTitle
  move 'Vision Eligibility' to $ReportTitle
  show '$ReportID= '     $ReportID
  show '$ReportTitle = ' $ReportTitle   
  show ' '
  Let $GEXXX912_Plan_Type_Alias       = 'HB.PLAN_TYPE'
  Let $GEXXX912_Benefit_Plan_Alias    = 'HB.BENEFIT_PLAN'
  
  If $prcs_process_instance = ''
    Do A000-SQRW-Run-Controls
  Else
    Do A001-Process-Scheduler-Run-Controls
  End-If
 
  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = ' $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA

  do Get-Current-DateTime

  let #input_ee_recs      = 0
  let #input_depend_recs  = 0
  let #write_hdr_recs     = 0         !# header recs on output file (should=1)
  let #write_dtl_recs     = 0         !# detail recs on output file (should=ees+dependents)
  let #write_trailer_recs = 0         !# trailer recs on output file (should=1)
  let #total_recs_output  = 0         !should=hdr+detail+trailer recs
  let #report_recs        = 0
  let #report_ee_recs     = 0
  let #report_dep_recs    = 0

  do Report
  do Commit-Transaction
!   ftp file to Cole Vision
  do Z010-set-system-commands-for-ftp    ! gex isdvnpk 06/10/2008 UnCommented call system for UAT       
  do Z015-cd-bindir                      ! gex isdvnpk 06/10/2008 UnCommented call system for UAT                               
  do Z020-exec-script-to-ftp-file        ! gex isdvnpk 06/10/2008 UnCommented call system for UAT                 
  date-time () hh:mi:ss &timeEnded
  display 'Report Ended: ' noline
  display &timeEnded
  do stdapi-term
end-report

!*********************************************************************
begin-procedure Report

date-time () hh:mi:ss &timeBegan
 move 'N' to $Errorfound
 date-time () MM/DD/YYYY &curr_date
 display 'Report Began on ' noline
 display &curr_date
 display 'time started: ' noline
 display &timeBegan
 move &curr_date to $curr_date

  do Format-datetime($curr_date,$curr_Date_dbf_1,{DEFMDY},'','native')
  do Convert-To-DTU-Date($curr_Date_dbf_1,$curr_Date_1)

  let $curr_yy = substr($curr_Date_1,1,4)    !this is 4 digit year
  let $curr_mm = substr($curr_Date_1,6,2)
  let $curr_dd = substr($curr_Date_1,9,2)

  let $run_dt = $curr_yy||$curr_mm||$curr_dd        
  do Format-datetime($run_dt, $run_date, {DEFCMP},'','native')

 if $sev-error = 'Y'
    goto Report-exit
 end-if
 
 do Z100-Select-Pay-End-Dt
 
!*********************SCM  TEMPORARY FOR TESTING:
! let $pay_end_dt = '26-OCT-2002'           !SCM   FOR TESTING ONLY... COMMENT FOR PROD
!***********************************************
 
 !let $X000_ORDER_BY = 'Cole Group#/BENEFIT_PLAN, BENEFIT_PROGRAM, NAME'
 let $X000_ORDER_BY = 'Group#/BENEFIT_PLAN, BENEFIT_PROGRAM, NAME'

 do P050-Print-Cover-Page
 do A010-Output-File-Process
 do W100-Write-Header-Record
 do A100-process-data

Report-Exit:
  
end-procedure

!*********************************************************************
!         Prints the header information in the report
!*********************************************************************

begin-heading 8

  #Include 'stdhdg01.sqc'
  print 'Pay End Date Reported '   (3,70)
  print $pay_end_dt                 ()

! print 'COLE'             (+3,1)
  print 'BENEFIT'          (+3,15)
!
  print 'BENEFIT'          (0,85)
  print 'COV'              (0,94)
  print 'EFFECT'           (0,99)
  print 'DEPENDENT'        (0,112)
  print 'DEPENDENT'        (0,158)
  print 'DEPEND'           (0,169)

  print 'GROUP#'            (+1,1)
  print 'PROGRAM'           (0,15)
!
  print 'EMPLID'            (0,26)
  print 'NAME'              (0,38)
!
  print 'PLAN'     	    (0,85)
  print 'CDE'               (0,94)
  print 'DATE'              (0,99)
  print 'NAME'              (0,112)
  print 'ID'                (0,158)  
  print 'RELAT'             (0,169)        

  print '-'               (+1,1,174) FILL

end-heading
 
begin-procedure A000-SQRW-Run-Controls

   Do Ask-As-Of-Date
   Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate

Display 'Enter Plan Type/Benefit Plan or leave blank to exit.'
  While 1=1
    Input $In-Plan-Type Maxlen=2 'Plan Type'
    Uppercase $In-Plan-Type
    If Rtrim($In-Plan-Type, ' ') = ''
      Break
    Else
      Input $In-Benefit-Plan Maxlen=6 'Benefit Plan'
      Uppercase $In-Benefit-Plan
      If Rtrim($In-Benefit-Plan, ' ') = ''
	Let $Include_Condition = '(' || $GEXXX912_Plan_Type_Alias || ' = '''  || $In-Plan-Type || ''')'
      Else
	Let $Include_Condition = '(' || $GEXXX912_Plan_Type_Alias || ' = '''  || $In-Plan-Type || ''' And ' || $GEXXX912_Benefit_Plan_Alias || ' = '''  || $In-Benefit-Plan || ''')'
      End-If
      Add 1 To #GEX_RC_BEN_PLAN_ROWS
      If #GEX_RC_BEN_PLAN_ROWS = 1
	Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '(' || $Include_Condition
      Else
	Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While

  If #GEX_RC_BEN_PLAN_ROWS = 0
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA || ')'
    Let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
  End-If
end-procedure
 
!****************************************************
begin-procedure A001-Process-Scheduler-Run-Controls
!****************************************************
   
    Do GEXRCBN1-SELECT-PARAMETERS

    Let $X000_Where_Select_AsOfDate = &GEX_RC_BEN.AsOfDate
    If Rtrim (&GEX_RC_BEN.AsOfDate, ' ') = ''
      Let $X000_Where_Select_AsOfDate = $AsOfToday
    End-If

   Do GEXXX912-Select-Benefit-Plan-Parameters
end-procedure

!******************************* 
! Output file processing
!*******************************

begin-procedure A010-Output-File-Process
#debug9 show 'A010-Output-File-Process'
 
let $filename = '{OUTFILE}'||'GIANTEAGLE_ELIG.DAT'      !name requested by Cole Vision
open $filename as 1

!open 'c:\temp\GIANTEAGLE_ELIG.DAT' as 1        !for testing only
     for-writing record=772			!AXL 9/26 was 770

if #writestat != 0
   display 'Error Opening output file.  Program terminating.'
   stop
end-if

end-procedure


!*********************************************************************
! Select all valid Employee Types for a specific company
!*********************************************************************

begin-procedure A100-Process-data
#debug9 show 'A100-Process-data'

display 'Begin process'

move 'N' to $rowfound
let $end_process = 'N'
let #inputtran = 0

begin-select ON-ERROR=SQL-Error-Found('A100-Process-data')
BP.GROUP_NBR
   move &BP.GROUP_NBR        to $group

PD.EMPLID  
   move &PD.EMPLID           to $emplid

HB.EMPL_RCD
   move &HB.EMPL_RCD         to #empl_rcd

PD.NAME
   move &PD.NAME             to $name

PD.FIRST_NAME_SRCH
   move &PD.FIRST_NAME_SRCH  to $first_name
   
PD.LAST_NAME_SRCH
   move &PD.LAST_NAME_SRCH   to $last_name

PD.ADDRESS1
   move &PD.ADDRESS1         to $address1
PD.CITY
   move &PD.CITY             to $city
PD.STATE
   move &PD.STATE            to $state

PD.POSTAL 
   move &PD.POSTAL           to $zip
PD.ADDRESS2
   move &PD.ADDRESS2         to #address2
PD.BIRTHDATE
   move &PD.BIRTHDATE        to $birthdate
PD.SEX
   move &PD.SEX              to $sex
JB.FULL_PART_TIME
   move &JB.FULL_PART_TIME   to $full_part_time
JB.EMPL_TYPE
   move &JB.EMPL_TYPE        to $empl_type
JB.COMPANY
   move &JB.COMPANY          to $company
JB.DEPTID
   move &JB.DEPTID           to $deptid
HB.COVRG_CD
   move &HB.COVRG_CD         to $covrg_cd
HB.EFFDT
   move &HB.EFFDT            to $hb_effdt
 
HB.COVERAGE_BEGIN_DT
   move &HB.COVERAGE_BEGIN_DT to $hb_coverage_begin_dt
HB.BENEFIT_PLAN
   move &HB.BENEFIT_PLAN     to $benefit_plan

HB.COVERAGE_ELECT
BPP.BENEFIT_PROGRAM
    move &BPP.BENEFIT_PROGRAM           to $benefit_program
    
    if #inputtran = 0             !very first time through 
       !AXL 04/02/2004 - Begin
       !let $save_bp_plan  = $benefit_plan
       let $save_group    = $group
       let $save_covrg_cd = $covrg_cd	
       !AXL 04/02/2004 End
    end-if
  
    add +1 to #input_ee_recs
    !GEXPY_848_E113102_01 2011-14-11 VENDAXG BEGIN
    !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
    !Do Get-7digit-Emplid 
    Do Get-SSN
    !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
    !GEXPY_848_E113102_01 2011-14-11 VENDAXG END
    Do F100-Format-EE-Detail-Record
    Do W200-Write-Detail-Record
    Do P100-Print-EE-Record
    Do A200-Get-Dependent 
    move 'Y' to $rowfound
    add 1 to #inputtran

    !AXL 04/02/2004 - Begin
    let $save_group = $group		
    !let $save_bp_plan = $benefit_plan
    let $save_covrg_cd = $covrg_cd	
    let $prior_detail_group_no = $detail_group_no
    !AXL 04/02/2004 - End


FROM PS_JOB JB,
     PS_PERSONAL_DATA PD,
     PS_HEALTH_BENEFIT HB,
     PS_BENEF_PLAN_TBL BP,
     PS_BEN_PROG_PARTIC BPP
 
WHERE 
!Jb.emplid in  ('160368326','107440247','296861424','196581778','104666859','208445593'
!			,'173644016','104666859','107440247','160368326','162560499','164607555'
!			,'166589140','167483802','173644016','177364981','177462650','180485279'
!			,'181609407','185527210','195689293','196581778','201402517','208445593'
!			,'216528563','235964123','268368252','269608676','270589339','282986673'
!			,'283845083','290843278','294426755','296861424') !VENDKXY added for test ee's
JB.EMPLID       = PD.EMPLID
AND   JB.EMPLID       = HB.EMPLID
AND   JB.EMPL_RCD     = HB.EMPL_RCD
!AND   JB.EMPL_STATUS  IN ('A', 'L', 'S', 'P')       !actives
AND   HB.PLAN_TYPE    = BP.PLAN_TYPE
AND   HB.BENEFIT_PLAN = BP.BENEFIT_PLAN
and   HB.PLAN_TYPE = '14'		!VENDKXY FOR ITG 45436 on 12/04/2007
AND   HB.COVERAGE_ELECT = 'E'                       !Elected coverage                 

AND   JB.EFFDT = (SELECT MAX(EFFDT)
		  FROM PS_JOB
		  WHERE EMPLID    = JB.EMPLID
		  AND   EMPL_RCD  = JB.EMPL_RCD
		  AND   EFFDT    <= $pay_end_dt)

AND   JB.EFFSEQ = (SELECT MAX(EFFSEQ)
		   FROM PS_JOB
		   WHERE EMPLID   = JB.EMPLID
		   AND   EMPL_RCD = JB.EMPL_RCD
		   AND   EFFDT    = JB.EFFDT)

AND   HB.EFFDT =  (SELECT MAX(EFFDT) 
                   FROM PS_HEALTH_BENEFIT
                   WHERE HB.EMPLID = EMPLID
                   AND HB.EMPL_RCD = EMPL_RCD
                   AND HB.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND HB.PLAN_TYPE   = PLAN_TYPE
                   AND HB.BENEFIT_NBR = BENEFIT_NBR
!		   AND   EFFDT    <= $pay_end_dt)
		   AND  (EFFDT+5) <= $pay_end_dt)       !GBD 01/02/2003

AND   BP.EFFDT =  (SELECT MAX(EFFDT)
                   FROM PS_BENEF_PLAN_TBL
                   WHERE BP.PLAN_TYPE = PLAN_TYPE
                   AND BP.BENEFIT_PLAN = BENEFIT_PLAN
!                   AND EFFDT <= HB.EFFDT)
		   AND EFFDT    <= $pay_end_dt)		!CXA 11/09/2004

!###########################################################################################
!VENDKXY FOR ITG 45436 on 12/04/2007
AND   HB.COVERAGE_BEGIN_DT =  (SELECT MAX(COVERAGE_BEGIN_DT) 
                   FROM PS_HEALTH_BENEFIT
                   WHERE HB.EMPLID = EMPLID
                   AND HB.EMPL_RCD = EMPL_RCD
                   AND HB.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND HB.PLAN_TYPE = PLAN_TYPE
                   AND HB.BENEFIT_NBR = BENEFIT_NBR
                   AND COVERAGE_BEGIN_DT  <= $X000_Where_Select_AsOfDate)
!#############################################################################################


AND   [$GEXXX912_Include_Benefit_Plan_Criteria]
 
AND    BPP.EMPLID             = JB.EMPLID
AND    BPP.EMPL_RCD           = JB.EMPL_RCD

AND    BPP.EFFDT              =
      (SELECT MAX(EFFDT)
       FROM   PS_BEN_PROG_PARTIC
       WHERE  EMPLID          = BPP.EMPLID
       AND    EMPL_RCD        = BPP.EMPL_RCD
       AND    EFFDT          <= JB.EFFDT)

!AXL 04/02/2004 - Begin
!ORDER BY BP.BENEFIT_PLAN, BPP.BENEFIT_PROGRAM, PD.LAST_NAME_SRCH
ORDER BY BP.GROUP_NBR, HB.COVRG_CD, PD.LAST_NAME_SRCH
!AXL 04/02/2004 - End

end-select

if $rowfound = 'N'
   display 'No Employees found for the Cole Vision Interface'
   display 'Created interface with ZERO employees'
   Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
   do W300-Write-Trailer-Record 
else

   do W300-Write-Trailer-Record

   close 1
   display 'close output file'  
   
   move #input_ee_recs      to $recee    999,999
   move #input_dep_recs     to $recdep   999,999
   move #write_hdr_recs     to $rech     999,999
   move #write_dtl_recs     to $recd     999,999
   move #write_trailer_recs to $rectr    999,999
   move #total_recs_output  to $rectot   999,999
   move #report_ee_recs     to $erptrecs 999,999
   move #report_dep_recs    to $drptrecs 999,999
   move #report_recs        to $rptrecs  999,999
 
   display '**************************************'
   display 'Employee Input Records:       ' noline
   display $recee
   display 'Dependent Input Records:      ' noline
   display $recdep
   display '#hdr recs on output file:     ' noline
   display $rech
   display '#detail recs on output file:  ' noline
   display $recd
   display '#trailer recs on output file: ' noline
   display $rectr
   display '#total recs on output file:   ' noline
   display $rectot
   display '#total recs on report:        ' noline
   display $rptrecs
   
   let $end_process = 'Y'
   do P400-covrg-change 			 !AXL 04/02/2004 - Add
   do P300-group-change  
   print 'Grand Total Employees on Report: '  (+2,20)
   print $erptrecs                            ()
   print 'Grand Total Dependents:          '  (+1,20)
   print $drptrecs                            ()
   print '****Grand Total****              '  (+2,20)
   print $rptrecs                             ()
   
end-if

end-procedure


!*********************************************************************
begin-procedure A200-Get-Dependent
!*********************************************************************
#debug9 show 'A200-Get-Dependent'

begin-select

HD.EMPLID

DP.DEPENDENT_BENEF
   move &DP.DEPENDENT_BENEF to $dependent_benef

DP.NAME
   move &DP.NAME            to $dep_name

DP.RELATIONSHIP
   move &DP.RELATIONSHIP    to $dep_relationship
DP.ADDRESS1
   move &DP.ADDRESS1        to $dep_address1
DP.ADDRESS2
   move &DP.ADDRESS2        to $dep_address2
DP.CITY
   move &DP.CITY            to $dep_city
DP.STATE
   move &DP.STATE           to $dep_state
DP.POSTAL
   move &DP.POSTAL          to $dep_postal

DP.BIRTHDATE
   move &DP.BIRTHDATE       to $dep_birthdate

DP.STUDENT
   move &DP.STUDENT         to $student

DP.SEX
   move &DP.SEX             to $dep_sex

HD.EFFDT
   move &HD.EFFDT           to $hd_effdt
HB2.COVRG_CD		    !AXL 03/24/2004 - Add	

   !AXL 03/24/2004 - Suppress Dependents for Employee-Only Coverage - Begin
   let $print_row = 'Y'

   If &HB2.COVRG_CD = '1' 
      let $print_row='N'
   End-if
   !AXL 03/24/2004 - Suppress Dependents for Employee-Only Coverage - End
   
   If $print_row ='Y'	    !AXL 03/24/2004 - Add
      add +1 to #input_dep_recs
      let $DependSSN = ' '
      Do G200-Get-Depend-NID                         !Get dependent SSN
      let $DependSSN = &NID.National_Id
      Do F200-Format-Depend-Detail-Rec
      Do W200-Write-Detail-Record                    !Write another detail w-dependent info
      Do P200-Print-Dependent-Record
   End-if                   !AXL 03/24/2004 - Add

FROM PS_DEPENDENT_BENEF DP,
     PS_HEALTH_DEPENDNT HD,
     PS_HEALTH_BENEFIT HB2,	!AXL 03/24/2004 - Add
     PS_BENEF_PLAN_TBL BP2	!AXL 03/24/2004 - Add

WHERE DP.EMPLID          = HD.EMPLID
AND   DP.EMPLID          = $emplid
AND   HD.EMPL_RCD        = #empl_rcd
AND   DP.DEPENDENT_BENEF = HD.DEPENDENT_BENEF
!AND   HD.PLAN_TYPE       = '14'                    !Vision
AND   HD.PLAN_TYPE       IN ( '14' ,'17')           ! VENDKXY for ITG 45436 on 12/04/2007
!AXL 03/24/2004 - Suppress Dependents for Employee-Only Coverage - Begin
AND   HB2.PLAN_TYPE    = HD.PLAN_TYPE
AND   HD.EMPLID          = HB2.EMPLID
AND   HD.EMPL_RCD        = HB2.EMPL_RCD
AND   HB2.PLAN_TYPE      = BP2.PLAN_TYPE
AND   HB2.BENEFIT_PLAN   = BP2.BENEFIT_PLAN
AND   HB2.COVERAGE_ELECT = 'E'              
!AXL 03/24/2004 - Suppress Dependents for Employee-Only Coverage - End
!AXL 03/04/2004 - Fix logic to select dependants properly - Begin
AND   HD.EFFDT = (SELECT MAX(EFFDT) FROM PS_HEALTH_DEPENDNT
		  WHERE HD.EMPLID         = EMPLID
                    AND HD.EMPL_RCD       = EMPL_RCD
                    AND HD.COBRA_EVENT_ID = COBRA_EVENT_ID
                    AND HD.PLAN_TYPE      = PLAN_TYPE
                    AND HD.BENEFIT_NBR    = BENEFIT_NBR
		    AND (EFFDT+5)       <= $pay_end_dt)  !GBD 01/02/2003
!AXL 03/04/2004 - End
!AXL 03/24/2004 - Suppress Dependents for Employee-Only Coverage - Begin
AND   HB2.EFFDT =  (SELECT MAX(EFFDT) FROM PS_HEALTH_BENEFIT
                   WHERE HB2.EMPLID = EMPLID
                   AND HB2.EMPL_RCD = EMPL_RCD
                   AND HB2.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND HB2.PLAN_TYPE   = PLAN_TYPE
                   AND HB2.BENEFIT_NBR = BENEFIT_NBR
		   AND  (EFFDT+5) <= $pay_end_dt) 
AND   BP2.EFFDT =  (SELECT MAX(EFFDT) FROM PS_BENEF_PLAN_TBL
                   WHERE BP2.PLAN_TYPE = PLAN_TYPE
                   AND BP2.BENEFIT_PLAN = BENEFIT_PLAN
                   AND EFFDT <= HB2.EFFDT) 
!AXL 03/24/2004 - Suppress Dependents for Employee-Only Coverage - End
ORDER BY HD.EMPLID, DP.DEPENDENT_BENEF

end-select

end-procedure

!Procedure is addead by VENDKXY for the new requirements in ITG 45436 on 12/04/2007
!*********************************************************************
! Evaluating Coverage Code
!*********************************************************************

begin-procedure Evaluate-covrg-cd

begin-select 
 
HB3.COVRG_CD
	move &HB3.COVRG_CD to $covrg_cd1 
HB3.PLAN_TYPE
	move &HB3.PLAN_TYPE to $plantype	
	
	if $plantype='17' and $prev_plantype='14'
	if $prev_covrg_cd='1'
       if $covrg_cd1='5'
          let $detail_tier = '2'
       END-IF
       if $covrg_cd1='6'
          let $detail_tier = '3'
       END-IF
      if $covrg_cd1='7'
          let $detail_tier = '4'
       END-IF
    end-if
    if &HB.COVRG_CD='3'
       if $covrg_cd1='5'
          let $detail_tier = '4'
       END-IF
       if $covrg_cd1='6'
         let $detail_tier = '3'
      END-IF
      if $covrg_cd1='7'
          let $detail_tier = '4'
       END-IF
    end-if
    else
    evaluate $covrg_cd1
    when = '1'
       let $detail_tier = '1'
       break
    when = '2'
    when = '5'      
       let $detail_tier = '2'
       break
    when = '3'
    when = '6'      
       let $detail_tier = '3'
       break
    when = '4'
    when = '7'      
       let $detail_tier = '4'
       break
    when-other
       let $detail_tier = '4'
  end-evaluate
    end-if
	let $prev_covrg_cd = $covrg_cd1
	let $prev_plantype = $plantype
FROM PS_DEPENDENT_BENEF DP1,
     PS_HEALTH_DEPENDNT HD1,
     PS_HEALTH_BENEFIT HB3,	
     PS_BENEF_PLAN_TBL BP3	

WHERE DP1.EMPLID          = HD1.EMPLID
AND   DP1.EMPLID          = $emplid
AND   HD1.EMPL_RCD        = #empl_rcd
AND   DP1.DEPENDENT_BENEF = HD1.DEPENDENT_BENEF
AND   HD1.PLAN_TYPE       IN ( '14' ,'17')           
AND   HB3.PLAN_TYPE    = HD1.PLAN_TYPE
AND   HD1.EMPLID          = HB3.EMPLID
AND   HD1.EMPL_RCD        = HB3.EMPL_RCD
AND   HB3.PLAN_TYPE      = BP3.PLAN_TYPE
AND   HB3.BENEFIT_PLAN   = BP3.BENEFIT_PLAN
AND   HB3.COVERAGE_ELECT = 'E'              
AND   HD1.EFFDT = (SELECT MAX(EFFDT) FROM PS_HEALTH_DEPENDNT
		  WHERE HD1.EMPLID         = EMPLID
                    AND HD1.EMPL_RCD       = EMPL_RCD
                    AND HD1.COBRA_EVENT_ID = COBRA_EVENT_ID
                    AND HD1.PLAN_TYPE      = PLAN_TYPE
                    AND HD1.BENEFIT_NBR    = BENEFIT_NBR
 		    AND EFFDT           <= $X000_Where_Select_AsOfDate)       
AND   HB3.EFFDT =  (SELECT MAX(EFFDT) FROM PS_HEALTH_BENEFIT
                   WHERE HB3.EMPLID = EMPLID
                   AND HB3.EMPL_RCD = EMPL_RCD
                   AND HB3.COBRA_EVENT_ID = COBRA_EVENT_ID
                   AND HB3.PLAN_TYPE   = PLAN_TYPE
                   AND HB3.BENEFIT_NBR = BENEFIT_NBR
		   AND EFFDT          <= $X000_Where_Select_AsOfDate)       
AND   BP3.EFFDT =  (SELECT MAX(EFFDT) FROM PS_BENEF_PLAN_TBL
                   WHERE BP3.PLAN_TYPE = PLAN_TYPE
                   AND BP3.BENEFIT_PLAN = BENEFIT_PLAN
                   AND EFFDT <= HB3.EFFDT) 
ORDER BY Hd1.PLAN_TYPE

end-select

end-procedure


!*********************************************************************
! Format EE Information for Cole Vision Interface
!*********************************************************************
begin-procedure F100-Format-EE-Detail-Record
#debug9 show 'F100-Format-EE-Detail-Record'

  !let $detail_group_no = '30539*'||$group           old Cole specification
  !let $detail_group_no = '9657198*'||$group           !AXL 9/26/2005 New Cole specification
   let $detail_group_no = '9766247*'||$group           !ISDVSXP 11/30/2009 New Cole specification
   
  !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
  !GEXPY_848_E113102_01 2011-14-11 VENDAXG BEGIN
  !let $detail_subscrib_membnum = $emplid
  !let $detail_subscrib_membnum = $Gex_Emplid
  !GEXPY_848_E113102_01 2011-14-11 VENDAXG END
  let $detail_subscrib_membnum = $emplid
  let $detail_subscrib_ssn     = $SSN
  let $detail_membnum          = $SSN
  let $detail_ssn              = $SSN	
  !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends  
  let $mid_init = ' '
  let $Name = rtrim($name,' ')

  Do Rotate-Name
  let $detail_last_name = substr($LastName,1,24)
  unstring $name by ' ' into $first_name_1 $mname
  let $detail_first_name = substr($first_name_1,1,15)
  let $detail_mid_init = $MidInitial

!**
  let $detail_address1 = substr($address1,1,30)
  let $detail_address2 = substr($address2,1,30)
  let $detail_state    = substr($state,1,2)
  let $detail_city     = $city
  let $detail_postal   = $zip
 
!**  convert dates to mmddccyy

  let $convert_birthdate = $birthdate   
  let $convert_effdt     = $hb_coverage_begin_dt 
  Do G300-Convert-Dates-To-mmddccyy
  let $detail_dob              = $birth_dt
  let $detail_effdt            = $covg_effdt  
  
! CJH - 1/16/2004 - add covrg code for EE + 1
!   if $covrg_cd = '1'                   !if employee only, set = 1, else set to family (=4)
!    let $detail_tier = '1'
!  else
!    let $detail_tier = '4'
!  end-if
!  evaluate $covrg_cd
!    when = '1'
!       let $detail_tier = '1'
!       break
!    when = '2'
!       let $detail_tier = '2'
!       break
!    when = '4'
!       let $detail_tier = '4'
!       break
!    when-other
!       let $detail_tier = '4'
!  end-evaluate
! CJH - 1/16/2004 - end 

!CXA - 11/10/2004 - begin 
!Cole has been advised the coverage codes are as follows:
!
!1 = Employee Only
!2 = Employee + Spouse
!3 = Employee + Child(ren)
!4 = Employee + Family
  evaluate $covrg_cd
    when = '1'
       let $detail_tier = '1'
       break
    when = '2'
    when = '5'      ! VENDKXY 12/04/2007- Added for plan type 17
       let $detail_tier = '2'
       break
    when = '3'
    when = '6'      ! VENDKXY 12/04/2007 - Added for plan type 17
       let $detail_tier = '3'
       break
    when = '4'
    when = '7'      ! VENDKXY 12/04/2007 - Added for plan type 17
       let $detail_tier = '4'
       break
    when-other
       let $detail_tier = '4'
  end-evaluate
! CXA - 11/10/2004 - end 

do Evaluate-covrg-cd      !Procedure is addead by VENDKXY for the new requirements in ITG 45436 on 12/04/2007

  let $detail_relationship_code = 'E'               !E=enrollee, S=spouse, D=dependent
  let $detail_sex               = $sex
  let $detail_company_cd        = $company
  let $detail_departmt_cd       = $deptid
  let $detail_status_code       = 'E'               !E=enrolled, D=disenrolled
    
!  display 'Employee:  ' noline
!  display $emplid

end-procedure



!*********************************************************************
! Format Dependent Record Information
!*********************************************************************
 
begin-procedure F200-Format-Depend-Detail-Rec
#debug9 show 'F200-Format-Depend-Detail-Rec'

  !let $detail_group_no = '30539*'||$group           old Cole specification
  !let $detail_group_no = '9657198*'||$group           !AXL 9/26/2005 New Cole specification
   let $detail_group_no = '9766247*'||$group           !ISDVSXP 11/30/2009 New Cole specification

  !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins   
  !GEXPY_848_E113102_01 2011-14-11 VENDAXG BEGIN
  !let $detail_subscrib_membnum = $emplid
  !let $detail_subscrib_membnum = $Gex_Emplid
  !GEXPY_848_E113102_01 2011-14-11 VENDAXG END 
 
  let $detail_subscrib_membnum = $emplid
  let $detail_subscrib_ssn     = $emplid
  let $detail_membnum          = $DependSSN
  let $detail_ssn              = $DependSSN
 !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends  

  let $mid_init = ' '
  let $Name = rtrim($dep_name,' ')

  Do Rotate-Name
  let $detail_last_name = substr($LastName,1,24)
  unstring $name by ' ' into $first_name_1 $mname
  let $detail_first_name = substr($first_name_1,1,15)       
  let $detail_mid_init = $MidInitial
  
!**
  let $detail_address1 = substr($dep_address1,1,30)
  let $detail_address2 = substr($dep_address2,1,30)
  let $detail_state    = substr($dep_state,1,2)
  let $detail_city     = $dep_city
  let $detail_postal   = $dep_postal
!**
  let $convert_birthdate = $dep_birthdate  
  let $convert_effdt     = $hb_coverage_begin_dt       !use ee covg begin dt for dep effdt
  Do G300-Convert-Dates-To-mmddccyy
  let $detail_dob              = $birth_dt
  let $detail_effdt            = $covg_effdt  

!**
  if $dep_relationship = 'SP' 
     let $detail_relationship_code = 'S'           !spouse
  else
     let $detail_relationship_code = 'D'           !dependent
  end-if
!**


   if $dep_relationship = 'NA'    ! VENDKXY 12/04/2007 - Added for plan type 17 start
     let $detail_relationship_code = 'P'           !domestic partner
   end-if 
   
   
  if $student = 'Y'
      let $detail_student_cd = 'FT'         !full time student need verified
  else
      let $detail_student_cd = '  '
  end-if
!**

! CJH - 1/16/2004 - comment out defaulting the covrg code - it is set above  
!   let $detail_tier = '4'                !ee only=1, family=4
! CJH
 
   let $detail_sex               = $dep_sex
!  let $detail_company_cd        = $company         !Dont send company for dependent
!  let $detail_departmt_cd       = $deptid          !Dont send dept for dependent
  let $detail_status_code       = 'E'               !E=enrolled, D=disenrolled

!  display 'Dependent of ee:  ' noline
!  display $emplid
!  display 'Depend SSN is ' noline
!  display $DependSSN

end-procedure

!***************************************
Begin-Procedure G200-Get-Depend-NID
!***************************************
#debug9 show 'G200-Get-Depend-NID'

Begin-SELECT

NID.NATIONAL_ID
   let $DependSSN = &NID.NATIONAL_ID

FROM PS_DEP_BENEF_NID NID
  WHERE NID.EMPLID = $emplid
    AND NID.DEPENDENT_BENEF = &DP.DEPENDENT_BENEF  

end-select

end-procedure

!********************************************************
!  Convert dates to format that Cole requires (mmddccyy)
!********************************************************  
begin-procedure G300-Convert-Dates-To-mmddccyy
#debug9 show 'G300-Convert-Dates-To-mmddccyy'

Do Convert-to-dtu-date($convert_birthdate,$bdate)
  let $b_cc = substr($bdate,1,2)
  let $b_yy = substr($bdate,3,2)
  let $b_mm = substr($bdate,6,2)
  let $b_dd = substr($bdate,9,2)

  let $birth_dt = $b_mm||$b_dd||$b_cc||$b_yy            !mmddccyy
 
 Do Convert-to-dtu-date($convert_effdt,$hbdate)

  let $h_cc = substr($hbdate,1,2)
  let $h_yy = substr($hbdate,3,2)
  let $h_mm = substr($hbdate,6,2)
  let $h_dd = substr($hbdate,9,2)

  let $covg_effdt   = $h_mm||$h_dd||$h_cc||$h_yy            !mmddccyy
  
 
end-procedure
 
!****************************************************
begin-procedure P050-Print-Cover-Page
!****************************************************
#debug9 Show 'P050-Print-Cover-Page'

  print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'          (+5,1)
  print '$Prcs_OPRID            ='                              (+2,5)
  print $Prcs_OPRID                                             (0,+2)
  print '$PRCS_RUN_CNTL_ID      ='                              (+1,5)
  print $PRCS_RUN_CNTL_ID                                       (0,+2)

  print 'SELECTION CRITERIA FOR THIS REPORT RUN:'               (+5,2)

  Print '$GEXXX912_Include_Benefit_Plan_Display_Criteria  = '   (+2,5)
  Print $GEXXX912_Include_Benefit_Plan_Display_Criteria         (0,+2)

  Print 'As Of Date :'                                          (+2,5)
  Print $AsOfDate                                               (0,+2)

  print 'SORT ORDER FOR THIS REPORT RUN:'                       (+5,2)
  print $X000_ORDER_BY                                          (+2,5)                                                             
   
  let #PAGE-COUNT = 0
  NEW-PAGE
  let #PAGE-COUNT = 1
End-Procedure

 
!*********************************************************************
! Print Record for Cole Vision Report
!*********************************************************************
begin-procedure P100-Print-EE-Record
#debug9 show 'P100-Print-EE-Record'

  if #current-line > 51
     new-page
  end-if
  !AXL 04/02/2004 - Begin
  if $save_covrg_cd <> $covrg_cd
    do P400-covrg-change                    !break on coverage code
  end-if

  !if $save_bp_plan <> $benefit_plan
  if $save_group <> $group
  !AXL 04/02/2004 - End
    do P300-group-change                    !break on benefit plan (cole group#)
  end-if

  print $detail_group_no        	(+2,1)
  print $benefit_program    		(0,17)
  print $detail_subscrib_ssn            (0,26)
  print $detail_last_name               (0,38,25)
  print $detail_first_name		(0,65,15)
  print $detail_mid_init	        (0,80,1) 
  print $benefit_plan        		(0,85)
  print $detail_tier	                (0,95)
  print $detail_effdt	                (0,98)   edit xx/xx/xxxx
  print $detail_relationship_code       (0,170)
  
  add 1 to #group_count_ee 
  add 1 to #report_ee_recs
  add 1 to #report_recs

  add 1 to #covrg_cd_count_ee 	 !AXL 04/02/2004 - Add

end-procedure

!********************************************* 
! Print Record for Cole Vision Report
!********************************************* 
begin-procedure P200-Print-Dependent-Record
#debug9 show 'P200-Print-Dependent-Record'

  if #current-line > 51
     new-page
  end-if

!  print $detail_group_no        	(+1,1)       !do not print group,program,plan for dependents
!  print $benefit_program    		(0,16)
  print $detail_subscrib_ssn            (+1,26)
!  print $benefit_plan        		(0,68)
  print $detail_effdt	                (0,98)  edit xx/xx/xxxx
  print $detail_last_name               (0,112,23)
  print $detail_first_name		(0,137,15) 
  print $detail_mid_init	        (0,155,1) 
  print $detail_ssn                 	(0,158)
  print $detail_relationship_code       (0,170)            	
    
  add 1  to #group_count_dep
  add 1  to #report_recs 
  add 1  to #report_dep_recs

  add 1  to #covrg_cd_count_dep		 !AXL 04/02/2004 - Add

end-procedure
 
!****************************************
!*** Cole Group Change (POD/COD)   ******
!****************************************
begin-procedure P300-group-change
#debug9 show 'P300-group-change'

print 'TOTALS FOR GROUP '         (+2,1)
print $prior_detail_group_no         ()
print '# Employees: '             (+0,45)
print #group_count_ee             (0,60) edit 999,999
print '# Dependents:'             (+0,80)
print #group_count_dep            (0,95) edit 999,999

move 0 to #group_count_ee 
move 0 to #group_count_dep 

 !AXL 04/02/2004 - Begin
 if $end_process = 'N'
    new-page
 end-if
 !AXL 04/02/2004 - End

end-procedure

!AXL 04/02/2004 - Begin
!****************************************
!*** Cole Coverage Code Change **
!****************************************
begin-procedure P400-covrg-change
#debug9 show 'P400-covrg-change'

print 'TOTALS FOR COVERAGE CODE '         (+2,1)
print $save_covrg_cd              ()
print '# Employees: '             (+0,45)
print #covrg_cd_count_ee          (0,60) edit 999,999
print '# Dependents:'             (+0,80)
print #covrg_cd_count_dep         (0,95) edit 999,999

move 0 to #covrg_cd_count_ee 
move 0 to #covrg_cd_count_dep 

if $end_process = 'N'
   new-page
end-if

end-procedure
!AXL 04/02/2004 - End

!*********************************************************************
! Write Header Record Information for Cole Vision Interface
!*********************************************************************
begin-procedure W100-Write-Header-Record
#debug9 show 'W100-Write-Header-Record'
  display 'Write Header Record'

  add 1 to #write_hdr_recs
  move 'HDR' to $header_rectype
 !move 'Jeanne Philippi   ' to $header_Contact_name             x(length=18)
 !move '412-967-4717'       to $header_Contact_phone            x(length=12)

  move 'BENEFITS DEPT     ' to $header_Contact_name             !(length=18)
  move '800-242-0414'       to $header_Contact_phone            !(length=12)

  let $header_Sender_contact = $header_Contact_name||$header_Contact_phone
  move 'ENR' to $header_jobtype            !SCM  Use 'TST' for test file, 'ENR' for prod 
 
  date-time () MM/DD/YYYY &date_created
  move &date_created to $date_created
  let $create_mm = substr($date_created,1,2)
  let $create_dd = substr($date_created,4,2)
  let $create_yyyy = substr($date_created,7,4)
  let $header_Create_date = $create_mm||$create_dd||$create_yyyy
!
  date-time () hh:mi:ss &timecreated
  move &timecreated to $time_created
  let $create_hh  = substr($time_created,1,2)
  let $create_min = substr($time_created,4,2)
  let $create_ss  = substr($time_created,7,2)
  let $header_Create_time = $create_hh||$create_min||$create_ss 

  write 1 from $header_rectype:3                         
	       'ENROLL  ':8                         	 !File type
               'GIANT EAGLE INC                ':30      !Organization Name
	       $header_Sender_contact:30                                                      
	       $header_Create_date:8                     !MMDDYYYY
	       $header_Create_time:6			 !HHMMSS
	       $header_Enr_entityid:12                   !Entity id-Assigned by Cole
	       $header_jobtype:3                         !TST,ENR,or AUD
	       'Y':1			 		 !full file indicator
	       'Y':1					 !Required if full file=Y
	       $header_enroll_dt:8			 !Date mbrs enrolled if mbr dt not supplied
	       $header_disenroll_dt:8			 !Date mbrs disenrolled

end-procedure W100-Write-Header-Record

!*********************************************************************
! Write Detail Record Information for Cole Vision Interface
!*********************************************************************
begin-procedure W200-Write-Detail-Record
#debug9 show 'W200-Write-Detail-Record'

  move 'DTL' to $detail_rectype  
  add 1 to #write_dtl_recs
 
  write 1 from $detail_rectype:3
               	$detail_subscrib_membnum:20         !ee ssn
               	$detail_subscrib_ssn:9              !ee ssn
               	$detail_subscrib_dob:8              !ee birthdate       mmddccyy
               	$detail_membnum:20                  !depend ssn (or if ee, ee ssn)                  
               	$detail_ssn:9                       !depend ssn (of if ee, ee ssn)
	       	$detail_relationship_code:2         !E=enrollee,S=spouse or D=dependent 
               	$detail_tier:3                      !coverage code (1=ee only,4=family)
               	$detail_dob:8                       !member birthdate   mmddccyy 
               	$detail_last_name:35
               	$detail_first_name:25
               	$detail_mid_init:25
                ' ':5                               !member salutation (Colonel, Ms, etc.)
               	' ':5                               !member generation (III)
               	' ':10                              !member suffix (Esquire, Jr)
               	$detail_address1:30
               	$detail_address2:30
               	$detail_city:30
               	$detail_state:4                     !standard 2 char state abbrev
               	$detail_postal:10
               	'USA':3                             !country code
               	$detail_area_code:3
               	$detail_phone:20
               	$detail_work_area_code:3
               	$detail_phone:20
               	$detail_work_ext:10
               	$detail_email:40
               	$detail_sex:1                       !f=female, m=male
               	$detail_status_code:3               !E=enrolled, D=disenrolled
               	$detail_effdt:8	
   	       	$detail_disenroll_dt:8
		$detail_deceased_dt:8
		$detail_company_cd:20
		$detail_division_cd:20
		$detail_departmt_cd:20
		$detail_HMO_cd:20
		$detail_group_no:20
		$detail_filler1:80	            !user defined (4 fields, 20 char each)
		$detail_student_cd:2                !FT, PT or 'N' not a student
		$detail_marital_cd:1		    !S,W,D,L or M
		$detail_filler2:36		    !diagnosis codes (6 fields, 6 char each)
		$detail_allowance1:4		    !user defined
		$detail_remain_allow1:62	    !user defined
		$detail_allowance2:4		    !user defined
		$detail_remain_allow2:62            !user defined
	       	                     
end-procedure

!*********************************************************************
! Write Trailer Record Information for Cole Vision Interface
!*********************************************************************
begin-procedure W300-Write-Trailer-Record
#debug9 show 'W300-Write-Trailer-Record'

  display 'Write Trailer Record'
  add 1 to #write_trailer_recs
  move 'TRL' to $trailer_rectype
  show '-------------------------------------------------'
  show '#write_hdr_recs:     ' #write_hdr_recs
  show '#write_dtl_recs:     ' #write_dtl_recs
  show '#write_trailer_recs: ' #write_trailer_recs
  
  let #total_recs_output = #write_hdr_recs + #write_dtl_recs + #write_trailer_recs
  move #total_recs_output  to $total_recs_output   9999999999
  show '#total_recs_output:  ' #total_recs_output
  show '$total_recs_output:  ' $total_recs_output
  show '--------------------------------------------------'
  write 1 from  $trailer_rectype:7
               	$total_recs_output:10

end-procedure W300-Write-Trailer-Record


!***********************************************************************
begin-procedure Z010-set-system-commands-for-ftp
!***********************************************************************

  do GEXXX951-Select-Oracle-Instance
  let $db_name = lower($GEXXX951_Oracle_Instance)
  let $pshome = getenv('PS_HOME')
  let $cd_command_bindir = 'cd '||'$BINDIR'
  let $ftp_command = '/bin/ksh  ' || $pshome || '/scripts/gex_shell/' || 'gexbn612.sh '||$db_name

end-procedure

!***********************************************************************
begin-procedure Z015-cd-bindir
!***********************************************************************
     show 'Z015-cd-bindir'
      CALL SYSTEM USING $cd_command_bindir #sys_status
      if #sys_status <> 0
         let $cd_display_msg = 'Command ' || $cd_command_bindir || ' did not work'
         display $cd_display_msg
	 display 'call system (cd bindir) in gexbn612 failed'
	 stop
      end-if

end-procedure

!***********************************************************************
begin-procedure Z020-exec-script-to-ftp-file
!***********************************************************************
     date-time () HH:MI:SS &ProcessTime
     show 'Executing FTP process started at: ' &ProcessTime

     show 'Z020-exec-script-to-ftp-file'
      CALL SYSTEM USING $ftp_command #sys_status
      if #sys_status <> 0
	 show 'Command ' $ftp_command ' Failed'
	 show 'FTP exec script within gexbn612 failed'
	 stop
      end-if

end-procedure
 
!****************************************
begin-procedure Z100-Select-Pay-End-Dt
!****************************************
#debug9 show 'Z100-Select-Pay-End-Dt'
begin-select
MAX(PC.PAY_END_DT)      &pay_end_dt
  move &pay_end_dt   to $pay_end_dt
  show '***  pay_end_dt   *** ' $pay_end_dt

FROM PS_PAY_CHECK PC

  where pc.pay_end_dt <= $X000_WHERE_SELECT_ASOFDATE
end-select

end-procedure


!************************************************** 
!Displays the error messages and stops execution
!************************************************** 
 
begin-procedure SQL-Error-Found($Proc_Name) 
!  do error-found
  display 'SQL Error in ' 
  display $Proc_Name 
  display $_sql-error
  display 'sql-status:' 
  display #_sql-status 

  STOP

end-procedure
!GEXPY_848_E113102_01 2011-14-11 VENDAXG BEGIN
!****************************************
Begin-Procedure Get-7digit-Emplid
!****************************************
Begin-Select 
MAP.EMPLID 
	
	Let $Gex_Emplid = &MAP.EMPLID 
	
FROM PS_GEX_EMPLID_MAPP MAP 
WHERE MAP.SSN = $emplid
AND MAP.EMPL_RCD = 0

End-Select
!****************************************
End-Procedure Get-7digit-Emplid
!****************************************
!GEXPY_848_E113102_01 2011-14-11 VENDAXG END
!GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
!****************************************
Begin-Procedure Get-SSN
!****************************************
Begin-Select 
PNID.NATIONAL_ID 
	
	Let $SSN = &PNID.NATIONAL_ID 
	
FROM PS_PERS_NID PNID 
WHERE PNID.EMPLID = $emplid
AND PNID.COUNTRY='USA'
AND PNID.NATIONAL_ID_TYPE='PR'
End-Select
!****************************************
End-Procedure Get-SSN
!****************************************
!GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#include 'askaod.sqc'    !Ask for AsOfDate
#include 'gexxx920.sqc'  !Get ben single row run control
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'rotname3.sqc'
#Include 'gexxx912.sqc'  !Get gex_rc_ben_plan run controls
