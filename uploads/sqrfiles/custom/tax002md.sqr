!***********************************************************************
!  TAX002MD:  Qtrly State Employee Wage List - MARYLAND                *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:52:47                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!VENDKXY   10/09/2008   Brought forward the changes from 8.3           *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Get USA variables

! VENDKXY Brought forward the changes from 8.3 BEGIN

#include 'setup03x.sqc' !Printer and page-size initialization

!begin-setup

!#include 'prt002.sqc'  !Printer and page-size initialization

!end-setup

! VENDKXY Brought forward the changes from 8.3 END


begin-report
  do Init-Report

  do Process-Main

  if #Employees_On_Page > 0 and #Employees_On_Page < 20
    do Page-Complete
  end-if

  do Stdapi-Term

end-report

begin-procedure Init-Report
  display 'Quarterly Employee Wage List - Maryland'

  do Init-DateTime
  do Init-Number

  do Get-Tax-Reporting-Run-Controls

  move $PeriodEndDate to $AsOfDate  !for included procedures

  do Initialize
  do Get-Calendar-Year-Id
  do Stdapi-Init

end-procedure

begin-procedure Initialize
#ifdef TAXTEST
  move 'CA' to $State
#else
  move 'MD' to $State
#endif
end-procedure

begin-heading 10
  print 'NAME OF EMPLOYER'        (+4,8)
  print 'MARYLAND ACCT#'          (0,38)
  print 'QTR ENDED'               (0,55)
  print 'PAGE NO.'                (0,70)
  uppercase $CompanyName
  print $CompanyName              (+2,8)
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    print &Employer_ID_SUT        (0,38)
  else
    print &Employer_ID_SWT        (0,38)
  end-if
  do Format-DateTime($PeriodEndDate, $out, {DEFMDY}, '', '')
  print $out                      (0,55)
  do Format-Number(#PAGE_NUM, $out, '999')
  print $out                      (0,70)
  print 'SSN'                     (+3,8)
  print 'NAME'                    (0,30)
  print 'TOTAL WAGES'             (0,58)
  print 'THIS PAGE TOTAL'         (55,38)
  do Format-Number(#PageTotal, $out, '99999999.99')
  print $out                      (0,58)
  print 'EMPLS ON PAGE:'          (56,38)
  do Format-Number(#Employees_On_Page, $out, '99')
  print $out                      (0,58)
  if #Page# = 1
    print 'ALL PAGES WAGES'       (57,38)
    do Format-Number(#AllTotal, $out, '999999999.99')
    print $out                    (0,57)
    print 'ALL PAGES EMPLS'       (58,38)
    do Format-Number(#emplcount, $out, '9999999')
    print $out                    (0,58)
  end-if
end-heading

begin-procedure Process-Main

  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
C.COMPANY         () on-break print=never level=1 before=Company-Change
C.STATE
C.EMPLID          () on-break print=never level=2 after=EmplID-Change
C.TAX_CLASS
C.LOCALITY
C.TXGRS_QTD
C.NLGRS_QTD

D1.NATIONAL_ID    () on-break print=never level=2 save=$SSN
PN.NAME           () on-break print=never level=2 save=$Name
  move &C.Company to $Company
  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_NAMES PN, PS_PERS_NID D1,PS_COMPANY_TBL A
WHERE D1.EMPLID           = C.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND PN.EMPLID           = C.EMPLID
  AND PN.NAME_TYPE = 'PRI'
  AND PN.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PN.EMPLID
         AND  PX.NAME_TYPE = PN.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfDate)
#ifdef TAXTEST
  AND C.STATE             = 'CA'
#else
  AND C.STATE             = 'MD'
#endif
  AND C.LOCALITY          = ' '
  AND C.TAX_CLASS         = 'U'
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = &TX.Balance_Year
  AND C.BALANCE_QTR       = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD    =
      (SELECT MAX(C1.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C1
       WHERE  C1.EMPLID       = C.EMPLID
         AND  C1.COMPANY      = C.COMPANY
         AND  C1.BALANCE_ID   = C.BALANCE_ID
         AND  C1.BALANCE_YEAR = C.BALANCE_YEAR
         AND  C1.BALANCE_QTR  = C.BALANCE_QTR
         AND  C1.STATE        = C.STATE
         AND  C1.LOCALITY     = C.LOCALITY
         AND  C1.TAX_CLASS    = C.TAX_CLASS)
  AND C.NLGRS_QTD > 0
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
ORDER BY C.COMPANY, D1.NATIONAL_ID, C.STATE, C.LOCALITY, C.TAX_CLASS
end-SELECT
end-procedure

begin-procedure Process-Tax-Balance
!  let #NLGrs_QTD = round(&C.NLGrs_QTD,0)
  if (&C.NLGrs_QTD - floor(&C.NLGrs_QTD))  > 0.5
    let #NLGrs_QTD = round(&C.NLGrs_QTD,0)
  else
    let #NLGrs_QTD = floor(&C.NLGrs_QTD)
  End-if

  do Format-Number(#NLGrs_QTD, $NLGrs_QTD, '99999999.99')
  add #NLGrs_QTD to #PageTotal
  move &C.EmplID to $EmplID
end-procedure

begin-procedure EmplID-Change
  move $SSN to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM($SSN, ' ') = ''
    print 'Applied For' (+2,8)
  else
    print $SSN          (+2,8)  edit xxxbxxbxxxx
  end-if

  do Rotate-Name
  uppercase $Name
  print $Name           (0,30,20)

  print $NLGrs_QTD      (0,58)

  move '' to $NLGrs_QTD

  add 1 to #Employees_On_Page

  if #Employees_On_Page = 20
    do Page-Complete
  end-if
end-procedure

begin-procedure Get-Total

  move 0 to #emplcount
  move 0 to #AllTotal

begin-SELECT

TB.NLGRS_QTD

!  let #NLGrs_QTD = round(&TB.NLGrs_QTD,0)
  if (&TB.NLGrs_QTD - floor(&TB.NLGrs_QTD))  > 0.5
    let #NLGrs_QTD = round(&TB.NLGrs_QTD,0)
  else
    let #NLGrs_QTD = floor(&TB.NLGrs_QTD)
  End-if

  add #NLGrs_QTD to #AllTotal
  add 1 to #emplcount

FROM  PS_TAX_BALANCE TB, PS_COMPANY_TBL A2
WHERE TB.COMPANY       = &C.Company
#ifdef TAXTEST
  AND TB.STATE         = 'CA'
#else
  AND TB.STATE         = 'MD'
#endif
  AND TB.LOCALITY      = ' '
  AND TB.TAX_CLASS     = 'U'                 ! Unemployment
  AND TB.BALANCE_ID    = $Calendar_Year_Id
  AND TB.BALANCE_YEAR  = &TX.Balance_Year
  AND TB.BALANCE_QTR   = &TX.Balance_Qtr
  AND TB.NLGRS_QTD     > 0
  AND TB.BALANCE_PERIOD       =
      (SELECT MAX(C2.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C2
       WHERE  C2.EMPLID        = TB.EMPLID
         AND  C2.COMPANY       = TB.COMPANY
         AND  C2.BALANCE_ID    = TB.BALANCE_ID
         AND  C2.BALANCE_YEAR  = TB.BALANCE_YEAR
         AND  C2.BALANCE_QTR   = TB.BALANCE_QTR
         AND  C2.STATE         = TB.STATE
         AND  C2.LOCALITY      = TB.LOCALITY
         AND  C2.TAX_CLASS     = TB.TAX_CLASS)
  AND A2.COMPANY = TB.COMPANY
  AND A2.EFF_STATUS = 'A'
  AND A2.TAX_REPORT_TYPE = '2'
  AND A2.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A2.COMPANY
         AND  EFFDT  <= $AsOfDate)
end-SELECT
end-procedure

begin-procedure Page-Complete
  add 1 to #PAGE_NUM
  new-page
  move 0 to #PageTotal
  move 0 to #Employees_On_Page
end-procedure

begin-procedure Company-Change
  if #Employees_On_Page < 20 and #Employees_On_Page > 0
    do Page-Complete
  end-if
  move 0 to #PAGE_NUM
  move &C.Company to $Company
  do Get-Company-Data
  do Get-State-Tax-Data             !Employer ID
  do Get-Total
end-procedure

begin-procedure Ignore
end-procedure

#Include 'rotname1.sqc'  !Rotate-Name procedure
#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
