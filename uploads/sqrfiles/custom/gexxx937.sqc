!**************************************************************************
!  GEXXX937:  Retrieve "Multiple" State Run Controls and                  *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX937_State_Alias                                                *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX937_INCLUDE_STATE_CRITERIA                                     *
!    $GEXXX937_EXCLUDE_STATE_CRITERIA                                     *
!    $GEXXX937_INCLUDE_STATE_DISPLAY_CRITERIA                             *
!    $GEXXX937_EXCLUDE_STATE_DISPLAY_CRITERIA                             *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX937_State_Alias = 'PT.STATE'                               *
!    Do GEXXX937-Select-State-Parameters                                  *
!    Begin-Select                                                         *
!    TXGRS_CUR                                                            *
!    FROM PS_PAY_TAX PT                                                   *
!    WHERE [$GEXXX937_EXCLUDE_STATE_CRITERIA]                             *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!  INITIALS     DATE            DESCRIPTION OF THE CHANGE                 *
!**************************************************************************
!  JDH          12/14/99        INITIAL CREATION                          *
!**************************************************************************


Begin-Procedure GEXXX937-Select-State-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_STATE_ROWS = 0
  Let $GEXXX937_INCLUDE_STATE_CRITERIA = ''
  Let $GEXXX937_EXCLUDE_STATE_CRITERIA = ''
  Let $GEXXX937_INCLUDE_STATE_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX937_EXCLUDE_STATE_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_STATE.STATE

  Add 1 To #GEX_RC_STATE_ROWS

  If #GEX_RC_STATE_ROWS = 1
    Let $GEXXX937_INCLUDE_STATE_CRITERIA = '(' || $GEXXX937_State_Alias || ' In ('''     || &GEX_RC_STATE.STATE || ''''
    Let $GEXXX937_EXCLUDE_STATE_CRITERIA = '(' || $GEXXX937_State_Alias || ' Not In (''' || &GEX_RC_STATE.STATE || ''''
  Else
    Let $GEXXX937_INCLUDE_STATE_CRITERIA = $GEXXX937_INCLUDE_STATE_CRITERIA || ',''' || &GEX_RC_STATE.STATE  || ''''
    Let $GEXXX937_EXCLUDE_STATE_CRITERIA = $GEXXX937_EXCLUDE_STATE_CRITERIA || ',''' || &GEX_RC_STATE.STATE  || ''''
  End-If

From  PS_GEX_RC_STATE GEX_RC_STATE

Where GEX_RC_STATE.OPRID                = [$OprID]
And   GEX_RC_STATE.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_STATE.STATE, ' ') != ' '

End-Select

  If #GEX_RC_STATE_ROWS = 0
    Let $GEXXX937_INCLUDE_STATE_CRITERIA = '1=1'
    Let $GEXXX937_EXCLUDE_STATE_CRITERIA = '1=1'
  Else
    Let $GEXXX937_INCLUDE_STATE_CRITERIA = $GEXXX937_INCLUDE_STATE_CRITERIA || '))'
    Let $GEXXX937_EXCLUDE_STATE_CRITERIA = $GEXXX937_EXCLUDE_STATE_CRITERIA || '))'
    Let $GEXXX937_INCLUDE_STATE_DISPLAY_CRITERIA = $GEXXX937_INCLUDE_STATE_CRITERIA
    Let $GEXXX937_EXCLUDE_STATE_DISPLAY_CRITERIA = $GEXXX937_EXCLUDE_STATE_CRITERIA
  End-If

End-Procedure
