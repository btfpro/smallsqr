!***********************************************************************
! GEXBN606:  401K Loan Repayment (Dreyfus)                             *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXBN606 :            401K Loan Repayment                            *
!                                                                      *
! Narrative:            This program creates an output file for        * 
!                       Dreyfus that contains the weekly 401K loan     * 
!                       repayment amount.                              *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_line                                       *
!                       pay_deduction                                  *
!                       gex_dreyfus_tbl                                *
!                       gex_dreyfus_map                                *   
!                                                                      *
! Written by:           Carl Buckrop                                   *
!                                                                      *
! Normally Run:         Weekly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         None                                         *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
!                                                                      *
! CWB       06/01/99    Initial Creation                               *
! CWB       09/24/99    Made changes to be able to use the Dreyfus     *
!                         plan numbers and deduction codes instead of  *
!                         401KPB.                                      *
!                                                                      *
! CJH       12/04/2000  Add break by Company to report for COD         *
!                                                                      *
! CWB       08/01/2001  Corrected the use-report logic for error rpts  *
!                                                                      *
! PSR       09/09/02    ps_genl_Deduction table key fields change      *  
!                       - Upgrade 8.3                                  * 
! GEX_SEC_REENG 2015-06-24 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************
 
#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Landscape)
Begin-setup
  declare-report deduction-list
Printer-Type=HPLASERJET
  end-declare

  declare-report error-list
Printer-Type=HPLASERJET
  end-declare
End-setup
#define #column1  001
#define #column2  006
#define #column3  013
#define #column4  026
#define #column5  059
#define #column6  071
#define #column7  081
#define #column8  089
#define #column9  119
#define #column10 138
#define #column11 158
       
Begin-Program
#debug9 Show 'Begin-Program'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Program

Begin-Heading 6 for-reports=(deduction-list)
#debug9 Show 'Begin-Heading'

  Move  'GEXBN606' to $ReportID 
  #Include 'stdhdg01.sqc'

  Print 'COMP'                 (+2,{#column1})
  Print 'GRP'                  (+0,{#column2})
  Print 'EMPLID'               (+0,{#column3})
  Print 'NAME'                 (+0,{#column4})
  Print 'W/E DATE'             (+0,{#column5})
  Print 'DED CODE'             (+0,{#column6})
  Print 'PLAN'                 (+0,{#column7})
  Print 'DESCRIPTION'          (+0,{#column8})
  Print '     DED AMOUNT'      (+0,{#column9})
  Print '   WEEKLY AMOUNT'     (+0,{#column10})
  Print 'MESSAGE'              (+0,{#column11})
End-Heading

Begin-Heading 7 for-reports=(error-list)
#debug9 Show 'Begin-Heading'

  Move  'BN606ERR' to $ReportID 
  #Include 'stdhdg01.sqc'

  Print '****ERROR LISTING****'(+2,) center  
  Print 'COMP'                 (+2,{#column1})
  Print 'GRP'                  (+0,{#column2})
  Print 'EMPLID'               (+0,{#column3})
  Print 'NAME'                 (+0,{#column4})
  Print 'W/E DATE'             (+0,{#column5})
  Print 'DED CODE'             (+0,{#column6})
  Print 'PLAN'                 (+0,{#column7})
  Print 'DESCRIPTION'          (+0,{#column8})
  Print '     DED AMOUNT'      (+0,{#column9})
  Print '   WEEKLY AMOUNT'     (+0,{#column10})
  Print 'MESSAGE'              (+0,{#column11})
End-Heading

Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXBN606'
  Let $ReportTitle   = '401K LOAN REPAYMENT'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show '$ReportDate  = ' $ReportDate
  Show '$ReportTime  = ' $ReportTime

  Let $GEXXX900_Company_Alias = 'pl.company'

  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
  Else
    Do P120-Process-Scheduler-Run-Controls
  End-If

  Do Format-DateTime($X000_Where_Select_AsOfDate, $X000_Report_Heading_AsOfDate, {DEFDATE}, '', '')
  Let $X000_Order_By = 'Benefit Plan, Emplid'

  Do P130-Print-Cover-Page
  Do P140-open-File
  Do P150-Get-Last-Confirmed-Pay
  Let $transaction_seq  = '01'
  Let #plan_header = 0
End-Procedure


Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Do Ask-As-Of-Date
  Let $X000_WHERE_SELECT_ASOFDATE = $AsOfDate

  Let $CompanyString = ''''
  Display 'Enter Company or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Concat '''' With $CompanyString
      Break
    End-If
    If $CompanyString <> ''''
      Concat ''',''' With $CompanyString
    End-If
    Concat $In-Company With $CompanyString
  End-While

  If $CompanyString = ''''''
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = '1=1'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX900_INCLUDE_COMPANY_CRITERIA = $GEXXX900_Company_Alias || ' In (' || $CompanyString || ')'
    Let $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA = $GEXXX900_INCLUDE_COMPANY_CRITERIA
  End-If
End-Procedure


Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXRCBN1-SELECT-PARAMETERS

  Let $X000_Where_Select_AsOfDate = &GEX_RC_BEN.AsOfDate
  If Rtrim (&GEX_RC_BEN.AsOfDate, ' ') = ''
    Let $X000_Where_Select_AsOfDate = $AsOfToday
  End-If

  Do GEXXX900-Select-Company-Parameters     
End-Procedure


Begin-Procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OprID          ='                                  (+2,5)
  Print $Prcs_OprID                                               (0,+2)
  Print '$Prcs_Run_Cntl_ID    ='                                  (+1,5)
  Print $Prcs_Run_Cntl_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX900_Include_Company_Display_Criteria   = '         (+2,5)
  Print $GEXXX900_Include_Company_Display_Criteria                (0,+2)
  Print 'As Of Date :'                                            (+2,5)
  Print $X000_Report_Heading_AsOfDate                             (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,2)
  Print $X000_Order_By                                            (+2,5)

  Let #PAGE-COUNT = 0
  New-Page
End-Procedure


Begin-Procedure P140-Open-File
#debug9 Show 'P140-Open-File'
  Let $file1 = '{OUTFILE}' || Lower($ReportID) || '.dat'

   Open $file1
       As 1
       For-Writing
       Record=80:Fixed
       Status=#filestat

  If #filestat != 0
     Show 'Error opening output file.  Program terminating.'
     Stop
  End-If
End-Procedure


Begin-Procedure P150-Get-Last-Confirmed-Pay
#debug9 Show 'P150-Get-Last-Confirmed-Pay'
Begin-Select
MAX(PAY_END_DT) &PAY_END_DT

  Let $pay_end_dt = &PAY_END_DT

  Do Format-DateTime($pay_end_dt, $payenddt, {DEFMDY}, '', '')

  Move '1' to $Year4
  Move '' to $_DDelimiter
  Do Format-DateTime($pay_end_dt, $payenddate, {DEFYMD}, '', '')

  Show 'Last confirmed pay end date:  ' $payenddt

FROM   PS_PAY_CALENDAR

WHERE  PAY_END_DT      <= $X000_WHERE_SELECT_ASOFDATE
AND    PAY_CONFIRM_RUN  = 'Y'
End-Select
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select

dt.gex_dreyfus_plan () On-break   
		    Level=1
		    Print=Change
		    Save=$gex_dreyfus_plan
		    After=P220-BP-Change
dt.descr
dt.dedcd
pl.name
pl.emplid
pl.empl_rcd
! CJH - 12/4/2000 - Break after company changes to subtotal by Company for COD
! pl.company
pl.company () On-break   
		    Level=2
		    Print=Change
		    Save=$comp
		    After=Company-Change
! CJH - 12/4/2000 - end
pl.paygroup
sum(pd.ded_cur)   &ded_cur

  let #ded_cur = &ded_cur

  Let $plan_number      = &dt.gex_dreyfus_plan

  Let $Data-Found-Flag = 'Y'

  do check-for-401KPB

  do select-wk-deduction
  If #ded_cur < 0
     Do P240-Print-Negative
! CWB 08/01/2001 BEGIN
     use-report deduction-list
! CWB 08/01/2001 END
  else
  If #ded_cur = 0
      Do P210-Print-Record 
  else
  If #ded_cur > 0
     if #plan_header = 0
         Do P230-Write-Header-Record
     end-if 
     Do P231-Write-T385-Record
     Add 1        To #bp-count
     Add #ded_cur To #bp_ded_cur
! CJH - 12/4/2000 - add accums for Company - COD Change
     Add 1        To #co-count
     Add #ded_cur To #co_ded_cur
! CJH - end
     Do P210-Print-Record
  End-if
  End-if
  End-if

From   ps_pay_deduction        pd,
       ps_pay_line             pl,
       ps_gex_dreyfus_tbl      dt  
Where  pd.dedcd                = dt.dedcd 
And    pd.plan_type            = '00'
And    pl.company              between 'A00' and 'Z99'
And    pl.company              = pd.company
And    pl.paygroup             = pd.paygroup
And    pl.pay_end_dt           = $pay_end_dt
And    pl.pay_end_dt           = pd.pay_end_dt
And    pl.off_cycle            = pd.off_cycle
And    pl.page_num                = pd.page_num
And    pl.line_num                = pd.line_num

And   [$GEXXX900_INCLUDE_COMPANY_CRITERIA]

Group By dt.gex_dreyfus_plan,
	 dt.descr,
	 dt.dedcd, 
	 pl.emplid,
	 pl.name,
	 pl.empl_rcd,
	 pl.company,
	 pl.paygroup

! CJH - 12/4/2000 - COD Changes
!Order By dt.gex_dreyfus_plan
!	 pl.emplid
Order By dt.gex_dreyfus_plan, 
         pl.company,
	 pl.emplid
! CJH - 12/4/2000 - COD Changes

End-Select
End-Procedure
!********************************************************************
! CJH - 12/4/2000 - COD change - begin
begin-procedure Company-Change
#debug9 Show 'Company-Change'
! CWB 08/01/2001 BEGIN
     use-report deduction-list
! CWB 08/01/2001 END

  print 'TOTALS FOR COMPANY ' (+2,1)
  print $comp         (0,+2)
  print #co-count                 (0,{#column5}) edit 999,999
  print #co_ded_cur               (0,{#column7}) edit 9,999,999,990.00


  Let #co-count   = 0
  Let #co_ded_cur = 0
  New-Page
End-Procedure
! CJH - 12/4/2000 -end

!********************************************************************
Begin-procedure  check-for-401KPB
Begin-Select
sum(pd1.ded_cur)   &401K_ded_cur

From   ps_pay_deduction        pd1,
       ps_pay_line             pl1 
Where  pd1.dedcd                = '401KPB' 
And    pd1.plan_type            = '00'
And    pl1.emplid               = &pl.emplid
And    pl1.company              = &pl.company
And    pl1.company              = pd1.company
And    pl1.paygroup             = &pl.paygroup
And    pl1.paygroup             = pd1.paygroup
And    pl1.pay_end_dt           = $pay_end_dt
And    pl1.pay_end_dt           = pd1.pay_end_dt
And    pl1.off_cycle            = pd1.off_cycle
And    pl1.page_num                = pd1.page_num
And    pl1.line_num                = pd1.line_num

End-Select

  Add &401K_ded_cur to #ded_cur
                     
End-Procedure

Begin-Procedure select-wk-deduction

begin-select
gd.ded_addl_amt
from ps_genl_deduction gd
where gd.emplid = &pl.emplid
!PSR - Upgrade 8.3 09/09/2002 Begin
!and gd.empl_rcd = &pl.empl_Rcd#
 and gd.company = &pl.company
!PSR - Upgrade 8.3 09/09/2002 End
and gd.dedcd = &dt.dedcd
And gd.effdt = (select max(effdt)
		from ps_Genl_Deduction
		where emplid = gd.emplid
	        !PSR - Upgrade 8.3 09/09/2002 Begin
		!and empl_Rcd# = gd.empl_Rcd#
                and company   = gd.company
		!PSR - Upgrade 8.3 09/09/2002 Begin
		and dedcd = gd.dedcd
		and effdt <= $pay_end_dt)
end-select
end-procedure


Begin-Procedure P210-Print-Record
#debug9 Show 'P210-Print-Record'
  use-report deduction-list
  Print &pl.company               (+1,{#column1})
  Print &pl.paygroup              (+0,{#column2})
  Print &pl.emplid                (+0,{#column3})  !Edit xxx-xx-xxxx  ! GEX_SEC_REENG 2015-06-24 Vahini Katta
  Print &pl.name                  (+0,{#column4})
  Print $payenddt                 (+0,{#column5})
  Print &dt.dedcd                 (+0,{#column6})
  Print $plan_number              (+0,{#column7})
  Print &dt.descr                 (+0,{#column8})
  Print #ded_cur                  (+0,{#column9})  Edit 9,999,999,990.00
  Print &gd.ded_addl_amt          (+0,{#column10})  edit 9,999,999,990.00
  If #ded_cur = 0
      Print '*ZERO AMOUNT*'     (+0,{#column11})
  End-if 
End-Procedure


Begin-Procedure P240-Print-Negative
#debug9 Show 'P240-Print-Negative'
  use-report error-list
  Print &pl.company               (+1,{#column1})
  Print &pl.paygroup              (+0,{#column2})
  Print &pl.emplid                (+0,{#column3})  !Edit xxx-xx-xxxx ! GEX_SEC_REENG 2015-06-24 Vahini Katta
  Print &pl.name                  (+0,{#column4})
  Print $payenddt                 (+0,{#column5})
  Print &dt.dedcd                 (+0,{#column6})
  Print $plan_number              (+0,{#column7})
  Print &dt.descr             (+0,{#column8})
  Print #ded_cur                  (+0,{#column9})  edit 9,999,999,990.00PF
  Print &gd.ded_addl_amt          (+0,{#column10})  edit 9,999,999,990.00PF
  Print 'NEGATIVE AMOUNT'         (+0,{#column11})
End-Procedure


	     
Begin-Procedure P220-BP-Change
#debug9 Show 'P220-BP-Change'
! CWB 08/01/2001 BEGIN
     use-report deduction-list
! CWB 08/01/2001 END

  print 'TOTALS FOR DREYFUS PLAN' (+2,1)
  print $gex_dreyfus_plan         (0,+2)
  print #bp-count                 (0,{#column5}) edit 999,999
  print #bp_ded_cur               (0,{#column7}) edit 9,999,999,990.00

  Add #bp_ded_cur To #total_ded

  Let #bp-count   = 0
  Let #bp_ded_cur = 0
  New-Page
  Do P233-Write-Trailer-Record
  Let #plan_header = 0
End-Procedure

begin-procedure P230-Write-Header-Record
#debug9 Show 'P230-Write-Header-Record'

  Let $date_trade = substr($payenddate,3,2)||substr($payenddate,6,2)||substr($payenddate,9,2) 
  Let $T002_file_id = $date_trade||'.385' 
 
  write 1 from '002':3 
	       ' ':2 
	       $plan_number:6
	       ' ':12 
	       'DEFINE':6
	       ' ':4
	       $T002_file_id:10
	       ' ':37
    status=#filestat

  add 1 to #recs-written
  add 1 to #total-recs
  move 1 to #plan_header
  
  if #filestat != 0
     display 'Error Writing output file.  Program terminating.'
     stop
  end-if
end-procedure

begin-procedure P231-Write-T385-Record
#debug9 Show 'P231-Write-T385-Record'

  Move #ded_cur To $ded_amt 0000000.00
  Let $T385_ded_cur = Substr($ded_amt,1,7) || Substr($ded_amt,9,2)
  Do Get-Emp-Ssn (&pl.emplid, $Ssn_Out)  !GEX_SEC_REENG 2016-04-10 Jyotsna Negi
  If $plan_number = '543353' 
  or $plan_number = '543354' 
     let $fund_list_select = '13'
  else
     let $fund_list_select = '  '
  end-if

  write 1 from '385':3
	       ' ':2 
	       $plan_number:6
	       ' ':3 
	       !&pl.emplid:9    !GEX_SEC_REENG 2016-04-10 Jyotsna Negi
         $Ssn_Out:9     !GEX_SEC_REENG 2016-04-10 Jyotsna Negi
	       $date_trade:6
	       '1':1
	       '001':3
	       ' ':1
	       $T385_ded_cur:9
	       ' ':10
	       '2':1
	       ' ':1
	       '1':1
	       ' ':8
               $fund_list_select:2
               ' ':14  
    status=#filestat


  if #filestat != 0
     display 'Error Writing output file.  Program terminating.'
     stop
  end-if

  Add 1 to #recs-written
  Add 1 to #total-recs
end-procedure


begin-procedure P233-Write-Trailer-Record
#debug9 Show 'P233-Write-Trailer-Record'

  Let $TZZZ_date   = substr($payenddate,6,2)||substr($payenddate,9,2)||substr($payenddate,1,4) 
  
  Let $client_name = 'GIANT EAGLE, INC' 

  Move #recs-written to $TZZZ_recs_total 000000

  write 1 from 'ZZZ':3
	       ' ':2 
	       $plan_number:6
	       $client_name:32
	       $TZZZ_date:8
	       $TZZZ_recs_total:6
	       ' ':23
    status=#filestat


  if #filestat != 0
     display 'Error Writing output file.  Program terminating.'
     stop
  end-if
    Display 'Records Written, including file header: ' Noline
    Display #recs-written 999,999,999

  move 0 to #recs-written
  add  1 to #total-recs

end-procedure



Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'        (25,) Center
  End-If

  Do Format-Number(#total-recs,$total-recs,'888,888,888')
  Show ''
  Show $total-recs ' records written to ' $file1
  Show ''

  Do P310-Close-File

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
End-Procedure


Begin-Procedure P310-Close-File
#debug9 Show 'P310-Close-File'
  Close 1

  If #filestat != 0
     Show 'Error closing output file.  Program terminating.'
     Stop
  End-If
End-Procedure


#include 'askaod.sqc'    !Ask for AsOfDate
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx920.sqc'  !Get ben single row run control
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2016-04-10 Jyotsna Negi