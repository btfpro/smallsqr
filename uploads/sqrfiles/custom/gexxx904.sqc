!**************************************************************************
!  GEXXX904:  Retrieve "Multiple" Grade Run Controls and                  *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX904_Grade_Alias                                                *
!    $GEXXX904_Sal_Admin_Plan_Alias                                       *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX904_INCLUDE_GRADE_CRITERIA                                     *
!    $GEXXX904_EXCLUDE_GRADE_CRITERIA                                     *
!    $GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA                             *
!    $GEXXX904_EXCLUDE_GRADE_DISPLAY_CRITERIA                             *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX904_Grade_Alias = 'J.GRADE'                                *
!    Let $GEXXX904_Sal_Admin_Plan_Alias = 'J.SAL_ADMIN_PLAN'              *
!    Do GEXXX904-Select-Grade-Parameters                                  *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX904_INCLUDE_GRADE_CRITERIA]                             *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX904-Select-Grade-Parameters

  Let $OprID             = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID       = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_GRADE_ROWS = 0
  Let $Include_Condition = ''
  Let $Exclude_Condition = ''
  Let $GEXXX904_INCLUDE_GRADE_CRITERIA = ''
  Let $GEXXX904_EXCLUDE_GRADE_CRITERIA = ''
  Let $GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX904_EXCLUDE_GRADE_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_GRADE.GRADE
GEX_RC_GRADE.SAL_ADMIN_PLAN

  Add 1 To #GEX_RC_GRADE_ROWS

  Let $Include_Condition = '(' || $GEXXX904_Grade_Alias          || ' = '''  || &GEX_RC_GRADE.GRADE          || ''' And '
                               || $GEXXX904_Sal_Admin_Plan_Alias || ' = '''  || &GEX_RC_GRADE.SAL_ADMIN_PLAN || ''')'

  Let $Exclude_Condition = '(' || $GEXXX904_Grade_Alias          || ' != ''' || &GEX_RC_GRADE.GRADE          || ''' Or '
                               || $GEXXX904_Sal_Admin_Plan_Alias || ' != ''' || &GEX_RC_GRADE.SAL_ADMIN_PLAN || ''')'

  If #GEX_RC_GRADE_ROWS = 1
    Let $GEXXX904_INCLUDE_GRADE_CRITERIA = '(' || $Include_Condition
    Let $GEXXX904_EXCLUDE_GRADE_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX904_INCLUDE_GRADE_CRITERIA =
        $GEXXX904_INCLUDE_GRADE_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX904_EXCLUDE_GRADE_CRITERIA =
        $GEXXX904_EXCLUDE_GRADE_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_GRADE GEX_RC_GRADE

Where GEX_RC_GRADE.OPRID                       = [$OprID]
And   GEX_RC_GRADE.RUN_CNTL_ID                 = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_GRADE.GRADE, ' ')          != ' '
And   Rtrim(GEX_RC_GRADE.SAL_ADMIN_PLAN, ' ') != ' '

End-Select

  If #GEX_RC_GRADE_ROWS = 0
    Let $GEXXX904_INCLUDE_GRADE_CRITERIA = $GEXXX904_Grade_Alias          || ' = ' || $GEXXX904_Grade_Alias  || ' And ' ||
                                           $GEXXX904_Sal_Admin_Plan_Alias || ' = ' || $GEXXX904_Sal_Admin_Plan_Alias
    Let $GEXXX904_EXCLUDE_GRADE_CRITERIA = $GEXXX904_Grade_Alias          || ' = ' || $GEXXX904_Grade_Alias  || ' And ' ||
                                           $GEXXX904_Sal_Admin_Plan_Alias || ' = ' || $GEXXX904_Sal_Admin_Plan_Alias
  Else
    Let $GEXXX904_INCLUDE_GRADE_CRITERIA = $GEXXX904_INCLUDE_GRADE_CRITERIA || ')'
    Let $GEXXX904_EXCLUDE_GRADE_CRITERIA = $GEXXX904_EXCLUDE_GRADE_CRITERIA || ')'
    Let $GEXXX904_INCLUDE_GRADE_DISPLAY_CRITERIA = $GEXXX904_INCLUDE_GRADE_CRITERIA
    Let $GEXXX904_EXCLUDE_GRADE_DISPLAY_CRITERIA = $GEXXX904_EXCLUDE_GRADE_CRITERIA
  End-If

End-Procedure
