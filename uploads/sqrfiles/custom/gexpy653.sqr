!***********************************************************************
!  GEXPY653 :  Create W-2 file for city of Pittsburgh                  *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!                                                                      *
!***********************************************************************
! GEXPY653:             City of Pittsburgh W2 file.                    *
!                                                                      *
! Narrative:            Ctreate a W2 file according to specs from the  *
!                       City of Pittsburgh.                            *
!                                                                      * 
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			      #debug8 key variable values              * 
!			      #debug7 data returned from sql calls     * 
!                                                                      *
! SQL Tables:    W2_COMPANY                                            * 
!                YE_EE                                                 * 
!                YE_DATA                                               *
!                YE_AMOUNTS                                            * 
!                                                                      *
! Written by:           Carl Buckrop                                   *                                                           
!                                                                      *
! Normally run:         Year-end                                       *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS	DATE		DESCRIPTION OF THE CHANGE              *
!***********************************************************************
!                                                                      *
! CWB           12/01/2003      Initial creation using GEXPY618 with   *
!                               modifications for City of Pittsburgh.  *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' 

!**********************************************************************
begin-heading 6
!**********************************************************************
#debug9 Show 'begin-heading'

#Include 'stdhdg01.sqc'

  print 'Company'              (6,2)
  print 'Emps'                 (,13)
  print 'City Earns'           (,25)
  print 'City Tax'             (,45)
  print 'School Earns'         (,59)
  print 'School Tax'           (,79)
  print 'Non PA Res Earns'     (,92)
  print 'Non PA Res Tax'       (,112)
  print 'Total Earns'          (,132)
  print 'Total Tax'            (,152)

end-heading
!**********************************************************************
Begin-Program
!**********************************************************************
  
  display ''
  display 'Creating W-2 File for City of Pittsburgh'

  date-time () HH:MI:SS &timebegun
  date-time () MM/DD/YYYY &datebegun
  show ' '
  show 'Report Started at ' &timebegun ' on ' &datebegun
  show ' '

  Move 0 to $Zero_fill 0999999999.99

  do Init-Report
  do Process-Main
  
  do Stdapi-Term

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  show ' '
  do EOJ
End-Program

!***********************************************************************
Begin-Procedure Init-Report
!***********************************************************************
 
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Get-Tax-Reporting-Run-Controls
  do Initialization
  move 'N' to $E_Written 

  Let $GEXXX915_Locality_Alias = 'YA.LOCALITY'

  If $Prcs_Process_Instance = ''
     Let $in-year = ''''
     Display 'Enter Tax Reporting Year.'
     Display 'If left blank, the year from Tax Rptg Parms page will be used.'
     Input $in-year Maxlen=4
     If Rtrim($In-year, ' ') = ''
     else 
       Let $CalYear = $In-year
     end-if 
     Let $LocalityString = ''''
     Display 'Enter Locality or leave blank to exit.'
     While 1=1
       Input $In-Locality Maxlen=10 'Locality'
       Uppercase $In-Locality
       If Rtrim($In-Locality, ' ') = ''
         Concat '''' With $LocalityString
         Break
         Add 1 To #GRL_ROWS
       End-If
       If $LocalityString <> ''''
         Concat ''',''' With $LocalityString
       End-If
       Concat $In-Locality With $LocalityString
     End-While

     If $LocalityString = ''''''
       Let $GEXXX915_INCLUDE_LOCALITY_CRITERIA = '1=1'
       Let $GEXXX915_INCLUDE_LOCALITY_DISPLAY_CRITERIA = 'ALL'
     Else
       Let $GEXXX915_INCLUDE_LOCALITY_CRITERIA = $GEXXX915_Locality_Alias || ' In (' || $LocalityString || ')'
       Let $GEXXX915_INCLUDE_LOCALITY_DISPLAY_CRITERIA = $GEXXX915_INCLUDE_LOCALITY_CRITERIA
     End-If
  Else
     do GEXXX915-Select-Locality-Parameters 
  End-if
  show '$GEXXX915_Include_Locality_Criteria ' $GEXXX915_Include_Locality_Criteria

  If $CalYear = ''
    Do GEXXX922-Select-Parameters
    Let $CalYear = $GEX_RC_PAY.BALNCE_YEAR
  end-if

  If $CalYear = ''
     move &TX.Balance_Year to $CalYear 9999
  End-If

  show ' '
  show '#########################################################'  
  show 'Calendar Year for which this report is run: ' $calyear
  show '#########################################################'  
  show ' '
 
  let $AsOfDate = $CalYear || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP}, '', 'native')
  let $EffDate = $CalYear || '1231'
  do Format-DateTime($EffDate, $EffDate, {DEFCMP}, '', 'native')
  do Open-File
		
End-Procedure

!***********************************************************************
Begin-Procedure Initialization
!***********************************************************************


  let $ReportID2 = 'GEXPY653'		
  let $ReportID  = 'GEXPY653'		
  let $ReportTitle = 'Pittsburgh W-2 File Summary Report'	
  show 'Pittsburgh  W-2 File Records'	
  show ' '

  do Get-Current-DateTime
  move '1' to $MMLZero
  move '1' to $year4	
  do Format-DateTime($AsOfToday, $960Date, {DEFYMD},'','')
  move $960Date to $AsOfYear xxxx   !extract the current year
  move $AsOfYear  to #AsOfYear
  move #AsOfYear  to #saveAsOfYear
  subtract 1    from #AsOfYear

  If &TX.Balance_Year <> #AsOfYear
    display ''
    display 'Please note : Current Year is not one greater than Tax Reporting Year.'
    display 'Current Year is    ' noline
    display  #saveAsOfYear  9999
    display 'Tax Reporting Year ' noline
    display &TX.Balance_Year
    display ''
  End-if

Begin-Select
TEMP_SSN_MASK
FROM PS_INSTALLATION
End-Select
End-Procedure

!***********************************************************************
Begin-Procedure Process-Main
!***********************************************************************
do Assign-Year-End-Variables

Begin-Select DISTINCT

A.W2_REPORTING_CO  	() 	on-break	level=1
				!print=never 
 				before=Before-company		
				AFTER=After-company
E.EMPLID           	() 	on-break 	level=2
				print=never
				before=Before-EmplID
                                AFTER=AFTER-EMPLID


A.FEDERAL_EIN
E.COMPANY
E.SEQUENCE_NUMBER
E.TAXFORM_ID
E.YE_FNAME
E.YE_MIDDLE_NAME
E.YE_LNAME
E.YE_SUFFIX
E.ADDRESS1
E.ADDRESS2
E.CITY
E.STATE
E.POSTAL
YA.STATE
YA.LOCALITY
YA.BOX
YA.W2_AMOUNT
E.SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta

   
  move &A.W2_REPORTING_CO to $w2_company
  move &E.Company         to $E.Company
  move &E.state           to $E.state   
  move &E.TaxForm_ID      to $E.TaxForm_ID
  move &E.Sequence_Number to #E.Sequence_Number

  let $a.federal_ein = &a.federal_ein
  let #Amount = &YA.W2_Amount 
  let $Locality = RTRIM(&YA.Locality,' ')
  let $YA.Box = RTRIM(&YA.Box,' ')

  if $e.state = 'PA'
     let $PA_res_ind = 'Y'
  else
     let $PA_res_ind = 'N'
  end-if 

  if $E_Written = 'N'
     do write-e-record
     let $first_company = 'Y'
  end-if

  do validate-locality
!  do get-res-ind
 
  evaluate $YA.Box
    when = $YE_Local_Wages         !Local wages
      let #EARNS = #Amount   
      do accum-earns
      #debug8 show $E.COMPANY ' ' &TX.Balance_Year ' ' &YA.LOCALITY ' ' $E.Emplid ' ' &YA.W2_Amount ' ' $E.TaxForm_ID ' ' #E.Sequence_Number ' ' &YA.BOX
      #debug8 show '$YA.Locality ' $YA.Locality ' $hold_locality ' $hold_locality ' $YA.BOX ' $YA.box ' $hold_box ' $hold_box '$E.EmplID ' $E.EmplID ' $hold_ssn ' $hold_ssn ' #Amount ' #amount ' #Local_Wages ' #local_wages ' #updt_wages ' #Updt_Wages 
      add 1 to #co_tot_emps
      add 1 to #rpt_tot_emps
      break
    when = $YE_Local_Tax           !Local withheld
      let  #TAX  = #Amount   
      do accum-taxes
      #debug8 show $E.COMPANY ' ' &TX.Balance_Year ' ' &YA.LOCALITY ' ' $E.Emplid ' ' &YA.W2_Amount ' ' $E.TaxForm_ID ' ' #E.Sequence_Number ' ' &YA.BOX
      #debug8 show '$YA.Locality ' $YA.Locality ' $hold_locality ' $hold_locality ' $YA.BOX ' $YA.box ' $hold_box ' $hold_box ' $E.EmplID ' $E.EmplID ' $hold_ssn ' $hold_ssn ' #Amount ' #amount ' #Local_tax ' #local_tax ' #updt_taxes ' #Updt_taxes
      break
  end-evaluate
 
FROM  SYSADM.PS_W2_COMPANY A, 
      SYSADM.PS_YE_EE EE, 
      SYSADM.PS_YE_DATA E,
      SYSADM.PS_YE_AMOUNTS YA

WHERE A.CALENDAR_YEAR   = $calYear
  AND A.TAXFORM_ID       =  'W'
  AND A.COMPANY          = E.COMPANY
  AND A.CALENDAR_YEAR    = E.CALENDAR_YEAR
  AND A.TAXFORM_ID       = E.TAXFORM_ID
  AND E.COMPANY          = EE.COMPANY
  AND E.EMPLID           = EE.EMPLID
  AND E.TAXFORM_ID       = EE.TAXFORM_ID
  AND E.CALENDAR_YEAR    = EE.CALENDAR_YEAR
  AND YA.CALENDAR_YEAR   = EE.CALENDAR_YEAR
  AND YA.TAXFORM_ID      = EE.TAXFORM_ID
  AND YA.COMPANY         = EE.COMPANY
  AND YA.EMPLID          = EE.EMPLID
  AND EE.PROCESS_FLAG    <> 'V'
  AND YA.BOX             IN ($YE_Local_Wages, $YE_Local_Tax)
  AND YA.STATE            <> ' '
  AND YA.SEQUENCE_NUMBER = E.SEQUENCE_NUMBER
  AND [$GEXXX915_INCLUDE_LOCALITY_CRITERIA]

ORDER BY   A.W2_REPORTING_CO, E.EMPLID

end-select

End-Procedure

!***********************************************************************
Begin-Procedure validate-locality
!***********************************************************************
  If  $locality <> 'I022193S'
  and $locality <> 'I022612C1'
  and $locality <> 'I022612C2'
  and $locality <> 'I022612M1'
  and $locality <> 'I022612M2'
  and $locality <> 'I022612S2'
    show 'Unknown locality for Pittsburgh. This SQR may need modification'
    show 'in the procedures Accum-Earns and Accum-Taxes'
    STOP
  end-if
End-procedure

!***********************************************************************
Begin-Procedure Before-company
!***********************************************************************
if $first_company = 'N'
  do write-e-record
end-if
End-Procedure

!***********************************************************************
Begin-Procedure After-company
!***********************************************************************
do print-co-totals
End-Procedure

!***********************************************************************
Begin-Procedure Before-EmplID
!***********************************************************************

  #ifdef TAXDISPLAY
    Show 'Processing EMPLID : ' &E.EMPLID
  #endif

  Let $FirstTime = 'Y'
  let $State-Reportable = 'N'

  let $FirstName = rtrim(&E.YE_Fname,' ')   
  let $MiddleName = rtrim(&E.YE_Middle_name,' ')
  let $LastName = rtrim(&E.YE_Lname,' ') 
  let $Suffix = rtrim(&E.YE_Suffix,' ') 
  if $suffix <> ''
    let $lastName = $lastname||' '||$suffix
  end-if
  if $middlename = ''
    let $empname = $firstname||' '||$lastname
  else
    let $empname = $firstname||' '||$lastname||' '||$middlename
  end-if

  uppercase $EmpName

  move &e.address1     to $E.address1 
  move &e.address2     to $E.address2 
  move &E.Emplid       to $E.Emplid
  move &E.ssn          to $E.ssn !GEX_SEC_REENG 2015-04-29 Vahini Katta
  move &E.City         to $E.City
  let  $E.StateCd = rtrim(&E.State, ' ')
  move &E.Postal       to $E.Zip

  do Populate-EE-Address

  End-Procedure

!***********************************************************************
Begin-Procedure After-EmplID
!***********************************************************************
  do assign-amounts

  add #city_earns to #co_city_earns
  add #city_tax to #co_city_tax
  add #school_earns to #co_school_earns
  add #school_tax to #co_school_tax
  add #non_pa_earns to #co_non_pa_earns
  add #non_pa_tax to #co_non_pa_tax
  add #tot_earns to #co_tot_earns
  add #tot_tax to #co_tot_tax

  add #city_earns to #rpt_city_earns
  add #city_tax to #rpt_city_tax
  add #school_earns to #rpt_school_earns
  add #school_tax to #rpt_school_tax
  add #non_pa_earns to #rpt_non_pa_earns
  add #non_pa_tax to #rpt_non_pa_tax
  add #tot_earns to #rpt_tot_earns
  add #tot_tax to #rpt_tot_tax

  MOVE 0 to $Zero_fill 0999999999.99

  do Write-W-Record    

  do Init-W-Record
  do init-amounts

  Let $LocalReportable = 'N'
  let $first_company = 'N'

End-Procedure

!**************************************************************************
  Begin-Procedure Populate-EE-Address
!**************************************************************************

  If rtrim($E.City,' ') = ''
    Show 'City is blank in YE data for employee "' $E.Emplid '" in the personal data.'
    Show 'Fix it and run the load program (Tax910ld.sqr) again.'
    Stop
  End-if


  move $E.City         to $City
  move $E.StateCd      to $StateCd
  move $E.Zip          to $Zip

  if rtrim($E.Address1, ' ') = ''
      move '        ' to $E.Address1
  else
      let $E.Address1 = rtrim($E.Address1, ' ')
  end-if

  if rtrim($E.Address2, ' ') = ''
      move '        ' to $E.Address2
  else
      let $E.Address2 = rtrim($E.Address2, ' ')
  end-if

  let $address = $E.Address1 || ' ' || $E.Address2 
  uppercase $Address
  uppercase $City
  uppercase $Statecd
End-Procedure

!***********************************************************************
begin-procedure accum-earns
!***********************************************************************


    if $locality = 'I022193S'
    or $locality = 'I022612S2'
         ADD #EARNS TO #SCHOOL_EARNS 

    else
    if $locality = 'I022612M1'
    or $locality = 'I022612M2'
!         show $E.emplid ' ' $E.state ' ' $PA_res_ind ' ' $Locality ' #earns ' #earns
      if $PA_res_ind = 'N'
         ADD #EARNS TO #NON_PA_EARNS 
      else
         ADD #EARNS TO #CITY_EARNS 
      end-if
    else
    if $locality = 'I022612C1'
    or $locality = 'I022612C2'
        ADD #EARNS TO #CITY_EARNS 
        ADD #EARNS TO #SCHOOL_EARNS 

    end-if
    end-if 
    end-if

  ADD #EARNS TO #TOT_EARNS 

End-Procedure

!***********************************************************************
begin-procedure accum-taxes
!***********************************************************************

    if $locality = 'I022193S'
    or $locality = 'I022612S2'
        ADD #TAX   TO #SCHOOL_TAX   
    else
    if $locality = 'I022612M1'
    or $locality = 'I022612M2'
      if $PA_res_ind = 'N'
        ADD #TAX   TO #NON_PA_TAX   
      else
        ADD #TAX   TO #CITY_TAX   
      end-if
    else
    if $locality = 'I022612C1'
    or $locality = 'I022612C2'
        let  #temp_tax = (#TAX / 3)
        let  #SCHOOL_TEMP_TAX = (#temp_tax * 2) 
        let  #CITY_TEMP_TAX = #temp_TAX 
        add  #school_temp_tax to #school_tax
        add  #city_temp_tax to  #city_tax

    end-if
    end-if
    end-if

  ADD #TAX TO #TOT_TAX   

End-Procedure

!***********************************************************************
begin-procedure assign-amounts
!***********************************************************************

      MOVE #CITY_EARNS   TO $CITY_EARNS   0999999999.99
      MOVE #CITY_TAX     TO $CITY_TAX     0999999999.99
      MOVE #SCHOOL_EARNS TO $SCHOOL_EARNS 0999999999.99
      MOVE #SCHOOL_TAX   TO $SCHOOL_TAX   0999999999.99
      MOVE #NON_PA_EARNS TO $NON_PA_EARNS 0999999999.99
      MOVE #NON_PA_TAX   TO $NON_PA_TAX   0999999999.99
      MOVE #TOT_EARNS    TO $TOT_EARNS    0999999999.99
      MOVE #TOT_TAX      TO $TOT_TAX      0999999999.99

end-procedure  

!***********************************************************************
begin-procedure Get-Res-Ind
!***********************************************************************

begin-SELECT      
LT.RESIDENT
LT.STATE

  if &LT.STATE <> 'PA'
    let $PA_res_ind = 'N'
  else
    let $PA_res_ind = 'Y'
  end-if

FROM PS_LOCAL_TAX_DATA LT
  WHERE  LT.EMPLID   = &E.EMPLID
     AND LT.COMPANY  = &E.COMPANY
     AND LT.STATE    = &YA.STATE
     AND LT.LOCALITY = &YA.LOCALITY
     AND LT.EFFDT =
        (SELECT MAX(LT1.EFFDT) FROM PS_LOCAL_TAX_DATA LT1 
        WHERE LT.EMPLID  = LT1.EMPLID
          AND LT.COMPANY = LT1.COMPANY
          AND LT.LOCALITY   = LT1.LOCALITY
          AND LT1.EFFDT <= $AsOfDate)
end-SELECT
end-procedure                      

!***********************************************************************
Begin-Procedure Write-E-Record
!***********************************************************************
Begin-SELECT
!CLT.EMPLOYER_ID_LWT
CLT.GEX_ACCOUNT_DESCR
  move &CLT.GEX_ACCOUNT_DESCR to $CLT.GEX_ACCOUNT_DESCR
FROM SYSADM.PS_CO_LOCALTAX_TBL CLT
WHERE CLT.COMPANY = &E.Company
  AND CLT.LOCALITY = &YA.LOCALITY      
  AND CLT.EFF_STATUS = 'A'
  AND CLT.EFFDT =
      (SELECT MAX(CLT1.EFFDT)
       FROM   SYSADM.PS_CO_LOCALTAX_TBL CLT1
       WHERE  CLT1.COMPANY = CLT.Company
         AND  CLT1.LOCALITY = CLT.LOCALITY
         AND  CLT1.EFF_STATUS = 'A'
         AND  CLT1.EFFDT  <= $EffDate)
end-SELECT

begin-select
w2c.federal_ein

  move &w2c.federal_ein to $W2C.FEDERAL_EIN

from ps_w2_company w2c
WHERE W2C.CALENDAR_YEAR   =  $calYear
  AND W2C.TAXFORM_ID       = 'W'
  AND W2C.COMPANY          = &E.COMPANY
end-select

  move $CLT.GEX_ACCOUNT_DESCR to $EMPLOYER_ID 099999999
  move $W2C.Federal_EIN       to $FedEIN   099999999
  Write 1 from 'E':1
               ' ':1
      $EMPLOYER_ID:9
           $FedEIN:9
          $CalYear:4
               ' ':203

  add 1 to #Tot_Record_Count
  move 'Y' to $E_Written
End-Procedure

!***********************************************************************
Begin-Procedure Write-W-Record
!***********************************************************************

  write 1 from 'W':1
               ' ':1
         !$E.emplid:9 !GEX_SEC_REENG 2015-04-29 Vahini Katta
            $E.ssn:9  !GEX_SEC_REENG 2015-04-29 Vahini Katta
          $EmpName:40
          $address:40
             $city:25
          $statecd:2
              $zip:5
       $city_earns:13
         $city_tax:13
     $school_earns:13
       $school_tax:13
     $non_pa_earns:13
       $non_pa_tax:13
        $tot_earns:13 
          $tot_tax:13 


  add 1 to #Tot_Record_Count

End-Procedure

!***********************************************************************
Begin-Procedure init-W-Record
!***********************************************************************

     move $zero_fill to $city_earns
     move $zero_fill to $city_tax
     move $zero_fill to $school_earns
     move $zero_fill to $school_tax
     move $zero_fill to $non_pa_earns
     move $zero_fill to $non_pa_tax
     move $zero_fill to $tot_earns
     move $zero_fill to $tot_tax

End-Procedure

!***********************************************************************
Begin-Procedure init-amounts
!***********************************************************************

     move 0 to #earns
     move 0 to #tax
     move 0 to #city_earns
     move 0 to #city_temp 
     move 0 to #city_tax
     move 0 to #city_temp_tax
     move 0 to #school_earns
     move 0 to #school_temp
     move 0 to #school_tax
     move 0 to #school_temp_tax
     move 0 to #non_pa_earns
     move 0 to #non_res_tax
     move 0 to #non_res_earns
     move 0 to #non_pa_tax
     move 0 to #tot_earns
     move 0 to #tot_tax
     move 0 to #temp_tax

End-Procedure

!***********************************************************************
Begin-Procedure print-co-totals
!***********************************************************************
     move #co_tot_emps       to $co_tot_emps     999,999 
     move #co_city_earns     to $co_city_earns   9,999,999,999.99
     move #co_city_tax       to $co_city_tax     9,999,999,999.99
     move #co_school_earns   to $co_school_earns 9,999,999,999.99
     move #co_school_tax     to $co_school_tax   9,999,999,999.99
     move #co_non_pa_earns   to $co_non_pa_earns 9,999,999,999.99
     move #co_non_pa_tax     to $co_non_pa_tax   9,999,999,999.99
     move #co_tot_earns      to $co_tot_earns    9,999,999,999.99
     move #co_tot_tax        to $co_tot_tax      9,999,999,999.99
  
     print $w2_company       (+1,5)
     print $co_tot_emps      (0,+2)
     print $co_city_earns    (0,+2)
     print $co_city_tax      (0,+2)
     print $co_school_earns  (0,+2)
     print $co_school_tax    (0,+2)
     print $co_non_pa_earns (0,+2)
     print $co_non_pa_tax   (0,+2)
     print $co_tot_earns     (0,+2)
     print $co_tot_tax       (0,+2) 
     
     show $w2_company     
     show $co_tot_emps    
     show $co_city_earns  
     show $co_city_tax    
     show $co_school_earns
     show $co_school_tax  
     show $co_non_pa_earns
     show $co_non_pa_tax  
     show $co_tot_earns   
     show $co_tot_tax     
     
     do init-co-totals
     
End-Procedure
     
!***********************************************************************
Begin-Procedure init-co-totals
!***********************************************************************
     
     move 0 to #co_tot_emps
     move 0 to #co_city_earns
     move 0 to #co_city_tax
     move 0 to #co_school_earns
     move 0 to #co_school_tax
     move 0 to #co_non_pa_earns
     move 0 to #co_non_pa_tax
     move 0 to #co_tot_earns
     move 0 to #co_tot_tax

     move '000000'   to $co_tot_emps
     move $zero_fill to $co_city_earns
     move $zero_fill to $co_city_tax
     move $zero_fill to $co_school_earns
     move $zero_fill to $co_school_tax
     move $zero_fill to $co_non_pa_earns
     move $zero_fill to $co_non_pa_tax
     move $zero_fill to $co_tot_earns
     move $zero_fill to $co_tot_tax

End-Procedure
!***********************************************************************
Begin-Procedure print-rpt-totals
!***********************************************************************
     move #rpt_tot_emps       to $rpt_tot_emps     999,999 
     move #rpt_city_earns     to $rpt_city_earns   9,999,999,999.99
     move #rpt_city_tax       to $rpt_city_tax     9,999,999,999.99
     move #rpt_school_earns   to $rpt_school_earns 9,999,999,999.99
     move #rpt_school_tax     to $rpt_school_tax   9,999,999,999.99
     move #rpt_non_pa_earns   to $rpt_non_pa_earns 9,999,999,999.99
     move #rpt_non_pa_tax     to $rpt_non_pa_tax   9,999,999,999.99
     move #rpt_tot_earns      to $rpt_tot_earns    9,999,999,999.99
     move #rpt_tot_tax        to $rpt_tot_tax      9,999,999,999.99
  
     print 'TOTAL'            (+2,3)
     print $rpt_tot_emps      (0,+2)
     print $rpt_city_earns    (0,+2)
     print $rpt_city_tax      (0,+2)
     print $rpt_school_earns  (0,+2)
     print $rpt_school_tax    (0,+2)
     print $rpt_non_pa_earns  (0,+2)
     print $rpt_non_pa_tax    (0,+2)
     print $rpt_tot_earns     (0,+2)
     print $rpt_tot_tax       (0,+2) 

     do init-co-totals

End-Procedure

!***********************************************************************
begin-procedure Open-File
!***********************************************************************
    display 'Upon successful conclusion of this program, one file will have'
    display 'been created:'
    display ''

    move 0 to #RecordCount

    let $FileID = '{OUTFILE}'||'gexpy653.dat'

    open $FileID as 1 for-writing record=227:fixed

    display $FileID  noline
    display ' contains W-2 file for City of Pittsburgh.'
    display ''

    do init-w-record 

end-procedure

!***********************************************************************
begin-procedure EOJ
!***********************************************************************

  do print-rpt-totals
  close 1
end-procedure

#Include 'getfrmat.sqc'  !Get-Diskette-Format procedure
#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'getsufix.sqc'  !Move suffix from name
#Include 'w2.sqc'        !Assign-Year-End-Variables
#include 'gexxx915.sqc'  !Get locality multiples table
#include 'gexxx922.sqc'  !Get gex_rc_pay parameters.
