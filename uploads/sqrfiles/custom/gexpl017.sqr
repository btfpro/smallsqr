!***********************************************************************
!   GEXPL017:   LIGHT DUTY HOURS AND EARNINGS                          *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!                                                                      *
!***********************************************************************
! GEXPl017:             LIGHT DUTY HOURS AND EARNINGS                  *
!                                                                      *
! Narrative:            This program lists current and YTD             *
!                       light duty hours and earnings.                 *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!			#debug8 key variable values                    * 
!			#debug7 data returned from sql calls           * 
!                                                                      *
! SQL Tables:           PS_EARNINGS_BAL                                *
!                       PS_PAY_OTH_EARNS                               *
!                       PS_PAY_EARNINGS                                *
!                       PS_PAY_CAL_BAL_ID                              *
!                       PS_PERSONAL_DATA                               *
!                       PS_EMPLOYMENT                                  *
!                       PS_JOB                                         *
!                                                                      *
! Written by:           Joe Besagar                                    *                                                           
!                                                                      *
! Normally run:         Monthly                                        *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                       Company Division Code                          *
!            		Company  	                                   *
!                       Company/Paygroup                               *
!      			Deptid                                         *
!                       Acct_cd                                        *	                                   
!     			Empl_Status	                                   *                                                                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS    DATE           DESCRIPTION OF THE CHANGE        *        
!***********************************************************************
!  GEXPL017  JNB    02/15/1997         INITIAL CREATION.               *  
!            JNB    04/06/1998         STANDARDIZING RUN CONTROL.      *   
!            JDH    01/14/1999         MODIFIED CODE TO GET EARNINGS   *
!                                      FOR LAST CONFIRMED PAY PERIOD.  *
!                                      ADDED SQRW RUN CONTROL LOGIC.   *
!                                      PERFORMANCE TUNING.             *
!	     SXK    10/21/1999         Modified to have union from     *
!					job_labor                      *
!            SXC    03/17/2000         Excluded the curr hrs & curr    *
!                                      earnings column from the report.*
!                                      Included the job code & descr   *
!                                      in the report.
!            PSR    08/27/02           EMPL_RCD is now part of the key for 
!                                      PS_EARNINGS_BAL - Upgrade 8.3
!
!  					AMS		09/05/07						Set-Optimizer-Goal commented out 
!          ISDVMXD 05/05/2009	     Update SQR to retrieve JOB TITLE *
!                                     from Position Data for ITG#60297*																part of 8.3 to 9.0 upgrade retrofit
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************


#include 'setenv.sqc' !Set environment
#include 'setup02.sqc' 


!selection criteria
#define #x000_col_selection_criteria_a 	02
#define #x000_col_selection_criteria_b 	05

!line 1
#define #x000_col_company	    01
#define #x000_col_paygroup          15
#define #x000_col_union  	    30
#define #x000_col_jobcode           40  !SXC : 03/17/2000
#define #x000_col_descr             50  !SXC : 03/17/2000
#define #x000_col_name  	    85
#define #x000_col_emplid            125
#define #x000_col_lightduty3        141
#define #x000_col_lightduty4        162


begin-heading 8
#debug9 show 'begin-heading'
  #Include 'stdhdg01.sqc'

  print 'Pay End Dt:'   (3,1)
  print $pay_end_dt_hd  (,+1)
  
!LINE 1
  print '      YTD HRS' (+3,{#x000_col_lightduty3})
  Print ' YTD EARNINGS' (0,{#x000_col_lightduty4})

!LINE 2
  print 'COMPANY'       (+1,{#x000_col_company})
  print 'PAYGROUP'      (0,{#x000_col_paygroup})
  print 'UNION'         (0,{#x000_col_union})
  print 'JOBCODE'       (0,{#x000_col_jobcode})   !SXC : 03/17/2000
  print 'JOBCODE DESCR' (0,{#x000_col_descr})     !SXC : 03/17/2000
  print 'EMPLOYEE NAME' (0,{#x000_col_name})
  print 'EMP#'          (0,{#x000_col_emplid})
  Print '   LIGHT DUTY' (0,{#X000_col_lightduty3})
  Print '   LIGHT DUTY' (0,{#X000_col_lightduty4})

  print '-'             (+1,{#x000_col_company},7)     Fill
  print '-'             (0,{#x000_col_paygroup},8)     Fill
  print '-'             (0,{#x000_col_union},5)        Fill
  print '-'             (0,{#x000_col_jobcode},7)      Fill    !SXC : 03/17/2000
  print '-'             (0,{#x000_col_descr},13)       Fill    !SXC : 03/17/2000
  print '-'             (0,{#x000_col_name},30)        Fill
  print '-'             (0,{#x000_col_emplid},9)       Fill
  Print '-'             (0,{#X000_col_lightduty3},13)  Fill
  Print '-'             (0,{#X000_col_lightduty4},13)  Fill
end-heading
                                                                     
 
begin-report
#debug9 show 'begin-report' 
  do Set-Optimizer-Goal
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do stdapi-init
  do P010-Init-Variables
  do Get-Calendar-Year-Id
  do Report

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  do stdapi-term
  do Reset
end-report


begin-procedure Set-Optimizer-Goal
  Begin-SQL
!    Alter Session Set OPTIMIZER_GOAL=RULE;
  End-SQL
end-procedure


begin-procedure P010-Init-Variables
#debug9 show 'P010-Init-Variables'
  let $x000_new_page_flag = 'N'

  let $ReportID    = 'GEXPL017'
  let $ReportTitle = 'LIGHT DUTY HOURS AND EARNINGS'                  
  let $Country     = 'USA'

  show '$ReportID='    $ReportID 
  show '$ReportTitle=' $ReportTitle

  Do GEXXX951-Select-Oracle-Instance

  Let $GEXXX900_Company_Alias  = 'JOB.COMPANY'                           
  Let $GEXXX901_Company_Alias  = 'JOB.COMPANY' 
  Let $GEXXX901_Paygroup_Alias = 'JOB.PAYGROUP'    
  Let $GEXXX902_Deptid_Alias   = 'JOB.DEPTID'  
  Let $GEXXX903_Emp_Stat_Alias = 'JOB.EMPL_STATUS'  
  Let $GEXXX907_Div_CD_Alias   = 'GDT.GEX_COMPANY_DIV_CD'
  Let $GEXXX909_Acct_CD_Alias  = 'JOB.ACCT_CD'

  if $Prcs_Process_Instance = ''
    Do GEXXX950-Input-Parameters
  else
    Do GEXXX922-Select-Parameters
    let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.ASOFDATE
    if rtrim($GEX_RC_PAY.ASOFDATE,' ') = ''
      let $X000_WHERE_SELECT_ASOFDATE = $asoftoday
    end-if
    do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE ,{DEFDATE}, '', '')
    Do GEXXX900-Select-Company-Parameters 
    Do GEXXX901-Select-Company-Paygroup-Parameters    
    Do GEXXX902-Select-Deptid-Parameters 
    Do GEXXX903-Select-Emp-Stat-Parameters          
    Do GEXXX907-Select-Div-CD-Parameters
    Do GEXXX909-Select-Acct-CD-Parameters
  end-if  

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
	          
  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  let $X000_ORDER_BY = 'Company, Paygroup, Union, Emp#'   

  show '$GEXXX907_INCLUDE_DIV_CD_CRITERIA                =' $GEXXX907_INCLUDE_DIV_CD_CRITERIA
  show '$GEXXX900_INCLUDE_COMPANY_CRITERIA               =' $GEXXX900_INCLUDE_COMPANY_CRITERIA
  show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA      =' $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA   
  show '$GEXXX902_INCLUDE_DEPTID_CRITERIA                =' $GEXXX902_INCLUDE_DEPTID_CRITERIA
  show '$GEXXX903_INCLUDE_EMP_STAT_CRITERIA              =' $GEXXX903_INCLUDE_EMP_STAT_CRITERIA
  show '$GEXXX909_INCLUDE_ACCT_CD_CRITERIA               =' $GEXXX909_INCLUDE_ACCT_CD_CRITERIA         
  show '$X000_WHERE_SELECT_ASOFDATE                      =' $X000_WHERE_SELECT_ASOFDATE 

  Do P120-Get-Last-Confirmed-Pay
  Do P130-Print-Cover-Page
end-procedure


Begin-Procedure P120-Get-Last-Confirmed-Pay
#debug9 Show 'P120-Get-Last-Confirmed-Pay'
Begin-Select
Max(pay_end_dt) &pay_end_dt

  Let $pay_end_dt       = &pay_end_dt
    do Format-DateTime($pay_end_dt, $pay_end_dt_hd,{DEFDATE}, '', '')
  Show 'Last confirmed pay end date:  ' $pay_end_dt

From   ps_pay_cal_bal_id
where  pay_end_dt <= $X000_WHERE_SELECT_ASOFDATE
And    balance_id  = 'CY'
End-Select
End-Procedure


begin-procedure P130-Print-Cover-Page
#debug9 Show 'P130-Print-Cover-Page'
  print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,{#x000_col_selection_criteria_a})
  print '$Prcs_OPRID          ='                              (+1,{#x000_col_selection_criteria_b})
  print $Prcs_OPRID                                           (0,+2)
  print '$PRCS_RUN_CNTL_ID    ='                              (+1,{#x000_col_selection_criteria_b})
  print $PRCS_RUN_CNTL_ID                                     (0,+2)

  print 'SELECTION CRITERIA FOR THIS REPORT RUN:'			(+5,{#x000_col_selection_criteria_a})
  print '$X000_WHERE_SELECT_ASOFDATE                         ='   (+1,{#x000_col_selection_criteria_b})
  print $X000_WHERE_SELECT_ASOFDATE                               (0,+2)
   
  print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA 			(0,+2)
  print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA    	      (0,+2)
  print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA 	(0,+2)
  print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	            (0,+2)
  print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         	(0,+2)
  print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA			(0,+2)

  Print 'As Of Date :'                                            (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                             (0,+2)

  Print 'Last confirmed pay end date:'                            (+2,5)
  Print $pay_end_dt                                               (0,+2)

  print 'SORT ORDER FOR THIS REPORT RUN:'					(+5,{#x000_col_selection_criteria_a})
  print '$X000_ORDER_BY                                      ='   (+1,{#x000_col_selection_criteria_b})
  print $X000_ORDER_BY 	(0,+2)

  let #page-count = 0
  NEW-PAGE
  LET #PAGE-COUNT = 1
end-procedure

! SXC - 03/17/2000
! Removed the tables PS_PAY_EARNINGS, PS_PAY_OTH_EARNS from the main select below.

Begin-procedure Report
#debug show 'Report'
  Let $x100_Get_Employees_Data_Flag = 'N'
  Let $Errorfound = 'N'

begin-select ON-ERROR=P900-SQL-Error-Found('Report')
job.COMPANY  ()    on-break
                 print=never
                 level=1
                 save=$prev_company
                 after=P140-company-change

job.PAYGROUP ()    on-break
                 print=never
                 level=2
                 save=$prev_paygroup
                 after=P130-paygroup-change       

job.EMPLID
job.empl_rcd
job.jobcode
job.effdt

JOB.UNION_CD        &JOB.UNION_CD       !SXK 10/21/99 
PR.NAME            &pr.name
PCBI.PAY_END_DT
PCBI.BALANCE_YEAR
PCBI.BALANCE_QTR
PCBI.BALANCE_PERIOD

JOB.POSITION_NBR !ISDVMXD added for ITG#60297 on 05/05/2009

  Let $errorfound = 'N'
  Let $x100_Get_Employees_Data_Flag = 'Y'
 
  move &job.COMPANY       to $Company
  move &job.PAYGROUP      to $PayGroup
  move &job.EMPLID        to $emplid
  move &job.EMPL_RCD     to #emplrcd
  move &pcbi.PAY_END_DT    to $pay_end_dt 
 
  Do P120-Get-Earnings-Balances

  if $found = 'Y'
  
  !ISDVMXD added for ITG#60297 on 05/05/2009 begins
     Let $Position_Nbr = &JOB.POSITION_NBR   
     Let $PosName = ''   
     Do Get-Position-Title 
     Let $jobtitle = $PosName
     
     if $PosName = ''     
     Do get_jobcode_desc   ! SXC 03/17/2000
     end-if
    !ISDVMXD added for ITG#60297 on 05/05/2009 ends
        
     print &job.COMPANY     (+1,{#x000_col_company})
     print &job.PAYGROUP    (,{#x000_col_paygroup})
     print &pr.name       (,{#x000_col_name},30)
     print &JOB.UNION_CD   (,{#x000_col_union})  !SXK 10/21/99

     print &job.jobcode     (,{#x000_col_jobcode})    !SXC 03/17/2000
     
     !print &j.descr         (,{#x000_col_descr})      ! SXC 03/17/2000 !ISDVMXD commented for ITG#60297 on 05/05/2009 
   
      print $jobtitle     (,{#x000_col_descr}) !ISDVMXD added for ITG#60297 on 05/05/2009    
     !GEX_SEC_REENG 2015-04-29 Vahini Katta Begins  
     do Get-Emp-Ssn (&job.EMPLID, $Ssn_Out)   
     !print &job.EMPLID      (,{#x000_col_emplid})
     print $Ssn_Out         (,{#x000_col_emplid})
     !GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
     print #sum_ytd_hrs   (,{#x000_col_lightduty3})  Edit 99,999,990.00
     print #sum_ytd_grs   (,{#x000_col_lightduty4})  Edit 99,999,990.00
  end-if

FROM   PS_GEX_DEPT_TBL     GDT,
       PS_PERSONAL_DATA    PR,
!SXK 10/21/99 Begin
!       PS_EMPLOYMENT       EM,
!SXK 10/21/99 End
       PS_JOB              JOB,
       PS_PAY_CAL_BAL_ID   PCBI
! SXC 03/17/2000 Begin
WHERE  JOB.effdt           =
      (Select Max(effdt)
       From   ps_job
       Where  emplid       = job.emplid
       And    empl_rcd    = job.empl_rcd
       And    effdt       <= $pay_end_dt
       And    company      = JOB.COMPANY
       And    paygroup     = JOB.paygroup)
And    JOB.effseq          =
      (Select Max(effseq)
       From   ps_job
       Where  emplid       = job.emplid
       And    empl_rcd    = job.empl_rcd
       And    effdt        = job.effdt
       And    company      = JOB.COMPANY
       And    paygroup     = JOB.paygroup)
! SXC 03/17/2000 End
And    PCBI.COMPANY        = job.COMPANY
AND    PCBI.PAYGROUP       = job.PAYGROUP
AND    PCBI.PAY_END_DT     = $pay_end_dt
AND    PCBI.BALANCE_ID     = $Calendar_Year_Id

!SXK 10/21/99 Begin
!AND    EM.EMPLID           = B.EMPLID
!AND    EM.EMPL_RCD        = B.EMPL_RCD

! AND JL.EMPLID = B.EMPLID ! SXC 03/17/2000, since PS_PAY_EARNINGS TBL no longer exists
                           ! in this select
! AND JL.EMPL_RCD = B.EMPLID ! SXC 03/17/2000, since PS_PAY_EARNINGS TBL no longer exists
                              ! in this select
!SXK 10/21/99 

AND    JOB.DEPTID          = GDT.DEPTID 

AND    GDT.EFFDT           =
      (SELECT MAX(EFFDT)
       FROM   PS_GEX_DEPT_TBL
       WHERE  DEPTID       = JOB.DEPTID
       AND    EFFDT       <= $pay_end_dt)

AND    GDT.SEQUENCE_NUMBER =
      (SELECT MAX(SEQUENCE_NUMBER)
       FROM   PS_GEX_DEPT_TBL
       WHERE  DEPTID       = GDT.DEPTID
       AND    EFFDT        = GDT.EFFDT)

AND    PR.EMPLID           = job.EMPLID

AND   [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
AND   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND   [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
AND   [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
AND   [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
!SXK 10/21/99

ORDER BY JOB.COMPANY, JOB.PAYGROUP, JOB.UNION_CD, JOB.EMPLID !SXK 10/21/99 & SXC 03/17/00
end-select
 
  if $x100_Get_Employees_Data_Flag = 'N'
    print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) CENTER
  else
    Print 'Report Totals'       (+3,8)
    Print #Rpt_total_ytd_hrs    (,{#x000_col_lightduty3}) Edit 99,999,990.00
    Print #Rpt_total_ytd_grs    (,{#x000_col_lightduty4}) Edit 99,999,990.00
  end-if
end-procedure


begin-procedure P120-Get-Earnings-Balances
#debug9 show 'P120-Get-Earnings-Balances'
  move 'N' to $found

begin-SELECT on-error=P900-sql-error-found('P120-Get-Earnings-Balances')
EB.HRS_YTD   &eb.hrs_ytd
EB.GRS_YTD   &eb.grs_ytd
 
  move &EB.HRS_YTD   to #sum_ytd_hrs
  move &EB.GRS_YTD   to #sum_ytd_grs

  add #sum_ytd_hrs   to #total_ytd_hrs
  add #sum_ytd_grs   to #total_ytd_grs

  add #sum_ytd_hrs   to #Rpt_total_ytd_hrs
  add #sum_ytd_grs   to #Rpt_total_ytd_grs
   
  move 'Y' to $found
  
FROM   PS_EARNINGS_BAL EB

WHERE  EB.EMPLID               = $EmplID
AND    EB.COMPANY              = $Company
AND    EB.BALANCE_ID           = $Calendar_Year_Id
AND    EB.BALANCE_YEAR         = &PCBI.BALANCE_YEAR
AND    EB.EMPL_RCD             = #emplrcd

AND    EB.BALANCE_PERIOD       =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_EARNINGS_BAL
       WHERE  EMPLID           = $Emplid
       AND    COMPANY          = $Company
       AND    BALANCE_ID       = $Calendar_Year_ID
       AND    BALANCE_YEAR     = &PCBI.BALANCE_YEAR
       AND    BALANCE_PERIOD  <= &PCBI.BALANCE_PERIOD
       AND    EB.SPCL_BALANCE  = 'N'
       AND    EMPL_RCD         = #emplrcd
       AND    EB.ERNCD         = '200')

AND    EB.SPCL_BALANCE         = 'N'
AND    EB.ERNCD                = '200'  
end-SELECT

  if $found = 'N'
    move 0 to #sum_ytd_hrs
    move 0 to #sum_ytd_grs
  end-if
end-procedure

!***********************************************************************
! SXC 03/13/20000 Begin.
Begin-Procedure get_jobcode_desc
Begin-Select
j.descr   &j.descr

     Let $jobtitle = rtrim(&j.descr,' ')!ISDVMXD added for ITG#60297 on 05/05/2009

From ps_jobcode_tbl j
Where j.jobcode = &job.jobcode AND
      j.eff_status = 'A' AND
      j.effdt = (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL
                    WHERE JOBCODE = &JOB.JOBCODE AND
                          EFF_STATUS = 'A' AND
                          EFFDT  <= &JOB.EFFDT )
End-Select
End-Procedure
! SXC 03/13/20000 End
!***********************************************************************

begin-procedure P130-Paygroup-Change
#debug9 show 'P130-Paygroup-Change' 
  Print 'Paygroup'             (+2,8)
  Print $prev_paygroup         (,+1)
  Print 'Totals'               (,+1)

  Print #Total_ytd_hrs         (,{#x000_col_lightduty3})  Edit 99,999,990.00
  Print #Total_ytd_grs         (,{#x000_col_lightduty4})  Edit 99,999,990.00
  Print ' '                    (+1)

  Let #Company_total_ytd_hrs    =  #Company_total_ytd_hrs  + #Total_ytd_hrs
  Let #Company_total_ytd_grs    =  #Company_total_ytd_grs  + #Total_ytd_grs
 
  Let #Total_ytd_hrs    = 0
  Let #Total_ytd_grs    = 0
end-procedure


begin-procedure P140-Company-Change
#debug9 show 'P140-Company-Change' 
  Print 'Company'             (+2,8)
  Print $prev_company         (,+1)
  Print 'Totals'              (,+1)

  Print #Company_Total_ytd_hrs        (,{#x000_col_lightduty3})  Edit 99,999,990.00
  Print #Company_Total_ytd_grs        (,{#x000_col_lightduty4})  Edit 99,999,990.00
 
  Let #Company_Total_ytd_hrs    = 0
  Let #Company_Total_ytd_grs    = 0

  New-Page
end-procedure
 

begin-procedure P900-SQL-Error-Found($Proc_Name) 
#debug9 Show 'P900-SQL-Error-Found'
  do P990-error-found
  display 'SQL Error in ' 
  display $Proc_Name 
  display $_sql-error
  display 'sql-status:' 
  display #_sql-status 
  STOP
end-procedure


begin-procedure P990-Error-Found
#debug9 Show 'P990-Error-Found'
  move 'Y' to $ErrorFound
  do Format-Number(#Inputrec, $out, '99999')
  print $out (+1,1)

  !let $EmplId_1st3 = substr($EmplId,1,3)
  !let $EmplId_mid2 = substr($EmplId,4,2)
  !let $EmplId_last4 = substr($EmplId,6,4)
 
  !String $EmplId_1st3 $EmplID_mid2 $EmplId_last4 by '-' into $EmplId11

  !print $EmplID11 (,8)
  print $EmplID (,8) !GEX_SEC_REENG 2015-04-29 Vahini Katta
end-procedure


#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'askaod.sqc'    !Ask for AsOfDate

#Include 'gexxx950.sqc'  !Get SQRW run control parameters
#Include 'gexxx951.sqc'  !Select Oracle Instance
#Include 'gexxx922.sqc'  !Run Control parameters from GEX_RC_PAY record 
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#Include 'getposnm.sqc'   !get position title  !ISDVMXD added for ITG#60297 on 05/05/2009
#Include 'getgeid.sqc'   !Get SSN !GEX_SEC_REENG 2015-04-29 Vahini Katta
