!***********************************************************************
! GEXPY671: This program creates an interface file for the United Way  *
!           term.                                         *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS    DATE           DESCRIPTION OF THE CHANGE        *
!***********************************************************************
! VENDAXG  AXG       08/09/2010       INITIAL CREATION(Clone of GEXPY642*
! GEXHR_848_E107873  08/29/2011       Kundan kumar Yeedibilli	       *
! Change the end date in order to take sunday's date.		       *
! GEXHR_848_E127782  06/04/2012       ISDVMZA -- Replace the hardcoded *
!                                     year with thrudate year          *
! GEXHR_848_E138624  11/04/2012       ISDVMZA -- exclude job code 90000*
!                                      - City Police from file.        *
! GEXHR_848_E156436_01  08/27/2013       Vahini Katta                  *
! Changed the file type to tab delimited                               *
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup02.sqc'
Begin-setup

Declare-variable

Date $Asoftoday
Date $from_date
Date $Thru_Date

End-Declare
End-Setup
Begin-Report

Do Init-DateTime
Do Init-Number
Do stdapi-init
Move 'GEXPY671' to $ReportID
Move 'United Way Term Interface' to $ReportTitle
display $ReportTitle

Do Get-Current-DateTime
Do Report
Do Commit-Transaction
date-time () hh:mi:ss &timeEnded
display 'Report Ended: ' noline
display &timeEnded
Do stdapi-term

End-Report

!************************************************************************
Begin-Procedure Report

Do Open-File

Do Print-Header-Term


 date-time () hh:mi:ss &timeBegan
 display 'Report Began: ' noline
 display &timeBegan

 Move 'N' to $Errorfound
 Move '0' to $filler

 Do Get-From-Thru-Date
 Do Process-Employee-Data

  If $sev-error = 'Y'
    goto report-exit
 End-if

Report-Exit:

  Print 'Total Transactions=' (+2,1)
  Do Format-Number(#InputTran, $out, '99999')
  Print $out ()
  Print '     Total Errors in Transactions=' ()
  Do Format-Number(#ErrorCount, $out, '99999')
  Print $out ()

  close 1

  date-time () hh:mi:ss &timeProcess
  display #InputTran 99999 noline
  display ' Transactions Processed: ' noline
  display &timeProcess

End-Procedure


begin-heading 7
  Move 'United Way Term Interface - ERROR REPORT' to $ReportTitle
  #Include 'stdhdg01.sqc'

  date-time () DD-MON-YYYY &curr_Date

   Print 'EMPLID' (+2,1)
   Print 'ERROR MESSAGE' (,15)

End-heading

!******************************************************************
!This procedure selects all the employees that satisfy the criteria
!and depending on the action taken upon thier job records, writes
!the necessary records into the output file
!******************************************************************

Begin-Procedure Process-Employee-Data

Move 'N' to $found
Let #inputtran = 0
display $reportdate_dbf

Begin-Select distinct
C.EMPLID
TO_CHAR(C.TERMINATION_DT,'MM/DD/YY') &Terminationdate
C.COMPANY

   Let $found = 'Y'

	Let $Emplid = &C.EMPLID
	Let $Company = &C.COMPANY
	Let $Termination_Dt = &Terminationdate

	Do Get-Name
	!Do Get-Emplid-Mapp !GEX_SEC_REENG 2015-04-29 Vahini Katta
	Do Get-YTD-Balance
	Do Print-Record-Term

FROM PS_JOB C
WHERE ((C.EMPL_STATUS NOT IN ('A','L','P','S') AND C.ACTION IN ('TER', 'RET','TWB','TWP','RWP') AND (C.ACTION_DT BETWEEN $From_Date AND $Thru_Date) )
     OR
    ((C.TERMINATION_DT BETWEEN $From_Date AND $Thru_Date) AND C.EMPL_STATUS <>  'U'))
  AND C.JOBCODE <> '90000'          ! GEXHR_848_E138624  11/04/2012  ISDVMZA


End-Select

If $found = 'N'
   add #1 to #errorcount
   Print 'No employees found for interface file' (,132)
End-if

End-Procedure Process-Employee-Data

!GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
!******************************************************************
! Get GE Employee Id from Employee ID table based on SSN
!***********************************************************************
!Begin-Procedure Get-Emplid-Mapp

!Begin-Select
!GEM.SSN,
!GEM.EMPLID,
!GEM.EMPL_RCD

!   Let $Emplid_7_digit = &GEM.EMPLID

!FROM PS_GEX_EMPLID_MAPP GEM
!WHERE GEM.SSN = $Emplid
!End-Select

!End-Procedure
!GEX_SEC_REENG 2015-04-29 Vahini Katta Ends

Begin-Procedure Get-Name
Begin-Select
A.FIRST_NAME
A.LAST_NAME
A.MIDDLE_NAME

	Let $First_Name_xls = Ltrim(Rtrim(&A.FIRST_NAME,' '),' ')
	Let $Last_Name_xls = Ltrim(Rtrim(&A.LAST_NAME,' '),' ')
	Let $Middle_Name_xls = Ltrim(Rtrim(&A.MIDDLE_NAME,' '),' ')


FROM PS_PERSONAL_DATA A
WHERE A.EMPLID = $Emplid
End-Select
End-Procedure Get-Name
!********************************************************************
!This procedure displays in the error report If there are any sql erros
!happened in the program
!********************************************************************

Begin-Procedure SQL-Error-Found($Proc_Name)

  Print 'SQL Error in ' (,21)
  Print $Proc_Name (,35)
  Print $_sql-error (+1,1)
  Print 'sql-status:' (+1,1)
  Print #_sql-status () edit 99999

End-Procedure

!*********************************************************************
!This procedure displays the error message for the records that are in
!progress
!*********************************************************************

!*********************************************************************
Begin-Procedure Print-Header-Term
!*********************************************************************
encode '<009>' into $tab
Let $delim = $tab
string 'First Name' ' Middle Name' ' Last Name' ' Emplid' ' Campaign Year' ' YTD Donation' ' Term Date'  by $delim into $actives
show '$actives :' $actives
write 1 from $actives
!*********************************************************************
End-Procedure Print-Header-Term
!*********************************************************************

!*********************************************************************
Begin-Procedure Print-Record-Term
!*********************************************************************
!GEXHR_848_E156436_01 08/27/2013 Vahini Katta Begins
!Let $Campaign_Year 	     =  '="' ||'2011' || '"'
!Let $Campaign_Year 	     =  '="' ||$balance_year || '"'
!Let $first_name_write        = '="' || $First_Name_xls  || '"'
!Let $middle_name_write       = '="' || $middle_name || '"'
!Let $last_name_write         = '="' || $Last_Name_xls || '"'
!Let $Emplid_write            = '="' || $Emplid_7_digit || '"'
!Let $Termination_Dt_write    = '="' || $Termination_Dt || '"'
!Let $Ytd_Donation            = '="' || To_Char(#Balance_YTD) || '"'

Let $Campaign_Year 	     =  $balance_year 
Let $first_name_write        =  $First_Name_xls  
Let $middle_name_write       =  $middle_name 
Let $last_name_write         =  $Last_Name_xls 
Let $Emplid_write            =  $Emplid !GEX_SEC_REENG 2015-04-29 Vahini Katta
Let $Termination_Dt_write    =  $Termination_Dt 
Let $Ytd_Donation            =  To_Char(#Balance_YTD) 

!GEXHR_848_E156436_01 08/27/2013 Vahini Katta Ends

String $first_name_write $middle_name_write $last_name_write $Emplid_write $Campaign_Year $Ytd_Donation $Termination_Dt_write by $delim into $actives
show '$actives :' $actives
Write 1 from $actives

!*********************************************************************
End-Procedure Print-Record-Term
!*********************************************************************


!*********************************************************************
Begin-Procedure Open-File
!*********************************************************************
   !GEXHR_848_E156436_01 08/27/2013 Vahini Katta Begins
   !Let $FileName = '{OUTFILE}'||'gexpy671.xls'
   Let $FileName = '{OUTFILE}'||'gexpy671.txt'
   !GEXHR_848_E156436_01 08/27/2013 Vahini Katta Ends
   open $FileName as 1 for-writing record=1000 status=#filestat

   If #filestat < 0
	display 'File Open error occured;  Cannot proceed'
	!goto Report-Exit
   End-If

   Show 'File: ' $FileName

!*********************************************************************
End-Procedure Open-File
!*********************************************************************


!*********************************************************************
Begin-Procedure Get-From-Thru-Date
!*********************************************************************
Begin-Select
GEX_DATE.FROMDATE
GEX_DATE.THRUDATE

	Let $From_Date = &GEX_DATE.FROMDATE
	Let $Thru_Date = &GEX_DATE.THRUDATE


From  PS_GEX_RC_HR GEX_DATE
Where GEX_DATE.OPRID                 = $Prcs_OprID
And   GEX_DATE.RUN_CNTL_ID           = $Prcs_Run_Cntl_ID
End-Select

   If $From_Date = ''
	Let $From_Date = dateadd($asoftoday,'day', -7)
   End-if
   Show '$From_Date :'$From_Date

   If $Thru_Date = ''
	!Let $Thru_Date = $asoftoday !GEXHR_848_E107873 08/29/2011 kundan kumar yeedibilli
	Let $Thru_Date = dateadd($asoftoday,'day', -1) !GEXHR_848_E107873 08/29/2011 kundan kumar yeedibilli
   End-if
   Show '$Thru_Date :'$Thru_Date

!*******  GEXHR_848_E127782 -- 06/04/2012 -- ISDVMZA  begin   ***********

   Let $balance_year = substr($Thru_Date,8,4)
   Let #bal_year  = $balance_year
   Show 'balance_year :' $balance_year

!*******  GEXHR_848_E127782 -- 06/04/2012 -- ISDVMZA  end     ***********


!*********************************************************************
End-Procedure Get-From-Thru-Date
!*********************************************************************

!*********************************************************************
Begin-Procedure Get-YTD-Balance
!*********************************************************************

Let #Balance_YTD = 0
Begin-Select
DD.DED_YTD

	Let #Balance_YTD = &DD.DED_YTD

FROM PS_DEDUCTION_BAL DD
WHERE DD.DEDCD = 'UNWAY'
!AND DD.BALANCE_YEAR = '2011'
AND DD.BALANCE_YEAR = #bal_year    !**  GEXHR_848_E127782 -- 06/04/2012 -- ISDVMZA
AND DD.BALANCE_QTR = (SELECT MAX(D.BALANCE_QTR) FROM PS_DEDUCTION_BAL D
			WHERE DD.EMPLID = D.EMPLID
			AND DD.COMPANY = D.COMPANY
			AND DD.BALANCE_YEAR = D.BALANCE_YEAR )
AND DD.BALANCE_PERIOD = (SELECT MAX(E.BALANCE_PERIOD) FROM PS_DEDUCTION_BAL E
			WHERE DD.EMPLID = E.EMPLID
			AND DD.COMPANY = E.COMPANY
			AND DD.BALANCE_YEAR = E.BALANCE_YEAR
			AND DD.BALANCE_QTR = E.BALANCE_QTR)
AND DD.EMPLID = $Emplid
AND DD.COMPANY = $Company
End-Select
!*********************************************************************
End-Procedure Get-YTD-Balance
!*********************************************************************

#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
!*#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'payrnctl.sqc'  !Select-Parameters
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions
