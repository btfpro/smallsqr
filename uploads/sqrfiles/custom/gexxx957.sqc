!**************************************************************************
!  GEXXX957:  Retrieve "Multiple" ACTION Run Controls                     *           
!              and  Return a Formatted Where-Clause                       *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX957_ACTION_Alias                                                *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX957_INCLUDE_ACTION_CRITERIA                                     *
!    $GEXXX957_EXCLUDE_ACTION_CRITERIA                                     *
!    $GEXXX957_INCLUDE_ACTION_DISPLAY_CRITERIA                             *
!    $GEXXX957_EXCLUDE_ACTION_DISPLAY_CRITERIA                             *
!                                                                         *                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX957_ACTION_Alias = 'J.ACTION'                                *
!    Do GEXXX957-Select-ACTION-Parameters                                  *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX957_EXCLUDE_ACTION_CRITERIA]                               *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE          DESCRIPTION OF THE CHANGE   *
!**************************************************************************
!	?	SCM		11/14/2000    INITIAL CREATION for COD    *
!**************************************************************************


Begin-Procedure GEXXX957-Select-ACTION-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_ACTION_ROWS = 0
  Let $GEXXX957_INCLUDE_ACTION_CRITERIA = ''
  Let $GEXXX957_EXCLUDE_ACTION_CRITERIA = ''
  Let $GEXXX957_INCLUDE_ACTION_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX957_EXCLUDE_ACTION_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_ACTION.ACTION

  Add 1 To #GEX_RC_ACTION_ROWS

  If #GEX_RC_ACTION_ROWS = 1
    Let $GEXXX957_INCLUDE_ACTION_CRITERIA = '(' || $GEXXX957_ACTION_Alias || ' In ('''     || &GEX_RC_ACTION.ACTION || ''''
    Let $GEXXX957_EXCLUDE_ACTION_CRITERIA = '(' || $GEXXX957_ACTION_Alias || ' Not In (''' || &GEX_RC_ACTION.ACTION || ''''
  Else
    Let $GEXXX957_INCLUDE_ACTION_CRITERIA = $GEXXX957_INCLUDE_ACTION_CRITERIA || ',''' || &GEX_RC_ACTION.ACTION  || ''''
    Let $GEXXX957_EXCLUDE_ACTION_CRITERIA = $GEXXX957_EXCLUDE_ACTION_CRITERIA || ',''' || &GEX_RC_ACTION.ACTION  || ''''
  End-If

From  PS_GEX_RC_ACTION GEX_RC_ACTION

Where GEX_RC_ACTION.OPRID                = [$OprID]
And   GEX_RC_ACTION.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_ACTION.ACTION, ' ') != ' '

End-Select

  If #GEX_RC_ACTION_ROWS = 0
    Let $GEXXX957_INCLUDE_ACTION_CRITERIA = $GEXXX957_ACTION_Alias || ' = ' || $GEXXX957_ACTION_Alias
    Let $GEXXX957_EXCLUDE_ACTION_CRITERIA = $GEXXX957_ACTION_Alias || ' = ' || $GEXXX957_ACTION_Alias
  Else
    Let $GEXXX957_INCLUDE_ACTION_CRITERIA = $GEXXX957_INCLUDE_ACTION_CRITERIA || '))'
    Let $GEXXX957_EXCLUDE_ACTION_CRITERIA = $GEXXX957_EXCLUDE_ACTION_CRITERIA || '))'
    Let $GEXXX957_INCLUDE_ACTION_DISPLAY_CRITERIA = $GEXXX957_INCLUDE_ACTION_CRITERIA
    Let $GEXXX957_EXCLUDE_ACTION_DISPLAY_CRITERIA = $GEXXX957_EXCLUDE_ACTION_CRITERIA
  End-If

End-Procedure
