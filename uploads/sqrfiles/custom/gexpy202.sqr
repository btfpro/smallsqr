!***********************************************************************
!  GEXPY202:  Part-Time Associates with More than 32 Hours             *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *        
! GEXPY202:         Report listing part-time EE's whose total hours    *
!                   worked > 32 hours.    
!                                                                      *        
! Narrative:        This program will generate a report listing        *
!                   part-time EE's whose total hours worked > 32.      *
!                   Regular and overtime hours are summed for total    *   
!                   hours worked.  Rpt is used corporate wide with     *
!                   an input of pay end date and will be distributed   * 
!                   to the region mgrs.  Report lists Company, paygroup*
!                   Pay end date, Region code, Region Mgr, Emplid,     *
!                   Name, Deptid, Acct_Cd, Hire_Dt, Reg Hrs, OT hrs,   * 
!                   Reg+OT hrs, Holiday hrs (info purposes only)       *
!                   TOtals by Acct_Cd, Department and Report Totals.   *   
!                                                                      *
! #Debugx Used:     #debug9 paragraph trace                            *
!	            #debug8 key variable values                        *
!	            #debug7 data returned from sql calls               * 
!                                                                      *  
! SQL Tables:	    GEX_DEPT_TBL                                       *
!                   EARNINGS_BAL                                       *
!                   PAY_OTH_EARNS                                      *
!                   PAY_CAL_BAL_ID                                     *
!                   ACCT_CD_TBL                                        *
!                   PERSONAL_DATA                                      *
!                   EMPLOYMENT                                         *
!                   JOB                                                *
!                                                                      *
! Written by:	    Susan Mahady   10/20/2000                          *       
!                                                                      *
! Normally Run:	    Weekly                                             *
!                                                                      *
! Control Parms:    FOR WHERE CLAUSE:                                  *
!				  Company Division Code                *
!				  Company  	                       *
!				  Paygroup  	                       *
!				  Deptid	                       *
!				  Employee Status	               *
!				  Account Code                         *
!				  As Of Date                           *                                                                   *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS   DATE         DESCRIPTION OF THE CHANGE                   *
!***********************************************************************
!    SCM      10/20/2000   Initial Creation                            *
!    SCM      03/01/2001   Added JobCode scrollbar to panel and code to*
!                          select specific part-time jobcodes.         *
!  RBC       09/26/2007     Modified to use GEX_ACCT_CD_TBL            *
!			    in 9.0 Upgrade.		
!GEX_SEC_REENG 09/16/2016 Vahini Katta 
!Changes related to Emplid conversion
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


begin-report
#debug9 Show 'begin-report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P000-Init-Report
  do P100-Process-Main
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc
  date-time () HH:MI:SS   &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
  Show 'Successful end of report'
end-report


begin-heading 8
#debug9 Show 'begin-heading'

  #Include 'stdhdg02.sqc'

  Let $X000_temp_out = 'Sorted by ' || $X000_ORDER_BY
  Print $X000_temp_out         (3,50)  
  
  Let $X000_temp_out = 'Pay Week Ending ' || $pay_end_dt
  Print $X000_temp_out         (4,65)  
   
   if $PayGroup <> $priorPayGroup
       do Get-PayGroup-Data
       move $PayGroup to $priorPayGroup
   end-if
   print 'Pay Group: '          (4,1)
   print $PayGroup              (0,13)
   print $PayGroupName          (0,17)

  print 'Pay End Dt:'          (5,1)
  print $pay_end_heading       (5,+1)

   print 'Region Code'          (6,1)
   print $Rgn_Cd                (0,14)
   print 'Region Manager'       (0,22)
   print $Rmgr_Lst_Nme          (0,39)

  PRINT 'EMPLOYEE NAME'        (+2,1)
  PRINT 'DEPARTMENT'           (,39)
  PRINT 'ACCT_CD'              (,57)
  !GEX_SEC_REENG 09/16/2016 Vahini Katta Begins
  !PRINT 'SSN#'                 (,75)
  PRINT 'EMPLID'               (,75)
  !GEX_SEC_REENG 09/16/2016 Vahini Katta Ends
  PRINT 'HIRE DATE'            (,94)
  PRINT 'REG HRS'              (,112)
  PRINT 'OVERTIME HRS'         (,127)
  PRINT 'TOTAL REG+OT'         (,147)
  PRINT 'HOLIDAY HRS'          (,165)
end-heading


begin-procedure P000-Init-Report
#debug9 Show 'P000-Init-Report'

  move 'GEXPY202'            to $ReportID
  move '                 PART-TIME ASSOCIATES WORKING > 32 HOURS WEEKLY' to $ReportTitle

  do Stdapi-Init

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  Let $GEXXX900_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
  Let $GEXXX902_Deptid_Alias   = 'J.DEPTID'
  Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'
  Let $GEXXX907_Div_CD_Alias   = 'GDT.GEX_COMPANY_DIV_CD'
  Let $GEXXX909_Acct_CD_Alias  = 'J.ACCT_CD'
  Let $GEXXX916_Jobcode_Alias  = 'J.JOBCODE'
  Let $GEXXX924_ERNCD_Alias    = 'POE1.ERNCD'           !Regular earnings codes
  Let $GEXXX955_ERNCD_Alias    = 'POE.ERNCD'            !Overtime earnings codes            
  Let $GEXXX956_ERNCD_Alias    = 'POE2.ERNCD'           !Holiday earnings codes

  if $prcs_process_instance = ''
    Do GEXXX950-Input-Parameters
  else
    Do GEXXX922-Select-Parameters

    Do GEXXX900-Select-Company-Parameters
    Do GEXXX901-Select-Company-Paygroup-Parameters
    Do GEXXX902-Select-Deptid-Parameters
    Do GEXXX903-Select-Emp-Stat-Parameters
    Do GEXXX907-Select-Div-CD-Parameters 
    Do GEXXX909-Select-Acct-CD-Parameters
    Do GEXXX916-Select-Jobcode-Parameters               !SCM 03/01/2001
    Do GEXXX924-Select-ERNCD-Parameters
    Do GEXXX955-Select-ERNCD-Parameters
    Do GEXXX956-Select-ERNCD-Parameters

    Let $X000_WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate
    If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
      Let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday
    End-If
  end-if

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Let $X000_ORDER_BY = 'Company, Paygroup, Region Code, Department, Account Code, Name'

  Show '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA
  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA
  Show '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA
  Show '$GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA          ='   $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA   !SCM 03/01/2001
  Show '$GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA-Regular    ='   $GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA
  Show '$GEXXX955_INCLUDE_ERNCD_DISPLAY_CRITERIA-OverTime   ='   $GEXXX955_INCLUDE_ERNCD_DISPLAY_CRITERIA
  Show '$GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA-Holiday    ='   $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA
  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE

  do P020-Get-Last-Confirmed-Pay
  Do Format-DateTime($pay_end_dt, $pay_end_heading, {DEFDATE}, '', '')
  do P050-Print-Cover-Page

!  Do Get-Calendar-Year-Id    !getbalid.sqc
end-procedure


Begin-Procedure P020-Get-Last-Confirmed-Pay
#debug9 Show 'P020-Get-Last-Confirmed-Pay'

Begin-Select Loops=1
pay_end_dt

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt


  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  else
      Show 'Processing for Pay End Dt: ' $pay_end_dt
  End-If

From   ps_pay_calendar

Where  pay_end_dt         =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt <= $X000_WHERE_SELECT_ASOFDATE 
       And    pay_confirm_run = 'Y')

And    pay_confirm_run = 'Y'
End-Select

End-Procedure


begin-procedure P050-Print-Cover-Page
#debug9 Show 'P050-Print-Cover-Page'

  If $prcs_process_instance <> ''
    Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'      (+5,1)
    Print '$Prcs_OPRID          ='                            (+2,5)
    Print $Prcs_OPRID                                         (0,+2)
    Print '$PRCS_RUN_CNTL_ID    ='                            (+1,5)
    Print $PRCS_RUN_CNTL_ID                                   (0,+2)
  End-If

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)
  Print '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   (+2,5)
  Print $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA                 (0,+2)
  Print '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,5)
  Print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   (+1,5)
  Print $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA	            (0,+2)
  Print '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   (+1,5)
  Print $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA               (0,+2)
  Print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  Print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA          ='   (+1,5)       !SCM 03/01/2001
  Print $GEXXX916_INCLUDE_JOBCODE_DISPLAY_CRITERIA                (0,+2)       !SCM 03/01/2001
  Print '$GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA-Regular    ='   (+1,5)
  Print $GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX955_INCLUDE_ERNCD_DISPLAY_CRITERIA-Overtime   ='   (+1,5)
  Print $GEXXX955_INCLUDE_ERNCD_DISPLAY_CRITERIA                (0,+2)
  Print '$GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA-Holiday    ='   (+1,5)
  Print $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA                (0,+2)

  Print 'As Of Date :'                                        (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                         (0,+2)
  Print 'Last confirmed pay end date:  '                      (+2,5)
  Print $pay_end_heading                                      (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
  Let #PAGE-COUNT = 1
End-Procedure


begin-procedure P100-Process-Main
#debug9 Show 'P100-Process-Main'

  Let $Data-Found-Flag = 'N'

begin-SELECT
!/*+ RULE */ 
J.COMPANY               ()         ON-BREAK    LEVEL=1
                                               PRINT=NEVER

J.PAYGROUP              ()         ON-BREAK    LEVEL=2
                                               PRINT=NEVER

GDT.GEX_RGN_CD          ()         ON-BREAK    LEVEL=3
                                               PRINT=NEVER

J.DEPTID                ()         ON-BREAK    LEVEL=4
                                               PRINT=NEVER
                                               SAVE=$previous_deptid
                                               AFTER=P600-Deptid-Change

ACT.DESCRSHORT          ()         ON-BREAK    LEVEL=5
                                               PRINT=NEVER
                                               SAVE=$previous_acct
                                               AFTER=P500-Acct-Change
!GDT.GEX_RGN_CD
GDT.GEX_RMGR_LST_NME
PD.NAME
J.EMPLID
J.EMPL_RCD
J.JOBCODE
!J.PAYGROUP
E.HIRE_DT

  Let $Data-Found-Flag = 'Y'

  let $paygroup = &J.PAYGROUP
  Let $Rgn_Cd = &GDT.GEX_RGN_CD
  Let $Rmgr_Lst_Nme = &GDT.GEX_RMGR_LST_NME
  Let $Company  = &j.company

  let #hrs_OT = 0
  let #hrs_reg = 0
  let #hrs_hol = 0
  do P200-Get-OT-Hours
  do P210-Get-REG-Hours
  do P212-Get-HOL-Hours
  let #tl_workhours = 0
  let #tl_workhours = #hrs_OT + #hrs_reg
  If #tl_workhours > 32                             !if reg hours plus OT hours > 32, print
  	do P400-Create-Detail
  end-if
!GEX-RBC Modified for Upgrade 9.0 on 09/26/2007 - Begin	
From   !PS_ACCT_CD_TBL     ACT,
       PS_GEX_ACCT_CD_TBL  ACT,  
!GEX-RBC Modified for Upgrade 9.0 on 09/26/2007 - END
       ps_gex_dept_tbl    gdt,
       PS_PERSONAL_DATA   PD,
       PS_EMPLOYMENT      E,
       PS_JOB             J

WHERE J.FULL_PART_TIME = 'P'                       !part-time ee's
AND   [$GEXXX916_INCLUDE_JOBCODE_CRITERIA]         !only specific part-time jobcodes- SCM 03/01/2001
AND    ACT.ACCT_CD         = J.ACCT_CD

! CHANDRA / FCIL / 08/17/2007 
! GEXPY202.sqr Giant Eagle Modification
! procedure P100-Process-Main uses PS_ACCT_CD_TBL in the select and where clause. 
! EFFDT is used in the where clause from PS_ACCT_CD_TBL and this field has been deleted 
! in peoplesoft 9.0 environment. Need to modify the where clause to remove the EFFDT logic 
! for PS_ACCT_CD_TBL.
!GEX-RBC Modified for Upgrade 9.0 on 09/26/2007 - Begin	
AND    ACT.EFFDT           =
     (SELECT MAX(EFFDT)
      FROM   PS_GEX_ACCT_CD_TBL
      WHERE  ACT.ACCT_CD  = ACCT_CD)

!GEX-RBC Modified for Upgrade 9.0 on 09/26/2007 - END
AND    PD.EMPLID           = J.EMPLID
AND    E.EMPLID            = J.EMPLID
AND    E.EMPL_RCD         = J.EMPL_RCD
AND    J.EFFDT             =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID       = J.EMPLID
       AND    EMPL_RCD    = J.EMPL_RCD
       AND    EFFDT       <= $pay_end_dt)
AND    J.EFFSEQ            =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB
       WHERE  EMPLID       = J.EMPLID
       AND    EMPL_RCD    = J.EMPL_RCD
       AND    EFFDT        = J.EFFDT)
And    j.deptid            = gdt.deptid
And    gdt.effdt           =
      (Select Max(effdt)
       From   ps_gex_dept_tbl
       Where  deptid       = gdt.deptid
       And    effdt       <= $pay_end_dt)
And    gdt.sequence_number =
      (Select Max(sequence_number)
       From   ps_gex_dept_tbl
       Where  deptid       = gdt.deptid
       And    effdt        = gdt.effdt)

AND   [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
AND   [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
AND   [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
AND   [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
AND   [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
AND   [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]

ORDER BY J.COMPANY,
       J.PAYGROUP,
       GDT.GEX_RGN_CD,
       J.DEPTID,
       ACT.DESCRSHORT,
       PD.NAME
end-SELECT

  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  Else
    Do P700-Print-Report-Totals
  End-If
end-procedure

begin-procedure P200-Get-OT-Hours
#debug9 Show 'P200-Get-OT-Hours'
begin-SELECT
!/*+ RULE */
SUM(POE.OTH_HRS) &hrs_OT

  let #hrs_OT = &hrs_OT

FROM   PS_PAY_EARNINGS    PE,
       PS_PAY_OTH_EARNS   POE

WHERE  PE.COMPANY         = &J.COMPANY
AND    PE.PAYGROUP        = &J.PAYGROUP
AND    PE.PAY_END_DT      = $pay_end_dt
AND    PE.EMPLID          = &J.EMPLID
AND    PE.EMPL_RCD       = &J.EMPL_RCD
AND    PE.OFF_CYCLE       = 'N'                !off-cycle only

AND    POE.COMPANY        = PE.COMPANY
AND    POE.PAYGROUP       = PE.PAYGROUP
AND    POE.PAY_END_DT     = PE.PAY_END_DT
AND    POE.OFF_CYCLE      = PE.OFF_CYCLE
AND    POE.PAGE_NUM          = PE.PAGE_NUM
AND    POE.LINE_NUM          = PE.LINE_NUM
AND    POE.ADDL_NBR          = PE.ADDL_NBR
AND   [$GEXXX955_INCLUDE_ERNCD_CRITERIA]
end-SELECT
end-procedure


begin-procedure P210-Get-REG-Hours
#debug9 Show 'P210-Get-REG-Hours'
begin-SELECT
!/*+ RULE */ 
SUM(POE1.OTH_HRS) &hrs_reg

  let #hrs_reg = &hrs_reg

FROM   PS_PAY_EARNINGS    PE1,
       PS_PAY_OTH_EARNS   POE1

WHERE  PE1.COMPANY         = &J.COMPANY
AND    PE1.PAYGROUP        = &J.PAYGROUP
AND    PE1.PAY_END_DT      = $pay_end_dt
AND    PE1.EMPLID          = &J.EMPLID
AND    PE1.EMPL_RCD       = &J.EMPL_RCD
AND    PE1.OFF_CYCLE       = 'N'                !off-cycle only

AND    POE1.COMPANY        = PE1.COMPANY
AND    POE1.PAYGROUP       = PE1.PAYGROUP
AND    POE1.PAY_END_DT     = PE1.PAY_END_DT
AND    POE1.OFF_CYCLE      = PE1.OFF_CYCLE
AND    POE1.PAGE_NUM          = PE1.PAGE_NUM
AND    POE1.LINE_NUM          = PE1.LINE_NUM
AND    POE1.ADDL_NBR          = PE1.ADDL_NBR
AND  [$GEXXX924_INCLUDE_ERNCD_CRITERIA]
                                 
end-SELECT
end-procedure

begin-procedure P212-Get-HOL-Hours
#debug9 Show 'P212-Get-HOL-Hours'
begin-SELECT
!/*+ RULE */
SUM(POE2.OTH_HRS) &hrs_hol

  let #hrs_hol = &hrs_hol

FROM   PS_PAY_EARNINGS    PE2,
       PS_PAY_OTH_EARNS   POE2

WHERE  PE2.COMPANY         = &J.COMPANY
AND    PE2.PAYGROUP        = &J.PAYGROUP
AND    PE2.PAY_END_DT      = $pay_end_dt
AND    PE2.EMPLID          = &J.EMPLID
AND    PE2.EMPL_RCD       = &J.EMPL_RCD
AND    PE2.OFF_CYCLE       = 'N'                !off-cycle only
AND    POE2.COMPANY        = PE2.COMPANY
AND    POE2.PAYGROUP       = PE2.PAYGROUP
AND    POE2.PAY_END_DT     = PE2.PAY_END_DT
AND    POE2.OFF_CYCLE      = PE2.OFF_CYCLE
AND    POE2.PAGE_NUM          = PE2.PAGE_NUM
AND    POE2.LINE_NUM          = PE2.LINE_NUM
AND    POE2.ADDL_NBR          = PE2.ADDL_NBR
AND    [$GEXXX956_INCLUDE_ERNCD_CRITERIA]

end-SELECT
end-procedure

begin-procedure P400-Create-Detail
#debug9 Show 'P400-Create-Detail'

  add 1 to #acct_count
  add 1 to #deptid_count

  add #hrs_reg to #total_hrs_reg
  add #hrs_reg to #deptid_hrs_reg
  add #hrs_reg to #acct_hrs_reg

  add #hrs_OT to #total_hrs_OT
  add #hrs_OT to #deptid_hrs_OT
  add #hrs_OT to #acct_hrs_OT

  add #tl_workhours to #total_hrs_regOT
  add #tl_workhours to #deptid_hrs_regOT
  add #tl_workhours to #acct_hrs_regOT

  add #hrs_hol to #total_hrs_hol
  add #hrs_hol to #deptid_hrs_hol
  add #hrs_hol to #acct_hrs_hol

 
  print &PD.NAME                    (+1,1)
  print &J.DEPTID                   (,39)
  print &ACT.DESCRSHORT             (,57)
  print &J.EMPLID                   (,75)   !edit xxx-xx-xxxx !GEX_SEC_REENG 09/16/2016 Vahini Katta
!  print &J.JOBCODE                  (,94)
  do Format-DateTime(&E.HIRE_DT,$HireDate,{DEFMDY},'','')
  print $HireDate                   (,94)
  print #hrs_reg                    (,110)  edit 999,999.99
  print #hrs_OT                     (,129)  edit 999,999.99
  print #tl_workhours               (,147)  edit 999,999.99
  print #hrs_Hol                    (,165)  edit 999,999.99

  let $save_rgn_cd = $Rgn_Cd
  let $save_rgn_mgr = $Rmgr_Lst_Nme

end-procedure


begin-procedure P500-Acct-Change
#debug9 Show 'P500-Acct-Change'

  if #acct_count > 0
    print '----------'              (+1,110)
    print '----------'              (,129)
    print '----------'              (,147)
    print '----------'              (,165)
    print 'TOTALS FOR'              (+1,75)
    print $previous_acct            (,+1)
    print #acct_hrs_reg             (,108)    edit 9,999,999.99
    print #acct_hrs_OT              (,127)    edit 9,999,999.99
    print #acct_hrs_regOT           (,145)    edit 9,999,999.99
    print #acct_hrs_hol             (,163)    edit 9,999,999.99
    print ''                        (+1)
    move 0 to #acct_hrs_reg
    move 0 to #acct_hrs_OT
    move 0 to #acct_hrs_regOT
    move 0 to #acct_hrs_hol
    move 0 to #acct_count
  end-if
end-procedure


begin-procedure P600-Deptid-Change
#debug9 Show 'P600-Deptid-Change'
  

  if #deptid_count > 0
    print '----------'              (+1,110)
    print '----------'              (,129)
    print '----------'              (,147)
    print '----------'              (,165)
    print 'TOTALS FOR DEPARTMENT'   (+1,75)
    print $previous_deptid          (,+1)
    print #deptid_hrs_reg           (,108)    edit 9,999,999.99
    print #deptid_hrs_OT            (,127)    edit 9,999,999.99
    print #acct_hrs_regOT           (,145)    edit 9,999,999.99
    print #deptid_hrs_hol           (,163)    edit 9,999,999.99
    move 0 to #deptid_hrs_reg
    move 0 to #deptid_hrs_OT
    move 0 to #deptid_hrs_regOT
    move 0 to #deptid_hrs_hol
    move 0 to #deptid_count
    new-page
  end-if
end-procedure


begin-procedure P700-Print-Report-Totals
#debug9 Show 'P700-Print-Report-Totals'

  print '----------'                (+1,110)
  print '----------'                (,129)
  print '----------'                (,147)
  print '----------'                (,165)
  print 'TOTALS FOR REPORT'         (+1,75)
  print #total_hrs_reg              (,108)    edit 9,999,999.99
  print #total_hrs_OT               (,127)    edit 9,999,999.99
  print #total_hrs_regOT            (,145)    edit 9,999,999.99
  print #total_hrs_hol              (,163)    edit 9,999,999.99
end-procedure


#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx904.sqc'  !Get grade multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx908.sqc'  !Get sal_admin_plan multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx916.sqc'  !Get jobcode multiple row table              !SCM 03/01/2001
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx924.sqc'  !Get erncd multiple row table (for reg earnings codes)
#include 'gexxx955.sqc'  !Get erncd multiple row table (for overtime earnings codes)
#include 'gexxx956.sqc'  !Get erncd multiple row table (for holiday earnings codes)
#include 'gexxx950.sqc'  !Get SQRW run controls

#include 'askaod.sqc'    !Ask As Of Date input
#include 'getcodta.sqc'  !Get company data
#include 'getpgdta.sqc'  !Get paygroup data
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
