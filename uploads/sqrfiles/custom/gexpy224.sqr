!***********************************************************************
! GEXPY224:  COD - Local 880 Holiday(Youngstown) Pay Process / Report  *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY224 :            COD - Local 880 Holiday(Youngstown)            *
!                        Pay Process / Report                          *
!                                                                      *
! Narrative:            This SQR is designed to update paysheets for   *
!                       Local 880(Youngstown) EEs to generate the      *
!                       correct holiday pay.  The program will         *
!                       determine how many hours to pay for holiday    *
!                       based on the hours worked over the last week.  *
!                                                                      *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_earnings                                   *
!                       pay_oth_earns                                  *
!                       job                                            *
!                       job_labor                                      *
!                                                                      *
! Written by:           R.Kepner                                       *
!                                                                      *
! Normally Run:         Weekly - prior to final calc                   *
!                                                                      *
!                                                                      *
! Control Parms:        NONE                                           *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE       DESCRIPTION OF THE CHANGE                       *
!***********************************************************************
!***********************************************************************
! RKK       05/15/2010            Creation, copied GEXPY203.           *                                                             
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#include 'setup32.sqc'

  #define #x000_col_selection_criteria_a   2
  #define #x000_col_selection_criteria_b   5

  #define #col_emplid			  1
  #define #col_name			    13
  #define #col_deptid			  40
  #define #col_vac_paid			52
  #define #col_502          70            
  #define #col_erncd        100            
  #define #col_hol_paid			110

begin-heading 8
#debug9 show 'begin-heading  for-reports=(Holiday-Hours)'
  move 'Local 880(Youngstown) Holiday Pay' to $ReportTitle
  #Include 'stdhdg01.sqc'
!         1         2         3         4         5         6         7         8         9         0         1         2         3         4         5         6         7
!12345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
!Emplid      Name                       Deptid      Total Hours Paid  Holiday Hours Submitted       ErnCd     Holiday Hours Paid
!--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------  

  Print 'Emplid'               (5,{#col_emplid})
  Print 'Name'                 (,{#col_name})
  Print 'Deptid'               (,{#col_deptid})
  Print 'Total Hours Paid'     (,{#col_vac_paid})                
  Print 'Holiday Hrs Paid'     (,{#col_hol_paid})               
  Print 'Holiday Hours Submitted'     (,{#col_502})               
  Print 'Erncd'                (,{#col_erncd})                   
  Print '-'                    (7,1,176) fill  

end-heading


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPY224'
  Let $ReportTitle   =   'COD - Local 880(Youngstown) Holiday Pay Process'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '


  Let $GEXXX901_Company_Alias  = 'A.COMPANY' 
  Let $GEXXX901_Paygroup_Alias = 'A.PAYGROUP'    
  Let $GEXXX913_Union_cd_Alias = 'D.UNION_CD'
  Let $GEXXX924_Erncd_Alias    = 'O.ERNCD'
  Let $GEXXX956_ERNCD_Alias    = 'B.ERNCD'           !Holiday earnings codes
  Let $GEXXX956B1_ERNCD_Alias   = 'B1.ERNCD'         !used for Holiday earnings codes to be excluded.


  If $prcs_process_instance = ''

    let $Prcs_OPRID = 'SQRW/pssqr'
    let $PRCS_RUN_CNTL_ID = 'manual'

  Display 'Enter Company/Paygroup or leave blank to exit.'
  While 1=1
    Input $In-Company Maxlen=3 'Company'
    Uppercase $In-Company
    If Rtrim($In-Company, ' ') = ''
      Break
    Else
      Input $In-Paygroup Maxlen=3 'Paygroup'
      Uppercase $In-Paygroup
      If Rtrim($In-Paygroup, ' ') = ''
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''')'
      Else
        Let $Include_Condition = '(' || $GEXXX901_Company_Alias || ' = '''  || $In-Company || ''' And ' || $GEXXX901_Paygroup_Alias || ' = '''  || $In-Paygroup || ''')'
      End-If
      Add 1 To #GEX_RC_PAYGROUP_ROWS
      If #GEX_RC_PAYGROUP_ROWS = 1
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While 

 If #GEX_RC_PAYGROUP_ROWS = 0
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '1=1'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If


 Display 'Enter Union Code or leave blank to exit.'
  While 1=1
    Input $In-Union-Cd Maxlen=3 'Union Code'
    Uppercase $In-Union-Cd
    If Rtrim($In-Union-Cd, ' ') = ''
      Break
    Else
        Let $Include_Condition = '(' || $GEXXX913_Union_cd_Alias || ' = '''  || $In-Union-cd || ''')'
      Add 1 To #GEX_RC_UNION_CD_ROWS
      If #GEX_RC_UNION_CD_ROWS = 1
        Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While 

 If #GEX_RC_UNION_CD_ROWS = 0
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = '1=1'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = 'ALL'
  Else
    Let $GEXXX913_INCLUDE_UNION_CD_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA || ')'
    Let $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA = $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  End-If


  Display 'Enter Earnings Codes(s) or leave blank to exit.'
  While 1=1
    Input $In-Erncd Maxlen=3 'Earnings Code'
    Uppercase $In-Erncd
    If Rtrim($In-Erncd, ' ') = ''
      Break
    Else
        Let $Include_Condition = '(' || $GEXXX924_Erncd_Alias || ' = '''  || $In-Erncd || ''')'
        Add 1 To #GEX_RC_ERNCD_ROWS
      If #GEX_RC_ERNCD_ROWS = 1
        Let $GEXXX924_INCLUDE_ERNCD_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX924_INCLUDE_ERNCD_CRITERIA = $GEXXX924_INCLUDE_ERNCD_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While 

   If #GEX_RC_ERNCD_ROWS = 0
      Let $GEXXX924_INCLUDE_ERNCD_CRITERIA = '1=1'
      Let $GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA = 'ALL'
   Else
      Let $GEXXX924_INCLUDE_ERNCD_CRITERIA = $GEXXX924_INCLUDE_ERNCD_CRITERIA || ')'
      Let $GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA = $GEXXX924_INCLUDE_ERNCD_CRITERIA
   End-If

Display 'Enter Holiday Earnings Codes(s) or leave blank to exit.'
  While 1=1
    Input $In-Erncd-Hl Maxlen=3 'Holiday Earnings Code'
    Uppercase $In-Erncd-Hl
    If Rtrim($In-Erncd-Hl, ' ') = ''
      Break
    Else
        Let $Include_Condition = '(' || $GEXXX956_Erncd_Alias || ' = '''  || $In-Erncd-Hl || ''')'
      Add 1 To #GEX_RC_ERNCD_HL_ROWS
      If #GEX_RC_ERNCD_HL_ROWS = 1
        Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = '(' || $Include_Condition
      Else
        Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = $GEXXX956_INCLUDE_ERNCD_CRITERIA || ' Or '  || $Include_Condition
      End-If
    End-If
  End-While 

   If #GEX_RC_ERNCD_HL_ROWS = 0
      Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = '1=1'
      Let $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA = 'ALL'
   Else
      Let $GEXXX956_INCLUDE_ERNCD_CRITERIA = $GEXXX956_INCLUDE_ERNCD_CRITERIA || ')'
      Let $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA = $GEXXX956_INCLUDE_ERNCD_CRITERIA
   End-If

    Input $In-partfull-req Maxlen=1 'Part time (P) or Full Time (F)'          
    let $GEXXX922_INCLUDE_FULL_PART_TIME_CRITERIA = $In-partfull-req          

  Else
   Do GEXXX922-Select-Parameters
   Do GEXXX901-Select-Company-Paygroup-Parameters    
   Do GEXXX913-Select-UNION-CD-Parameters
   Do GEXXX924-Select-ERNCD-Parameters
   Do GEXXX956-Select-ERNCD-Parameters            
   let $GEXXX956_INCLUDE_ERNCD_CRITERIA_Save = $GEXXX956_INCLUDE_ERNCD_CRITERIA 
   let $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA_Save = $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA   
   Let $GEXXX956_ERNCD_Alias    = 'B1.ERNCD'          
   Do GEXXX956-Select-ERNCD-Parameters               

   let $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA = $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA_Save 
   let $GEXXX956_INCLUDE_ERNCD_CRITERIA = $GEXXX956_INCLUDE_ERNCD_CRITERIA_Save                

   let $GEXXX922_INCLUDE_FULL_PART_TIME_CRITERIA = $GEX_RC_PAY.FULL_PART_TIME    
   
   
  End-If
 
  let $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA     = $GEXXX956_INCLUDE_ERNCD_CRITERIA
  
  let $X000_ORDER_BY = 'DEPTID, EMPLID'

  show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA      = ' $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA   
  show '$GEXXX913_INCLUDE_UNION_CD_CRITERIA              = ' $GEXXX913_INCLUDE_UNION_CD_CRITERIA
  show '$GEXXX924_INCLUDE_ERNCD_CRITERIA                 = ' $GEXXX924_INCLUDE_ERNCD_CRITERIA
  show '$GEXXX956_INCLUDE_ERNCD_CRITERIA-Holiday         = ' $GEXXX956_INCLUDE_ERNCD_CRITERIA 
  Show '$GEXXX922_INCLUDE_FULL_PART_TIME_CRITERIA        = ' $GEXXX922_INCLUDE_FULL_PART_TIME_CRITERIA   

  print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,{#x000_col_selection_criteria_a})
  print '$Prcs_OPRID          ='                                  (+1,{#x000_col_selection_criteria_b})
  print $Prcs_OPRID                                               (0,+2)
  print '$PRCS_RUN_CNTL_ID    ='                                  (+1,{#x000_col_selection_criteria_b})
  print $PRCS_RUN_CNTL_ID                                         (0,+2)

  print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,{#x000_col_selection_criteria_a})
  print '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA       (0,+2)
  print '$GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA         ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX913_INCLUDE_UNION_CD_DISPLAY_CRITERIA	          (0,+2)
  print '$GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA            ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX924_INCLUDE_ERNCD_DISPLAY_CRITERIA                  (0,+2)
 
  print '$GEXXX922_INCLUDE_FULL_PART_TIME_CRITERIA           ='   (+1,{#x000_col_selection_criteria_b})
  print $GEXXX922_INCLUDE_FULL_PART_TIME_CRITERIA                 (0,+2)

  Print '$GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA-Holiday    ='   (+1,{#x000_col_selection_criteria_b})
  Print $GEXXX956_INCLUDE_ERNCD_DISPLAY_CRITERIA                (0,+2)

  print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,{#x000_col_selection_criteria_a})
  print '$X000_ORDER_BY                                      ='   (+1,{#x000_col_selection_criteria_b})
  print $X000_ORDER_BY 	(0,+2)
  
  let #page-count = 0
  NEW-PAGE

End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'

 do get-pay-end

Begin-SELECT On-Error=P290-SQL-Error('P200-main-process')
B.COMPANY
B.PAYGROUP
B.PAY_END_DT
B.OFF_CYCLE
B.PAGE_NUM
B.LINE_NUM
B.ADDL_NBR
A.DEPTID
A.EMPLID
B.ERNCD
B.OTH_HRS
B.OTH_EARNS
B.SEPCHK
B.JOB_PAY
B.ADD_GROSS
B.TAX_METHOD
B.ADDL_SEQ
B.TL_SOURCE
B.RATE_USED
B.BAS_CREDIT_SW
B.COMP_RATECD
B.COMPRATE
B.COMPRATE_USED
B.HRS_DIST_SW
D.FULL_PART_TIME &full_part
PER.NAME &name
D.UNION_CD
A.EARNS_END_DT

  let $full_part = &full_part
  let $name = &name
  let $erncd = &B.ERNCD                           
  let $UNION_CD = &D.UNION_CD
  
  print &A.EMPLID (+1,{#col_emplid})
  print $name (,{#col_name})
  print &A.DEPTID (,{#col_deptid})
  
  move &B.OTH_HRS to #weeks_requested
  #debug9 Show '&B.OTH_HRS: '&B.OTH_HRS
  #debug9 Show '#weeks_requested: '#weeks_requested
  
  If &B.OTH_HRS > 0
    if &A.EARNS_END_DT <> &B.PAY_END_DT
      do get-hours-worked-by-EED
    else
      do get-hours-worked
    end-if
  Else
!    let #weeks_retrieved = 0
!    let #hol_hrs = 0
!    let #hol_hrs_502 = 0
!    let #hol_hrs_505 = 0
!    let #hol_hrs_514 = 0
!    let #hol_hrs_521 = 0
!    let #total_hrs = 0
!    Let #avg_hrs = 0	 	 

    evaluate $ERNCD
     when = '502'
       let #hol_hrs_502 = #hol_hrs_502 + &B.OTH_HRS
       break
     when = '505'
       let #hol_hrs_505 = #hol_hrs_505 + &B.OTH_HRS
       break
     when = '514'
       let #hol_hrs_514 = #hol_hrs_514 + &B.OTH_HRS
       break
     when = '521'
       let #hol_hrs_521 = #hol_hrs_521 + &B.OTH_HRS
       break
     when-other
       break
    end-evaluate

  end-if

  if $error = 'N'
    do update-pay-oth-earns
    do update-pay-earnings                   
  end-if

FROM PS_PAY_EARNINGS A,
     PS_PAY_OTH_EARNS B,
     PS_JOB D,
     PS_PERSONAL_DATA PER
WHERE A.COMPANY = B.COMPANY
  AND A.PAYGROUP = B.PAYGROUP
  AND A.PAY_END_DT = B.PAY_END_DT
  AND A.OFF_CYCLE = B.OFF_CYCLE
  AND A.PAGE_NUM = B.PAGE_NUM
  AND A.LINE_NUM = B.LINE_NUM
  AND A.ADDL_NBR = B.ADDL_NBR
  AND A.OFF_CYCLE = 'N'
  AND A.PAY_END_DT = $pay_end_dt
  AND A.EMPLID   = D.EMPLID		
  AND A.EMPL_RCD = D.EMPL_RCD		
  AND D.EMPLID = PER.EMPLID
  AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
  AND [$GEXXX956_INCLUDE_ERNCD_CRITERIA]               
  AND D.FULL_PART_TIME = $GEXXX922_INCLUDE_FULL_PART_TIME_CRITERIA    
  AND D.EFFDT = (select max(effdt) from ps_job
                 where emplid   = d.emplid
                   and empl_rcd = d.empl_rcd
                   and effdt   <= $pay_end_dt)
  AND D.EFFSEQ =(SELECT MAX(EFFSEQ) FROM PS_JOB 
                WHERE d.EMPLID   = EMPLID		
                  AND d.EMPL_RCD = EMPL_RCD	
                  AND d.EFFDT    = EFFDT)
  AND [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]
  order by A.DEPTID, A.EMPLID  
  
End-SELECT
End-Procedure

!*********************************************************************
begin-procedure get-hours-worked
!*********************************************************************
#debug9 Show 'get-hours-worked'
#debug9 Show 'get-hours-worked emplid : '&A.EMPLID

 let #weeks_retrieved = 0
 let #hol_hrs = 0
 let #total_hrs = 0
 Let #avg_hrs = 0	 	 
 
begin-SELECT On-Error=P290-SQL-Error('3')
P.COMPANY
P.PAYGROUP
P.PAY_END_DT () On-Break Print=Never Level=1 After=Prn-Weekly-Sum_Hrs 
O.ERNCD
O.OTH_HRS

 if &O.ERNCD = '502'
     let #hol_hrs = #hol_hrs + &O.OTH_HRS
 end-if
 
 if #weeks_retrieved > 0
    exit-select
 end-if

 if isnull(&O.OTH_HRS)	
   let #sum_hrs = #sum_hrs + 0
 else
   Let #Pos = 0
   Let #Pos = Instr($GEXXX924_INCLUDE_ERNCD_CRITERIA, &O.ERNCD, 1)
   if #Pos > 0
     let #sum_hrs = #sum_hrs + &O.OTH_HRS
   end-if   
 end-if			

FROM PS_PAY_EARNINGS P,
     PS_PAY_OTH_EARNS O
WHERE P.COMPANY = O.COMPANY (+)
  AND P.PAYGROUP = O.PAYGROUP (+)
  AND P.PAY_END_DT = O.PAY_END_DT (+)
  AND P.OFF_CYCLE = O.OFF_CYCLE (+)
  AND P.PAGE_NUM = O.PAGE_NUM (+)
  AND P.LINE_NUM = O.LINE_NUM (+)
  AND P.ADDL_NBR = O.ADDL_NBR (+)
  AND P.PAY_END_DT = P.EARNS_END_DT
  AND P.PAY_END_DT = $pay_end_dt
  AND P.EMPLID = &A.EMPLID
ORDER BY P.PAY_END_DT desc
end-SELECT

begin-SELECT On-Error=P290-SQL-Error('3')
O3.OTH_HRS
O3.ERNCD

 if &O3.ERNCD = '502'
     let #hol_hrs_502 = #hol_hrs_502 + &O3.OTH_HRS
 end-if

 if &O3.ERNCD = '505'
     let #hol_hrs_505 = #hol_hrs_505 + &O3.OTH_HRS
 end-if

 if &O3.ERNCD = '514'
     let #hol_hrs_514 = #hol_hrs_514 + &O3.OTH_HRS
 end-if

 if &O3.ERNCD = '521'
     let #hol_hrs_521 = #hol_hrs_521 + &O3.OTH_HRS
 end-if
 
FROM PS_PAY_EARNINGS P3,
     PS_PAY_OTH_EARNS O3
WHERE P3.COMPANY = O3.COMPANY (+)
  AND P3.PAYGROUP = O3.PAYGROUP (+)
  AND P3.PAY_END_DT = O3.PAY_END_DT (+)
  AND P3.OFF_CYCLE = O3.OFF_CYCLE (+)
  AND P3.PAGE_NUM = O3.PAGE_NUM (+)
  AND P3.LINE_NUM = O3.LINE_NUM (+)
  AND P3.ADDL_NBR = O3.ADDL_NBR (+)
  AND P3.PAY_END_DT = P3.EARNS_END_DT
  AND P3.PAY_END_DT = $pay_end_dt
  AND P3.EMPLID = &A.EMPLID
end-SELECT


 if #weeks_retrieved = 0 
    move 'Y' to $error
 else
    move 'N' to $error
 end-if

end-procedure
!*********************************************************************
begin-procedure get-hours-worked-by-EED
!*********************************************************************
#debug9 Show 'get-hours-worked-by-EED'
#debug9 Show 'get-hours-worked-by-EED emplid : '&A.EMPLID

 let #weeks_retrieved = 0
 let #hol_hrs = 0
 let #hol_hrs_502 = 0
 let #hol_hrs_505 = 0
 let #hol_hrs_514 = 0
 let #hol_hrs_521 = 0
 let #total_hrs = 0
 Let #avg_hrs = 0	 	 
 
begin-SELECT On-Error=P290-SQL-Error('3')
P1.COMPANY
P1.PAYGROUP
P1.PAY_END_DT () On-Break Print=Never Level=1 After=Prn-Weekly-Sum_Hrs 
O1.ERNCD
O1.OTH_HRS

 if #weeks_retrieved > 0
    exit-select
 end-if

 if isnull(&O1.OTH_HRS)	
   let #sum_hrs = #sum_hrs + 0
 else
   Let #Pos = 0
   Let #Pos = Instr($GEXXX924_INCLUDE_ERNCD_CRITERIA, &O1.ERNCD, 1)
   if #Pos > 0
     let #sum_hrs = #sum_hrs + &O1.OTH_HRS
   end-if   
 end-if			

FROM PS_PAY_EARNINGS P1,
     PS_PAY_OTH_EARNS O1
WHERE P1.COMPANY = O1.COMPANY (+)
  AND P1.PAYGROUP = O1.PAYGROUP (+)
  AND P1.PAY_END_DT = O1.PAY_END_DT (+)
  AND P1.OFF_CYCLE = O1.OFF_CYCLE (+)
  AND P1.PAGE_NUM = O1.PAGE_NUM (+)
  AND P1.LINE_NUM = O1.LINE_NUM (+)
  AND P1.ADDL_NBR = O1.ADDL_NBR (+)
  AND P1.PAY_END_DT = &A.EARNS_END_DT
  AND P1.EMPLID = &A.EMPLID
ORDER BY P1.PAY_END_DT desc
end-SELECT

begin-SELECT On-Error=P290-SQL-Error('3')
O2.OTH_HRS
O2.ERNCD

 if &O2.ERNCD = '502'
     let #hol_hrs_502 = #hol_hrs_502 + &O2.OTH_HRS
 end-if

 if &O2.ERNCD = '505'
     let #hol_hrs_505 = #hol_hrs_505 + &O2.OTH_HRS
 end-if
#debug9 Show 'get-hours-worked-by-EED: '#hol_hrs_505

 if &O2.ERNCD = '514'
     let #hol_hrs_514 = #hol_hrs_514 + &O2.OTH_HRS
 end-if

 if &O2.ERNCD = '521'
     let #hol_hrs_521 = #hol_hrs_521 + &O2.OTH_HRS
 end-if
 
FROM PS_PAY_EARNINGS P2,
     PS_PAY_OTH_EARNS O2
WHERE P2.COMPANY = O2.COMPANY (+)
  AND P2.PAYGROUP = O2.PAYGROUP (+)
  AND P2.PAY_END_DT = O2.PAY_END_DT (+)
  AND P2.OFF_CYCLE = O2.OFF_CYCLE (+)
  AND P2.PAGE_NUM = O2.PAGE_NUM (+)
  AND P2.LINE_NUM = O2.LINE_NUM (+)
  AND P2.ADDL_NBR = O2.ADDL_NBR (+)
  AND P2.PAY_END_DT = $pay_end_dt
  AND P2.EMPLID = &A.EMPLID
end-SELECT

 if #weeks_retrieved = 0 
    move 'Y' to $error
 else
    move 'N' to $error
 end-if

end-procedure

!*********************************************************************
!Print Weekly Sum Hours		! CXA   08/26/2005 
!*********************************************************************
begin-procedure Prn-Weekly-Sum_Hrs 
#debug9 Show 'Prn-Weekly-Sum_Hrs'

 let #weeks_retrieved = #weeks_retrieved + 1

 evaluate #weeks_retrieved
 when = 1
    break
 end-evaluate
     
 let #total_hrs = #total_hrs + #sum_hrs

 let #sum_hrs = 0

end-procedure

!*********************************************************************
!Gets the Pay End Date to process for
!*********************************************************************
begin-procedure Get-Pay-End 
#debug9 Show 'Get-Pay-End'

 let $Found = 'N'
begin-select loops=1 On-Error=P290-SQL-Error('get-pay-end')

Y1.PAY_END_DT 
Y1.pay_begin_dt 

  move 'Y' to $Found
  move &Y1.PAY_END_DT to $pay_end_dt
  move &Y1.PAY_BEGIN_DT TO $pay_begin_dt

FROM PS_PAY_CALENDAR Y1
WHERE Y1.PAY_SHEETS_RUN = 'Y'
  AND Y1.PAY_CONFIRM_RUN = 'N'
  AND Y1.PAY_END_DT = (SELECT MAX(PAY_END_DT)
                         FROM PS_PAY_CALENDAR
                        WHERE PAY_SHEETS_RUN = 'Y'
                          AND PAY_CONFIRM_RUN = 'N'                  
                          AND PAY_END_DT <= $AsOfToday
                          AND (COMPANY, PAYGROUP) in (
                        select distinct a.company, a.paygroup
                        from ps_pay_calendar a where [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]))
  AND Y1.RUN_ID > ' '

end-select

  if $Found = 'N'
    display 'Error --  Pay Calendar not found or not setup for the Run ID.'
    print 'Error --  Pay Calendar not found or not setup for the Run ID.' (+1,1,60)
  else
    display 'Processing for Pay End Date: ' noline
    display $pay_end_dt
  end-if
end-procedure

!***************************************************************************
Begin-Procedure update-pay-oth-earns
#debug9 Show 'Update-pay-oth-earns'
#debug9 Show 'total_hrs: '#total_hrs
#debug9 Show '&B.ERNCD: '&B.ERNCD
#debug9 Show '#hol_hrs_502: '#hol_hrs_502

 let #hrs_paid = (#total_hrs/1)

 Evaluate &B.ERNCD
   when = '502'
     print #hol_hrs_502 (,{#col_502})
     break
   when = '505'
     print #hol_hrs_505 (,{#col_502})
     break
   when = '514'
     print #hol_hrs_514 (,{#col_502})
     break
   when = '521'
     print #hol_hrs_521 (,{#col_502})
     break
   when-other
     break
 end-evaluate

 if #hol_hrs_502 < 0
  let #hours_earned = #hol_hrs_502
 else
   if #hol_hrs_505 < 0
    let #hours_earned = #hol_hrs_505
   else
     if #hol_hrs_514 < 0
      let #hours_earned = #hol_hrs_514
     else
       if #hol_hrs_521 < 0
        let #hours_earned = #hol_hrs_521
       else
        do select-hours-earned
       end-if
     end-if
   end-if
 end-if
 
 print #hrs_paid (,{#col_vac_paid})
 print #hours_earned (,{#col_hol_paid})
 print $erncd    (,{#col_erncd})         

#debug9 Show '#hrs_paid=='  #hrs_paid 

 Begin-SQL On-Error=P290-SQL-Error('Update-pay-oth-earns')
     Update ps_pay_oth_earns uoe
        Set uoe.oth_hrs = #hours_earned
            Where uoe.company = &B.COMPANY
              and uoe.paygroup = &B.PAYGROUP
             and uoe.pay_end_dt = &B.PAY_END_DT
              and uoe.off_cycle = &B.OFF_CYCLE
             and uoe.page_num = &B.PAGE_NUM
             and uoe.line_num = &B.LINE_NUM
             and uoe.addl_nbr = &B.ADDL_NBR
             and uoe.erncd = &B.ERNCD
 End-SQL

 let #hol_hrs = 0
 let #hol_hrs_502 = 0
 let #hol_hrs_505 = 0
 let #hol_hrs_514 = 0
 let #hol_hrs_521 = 0
 let #total_hrs = 0
 Let #avg_hrs = 0	 	 

end-procedure

!**********************************************************************
begin-procedure update-pay-earnings
#debug9 Show 'update-pay-earnings'

! Update pay earnings to set the pay line status to 'Updated' so that a recalc occurs. 

begin-sql On-Error=P290-SQL-Error('Update-pay-earnings')
UPDATE ps_pay_earnings ue
       set ue.pay_line_status = 'U'
 WHERE ue.company = &B.COMPANY
   AND ue.paygroup = &B.PAYGROUP
   AND ue.pay_end_dt = &B.pay_end_dt
   AND ue.off_cycle = &B.off_cycle
   AND ue.page_num = &B.page_num
   AND ue.line_num = &B.line_num   
end-sql

end-procedure
!**********************************************************************
!**********************************************************************
begin-procedure select-hours-earned
#debug9 Show 'select-hours-earned'
#debug9 Show 'select-hours-earned Prcs_OprID:'$Prcs_OprID
#debug9 Show 'select-hours-earned Prcs_Run_Cntl_ID: '$Prcs_Run_Cntl_ID
#debug9 Show 'select-hours-earned hrs_paid: '#hrs_paid

let $found-hours = 'N'

begin-select 
a.hours_earned  &a.hours_earned

   let $found-hours = 'Y'
   let #hours_earned = &a.hours_earned
	
from sysadm.ps_gex_rc_gexpy224 a
where a.oprid = $Prcs_OprID
and a.run_cntl_id = $Prcs_Run_Cntl_ID
and a.hours1 <= #hrs_paid
and a.hours2 >= #hrs_paid

end-select

if $found-hours = 'N'
   let #hours_earned = #hrs_paid
end-if

#debug9 Show 'select-hours-earned hrs_paid: '$found-hours
#debug9 Show 'select-hours-earned hrs_paid: '#hours_earned

end-procedure
!**********************************************************************
Begin-Procedure P290-SQL-Error($Action)
#debug9 Show 'P290-SQL-Error'
  Show $Action ' failed'
  Show 'SQL Error:  '  $_sql-error
  Stop
End-Procedure
!*********************************************************************
Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Procedure
!********************************************************************
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx901.sqc'  !
#include 'gexxx950.sqc'  !
#include 'gexxx913.sqc'  !
#include 'gexxx924.sqc'  !
#include 'gexxx956.sqc'  !Get erncd multiple row table (for holiday earnings codes)
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
