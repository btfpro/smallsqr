!***********************************************************************
! GEXBN821:  Students Reclassified                                     *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXBN821 :            Students Reclassified                          *
!                                                                      *
! Narrative:            All employees in BP R08, R10, R12, R16,        *
!                       and R30, R34, R35 who are currently in regular *
!                       part time status and were previously in student*
!                       part time status, and have less than 12 months *
!                       of service, will be reported.                  *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           ben_prog_partic                                *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Monthly - Prior to actual hours sqr programs   *
!                                                                      *
! Control Parms:        None                                           *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! JDH       09/03/98   Initial Creation                                *
! SXK	    10/29/98   Modified to change 12 months period to 3 months *
! CJH       11/28/2000 Added Benefit Programs to logic for COD         *
!                                                                      *
! ISDVNPK 08/23/2007 Changed XLATTABLE to PSXLATITEM as part of 9.0 upgrade  
! GEX_SEC_REENG 2015-06-24 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and Page-Size Initialization (Portrait)


#define #column1  001
#define #column2  020
#define #column3  060
#define #column4  078
#define #column5  096
#define #column6  113
#define #column7  130

Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Heading 7
#debug9 Show 'Begin-Heading'
  #Include 'stdhdg01.sqc'

  Print 'Benefit Program: '   (3,1)
  Print $save_benefit_program ()
  Print 'REG/TEMP'            (+3,{#column4})
  Print 'SERVICE'             (+0,{#column5})
  Print 'REG'                 (+0,{#column6})

  Print 'EMPLOYEE ID'         (+1,{#column1})
  Print 'NAME'                (+0,{#column2})
  Print 'ACTION DATE'         (+0,{#column3})
  Print ' EFFDT '             (+0,{#column4})
  Print 'DATE'                (+0,{#column5})
  Print 'TEMP'                (+0,{#column6})
  Print 'DEPTID'              (+0,{#column7})
End-Heading


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      = 'GEXBN821'
  Let $ReportTitle   = 'STUDENTS RECLASSIFIED'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle
  Show ' '
  Show 'Report Began:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime

! Let $end_month        = '08'
! Let $end_yy           = '1998'
! Let $check_dt         = '01-AUG-1998'
! Let $check_dt_min_dbf = '01-AUG-1998'

  Do Edit-Pay-Calendar

  Let $AsOfDate = $effdt_dbf
    
  Do Convert-To-DTU-Date($AsOfDate,$AsOfDate_DTU)
  do dtu-add-months($AsofDate_dtu,-1,$Asofdate_dtu)
  do convert-from-dtu-date($asofdate_dtu,$asofdate)

  Do dtu-add-months($AsOfDate_DTU,-3,$3monthsAgoDate_DTU)
  Do Convert-From-DTU-Date($3monthsagoDate_DTU,$3monthsAgoDate)

End-Procedure


Begin-Procedure P110-Get-Xlat
#debug9 Show 'P110-Get-Xlat'
Begin-Select On-Error=P260-SQL-Error-Found('P110-Get-Xlat')
xlatshortname &reg_temp_descr
From   PSXLATITEM ! ISDVNPK 08/23/2007 Changed XLATTABLE to PSXLATITEM as part of 9.0 upgrade  
Where  fieldname  = 'REG_TEMP'
And    fieldvalue = &j.reg_temp
End-Select
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select On-Error=P260-SQL-Error-Found('P200-Main-Process')
bpp.benefit_program  ()  On-Break  Level=1
                                   Print=Never
                                   Save=$save_benefit_program
                                   After=P240-After-Benefit-Program
bpp.emplid
bpp.empl_rcd
pd.name
e.service_dt

  Do P210-Fetch-Job-Rows

From   ps_personal_data       pd,
       ps_employment          e,
       ps_ben_prog_partic     bpp

! 11/28/2000 - CJH - add BPs for COD
!  Where  bpp.benefit_program    In ('R08','R10','R12','R16','R30')
Where  bpp.benefit_program    In ('R08','R10','R12','R16','R30','R34','R35')
! 11/28/2000 - end


And    bpp.effdt               =
      (Select Max(effdt)
       From   ps_ben_prog_partic
       Where  emplid           = bpp.emplid
       And    empl_rcd        = bpp.empl_rcd
       And    cobra_event_id   = bpp.cobra_event_id
       And    effdt           <= $AsOfDate)

And    e.emplid                = bpp.emplid
And    e.empl_rcd             = bpp.empl_rcd

And    pd.emplid               = bpp.emplid

Order By bpp.benefit_program, pd.name
End-Select
End-Procedure


Begin-Procedure P210-Fetch-Job-Rows
#debug9 Show 'P210-Fetch-Job-Rows'
Begin-Select On-Error=P260-SQL-Error-Found('P210-Fetch-Job-Rows')
j.emplid
j.effdt
j.deptid
j.reg_temp
j.action_dt
 
  Do P230-Print-Record

From   ps_job                   j

Where  j.emplid                 = &bpp.emplid
And    j.empl_rcd              = &bpp.empl_rcd

And    j.effdt                  =
      (Select Min(effdt)
       From   ps_job
       Where  emplid            = &bpp.emplid
       And    empl_rcd         = &bpp.empl_rcd
       And    effdt            >= $3monthsAgoDate
       And    effdt            <= $AsOfDate
       And    effdt            >=
             (Select Max(effdt)
              From   ps_job
              Where  emplid     = &bpp.emplid
              And    empl_rcd  = &bpp.empl_rcd
              And    effdt     <= $AsOfDate
              And    reg_temp   = 'S')
       And    reg_temp          <> 'S')

And    j.effseq                 =
      (Select Max(effseq)
       From   ps_job
       Where  emplid            = &bpp.emplid
       And    empl_rcd         = &bpp.empl_rcd
       And    effdt             = j.effdt)

And    j.reg_temp               <> 'S'
End-Select
End-Procedure


Begin-Procedure P230-Print-Record
#debug9 Show 'P230-Print-Record'
  Let $Data-Found-Flag = 'Y'
  Let $BP-Data-Found-Flag = 'Y'

  !Do Format-DateTime(&j.effdt, $effdt_out, {DEFMDY}, '', '') ! VENDRBC Added for upgarde 9.0 on
  Let $effdt_out = edit(&j.effdt,'mm/dd/yy') ! VENDRBC Added for upgarde 9.0 on
 !Do Format-DateTime(&e.service_dt, $service_dt_out, {DEFMDY}, '', '') ! VENDRBC Added for upgarde 9.0 on
  Let $service_dt_out = edit(&e.service_dt,'mm/dd/yy') ! VENDRBC Added for upgarde 9.0 on
  !Do Format-DateTime(&j.action_dt, $action_dt_out, {DEFMDY}, '', '') ! VENDRBC Added for upgarde 9.0 on
  Let $action_dt_out=edit(&j.action_dt,'mm/dd/yy') ! VENDRBC Added for upgarde 9.0 on
    
  Do P110-Get-Xlat

  Print &j.emplid             (+1,{#column1})   !Edit xxx-xx-xxxx ! GEX_SEC_REENG 2015-06-24 Vahini Katta
  Print &pd.name              (+0,{#column2},030)
  Print $action_dt_out        (+0,{#column3})    
  Print $effdt_out            (+0,{#column4})
  Print $service_dt_out       (+0,{#column5})
  Print &reg_temp_descr       (+0,{#column6})
  Print &j.deptid             (+0,{#column7})

End-Procedure


Begin-Procedure P240-After-Benefit-Program
#debug9 Show 'P240-After-Benefit-Program'
  If $BP-Data-Found-Flag = 'Y'
    New-Page
    Let $BP-Data-Found-Flag = 'N'
  End-If
End-Procedure


Begin-Procedure P260-SQL-Error-Found($Proc_Name) 
#debug9 Show 'P260-SQL-Error-Found'
  Display 'SQL Error in ' 
  Display $Proc_Name 
  Display $_sql-error
  Display 'sql-status:' 
  Display #_sql-status 
  Do P270-Emplid-in-Error
End-Procedure


Begin-Procedure P270-Emplid-in-Error
#debug9 Show 'P270-Emplid-in-Error'
  Display ''
  Display 'Error occurred while processing emplid ' Noline
  Display &j.emplid
  Display ''
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  If $Data-Found-Flag <> 'Y'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc
  Do Get-Current-DateTime    !curdttim.sqc

  Show ' '
  Show 'Report Ended:'
  Show '  Date = ' $ReportDate
  Show '  Time = ' $ReportTime
  Show ' '
  Show 'Successful end of report'
End-Procedure


#include 'gexxx920.sqc'  !Get ben single row run control
#include 'gexbnmdt.sqc'  !Get pay_end_dt for last confirmed pay period
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
