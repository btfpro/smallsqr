!********************************************************************!
!GEXBN642.SQR - Daily OE Change Files                                !
!********************************************************************!
!         !!!!!  This version is for HR 9.0 PeopleSoft !!!!!         !
!********************************************************************!
!                                                                    !
!Description - This program is designed to export data from Ben      ! 
!              Admin prepared data only.  Any employee outside the   !
!              Ben Admin schedule will not be included in the files. !
!              (PeopleSoft to Univers)                               !
!                                                                    !
!Database Tables:                                                    !
!                 PS_BAS_PARTIC           SELECT                     !
!                 PS_BAS_PARTIC_COST      SELECT                     !
!                 PS_BAS_PARTIC_PLAN      SELECT                     !
!                 PS_BAS_PARTIC_DPND      SELECT                     !
!                 PS_BAS_PARTIC_OPTN      SELECT                     !
!                 PS_CBP_RUN_XXXBAS       SELECT                     !
!                 PS_COVRG_CD_TBL         SELECT                     !
!                 PS_DEP_BEN              SELECT                     !
!                 PS_DEP_BEN_EFF          SELECT                     !
!                 PS_DEP_BEN_NAME         SELECT                     !
!                 PS_DEP_BENEF_NID        SELECT                     !
!                 PS_PERSON               SELECT                     !
!                 PS_PERSON_NAME          SELECT                     !
!                 PS_PERS_DATA_EFFDT      SELECT                     !
!                 PS_PERS_NID             SELECT                     !
!                 PS_BEN_DEFN_OPTN        SELECT                     !
!                 PS_BENEF_PLAN_TBL       SELECT                     !
!                                                                    !
!                                                                    !
!Runtime Prompts: Run Mode (by Schedule ID, then by Status Date, or  !
!                           by Emplid)                               !
!                 Output File location                               !
!                                                                    !
!                                                                    !
!********************************************************************!
!********************************************************************!
!
! Date       programmer                    Modification
! ----       ----------     --------------------------------------------
!
!***********************************************************************

!-----------------------------------------------------------------------
!   Set Environment                                                    -
!-----------------------------------------------------------------------
#include 'setenv.sqc'    !Set environment
#include 'setup01.sqc'   !Printer and page-size initialization


!-----------------------------------------------------------------------
!  Report Calls                                                        -
!-----------------------------------------------------------------------
begin-report

        do init-number
        do init-datetime
        do get-current-datetime
        
        do Init-Report
        show ' '
        display 'Start Run Time: ' noline
        do display-time
        
        display 'Extracting Data From PeopleSoft Benefits Administration ' noline
        do display-time

        do open-files
        do Create-Benefit-Options-File
        do close-files
        
        show ' '
        display 'End Run Time: ' noline
        do display-time
        do Stdapi-Term

end-report


!-----------------------------------------------------------------------
! Function:    display-time                                            -
! Description: gets current date time and displays it                  -
! Called By:   Report                                                  -
! Calls:       get-current-datetime                                    -
!-----------------------------------------------------------------------
begin-procedure display-time

        do get-current-datetime
        display $asofnow
        show ' '
        
end-procedure


!-----------------------------------------------------------------------
! Function:    Init-Report                                             -
! Description: this section initializes all the variables              -
! Called By:   Report                                                  -
! Calls:       Select-Parameters, Ask-Values                           -
!-----------------------------------------------------------------------
begin-procedure Init-Report

  move 'GEXBN642'   to $ReportID
  move 'Benefits Enrollment Outbound Process File Creation ' to $ReportTitle
  display $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-Values
  else
    do Get-prcsoutputdir !procedure to get the output destination
    do Select-Parameters
  end-if

   Let $Quote = '"'
   Let $Comma = ','

end-procedure Init-Report


!-----------------------------------------------------------------------
! Function:    ASK-VALUES                                              -
! Description: Obtain input values upon execution using SQRW.          -
! Called By:   Init-Report                                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure ask-values
#debugd show 'Ask-Values'

  input $IN_Sched_Id    maxlen=6  'Enter the Schedule ID to process: MUST BE ENTERED ' type=char
  input $IN_Status_Dt   maxlen=11 'Enter the Status Date to Process, MM-DD-YYYY or leave blank for all' type=date
  
 !File Path Default 
  If $File_Path = ' ' or $File_Path = ''
     Let $File_Path = 'C:\Temp\'
  End-If

  Let $Benefit_Options_File    = $File_Path || 'bas_benoptns_file.csv'

 !Sched_ID is Required.
  If $IN_Sched_ID = ' ' or $IN_Sched_ID = ''
     Display 'Schedule ID MUST BE Entered'
     input $IN_Sched_Id    maxlen=6  'Enter the Schedule ID to process: ' type=char
  End-If

  If $IN_Sched_ID = ' ' or $IN_Sched_ID = ''
     Display 'Schedule ID was not entered, stopping program'
     stop quiet
  End-If

  If $IN_Sched_ID <> '' and $In_Sched_ID <> ' '
     Let $Where  = 'WHERE B.SCHED_ID = ' || $IN_Sched_ID
  End-If
  
  
  show ' '
  show '*** Input Parameters ***'
  show '$In_Sched_Id  = ' $In_Sched_ID
  show ' '
  show '*** File Locations ***'
  show $Benefit_Options_File 
  show ' '
  show '*** Dynamic Where Clauses ***'
  show '$Where  = ' $Where 
  show ' '

end-procedure


!-----------------------------------------------------------------------
! Function:    Select-Parameters                                       -
! Description: Select Values from Run Control                          -
! Called By:   Init-Report                                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Select-Parameters
#debugd show 'Select-Parameters'

 show '$prcs_run_cntl_id = ' $prcs_run_cntl_id
 show '$prcs_oprid       = ' $prcs_oprid
 
BEGIN-SELECT
RC.SCHED_ID
RC.STATUS_DT
RC.RUNDATE

   if ISNULL(&RC.RUNDATE)
      MOVE $ASOFTODAY TO $RUNDATE
   ELSE  
      MOVE &RC.RUNDATE TO $RUNDATE
   END-IF  
   
   if ISNULL(&RC.STATUS_DT)
      MOVE $ASOFTODAY TO $Status_Dt
   ELSE  
      MOVE &RC.STATUS_DT TO $Status_Dt
   END-IF  
   
   let $IN_Sched_Id  = &RC.SCHED_ID
   LET $File_Date = edit($Status_Dt,'mmddyyyy') 
     
   show '$Status_Dt :' $Status_Dt
   show '$RUNDATE   :' $RUNDATE
  
   let $Benefit_Options_File    = $origoutdest || $ReportID ||'_'|| $File_Date || '_' || 'bas_benoptns_file.csv'     
   let $job_file                = $origoutdest || $ReportID ||'_'|| $File_Date || '_' || 'job_file.csv'  
   let $pre_dep_file            = $origoutdest || $ReportID ||'_'|| $File_Date || '_' || 'pre_dep_file.csv'

FROM PS_CBP_RUN_XXXBAS RC
WHERE RC.oprid       = $prcs_oprid
  AND RC.run_cntl_id = $prcs_run_cntl_id
END-SELECT

BEGIN-SELECT
RC1.DEDCD

 if isnull($DedCriteria)
    let $DedCriteria = $DedCriteria ||''''|| &RC1.DEDCD || ''''
 else
    let $DedCriteria = $DedCriteria ||','||''''|| &RC1.DEDCD || ''''
 end-if
   show '$DedCriteria :' $DedCriteria
   
FROM PS_GEX_CBP_RUN RC1
WHERE RC1.oprid       = $prcs_oprid
AND RC1.run_cntl_id = $prcs_run_cntl_id
END-SELECT
 
 if isnull($DedCriteria)
  move '' to $SlctDedCriteria
  move '' to $SlctDedCriteria1
 else
  let $SlctDedCriteria = 'DD.DEDCD in (' || $DedCriteria || ')' || ' AND'
  let $SlctDedCriteria1 ='GEN.DEDCD in (' || $DedCriteria || ')' || ' AND'
 end-if 

  show '$SlctDedCriteria :' $SlctDedCriteria
  show '$SlctDedCriteria1:' $SlctDedCriteria1

    If $IN_Sched_ID <> '' and $In_Sched_ID <> ' '
       Let $Where = 'WHERE O.SCHED_ID = ' || '''' || $IN_Sched_ID || ''''
    End-If
       
    show ' '
    show '*** Run Control Parameters ***'
    show '$In_Sched_Id  = ' $In_Sched_ID
    show '$In_Status_Dt = ' $In_Status_Dt
    show ' '
    show '*** File Locations ***'
    show $Benefit_Options_File 
    show ' '
    show '*** Dynamic Where Clauses ***'
    show '$Where  = ' $Where 
    show '$Where2 = ' $Where2
  show ' '

end-procedure !Select-Parameters


!-----------------------------------------------------------------------
! Function:    Open-Files                                              -
! Description: Opens Files for Writing                                 -
! Called By:   Report                                                  -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure open-files

 !Open the Benefit Options file
   open $Benefit_Options_File as 2 for-writing record=1100:fixed status=#filestatus2
   open $job_file as 3 for-writing record=1500:fixed status=#filestatus3
   open $pre_dep_file as 4 for-writing record=1500:fixed status=#filestatus4

     if #filestatus2 = -1
       show ' '
       show ' '
       show ' ***** WARNING ***** Open for 2' $Benefit_Options_File ' in program ' $report_name ' failed.'
       show ' '
       show ' '
       stop quiet
     else
       let $error_flag = 'N'
       show ' '
       show 'File Opened for Writing: ' $Benefit_Options_File
       show ' '
     end-if
     
     if #filestatus3 = -1
            show ' '
            show ' '
            show ' ***** WARNING ***** Open for 3' $job_file ' in program ' $report_name ' failed.'
            show ' '
            show ' '
            stop quiet
          else
            let $error_flag = 'N'
            show ' '
            show 'File Opened for Writing: ' $job_file
     end-if
     
     if #filestatus4 = -1
            show ' '
            show ' '
            show ' ***** WARNING ***** Open for 4' $pre_dep_file ' in program ' $report_name ' failed.'
            show ' '
            show ' '
            stop quiet
          else
            let $error_flag = 'N'
            show ' '
            show 'File Opened for Writing: ' $pre_dep_file
     end-if

end-procedure open-files


!-----------------------------------------------------------------------
! Function:    Close-Files                                             -
! Description: Closes Files                                            -
! Called By:   Report                                                  -
! Calls:       None                                                    -
! Parameters:  #file_num                                               - 
!-----------------------------------------------------------------------
begin-procedure close-files

close 2
close 3
close 4

end-procedure close-files
!-----------------------------------------------------------------------
! Function:    Create-Benefit-Options-File                             -
! Description: select Benefit Options Data and create the file         -
! Called By:   Report                                                  -
! Calls:       Write-Benefit-Options-File-Header                       -
!              Get-PersNid                                             -
!              Get-Ben-Defn-Optn-Data                                  -
!              Get-Benefit-Plan-Tbl-Data                               -
!              Clear-Variables                                         -
!              Clear-Dependent-Variables                               -
!-----------------------------------------------------------------------
begin-procedure Create-Benefit-Options-File
#debugd show 'Create-Benefit-Options-File'
Do Write-Benefit-Options-File-Header
do write-header-job-file
do write-header-pre-dep
do Clear-Variables
Do Clear-Benefits-Variables

BEGIN-SELECT
O.SCHED_ID
O.EMPLID
O.BENEFIT_RCD_NBR
O.EVENT_ID
PO.PLAN_TYPE
PO.OPTION_ID
PO.BENEFIT_PROGRAM
PO.EVENT_DT
PO.DISPLAY_PLN_SEQ
PO.DISPLAY_OPT_SEQ
PO.OPTION_TYPE
PO.BENEFIT_PLAN
PO.COVRG_CD
PO.OPTION_CD
PO.OPTION_LVL
PO.DEFAULT_IND
PO.PROOF_REQ_IND
PO.HISTORY_ONLY
PO.CALCULATED_BASE
PO.PREMIUM_BASE
PO.DED_CLASS
O.STATUS_DT
O.JOB_EFFDT

   LET $SCHED_ID        = &O.SCHED_ID
   LET $EMPLID          = &O.EMPLID
   LET $BENEFIT_RCD_NBR = &O.BENEFIT_RCD_NBR
   LET $EVENT_ID        = &O.EVENT_ID
   LET $PLAN_TYPE       = &PO.PLAN_TYPE
   LET $OPTION_ID       = &PO.OPTION_ID
   LET $BENEFIT_PROGRAM = &PO.BENEFIT_PROGRAM
   LET $EVENT_DT        = &PO.EVENT_DT
   LET $DISPLAY_PLN_SEQ = &PO.DISPLAY_PLN_SEQ
   LET $DISPLAY_OPT_SEQ = &PO.DISPLAY_OPT_SEQ
   LET $OPTION_TYPE     = &PO.OPTION_TYPE
   LET $BENEFIT_PLAN    = &PO.BENEFIT_PLAN
   LET $COVRG_CD        = &PO.COVRG_CD
   LET $OPTION_CD       = &PO.OPTION_CD
   LET $OPTION_LVL      = &PO.OPTION_LVL
   LET $DEFAULT_IND     = &PO.DEFAULT_IND
   LET $PROOF_REQ_IND   = &PO.PROOF_REQ_IND
   LET $HISTORY_ONLY    = &PO.HISTORY_ONLY
   LET $CALCULATED_BASE = &PO.CALCULATED_BASE
   LET $PREMIUM_BASE    = &PO.PREMIUM_BASE
   LET $DED_CLASS       = &PO.DED_CLASS
   LET $Selected        = 'N'                       !Defaulting for Giant Eagle

  !Set the Pre-Tax value (1 = True, 0 = False)
   EVALUATE $DED_CLASS
   WHEN = 'A'             !After Tax
      Let $Pre-Tax = '0'
   WHEN = 'B'             !Before Tax
      Let $Pre-Tax = '1'
   WHEN = 'L'             !QC Taxable Benefit
      Let $Pre-Tax = '0'
   WHEN = 'N'             !Non-Taxable Benefit
      Let $Pre-Tax = '0'
   WHEN = 'P'             !Non-Taxable BTax Benefit
      Let $Pre-Tax = '1'
   WHEN = 'T'             !Taxable Benefit
      Let $Pre-Tax = '0'
   END-EVALUATE

   DO GET-GEX-EMPLID

   Do Get-PersNid
   Do Get-Ben-Defn-Optn-Data
   Do Get-Coverage-Code-Description
   Do Get-Benefit-Plan-Tbl-Data
   Do Get-Bas-Partic-Cost-Data
   if $Cost_Found='N'
      Do Write-Benefit-Options-File
   end-if
   
   if $EMPLID<>$prev_emplid
      do job-file
      do pre-dep-file
   end-if
   Do Clear-Benefits-Variables
   let $prev_emplid=&O.EMPLID
   

FROM PS_BAS_PARTIC O, PS_BAS_PARTIC_PLAN PL, PS_BAS_PARTIC_OPTN PO
[$WHERE]
AND O.STATUS_DT       = $Status_Dt
AND O.SCHED_ID        = PO.SCHED_ID
AND O.EMPLID          = PO.EMPLID
AND O.BENEFIT_RCD_NBR = PO.BENEFIT_RCD_NBR
AND O.EVENT_ID        = PO.EVENT_ID
AND O.SCHED_ID        = PL.SCHED_ID
AND O.EMPLID          = PL.EMPLID
AND O.BENEFIT_RCD_NBR = PL.BENEFIT_RCD_NBR
AND O.EVENT_ID        = PL.EVENT_ID
AND PL.PLAN_TYPE      = PO.PLAN_TYPE
AND PO.OPTION_TYPE    in ('O', 'W')
ORDER BY O.SCHED_ID, O.EMPLID, O.BENEFIT_RCD_NBR, O.EVENT_ID, PO.PLAN_TYPE, PO.OPTION_ID
END-SELECT

end-procedure Create-Benefit-Options-File


!-----------------------------------------------------------------------
! Function:    Get-Ben-Defn-Optn-Data                                  -
! Description: gets benefit Option Definition data                     -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Get-Ben-Defn-Optn-Data
#debugd show '...Get-Ben-Defn-Optn-Data: '
#debugd show '...Get-Ben-Defn-Optn-Data: '$BENEFIT_PROGRAM
#debugd show '...Get-Ben-Defn-Optn-Data: '$PLAN_TYPE
#debugd show '...Get-Ben-Defn-Optn-Data: '$OPTION_ID

LET $BO_OPTION_CD      = ' '
LET $BO_BENEFIT_PLAN   = ' '
LET $BO_COVRG_CD       = ' '

BEGIN-SELECT
BO.OPTION_CD
BO.BENEFIT_PLAN
BO.COVRG_CD

   MOVE &BO.OPTION_CD    TO $BO_OPTION_CD
   MOVE &BO.BENEFIT_PLAN TO $BO_BENEFIT_PLAN
   MOVE &BO.COVRG_CD     TO $BO_COVRG_CD
  
FROM PS_BEN_DEFN_OPTN BO
WHERE BO.BENEFIT_PROGRAM = $BENEFIT_PROGRAM
  AND BO.PLAN_TYPE       = $PLAN_TYPE
  AND BO.OPTION_ID       = $OPTION_ID
  AND BO.EFFDT = (SELECT MAX(BO1.EFFDT) FROM PS_BEN_DEFN_OPTN BO1
                   WHERE BO1.BENEFIT_PROGRAM = BO.BENEFIT_PROGRAM
                     AND BO1.PLAN_TYPE       = BO.PLAN_TYPE
                     AND BO1.OPTION_ID       = BO.OPTION_ID
                     AND BO1.EFFDT          <= &O.JOB_EFFDT)
END-SELECT

end-procedure Get-Ben-Defn-Optn-Data


!-----------------------------------------------------------------------
! Function:    Get-Bas-Partic-Cost-Data                                -
! Description: gets bas partic cost data & writes the file             -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       Write-Benefits-Options-File                             -
!-----------------------------------------------------------------------
begin-procedure Get-Bas-Partic-Cost-Data
#debugd show '...Get-Bas-Partic-Cost-Data'
#debugd show '...Get-Bas-Partic-Cost-Data'$SCHED_ID
#debugd show '...Get-Bas-Partic-Cost-Data'$EMPLID
#debugd show '...Get-Bas-Partic-Cost-Data'$BENEFIT_RCD_NBR
#debugd show '...Get-Bas-Partic-Cost-Data'$EVENT_ID
#debugd show '...Get-Bas-Partic-Cost-Data'$PLAN_TYPE
#debugd show '...Get-Bas-Partic-Cost-Data'$OPTION_ID

  Let #EE_Annual_Cost = 0
  Let #ER_Annual_Cost = 0
  Let #Credit         = 0
  Move 'N' to $Cost_Found
begin-select
BPC.COST_ID        
BPC.BENEFIT_PROGRAM
BPC.EVENT_DT       
BPC.DISPLAY_PLN_SEQ
BPC.DISPLAY_OPT_SEQ
BPC.COST_TYPE                   !P = PRICE, C = CREDIT
BPC.ERNCD          
BPC.ANNL_AMT_A_TAX 
BPC.ANNL_AMT_B_TAX 
BPC.ANNL_AMT_NT_TAX
BPC.DEDN_AMT_A_TAX 
BPC.DEDN_AMT_B_TAX 
BPC.DEDN_AMT_NT_TAX
BPC.PROVINCE       
BPC.CNSLSTX_AMT    
BPC.CNSLSTX_ANN_AMT

  Move 'Y' to $Cost_Found
  Let #EE_Annual_Cost = &BPC.ANNL_AMT_A_TAX + &BPC.ANNL_AMT_B_TAX
  Let #ER_Annual_Cost = &BPC.ANNL_AMT_NT_TAX

  If &BPC.COST_TYPE = 'C'
     Let #Credit = &BPC.ANNL_AMT_A_TAX + &BPC.ANNL_AMT_B_TAX
  End-If

  Do Write-Benefit-Options-File

FROM PS_BAS_PARTIC_COST BPC
WHERE BPC.SCHED_ID        = $SCHED_ID
  AND BPC.EMPLID          = $EMPLID
  AND BPC.BENEFIT_RCD_NBR = $BENEFIT_RCD_NBR
  AND BPC.EVENT_ID        = $EVENT_ID
  AND BPC.PLAN_TYPE       = $PLAN_TYPE
  AND BPC.OPTION_ID       = $OPTION_ID
end-select

end-procedure Get-Bas-Partic-Cost-Data


!-----------------------------------------------------------------------
! Function:    Get-Benefit-Plan-Tbl-Data                               -
! Description: gets benefit plan table data                            -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Get-Benefit-Plan-Tbl-Data
#debugd show '...Get-Benefit-Plan-Tbl-Data'
#debugd show '...Get-Benefit-Plan-Tbl-Data'$PLAN_TYPE
#debugd show '...Get-Benefit-Plan-Tbl-Data'$BENEFIT_PLAN

LET $BEN_PLAN_DESCR      = ' '
LET $BEN_PLAN_DESCRSHORT = ' '
LET $DEDCD               = ' '
LET $VENDOR_ID           = ' '

BEGIN-SELECT
B1.DESCR
B1.DESCRSHORT
B1.DEDCD
B1.VENDOR_ID

  MOVE &B1.DESCR      TO $BEN_PLAN_DESCR
  MOVE &B1.DESCRSHORT TO $BEN_PLAN_DESCRSHORT
  MOVE &B1.DEDCD      TO $DEDCD
  MOVE &B1.VENDOR_ID  TO $VENDOR_ID
  
FROM PS_BENEF_PLAN_TBL B1
WHERE B1.PLAN_TYPE    = $PLAN_TYPE
  AND B1.BENEFIT_PLAN = $BENEFIT_PLAN
  AND B1.EFFDT = (SELECT MAX(B2.EFFDT) FROM PS_BENEF_PLAN_TBL B2
                   WHERE B2.PLAN_TYPE    = B1.PLAN_TYPE
                     AND B2.BENEFIT_PLAN = B1.BENEFIT_PLAN
                     AND B2.EFFDT       <= $RUNDATE)
END-SELECT

end-procedure get-benefit-plan-tbl-data


!-----------------------------------------------------------------------
! Function:    Get-Coverage-Code-Description                           -
! Description: gets the coverage code description                      -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Get-Coverage-Code-Description
#debugd show '...Get-Coverage-Code-Description'
#debugd show '...Get-Coverage-Code-Description'$COVRG_CD
LET $COVRG_CD_DESCR      = ' '
LET $COVRG_CD_DESCRSHORT = ' '

BEGIN-SELECT
C1.DESCR
C1.DESCRSHORT

  MOVE &C1.DESCR      TO $COVRG_CD_DESCR
  MOVE &C1.DESCRSHORT TO $COVRG_CD_DESCRSHORT
  
FROM PS_COVRG_CD_TBL C1
WHERE C1.COVRG_CD = $COVRG_CD 
  AND C1.EFFDT    = (SELECT MAX(C2.EFFDT) FROM PS_COVRG_CD_TBL C2
                      WHERE C2.COVRG_CD = C1.COVRG_CD 
                        AND C2.EFFDT   <= $RUNDATE)
END-SELECT

end-procedure get-coverage-code-description


!-----------------------------------------------------------------------
! Function:    Write-Benefit-Options-File-Header                       -
! Description: Write header record for output file, {pre_dep_file}     -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Write-Benefit-Options-File-Header
#debugd show '...Write-Benefit-Options-File-Header'
  
        write 2 from    $Quote 'Plan Description     ' $Quote $Comma
                        $Quote 'Coverage Description ' $Quote $Comma
                        $Quote 'EE SSN               ' $Quote $Comma
                        $Quote 'Emplid               ' $Quote $Comma
                        $Quote 'EE Annual Cost       ' $Quote $Comma
                        $Quote 'Org Annual Cost      ' $Quote $Comma
                        $Quote 'Pre-Tax              ' $Quote $Comma
                        $Quote 'Credit               ' $Quote $Comma  
                        $Quote 'Selected             ' $Quote $Comma
                        $Quote 'BEN_PROGRAM          ' $Quote $Comma
                        $Quote 'PLAN_TYPE            ' $Quote $Comma
                        $Quote 'BENEFIT_PLAN         ' $Quote $Comma
                        $Quote 'COVRG_CD             ' $Quote $Comma
                        $Quote 'OPTION_CD            ' $Quote $Comma
                        $Quote 'Sched_ID             ' $Quote $Comma
                        $Quote 'Benefit_Rcd_Nbr      ' $Quote $Comma
                        $Quote 'Event ID             ' $Quote $Comma
                        $Quote 'Option ID            ' $Quote $Comma
                        $Quote 'Event Date           ' $Quote $Comma
                        $Quote 'Display Pln Seq      ' $Quote $Comma
                        $Quote 'Display Opt Seq      ' $Quote $Comma
                        $Quote 'Option Type          ' $Quote $Comma
                        $Quote 'Option Lvl           ' $Quote $Comma
                        $Quote 'Default Ind          ' $Quote $Comma
                        $Quote 'Proof Req Ind        ' $Quote $Comma
                        $Quote 'History Only         ' $Quote $Comma
                        $Quote 'Calculated Base      ' $Quote $Comma
                        $Quote 'Premium Base         ' $Quote $Comma
                        $Quote 'Ded Class            ' $Quote $Comma
                        $Quote 'Ben Plan Descrshort  ' $Quote $Comma
                        $Quote 'Dedcd                ' $Quote $Comma
                        $Quote 'Vendor ID            ' $Quote 

end-procedure !Write-Benefit-Options-File-Header

                                              
!-----------------------------------------------------------------------
! Function:    Write-Benefit-Options-File                              -
! Description: Write Data to output file, {pre_dep_file}               -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Write-Benefit-Options-File
#debugd show '...Write-Benefit-Options-File'
#debugd show '...Write-Benefit-Options-File'$EMPLID
#debugd show '...Write-Benefit-Options-File'$OPTION_TYPE

        let $EE_Annual_Cost = edit(#EE_Annual_Cost, '999999.99')
        let $ER_Annual_Cost = edit(#ER_Annual_Cost,'999999.99')
        let $Credit         = edit(#Credit,'999999.99')


        write 2 from    $Quote $BEN_PLAN_DESCR         $Quote $Comma
                        $Quote $COVRG_CD_DESCR         $Quote $Comma
                        $Quote $NATIONAL_ID            $Quote $Comma
                        $Quote $GEX-EMPLID             $Quote $Comma
                        $Quote $EE_Annual_Cost         $Quote $Comma
                        $Quote $ER_Annual_Cost         $Quote $Comma
                        $Quote $PRE-TAX                $Quote $Comma
                        $Quote $Credit                 $Quote $Comma  
                        $Quote $Selected               $Quote $Comma
                        $Quote $BENEFIT_PROGRAM        $Quote $Comma
                        $Quote $PLAN_TYPE              $Quote $Comma
                        $Quote $BENEFIT_PLAN           $Quote $Comma
                        $Quote $COVRG_CD               $Quote $Comma
                        $Quote $OPTION_CD              $Quote $Comma
                        $Quote $SCHED_ID               $Quote $Comma
                        $Quote $BENEFIT_RCD_NBR        $Quote $Comma
                        $Quote $EVENT_ID               $Quote $Comma
                        $Quote $OPTION_ID              $Quote $Comma
                        $Quote $EVENT_DT               $Quote $Comma
                        $Quote $DISPLAY_PLN_SEQ        $Quote $Comma
                        $Quote $DISPLAY_OPT_SEQ        $Quote $Comma
                        $Quote $OPTION_TYPE            $Quote $Comma
                        $Quote $OPTION_LVL             $Quote $Comma
                        $Quote $DEFAULT_IND            $Quote $Comma
                        $Quote $PROOF_REQ_IND          $Quote $Comma
                        $Quote $HISTORY_ONLY           $Quote $Comma
                        $Quote $CALCULATED_BASE        $Quote $Comma
                        $Quote $PREMIUM_BASE           $Quote $Comma
                        $Quote $DED_CLASS              $Quote $Comma
                        $Quote $BEN_PLAN_DESCRSHORT    $Quote $Comma
                        $Quote $DEDCD                  $Quote $Comma
                        $Quote $VENDOR_ID              $Quote 
                                                   
end-procedure !Write-Benefit-Options-File


!-----------------------------------------------------------------------
! Function:    Clear-Benefits-Variables                                -
! Description: Clear the variables for the next employee               -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Clear-Benefits-Variables
#debugd show '...Clear-Benefits-Variables'      

   let #EE_Annual_Cost       = 0
   let #ER_Annual_Cost       = 0
   let #Credit               = 0
   let $BEN_PLAN_DESCR       = ' '
   let $COVRG_CD_DESCR       = ' '
   let $NATIONAL_ID          = ' '
   let $EMPLID               = ' '
   let $EE_Annual_Cost       = ' '
   let $ER_Annual_Cost       = ' '
   let $PRE-TAX              = ' '
   let $Credit               = ' '
   let $Selected             = ' '
   let $BENEFIT_PROGRAM      = ' '
   let $PLAN_TYPE            = ' '
   let $BENEFIT_PLAN         = ' '
   let $COVRG_CD             = ' '
   let $OPTION_CD            = ' '
   let $SCHED_ID             = ' '
   let $BENEFIT_RCD_NBR      = ' '
   let $EVENT_ID             = ' '
   let $OPTION_ID            = ' '
   let $EVENT_DT             = ' '
   let $DISPLAY_PLN_SEQ      = ' '
   let $DISPLAY_OPT_SEQ      = ' '
   let $OPTION_TYPE          = ' '
   let $OPTION_LVL           = ' '
   let $DEFAULT_IND          = ' '
   let $PROOF_REQ_IND        = ' '
   let $HISTORY_ONLY         = ' '
   let $CALCULATED_BASE      = ' '
   let $PREMIUM_BASE         = ' '
   let $DED_CLASS            = ' '
   let $BEN_PLAN_DESCRSHORT  = ' '
   let $DEDCD                = ' '
   let $VENDOR_ID      = ' '

end-procedure !Clear-Benefits-Variables
!-----------------------------------------------------------------------
! Function:    job-file                                                -
! Description: Process Job and Personal Data and write it out          -
! Called By:   Report                                                  -
! Calls:       get-jobcode                                             -
!              get-per1                                                -
!              get-per2                                                -
!              get-per3                                                -
!              get-per4                                                -
!              get-per5                                                - 
!              get-persnid                                             -
!              get-PAYGROUP                                            -
!              get-fedtax                                              -
!              get-statetax                                            - 
!              GET-GEX-EMPLID                                          -
!              write-row-job-file                                      -
!              GET-BEN-PROG                                            -
!-----------------------------------------------------------------------
begin-procedure job-file
#debugd show 'job-file'

  do get-52-weeks
     
BEGIN-SELECT
J1.JOBCODE  
J1.DEPTID 
J1.EMPL_TYPE 
J1.FULL_PART_TIME 
J1.STD_HOURS 
J1.HOURLY_RT !GEX001
J1.ANNUAL_RT 
J1.COMPANY 
J1.PAYGROUP 
J1.EMPL_CLASS 
J1.REG_TEMP 
J1.EMPL_STATUS 
J1.OFFICER_CD 
J1.ELIG_CONFIG1 
J1.ELIG_CONFIG2 
J1.ELIG_CONFIG3 
J1.ELIG_CONFIG4 
J1.ELIG_CONFIG5 
J1.ELIG_CONFIG6 
J1.ELIG_CONFIG7 
J1.ELIG_CONFIG8 
J1.ELIG_CONFIG9 
J1.EFFDT
J1.EMPLID
J1.EMPL_RCD
J1.BUSINESS_UNIT
J1.ANNL_BENEF_BASE_RT
J1.LOCATION
J1.PER_ORG
J1.UNION_CD 
J1.LAST_HIRE_DT

        let $EMPLID             = &J1.EMPLID         
        let $EMPL_RCD           = &J1.EMPL_RCD
        let $JOBCODE            = &J1.JOBCODE         
        let $DEPTID             = &J1.DEPTID          
        let $EMPL_TYPE          = &J1.EMPL_TYPE       
        let $FULL_PART_TIME     = &J1.FULL_PART_TIME  
        let $STD_HOURS          = &J1.STD_HOURS   
        let $HOURLY_RT          = &J1.HOURLY_RT !GEX001
        let $ANNUAL_RT          = &J1.ANNUAL_RT       
        let $COMPANY            = &J1.COMPANY         
        let $PAYGROUP           = &J1.PAYGROUP        
        let $EMPL_CLASS         = &J1.EMPL_CLASS      
        let $REG_TEMP           = &J1.REG_TEMP        
        let $EMPL_STATUS        = &J1.EMPL_STATUS     
        let $OFFICER_CD         = &J1.OFFICER_CD      
        let $ELIG_CONFIG1       = &J1.ELIG_CONFIG1    
        let $ELIG_CONFIG2       = &J1.ELIG_CONFIG2    
        let $ELIG_CONFIG3       = &J1.ELIG_CONFIG3    
        let $ELIG_CONFIG4       = &J1.ELIG_CONFIG4    
        let $ELIG_CONFIG5       = &J1.ELIG_CONFIG5    
        let $ELIG_CONFIG6       = &J1.ELIG_CONFIG6    
        let $ELIG_CONFIG7       = &J1.ELIG_CONFIG7    
        let $ELIG_CONFIG8       = &J1.ELIG_CONFIG8    
        let $ELIG_CONFIG9       = &J1.ELIG_CONFIG9 
        LET $ANNL_BENEF_BASE_RT = &J1.ANNL_BENEF_BASE_RT
        LET $LOCATION           = &J1.LOCATION    
        LET $UNION_CD           = &J1.UNION_CD 
        LET $LAST_HIRE_DT       = &J1.LAST_HIRE_DT

  show '&J1.EMPLID :' &J1.EMPLID

  
  IF &J1.EMPL_TYPE='H'
     MOVE 0 TO $ANNUAL_RT
  ELSE
     MOVE 0 TO $HOURLY_RT
  END-IF   
          
        DO GET-JOBCODE
        DO GET-PER1
        DO GET-PER2
        DO GET-PER3
        DO GET-PER4
        DO GET-PER5
        DO GET-PERSNID
        DO GET-PAYGROUP
        DO GET-FEDTAX
        DO GET-STATETAX
        DO GET-BEN-PROG
        DO GET-GEX-EMPLID
        DO get-benefit-service-date
        DO get-current-pay-deductions
        DO get-average-hours-worked
        
        DO WRITE-ROW-JOB-FILE

FROM PS_JOB J1
WHERE J1.EMPLID = &O.EMPLID
  AND J1.EFFDT       = (SELECT MAX(C_ED.EFFDT) FROM PS_JOB C_ED 
                        WHERE J1.EMPLID   = C_ED.EMPLID 
                          AND J1.EMPL_RCD = C_ED.EMPL_RCD 
                          AND C_ED.EFFDT <= &O.JOB_EFFDT)
  AND J1.EFFSEQ      = (SELECT MAX(C_ES.EFFSEQ) FROM PS_JOB C_ES 
                        WHERE J1.EMPLID   = C_ES.EMPLID 
                          AND J1.EMPL_RCD = C_ES.EMPL_RCD 
                          AND J1.EFFDT    = C_ES.EFFDT) 
  AND J1.COMPANY <> J1.PAYGROUP                               !Exclude Giant Eagle Independent store locations
ORDER BY J1.EMPLID
END-SELECT

end-procedure job-file


!-----------------------------------------------------------------------
! Function:    get-per1                                                -
! Description: Get personal information                                -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-per1
#debugd show '...get-per1'

        let $BIRTHDATE      = ' '      
        
begin-select
PER1.BIRTHDATE                   

        let $BIRTHDATE      = &PER1.BIRTHDATE      
        
FROM PS_PERSON PER1
WHERE PER1.EMPLID   = &J1.EMPLID
end-select

end-procedure get-per1


!-----------------------------------------------------------------------
! Function:    get-per2                                                -
! Description: Get Name information                                    -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-per2
#debugd show '...get-per2'

        let $NAME           = ' '           
        let $LAST_NAME      = ' '      
        let $MIDDLE_NAME    = ' '    
        let $FIRST_NAME     = ' ' 

begin-select
PER2.NAME        
PER2.LAST_NAME   
PER2.MIDDLE_NAME  
PER2.FIRST_NAME  

        let $NAME           = &PER2.NAME           
        let $LAST_NAME      = &PER2.LAST_NAME      
        let $MIDDLE_NAME    = &PER2.MIDDLE_NAME    
        let $FIRST_NAME     = &PER2.FIRST_NAME 
        
FROM PS_PERSON_NAME     PER2
WHERE PER2.EMPLID = &J1.EMPLID
end-select

end-procedure get-per2


!-----------------------------------------------------------------------
! Function:    get-per3                                                -
! Description: Get personal address information                        -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-per3
#debugd show '...get-per3'

        let $ADDRESS1       = ' '
        let $ADDRESS2       = ' '
        let $CITY           = ' '
        let $STATE          = ' '
        let $POSTAL         = ' ' 

begin-select
PER3.ADDRESS1  
PER3.ADDRESS2  
PER3.CITY       
PER3.STATE     
PER3.POSTAL   

        let $ADDRESS1       = &PER3.ADDRESS1       
        let $ADDRESS2       = &PER3.ADDRESS2       
        let $CITY           = &PER3.CITY           
        let $STATE          = &PER3.STATE          
        let $POSTAL         = &PER3.POSTAL 
        
FROM PS_PERSON_ADDRESS  PER3
WHERE PER3.EMPLID = &J1.EMPLID
end-select

end-procedure get-per3


!-----------------------------------------------------------------------
! Function:    get-per4                                                -
! Description: Get phone number                                        -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-per4
#debugd show '...get-per4'

        let $PHONE          = ' '

begin-select
PER4.PHONE   

        let $PHONE          = &PER4.PHONE 
        
FROM PS_PERSON_PHONE    PER4
WHERE PER4.EMPLID = &J1.EMPLID
end-select

end-procedure get-per4


!-----------------------------------------------------------------------
! Function:    get-per5                                                -
! Description: Get personal data information                           -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-per5
#debugd show '...get-per5'

        let $SEX            = ' '            
        let $MAR_STATUS     = ' '
        
begin-select
PER5.SEX           
PER5.MAR_STATUS 

        let $SEX            = &PER5.SEX            
        let $MAR_STATUS     = &PER5.MAR_STATUS 
        
FROM PS_PERS_DATA_EFFDT PER5
WHERE PER5.EMPLID = &J1.EMPLID
  AND PER5.EFFDT = (SELECT MAX(PER5X.EFFDT) 
                    FROM PS_PERS_DATA_EFFDT PER5X 
                    WHERE PER5X.EMPLID = &J1.EMPLID)
end-select

end-procedure get-per5


!-----------------------------------------------------------------------
! Function:    get-jobcode                                             -
! Description: Get jobcode information                                 -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-jobcode
#debugd show '...get-jobcode'

        let $DESCR = ' '
        
BEGIN-SELECT
JC.DESCR

        LET $DESCR = &JC.DESCR 
        
FROM PS_JOBCODE_TBL JC, 
     PS_SET_CNTRL_REC B2
WHERE JC.JOBCODE       = &J1.JOBCODE 
  AND B2.SETCNTRLVALUE = &J1.BUSINESS_UNIT
  AND B2.RECNAME       = 'JOBCODE_TBL'
  AND B2.SETID         = JC.SETID
  AND JC.EFFDT         = (SELECT MAX(JC1.EFFDT) FROM PS_JOBCODE_TBL JC1 
                          WHERE JC1.SETID   = JC.SETID 
                            AND JC1.JOBCODE = JC.JOBCODE 
                            AND JC1.EFFDT  <= &J1.EFFDT)  
END-SELECT

end-procedure get-jobcode


!-----------------------------------------------------------------------
! Function:    get-persnid                                             -
! Description: Get persnid information                                 -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-persnid
#debugd show '...get-persnid'

        let $NATIONAL_ID = ' '
        
BEGIN-SELECT
PDB.NATIONAL_ID 

        LET $NATIONAL_ID = &PDB.NATIONAL_ID  

FROM PS_PERS_NID PDB
WHERE PDB.EMPLID = $Emplid !&J1.EMPLID  
END-SELECT

end-procedure get-persnid


!-----------------------------------------------------------------------
! Function:    get-PAYGROUP                                            -
! Description: Get pay frequency information                           -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-PAYGROUP
#debugd show '...get-paygroup'

        let $PAY_FREQUENCY = ' '
        
BEGIN-SELECT
PG.PAY_FREQUENCY 

        LET $PAY_FREQUENCY = &PG.PAY_FREQUENCY  

FROM PS_PAYGROUP_TBL PG
WHERE PG.COMPANY  = &J1.COMPANY  
  AND PG.PAYGROUP = &J1.PAYGROUP 
  AND PG.EFFDT    = (SELECT MAX(PG1.EFFDT) FROM PS_PAYGROUP_TBL PG1 
                    WHERE PG1.COMPANY  = PG.COMPANY 
                      AND PG1.PAYGROUP = PG.PAYGROUP  
                      AND PG1.EFFDT   <= &J1.EFFDT)
END-SELECT

end-procedure get-PAYGROUP


!-----------------------------------------------------------------------
! Function:    get-fedtax                                              -
! Description: Get fed tax information                                 -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-fedtax
#debugd show '...get-fedtax'

        let $FWT_MAR_STATUS = ' ' 
        let $FWT_ALLOWANCES = ' '
        
BEGIN-SELECT
FTD.FWT_MAR_STATUS 
FTD.FWT_ALLOWANCES 

        LET $FWT_MAR_STATUS = &FTD.FWT_MAR_STATUS 
        LET $FWT_ALLOWANCES = &FTD.FWT_ALLOWANCES 

FROM PS_FED_TAX_DATA FTD 
WHERE FTD.EMPLID  = &J1.EMPLID  
  AND FTD.COMPANY = &J1.COMPANY
  AND FTD.EFFDT   = (SELECT MAX(D_ED.EFFDT) FROM PS_FED_TAX_DATA D_ED 
                     WHERE FTD.EMPLID  = D_ED.EMPLID 
                       AND FTD.COMPANY = D_ED.COMPANY 
                       AND D_ED.EFFDT <= &J1.EFFDT)
END-SELECT

END-PROCEDURE GET-FEDTAX


!-----------------------------------------------------------------------
! Function:    get-statetax                                            -
! Description: Get state tax information                               -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-statetax
#debugd show '...get-statetax'

        let $SWT_MAR_STATUS = ' ' 
        let $SWT_ALLOWANCES = ' '
        
BEGIN-SELECT
STE.SWT_MAR_STATUS 
STE.SWT_ALLOWANCES 

        LET $SWT_MAR_STATUS = &STE.SWT_MAR_STATUS 
        LET $SWT_ALLOWANCES = &STE.SWT_ALLOWANCES  

FROM PS_STATE_TAX_DATA STE
WHERE STE.EMPLID  = &J1.EMPLID  
  AND STE.COMPANY = &J1.COMPANY
  AND STE.EFFDT   = (SELECT MAX(E_ED.EFFDT) FROM PS_STATE_TAX_DATA E_ED 
                     WHERE STE.EMPLID  = E_ED.EMPLID 
                       AND STE.COMPANY = E_ED.COMPANY 
                       AND E_ED.EFFDT <= &J1.EFFDT)
END-SELECT

END-PROCEDURE GET-STATETAX


!-----------------------------------------------------------------------
! Function:    get-ben-prog                                            -
! Description: Get Benefit Program (ben program that is inserted       -
!              (usually defaulted paygroup) when emplid is hired.      -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-ben-prog
#debugd show '...get-ben-prog'

        let $BENEFIT_PROGRAM = ' ' 
        
BEGIN-SELECT
BPP1.BENEFIT_PROGRAM

        LET $BENEFIT_PROGRAM = &BPP1.BENEFIT_PROGRAM

FROM PS_BEN_PROG_PARTIC BPP1
WHERE BPP1.EMPLID         = &J1.EMPLID
  AND BPP1.EMPL_RCD       = &J1.EMPL_RCD
  AND BPP1.COBRA_EVENT_ID = 0                  
  AND BPP1.EFFDT          = (SELECT MAX(BPP2.EFFDT) FROM PS_BEN_PROG_PARTIC BPP2
                           WHERE BPP2.EMPLID          = BPP1.EMPLID
                             AND BPP2.EMPL_RCD        = BPP1.EMPL_RCD
                             AND BPP2.COBRA_EVENT_ID  = 0
                             AND BPP2.EFFDT          <= &J1.EFFDT)
END-SELECT

end-procedure get-ben-prog


!-----------------------------------------------------------------------
! Function:    get-gex-emplid                                          -
! Description: Get Giant Eagles 7 digit emplid                         -
! Called By:   job-file, pre-dep-file                                  -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-gex-emplid
#debugd show '...get-gex-emplid'

        let $GEX-EMPLID = ' '
        
BEGIN-SELECT
GEX.EMPLID

        LET $GEX-EMPLID = &GEX.EMPLID

FROM PS_GEX_EMPLID_MAPP GEX
WHERE GEX.SSN      = $EMPLID
END-SELECT

end-procedure get-gex-emplid
!-----------------------------------------------------------------------
! Function:    get-52-weeks                                            -
! Description: Get Giant Eagles 52 weeks                               -
! Called By:   job-file, pre-dep-file                                  -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-52-weeks
#debugd show '...get-52-weeks'

  do convert-to-dtu-date($RUNDATE,$today_dtu)
  do dtu-subtract-days($today_dtu,366,$begin_dt_dtu)
  do convert-from-dtu-date($begin_dt_dtu,$begin_Dt)
  display $begin_dt
 
begin-select 

Y1.PAY_END_DT 
Y1.pay_begin_dt 

  move &Y1.PAY_END_DT to $pay_end_dt
  move &Y1.PAY_BEGIN_DT TO $pay_begin_dt

FROM PS_PAY_CALENDAR Y1
WHERE Y1.PAY_END_DT = (SELECT MAX(PAY_END_DT)
                         FROM PS_PAY_CALENDAR
                        WHERE PAY_END_DT >= $begin_dt
                          AND PAY_BEGIN_DT <= $begin_dt)
  AND Y1.RUN_ID > ' '

end-select

end-procedure get-52-weeks
!-----------------------------------------------------------------------
! Function:    get-average-hours-worked                                -
! Description: Get Giant Eagles Average hours worked                   -
! Called By:   job-file, pre-dep-file                                  -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-average-hours-worked
#debugd show '...get-average-hours-worked'
        
BEGIN-SELECT
COUNT(PAYCHECK_NBR) &COUNT_PAYS

FROM PS_PAY_CHECK AVGHRS
WHERE AVGHRS.EMPLID      = $EMPLID
  AND AVGHRS.EMPL_RCD    = $EMPL_RCD
  and AVGHRS.PAY_END_DT BETWEEN $pay_end_dt AND &O.JOB_EFFDT
END-SELECT
        
BEGIN-SELECT
sum(B.oth_hrs) &b.oth_hrs

  if &COUNT_PAYS > 0
    let #avg_weekly_hrs_worked = &b.oth_hrs / &COUNT_PAYS
    multiply 1 times #avg_weekly_hrs_worked round=2
    let $AVG_HOURS          = #avg_weekly_hrs_worked       
  end-if 

FROM PS_PAY_EARNINGS A, PS_PAY_OTH_EARNS B 
WHERE A.COMPANY = B.COMPANY 
AND A.PAYGROUP = B.PAYGROUP 
AND A.PAY_END_DT = B.PAY_END_DT 
AND A.OFF_CYCLE = B.OFF_CYCLE 
AND A.PAGE_NUM = B.PAGE_NUM 
AND A.LINE_NUM = B.LINE_NUM 
AND A.ADDL_NBR = B.ADDL_NBR 
AND A.emplid = $EMPLID
and A.empl_rcd = $EMPL_RCD
and A.company = $COMPANY
and A.PAY_END_DT BETWEEN $pay_end_dt AND &O.JOB_EFFDT
END-SELECT

end-procedure get-average-hours-worked


!*********************************************************************
begin-procedure get-current-pay-deductions
#debugd show '...get-current-pay-deductions'

  let $MONTH_DEDUCT = 0
  let #MONTH_DEDUCT = 0
  let $WEEK_DEDUCT = 0
  let #WEEK_DEDUCT = 0
  let #sum_week_ded_amt=0
  let #sum_month_ded_amt=0

begin-select 
GEN.DEDCD
gen.ded_addl_amt
!sum(gen.ded_addl_amt) &gen.ded_addl_amt

    do get-deduction-freq
    
    if isnull(&DF.PAY_FREQUENCY) or &DF.PAY_FREQUENCY='W'
       let #sum_week_ded_amt = #sum_week_ded_amt + &gen.ded_addl_amt
       let #WEEK_DEDUCT = #sum_week_ded_amt
       multiply 1 times #WEEK_DEDUCT round=2
       let $WEEK_DEDUCT = #WEEK_DEDUCT
    end-if
    
    if &DF.PAY_FREQUENCY='M'
        let #sum_month_ded_amt = #sum_month_ded_amt + &gen.ded_addl_amt
        let #MONTH_DEDUCT = #sum_month_ded_amt
        multiply 1 times #MONTH_DEDUCT round=2
        let $MONTH_DEDUCT = #MONTH_DEDUCT
    END-IF

FROM ps_genl_deduction gen
WHERE [$SlctDedCriteria1] !GEX001
  gen.emplid    = $emplid !GEX001
  and gen.company = $COMPANY !GEX001
  and gen.effdt     = 
         (select max(gen1.effdt) 
          FROM PS_GENL_DEDUCTION gen1
          WHERE gen1.emplid    = gen.emplid
            and gen1.company   = gen.company             
            and gen1.dedcd     = gen.dedcd
            and gen1.effdt     < &O.JOB_EFFDT) 
  and gen.deduction_end_dt is null
  
end-select
end-procedure get-current-pay-deductions

begin-procedure get-deduction-freq
begin-select
DF.PAY_FREQUENCY

from ps_deduction_freq DF
where DF.DEDCD=&GEN.DEDCD
AND DF.EFFDT=(SELECT MAX(DF1.EFFDT) FROM PS_DEDUCTION_FREQ DF1
WHERE DF1.PLAN_TYPE=DF.PLAN_TYPE
AND DF1.DEDCD=DF.DEDCD
AND DF1.EFFDT<= &O.JOB_EFFDT)
end-select
end-procedure get-deduction-freq
!-----------------------------------------------------------------------
! Function:    get-benefit-service-date                                -
! Description: Get Giant Eagles Benefits Service dates                 -
! Called By:   job-file, pre-dep-file                                  -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-benefit-service-date
#debugd show '...get-benefit-service-date'

Begin-Select

PD.SERVICE_DT

    let $eben_service_dt = &PD.SERVICE_DT

FROM ps_per_org_asgn PD		
Where PD.Emplid = $Emplid 
and PD.PER_ORG = 'EMP'


End-Select

end-procedure

!-----------------------------------------------------------------------
! Function:    write-header-job-file                                   -
! Description: Write header record for output file, {job_file}         -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure write-header-job-file
  
        write 3 from    $Quote 'NATIONAL_ID        '  $Quote $Comma
                        $Quote 'NAME               '  $Quote $Comma
                        $Quote 'LAST_NAME          '  $Quote $Comma
                        $Quote 'MIDDLE_NAME        '  $Quote $Comma
                        $Quote 'FIRST_NAME         '  $Quote $Comma
                        $Quote 'BIRTHDATE          '  $Quote $Comma
                        $Quote 'ADDRESS1           '  $Quote $Comma
                        $Quote 'ADDRESS2           '  $Quote $Comma
                        $Quote 'CITY               '  $Quote $Comma
                        $Quote 'STATE              '  $Quote $Comma
                        $Quote 'POSTAL             '  $Quote $Comma
                        $Quote 'PHONE              '  $Quote $Comma
                        $Quote 'SEX                '  $Quote $Comma
                        $Quote 'MAR_STATUS         '  $Quote $Comma
                        $Quote 'EMPLID             '  $Quote $Comma
                        $Quote 'EMPL_RCD           '  $Quote $Comma
                        $Quote 'LAST_HIRE_DT       '  $Quote $Comma
                        $Quote 'JOBCODE            '  $Quote $Comma
                        $Quote 'DESCR              '  $Quote $Comma
                        $Quote 'DEPTID             '  $Quote $Comma
                        $Quote 'EMPL_TYPE          '  $Quote $Comma
                        $Quote 'FULL_PART_TIME     '  $Quote $Comma
                        $Quote 'STD_HOURS          '  $Quote $Comma
                        $Quote 'ANNUAL_RT          '  $Quote $Comma
                        $Quote '52                 '  $Quote $Comma
                        $Quote 'PAY_FREQUENCY      '  $Quote $Comma
                        $Quote 'FWT_MAR_STATUS     '  $Quote $Comma
                        $Quote 'FWT_ALLOWANCES     '  $Quote $Comma
                        $Quote 'SWT_MAR_STATUS     '  $Quote $Comma
                        $Quote 'SWT_ALLOWANCES     '  $Quote $Comma
                        $Quote 'COMPANY            '  $Quote $Comma
                        $Quote 'PAYGROUP           '  $Quote $Comma
                        $Quote 'EMPL_CLASS         '  $Quote $Comma
                        $Quote 'REG_TEMP           '  $Quote $Comma
                        $Quote 'EMPL_STATUS        '  $Quote $Comma
                        $Quote 'UNION_CD           '  $Quote $Comma
                        $Quote 'OFFICER_CD         '  $Quote $Comma
                        $Quote 'ELIG_CONFIG1       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG2       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG3       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG4       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG5       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG6       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG7       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG8       '  $Quote $Comma
                        $Quote 'ELIG_CONFIG9       '  $Quote $Comma  
                        $Quote 'ANNL_BENEF_BASE_RT '  $Quote $Comma
                        $Quote 'LOCATION           '  $Quote $Comma
                        $Quote 'BENEFIT_PROGRAM    '  $Quote $Comma
                        $Quote 'AVERAGE HOURS      '  $Quote $Comma
                        $Quote 'EBENE SERVICE DT   '  $Quote $Comma
                        $Quote 'MONTHLY DEDUCTIONS '  $Quote $Comma
                        $Quote 'WEEKLY DEDUCTIONS  '  $Quote $Comma  !GEX001
                        $Quote 'HOURLY_RT          '  $Quote         !GEX001
                        
end-procedure !write-header-job-file

                                              
!-----------------------------------------------------------------------
! Function:    write-row-job-file                                      -
! Description: Write Data to output file, {job_file}                   -
! Called By:   job-file                                                -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure write-row-job-file
#debug7 show 'write-row-job-file emplid ' $EMPLID


        write 3 from    $Quote $NATIONAL_ID        $Quote $Comma
                        $Quote $NAME               $Quote $Comma
                        $Quote $LAST_NAME          $Quote $Comma
                        $Quote $MIDDLE_NAME        $Quote $Comma
                        $Quote $FIRST_NAME         $Quote $Comma
                        $Quote $BIRTHDATE          $Quote $Comma
                        $Quote $ADDRESS1           $Quote $Comma
                        $Quote $ADDRESS2           $Quote $Comma
                        $Quote $CITY               $Quote $Comma
                        $Quote $STATE              $Quote $Comma
                        $Quote $POSTAL             $Quote $Comma
                        $Quote $PHONE              $Quote $Comma
                        $Quote $SEX                $Quote $Comma
                        $Quote $MAR_STATUS         $Quote $Comma
                        $Quote $GEX-EMPLID         $Quote $Comma       
                        $Quote $EMPL_RCD           $Quote $Comma
                        $Quote $LAST_HIRE_DT       $Quote $Comma       
                        $Quote $JOBCODE            $Quote $Comma
                        $Quote $DESCR              $Quote $Comma
                        $Quote $DEPTID             $Quote $Comma
                        $Quote $EMPL_TYPE          $Quote $Comma
                        $Quote $FULL_PART_TIME     $Quote $Comma
                        $Quote $STD_HOURS          $Quote $Comma
                        $Quote $ANNUAL_RT          $Quote $Comma
                        $Quote $52                 $Quote $Comma
                        $Quote $PAY_FREQUENCY      $Quote $Comma
                        $Quote $FWT_MAR_STATUS     $Quote $Comma
                        $Quote $FWT_ALLOWANCES     $Quote $Comma
                        $Quote $SWT_MAR_STATUS     $Quote $Comma
                        $Quote $SWT_ALLOWANCES     $Quote $Comma
                        $Quote $COMPANY            $Quote $Comma
                        $Quote $PAYGROUP           $Quote $Comma
                        $Quote $EMPL_CLASS         $Quote $Comma
                        $Quote $REG_TEMP           $Quote $Comma
                        $Quote $EMPL_STATUS        $Quote $Comma
                        $Quote $UNION_CD           $Quote $Comma
                        $Quote $OFFICER_CD         $Quote $Comma
                        $Quote $ELIG_CONFIG1       $Quote $Comma
                        $Quote $ELIG_CONFIG2       $Quote $Comma
                        $Quote $ELIG_CONFIG3       $Quote $Comma
                        $Quote $ELIG_CONFIG4       $Quote $Comma
                        $Quote $ELIG_CONFIG5       $Quote $Comma
                        $Quote $ELIG_CONFIG6       $Quote $Comma
                        $Quote $ELIG_CONFIG7       $Quote $Comma
                        $Quote $ELIG_CONFIG8       $Quote $Comma
                        $Quote $ELIG_CONFIG9       $Quote $Comma
                        $Quote $ANNL_BENEF_BASE_RT $Quote $Comma
                        $Quote $LOCATION           $Quote $Comma
                        $Quote $BENEFIT_PROGRAM    $Quote $Comma
                        $Quote $AVG_HOURS          $Quote $Comma
                        $Quote $EBEN_SERVICE_DT    $Quote $Comma
                        $Quote $MONTH_DEDUCT       $Quote $Comma
                        $Quote $WEEK_DEDUCT        $Quote $Comma !GEX001
                        $Quote $HOURLY_RT          $Quote        !GEX001

end-procedure !write-row-job-file


!-----------------------------------------------------------------------
! Function:    pre_dep_file                                            -
! Description: Process Pre-dependent Data and write it out             -
! Called By:   Report                                                  -
! Calls:       write-row-pre-dep                                       -
!              GET-GEX-EMPLID                                          -
!              GET-PERSNID-DEP                                         -
!-----------------------------------------------------------------------
begin-procedure pre-dep-file
#debugd show 'pre-dep-file'
 
begin-select
DB.EMPLID 
DB.DEPENDENT_BENEF 
DB.NAME 
DB.RELATIONSHIP 
DB.DEP_BENEF_TYPE 
DB.MAR_STATUS 
DB.SEX 
DB.BIRTHDATE 
DB.STUDENT 
DB.DISABLED 
DB.STUDENT_STATUS_DT 
DB.SMOKER
DB.LAST_NAME      
DB.FIRST_NAME     
DB.MIDDLE_NAME      

        let $EMPLID              = &DB.EMPLID              
        let $DEPENDENT_BENEF     = &DB.DEPENDENT_BENEF     
        let $NAME                = &DB.NAME                
        let $RELATIONSHIP        = &DB.RELATIONSHIP        
        let $DEP_BENEF_TYPE      = &DB.DEP_BENEF_TYPE      
        let $MAR_STATUS          = &DB.MAR_STATUS          
        let $SEX                 = &DB.SEX                 
        let $BIRTHDATE           = &DB.BIRTHDATE           
        let $STUDENT             = &DB.STUDENT             
        let $DISABLED            = &DB.DISABLED            
        let $STUDENT_STATUS_DT   = &DB.STUDENT_STATUS_DT   
        let $SMOKER              = &DB.SMOKER  
        let $LAST_NAME           = &DB.LAST_NAME         
        let $FIRST_NAME          = &DB.FIRST_NAME        
        let $MIDDLE_NAME         = &DB.MIDDLE_NAME  
                              
        DO GET-GEX-EMPLID
        DO GET-PERSNID-DEP  
        DO GET-DEP-CERT
        DO WRITE-ROW-PRE-DEP

FROM PS_DEPENDENT_BENEF DB, PS_JOB JDB
WHERE DB.EMPLID=JDB.EMPLID
  AND DB.EMPLID=&O.EMPLID
  AND JDB.EMPL_STATUS IN ('A','L','P','S')
  AND JDB.EFFDT       = (SELECT MAX(JDB1.EFFDT) FROM PS_JOB JDB1
                        WHERE JDB1.EMPLID   = JDB.EMPLID 
                          AND JDB1.EMPL_RCD = JDB.EMPL_RCD 
                          AND JDB1.EFFDT <= &O.JOB_EFFDT) 
  AND JDB.EFFSEQ      = (SELECT MAX(JDB2.EFFSEQ) FROM PS_JOB JDB2
                        WHERE JDB2.EMPLID   = JDB.EMPLID 
                          AND JDB2.EMPL_RCD = JDB.EMPL_RCD 
                          AND JDB2.EFFDT    = JDB.EFFDT) 
  AND JDB.COMPANY <> JDB.PAYGROUP
ORDER BY DB.EMPLID
end-select

end-procedure pre-dep-file


!-----------------------------------------------------------------------
! Function:    get-persnid-dep                                         -
! Description: Get persnid information                                 -
! Called By:   pre-dep-file                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure get-persnid-dep
#debugd show '...get-persnid-dep'

        let $NATIONAL_ID = ' '
        
begin-select
DBN.NATIONAL_ID 

        let $NATIONAL_ID = &DBN.NATIONAL_ID  

FROM PS_DEP_BENEF_NID DBN
WHERE DBN.EMPLID          = &DB.EMPLID 
  AND DBN.DEPENDENT_BENEF = &DB.DEPENDENT_BENEF 
end-select

end-procedure get-persnid-dep

!-----------------------------------------------------------------------
! Function:    GET-DEP-CERT                                            -
! Description: Get persnid information                                 -
! Called By:   pre-dep-file                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Get-Dep-Cert
#debugd show '...Get-Dep-Cert'
begin-SELECT
C.GEX_DEP_CERT

        LET $DEP_CERT=&C.GEX_DEP_CERT

FROM PS_DEP_BEN_EFF C
WHERE C.EMPLID=&DB.EMPLID
AND C.DEPENDENT_BENEF=&DB.DEPENDENT_BENEF
AND C.EFFDT=(SELECT MAX(C1.EFFDT) FROM PS_DEP_BEN_EFF C1
              WHERE C1.EMPLID=C.EMPLID
              AND C1.DEPENDENT_BENEF=C.DEPENDENT_BENEF
              AND C1.EFFDT<= &O.JOB_EFFDT)
end-select
end-procedure


!-----------------------------------------------------------------------
! Function:    write-header-pre-dep                                    -
! Description: Write header record for output file, {pre_dep_file}     -
! Called By:   pre-dep-file                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure write-header-pre-dep
  
        write 4 from    $Quote 'EMPLID             ' $Quote $Comma
                        $Quote 'DEPENDENT_BENEF    ' $Quote $Comma
                        $Quote 'NAME               ' $Quote $Comma
                        $Quote 'LAST_NAME          ' $Quote $Comma
                        $Quote 'FIRST_NAME         ' $Quote $Comma  
                        $Quote 'MIDDLE_NAME        ' $Quote $Comma
                        $Quote 'RELATIONSHIP       ' $Quote $Comma
                        $Quote 'DEP_BENEF_TYPE     ' $Quote $Comma
                        $Quote 'MAR_STATUS         ' $Quote $Comma
                        $Quote 'SEX                ' $Quote $Comma
                        $Quote 'BIRTHDATE          ' $Quote $Comma
                        $Quote 'STUDENT            ' $Quote $Comma
                        $Quote 'DISABLED           ' $Quote $Comma
                        $Quote 'STUDENT_STATUS_DT  ' $Quote $Comma
                        $Quote 'NATIONAL_ID        ' $Quote $Comma
                        $Quote 'SMOKER             ' $Quote $Comma
                        $Quote 'GEX_DEP_CERT       ' $Quote $Comma !GEX001
                        $Quote '10 DIGIT EMPLID    ' $Quote        !GEX001
   
end-procedure !write-header-pre-dep

                                              
!-----------------------------------------------------------------------
! Function:    write-row-pre-dep                                       -
! Description: Write Data to output file, {pre_dep_file}               -
! Called By:   pre-dep-file                                            -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure write-row-pre-dep
 
        write 4 from    $Quote $GEX-EMPLID          $Quote $Comma          
                        $Quote $DEPENDENT_BENEF     $Quote $Comma
                        $Quote $NAME                $Quote $Comma
                        $Quote $LAST_NAME           $Quote $Comma
                        $Quote $FIRST_NAME          $Quote $Comma  
                        $Quote $MIDDLE_NAME         $Quote $Comma
                        $Quote $RELATIONSHIP        $Quote $Comma
                        $Quote $DEP_BENEF_TYPE      $Quote $Comma
                        $Quote $MAR_STATUS          $Quote $Comma
                        $Quote $SEX                 $Quote $Comma
                        $Quote $BIRTHDATE           $Quote $Comma
                        $Quote $STUDENT             $Quote $Comma
                        $Quote $DISABLED            $Quote $Comma
                        $Quote $STUDENT_STATUS_DT   $Quote $Comma
                        $Quote $NATIONAL_ID         $Quote $Comma
                        $Quote $SMOKER              $Quote $Comma
                        $Quote $DEP_CERT            $Quote $Comma !GEX001
                        $Quote $EMPLID              $Quote        !GEX001
                                                   
end-procedure !write-row-pre-dep

!-----------------------------------------------------------------------
! Function:    Clear-Variables                                         -
! Description: clears values                                           -
! Called By:   Create-Benefit-Options-File                             -
! Calls:       None                                                    -
!-----------------------------------------------------------------------
begin-procedure Clear-Variables
#debugd show '...Clear-Variables'
  
   LET $SCHED_ID           = ' '
   LET $EMPLID             = ' '
   LET $BENEFIT_RCD_NBR    = ' '
   LET $EVENT_ID           = ' '
   !LET $STATUS_DT          = ' '
   LET $EVENT_CLASS        = ' '
   LET $BAS_PROCESS_STATUS = ' '
   LET $PLAN_TYPE          = ' '
   LET $BENEFIT_PROGRAM    = ' '
   LET $EVENT_DT           = ' '
   LET $COVERAGE_ELECT     = ' '
   LET $ELECT_ALLOWED      = ' '
   LET $OPTION_CD          = ' '
   LET $EMPL_CONTRBUTN_AMT = ' '
   LET $ANNUAL_PLEDGE      = ' '
   LET $GOAL_AMT           = ' '
   LET $IGNORE_PLAN        = ' '
   LET $HLTH_PROVIDER_ID   = ' '
   LET $PREVIOUSLY_SEEN    = ' '
   Let $DEP_BENEF_TYPE     = ' '

end-procedure Clear-Variables

!-----------------------------------------------------------------------
! SQC Files for called procedures                                      -
!-----------------------------------------------------------------------
#include 'stdapi.sqc'
#include 'reset.sqc'     !Reset printer procedure
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines to calculate with dates
#include 'getlogou.sqc'  !Routine process run control parameters 
! -------------------  END OF FILE   ---------------------------------
