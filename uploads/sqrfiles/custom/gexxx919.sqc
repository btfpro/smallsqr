!**************************************************************************
!  GEXXX919:  Retrieve "Multiple" DEDCD Run Controls and                  *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX919_DEDCD_Alias                                                *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX919_INCLUDE_DEDCD_CRITERIA                                     *
!    $GEXXX919_EXCLUDE_DEDCD_CRITERIA                                     *
!    $GEXXX919_INCLUDE_DEDCD_DISPLAY_CRITERIA                             *
!    $GEXXX919_EXCLUDE_DEDCD_DISPLAY_CRITERIA                             *
!                                                                         *                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX919_DEDCD_Alias = 'J.DEDCD'                                *
!    Do GEXXX919-Select-DEDCD-Parameters                                  *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE EMPL_STATUS = 'A'                                              *
!    AND [$GEXXX919_EXCLUDE_DEDCD_CRITERIA]                               *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		03/12/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX919-Select-DEDCD-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_DEDCD_ROWS = 0
  Let $GEXXX919_INCLUDE_DEDCD_CRITERIA = ''
  Let $GEXXX919_EXCLUDE_DEDCD_CRITERIA = ''
  Let $GEXXX919_INCLUDE_DEDCD_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX919_EXCLUDE_DEDCD_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_DEDCD.DEDCD

  Add 1 To #GEX_RC_DEDCD_ROWS

  If #GEX_RC_DEDCD_ROWS = 1
    Let $GEXXX919_INCLUDE_DEDCD_CRITERIA = '(' || $GEXXX919_DEDCD_Alias || ' In ('''     || &GEX_RC_DEDCD.DEDCD || ''''
    Let $GEXXX919_EXCLUDE_DEDCD_CRITERIA = '(' || $GEXXX919_DEDCD_Alias || ' Not In (''' || &GEX_RC_DEDCD.DEDCD || ''''
  Else
    Let $GEXXX919_INCLUDE_DEDCD_CRITERIA = $GEXXX919_INCLUDE_DEDCD_CRITERIA || ',''' || &GEX_RC_DEDCD.DEDCD  || ''''
    Let $GEXXX919_EXCLUDE_DEDCD_CRITERIA = $GEXXX919_EXCLUDE_DEDCD_CRITERIA || ',''' || &GEX_RC_DEDCD.DEDCD  || ''''
  End-If

From  PS_GEX_RC_DEDCD GEX_RC_DEDCD

Where GEX_RC_DEDCD.OPRID                = [$OprID]
And   GEX_RC_DEDCD.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_DEDCD.DEDCD, ' ') != ' '

End-Select

  If #GEX_RC_DEDCD_ROWS = 0
    Let $GEXXX919_INCLUDE_DEDCD_CRITERIA = $GEXXX919_DEDCD_Alias || ' = ' || $GEXXX919_DEDCD_Alias
    Let $GEXXX919_EXCLUDE_DEDCD_CRITERIA = $GEXXX919_DEDCD_Alias || ' = ' || $GEXXX919_DEDCD_Alias
  Else
    Let $GEXXX919_INCLUDE_DEDCD_CRITERIA = $GEXXX919_INCLUDE_DEDCD_CRITERIA || '))'
    Let $GEXXX919_EXCLUDE_DEDCD_CRITERIA = $GEXXX919_EXCLUDE_DEDCD_CRITERIA || '))'
    Let $GEXXX919_INCLUDE_DEDCD_DISPLAY_CRITERIA = $GEXXX919_INCLUDE_DEDCD_CRITERIA
    Let $GEXXX919_EXCLUDE_DEDCD_DISPLAY_CRITERIA = $GEXXX919_EXCLUDE_DEDCD_CRITERIA
  End-If

End-Procedure
