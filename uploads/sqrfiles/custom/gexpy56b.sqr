!***********************************************************************
!  GEXPY056:  Payroll Check Print - USA - With Micr Code               *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! GiantEagle, Inc.; it is not to be copied, reproduced, or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of GiantEagle.                        *
!                                                                      *
!***********************************************************************
!                                                                      *
!      $Date:: 09/10/14         $                                      *
!  $Revision:: 01               $                                      *
!  $Workfile:: GEXPY56b.SQR     $                                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
! Function: Generates paychecks and earnings statements for a given    *
!           RUN_ID or (partial) pay calendar for B Run.                *
!                                                                      *
! Comments: Utilizes a bank microcode soft font and a custom signature *
!           font catridge to allow printing of the entire check on an  *
!           HP LaserJet printer so equipped and configured.            *
!           Modified the Pay Check sort  to print  according           *
!           to the Home-Deptid,Deptid, Sort order and Paygroup         *   
!                                                                      *
!***********************************************************************
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  ID#     INITIALS    DATE           DESCRIPTION OF THE CHANGE	       *
!***********************************************************************
!  GEXPY56b   Surya Sobha      09/10/2014     INITIAL CREATION         *
!  CSR1578    Surya Sobha      07/07/2015     Changed the col value for* 
!		the MICR Font and MICR Line from 11 to 8, as we        *
!		installed the new printer for the paycheck printing    *
!GEX_SEC_REENG Surya Sobha     06/19/2015                              *
!	Changes related to gex_emplid_mapp/7 digit emplid              *
!       Changes releated to Peopletools Upgrade 8.52                   *
!***********************************************************************

#include 'setenv.sqc'    !set database environment
#include 'setup07.sqc'
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'getdptnm.sqc'  !Get-Department-Name procedure
#include 'getjobtl.sqc'  !Get-Job-Title procedure
#include 'getlcdta.sqc'  !Get-Local-Tax-Data
#include 'getlocnm.sqc'  !Get-Location-Name procedure
#include 'ldnumlit.sqc'  !Load-NumberLits-Array procedure
#include 'netinwds.sqc'  !Net-Pay-In-Words procedure
#include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'rotname1.sqc'  !Rotate-Name procedure
#include 'datetime.sqc'  !general date and time formatting procedures
#include 'number.sqc'    !general number formatting procedures
#include 'payinit.sqc'   !Payroll-Report-Initialization procedure
#include 'payrnctl.sqc'  !Get-Run-Control procedure
#include 'stdapi.sqc'    !StdAPI-Init procedure
#include 'reset.sqc'     !Reset printer procedure
#include 'getbalid.sqc'  !Get-Calendar-Year-Id
#include 'gexaltse.sqc'  ! Alter session SQC
#Include 'getactnm.sqc'  !Get-Acctcd-Name Procedure
#Include 'gexpydt.sqc'   !Modify the pay period begin and end dates

!***********************************************************************
begin-setup
!***********************************************************************

#ifndef MVS
  printer-init <27>E<27>&l0L
  !            |    |--> Perforation Skip
  !            |--> Reset

  declare procedure
    before-report = Init-Page-Size
#endif

  page-size 85 131
end-setup

!***********************************************************************
begin-procedure Init-Page-Size
!***********************************************************************
! This sends a 'Perforation Skip' command to disable any bottom
! margin and obtain the maximum PCL page.  This is required to
! print the MICR line sufficiently close to the bottom of the
! form to be correctly positioned on the check.

  encode '<27>&l0L' into $perforation_skip

  encode '<27>&l4H' into $chktray
  encode '<27>&l2H' into $septray
  print $chktray () code

end-procedure


!***********************************************************************
begin-procedure Init-Variables
!***********************************************************************

  let $ReportID    = 'GEXPY056'
  let $ReportTitle = 'Payroll Check Print'
  let $Country     = 'USA'

  let #StdFont     = 5                  !Times Roman proportional
  let #StmtPntSize = 7                  !statement point-size
  let #ChkPntSize  = 9                  !check point-size

!printer escape sequence for the check signature font

  encode '<27>(0U<27>(s1p33.0v0s0b100T' into $SigFont
  encode '<27>(0U<27>(s0p8.00h9.00v0s0b0T' into $micrfont

!the bank city/state prefix as printed on the check as part of the
!hyphenated numerator of the fractional bank routing number

  let $BankDistrict = '56'              !for paygroup $BankID

end-procedure


!***********************************************************************
begin-procedure Init-Arrays
!***********************************************************************

  #define m  6  !number of separate detail sections
  #define n 13  !maximum number of detail entries per section

  let #RatesRow       = 0
  let #HoursRow       = 1
  let #EarnsRow       = 2
  let #PreTaxDedsRow  = 3
  let #TaxesRow       = 4
  let #BenefitsRow    = 5
  let #lastRow  = {m}-1
  let #lastCol  = 2*({n}-1)  
  #define new_n 26

  create-array name  = ETDarray         -
               size  = {m}                -
               field = Code:char:{new_n}  -
               field = Name:char:{new_n}  -
               field = Cur:number:{new_n} -
               field = YTD:number:{new_n}

  let $lastCode = '***'
  let $lastName = 'Other'
  let #lastRate = 0

  do Load-NumberLits-Array  !load literals for converting Net Pay to words

end-procedure


!***********************************************************************
begin-report
!***********************************************************************

  do Init-DateTime

  show ' '
  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '

  do Init-Number
  do Get-Calendar-Year-Id
  do StdAPI-Init
  do Init-Variables
  do Init-Arrays
  do Payroll-Report-Initialization      !calls "Report" procedure

! Load Lookups for Deduction names and Earnings names

  LOAD-LOOKUP
    NAME=deductions_lookup
    ROWS=200
    TABLE='ps_deduction_tbl dt'
    KEY='dt.PLAN_TYPE||dt.DEDCD'
    RETURN_VALUE='dt.DESCR||''&%&''||dt.DESCRSHORT||''&%&''||dt.SPCL_PROCESS'
    WHERE='dt.EFFDT=(SELECT MAX(DD.EFFDT) FROM PS_DEDUCTION_TBL DD WHERE DD.PLAN_TYPE=dt.PLAN_TYPE AND DD.DEDCD=dt.DEDCD AND DD.EFFDT <= SYSDATE)'

  LOAD-LOOKUP
    NAME=earnings_lookup
    ROWS=200
    TABLE='ps_earnings_tbl et'
    KEY='et.ERNCD'
    RETURN_VALUE='et.DESCR||''&%&''||et.DESCRSHORT'
    WHERE='et.EFFDT=(SELECT MAX(EE.EFFDT) FROM PS_EARNINGS_TBL EE WHERE EE.ERNCD = et.ERNCD AND EE.EFFDT <= SYSDATE)'

  let $mail_drop_flag = ' '

  let $Where = 'AND P.MAIL_DROP <> ''XHOME'' AND D.DEPTID_HOME    <> ''9999'''
  show 'Where 1 : ' $Where
  let $OrderBy = 'ORDER BY D.DIVISION DESC, D.DEPTID_HOME DESC , D.SORT_ORDER DESC, B.COMPANY, B.PAYGROUP , B.PAYCHECK_NAME'
  do Get-Paychecks
  
   
  let $Where = 'AND (P.MAIL_DROP = ''XHOME'' OR  D.DEPTID_HOME    = ''9999'')'
  show 'Where 2 : ' $Where
  let $OrderBy = 'ORDER BY D.DEPTID, B.COMPANY, B.PAYGROUP , B.PAYCHECK_NAME'
  let $mail_drop_flag = 'Y'
  do Get-Paychecks

  display 'Total Checks  ' noline
  display #TotalChecks  999,999,999
  display 'Total Net Pay ' noline
  display #TotalNetPay  $$$,$$$,$$9.99mi

  date-time () HH:MI:SS &timeended
  date-time () MM/DD/YYYY &dateended
  show ' '
  show 'Report Ended at ' &timeended ' on ' &dateended
  show ' '

  do StdAPI-Term
  do Reset

end-report


!***********************************************************************
begin-procedure Report
!***********************************************************************

  if $Prcs_Process_Instance = ''
    display 'Enter a Beginning Check No. if restarting the run; ' noline
    display 'otherwise leave blank'
    input $BeginCheck# maxlen=8 'Enter Beginning Check No.' type=integer
    move  $BeginCheck# to #BeginCheck#

    input $ThruCheck# maxlen=8 'Enter Ending Check No.' type=integer
    if rtrim($thrupaycheck_nbr,' ') <> ''
       move  $ThruCheck# to #ThruCheck#
    else
       let $ThruCheck = '9999999'
       move $ThruCheck to #ThruCheck#
    end-if

    input $check_numbers maxlen=7 'Enter Individual Check#. or leave blank' type=integer
    while rtrim($check_numbers,' ') <> ''

       add 1 to #check_count_1
       if #check_count_1 = 1
          let $check_string = $check_string|| ' AND '||'((B.PAYCHECK_NBR = '||$check_numbers||')'
       else
          let $check_string = $check_string|| ' OR '||'(B.PAYCHECK_NBR = '||$check_numbers||')'
       end-if
       input $check_numbers maxlen=7 'Enter Individual Check#. or leave blank' type=integer
    end-while
    if #check_count_1 = 0
       let $check_string = $check_string||')'
    else
       let $check_string = $check_string||'))'
    end-if
    move $check_String to $GEXXX932_INCLUDE_CHECK_CRITERIA
    
    input $CheckMessage maxlen=110 'Enter Check Message'
  else
     move #RC_PAYINIT.Chkadv_No_Start to #BeginCheck# 	
     move #RC_PAYINIT.Chkadv_No_Thru  to #ThruCheck#  	

     let $GEXXX932_Check_Alias = 'B.PAYCHECK_NBR'
     Do GEXXX932-Select-Check-Parameters
     let $GEXXX932_INCLUDE_CHECK_CRITERIA = 'AND '||$GEXXX932_INCLUDE_CHECK_CRITERIA||')'
     move $RC_PAYINIT.Message_Text   to $CheckMessage
  end-if

  display $GEXXX932_INCLUDE_CHECK_CRITERIA

end-procedure


!***********************************************************************
begin-procedure Get-Paychecks
!***********************************************************************

  let $first_chk_adv = 'Y'		

begin-SELECT
D.DEPTID_HOME,
D.DIVISION,
D.SORT_ORDER,
A.COMPANY,
A.PAYGROUP,
A.PAY_BEGIN_DT,
A.PAY_END_DT,
A1.BALANCE_ID, 
A1.BALANCE_QTR,
A1.BALANCE_YEAR,
A1.BALANCE_PERIOD,
A.CHECK_DT,
B.OFF_CYCLE,
B.PAGE_NUM,
B.LINE_NUM,
B.SEPCHK,
B.FORM_ID,
B.PAYCHECK_NBR,
B.CHECK_DT,
B.EMPLID,
B.EMPL_RCD,
B.NAME,
B.PAYCHECK_NAME,
B.ADDRESS1,
B.ADDRESS2,
B.CITY,
B.STATE,
B.POSTAL,
B.UPDATE_DT,
B.PAYCHECK_OPTION,
B.PAYCHECK_ADDR_OPTN,
B.TOTAL_GROSS,
B.TOTAL_TAXES,
B.TOTAL_DEDUCTIONS,
B.NET_PAY,
B.PAY_SHEET_SRC, 
B.DEPTID,
B.BENEFIT_RCD_NBR,
G.EFFDT,
Y.TOTAL_GROSS_YTD,
Y.TOTAL_TAXES_YTD,
Y.TOTAL_DEDNS_YTD,
Y.NET_PAY_YTD

!  display 'In the main'

  if $first_chk_adv = 'Y'
     display ' '
     display ' '
     display '***************************************************'
     display 'Beginning Check # ' noline
     display &B.PAYCHECK_NBR
     display '***************************************************'
     display ' '
     display ' '
     let $first_chk_adv = 'N'
  end-if
  
    move &D.DEPTID_HOME to $deptid_home
    move &D.DIVISION to $division
 
    move &A.Company    to $Company
    move &A.Pay_End_Dt to $AsOfDate
    do Get-Company-Data
    move ''            to $PayGroup
 

    move &A.PayGroup   to $PayGroup
    move &A.Pay_End_Dt to $AsOfDate
    do Get-PayGroup-Data

  do Reset-Variables
  do Get-Ee-Job-Data

  if (rtrim($mail_drop_flag,' ') = '')
    if ($division <> $prev_division) and (rtrim($prev_division,' ') <> '')
    show '$division: ' $division
    show '$prev_division: ' $prev_division
    
        print $septray () code
    	new-page
    	print $chktray () code
    	print $septray () code
    	new-page
    	print $chktray () code
    	print $septray () code
    	new-page
    	print $chktray () code
    	print $septray () code
    	new-page
    	print $chktray () code
    	    	
    end-if
    move $division to $prev_division
  
    if ($deptid_home <> $prev_deptid_home) and (rtrim($prev_deptid_home,' ') <> '')
        print $septray () code
    	new-page
    	print $chktray () code
    end-if
  end-if
  	move $deptid_home to $prev_deptid_home

  if $mail_drop_flag = 'Y' 
    let $mail_drop_flag = 'N'
    print $septray () code
    new-page
    print $chktray () code
    print $septray () code
    new-page
    print $chktray () code
    print $septray () code
    new-page
    print $chktray () code
    print $septray () code
    new-page
    print $chktray () code
    print $septray () code
    new-page
    print $chktray () code
  end-if

  move &B.Deptid to $Prev_Deptid
  move $mail_drop to $Prev_mail_drop

  do Get-Ee-Tax-Data
  do Get-Earnings-Detail
  do Get-Taxes-Detail
  do Get-Deductions-Detail

  if &B.PayCheck_Option = '&'           !both advice and check
    do Get-Net-Pay-Distribution
  else
    move &B.Net_Pay to #CheckNetPay
  end-if

  do Get-Vacation-Balances

   move &A.PAY_BEGIN_DT to $A.PAY_BEGIN_DT 
   move &A.PAY_END_DT to $A.PAY_END_DT 
  
   do paydates   
  let $extra_page = 'N'

  do Print-Statement

  if $extra_page = 'Y'
     do print-extra-page
  end-if

  Let $EE_Had_TBP_Erncd535 = 'N'          
  
  do print-check

  add 1            to #TotalChecks
  add #CheckNetPay to #TotalNetPay



FROM  PS_PAY_CALENDAR A, PS_PAY_CAL_BAL_ID A1,
      PS_PAY_CHECK    B,
      PS_PAYGROUP_TBL G,
      PS_CHECK_YTD    Y,
      PS_GEX_DEPT_TBL D,
      PS_PAYROLL_DATA P
WHERE [$SlctCalendar]
  AND A.COMPANY = A1.COMPANY AND A.PAYGROUP = A1.PAYGROUP
  AND A.PAY_END_DT = A1.PAY_END_DT AND A1.BALANCE_ID = $Calendar_Year_Id
  AND B.OFF_CYCLE IN ($SlctOffCycleA, $SlctOffCycleB)
  AND B.PAGE_NUM BETWEEN #SlctPageFrom AND #SlctPageThru
  AND B.COMPANY         = A.COMPANY
  AND B.PAYGROUP        = A.PAYGROUP
  AND B.PAY_END_DT      = A.PAY_END_DT
  AND B.PAYCHECK_NBR         <> 0
  AND ((B.PAYCHECK_NBR         >= #BeginCheck#
  AND B.PAYCHECK_NBR         <= #ThruCheck#)
  [$GEXXX932_INCLUDE_CHECK_CRITERIA]
  AND B.PAYCHECK_OPTION IN ('C','&')
  AND B.PAYCHECK_STATUS = 'F'
  AND G.COMPANY         = A.COMPANY
  AND G.PAYGROUP        = A.PAYGROUP
  AND G.COUNTRY         = $Country
  AND Y.COMPANY         = A.COMPANY
  AND Y.BALANCE_ID   =  A1.BALANCE_ID
  AND Y.BALANCE_YEAR   =  A1.BALANCE_YEAR
  AND Y.BALANCE_QTR    = A1.BALANCE_QTR
  AND Y.BALANCE_PERIOD  = A1.BALANCE_PERIOD
  AND Y.EMPLID          = B.EMPLID
  AND G.EFFDT           =     (SELECT MAX(EFFDT)
       			FROM PS_PAYGROUP_TBL
     			WHERE COMPANY    = A.COMPANY
        		AND PAYGROUP   = A.PAYGROUP
        		AND EFF_STATUS = 'A'
        		AND EFFDT     <= A.PAY_END_DT)
  AND D.SETID           = 'COMMN'
  AND D.DEPTID          = B.DEPTID
  AND D.EFFDT           = (SELECT MAX(EFFDT)
       			FROM PS_GEX_DEPT_TBL D1
      			WHERE D1.SETID   = D.SETID
        		AND D1.DEPTID  = D.DEPTID
        		AND D1.EFFDT     <= A.PAY_END_DT) 
  AND D.SEQUENCE_NUMBER = (SELECT MAX(D2.SEQUENCE_NUMBER) 
     			FROM PS_GEX_DEPT_TBL D2 
     			WHERE D2.SETID=D.SETID 
     			AND D2.DEPTID=D.DEPTID 
     			AND D2.EFFDT=D.EFFDT)
  AND P.COMPANY        =   A.COMPANY
  AND P.EMPLID         =   B.EMPLID
  [$Where]
  [$OrderBy]
  
end-SELECT

  !New display msg 
  display ' '
  display ' '
  display '***************************************************'
  display 'Ending Check # ' noline
  display &B.PAYCHECK_NBR
  display '***************************************************'
  display ' '
  display ' '
  !New display msg

end-procedure

!***********************************************************************
begin-procedure Get-Company-Data
!***********************************************************************
begin-SELECT
CT.DESCR,
CT.ADDRESS1,
CT.ADDRESS2,
CT.CITY,
CT.STATE,
CT.postal

  let $CompanyName      = &CT.Descr
  let $CompanyCityState =
      rtrim(&CT.City,' ') ||' '|| rtrim(&CT.State,' ') ||'  '|| &CT.postal

  let $CompanyAddrLine1   = &CT.Address1
  let $CompanyAddrLine3   = ''
  if rtrim(&CT.Address2,' ') = ''
    let $CompanyAddrLine2 = $CompanyCityState
  else
    let $CompanyAddrLine2 = &CT.Address2
    let $CompanyAddrLine3 = $CompanyCityState
  end-if

FROM  PS_COMPANY_TBL CT
WHERE COMPANY = &A.Company
  AND EFFDT   =
    (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
      WHERE COMPANY = CT.COMPANY
        AND EFFDT  <= &A.Pay_End_Dt)
end-SELECT

end-procedure


!***********************************************************************
begin-procedure Get-PayGroup-Data
!***********************************************************************

begin-SELECT
PG.DESCR,
PG.ERNCD_REG_HRS,
PG.ERNCD_OT_HRS,
PG.ERNCD_REG_EARNS,
PG.SRC_BANK_ID, 
SB.SRC_BANK_ID,
SB.BANK_CD,
SB.ACCOUNT_EC_ID,
BT.DESCRSHORT,
BT.BANK_NM,     
BT.ADDRESS1,
BT.ADDRESS2,
BT.CITY,
BT.STATE,
BT.postal

  let $PayGroupName = &PG.Descr
  let $TransitNum   = &SB.BANK_CD
  let $AccountNum   = translate(rtrim(&SB.ACCOUNT_EC_ID,' '),'-','D')
  let $BankID       = $BankDistrict || '-' ||

                      SUBSTR(&SB.BANK_CD,6,3) || '/' ||
                      SUBSTR(&SB.BANK_CD,2,3)

  let $ErnCd = &PG.ErnCd_Reg_Hrs
  do lookup-erncd-name
  move $EarningsName to $EarnNameRegHrs
  let $ErnCd = &PG.ErnCd_OT_Hrs
  do lookup-erncd-name
  move $EarningsName to $EarnNameOTHrs
  let $ErnCd = &PG.ErnCd_Reg_Earns
  do lookup-erncd-name
  move $EarningsName to $EarnNameRegEarns
  let $BankName      = upper(&BT.BANK_NM)   
  let $BankCityState = 'Ashland, OH  44805'
  let $BankAddrLine1   = &BT.Address1
  let $BankAddrLine3   = ''
  if rtrim(&BT.Address2,' ') = ''
    let $BankAddrLine2 = $BankCityState
  else
    let $BankAddrLine2 = &BT.Address2
    let $BankAddrLine3 = $BankCityState
  end-if

FROM  PS_PAYGROUP_TBL PG,
      PS_SRC_BANK SB,
      PS_BANK_EC_TBL BT

WHERE PG.COMPANY  = &A.Company
  AND PG.PAYGROUP = &A.PayGroup
  AND PG.COUNTRY  = $Country
  AND PG.EFFDT    = &G.Effdt
  AND PG.SRC_BANK_ID = SB.SRC_BANK_ID
  AND BT.BANK_CD     = SB.BANK_CD
end-SELECT

end-procedure


!***********************************************************************
begin-procedure Get-Ee-Job-Data
!***********************************************************************

begin-SELECT

J.DEPTID,
J.JOBCODE,
J.LOCATION,
J.COMPRATE,
J.COMP_FREQUENCY, 
J.ACCT_CD, 
J.EMPL_TYPE
P.NAME,
P.ADDRESS1,
P.ADDRESS2,
P.CITY,
P.STATE,
P.Postal, 
EMP.MAIL_DROP
!P1.EMPLID	!GEX_SEC_REENG 06/19/2015 Surya Sobha
P.EMPLID	!GEX_SEC_REENG 06/19/2015 Surya Sobha
J.Position_nbr	    

  let $DeptID   = rtrim(&J.DeptID,' ')
  do Get-Department-Name
  let $AcctCD   = rtrim(&J.Acct_cd,' ')
  do Get-Acct-CD-Descr
  let $JobCode  = rtrim(&J.JobCode,' ')
  do Get-Job-Title
  
         Let $Position_Nbr = &J.Position_nbr
 	 Do Get-Position-Title			 !getposnm.sqc
  	 IF $PosName = '' 
  		Let $jobtitle = $jobtitle
	 Else
 		Let $jobtitle = $PosName
	 End-if
  
  let $Location = rtrim(&J.Location,' ')
  do Get-Location-Name

  let $FieldName  = 'COMP_FREQUENCY'
  let $FieldValue = &J.Comp_Frequency
  do Read-Translate-Table
  move $XLatLongName to $CompFrequency

  let $Name = &P.Name
  do Rotate-Name
  move $Name to $HomeName


  let $HomeCityState =
      rtrim(&P.City,' ') ||' '|| rtrim(&P.State,' ') ||'  ' || &P.postal


  let $HomeAddrLine1   = &P.Address1
  let $HomeAddrLine3   = ''
  if rtrim(&P.Address2,' ') = ''
    let $HomeAddrLine2 = $HomeCityState
  else
    let $HomeAddrLine2 = &P.Address2
    let $HomeAddrLine3 = $HomeCityState
  end-if

     let $mail_drop = rtrim(&emp.mail_drop,' ')
     !let $Emplid = &P1.EMPLID	!GEX_SEC_REENG 06/19/2015 Surya Sobha
     Let $Emplid = &P.EMPLID	!GEX_SEC_REENG 06/19/2015 Surya Sobha

FROM  PS_JOB           J,
      PS_PERSONAL_DATA P,
      !PS_GEX_EMPLID_MAPP P1,	!GEX_SEC_REENG 06/19/2015 Surya Sobha
      PS_PAYROLL_DATA EMP
WHERE !P1.SSN = J.EMPLID 	!GEX_SEC_REENG 06/19/2015 Surya Sobha
  !and P1.EMPL_RCD = J.EMPL_RCD !GEX_SEC_REENG 06/19/2015 Surya Sobha
  P.EMPLID = J.EMPLID
  AND EMP.EMPLID = J.EMPLID
  AND EMP.COMPANY = J.COMPANY
  AND J.EMPLID = &B.EmplID
  AND J.EMPL_RCD = &b.empl_rcd
  AND J.EFFDT  =
    (SELECT MAX(EFFDT)
       FROM PS_JOB
      WHERE EMPLID    = J.EMPLID
        AND EMPL_RCD = J.EMPL_RCD
        AND EFFDT    <= &A.Pay_End_Dt)
  AND J.EFFSEQ =
    (SELECT MAX(EFFSEQ)
       FROM PS_JOB
      WHERE EMPLID    = J.EMPLID
        AND EMPL_RCD = J.EMPL_RCD
        AND EFFDT     = J.EFFDT)
end-SELECT

end-procedure


!***********************************************************************
begin-procedure Get-Ee-Tax-Data
!***********************************************************************

begin-SELECT
FT.EFFDT
FT.FWT_MAR_STATUS
FT.FWT_ALLOWANCES
FT.FWT_ADDL_AMT
FT.FWT_ADDL_PCT
ST.STATE
ST.RESIDENT
ST.SWT_MAR_STATUS
ST.SWT_ALLOWANCES
ST.SWT_ADDL_ALLOWNCES
ST.SWT_ADDL_AMT
ST.SWT_ADDL_PCT

  let $FieldName    = 'FWT_MAR_STATUS'
  let $FieldValue   = &FT.FWT_Mar_Status
  do Read-Translate-Table
  let $FWTMarStatus = $XlatShortName

  do Get-SWT-Marital-Status

FROM  PS_FED_TAX_DATA   FT,
      PS_STATE_TAX_DATA ST
WHERE ST.EMPLID   = FT.EMPLID
  AND ST.COMPANY  = FT.COMPANY
  AND ST.EFFDT    = FT.EFFDT
  AND ST.RESIDENT = 'Y'
  AND FT.EMPLID   = &B.EmplID
  AND FT.COMPANY  = &A.Company
  AND FT.EFFDT    =
    (SELECT MAX(EFFDT)
       FROM PS_FED_TAX_DATA
      WHERE EMPLID  = FT.EMPLID
        AND COMPANY = FT.COMPANY
        AND CREATION_DT <= &B.UPDATE_DT  
        AND EFFDT  <= &A.Check_Dt)       
end-SELECT

end-procedure


!***********************************************************************
begin-procedure Get-SWT-Marital-Status
!***********************************************************************

begin-SELECT
                              
SM.DESCRLONG

  let $SWTMarStatus = substr(&SM.DescrLong,1,25)

FROM  PS_SWT_MARSTAT_TBL SM
WHERE SM.STATE          = &ST.State
  AND SM.SWT_MAR_STATUS = &ST.SWT_Mar_Status
  AND SM.EFFDT          =
    (SELECT MAX(EFFDT)
       FROM PS_SWT_MARSTAT_TBL
      WHERE STATE          = SM.STATE
        AND SWT_MAR_STATUS = SM.SWT_MAR_STATUS
        AND EFFDT         <= &A.Pay_End_Dt)
end-SELECT

end-procedure


!***********************************************************************
begin-procedure Get-Earnings-Detail
!***********************************************************************

begin-SELECT
E.HOURLY_RT
E.REG_HRS
E.REG_HRLY_EARNS
E.OT_HRS
E.OT_HRLY_EARNS
E.REG_EARNS
E.COMPRATE_USED_REG	
E.COMPRATE_USED_OT	
E.RATE_USED		

E.ADDL_NBR

  let #k = 1
  WHILE #k <= 3
    evaluate #k
      when = 1
        move &PG.ErnCd_Reg_Hrs   to $Code
        move $EarnNameRegHrs     to $Name

            if &A.COMPANY = 'GEI' or                     
               &A.COMPANY = 'KAP' or                    
	       &A.COMPANY = 'RFI'                       
	      if &A.PAYGROUP = 'OFF' or &A.PAYGROUP = 'EXE'
                 or &A.PAYGROUP = 'KAP'  
                 or &A.PAYGROUP = 'OF4'                 

	        if &J.EMPL_TYPE = 'S'
	              move 0 to #hours
	              move 0 to #Rate
	        else
		      
                        move &E.Comprate_used_reg  to #Rate	
		        move &E.Reg_Hrs          to #Hours
	        end-if
	      else
                move &E.Comprate_used_reg  to #Rate	
	        move &E.Reg_Hrs          to #Hours
	      end-if
            else
              move &E.Comprate_used_reg  to #Rate	
	      move &E.Reg_Hrs          to #Hours
	    end-if
	

        move &E.Reg_Hrly_Earns   to #Earns
      when = 2
        move &PG.ErnCd_OT_Hrs    to $Code
        move $EarnNameOTHrs      to $Name
        move &E.OT_Hrs           to #Hours
        move &E.OT_Hrly_Earns    to #Earns
      when = 3
        move &PG.ErnCd_Reg_Earns to $Code
        move $EarnNameRegEarns   to $Name
        move 0                   to #Rate
        move 0                   to #Hours
        move &E.Reg_Earns        to #Earns
     end-evaluate
     move #RatesRow              to #i
     do Update-ETDarray-Current-Earnings
     add 1 to #k
  END-WHILE
  do Get-Other-Earnings-Detail 
FROM  PS_PAY_EARNINGS E
WHERE E.COMPANY    = &A.Company
  AND E.PAYGROUP   = &A.PayGroup
  AND E.PAY_END_DT = &A.Pay_End_Dt
  AND E.OFF_CYCLE  = &B.Off_Cycle
  AND E.PAGE_NUM      = &b.page_num
  AND E.LINE_NUM      = &b.line_num
  AND E.SEPCHK     = &B.SepChk
  AND E.SINGLE_CHECK_USE IN ('C', 'N')
ORDER BY E.ADDL_NBR
end-SELECT
  do Get-Earning-Balances
end-procedure


!***********************************************************************
begin-procedure Get-Other-Earnings-Detail
!***********************************************************************

begin-SELECT
                                
OE.ERNCD
  let $ErnCd = &OE.ErnCd
  do lookup-erncd-name  

OE.COMP_RATECD				
OE.COMPRATE_USED			
OE.RATE_USED				
OE.OTH_HRS
OE.OTH_EARNS
  move $ErnCd        to $Code
  move $EarningsName to $Name

  if $erncd = '535'                      
     let $EE_Had_TBP_Erncd535 = 'Y'      
  end-if                                

   if ($erncd = '515') or ($erncd = '170') or ($erncd = '167') or ($erncd = '700') or ($erncd = '701')
     move 0 to #rate
     move 0 to #hours
  else

    if &A.COMPANY = 'GEI' or                     
       &A.COMPANY = 'KAP' or                    
       &A.COMPANY = 'RFI'                        
     if &A.PAYGROUP = 'OFF' or &A.PAYGROUP = 'EXE'
         or &A.PAYGROUP = 'KAP'  
         or &A.PAYGROUP = 'OF4'                  
 
        if &J.EMPL_TYPE = 'S'
           if $Erncd = 'REG'
              move 0 to #hours
              move 0 to #Rate
           else
              move &OE.Oth_Hrs   to #Hours
              
              do Check-Override-Rate
              if $override-yes = 'Y'
                 move &OV.Perunit_Ovr_Rt to #Rate
              else
!	         
                 move &OE.Comprate_used  to #Rate	
              end-if
              
           end-if
        else
           move &OE.Oth_Hrs   to #Hours
           do Check-Override-Rate
           if $override-yes = 'Y'
              move &OV.Perunit_Ovr_Rt to #Rate
           else
              move &OE.Comprate_used  to #Rate	
           end-if
        end-if
      else
        move &OE.Oth_Hrs   to #Hours
        do Check-Override-Rate
        if $override-yes = 'Y'
           move &OV.Perunit_Ovr_Rt to #Rate
        else
           move &OE.Comprate_used  to #Rate	
        end-if
      end-if
   else
     move &OE.Oth_Hrs   to #Hours
     do Check-Override-Rate
     if $override-yes = 'Y'
        move &OV.Perunit_Ovr_Rt to #Rate
     else
         move &OE.Comprate_used  to #Rate	
     end-if
   end-if

  end-if
  move &OE.Oth_Earns to #Earns
  move #RatesRow     to #i
 
  do Update-ETDarray-Current-Earnings
  
FROM  PS_PAY_OTH_EARNS OE
WHERE OE.COMPANY    = &A.Company
  AND OE.PAYGROUP   = &A.PayGroup
  AND OE.PAY_END_DT = &A.Pay_End_Dt
  AND OE.OFF_CYCLE  = &B.Off_Cycle
  AND OE.PAGE_NUM      = &b.page_num
  AND OE.LINE_NUM      = &b.line_num
  AND OE.SEPCHK     = &B.SepChk
  AND OE.ADDL_NBR      = &E.Addl_nbr
ORDER BY OE.ERNCD
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Check-Override-Rate
!***********************************************************************

  let $override-yes = 'N'

begin-SELECT
OV.PERUNIT_OVR_RT

  let $override-yes = 'Y'

FROM  PS_EARNINGS_TBL OV
WHERE OV.ERNCD        = &OE.ERNCD
  AND OV.EFF_STATUS   = 'A'
  AND OV.PAYMENT_TYPE = 'U'
  AND OV.EFFDT        =
     (SELECT MAX(EFFDT)
        FROM PS_EARNINGS_TBL
       WHERE ERNCD         = OV.ERNCD
         AND EFF_STATUS    = OV.EFF_STATUS
         AND EFFDT        <= &A.Pay_End_Dt)
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Get-Earning-Balances
!***********************************************************************

  let #Cur = 0
  let #ern_pre_count = 0

begin-SELECT
                      
EB.ERNCD,EB.HRS_YTD,EB.GRS_YTD

  let $ErnCd = &EB.ErnCd
  do lookup-erncd-name
  let $Code = $ErnCd
  let $Name = $EarningsName

  let #k = 1
  WHILE #k <= 3
    evaluate #k
      when = 1
        move 0           to #YTD
        move #RatesRow   to #i
      when = 2
       if ($erncd = '515') OR ($erncd = '170') or ($erncd = '167') or ($erncd = '700') or ($erncd = '701') 
           move 0 to #ytd
        else
            if &A.COMPANY = 'GEI' or                     
               &A.COMPANY = 'KAP' or                     
	       &A.COMPANY = 'RFI'                        
	     if &A.PAYGROUP = 'OFF' or &A.PAYGROUP = 'EXE'
                or &A.PAYGROUP = 'KAP'   
                or &A.PAYGROUP = 'OF4'                   
 
	        if &J.EMPL_TYPE = 'S'
	           if $Erncd = 'REG'
	              move 0 to #YTD
                   else
	              move &EB.Hrs_YTD to #YTD
	           end-if
                else
	           move &EB.Hrs_YTD to #YTD
	        end-if
             else
                 move &EB.Hrs_YTD to #YTD
	     end-if
          else
             move &EB.Hrs_YTD to #YTD
	  end-if
        end-if
        move #HoursRow   to #i
      when = 3
        move &EB.Grs_YTD to #YTD
        move #EarnsRow   to #i
    end-evaluate
    do Update-ETDarray
    add 1 to #k
  END-WHILE

  add 1 to #ern_pre_count

FROM  PS_EARNINGS_BAL EB
WHERE EB.EMPLID        = &B.EmplID
  AND EB.COMPANY       = &A.Company
  AND EB.BALANCE_ID    = &A1.BALANCE_ID
  and EB.BALANCE_QTR > 0
  AND EB.BALANCE_YEAR  = &A1.BALANCE_YEAR
  AND EB.SPCL_BALANCE  = 'N'
  AND EB.EMPL_RCD      = &B.EMPL_RCD
  and eb.erncd > ' '
  AND ((EB.ERNCD <> '700') AND  (EB.ERNCD <> '701'))
  AND EB.BALANCE_PERIOD       =
    (SELECT MAX(BALANCE_PERIOD)
       FROM PS_EARNINGS_BAL
      WHERE EMPLID        = EB.EMPLID
        AND COMPANY       = EB.COMPANY
        AND BALANCE_ID    = EB.BALANCE_ID
        and BALANCE_QTR > 0
        AND BALANCE_YEAR  = EB.BALANCE_YEAR
        AND SPCL_BALANCE  = EB.SPCL_BALANCE
        AND ERNCD         = EB.ERNCD
        AND EMPL_RCD      = EB.EMPL_RCD
        AND BALANCE_PERIOD      <= &A1.BALANCE_PERIOD)
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Get-Taxes-Detail
!***********************************************************************

  let #YTD = 0
  let #i   = #TaxesRow

begin-SELECT
T.STATE,
T.LOCALITY,
T.TAX_CLASS,
T.TAX_CUR,
T.TXGRS_CUR

  let $Code = rtrim(&T.State,' ') || rtrim(&T.Locality,' ') || &T.Tax_Class

  if rtrim(&T.State, ' ') = '$U'
    let $Name = 'Fed'
    if &T.Tax_Class = 'H'               !withholding
      move &T.TxGrs_Cur to #FedTaxGrossCur
    end-if
  else


    let $Name = rtrim(&T.State, ' ')

    if rtrim(&T.Locality,' ') <> ''     !if a Locality


      let $State = rtrim(&T.State, ' ')
      let $Locality = rtrim(&T.Locality, ' ')

      do Get-Local-Tax-Data
      let $Name = $Name || ' ' || $LocalAbbr
    end-if
  end-if
  let $FieldName  = 'TAX_CLASS'
  let $FieldValue = &T.Tax_Class
  do Read-Translate-Table
  let $Name = $Name || ' ' || $XLatShortName

  move &T.Tax_Cur to #Cur
  do Update-ETDarray

FROM  PS_PAY_TAX T
WHERE T.COMPANY    = &A.Company
  AND T.PAYGROUP   = &A.PayGroup
  AND T.PAY_END_DT = &A.Pay_End_Dt
  AND T.OFF_CYCLE  = &B.Off_Cycle
  AND T.PAGE_NUM      = &b.page_num
  AND T.LINE_NUM      = &b.line_num
  AND T.SEPCHK     = &B.SepChk
  AND T.TAX_CLASS IN ('B','C','D','F','G','H','M','N','P','T','V','7') 
ORDER BY T.STATE,T.LOCALITY,T.TAX_CLASS DESC
end-SELECT
  do Get-Tax-Balances
end-procedure


!***********************************************************************
begin-procedure Get-Tax-Balances
!***********************************************************************

  let #Cur = 0
  let #tax_pre_count = 0

begin-SELECT
TB.STATE,
TB.LOCALITY,
TB.TAX_CLASS,
TB.TAX_YTD,
TB.TXGRS_YTD

  let $Code = rtrim(&TB.State, ' ') || rtrim(&TB.Locality,' ') || &TB.Tax_Class  !-:

  if rtrim(&TB.State, ' ') = '$U'
    let $Name = 'Fed'
    if &TB.Tax_Class = 'H'               !withholding
      move &TB.TxGrs_YTD to #FedTaxGrossYTD
    end-if
  else

    let $Name = rtrim(&TB.State,' ')
    if rtrim(&TB.Locality,' ') <> ''     !if a Locality


      let $State = rtrim(&TB.State, ' ')
      let $Locality = rtrim(&TB.Locality, ' ')

      do Get-Local-Tax-Data
      let $Name = $Name || ' ' || $LocalAbbr
    end-if
  end-if
  let $FieldName  = 'TAX_CLASS'
  let $FieldValue = &TB.Tax_Class
  do Read-Translate-Table
  let $Name = $Name || ' ' || $XLatShortName

  move &TB.Tax_YTD to #YTD
  do Update-ETDarray

FROM  PS_TAX_BALANCE TB
WHERE TB.EMPLID        = &B.EmplID
  AND TB.COMPANY       = &A.Company
  AND TB.BALANCE_ID    = &A1.BALANCE_ID
  and tb.balance_qtr > 0
  AND TB.BALANCE_YEAR  = &A1.BALANCE_YEAR
  AND TB.TAX_CLASS IN ('B','C','D','F','G','H','M','N','P','V','T','7')  
  AND TB.BALANCE_PERIOD      =
    (SELECT MAX(BALANCE_PERIOD)
       FROM PS_TAX_BALANCE
      WHERE EMPLID        = TB.EMPLID
        AND COMPANY       = TB.COMPANY
        AND BALANCE_ID    = TB.BALANCE_ID
        and balance_qtr > 0
        AND BALANCE_YEAR  = TB.BALANCE_YEAR
        AND STATE         = TB.STATE
        AND (LOCALITY     = TB.LOCALITY
         OR  LOCALITY = ' ' and TB.LOCALITY = ' ')
        AND TAX_CLASS     = TB.TAX_CLASS
        AND BALANCE_PERIOD      <= &A1.BALANCE_PERIOD)
  and not (tb.tax_class='P' and tb.state='PA' and tb.locality=' ')
ORDER BY TB.STATE,TB.LOCALITY,TB.TAX_CLASS DESC
end-SELECT

end-procedure


!***********************************************************************
begin-procedure Get-Deductions-Detail
!***********************************************************************

  let #YTD = 0

begin-SELECT

N.PLAN_TYPE,N.DEDCD,N.DED_CLASS,N.DED_CUR

  let $PlanType = &N.Plan_Type
  let $DedCd    = &N.DedCd

   do lookup-ded-name

  if $Garn_Process = 'G'

    do Get-Garnishment-Detail
  else
    let $Code = $PlanType || $DedCd || &N.Ded_Class
    let $Name = $DeductionName

    evaluate &N.Ded_Class
      when = 'B'           !Before-tax deduction

      when = 'A'           !After-tax deduction

        move #PreTaxDedsRow  to #i

      when = 'N'           !Non-taxable benefit
      when = 'P'
        move #BenefitsRow    to #i
      when = 'T'           !Taxable benefit
        move #BenefitsRow    to #i
        concat '*' with $Name
      when-other

        move #PreTaxDedsRow to #i

    end-evaluate

    move &N.Ded_Cur to #Cur
    do Update-ETDarray
  end-if

FROM  PS_PAY_DEDUCTION N
WHERE N.COMPANY    = &A.Company
  AND N.PAYGROUP   = &A.PayGroup
  AND N.PAY_END_DT = &A.Pay_End_Dt
  AND N.OFF_CYCLE  = &B.Off_Cycle
  AND N.PAGE_NUM      = &b.page_num
  AND N.LINE_NUM      = &b.line_num
  AND N.SEPCHK     = &B.SepChk
end-SELECT
  do Get-Deduction-Balances
end-procedure


!**********************************************************************
begin-procedure Get-Garnishment-Detail
!**********************************************************************

  let #YTD = 0

begin-SELECT
GR.GARNID, GR.DEDUCT_AMT

  let $GarnID = RTRIM(&GR.Garnid, ' ')
  do Get-Garn-Type
  let $FieldName = 'GARN_TYPE'
  let #GarnAmt = &GR.Deduct_Amt

  do Read-Translate-Table
  let $GarnName = $XlatShortName

  let $Name = $GarnID || ' ' || $GarnName
  let $Code = 'GG' || $GarnID || &N.Ded_Class

  move #PreTaxDedsRow to #i

  move #GarnAmt to #Cur
  do Update-ETDarray

FROM PS_PAY_GARNISH GR
WHERE GR.COMPANY = &A.COMPANY
  AND GR.PAYGROUP = &A.PAYGROUP
  AND GR.PAY_END_DT = &A.PAY_END_DT
  AND GR.OFF_CYCLE = &B.OFF_CYCLE
  AND GR.PAGE_NUM = &B.PAGE_NUM
  AND GR.LINE_NUM = &B.LINE_NUM
ORDER BY GR.GARNID
end-SELECT
end-procedure


!***********************************************************************
begin-procedure Get-Garn-Type
!***********************************************************************

begin-SELECT

GS.GARN_TYPE

  let $FieldValue = &GS.Garn_Type

FROM PS_GARN_SPEC GS
WHERE GS.EMPLID = &B.EMPLID
  AND GS.COMPANY = &A.COMPANY
  AND GS.GARNID = $Garnid
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Get-Deduction-Balances
!***********************************************************************

  let #Cur = 0
  let #ded_pre_count_p = 0
  let #ded_pre_count_t = 0

begin-SELECT

NB.PLAN_TYPE,NB.DEDCD,NB.DED_CLASS,NB.DED_YTD

  let $PlanType = &NB.Plan_Type
  let $DedCd = &NB.DEDCD

   do lookup-ded-name

  if $Garn_Process = 'G'

    do Get-Garn-Balances

  else

    let $Code = &NB.Plan_Type || &NB.DedCd || &NB.Ded_Class

    let $PlanType = &NB.Plan_Type
    let $DedCd    = &NB.DedCd
    let $Name = $DeductionName

    evaluate &NB.Ded_Class
      when = 'B'           !Before-tax deduction


      when = 'A'           !After-tax deduction

        move #PreTaxDedsRow  to #i

      when = 'N'           !Non-taxable benefit
      when = 'P'
        move #BenefitsRow    to #i


      when = 'T'           !Taxable benefit
        move #BenefitsRow    to #i
        concat '*' with $Name

      when-other

        move #PreTaxDedsRow to #i


    end-evaluate

    if $DedCd = 'PAYADJ'
      move 0 to #YTD
    else 
      move &NB.Ded_YTD to #YTD
    end-if

    do Update-ETDarray
  end-if

FROM  PS_DEDUCTION_BAL NB
WHERE NB.EMPLID        = &B.EmplID
  AND NB.COMPANY       = &A.Company
  AND NB.BALANCE_ID    = &A1.BALANCE_ID
  AND NB.BALANCE_YEAR  = &A1.BALANCE_YEAR
  AND NB.BALANCE_PERIOD       =
    (SELECT MAX(BALANCE_PERIOD)
       FROM PS_DEDUCTION_BAL
      WHERE EMPLID        = NB.EMPLID
        AND COMPANY       = NB.COMPANY
        AND BALANCE_ID    = NB.BALANCE_ID
        AND BALANCE_YEAR  = NB.BALANCE_YEAR
        AND PLAN_TYPE     = NB.PLAN_TYPE
        AND BENEFIT_PLAN  = NB.BENEFIT_PLAN
        AND DEDCD         = NB.DEDCD
        AND DED_CLASS     = NB.DED_CLASS
        AND BENEFIT_RCD_NBR = NB.BENEFIT_RCD_NBR
        AND BALANCE_PERIOD     <= &A1.BALANCE_PERIOD)
        and nb.ded_ytd > 0

end-SELECT
end-procedure


!**********************************************************************
begin-procedure Get-Garn-Balances
!**********************************************************************

  let #Cur = 0

begin-SELECT
NG.GARNID, NG.DED_CLASS, NG.DED_YTD

  let $GarnID = RTRIM(&NG.Garnid, ' ')
  let #YTD = &NG.Ded_YTD

  do Get-Garn-Type
  do Read-Translate-Table
  let $GarnName = $XlatShortName
  let $Name = 'GARN' || ' ' || $GarnID || ' ' || $GarnName
  let $Code = 'GG' || $GarnID || &NG.Ded_Class

  move #PreTaxDedsRow to #i
  do Update-ETDarray

FROM  PS_GARN_BALANCE NG
WHERE NG.EMPLID = &B.EMPLID
  AND NG.COMPANY = &A.COMPANY
  AND NG.BALANCE_ID = &A1.BALANCE_ID
  AND NG.BALANCE_YEAR = &A1.BALANCE_YEAR
  AND NG.BALANCE_PERIOD =
      (SELECT MAX(BALANCE_PERIOD)
         FROM PS_GARN_BALANCE
        WHERE EMPLID = NG.EMPLID
          AND COMPANY = NG.COMPANY
          AND BALANCE_ID = NG.BALANCE_ID
          AND BALANCE_YEAR = NG.BALANCE_YEAR
          AND PLAN_TYPE = NG.PLAN_TYPE
          AND BENEFIT_PLAN = NG.BENEFIT_PLAN
          AND DEDCD = NG.DEDCD
          AND DED_CLASS = NG.DED_CLASS
          AND GARNID = NG.GARNID
          AND BALANCE_PERIOD <= &A1.BALANCE_PERIOD)
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Get-Net-Pay-Distribution
!***********************************************************************

begin-SELECT

PD.PAYCHECK_NBR,PD.ACCOUNT_TYPE,PD.DEPOSIT_AMOUNT

  if &PD.Account_Type = '$'             !check
    add &PD.Deposit_Amount to #CheckNetPay
  else
    move &PD.payCheck_nbr        to #Advice#
    add &PD.Deposit_Amount to #DepositNetPay
  end-if

FROM  PS_PAY_DISTRIBUTN PD
WHERE PD.COMPANY    = &A.Company
  AND PD.PAYGROUP   = &A.PayGroup
  AND PD.PAY_END_DT = &A.Pay_End_Dt
  AND PD.OFF_CYCLE  = &B.Off_Cycle
  AND PD.PAGE_NUM      = &b.page_num
  AND PD.LINE_NUM      = &b.line_num
  AND PD.SEPCHK     = &B.SepChk
end-SELECT

end-procedure


!***********************************************************************
begin-procedure Get-Vacation-Balances
!***********************************************************************

begin-SELECT LOOPS=1
V.HRS_CARRYOVER,
V.HRS_EARNED_YTD,
V.HRS_TAKEN_YTD,
V.HRS_ADJUST_YTD,
V.HRS_BOUGHT_YTD,
V.HRS_SOLD_YTD,
V.HRS_TAKEN_UNPROC,
V.HRS_ADJUST_UNPROC
V.HRS_BOUGHT_UNPROC,
V.HRS_SOLD_UNPROC,
V.ACCRUAL_PROC_DT

  let #VacStartBal = &V.Hrs_Carryover
  let #VacEarned   = &V.Hrs_Earned_YTD
  let #VacBought   = &V.Hrs_Bought_YTD + &V.Hrs_Bought_Unproc
  let #VacTaken    = &V.Hrs_Taken_YTD  + &V.Hrs_Taken_Unproc
  let #VacSold     = &V.Hrs_Sold_YTD   + &V.Hrs_Sold_Unproc
  let #VacAdjust   = &V.Hrs_Adjust_YTD + &V.Hrs_Adjust_Unproc
  let #VacEndBal   = #VacStartBal + #VacEarned + #VacBought
                     - #VacTaken - #VacSold + #VacAdjust

FROM  PS_LEAVE_ACCRUAL V
WHERE V.EMPLID           = &B.Emplid
  AND V.COMPANY          = &A.Company
  AND V.PLAN_TYPE        = '51'                 !Vacation
  AND V.ACCRUAL_PROC_DT <= &B.Check_Dt
ORDER BY V.ACCRUAL_PROC_DT DESC
end-SELECT

end-procedure


!***********************************************************************
begin-procedure lookup-ded-name
!***********************************************************************
 let $lookdedcd = $PlanType||$dedcd
 lookup deductions_lookup $lookdedcd $ded_descrs
 unstring $ded_descrs by '&%&' into $DeductionName $DeductionAbbrv $Garn_Process
 
end-procedure

begin-procedure lookup-erncd-name

 lookup earnings_lookup $erncd $erns_names
 unstring $erns_names by '&%&' into $EarningsName $EarningsAbbrv
 
end-procedure

!***********************************************************************
begin-procedure Get-Earning-Balances-count
!***********************************************************************

let #ern_count = 0

begin-SELECT
count(EBc.ERNCD) &ern_count

  let #ern_count = &ern_count

FROM  PS_EARNINGS_BAL EBc
WHERE EBc.EMPLID        = &B.EmplID
  AND EBc.COMPANY       = &A.Company
  AND EBc.BALANCE_ID    = &A1.BALANCE_ID
  and EBc.BALANCE_QTR > 0
  AND EBc.BALANCE_YEAR  = &A1.BALANCE_YEAR
  AND EBc.SPCL_BALANCE  = 'N'
  AND EBc.EMPL_RCD      = &B.EMPL_RCD
  and ebc.erncd > ' '
  AND EBc.BALANCE_PERIOD       =
    (SELECT MAX(BALANCE_PERIOD)
       FROM PS_EARNINGS_BAL
      WHERE EMPLID        = EBc.EMPLID
        AND COMPANY       = EBc.COMPANY
        AND BALANCE_ID    = EBc.BALANCE_ID
        and BALANCE_QTR > 0
        AND BALANCE_YEAR  = EBc.BALANCE_YEAR
        AND SPCL_BALANCE  = EBc.SPCL_BALANCE
        AND ERNCD         = EBc.ERNCD
        AND EMPL_RCD      = EBc.EMPL_RCD
        AND BALANCE_PERIOD      <= &A1.BALANCE_PERIOD)
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Get-Tax-Balances-Count
!***********************************************************************

  let #Tax_count = 0

begin-SELECT

count(*) &tax_count



  let #tax_count = &tax_count

FROM  PS_TAX_BALANCE TBc
WHERE TBc.EMPLID        = &B.EmplID
  AND TBc.COMPANY       = &A.Company
  AND TBc.BALANCE_ID    = &A1.BALANCE_ID
  and tbc.balance_qtr > 0
  AND TBc.BALANCE_YEAR  = &A1.BALANCE_YEAR
   AND TBc.TAX_CLASS IN ('B','C','D','F','G','H','M','N','P','T','V','7') 
  AND TBc.BALANCE_PERIOD      =
    (SELECT MAX(BALANCE_PERIOD)
       FROM PS_TAX_BALANCE
      WHERE EMPLID        = TBc.EMPLID
        AND COMPANY       = TBc.COMPANY
        AND BALANCE_ID    = TBc.BALANCE_ID
        and balance_qtr > 0
        AND BALANCE_YEAR  = TBc.BALANCE_YEAR
        AND STATE         = TBc.STATE
        AND (LOCALITY     = TBc.LOCALITY
         OR  LOCALITY = ' ' and TBc.LOCALITY = ' ')
        AND TAX_CLASS     = TBc.TAX_CLASS
        AND BALANCE_PERIOD      <= &A1.BALANCE_PERIOD)
   and not (TBC.tax_class='P' and TBC.state='PA' and TBC.locality=' ')
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Get-Deduction-Balances-count
!***********************************************************************

  let #ded_count_p = 0

begin-SELECT
count(*) &ded_count_p

  let #ded_count_p = &ded_count_p
FROM  PS_DEDUCTION_BAL NBc
WHERE NBc.EMPLID        = &B.EmplID
  AND NBc.COMPANY       = &A.Company
  AND NBc.BALANCE_ID    = &A1.BALANCE_ID
  AND NBc.BALANCE_YEAR  = &A1.BALANCE_YEAR
  AND NBc.DED_CLASS IN ('A','B')
  AND NBc.BALANCE_PERIOD       =
    (SELECT MAX(BALANCE_PERIOD)
       FROM PS_DEDUCTION_BAL
      WHERE EMPLID        = NBc.EMPLID
        AND COMPANY       = NBc.COMPANY
        AND BALANCE_ID    = NBc.BALANCE_ID
        AND BALANCE_YEAR  = NBc.BALANCE_YEAR
        AND PLAN_TYPE     = NBc.PLAN_TYPE
        AND DEDCD         = NBc.DEDCD
        AND BENEFIT_RCD_NBR = NBc.BENEFIT_RCD_NBR
        AND DED_CLASS     = NBc.DED_CLASS
        AND BALANCE_PERIOD     <= &A1.BALANCE_PERIOD)
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Get-Deduction-Balances-count-1
!***********************************************************************

  let #ded_count_t = 0

begin-SELECT
count(*) &ded_count_t

  let #ded_count_t = &ded_count_t
FROM  PS_DEDUCTION_BAL NBt
WHERE NBt.EMPLID        = &B.EmplID
  AND NBt.COMPANY       = &A.Company
  AND NBt.BALANCE_ID    = &A1.BALANCE_ID
  AND NBt.BALANCE_YEAR  = &A1.BALANCE_YEAR
  AND nBT.DED_CLASS NOT IN ('A','B')
  AND NBt.BALANCE_PERIOD       =
    (SELECT MAX(BALANCE_PERIOD)
       FROM PS_DEDUCTION_BAL
      WHERE EMPLID        = NBt.EMPLID
        AND COMPANY       = NBt.COMPANY
        AND BALANCE_ID    = NBt.BALANCE_ID
        AND BALANCE_YEAR  = NBt.BALANCE_YEAR
        AND PLAN_TYPE     = NBt.PLAN_TYPE
        AND DEDCD         = NBt.DEDCD
        AND DED_CLASS     = NBt.DED_CLASS
        AND BENEFIT_RCD_NBR = NBt.BENEFIT_RCD_NBR
        AND BALANCE_PERIOD     <= &A1.BALANCE_PERIOD)
end-SELECT

end-procedure

!***********************************************************************
begin-procedure Reset-Variables
!***********************************************************************

!reset employee tax data
  move ' ' to $FWTMarStatus
  move ' ' to $SWTMarStatus

!reset current and YTD amounts
  move 0 to #Cur
  move 0 to #YTD

!reset ETDarray
  move 0 to #i
  while #i <= #lastRow
    move 0 to #j
    while #j <= #lastCol
      put ' ' ' ' 0 0 into ETDarray(#i) Code(#j) Name(#j) Cur(#j) YTD(#j)
      add 1 to #j
    end-while
    add 1 to #i
  end-while

!reset vacation totals
  move 0 to #VacStartBal
  move 0 to #VacEarned
  move 0 to #VacBought
  move 0 to #VacTaken
  move 0 to #VacSold
  move 0 to #VacAdjust
  move 0 to #VacEndBal

!reset current and YTD recalc totals
  move 0 to #HourCurTotal
  move 0 to #HourYTDTotal
  move 0 to #PreTaxDedCurTotal
  move 0 to #PreTaxDedYTDTotal
  move 0 to #PostTaxDedCurTotal
  move 0 to #PostTaxDedYTDTotal
  move 0 to #FedTaxGrossCur
  move 0 to #FedTaxGrossYTD

!reset net pay distribution totals
  move 0 to #CheckNetPay
  move 0 to #DepositNetPay

end-procedure


!***********************************************************************
begin-procedure Update-ETDarray-Current-Earnings
!***********************************************************************
!****** If paysheet hours or earnings NOT 0, then  
!****** this code gets the erncd and rate from the array...If the erncd and rate
!****** match, it loads the earnings to the array with the same subscript (j).
!****** Then when the array is read to print on the check, if j is the
!****** same, it rolls up the earnings.  If j is different, it prints a separate 
!****** detail entry.
 
  if #Hours <> 0 or #Earns <> 0
    
    move #RatesRow to #i
    move 0 to #j
    while #j <= #lastCol
      get $jCode #jRate from ETDarray(#i) Code(#j) Cur(#j)
      if rtrim($jCode,' ') = ''                 !if array entry is null
        break                                   !exit the "while"
      else
        if $Code = $jCode and #Rate = #jRate    !if array entry erncd-rate match,exit while,do not increase j. 
!         if $Code = $jCode                     !If erncd are equal, exit while,do not increase j.           
          break                                 !exit the "while"
        else
          if #j = #lastCol
            move $lastCode to $Code
            move $lastName to $Name
            move #lastRate to #Rate
            break                               !exit the "while"
          end-if
        end-if
      end-if
      add 1 to #j                               !increment j inside the while loop.
    end-while
   
    while #i <= #EarnsRow
      evaluate #i
        when = #RatesRow
          move #Rate  to #Cur
        when = #HoursRow
          move #Hours to #Cur
        when = #EarnsRow
          move #Earns to #Cur
        when-other
          move 0      to #Cur
      end-evaluate

      put $Code $Name into ETDarray(#i) Code(#j) Name(#j)
     
      if #i = #RatesRow
        put #Cur     into ETDarray(#i) Cur(#j)
      else
        array-add #Cur to ETDarray(#i) Cur(#j)
      end-if
      add 1 to #i
    end-while

  end-if
end-procedure


!***********************************************************************
begin-procedure Update-ETDarray
!***********************************************************************

  move 0 to #j
  while #j <= #lastCol

    get $jCode from ETDarray(#i) Code(#j)
    if rtrim($jCode,' ') = ''                   !if array entry is null
      put $Code $Name into ETDarray(#i) Code(#j) Name(#j)
      break                                     !exit the "while"
    else
      if $Code = $jCode                         !if array entry match
        break                                   !exit the "while"
      else
        if #j = #lastCol
          put $lastCode $lastName into ETDarray(#i) Code(#j) Name(#j)
          break                                 !exit the "while"
        end-if
      end-if
    end-if
    add 1 to #j

  end-while

  array-add #Cur #YTD to ETDarray(#i) Cur(#j) YTD(#j)

end-procedure


!***********************************************************************
begin-procedure Read-ETDarray
!***********************************************************************

  move 0 to #i
  while #i <= #lastRow
    get $Name #Cur #YTD from ETDarray(#i) Name(#j) Cur(#j) YTD(#j)
    evaluate #i
      when = #RatesRow
        move $Name to $EarnName
        move #Cur  to #EarnRate
      when = #HoursRow
        move #Cur  to #HourCur
        move #YTD  to #HourYTD
        add  #Cur  to #HourCurTotal
        add  #YTD  to #HourYTDTotal
      when = #EarnsRow
        move #Cur  to #EarnCur
        move #YTD  to #EarnYTD
      when = #PreTaxDedsRow
        move $Name to $PreTaxDedName
        move #Cur  to #PreTaxDedCur
        move #YTD  to #PreTaxDedYTD
        add  #Cur  to #PreTaxDedCurTotal
        add  #YTD  to #PreTaxDedYTDTotal
      when = #TaxesRow
        move $Name to $TaxName
        move #Cur  to #TaxCur
        move #YTD  to #TaxYTD
      when = #BenefitsRow
        move $Name to $BenefitName
        move #Cur  to #BenefitCur
        move #YTD  to #BenefitYTD
    end-evaluate
    add 1 to #i
  end-while
end-procedure


!***********************************************************************
begin-procedure Print-Statement
!***********************************************************************

    do Declare-Printer-Statement	

!print the pay period information

  let #row = 1

  move 11 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  print $CompanyName            (#row,3) bold

!******** Print 'TBP' on bonus checks addr front in big font
  if $EE_Had_TBP_Erncd535 = 'Y'                   !if ee had an erncd of 535, this is a
      let #row = 2                                !team bonus check.  Print 'TBP'                                 
      move 30 to #companypntsize                  !in upper right corner of addr flap 
      graphic () font :#StdFont :#CompanyPntSize  !move down a row to accomodate big font
      print 'TBP'            (#row,80) bold
  end-if
  
  move 9 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  position (+2)
  do Format-Check-Name-Address
  print $checkdept (+1,3)
  print $checkacct (+1,3)
  print 'Mail Drop: ' (+1,3)
  print &EMP.MAIL_DROP ()

!print the ETD detail information

  let #row1 = 23  
  let #row2 = 40  

  columns 43

  graphic (#row1,1,81) box  1 10 12
  graphic (#row1,,81)  box  1 10

  columns 3 43 3 43 84

  graphic (+1,1,40)    box 2 10 
  graphic (+2,,40)     box 9 10
  graphic (+9,,40)     box 1 10
  graphic (+1,,40)     box 4 10
  graphic (+4,,81)     box 1 10
  next-column
  let #row3 = #row1 + 1
  graphic (#row3,1,81) box  2 10
  graphic (+2,,81)     box 13 10
  graphic (+13,,81)    box  1 10
  next-column
  graphic (#row2,1,40) box  1 10 12
  graphic (#row2,,40)  box  1 10
  graphic (+1,,40)     box  1 10
  graphic (+1,,40)     box 13 10   
  graphic (+13,,40)    box  1 10
  next-column
  graphic (#row2,1,41) box  1 10 12
  graphic (#row2,,41)  box  1 10
  graphic (+1,,41)     box  1 10
  graphic (+1,,41)     box 13 10  
  graphic (+13,,41)    box  1 10
  next-column
  graphic (#row2,1,40) box  1 10 12
  graphic (#row2,,40)  box  1 10
  graphic (+1,,40)     box  1 10
  graphic (+1,,40)     box 13 10   
  graphic (+13,,40)    box  1 10

  use-column 0

  move 9 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  print $CompanyName            (#row1,4) bold
 
  graphic () font :#StdFont :#StmtPntSize
 
  print 'HOURS AND EARNINGS'    (#row1,71) bold

!print the employee information

  print 'Pay Begin Dt:'       (+1,4)
  do Format-DateTime($A.Pay_Begin_Dt, $out, {DEFDATE}, '', '') 
  
  print $out                    (,16)
  print 'Check Dt:'        (,25)
  do Format-DateTime(&B.Check_Dt, $out, {DEFDATE}, '', '')
  print $out                    (,35)

  print '-'                     (,77,16) fill
  print 'Current'               (,87)
  print '-'                     (,93,16)   fill
  print '-'                     (,107,12)   fill
  print 'YTD'                   (,114)
  print '-'                     (,118,10)   fill

  print 'Pay End Dt:'       (+1,4)
  
  do Format-DateTime($A.Pay_End_Dt, $out, {DEFDATE}, '', '')  
 
  print $out                    (,16)
  print 'Check #:'              (,25)
 
  do Format-Number(&B.PAYCHECK_NBR, $out, '09999999')   
  print $out                    (,35) bold

  print 'Description'           (,44)
  print 'Rate'                  (,77)
  print 'Hours'                 (,88)
  print 'Earnings'              (,96)
  print 'Hours'                 (,107)
  print 'Earnings'              (,117)

  print $HomeName               (+1,4) bold
  print $HomeAddrLine1          (+1,4)
  print $HomeAddrLine2          (+1,4)
  print $HomeAddrLine3          (+1,4)

  print 'Emp ID: '            (,23)  
  print $Emplid                  (,32)     
  
  print 'Department:'           (+1,4)
  let $out = $DeptID || '-' || $DeptName
  print $out                    (,16)

  print 'Acct CD:'              (+1,4)
  let $out = $AcctCD || '-' || $AcctCDName
  print $out                    (,16)

  print 'Location:'             (+1,4)
  print $LocationName           (,16)
  print 'Job Title:'            (+1,4)
  print $JobTitle               (,16)
  print 'Pay Group:'            (+1,4)
  let $out = $PayGroup || '-' || $PayGroupName
  print $out                    (,16)

  let #row = 35
  columns 4 20 30

  print 'TAX DATA:'             (#row,1)
  print 'Marital Status:'       (+1,1)
  print 'Allowances:'           (+1,1)
  print 'Addl. Pct.:'           (+1,1)
  print 'Addl. Amt.:'           (+1,1)

  next-column
  print 'Federal'               (#row,1)
  print $FWTMarStatus           (+1,1)
  let   $out = ltrim(edit(&FT.FWT_Allowances,'999'),' ')
  print $out                    (+1,1)
  let   $out = ltrim(edit(&FT.FWT_Addl_Pct,'b9.999'),' ')
  print $out                    (+1,1)
  let   $out = ltrim(edit(&FT.FWT_Addl_Amt,'b9,999.99'),' ')
  print $out                    (+1,1)

  next-column
  let $ResidentState = rtrim(&ST.State, ' ') || ' State'
  print $ResidentState          (#row,1)
  print $SWTMarStatus           (+1,1)
  let   $out = ltrim(edit(&ST.SWT_Allowances,'999'),' ')
  print $out                    (+1,1)
  if &ST.SWT_Addl_Allownces > 0
    let   $out = '+' || ltrim(edit(&ST.SWT_Addl_Allownces,'999'),' ')
    print $out                  ()
  end-if
  let   $out = ltrim(edit(&ST.SWT_Addl_Pct,'b9.999'),' ')
  print $out                    (+1,1)
  let   $out = ltrim(edit(&ST.SWT_Addl_Amt,'b9,999.99'),' ')
  print $out                    (+1,1)

  use-column 0

  
  print 'TAXES'                 (#row2,16)      bold
  print 'Description'           (+1,4)
  print ' Current'              (,26)
  print ' YTD'                  (,38)

  print 'DEDUCTIONS' (#row2,59) bold
  print 'EMPLOYER PAID BENEFITS'(,95)      bold
  print 'Description'           (+1,44)
  print ' Current'              (,68)
  print ' YTD'                  (,79)
  print 'Description'           (,85)
  print ' Current'              (,109)
  print ' YTD'                  (,119)

  let #row1 = #row1 + 3
  let #row2 = #row2 + 2

  columns 44 75 87 94 106 113 4 24 33 44 66 74 85 107 114

  move 0 to #j

  while #j <= {n}
     if #j = {n}

    if #ern_pre_count <= {n}
      move 'Total:'             to $EarnName
      move 0                    to #EarnRate
      move #HourCurTotal        to #HourCur
      move #HourYTDTotal        to #HourYTD
      move &B.Total_Gross       to #EarnCur
      move &Y.Total_Gross_YTD   to #EarnYTD
      move 'N' to $Total_Print   !SXK 02/18/98
    else
      move '* Continued on next page' to $EarnName
      move 0 to #earnrate
      move 0 to #hourcur
      move 0 to #hourytd
      move 0 to #earncur
      move 0 to #earnytd
      move 'Y' to $extra_page
    end-if

      if #ded_pre_count_p <= {n}
         move 'Total:'             to $PreTaxDedName
         move #PreTaxDedCurTotal   to #PreTaxDedCur
         move #PreTaxDedYTDTotal   to #PreTaxDedYTD
      else
         move '* Continued on next page' to $pretaxdedname
         move 0 to #pretaxdedcur
         move 0 to #pretaxdedytd
         move 'Y' to $extra_page
      end-if
      if #tax_pre_count <= {n}
         move 'Total:'             to $TaxName
         move &B.Total_Taxes       to #TaxCur
         move &Y.Total_Taxes_YTD   to #TaxYTD
      else
         move '* Continued on next page' to $TaxName
         move 0 to #taxcur
         move 0 to #taxytd
         move 'Y' to $extra_page
      end-if

      if #ded_pre_count_t <= {n}

         move '* Taxable'          to $BenefitName
         move 0                    to #BenefitCur
         move 0                    to #BenefitYTD
      else
         move '* Continued on next page'          to $BenefitName
         move 0                    to #BenefitCur
         move 0                    to #BenefitYTD
         move 'Y' to $extra_page
      end-if
      else
         do Read-ETDarray
      end-if

       print $EarnName             (#row1,1)

       if substr($EarnName,1,1) = '*'
          let $continue = 'Y'
       else
          let $continue = 'N'
       end-if

       next-column


       if #HourCur <> 0
           print #EarnRate           () edit b999.9999mi 
       else
          if $EarnName = 'Team Bonus Program'
             print #EarnRate           () edit b999.9999mi  
          end-if
       end-if

       next-column
       print #HourCur              () edit b999.99mi
       next-column
       if rtrim($EarnName,' ') <> '' and $continue = 'N'
          print #EarnCur            () edit 9999,990.99mi
       end-if
       next-column
       print #HourYTD              () edit b9999.99mi
       next-column

       if rtrim($EarnName,' ') <> '' and $continue = 'N'
          if $EarnName = 'Team Bonus Program' and #EarnYTD = 0             
          else            
             print #EarnYTD            () edit 999999,990.99mi
          end-if
       end-if

    next-column
    print $TaxName              (#row2,1)
    if substr($taxname,1,1) = '*'
       let $continue = 'Y'
    else
       let $continue = 'N'
    end-if
    next-column
    if rtrim($TaxName,' ') <> '' and $continue = 'N'
      print #TaxCur             () edit 9999,990.99mi
      next-column
      print #TaxYTD             () edit 99999,990.99mi
      add 1 to #tax_pre_count      
    else
      next-column
    end-if

    next-column
    print $PreTaxDedName        (#row2,1)
    if substr($pretaxdedname,1,1) = '*'
       let $continue = 'Y'
    else
       let $continue = 'N'
    end-if
    next-column
    if rtrim($PreTaxDedName,' ') <> '' and $continue = 'N'
      print #PreTaxDedCur       () edit 9,999,990.99mi
      next-column
      print #PreTaxDedYTD       () edit 99,999,990.99mi
      add 1 to #ded_pre_count_p     
    else
      next-column
    end-if

    next-column
    print $BenefitName          ()
    if substr($benefitname,1,1) = '*'
       let $continue = 'Y'
    else
       let $continue = 'N'
    end-if
    next-column
    if rtrim($BenefitName,' ') <> '' and $continue = 'N'
      if #j <= #lastCol         !not totals line
        print #BenefitCur       () edit 9,999,990.99mi
        next-column
        print #BenefitYTD       () edit 99,999,990.99mi
        add 1 to #ded_pre_count_t     
      end-if
    else
      next-column
    end-if

    add 1 to #row1
    add 1 to #row2
    next-column
    add 1 to #j

  end-while

!print the current and YTD summary totals

  let #row = 56 

  columns 3

  graphic (#row,1,121) box 1 10 15
  graphic (#row,,121)  box 1 10
  graphic (+1,,121)    box 2 10

  columns 4 24 48 72 94 113

  print ''                      (#row,1)
  print 'Current:'              (+1,1)
  print 'YTD:'                  (+1,1)

  next-column
  print 'TOTAL GROSS'           (#row,-2) bold
  print &B.Total_Gross          (+1,1) edit 999,999,990.99mi
  print &Y.Total_Gross_YTD      (+1,1) edit 999,999,990.99mi

  next-column
  print 'FED TAXABLE GROSS'     (#row,-8) bold
  print #FedTaxGrossCur         (+1,1) edit 999,999,990.99mi
  print #FedTaxGrossYTD         (+1,1) edit 999,999,990.99mi

  next-column
  print ' TOTAL TAXES'          (#row,-3) bold
  print &B.Total_Taxes          (+1,1) edit 99,999,990.99mi
  print &Y.Total_Taxes_YTD      (+1,1) edit 99,999,990.99mi

  next-column
  print 'TOTAL DEDUCTIONS'      (#row,-8) bold
  print &B.Total_Deductions     (+1,1) edit 99,999,990.99mi
  print &Y.Total_Dedns_YTD      (+1,1) edit 99,999,990.99mi

  next-column
  print ' NET PAY'              (#row,+2) bold
  print &B.Net_Pay              (+1,1) edit 999,999,990.99mi
  print &Y.Net_Pay_YTD          (+1,1) edit 999,999,990.99mi

  let #row = 59

  columns 4

  print 'MESSAGE: '             (#row,1) bold
  print $CheckMessage           ()


if $extra_page = 'Y'
  print 'CONTINUED ON NEXT PAGE' (68) center bold

!Print the employee address on the flap

  do Declare-Printer-Check
  
  let $h_name = upper($checkname)  
  position (8)
  move 11 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  print $H_Name               (,50) bold

  move 9 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  position (+1) 
  print $checkAddrLine1          (,50)
  position (+1) 
  print $checkAddrLine2          (,50)
  position (+1) 
  print $checkAddrLine3          (,50) 

end-if

end-procedure


!***********************************************************************
begin-procedure Print-Check
!***********************************************************************

  do Declare-Printer-Check	

!print the company and bank information and the check number

  let #row = 62

  columns 4 59 71 114
  graphic () font :#StdFont :#ChkPntSize

  print $CompanyName            (#row,1) bold
  print $CompanyAddrLine1       (+1,1)
  print $CompanyAddrLine2       (+1,1)
  print $CompanyAddrLine3       (+1,1)

  next-column
  graphic () font :#StdFont :#StmtPntSize
  print $BankID                 (#row,1)

  next-column
  graphic () font :#StdFont :#ChkPntSize

  print $BankName               (#row,1) bold
  print $BankAddrLine1          (+1,1)
  print $BankAddrLine2          (+1,1)
  print $BankAddrLine3          (+1,1)

  next-column
  print 'Check No.'             (#row,1) bold
  do Format-Number(&B.PAYCHECK_NBR, $out, '99999999')   
  let $out = ltrim($out,' ')
  print $out                    (+1,1)   bold

!print the check date and numeric amount

  let #row = 65  

  columns 96 109

  print 'Date:'                 (#row,1) bold

  next-column
  do Format-DateTime(&B.Check_Dt, $out, {DEFDATE}, '', '')
  print $out                    (#row,1) bold

  next-column
  add 2 to #row
  print 'Pay Amount:'           (#row,1) bold

  next-column
  do Format-Number(#CheckNetPay, $out, '$$$$,$$$,$$9.99mi')
  let $out = rpad(ltrim($out,' '),14,'*')
  print $out                    (#row,1) bold

!print the amount in words and the payee information

  let #row = 68

  columns 4 15

  print 'Pay'                   (#row,1) bold

  next-column
  graphic                       (#row,1,81) horz-line 7
  move #CheckNetPay to #NetPay
  do Net-Pay-In-Words           !convert numbers to words
  let $NetInWords = '****' || upper($NetInWords) || '****'
  print $NetInWords             ()

  next-column
  print 'To The'                (+2,1) bold

  print '** VOID AFTER 6 MONTHS **' (,65) bold

  print 'Order Of'              (+1,1) bold

  next-column
  print $CheckName              (+1,1) bold
  print $CheckAddrLine1         (+1,1)
  print $CheckAddrLine2         (+1,1)
  print $CheckAddrLine3         (+1,1)
  print $CheckLocation          (+1,1)

!print the check signature

  let #row = 75 

  columns 82  

  print $SigFont                (#row,1) code
  print 'A'                     (,+2)

!print the bank microcode

  use-column 0
  do Format-Number(&B.PAYCHECK_NBR, $CheckNum, '09999999')   
  let #begin = 1
  let #end = 1

  let $transit_string = ':' 
  while RTRIM(substr($TransitNum,#begin,#end),' ') <> ''
       let $partial_String = substr($TransitNum,#begin,#end)
       let $partial_String = RTRIM($partial_string,' ')
       let $transit_String = $transit_String||$partial_String
       let #begin = #begin + #end
       let #end = 2
  end-while

  let $transit_string = $transit_string||':'

  let #begin = 1
  let #end = 2

  let $acct_string = ''
  while RTRIM(substr($AccountNum,#begin,#end),' ') <> ''
       let $partial_String = substr($AccountNum,#begin,#end)
       let $partial_String = RTRIM($partial_string,' ')
       if #begin = 1
          let $acct_String = $acct_String||$partial_String
       else
          let $acct_String = $acct_String||$partial_String
       end-if
       let #begin = #begin + #end
       let #end = 2
  end-while

  let $acct_string = $acct_string||'<'

  let #begin = 1
  let #end = 1
  let $check_string = '<'
  while RTRIM(substr($CheckNum,#begin,#end),' ') <> ''
       let $partial_String = substr($CheckNum,#begin,#end)
       let $partial_String = RTRIM($partial_string,' ')
       let $check_String = $check_String||$partial_String
       let #begin = #begin + #end
       let #end = 2
  end-while

  let $check_string = $check_string||'<'


  let $MicrLine = $check_String||' '||$transit_string||' '||$acct_String

  !  print $micrfont (80,11) code   !Commented by Surya Sobha - CSR 1578
  !  print $MicrLine               (80,11)  !Commented by Surya Sobha - CSR 1578
  print $micrfont (80,8) code		!Added by Surya Sobha - CSR 1578
  print $MicrLine               (80,8)	!Added by Surya Sobha - CSR 1578
  
!Print the employee address on the flap

  do Declare-Printer-Check
  
  let $h_name = upper($checkname)  
  position (8)
  move 11 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  print $H_Name               (,50) bold

  move 9 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  position (+1) 
  print $checkAddrLine1          (,50)
  position (+1) 
  print $checkAddrLine2          (,50)
  position (+1) 
  print $checkAddrLine3          (,50) 
  

  new-page

end-procedure

!***********************************************************************
begin-procedure Print-Extra-Page
!***********************************************************************
  new-page

 do Declare-Printer-Statement

!print the pay period information

  let #row = 1

  move 11 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  print $CompanyName            (#row,3) bold
 
!******** On bonus checks addr front in big font
  if $EE_Had_TBP_Erncd535 = 'Y'                   !if ee had an erncd of 535, this is a
      let #row = 2                                !team bonus check.  Print 'TBP'                                 
      move 30 to #companypntsize                  !in upper right corner of addr flap 
      graphic () font :#StdFont :#CompanyPntSize  !move down a row to accomodate big font
      print 'TBP'            (#row,80) bold
  end-if
 
  move 9 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  position (+2)
  do Format-Check-Name-Address
  print $checkdept (+1,3)
  print $checkacct (+1,3)
  print 'Mail Drop: ' (+1,3)
  print &EMP.MAIL_DROP ()


!print the ETD detail information

  let #row1 = 23  
  let #row2 = 40 

  columns 43

  graphic (#row1,1,81) box  1 10 12
  graphic (#row1,,81)  box  1 10

  columns 3 43 3 43 84

  graphic (+1,1,40)    box 2 10 
  graphic (+2,,40)     box 9 10
  graphic (+9,,40)     box 1 10
  graphic (+1,,40)     box 4 10
  graphic (+4,,81)     box 1 10
  next-column
  let #row3 = #row1 + 1
  graphic (#row3,1,81) box  2 10
  graphic (+2,,81)     box 13 10
  graphic (+13,,81)    box  1 10
  next-column
  graphic (#row2,1,40) box  1 10 12
  graphic (#row2,,40)  box  1 10
  graphic (+1,,40)     box  1 10
  graphic (+1,,40)     box 13 10   
  graphic (+13,,40)    box  1 10
  next-column
  graphic (#row2,1,41) box  1 10 12
  graphic (#row2,,41)  box  1 10
  graphic (+1,,41)     box  1 10
  graphic (+1,,41)     box 13 10  
  graphic (+13,,41)    box  1 10
  next-column
  graphic (#row2,1,40) box  1 10 12
  graphic (#row2,,40)  box  1 10
  graphic (+1,,40)     box  1 10
  graphic (+1,,40)     box 13 10  
  graphic (+13,,40)    box  1 10

  use-column 0

  move 9 to #companypntsize
  graphic () font :#StdFont :#CompanyPntSize
  print $CompanyName            (#row1,4) bold
 
  graphic () font :#StdFont :#StmtPntSize
 
  print 'HOURS AND EARNINGS'    (#row1,71) bold

!print the employee information

  print 'Pay Begin Dt:'       (+1,4)
  do Format-DateTime($A.Pay_Begin_Dt, $out, {DEFDATE}, '', '') 

  print $out                    (,16)
  print 'Check Dt:'        (,25)
  do Format-DateTime(&B.Check_Dt, $out, {DEFDATE}, '', '')
  print $out                    (,35)

  print '-'                     (,77,16) fill
  print 'Current'               (,87)
  print '-'                     (,93,16)   fill
  print '-'                     (,107,12)   fill
  print 'YTD'                   (,114)
  print '-'                     (,118,10)   fill

  print 'Pay End Dt:'       (+1,4)
  
  do Format-DateTime($A.Pay_End_Dt, $out, {DEFDATE}, '', '') 

  print $out                    (,16)
  print 'Check #:'              (,25)
  
  do Format-Number(&B.PAYCHECK_NBR, $out, '09999999')   
  print $out                    (,35) bold

  print 'Description'           (,44)
  print 'Rate'                  (,77)
  print 'Hours'                 (,88)
  print 'Earnings'              (,96)
  print 'Hours'                 (,107)
  print 'Earnings'              (,117)

  print $HomeName               (+1,4) bold
  print $HomeAddrLine1          (+1,4)
  print $HomeAddrLine2          (+1,4)
  print $HomeAddrLine3          (+1,4)

  print 'Emp ID: '             (,23) 
  print $Emplid                 (,32)       

  print 'Department:'           (+1,4)
  let $out = $DeptID || '-' || $DeptName
  print $out                    (,16)

  print 'Acct CD:'              (+1,4)
  let $out = $AcctCD || '-' || $AcctCDName
  print $out                    (,16)

  print 'Location:'             (+1,4)
  print $LocationName           (,16)
  print 'Job Title:'            (+1,4)
  print $JobTitle               (,16)
  print 'Pay Group:'            (+1,4)
  let $out = $PayGroup || '-' || $PayGroupName
  print $out                    (,16)

  let #row = 35
  columns 4 20 30

  print 'TAX DATA:'             (#row,1)
  print 'Marital Status:'       (+1,1)
  print 'Allowances:'           (+1,1)
  print 'Addl. Pct.:'           (+1,1)
  print 'Addl. Amt.:'           (+1,1)

  next-column
  print 'Federal'               (#row,1)
  print $FWTMarStatus           (+1,1)
  let   $out = ltrim(edit(&FT.FWT_Allowances,'999'),' ')
  print $out                    (+1,1)
  let   $out = ltrim(edit(&FT.FWT_Addl_Pct,'b9.999'),' ')
  print $out                    (+1,1)
  let   $out = ltrim(edit(&FT.FWT_Addl_Amt,'b9,999.99'),' ')
  print $out                    (+1,1)

  next-column
  let $ResidentState = rtrim(&ST.State, ' ') || ' State'
  print $ResidentState          (#row,1)
  print $SWTMarStatus           (+1,1)
  let   $out = ltrim(edit(&ST.SWT_Allowances,'999'),' ')
  print $out                    (+1,1)
  if &ST.SWT_Addl_Allownces > 0
    let   $out = '+' || ltrim(edit(&ST.SWT_Addl_Allownces,'999'),' ')
    print $out                  ()
  end-if
  let   $out = ltrim(edit(&ST.SWT_Addl_Pct,'b9.999'),' ')
  print $out                    (+1,1)
  let   $out = ltrim(edit(&ST.SWT_Addl_Amt,'b9,999.99'),' ')
  print $out                    (+1,1)

  use-column 0

  
  print 'TAXES'                 (#row2,16)      bold
  print 'Description'           (+1,4)
  print ' Current'              (,26)
  print ' YTD'                  (,38)

  print 'DEDUCTIONS' (#row2,59) bold
  print 'EMPLOYER PAID BENEFITS'(,95)      bold
  print 'Description'           (+1,44)
  print ' Current'              (,68)
  print ' YTD'                  (,79)
  print 'Description'           (,85)
  print ' Current'              (,109)
  print ' YTD'                  (,119)

  let #row1 = #row1 + 3
  let #row2 = #row2 + 2

  columns 44 75 87 94 106 113 4 24 33 44 66 74 85 107 114

  let #j = {n}

  while #j <= {new_n}
     if #j = {new_n}
     if #ern_pre_count > {n}
      move 'Total:'             to $EarnName
      move 0                    to #EarnRate
      move #HourCurTotal        to #HourCur
      move #HourYTDTotal        to #HourYTD
      move &B.Total_Gross       to #EarnCur
      move &Y.Total_Gross_YTD   to #EarnYTD
      move 'N' to $Total_Print  
     end-if
     if #ded_pre_count_p > {n}
         move 'Total:'             to $PreTaxDedName
         move #PreTaxDedCurTotal   to #PreTaxDedCur
         move #PreTaxDedYTDTotal   to #PreTaxDedYTD
     end-if
     if #tax_pre_count > {n}
         move 'Total:'             to $TaxName
         move &B.Total_Taxes       to #TaxCur
         move &Y.Total_Taxes_YTD   to #TaxYTD
     end-if
     if #ded_pre_count_t > {n}
         move '* Taxable'          to $BenefitName
         move 0                    to #BenefitCur
         move 0                    to #BenefitYTD
     end-if
      else
         do Read-ETDarray
      end-if


       print $EarnName             (#row1,1)
       next-column
       if #HourCur <> 0
          print #EarnRate           () edit b999.9999mi 
       end-if
       next-column
       print #HourCur              () edit b999.99mi
       next-column
       if rtrim($EarnName,' ') <> ''
          print #EarnCur            () edit 9999,990.99mi
       end-if
       next-column
       print #HourYTD              () edit b9999.99mi
       next-column
       if rtrim($EarnName,' ') <> ''
          print #EarnYTD            () edit 999999,990.99mi
       end-if

    next-column
    print $TaxName              (#row2,1)
    next-column
    if rtrim($TaxName,' ') <> ''
      print #TaxCur             () edit 9999,990.99mi
      next-column
      print #TaxYTD             () edit 99999,990.99mi
    else
      next-column
    end-if

    next-column
    print $PreTaxDedName        (#row2,1)
    next-column
    if rtrim($PreTaxDedName,' ') <> ''
      print #PreTaxDedCur       () edit 9,999,990.99mi
      next-column
      print #PreTaxDedYTD       () edit 99,999,990.99mi
    else
      next-column
    end-if

    next-column
    print $BenefitName          ()
    next-column
    if rtrim($BenefitName,' ') <> ''
      if #j <= #lastCol         !not totals line
        print #BenefitCur       () edit 9,999,990.99mi
        next-column
        print #BenefitYTD       () edit 99,999,990.99mi
      end-if
    else
      next-column
    end-if

    add 1 to #row1
    add 1 to #row2
    next-column
    add 1 to #j

  end-while

end-procedure

!***********************************************************************
begin-procedure Declare-Printer-Statement
!***********************************************************************

  declare printer
    type          = HPLASERJET
    orientation   = portrait
    lines-inch    = 8
    chars-inch    = 16.6
    top-margin    = 0.30
    left-margin   = 0.25
    font          = 5
    point-size    = 7
    lines-inch    = 8
end-procedure


!***********************************************************************
begin-procedure Declare-Printer-Check
!***********************************************************************

  declare printer
    type          = HPLASERJET
    orientation   = portrait
    lines-inch    = 8
    chars-inch    = 16.6
    top-margin    = 0.00
    left-margin   = 0.25
    font          = 5
    point-size    = 9

end-procedure


!***********************************************************************
begin-procedure Declare-Printer-Microcode
!***********************************************************************

  declare printer
    type          = HPLASERJET
    orientation   = portrait
    top-margin    = 0.00
    left-margin   = 0.00
    symbol-set    = 0Q
    font          = 620
    font-style    = fixed
    point-size    = 8.4
    line-size     = 9.05
    chars-inch    = 8.0
!   char-size     = 7.2

end-procedure


!***********************************************************************
begin-procedure Format-Check-Name-Address
!***********************************************************************

  if rtrim(&B.PayCheck_Name,' ') <> ''
    let $Name = &B.PayCheck_Name
    do Rotate-Name
    let $CheckName = upper($Name)
  end-if

  let $CheckAddrLine3 = ' '
  let $CheckLocation =  ' '

  evaluate &B.PayCheck_Addr_Optn
    when = 'C'  !Mail to Check Address
    when = 'H'  !Mail to Home Address
    when = 'M'  !Mail to Mail Address

      let $CheckCityState =
        rtrim(&B.City,' ') || edit(&B.State,',bxxbb') || &B.postal

      let $CheckAddrLine1   = &B.Address1
      if rtrim(&B.Address2,' ') = ''
        let $CheckAddrLine2 = $CheckCityState
      else
        let $CheckAddrLine2 = &B.Address2
        let $CheckAddrLine3 = $CheckCityState
      end-if

      let $CheckLocation = 'Location: ' || $LocationName

    when = 'D'  !Deliver to Home Department
      let $CheckAddrLine1 = 'Department ID: ' || $DeptID
      let $CheckAddrLine2 = $DeptName

    when = 'L'  !Deliver to Job Location
    when = 'P'  !Deliver to Check Location
      let $CheckAddrLine1 = 'Location ID: ' || $Location
      let $CheckAddrLine2 = $LocationName
  end-evaluate

  let $checkdept = 'Department: ' || $deptid || '-' || $deptname
  let $checkacct = 'Account CD: ' || $acctcd || '-' || $acctcdname


end-procedure

#Include 'gexxx932.sqc'  !Get Multiple Individual Check Numbers
#Include 'getposnm.sqc'  !Get-Position-Title    