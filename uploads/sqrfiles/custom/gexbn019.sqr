!***********************************************************************
!  GEXBN019:  PHARMACY NORMAL HOURS REPORT                             *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXBN019:             PHARMACY NORMAL HOURS REPORT                   *
!                                                                      *
! Narrative:            This report shows the pharmacy employees       *
!                       standard hours along with information such as  *
!                       company, department, jobcode and union code    *
!                                                                      *
! #Debugx Used:	        #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:	        personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       ben_prog_partic                                *
!                                                                      *
! Written by:           Joe Besagar                                    *
!                                                                      *
! Normally Run:	        Quarterly                                      *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Multiple Benefit Program(s)                  *
!                         Multiple Account Code(s)                     *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS     DATE         DESCRIPTION OF THE CHANGE                 *
!***********************************************************************
!  JNB          06/29/98     Initial Creation                          *
!  JDH	        11/23/98     Added page break on deptid                *
!                            Added SQRW run control logic              *
!  JDH          02/15/99     Changed sort order.                       *
!                            Added benefit program and job description *
!  SXK          10/20/99     Modified to get union_cd from job_labor   *
!  GBD          07/15/02     Added Fieldvalue to effective dated       *
!                            selection of the Load-Lookup procedure.   *
!  SRK          07/29/02     Modified to get union_cd from job as part *
!                            of 8.3 upgrade                            *
! GEX-TLL       02/12/08     Retrofitting HCM 9.0 Upgrade              *
!			     Replaced XLATTABLE table with PSXLATITEM  *
!			     					       *
! ISDVMXD	02/16/09     ITG 56749 - Replace LOOKUP for Jobtitle   *
!				with GETPOSNM.SQC 		       *	
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


#define #column1     1
#define #column2    18
#define #column3    54
#define #column4    67
#define #column5   103
#define #column6   139
#define #column7   150
#define #column8   163


begin-report
#debug9 Show 'begin-report'

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do P000-Init-Report
  do P200-Process-Main
  do Reset                   !reset.sqc
  do Stdapi-Term             !stdapi.sqc
  Show 'Successful end of report'
end-report


begin-heading 8
#debug9 Show 'begin-heading'

  #Include 'stdhdg01.sqc'

  Print 'Deptid:'       (3,1)
  Print $deptid         (3,13)

  Let $X000_temp_out = 'Report as of ' || $X000_REPORT_HEADING_ASOFDATE
  Print $X000_temp_out         (3,)  center

  PRINT 'BENEFIT'       (+3,{#column7})
  PRINT 'EMPLOYEE'      (,{#column5})

  PRINT 'SSN#'          (+1,{#column1})
  PRINT 'NAME'          (,{#column2})
  PRINT 'COMPANY'       (,{#column3})
  PRINT 'JOBCODE'       (,{#column4})
  PRINT 'STATUS'        (,{#column5})
  PRINT 'UNION'         (,{#column6})
  PRINT 'PROGRAM'       (,{#column7})
  PRINT '   STD HOURS'  (,{#column8})

  PRINT '-'             (+1,{#column1},11) Fill
  PRINT '-'             (,{#column2},30)   Fill
  PRINT '-'             (,{#column3},7)    Fill
  PRINT '-'             (,{#column4},30)   Fill
  PRINT '-'             (,{#column5},30)   Fill
  PRINT '-'             (,{#column6},5)    Fill
  PRINT '-'             (,{#column7},7)    Fill
  PRINT '-'             (,{#column8},12)   Fill
end-heading


begin-procedure P000-Init-Report
#debug9 Show 'P000-Init-Report'

  move 'GEXBN019'              to $ReportID
  move 'PHARMACY NORMAL HOURS' to $ReportTitle

  do Stdapi-Init

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle
  Show '$ReportDate='  $ReportDate
  Show '$ReportTime='  $ReportTime

  Let $GEXXX911_BENEFIT_PROGRAM_ALIAS = 'BPP.BENEFIT_PROGRAM'
  Let $GEXXX911_Plan_Type_Alias       = '1'
  Let $GEXXX911_Benefit_Plan_Alias    = '1'

  Let $GEXXX909_Acct_CD_Alias         =  'J.ACCT_CD'

  if $prcs_process_instance = ''
    Let $BenefitProgramString = ''''
    Display 'Enter Benefit Program or leave blank to exit.'
    While 1=1
      Input $In-Benefit-Program Maxlen=3 'Benefit Program'
      Uppercase $In-Benefit-Program
      If Rtrim($In-Benefit-Program, ' ') = ''
        Concat '''' With $BenefitProgramString
        Break
      End-If
      If $BenefitProgramString <> ''''
        Concat ''',''' With $BenefitProgramString
      End-If
      Concat $In-Benefit-Program With $BenefitProgramString
    End-While

    If $BenefitProgramString = ''''''
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = '1=1'
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX911_Benefit_Program_Alias || ' In (' || $BenefitProgramString || ')'
      Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA
    End-If

    Let $AcctCdString = ''''
    Display 'Enter Account Code or leave blank to exit.'
    While 1=1
      Input $In-AcctCd Maxlen=25 'Account Code'
      Uppercase $In-AcctCd
      If Rtrim($In-AcctCd, ' ') = ''
        Concat '''' With $AcctCdString
        Break
      End-If
      If $AcctCdString <> ''''
        Concat ''',''' With $AcctCdString
      End-If
      Concat $In-AcctCd With $AcctCdString
    End-While

    If $AcctCdString = ''''''
      Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = '1=1'
      Let $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA = 'ALL'
    Else
      Let $GEXXX909_INCLUDE_ACCT_CD_CRITERIA = $GEXXX909_Acct_Cd_Alias || ' In (' || $AcctCdString || ')'
      Let $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA = $GEXXX909_INCLUDE_ACCT_CD_CRITERIA
    End-If
  else
    Do  GEXXX909-Select-Acct-CD-Parameters
    Do  GEXXX911-Select-BENEFIT-PROGRAM-Parameters
  End-If

  let $X000_WHERE_SELECT_ASOFDATE = $AsOfToday

  Do Format-DateTime($X000_WHERE_SELECT_ASOFDATE, $X000_REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')
  Let $X000_ORDER_BY = 'J.DEPTID, J.EMPLID'

  Show '$X000_WHERE_SELECT_ASOFDATE ='   $X000_WHERE_SELECT_ASOFDATE
  show '$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA       =' $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA
  show '$GEXXX909_INCLUDE_ACCT_CD_CRITERIA               =' $GEXXX909_INCLUDE_ACCT_CD_CRITERIA         

  Do P050-Print-Cover-Page
  Do P060-Position_Data-Lookup
  Do P061-Jobcode-Lookup		!modified by isdvmxd for ITG 56749 on 02/16/2009
  Do P070-Get-Xlat-Lookup
end-procedure


begin-procedure P050-Print-Cover-Page
#debug9 Show 'P050-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'            (+5,1)
  Print '$Prcs_OPRID          ='                                  (+2,5)
  Print $Prcs_OPRID                                               (0,+2)
  Print '$PRCS_RUN_CNTL_ID    ='                                  (+1,5)
  Print $PRCS_RUN_CNTL_ID                                         (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'                 (+5,2)

  Print 'As Of Date :'                                            (+2,5)
  Print $X000_REPORT_HEADING_ASOFDATE                             (0,+2)

  
  print '$GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA  ='   (+2,5)
  print $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA 	  (0,+2)

  print '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   (+1,5)
  print $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA                (0,+2)
  Print 'SORT ORDER FOR THIS REPORT RUN:'                         (+5,2)
  Print $X000_ORDER_BY                                            (+2,5)

  Let #PAGE-COUNT = 0
  NEW-PAGE
End-Procedure


Begin-Procedure P061-Jobcode-Lookup		!modified by isdvmxd for ITG 56749 on 02/16/2009
#debug9 Show 'P061-Jobcode-Lookup'		!modified by isdvmxd for ITG 56749 on 02/16/2009

Load-Lookup
     Name=Jobcode_Lookup
     Rows=100
     Table='PS_JOBCODE_TBL JT'
     Key='JT.JOBCODE'
     Return_Value='JT.DESCR'
     Where='JT.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL WHERE JOBCODE = JT.JOBCODE AND EFFDT <= SYSDATE)'
     Quiet
End-Procedure


!ISDVMXD added for ITG 56749 on 02/16/2009 -- Begin
Begin-Procedure P060-Position_Data-Lookup
#debug9 Show 'P060-Position_Data-Lookup'

Load-Lookup
     Name=Position_Data_Lookup
     Rows=100
     Table='PS_POSITION_DATA PDT'
     Key='PDT.POSITION_NBR'
     Return_Value='PDT.DESCR'
     Where='PDT.EFFDT = (SELECT MAX(EFFDT) FROM PS_POSITION_DATA WHERE POSITION_NBR = PDT.POSITION_NBR AND EFF_STATUS = ''A'' AND EFFDT <= SYSDATE)'
     !Quiet
End-Procedure P060-Position_Data-Lookup
!ISDVMXD added for ITG 56749 on 02/16/2009 -- End

begin-procedure P070-Get-Xlat-Lookup
#debug9 Show 'P070-Get-Xlat-Lookup'

  Let $Where = '     x.fieldname   = ''REG_TEMP''' ||
	       ' And x.eff_status  = ''A''' ||
	     !GEX-TLL  02/12/2008 HCM 9.0 Upgrade Begin 
	      ! ' And x.language_cd = ''ENG''' ||
	     !GEX-TLL  02/12/2008 HCM 9.0 Upgrade End   
	       ' And x.effdt       = (Select Max(effdt)' ||
	   !GEX-TLL  02/12/2008 HCM 9.0 Upgrade Begin 
	     ! '                      From   xlattable x1' ||
	       '                      From   psxlatitem x1' ||
	   !GEX-TLL  02/12/2008 HCM 9.0 Upgrade End 
	       '                      Where  x1.fieldname    = x.fieldname' ||
!GBD 07/15/2002 Begin
!	       '                      And    x1.language_cd  = ''ENG''' ||
!GEX-TLL  02/12/2008 HCM 9.0 Upgrade Begin 
	      ! '                      And    x1.language_cd  = x.language_cd' ||
!GEX-TLL  02/12/2008 HCM 9.0 Upgrade End 	       
	       '                      And    x1.fieldvalue   = x.fieldvalue' ||
!GBD 07/15/2002 End
	       '                      And    x1.effdt       <= sysdate' ||
	       '                      And    x1.eff_status   = ''A'')'

  Load-Lookup
    Name=XlatLookup
    Rows=10
!GEX-TLL  02/12/2008 HCM 9.0 Upgrade Begin     
   !Table='XLATTABLE X'
    Table='PSXLATITEM X'
!GEX-TLL  02/12/2008 HCM 9.0 Upgrade End
    Key='X.FIELDVALUE'
    Return_Value='X.XLATLONGNAME'
    Where=$Where
    Quiet
End-Procedure


begin-procedure P200-Process-Main
#debug9 Show 'P200-Process-Main'
  Let $Data-Found-Flag = 'N'

Begin-Select
J.DEPTID      ()  ON-BREAK
                  LEVEL=1
                  PRINT=NEVER
                  SAVE=$PREV_DEPT
                  AFTER=P200-DEPT-CHANGE 

J.COMPANY
J.JOBCODE
J.EMPLID
J.REG_TEMP
J.STD_HOURS
!SXK 10/20/1999 Begin
!EMP.UNION_CD
!SRK 07/29/2002 Begin
!JL.Union_Cd
J.Union_CD
!SRK 07/29/2002 End
!SXK 10/20/1999 End
J.POSITION_NBR			!added by ISDVMXD for ITG 56749 on 02/16/2009
PR.NAME
BPP.BENEFIT_PROGRAM

  Let $Data-Found-Flag = 'Y'

  Let $deptid = &J.DEPTID

  Add 1 To #dept_count
  Add 1 To #report_count

	!Show 'EMPLID = ' &J.EMPLID		!isdvmxd added for ITg 56749
  Do P210-Print-Record

FROM   PS_PERSONAL_DATA   PR,
!SXK 10/20/1999 Begin
!       PS_EMPLOYMENT      EMP,
!SRK 07/29/2002 Begin
!       PS_JOB_LABOR       JL,
!SRK 07/29/2002 End
!sxk 10/20/1999 End
       PS_JOB             J,
       PS_BEN_PROG_PARTIC BPP

WHERE  J.EFFDT            =
      (SELECT MAX(EFFDT)
       FROM   PS_JOB
       WHERE  EMPLID      = J.EMPLID
       AND    EMPL_RCD   = J.EMPL_RCD
       AND    EFFDT      <= $X000_WHERE_SELECT_ASOFDATE)

AND    J.EFFSEQ           =
      (SELECT MAX(EFFSEQ)
       FROM   PS_JOB
       WHERE  EMPLID      = J.EMPLID
       AND    EMPL_RCD   = J.EMPL_RCD
       AND    EFFDT       = J.EFFDT)

!SXK 10/20/1999 Begin
!AND    J.EMPLID           = EMP.EMPLID
!AND    J.EMPL_RCD        = EMP.EMPL_RCD
!SRK 07/29/02 Begin
!AND    J.EMPLID = JL.EMPLID
!AND    J.EMPL_RCD = JL.EMPL_RCD
!AND   JL.EFFDT = (SELECT MAX(EFFDT)
!		FROM PS_JOB_LABOR
!		WHERE EMPLID = JL.EMPLID
!		AND EMPL_RCD = JL.EMPL_RCD
!		AND EFFDT <= $X000_WHERE_SELECT_ASOFDATE)
!and   jl.effseq = (select max(effseq)
!		from ps_job_labor
!		where emplid = jl.emplid
!		and empl_Rcd# = jl.empl_rcd
!		and effdt = jl.effdt)

!AND    JL.EMPLID         = PR.EMPLID 
And j.emplid = pr.emplid
!SRK 07/29/02 End
!SXK 10/20/1999 End

AND    J.EMPL_STATUS     <> 'T'

AND    BPP.EMPLID         = J.EMPLID
AND    BPP.EMPL_RCD      = J.EMPL_RCD

AND    BPP.COBRA_EVENT_ID     = 0

AND    BPP.EFFDT              =
      (SELECT MAX(EFFDT)
       FROM   PS_BEN_PROG_PARTIC
       WHERE  EMPLID          = BPP.EMPLID
       AND    EMPL_RCD       = BPP.EMPL_RCD
       AND    COBRA_EVENT_ID  = BPP.COBRA_EVENT_ID
       AND    EFFDT          <= $X000_WHERE_SELECT_ASOFDATE)       

AND   [$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA]
AND   [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]

ORDER  BY [ $X000_ORDER_BY ] 
end-SELECT

  If $Data-Found-Flag = 'N'
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,)  Center
  Else
    Print 'Total Employees for Report'   (+2,{#column2})
    Print #report_count                  (,{#column3})  edit 9,999,999
  End-If
end-procedure


begin-procedure P200-DEPT-CHANGE 
#debug9 Show 'P200-DEPT-CHANGE '
  Print 'Total Employees for Dept'       (+2,{#column2})
  Print $prev_dept                       (,+1)                       
  Print ':'                              ()
  Print #dept_count                      (,{#column3})  edit 9,999,999
  Let #dept_count = 0
  new-page
end-procedure


begin-procedure P210-PRINT-RECORD
#debug9 Show 'P210-PRINT-RECORD'
  Lookup XlatLookup &j.reg_temp $reg_temp
  Lookup Position_Data_Lookup &J.POSITION_NBR  $PosName		!ISDVMXD added for ITG 56749 on 02/16/2009
  Lookup Jobcode_Lookup &j.jobcode $JobTitle

	!Show 'POSNAME = ' $PosName
	!Show 'JOBTITLE = ' $JobTitle

  Print &J.EMPLID             (+1,{#column1})   edit xxx-xx-xxxx
  Print &PR.NAME              (,{#column2},30)
  Print &J.COMPANY            (,{#column3})
  !isdvmxd added for ITG 56749 on 02/16/2009 - Begin
  If isblank($PosName)
  	!Show 'Posname is blank so jobtitle = ' $JobTitle
  	Print $JobTitle             (,{#column4})
  Else
  	!Show 'Posname is NNNOOTTT blank, so Posname = ' $PosName
  	Print $PosName              (,{#column4})
  End-If
  !isdvmxd added for ITG 56749 on 02/16/2009 - End 
  Print $reg_temp             (,{#column5})
  Print &J.UNION_CD         (,{#column6})
  Print &BPP.BENEFIT_PROGRAM  (,{#column7})
  Print &J.STD_HOURS          (,{#column8})   edit 9,999,999.99
end-procedure


#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx911.sqc'  !Get benefit_program multiple row run control
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
