!***********************************************************************
!  GEXBN830:  Delete Address change Bas_Activity rows                  *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXBN830 :            Delete Address Change Bas_Activity Rows        *
!                                                                      *
! Narrative:            This program deletes all the rows from         *
!			PS_BAS_ACTIVITY table where bas_action_source  *
!			is TP. The reason is if we let it sit there and*
!			benefits have to deal with all these rows even *
!			though these rows have nothing to do with      *
!			eligibility.                                   *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           PS_BAS_ACTIVITY                                *
!                                                                      *
! Written by:           Sreeni Kutam                                   *
!                                                                      *
! Normally Run:         Daily                                          *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         As Of Date                                   *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! SXK       01/25/00    Initial Creation                               *
!                                                                      *
! GBD       01/02/2003  Add code to select Event Maintenance rows from *
!                       the Bas_Activity table, email the list to HRIS *
!                       personnel, and delete them from Bas_Activity.  *
!                                                                      *
! GBD       02/28/2003  v8.3 Upgrade - added production changes.       *
!                                                                      *
! GBD       01/24/2003  Add code to select email parameters from       *
!                       GEX_INTFC_RULE/DATA.                           *
!                                                                      *
! AXL       09/24/2004  Change data values to reflect changes in the   *
!                       Local 23 contract.                             *
!                                                                      *
! AXL       11/10/2004  Modify GEXBN830 to stop deleting from BAS      *
!                       ACTIVITY where bas_action_source = 'TP'.       *
! MXR       05/15/2007  Modify GEXBN830 to accommodate Open Enrollment *
!                       for Visura                                     *
! MXR       11/29/2007  Modify hardcoded values in existing SQR to     *
!                       delete rows from BAS Activity with a specific  *
!                       date range for benefit programs going through  *
!                       Open Enrollment (C01, C02, C03, C08 and C16)   *
! DZK       11/06/2009  Modify hardcoded values in existing SQR to     *
!                       delete rows from BAS Activity with a specific  *
!                       date range for benefit programs going through  *
!                       Open Enrollment OE10-('C01','C02','C03','C08') *
! ISDVNPK   12/02/2010  Modify hardcoded values in existing SQR as     *
!                           part of OE11 changes                       *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)

Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
   !GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes
    Let $GEXXX911_Benefit_Program_Alias = 'delx.BENEFIT_PROGRAM'
    Do GEXXX911-Select-Benefit-Program-Parameters
    Show 'BENEFIT_PROGRAM     ='  $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA
    #debug9  show '$GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA before P200-Main-Process ='  $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA
   ! GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes
  Do P200-Main-Process
   ! GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes
    Let $GEXXX911_Benefit_Program_Alias = 'bpp.BENEFIT_PROGRAM'
    Do GEXXX911-Select-Benefit-Program-Parameters
    #debug9  show '$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA before P210-Select-EMList ='  $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA
    Let $GEXXX978_SCHED_ID_Alias = 'bp.SCHED_ID'
    Do GEXXX978-Select-sched-id-Parameters
    Show 'SCHED_ID     ='  $GEXXX978_INCLUDE_SCHED_ID_CRITERIA
    #debug9  show '$GEXXX978_INCLUDE_SCHED_ID_CRITERIA before P210-Select-EMList ='  $GEXXX978_INCLUDE_SCHED_ID_CRITERIA
   ! GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes
  Do P210-Select-EMList			! GBD 01/02/2003
  Do Commit-Transaction                 ! GBD 01/02/2003
  Do P300-Finalization
End-Report

Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXBN830'
  Let $ReportTitle   =   'Delete Address Change rows from bas_activity'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

! GEX ISDVNPK 12/02/2010  Uncommented this as part of OE11 changes
  If $prcs_process_instance = ''
    Do P110-SQRW-Run-Controls
   Else
     Do P120-Process-Scheduler-Run-Controls
  End-If
! GEX ISDVNPK 12/02/2010  Uncommented this as part of OE11 changes
  
  Date-time () HH:MI:SS &timeBegan
  Date-time () MM/DD/YYYY &dateBegan
  Show 'Process Began at ' &timeBegan ' on ' &dateBegan
  Show ' '
  show '$start_date         ='  $start_date
  show '$end_date           ='  $end_date
 
End-Procedure

Begin-Procedure P110-SQRW-Run-Controls
#debug9 Show 'P110-SQRW-Run-Controls'
  Do Ask-As-Of-Date
   
End-Procedure

Begin-Procedure P120-Process-Scheduler-Run-Controls
#debug9 Show 'P120-Process-Scheduler-Run-Controls'
  Do GEXRCBN1-SELECT-PARAMETERS
  
!GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes  
  if rtrim(&GEX_RC_BEN.FROMDATE,' ') = ''
         let $start_date = ''
  else
     let $start_date = &GEX_RC_BEN.FROMDATE
  end-if
  if rtrim(&GEX_RC_BEN.THRUDATE,' ') = ''
     let $end_date  = ''
  else
     let $end_date = &GEX_RC_BEN.THRUDATE
  end-if

    Let $GEXXX979_BAS_ACTION_SOURCE_Alias = 'DEL.bas_action_source'
    Do GEXXX979-Select-bas-action-source-Parameters
    show '$GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA =' $GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA

! GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes

End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Sql On-Error=P290-SQL-Error-Found('P200-Main-Process')
delete from ps_bas_activity DEL
where [$GEXXX979_INCLUDE_BAS_ACTION_SOURCE_CRITERIA] ! DEL.bas_action_source = 'TP'  GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes
!and event_dt <= $asofdate
!delete address change events for employees
!in all benefit programs except C01, C02, C03, C08, C16 
and del.emplid||del.empl_rcd in
   (select delx.emplid||delx.empl_rcd
      from ps_ben_prog_partic delx
           where delx.benefit_program not in  ('C01','C02','C03','C08')	! DZK 11/06/2009  added this as per itg#69108. 
           ![$GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA]
           ! GEX ISDVNPK 12/02/2010  Modified this as part of OE11 changes
           and delx.effdt = (select max(effdt) from ps_ben_prog_partic
                    where emplid    = delx.emplid
                    and   empl_rcd  = delx.empl_rcd
                    and   effdt    <= DEL.event_dt))
end-sql

end-procedure
! GBD 01/02/2003 Begin
!***********************************************************************
Begin-Procedure P210-Select-EMList
!***********************************************************************
  let $first_row = 'Y'
  let $em_found = 'N'
  let $filler = ' '

begin-select 
bas.emplid
bas.empl_rcd
bas.event_dt
bas.bas_action

  let $emplid = &bas.emplid
 
  let #empl_rcd = &bas.empl_rcd
  Do Format-Number(#empl_rcd, $empl_rcd,'9')

  let $bas_event_dt = &bas.event_dt
  Do Convert-to-dtu-date($bas_event_dt,$event_dbf)
  let $event_yy = substr($event_dbf,1,4)
  let $event_mm = substr($event_dbf,6,2)
  let $event_dd = substr($event_dbf,9,2)
  let $event_dt = $event_mm||'/'||$event_dd||'/'||$event_yy

  let $bas_action = &bas.bas_action

  let $em_found = 'Y'
  if $first_row = 'Y'
     Do P215-Open-EMList
     let $first_row = 'N'
     write 1 from 'Emplid     ':11
                  ' Empl_Rcd':9
                  '   Event Dt':11
                  ' Bas Action':11
  end-if
  
  write 1 from  $emplid:11
                $filler:8
                $empl_rcd:1
                $filler:1
                $event_dt:10
                $filler:8
                $bas_action:3

from ps_bas_activity bas
!where bas.event_dt between  '27-DEC-2009' AND '31-DEC-2009'      !DZK 11/06/2009  Added this line as per new schedule and commented above line
where bas.event_dt between  $start_Date AND $end_date      ! GEX ISDVNPK 12/02/2010  Added this line as part of OE11 changes
and   bas.emplid||bas.empl_rcd in (select bpp.emplid||bpp.empl_rcd
                                    from ps_ben_prog_partic bpp
                                    where [$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA]  ! bpp.benefit_program in ('C01','C02','C03','C08') !DZK 11/06/2009 Commented the above line as per the itg 69108
                                    ! GEX ISDVNPK 12/02/2010  Added & commented the below line as part of OE11 changes
                                    and   bpp.effdt = (select max(bpc.effdt) 
                                                       from ps_ben_prog_partic bpc
                                                       where bpc.emplid    = bpp.emplid
                                                       and   bpc.empl_rcd  = bpp.empl_rcd
                                                       and   bpc.effdt    <= bas.event_dt))
and   exists (select bp.emplid
              from ps_bas_partic bp
              where bp.emplid   = bas.emplid
             !  and bp.sched_id = 'OE10')   !DZK 11/06/2009 added this line and commented the above line as per new schedule(ITG# 69108)
               and [$GEXXX978_INCLUDE_SCHED_ID_CRITERIA])  ! GEX ISDVNPK 12/02/2010  Added this line as part of OE11 changes
end-select
      
  if $em_found = 'Y'
     Close 1
     Do P220-Email-EMList
     ! GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes
      Let $GEXXX911_Benefit_Program_Alias = 'bpp2.BENEFIT_PROGRAM'
    	Do GEXXX911-Select-Benefit-Program-Parameters
    	#debug9  show '$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA before P230-Delete-EMList  ='  $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA
      Let $GEXXX978_SCHED_ID_Alias = 'bp2.SCHED_ID'
      Do GEXXX978-Select-sched-id-Parameters
      #debug9  show '$GEXXX978_INCLUDE_SCHED_ID_CRITERIA before P210-Select-EMList ='  $GEXXX978_INCLUDE_SCHED_ID_CRITERIA
     ! GEX ISDVNPK 12/02/2010  Added this as part of OE11 changes
      Do P230-Delete-EMList
  end-if  

End-Procedure

!***********************************************************************
Begin-Procedure P215-Open-EMList
!***********************************************************************
let $filename = '{OUTFILE}'||'gexbn830.dat'
open $filename as 1

     for-writing record=42

if #writestat != 0
   display 'Error Opening output file.  Program terminating.'
   stop
end-if

End-Procedure

!***********************************************************************
Begin-Procedure P220-Email-EMList
!***********************************************************************

  Do GEXXX951-Select-Oracle-Instance
  let $db_name = lower($GEXXX951_Oracle_Instance)
  Do GEXXX971-Select-Trans-Type-Parameters
  if ($GEXXX971_EMAIL_ADDR_CRITERIA = '') and ($GEXXX971_FTP_ADDR_CRITERIA = '')
     show 'Either no rows exist on GEX_INTFC_RULE/DATA for this Oprid/Run Control ID'
     show 'or the most current rows under this ID have been set to inactive. Data will'
     show 'not be sent.'
  else
     if $GEXXX971_GEX_TRANS_TYPE = 'E'
        show $GEXXX971_EMAIL_ADDR_CRITERIA
        show 'This is the email statement produced: '
        let $UNIX_MAIL_COMMAND = '/usr/local/bin/todos -v $PS_HOME/outgoing/gexbn830.dat '||
        '&&/usr/local/bin/mailer.pl "'||$GEXXX971_EMAIL_ADDR_CRITERIA||'" '||
        '"GEXBN830 - Event Maintenance Rows Deleted from BAS_ACTIVITY" '||
        '"$BINDIR/gexbn830_msg.sh" $PS_HOME/outgoing/gexbn830.dat'


        show $UNIX_MAIL_COMMAND

        CALL SYSTEM USING $UNIX_MAIL_COMMAND #system_status

        if #system_status <> 0
           display 'eMail of gexbn830.dat failed. File exists in $PS_HOME/outgoing directory.'
           STOP
        else
           display 'gexbn830.dat eMailed successfully.'
        end-if
     end-if
     if $GEXXX971_GEX_TRANS_TYPE = 'F'
        show $GEXXX971_FTP_ADDR_CRITERIA
        show 'Need to insert a Call FTP shell script to run ftp'
     end-if

  end-if

End-Procedure

!***********************************************************************
Begin-Procedure P230-Delete-EMList
!***********************************************************************
begin-sql 
delete from
ps_bas_activity bas2
!where bas2.event_dt between  '27-DEC-2009' AND '31-DEC-2009'      !DZK 11/06/2009  Added this line as per new schedule and commented above line
where bas2.event_dt between  $start_Date AND $end_date        ! GEX ISDVNPK 12/02/2010  Added this line as part of OE11 changes
and   bas2.emplid||bas2.empl_rcd in (select bpp2.emplid||bpp2.empl_rcd
                                    from ps_ben_prog_partic bpp2
                                    where [$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA]  ! bpp2.benefit_program in ('C01','C02','C03','C08') !DZK 11/06/2009 commented the above line,modified the benefit program as per new schedule 
                                    	! GEX ISDVNPK 12/02/2010  Added & commented the below line as part of OE11 changes
                                       and   bpp2.effdt = (select max(bpc2.effdt) 
                                                       from ps_ben_prog_partic bpc2
                                                       where bpc2.emplid    = bpp2.emplid
                                                       and   bpc2.empl_rcd  = bpp2.empl_rcd
                                                       and   bpc2.effdt    <= bas2.event_dt))
and   exists (select bp2.emplid
              from ps_bas_partic bp2
              where bp2.emplid   = bas2.emplid
	       ! and bp2.sched_id = 'OE10' )   !DZK 11/06/2009 added this line and commented the above line as per new schedule(ITG# 69108) 
	        and [$GEXXX978_INCLUDE_SCHED_ID_CRITERIA])  ! GEX ISDVNPK 12/02/2010  Added this line as part of OE11 changes
	       


end-sql

End-Procedure

Begin-Procedure P290-SQL-Error-Found($Proc_Name)
#debug9 Show 'P290-SQL-Error-Found'
  Show ' '
  Show 'SQL Error in ' $Proc_Name 
  Show 'SQL Error:  ' $_sql-error
  Stop
End-Procedure

Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Date-time () HH:MI:SS &timeEnded
  Date-time () MM/DD/YYYY &dateEnded
  Show 'Process Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of process'
End-Procedure

!***********************************************************************
#include 'askaod.sqc'    !Ask user for $AsOfDate
#include 'gexxx920.sqc'  !Get ben single row run control
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
#Include 'tranctrl.sqc'                                ! GBD 01/02/2003
#Include 'gexxx951.sqc'  !Oracle dbase name              GBD 01/24/2003
#Include 'gexxx971.sqc'  !Email and FTP parameters       GBD 01/24/2003
#Include 'gexxx911.sqc'  !Get Benefit Program Multiples Run Controls
#Include 'gexxx978.sqc'  !Get Sched ID Multiples Run Controls
#Include 'gexxx979.sqc'  !Get BAS ACTION SOURCE Multiples Run Controls

