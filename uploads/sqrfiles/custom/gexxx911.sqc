!**************************************************************************
!  GEXXX911:  Retrieve "Multiple" Benefit Program Run Controls and        *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX911_Benefit_Program_Alias                                      *
!    $GEXXX911_Plan_Type_Alias                                            *
!    $GEXXX911_Benefit_Plan_Alias                                         *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA                           *
!    $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA                           *
!    $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA                   *
!    $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA                   *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX911_Benefit_Program_Alias  = 'bpp.benefit_program'         *
!    Let $GEXXX911_Plan_Type_Alias        = 'hb.plan_type'                *
!    Let $GEXXX911_Benefit_Plan_Alias     = 'hb.benefit_plan'             *
!    Do GEXXX911-Select-Benefit-Program-Parameters                        *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    From ps_health_benefit hb                                            *
!    Where [$GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA]                   *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!	?	JDH 		05/28/98 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX911-Select-Benefit-Program-Parameters

  Let $OprID                = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID          = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_BEN_PRGM_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = ''
  Let $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = ''
  Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_BEN_PRGM.BENEFIT_PROGRAM
GEX_RC_BEN_PRGM.PLAN_TYPE
GEX_RC_BEN_PRGM.BENEFIT_PLAN

  Add 1 To #GEX_RC_BEN_PRGM_ROWS

  If Rtrim(&GEX_RC_BEN_PRGM.PLAN_TYPE, ' ') = ''
    Let $Include_Condition =   '(' || $GEXXX911_Benefit_Program_Alias || ' = '''  || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM || ''')'
    Let $Exclude_Condition =   '(' || $GEXXX911_Benefit_Program_Alias || ' != ''' || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM || ''')'
  Else
    If Rtrim(&GEX_RC_BEN_PRGM.BENEFIT_PLAN, ' ') = ''
      Let $Include_Condition = '(' || $GEXXX911_Benefit_Program_Alias || ' = '''  || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM ||
                         ''' And ' || $GEXXX911_Plan_Type_Alias       || ' = '''  || &GEX_RC_BEN_PRGM.PLAN_TYPE       || ''')'
      Let $Exclude_Condition = '(' || $GEXXX911_Benefit_Program_Alias || ' != ''' || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM ||
                         ''' Or '  || $GEXXX911_Plan_Type_Alias       || ' != ''' || &GEX_RC_BEN_PRGM.PLAN_TYPE       || ''')'
    Else
      Let $Include_Condition = '(' || $GEXXX911_Benefit_Program_Alias || ' = '''  || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM ||
                         ''' And ' || $GEXXX911_Plan_Type_Alias       || ' = '''  || &GEX_RC_BEN_PRGM.PLAN_TYPE       ||
                         ''' And ' || $GEXXX911_Benefit_Plan_Alias    || ' = '''  || &GEX_RC_BEN_PRGM.BENEFIT_PLAN    || ''')'
      Let $Exclude_Condition = '(' || $GEXXX911_Benefit_Program_Alias || ' != ''' || &GEX_RC_BEN_PRGM.BENEFIT_PROGRAM ||
                         ''' Or '  || $GEXXX911_Plan_Type_Alias       || ' != ''' || &GEX_RC_BEN_PRGM.PLAN_TYPE       ||
                         ''' Or '  || $GEXXX911_Benefit_Plan_Alias    || ' != ''' || &GEX_RC_BEN_PRGM.BENEFIT_PLAN    || ''')'
    End-If
  End-If

  If #GEX_RC_BEN_PRGM_ROWS = 1
    Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = '(' || $Include_Condition
    Let $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_BEN_PRGM GEX_RC_BEN_PRGM

Where GEX_RC_BEN_PRGM.OPRID                         = [$OprID]
And   GEX_RC_BEN_PRGM.RUN_CNTL_ID                   = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_BEN_PRGM.BENEFIT_PROGRAM,  ' ') != ' '
!And  Rtrim(GEX_RC_BEN_PRGM.PLAN_TYPE, ' ')        != ' '
!And  Rtrim(GEX_RC_BEN_PRGM.BENEFIT_PLAN, ' ')     != ' '

End-Select

  If #GEX_RC_BEN_PRGM_ROWS = 0
    Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX911_Benefit_Program_Alias || ' = '  || $GEXXX911_Benefit_Program_Alias || ' And ' ||
                                                     $GEXXX911_Plan_Type_Alias       || ' = '  || $GEXXX911_Plan_Type_Alias       || ' And ' ||
                                                     $GEXXX911_Benefit_Plan_Alias    || ' = '  || $GEXXX911_Benefit_Plan_Alias
    Let $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA = $GEXXX911_Benefit_Program_Alias || ' != ' || $GEXXX911_Benefit_Program_Alias || ' Or '  ||
                                                     $GEXXX911_Plan_Type_Alias       || ' != ' || $GEXXX911_Plan_Type_Alias       || ' Or '  ||
                                                     $GEXXX911_Benefit_Plan_Alias    || ' != ' || $GEXXX911_Benefit_Plan_Alias
  Else
    Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA         = $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA || ')'
    Let $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA         = $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA || ')'
    Let $GEXXX911_INCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = $GEXXX911_INCLUDE_BENEFIT_PROGRAM_CRITERIA
    Let $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_DISPLAY_CRITERIA = $GEXXX911_EXCLUDE_BENEFIT_PROGRAM_CRITERIA
  End-If

End-Procedure
