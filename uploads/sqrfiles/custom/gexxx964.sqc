!**************************************************************************
!  GEXXX964:  Retrieve "Multiple" Paygroup Run Controls and               *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variables prior to calling this SQC:       *
!    $GEXXX964_Company_Alias                                              *
!    $GEXXX964_Paygroup_Alias                                             *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA                          *
!    $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA                          *
!    $GEXXX964_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA                          *
!    $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA                          *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX964_Company_Alias  = 'J.COMPANY'                           *
!    Let $GEXXX964_Paygroup_Alias = 'J.PAYGROUP'                          *
!    Do GEXXX964-Select-Company-Paygroup-Parameters                       *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_JOB J                                                        *
!    WHERE [$GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA]                  *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!	ID#	INITIALS	DATE		DESCRIPTION OF THE CHANGE *
!**************************************************************************
!  HR-10078	CWB 		04/18/2002 	INITIAL CREATION          *
!**************************************************************************


Begin-Procedure GEXXX964-Select-Company-Paygroup-Parameters

  Let #GEX_RC_PAYGP_SL_ROWS = 0
  Let $Include_Condition    = ''
  Let $Exclude_Condition    = ''
  Let $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA = ''
  Let $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = ''
  Let $GEXXX964_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_PAYGP_SL.COMPANY
GEX_RC_PAYGP_SL.PAYGROUP

  Add 1 To #GEX_RC_PAYGP_SL_ROWS

  Let $Include_Condition = '(' || $GEXXX964_Company_Alias  || ' = '''  || &GEX_RC_PAYGP_SL.COMPANY  || ''' And ' || $GEXXX964_Paygroup_Alias || ' = '''  || &GEX_RC_PAYGP_SL.PAYGROUP || ''')'
  Let $Exclude_Condition = '(' || $GEXXX964_Company_Alias  || ' != ''' || &GEX_RC_PAYGP_SL.COMPANY  || ''' Or '  || $GEXXX964_Paygroup_Alias || ' != ''' || &GEX_RC_PAYGP_SL.PAYGROUP || ''')'

  If #GEX_RC_PAYGP_SL_ROWS = 1
    Let $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Include_Condition
    Let $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = '(' || $Exclude_Condition
  Else
    Let $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ' Or '  || $Include_Condition
    Let $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA || ' And ' || $Exclude_Condition
  End-If

From  PS_GEX_RC_PAYGP_SL GEX_RC_PAYGP_SL

Where GEX_RC_PAYGP_SL.GEX_SEQ             = #GEX_SEQ
And   Rtrim(GEX_RC_PAYGP_SL.COMPANY,  ' ') != ' '
And   Rtrim(GEX_RC_PAYGP_SL.PAYGROUP, ' ') != ' '

End-Select

  If #GEX_RC_PAYGP_SL_ROWS = 0
    Let $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX964_Company_Alias  || ' = ' || $GEXXX964_Company_Alias  || ' And ' ||
                                                      $GEXXX964_Paygroup_Alias || ' = ' || $GEXXX964_Paygroup_Alias
    Let $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX964_Company_Alias  || ' = ' || $GEXXX964_Company_Alias  || ' And ' ||
                                                      $GEXXX964_Paygroup_Alias || ' = ' || $GEXXX964_Paygroup_Alias
  Else
    Let $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA = $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA || ')'
    Let $GEXXX964_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX964_INCLUDE_COMPANY_PAYGROUP_CRITERIA
    Let $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA = $GEXXX964_EXCLUDE_COMPANY_PAYGROUP_CRITERIA
  End-If

End-Procedure
