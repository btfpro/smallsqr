!***********************************************************************
!  GEXPY105:  ATTENDANCE CARDS                                         *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY105:             Attendance Cards Report                        *
!                                                                      *
! Narrative:            The Report is FTP'd and Sandy Granberg uses    *
!                       for her process to produce Attndance Cards     *
!                                                                      *
! SQL Tables:           personal_data                                  *
!                       job                                            *
!                       employment                                     *
!                                                                      *
! Written by:           Joe Besagar                                    *
!                                                                      *
! Normally Run:	        Yearly                                         *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Company Division Code                        *
!                         Company                                      *
!                         Paygroup                                     *
!                         Deptid                                       *
!                         Employee Status                              *
!                         Account Code                                 *
!                         As Of Date                                   *
!                                                                      *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!INITIALS  DATE     DESCRIPTION OF THE CHANGE                          *
!***********************************************************************
! JNB  10/21/99     Initial Creation                                   *
!                                                                      *
! JNB  02/03/2000   Changed zip to postal for 7.5 upgrade              *
!                                                                      *
! CWB  11/07/2000   Added logic to get the year for the                *
!                   printline from the AsOfDate on the panel.          *
!                                                                      *
! AXL  12/07/2001   Create a DAT file instead of an LIS file           *
!                                                                      *
! SCM  11/25/2003   Changed indent because of hole punch on new form   *
!                   Moved everything to the right.  Removed mainframe  *
!                   newpage character and replaced w/unix decimal value*
!                                                                      *
! AXL  12/02/2004   Restored indent to 5 spaces due to a change in the *
!                   form.                                              *
!                                                                      *
! CWB  11/28/2006   ITG 37558 Remove SSN, addresses and birthdate      *
!                   from the cards.                                    *
!                                                                      *
!Manish 8/20/2007   HCM 9.0 Upgrade                                    *
!GEXMXT 4/02/2008   ADDED THE join condition for PER_ORG_INST          *
! MXR   06/11/2008  Uncommented one line for hire_dt
!***********************************************************************

#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'
!***********************************************************************
Begin-Setup
!***********************************************************************

#Include 'setupdb.sqc' 

End-Setup

!***********************************************************************
Begin-Report
!***********************************************************************

  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc 
  show '$prcs_process_instance = ' $prcs_process_instance             !SCM  11/25/2003

  Do Initialization
  Do Open-File
  Do Main-Process
  Do EOJ-Process

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report

!***********************************************************************
Begin-Procedure Initialization
!***********************************************************************

  Let $GEXXX900_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Company_Alias  = 'J.COMPANY'
  Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'
  Let $GEXXX902_Deptid_Alias   = 'J.DEPTID'
  Let $GEXXX903_Emp_Stat_Alias = 'J.EMPL_STATUS'
  Let $GEXXX907_Div_CD_Alias   = 'GDT.GEX_COMPANY_DIV_CD'                    
  Let $GEXXX909_Acct_CD_Alias  = 'J.ACCT_CD'

  If $prcs_process_instance = ''
    Do SQRW-Run-Controls  
  Else
    Do Process-Scheduler-Run-Controls
  End-if

  encode '<012>' into $page_break          ! Unix page break-decimal value-SCM  11/25/2003
   
  !CWB 11/07/2000 Begin
  Let $Card_Year = substr($WHERE_SELECT_ASOFDATE,8,4)  
  show '$Card_Year ' $Card_Year
  !CWB 11/07/2000 End

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  Let $ReportID    = 'GEXPY150'
  Let $ReportTitle = 'ATTENDANCE CARDS'

  Show '$ReportID='    $ReportID
  Show '$ReportTitle=' $ReportTitle

  Let $ORDER_BY = 'J.COMPANY, J.PAYGROUP, J.DEPTID, P.NAME'  
  Do Format-DateTime($WHERE_SELECT_ASOFDATE, $REPORT_HEADING_ASOFDATE, {DEFDATE}, '', '')


  Show '$GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA          ='   $GEXXX900_INCLUDE_COMPANY_DISPLAY_CRITERIA
  Show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA ='   $GEXXX901_INCLUDE_COMPANY_PAYGROUP_DISPLAY_CRITERIA
  Show '$GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA           ='   $GEXXX902_INCLUDE_DEPTID_DISPLAY_CRITERIA
  Show '$GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA         ='   $GEXXX903_INCLUDE_EMP_STAT_DISPLAY_CRITERIA
  Show '$GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA           ='   $GEXXX907_INCLUDE_DIV_CD_DISPLAY_CRITERIA  
  Show '$GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA          ='   $GEXXX909_INCLUDE_ACCT_CD_DISPLAY_CRITERIA
  Show '$WHERE_SELECT_ASOFDATE                              ='   $WHERE_SELECT_ASOFDATE
  Show '$ORDER_BY                                           ='   $ORDER_BY

End-Procedure

!***********************************************************************
begin-procedure Open-File
!***********************************************************************

!  let $file1 = '{OUTFILE}' || lower($ReportID) || '.dat'          
!***   SCM  11/25/2003   Since this file is now being printed by gexpy077, the file
!***   name needed to have the processinstance number and be a .LIS file.
 
  let $file1 = '{OUTFILE}' || lower($ReportID) ||'_' || $prcs_process_instance ||'_1' ||'.LIS'

  show 'output file name is $file1 = ' $file1
!***   SCM  11/25/2003  end
 
  open $file1 as 1 for-writing
        record=60:fixed                         
        status=#filestat
  if #filestat < 0
     display 'Open failed for file <' noline
     display $file1 noline
     display '>'
     stop
  else
     !AXL 12/2/2004 Knock out 3 spaces - Begin
     let $Filler  = '     ' || 'XXXXXXXXXXXXXXXXXXXXXXXXX2000-00'
     let $Filler1 = '     ' || 'XXXXXXXXXXXXXXXXXXXXXXXXXXXXXX'
     let $Filler2 = '     ' || 'XXXXXXXXXXXXXXXXXXXXXXXXX00000'
     let $Filler3 = '     ' || '999-99-9999              '
     let $Filler4 = '     ' || 'XXX  XXX  XXXX  XXXXXXX'
     let $Filler5 = '     ' || 'XXXXX  XXXXXXXXXX'
     let $Filler6 = '     ' || 'BD  99/99/99  HD  99/99/99'    
     let $filler7 = '     ' || 'DEPTID'
     !AXL 12/2/2004 Knock out 3 spaces - End
     
     while #count < 3
!       write 1 from  '1':1		!For main frame page-breaking    SCM  11/25/2003
       write 1 from  $page_break:1              !SCM  11/25/2003  unix new-page
       write 1 from $Filler:45
       write 1 from $Filler1:45
       write 1 from $Filler1:45
       write 1 from $Filler2:45
       write 1 from $Filler3:45
       write 1 from $Filler4:45
       write 1 from $Filler5:45
       write 1 from $Filler6:45
       write 1 from $filler7:45		!AXL 12/2/2004 New
       add 1 to #count
      end-while		
  end-if

end-procedure

!***********************************************************************
Begin-Procedure SQRW-Run-Controls 
!***********************************************************************

  display ''
  display 'Year printed on cards is derived from the ensuing date prompt'
  display ''

  Do GEXXX950-Input-Parameters
  Let $WHERE_SELECT_ASOFDATE = $X000_WHERE_SELECT_ASOFDATE

End-Procedure

!***********************************************************************
Begin-Procedure Process-Scheduler-Run-Controls 
!***********************************************************************

  Do GEXXX922-Select-Parameters
  Do GEXXX900-Select-Company-Parameters
  Do GEXXX901-Select-Company-Paygroup-Parameters
  Do GEXXX902-Select-Deptid-Parameters
  Do GEXXX903-Select-Emp-Stat-Parameters
  Do GEXXX907-Select-Div-CD-Parameters 
  Do GEXXX909-Select-Acct-CD-Parameters

  Let $AsOfDate = $GEX_RC_PAY.AsOfDate
  Let $WHERE_SELECT_ASOFDATE = $GEX_RC_PAY.AsOfDate
  If Rtrim ($GEX_RC_PAY.AsOfDate, ' ') = ''
    Let $WHERE_SELECT_ASOFDATE = $AsOfToday
  End-If

End-Procedure

!***********************************************************************
Begin-Procedure Main-Process
!***********************************************************************

  Let $Data-Found-Flag = 'N'
 
Begin-Select
J.COMPANY  
J.DEPTID   
J.ACCT_CD  
J.JOBCODE  
J.EMPLID 
J.EMPL_STATUS  
SUBSTR(J.EMPLID,6,4) &last_4_digits
J.POSITION_NBR
J.PAYGROUP 
J.SETID_DEPT
P.NAME     &p.name
P.ADDRESS1 &p.address1
P.ADDRESS2 &p.address2
P.CITY     &p.city
P.STATE    &p.state
P.POSTAL
P.BIRTHDATE
PO.ORIG_HIRE_DT                 

  let $jobcode  = &j.jobcode
  let $Acctcd   = &j.acct_cd
 
  do Get-Acct-cd-descr 
  let $acctdescr = upper($AcctNameAbbrv)
  
  let $name     = upper(&p.name) 
  let $deptid   = &j.deptid
  let $BU       = &J.SETID_DEPT
  
  do Get-Department-Name 
  Do get-job-descr
   if $Jobcode_Found='N'
      do get-jobcode-descr
   end-if
  

  Let $Data-Found-Flag = 'Y'
  
  do format-datetime(&po.orig_hire_dt,$orig_hire_dt,{DEFDATE},'','')	

  add 1 to #record_count
  
  Do Write-Record

FROM PS_JOB J,
     PS_PERSONAL_DATA P,
     PS_PER_ORG_INST PO,	
     PS_EMPLOYMENT E,
     PS_GEX_DEPT_TBL GDT
  WHERE J.EMPLID = P.EMPLID
    AND P.EMPLID = E.EMPLID
    AND PO.EMPLID = J.EMPLID
    AND PO.EMPLID = P.EMPLID
    AND PO.EMPLID = E.EMPLID
    AND PO.ORG_INSTANCE_ERN  = J.EMPL_RCD	
    AND J.EMPL_RCD = E.EMPL_RCD
    AND J.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_JOB
        WHERE J.EMPLID = EMPLID
          AND J.EMPL_RCD = EMPL_RCD
          AND EFFDT <= $WHERE_SELECT_ASOFDATE)
    AND J.EFFSEQ =
        (SELECT MIN(EFFSEQ) FROM PS_JOB
        WHERE J.EMPLID = EMPLID
          AND J.EMPL_RCD = EMPL_RCD
          AND J.EFFDT = EFFDT)
    AND GDT.DEPTID = J.DEPTID
    AND GDT.EFFDT=
        (SELECT MAX(EFFDT) FROM PS_GEX_DEPT_TBL
        WHERE GDT.DEPTID = DEPTID
          AND EFFDT <= $WHERE_SELECT_ASOFDATE)
    AND GDT.SEQUENCE_NUMBER =
        (SELECT MAX(SEQUENCE_NUMBER) FROM PS_GEX_DEPT_TBL
        WHERE GDT.DEPTID = DEPTID
          AND GDT.EFFDT  = EFFDT)
     AND [$GEXXX900_INCLUDE_COMPANY_CRITERIA]
     AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA] 
     AND [$GEXXX903_INCLUDE_EMP_STAT_CRITERIA]
     AND [$GEXXX902_INCLUDE_DEPTID_CRITERIA]
     AND [$GEXXX907_INCLUDE_DIV_CD_CRITERIA]
     AND [$GEXXX909_INCLUDE_ACCT_CD_CRITERIA]
Order By [$ORDER_BY]

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure get-job-descr
!***********************************************************************
  let $Jobcode_Found='N'
Begin-Select
D.JOBCODE
D.DESCR
  
  let $Jobcode_Found='Y'
  let $jobdescr     = &D.DESCR

FROM PS_POSITION_DATA D 
WHERE D.jobcode = $jobcode 
AND D.POSITION_NBR=&J.POSITION_NBR
AND D.EFFDT =(SELECT MAX(D1.EFFDT) FROM PS_POSITION_DATA D1
               WHERE D1.POSITION_NBR=D.POSITION_NBR
               AND D1.EFFDT<=$WHERE_SELECT_ASOFDATE)
End-Select
End-Procedure

!***********************************************************************
Begin-Procedure get-jobcode-descr
!***********************************************************************

Begin-Select

JD.JOBCODE
JD.DESCRSHORT

  let $jobdescr     = &JD.DESCRSHORT

FROM PS_JOBCODE_TBL JD 
WHERE JD.jobcode = $jobcode 
  AND JD.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL
        WHERE JOBCODE = JD.JOBCODE 
          AND EFFDT <= $WHERE_SELECT_ASOFDATE)

End-Select

End-Procedure

!***********************************************************************
Begin-Procedure Write-Record
!***********************************************************************

  let $indent = '     '   !AXL 12/02/2004  deleted 3 spaces
     
  let $emplid = edit(&j.emplid,'xxx-xx-xxxx')
  let $SSN      = &last_4_digits
  let $Status   = &J.EMPL_STATUS
  
  !let $Line1 =  $indent || $NAME
  !let $Line2 =  $indent || 'TM SS#: XXX-XX-' || $SSN
  !let $Line3 =  $indent || 'Hire Date:' || $orig_hire_dt
  !let $Line4 =  $indent || 'Store#&Location:' ||$deptid|| '  ' ||$DeptName
  !let $Line5 =  $indent || 'Job Title/Department:' ||$jobdescr||'  ' ||$acctdescr
  !let $Line6 =  $indent || 'Status:'||$Status
  
  let $Line1 =  $indent || $NAME
  let $Line2 =  $indent || 'XXX-XX-' ||$SSN
  let $Line3 =  $indent || $orig_hire_dt
  let $Line4 =  $indent || $deptid|| '  ' ||$DeptName
  let $Line5 =  $indent || $jobdescr||'  ' ||$acctdescr
  let $Line6 =  $indent || $Status
  
  write 1 from $page_break:1            
  write 1 from $Line1:60
  write 1 from $LINE2:60
  write 1 from $LINE3:60
  write 1 from $LINE4:60
  write 1 from $LINE5:60
  write 1 from $LINE6:60
  
  write 1 from $page_break:1            
  write 1 from $Line1:60
  write 1 from $LINE2:60
  write 1 from $LINE3:60
  write 1 from $LINE4:60
  write 1 from $LINE5:60
  write 1 from $LINE6:60
    
End-Procedure

!***********************************************************************
Begin-Procedure EOJ-Process
!***********************************************************************
  let $records = edit(#record_count,'999,999')

  If $Data-Found-Flag = 'N'
    display 'NO DATA SELECTED FOR THIS REPORT RUN'
  else 
    display 'Total Records Printed For This Run: ' noline
    display $records
  End-If

  close 1

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

End-Procedure


#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'askaod.sqc'    !Ask for As-Of-Date parameter
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getcodta.sqc'  !Get Company Data
#Include 'getpgdta.sqc'  !Get Paygroup Data
#include 'gexxx900.sqc'  !Get company multiple row table
#include 'gexxx901.sqc'  !Get company/paygroup multiple row table
#include 'gexxx902.sqc'  !Get deptid multiple row table
#include 'gexxx903.sqc'  !Get employee status multiple row table
#include 'gexxx907.sqc'  !Get company/division code multiple row table
#include 'gexxx909.sqc'  !Get acct_cd code multiple row table
#include 'gexxx922.sqc'  !Get pay single row run control
#include 'gexxx950.sqc'  !Get SQRW run controls
#include 'getactnm.sqc'  !Get Account code descr 
#include 'getdptnm.sqc'  !Get Department Descr