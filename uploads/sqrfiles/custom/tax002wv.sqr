!***********************************************************************
!  TAX002WV:  Qtrly State Employee Wage List - WEST VIRGINIA           *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:08:53                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101   
!
!VENDKXY   10/09/2008   Brought forward the changes from 8.3           *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Get USA variables

! VENDKXY Brought forward the changes from 8.3 BEGIN

#include 'setup03x.sqc' !Printer and page-size initialization

!begin-setup

!#include 'prt002.sqc'  !Printer and page-size initialization

!end-setup

! VENDKXY Brought forward the changes from 8.3 END

begin-report
  do Init-Report

  do Process-Main

  if #Employees_On_Page > 0 and #Employees_On_Page < 18
    do Page-Complete
  end-if

  do Stdapi-Term

end-report

begin-procedure Init-Report
  display 'Quarterly Employee Wage List - West Virginia'

  do Init-DateTime
  do Init-Number

  do Get-Tax-Reporting-Run-Controls

  move $PeriodEndDate to $AsOfDate  !for included procedures

  do Initialize
  do Get-Calendar-Year-Id
  do Stdapi-Init

end-procedure

begin-procedure Initialize
#ifdef TAXTEST
  move 'CA' to $State
#else
  move 'WV' to $State
#endif
end-procedure

begin-heading 19
  uppercase $RptQtrYr

  uppercase $CompanyName
  print $CompanyName              (+11,8)
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    print &Employer_ID_SUT        (,54)
  else
    print &Employer_ID_SWT        (,54)
  end-if
  print $RptQtrYr                 (,69,4)
  move $Compnyadd1 to $CoAddress1
  uppercase $CoAddress1
  print $CoAddress1               (+1,8)
  let $field = RTRIM($Compnycity, ' ')
  uppercase $field
  print $field                    (+1,8)
  print &CT.State                 ()      edit ,bxx
  move  &CT.Postal               to $Zip
  move  $Zip                     to $ZipExtsn ~~~~~~xxxx

  if instr($Zip,'-',6) = 0
  and $ZipExtsn <> '    '
      let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if
  print $Zip                      (0,+2)

  Evaluate &TX.Balance_Qtr
   when = 1
     move '0430' to $HoldDueDate
     move &TX.Balance_Year to #HoldYear
   when = 2
     move '0731' to $HoldDueDate
     move &TX.Balance_Year to #HoldYear
   when = 3
     move '1031' to $HoldDueDate
     move &TX.Balance_Year to #HoldYear
   when = 4
     move '0131' to $HoldDueDate
     move &TX.Balance_Year to #BalanceYear
     let #HoldYear = &TX.Balance_Year + 1
  end-evaluate
  print $HoldDueDate                  (+1,71)  edit xx/xx/
  do Format-Number(#HoldYear, $out, '9999')
  print $out                           (,77)
  if #PAGE_NUM = 1
    do Format-Number(&EmplCount, $out, '99999')
    print $out                      (63,5) edit  xbxbxbxbxb
    do Format-Number(&TB.NLGrs_QTD, $out, 'b99999999V99')
    print $out                      (,28) edit  xbxbxbxbxbxbxbxbxbxbx
  end-if
   do Format-Number(#PageTotal, $out, 'b9999999V99')
   print $out                       (63,60) edit xbxbxbxbxbxbxbxbxbx
   do Format-Number(#PAGE_NUM, $out, '999')
   print $out                      (65,6)
   do Format-Number(#TotalPages, $out, '999')
   print $out                      (,+3)
end-heading

begin-procedure Process-Main

  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
C.COMPANY        () on-break print=never level=1 before=Company-Change
C.STATE
C.EMPLID         () on-break print=never level=2 after=EmplID-Change
C.TAX_CLASS
C.LOCALITY
C.TXGRS_QTD
C.NLGRS_QTD

D1.NATIONAL_ID   () on-break print=never level=2 save=$SSN
PN.LAST_NAME      () on-break print=never level=2 save=$Name
PN.FIRST_NAME
PN.MIDDLE_NAME

  move &C.Company        to $Company
  move &PN.First_Name    to $FName
  move &PN.Middle_Name   to $MName
  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_PERSON_NAME PN,
      PS_PERS_NID D1, PS_COMPANY_TBL A
WHERE C.EMPLID            = D1.EMPLID
  AND C.EMPLID            = PN.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
#ifdef TAXTEST
  AND C.STATE             = 'CA'
#else
  AND C.STATE             = 'WV'
#endif
  AND C.LOCALITY          = ' '
  AND C.TAX_CLASS         = 'U'
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = &TX.Balance_Year
  AND C.BALANCE_QTR       = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD    =
      (SELECT MAX(C1.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C1
       WHERE  C1.EMPLID        = C.EMPLID
         AND  C1.COMPANY       = C.COMPANY
         AND C1.BALANCE_ID     = C.BALANCE_ID
         AND C1.BALANCE_YEAR   = C.BALANCE_YEAR
         AND C1.BALANCE_QTR    = C.BALANCE_QTR
         AND  C1.STATE         = C.STATE
         AND  C1.LOCALITY      = C.LOCALITY
         AND  C1.TAX_CLASS     = C.TAX_CLASS)
  AND C.NLGRS_QTD > 0
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
ORDER BY C.COMPANY, D1.NATIONAL_ID, C.STATE, C.LOCALITY, C.TAX_CLASS
end-SELECT
end-procedure

begin-procedure Process-Tax-Balance
  do Format-Number(&C.NLGrs_QTD, $NLGrs_QTD, 'b9999999V99')
  add &C.NLGrs_QTD to #PageTotal
  move &C.EmplID to $EmplID
end-procedure

begin-procedure EmplID-Change
  move $SSN to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM($SSN, ' ') = ''
    print 'Applied For' (+2,8)
  else
    print $SSN          (+2,7)  edit xbxbxbbbxbxbbbxbxbxbx
  end-if

  uppercase $Name
  uppercase $FName
  uppercase $MName

  print $Name           (0,34,16)  edit xbxbxbxbxbxbxbxb
  print $FName          (0,52,1)
  print $MName          (0,55,1)

  print  $NLGrs_QTD     (,60) edit xbxbxbxbxbxbxbxbxbx
  move '' to $NLGrs_QTD

  add 1 to #Employees_On_Page

  if #Employees_On_Page = 18
    do Page-Complete
  end-if
end-procedure

begin-procedure Get-Total
begin-SELECT

COUNT(*)          &EmplCount,

#ifdef DB2ALL
DECIMAL(SUM(TB.NLGRS_QTD),15,3) &TB.NLGrs_QTD
#else
SUM(TB.NLGRS_QTD)    &TB.NLGrs_QTD
#endif

FROM  PS_TAX_BALANCE TB, PS_COMPANY_TBL A2
WHERE TB.COMPANY       = &C.Company
#ifdef TAXTEST
  AND TB.STATE         = 'CA'
#else
  AND TB.STATE         = 'WV'
#endif
  AND TB.LOCALITY      = ' '
  AND TB.TAX_CLASS     = 'U'                 ! Unemployment
  AND TB.BALANCE_ID    = $Calendar_Year_Id
  AND TB.BALANCE_YEAR  = &TX.Balance_Year
  AND TB.BALANCE_QTR   = &TX.Balance_Qtr
  AND TB.NLGRS_QTD     > 0
  AND TB.BALANCE_PERIOD       =
      (SELECT MAX(C2.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C2
       WHERE  C2.EMPLID        = TB.EMPLID
         AND  C2.COMPANY       = TB.COMPANY
         AND C2.BALANCE_ID    = TB.BALANCE_ID
         AND C2.BALANCE_YEAR  = TB.BALANCE_YEAR
         AND C2.BALANCE_QTR   = TB.BALANCE_QTR
         AND  C2.STATE         = TB.STATE
         AND  C2.LOCALITY      = TB.LOCALITY
         AND  C2.TAX_CLASS     = TB.TAX_CLASS)
  AND A2.COMPANY = TB.COMPANY
  AND A2.EFF_STATUS = 'A'
  AND A2.TAX_REPORT_TYPE = '2'
  AND A2.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A2.COMPANY
         AND  EFFDT  <= $AsOfDate)
GROUP BY TB.COMPANY, TB.STATE
end-SELECT

  let #TotalPages = CEIL(&EmplCount/18)
end-procedure

begin-procedure Page-Complete
  add 1 to #PAGE_NUM

  new-page
  move 0 to #PageTotal
  move 0 to #Employees_On_Page
end-procedure

begin-procedure Company-Change
  if #Employees_On_Page < 18 and #Employees_On_Page > 0
    do Page-Complete
  end-if
  move 0 to #PAGE_NUM
  move 0 to #TotalPages
  move &C.Company to $Company
  do Get-Company-Data
  do Get-State-Tax-Data             !Employer ID
  do Get-Total
end-procedure

begin-procedure Ignore
end-procedure

#Include 'rotname1.sqc'  !Rotate-Name procedure
#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
