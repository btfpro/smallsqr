!**************************************************************************
!  GEXXX927:  Retrieve "Multiple" Vendor ID Run Controls and              *
!               Return a Formatted Where-Clause                           *
!-------------------------------------------------------------------------*
!                                                                         *
!  User must set the following variable prior to calling this SQC:        *
!    $GEXXX927_Vendor_ID_Alias                                            *
!                                                                         *
!  User can access the following variables after calling this SQC:        *
!    $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA                                 *
!    $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA                                 *
!    $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA                         *
!    $GEXXX927_EXCLUDE_VENDOR_ID_DISPLAY_CRITERIA                         *
!                                                                         *
!                                                                         *
!  Sample usage:                                                          *
!    Let $GEXXX927_Vendor_ID_Alias = 'BPT.VENDOR_ID'                       *
!    Do GEXXX927-Select-Vendor-ID-Parameters                              *
!    Begin-Select                                                         *
!    EMPLID                                                               *
!    FROM PS_BENEF_PLAN_TBL BPT                                           *
!    WHERE [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA]                         *
!    End-Select                                                           *
!                                                                         *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
! This module is the confidential and proprietary information of          *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted    *
! in any form, by any means, in whole or in part, nor is it to be used    *
! for any purpose other than that for which it is expressly provided      *
! without the written permission of Giant Eagle.                          *
!                                                                         *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved            *
!                                                                         *
!**************************************************************************


!**************************************************************************
!                         MODIFICATION HISTORY                            *
!**************************************************************************
!       ID#     INITIALS        DATE            DESCRIPTION OF THE CHANGE *
!**************************************************************************
!       ?       JDH             10/20/98        INITIAL CREATION          *
!                                                                         *
!   HR10313     GBD             09/12/02        v8.3 Upgrade - Provider   *
!                                               changed to Vendor_ID      *
!**************************************************************************


Begin-Procedure GEXXX927-Select-Vendor-ID-Parameters

  Let $OprID               = '''' || $Prcs_OprID       || ''''
  Let $Run_Cntl_ID         = '''' || $Prcs_Run_Cntl_ID || ''''
  Let #GEX_RC_VENDOR_ID_ROWS = 0
  Let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = ''
  Let $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA = ''
  Let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'ALL'
  Let $GEXXX927_EXCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'NONE'

Begin-Select

GEX_RC_VENDOR.VENDOR_ID

  Add 1 To #GEX_RC_VENDOR_ID_ROWS

  If #GEX_RC_VENDOR_ID_ROWS = 1
    Let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '(' || $GEXXX927_Vendor_ID_Alias || ' In ('''     || &GEX_RC_VENDOR.VENDOR_ID || ''''
    Let $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA = '(' || $GEXXX927_Vendor_ID_Alias || ' Not In (''' || &GEX_RC_VENDOR.VENDOR_ID || ''''
  Else
    Let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA || ',''' || &GEX_RC_VENDOR.VENDOR_ID  || ''''
    Let $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA = $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA || ',''' || &GEX_RC_VENDOR.VENDOR_ID  || ''''
  End-If

From  PS_GEX_RC_VENDOR GEX_RC_VENDOR

Where GEX_RC_VENDOR.OPRID                = [$OprID]
And   GEX_RC_VENDOR.RUN_CNTL_ID          = [$Run_Cntl_ID]
And   Rtrim(GEX_RC_VENDOR.VENDOR_ID, ' ') != ' '

End-Select

  If #GEX_RC_VENDOR_ID_ROWS = 0
    Let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = $GEXXX927_Vendor_ID_Alias || ' = ' || $GEXXX927_Vendor_ID_Alias
    Let $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA = $GEXXX927_Vendor_ID_Alias || ' = ' || $GEXXX927_Vendor_ID_Alias
  Else
    Let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA || '))'
    Let $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA = $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA || '))'
    Let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
    Let $GEXXX927_EXCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_EXCLUDE_VENDOR_ID_CRITERIA
  End-If

End-Procedure
