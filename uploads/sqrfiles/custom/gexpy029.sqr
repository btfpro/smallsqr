!***********************************************************************
! GEXPY029:  This quarterly report will show who paid                  *
!            during the previous quarter.                              *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced, or transmitted*
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS    DATE           DESCRIPTION OF THE CHANGE                *
!***********************************************************************
!  JDH         07/22/1999     INITIAL CREATION                         *
!  SXK         11/29/1999     Changed from zip to postal               *
!  CWB         10/12/2000     Removed $state from the procedure        *
!                              get-locality-desc to get correct desc.  *
!                                                                      *
!  CWB         05/22/2002     Fixed page break problems                *
!                                                                      *
!  DXS         04/07/2008     Added hint for performance               *
!                                                                      *
! GEX_SEC_REENG 2015-06-24 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
!                                                                      *
!  MM          08/14/2017     Sort output by State and Locality        *
!                             instead of Company and Locality          *
!***********************************************************************


#include 'setenv.sqc' !Set environment


begin-setup
  #include 'setup32a.sqc'

  declare-report Local-Tax
Printer-Type=HPLASERJET
    layout=DEFAULT
  end-declare

  declare-report Report-log
Printer-Type=HPLASERJET
    layout=DEFAULT
  end-declare
end-setup


begin-heading 6 for-reports=(report-log)
  let $ReportID    = 'PY029LOG'
  let $ReportTitle = 'Report Log'

 #Include 'stdhdg01.sqc'

  print 'Trans#'  (+2,1)
  print 'Emplid'  (,18)
  print 'Message' (,50)

  print '-'       (+1,1,174) fill
end-heading


begin-heading 7 for-reports=(Local-Tax)
  let $ReportID    = 'PY029TAX'
  let $ReportTitle = 'Local Tax Report'

!  MM  08/14/2017 BEGIN
! #Include 'stdhdg02.sqc' 
 #Include 'stdhdg01.sqc'  !Inculde Report Header 

  if $state <> $priorState
    do Get-State-Data
    move $state to $priorState
  end-if

  print 'State:'          (3,1,11)
  print $state           (0,13,3)
  print $StateName       (0,17)

!  MM  08/14/2017 END

  if $Locality <> $priorLocality
    do get-locality-desc
    move $Locality to $priorLocality
  end-if

  print $reporting_quarter   (3,) center

  print 'Locality:'           (+1,1)
  print $Locality            (,13)
  print $locality_Desc       (,+1)

  print 'Name'               (+2,1)
 !print 'Employee ID'        (,060)                                     !  MM  08/14/2017
  print 'Employee ID'        (,040)                                     !  MM  08/14/2017   Changed the Position for Employee ID
  print 'Company'            (,060)                                     !  MM  08/14/2017   Added Company to the detail
  print 'QTD Taxable Gross'  (,080)
  print 'YTD Taxable Gross'  (,106)
  print 'QTD Tax Deduction'  (,128)
  print 'YTD Tax Deduction'  (,150)

 !print '-'                  (+2,1,50) fill                             !  MM  08/14/2017
  print '-'                  (+2,1,30) fill                             !  MM  08/14/2017
  print '-'                  (,040,11) fill                             !  MM  08/14/2017
  print '-'                  (,060,11) fill  
  print '-'                  (,080,17) fill  
  print '-'                  (,106,17) fill  
  print '-'                  (,128,17) fill  
  print '-'                  (,150,17) fill  
end-heading


begin-report
  use-report Local-Tax

  do Init-DateTime
  do Init-Number
  do stdapi-init
  
  move 'Local Tax Report' to $ReportTitle
  display $ReportTitle

  do Get-Current-DateTime
  let $reportdate_mm = substr($reportdate,1,2)
  let $reportdate_dd = substr($reportdate,4,2)
  let $reportdate_yy = substr($reportdate,7,2)
  do MakeYear4Digits ($reportdate_yy)

  let $reportdate_ccyyyy = $reportdate_mm||'/'||$reportdate_dd||'/'||$reportdate_yy

  do format-datetime($reportdate_ccyyyy,$reportdate_ccyy,{DEFMDY},'','NATIVE') 

  If $prcs_process_instance = ''
    Input $balance_year Maxlen=4 'Balance Year'
    Input $balance_qtr  Maxlen=2 'Balance Quarter'
    Let #balance_year = $balance_year
    Let #balance_qtr  = $balance_qtr
  Else
    Do GEXXX922-Select-Parameters
    Let #balance_year = #GEX_RC_PAY.Balance_Year
    Let #balance_qtr  = #GEX_RC_PAY.Balance_Qtr
  End-If

  If #balance_year = 0 Or
     #balance_qtr  = 0
    Do Get-Default-Quarter
  End-If

  Move #balance_year To $balance_year 9999
  Move #balance_qtr  To $balance_qtr  9

  Let $reporting_quarter = 'Balance Year=' || $balance_year || ' Balance Quarter=' || $balance_qtr

  do Report
  do Commit-Transaction

  date-time () hh:mi:ss &timeEnded
  display 'Report Ended: ' noline
  display &timeEnded

  do stdapi-term
  do reset
end-report


begin-procedure Get-Default-Quarter
begin-select loops=1
balance_year
balance_period

  let #balance_year   = &balance_year
  let #balance_period = &balance_period

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  evaluate #balance_qtr
    when = 1
      subtract 1 from #balance_year
      let #balance_qtr = 4
      break
    when-other
      subtract 1 from #balance_qtr
  end-evaluate

from   ps_balance_id_def
where  balance_id = 'CY'
and    sysdate between begin_dt and end_dt
end-select
end-procedure


begin-procedure Report
  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan

  move 'N' to $Errorfound

  do Process-Employee-Data

  date-time () hh:mi:ss &timeProcess
  display #InputTran 99999 noline
  display ' Transactions Processed: ' noline
  display &timeProcess
end-procedure


begin-procedure Process-Employee-Data
  move 'N' to $errorfound
  move 'N' to $found
begin-select
/*+ index(c,ps_personal_data) */  ! DXS, 04/07/08, Added hint for performance
!  MM  08/14/2017 BEGIN       Sort/print by State and Locality
!B.COMPANY  () on-break level=1
!                       print=never
!                       save=$prev_company
!                       after=company-change

C.STATE    () on-break level=1
                       print=never
                       save=$prev_state
                       after=state-change
!  MM  08/14/2017 END
B.LOCALITY () on-break level=2
                       print=never
                       save=$prev_locality
                       before=new-page
                       after=locality-change

B.BALANCE_QTR
B.BALANCE_PERIOD
B.TXGRS_QTD
B.TXGRS_YTD
B.TAX_QTD
B.TAX_YTD
C.NAME
C.ADDRESS1
C.ADDRESS2
C.CITY
!C.STATE                                    !  MM  08/14/2017
B.COMPANY                                   !  MM  08/14/2017
!SXK 11/29/99 Changed from zip to postal
C.POSTAL
B.EMPLID

  add 1 to #inputtran

  move &B.COMPANY     to $company
  move &B.LOCALITY    to $locality
  move &C.STATE       to $state

  move 'Y' to $found

  add 1 to #locality_count

  if &B.BALANCE_QTR = #balance_qtr
    let #tax_qtd   = &B.TAX_QTD
    let #txgrs_qtd = &B.TXGRS_QTD
  else
    let #tax_qtd   = 0
    let #txgrs_qtd = 0
  end-if

  add #tax_qtd   to #locality_tax_qtd
  add #txgrs_qtd to #locality_txgrs_qtd

  add &B.TXGRS_YTD to #locality_txgrs_ytd
  add &B.TAX_YTD   to #locality_tax_ytd

  do print-detail-info

FROM   PS_PERSONAL_DATA  C,
       PS_TAX_BALANCE    B

WHERE  C.EMPLID            = B.EMPLID

AND    B.BALANCE_ID        = 'CY'
AND    B.BALANCE_YEAR      = #balance_year

AND    B.BALANCE_PERIOD    =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE
       WHERE  EMPLID       = B.EMPLID
       AND    COMPANY      = B.COMPANY
       AND    BALANCE_ID   = B.BALANCE_ID
       AND    BALANCE_YEAR = B.BALANCE_YEAR
       AND    BALANCE_QTR <= #balance_qtr
       AND    TAX_CLASS    = B.TAX_CLASS
       AND    LOCALITY     = B.LOCALITY)

AND    B.TAX_CLASS        <> 'P'     !Eliminate Occupation Tax Class
AND    B.LOCALITY         <> ' '

!ORDER BY B.COMPANY, B.LOCALITY, B.EMPLID                  !  MM  08/14/2017
ORDER BY C.STATE, B.LOCALITY, B.COMPANY, B.EMPLID          !  MM  08/14/2017
end-select
  if $found = 'N'
    do error-found
    ! show 'Doing new-page .. no employees'
    new-page
    print 'No Employees found for the Report '(,34)
    use-report Local-Tax
  end-if
end-procedure


begin-procedure new-page
! CWB 05/22/2002 BEGIN
!  if #page-count > 1
  if $prev_locality <> ''
! CWB 05/22/2002 END
    ! show 'in procedure new-page'
    new-page
  end-if
end-procedure


begin-procedure locality-change
  print 'Total for Locality' (+2,1)
  print $prev_locality       (,+1)
SHOW #locality_count
 !print #locality_count      (,60)  edit 999,999,990          !  MM  08/14/2017    Changed the position of locality count
  print #locality_count      (,40)  edit 999,999,990          !  MM  08/14/2017 
  print #locality_txgrs_qtd  (,80)  edit 99,999,999,990.00
  print #locality_txgrs_ytd  (,106) edit 99,999,999,990.00
  print #locality_tax_qtd    (,128) edit 99,999,999,990.00
  print #locality_tax_ytd    (,150) edit 99,999,999,990.00

!  MM  08/14/2017  BEGIN
!  add #locality_count     to #company_count
!  add #locality_txgrs_qtd to #company_txgrs_qtd
!  add #locality_txgrs_ytd to #company_txgrs_ytd
!  add #locality_tax_qtd   to #company_tax_qtd
!  add #locality_tax_ytd   to #company_tax_ytd

  add #locality_count     to #state_count
  add #locality_txgrs_qtd to #state_txgrs_qtd
  add #locality_txgrs_ytd to #state_txgrs_ytd
  add #locality_tax_qtd   to #state_tax_qtd
  add #locality_tax_ytd   to #state_tax_ytd
!  MM  08/14/2017  END

  move 0 to #locality_count
  move 0 to #locality_txgrs_qtd
  move 0 to #locality_txgrs_ytd
  move 0 to #locality_tax_qtd
  move 0 to #locality_tax_ytd
end-procedure


begin-procedure company-change
  print 'Total for Company' (+2,1)
  print $prev_company       (,+1)

  print #company_count      (,60)  edit 999,999,990
  print #company_txgrs_qtd  (,80)  edit 99,999,999,990.00
  print #company_txgrs_ytd  (,106) edit 99,999,999,990.00
  print #company_tax_qtd    (,128) edit 99,999,999,990.00
  print #company_tax_ytd    (,150) edit 99,999,999,990.00

  move 0 to #company_count
  move 0 to #company_txgrs_qtd
  move 0 to #company_txgrs_ytd
  move 0 to #company_tax_qtd
  move 0 to #company_tax_ytd
end-procedure

!  MM  08/14/2017  BEGIN
begin-procedure state-change
  print 'Total for State' (+2,1)
  print $prev_state       (,+1)

  print #state_count      (,40)  edit 999,999,990
  print #state_txgrs_qtd  (,80)  edit 99,999,999,990.00
  print #state_txgrs_ytd  (,106) edit 99,999,999,990.00
  print #state_tax_qtd    (,128) edit 99,999,999,990.00
  print #state_tax_ytd    (,150) edit 99,999,999,990.00

  move 0 to #state_count
  move 0 to #state_txgrs_qtd
  move 0 to #state_txgrs_ytd
  move 0 to #state_tax_qtd
  move 0 to #state_tax_ytd
end-procedure
!  MM  08/14/2017  END

begin-procedure print-detail-info
! CWB 05/22/2002 BEGIN
  if (#sqr-max-lines - #current-line) <= 6 
    ! show 'new-page in print-detail-info' ! and = to <=
    new-page
  end-if
! CWB 05/22/2002 END
  let $City = Rtrim(&C.CITY, ' ')
  ! show 'Printing ....' &C.NAME ' ' #sqr-max-lines ' ' #current-line
  print &C.NAME       (+2,1)
 !print &B.EMPLID     (,60)          !edit xxx-xx-xxxx !GEX_SEC_REENG 2015-06-24 Vahini Katta    !  MM  08/14/2017 
  print &B.EMPLID     (,40)          !  MM  08/14/2017 
  print &B.COMPANY    (,60)          !  MM  08/14/2017 
  print #txgrs_qtd    (,80)  edit 99,999,999,990.00
  print &B.TXGRS_YTD  (,106) edit 99,999,999,990.00
  print #tax_qtd      (,128) edit 99,999,999,990.00
  print &B.TAX_YTD    (,150) edit 99,999,999,990.00

  print &C.ADDRESS1   (+1,1)

  if RTRIM(&C.ADDRESS2, ' ') <> ''
    print &C.ADDRESS2 (+1,1)
  end-if

  print $City         (+1,1)
  print ','           (,)
  print &C.STATE      (,+1)
  print &C.POSTAL        (,+2)
end-procedure


begin-procedure get-locality-desc
begin-select
L.LOCALITY_NAME

  move &L.LOCALITY_NAME to $locality_desc

FROM  PS_LOCAL_TAX_TBL L

!CWB - 10/12/2000 Begin
!WHERE L.STATE          = $state
!AND   L.LOCALITY       = $locality
!CWB - 10/12/2000 End

WHERE   L.LOCALITY       = $locality
AND   L.EFF_STATUS     = 'A'
AND   L.EFFDT          =
     (SELECT MAX(EFFDT)
      FROM   PS_LOCAL_TAX_TBL 

!CWB - 10/12/2000 Begin
!      WHERE  STATE     = $state
!      AND    LOCALITY  = $locality
!CWB - 10/12/2000 End

      WHERE    LOCALITY  = $locality
      AND    EFFDT    <= sysdate)
end-SELECT
end-procedure


!  MM  08/14/2017  BEGIN
begin-procedure get-State-Data
begin-select
DESCR 

   move &DESCR to $StateName

FROM PS_STATE_NM_US_VW
WHERE STATE = $state
end-select
end-procedure
!  MM  08/14/2017  END

begin-procedure SQL-Error-Found($Proc_Name) 
  do error-found
  print 'SQL Error in ' (,34)
  print $Proc_Name ()  
  print $_sql-error () 
  print 'sql-status:' ()
  print #_sql-status () edit 99999 
  use-report Local-Tax
end-procedure


begin-procedure Error-Found
  move 'Y' to $ErrorFound
  use-report report-log
  do Format-Number(#InputTran, $out, '99999')
  print $out (+1,1)
  !GEX_SEC_REENG 2015-06-24 Vahini Katta begins
  !let $EmplId_1st3 = substr($EmplId,1,3)
  !let $EmplId_mid2 = substr($EmplId,4,2)
  !let $EmplId_last4 = substr($EmplId,6,4)
 
  !String $EmplId_1st3 $EmplID_mid2 $EmplId_last4 by '-' into $EmplId11

  !print $EmplID11 (,16)
  print $EmplId (,16)
  !GEX_SEC_REENG 2015-06-24 Vahini Katta Ends
end-procedure

#Include 'gexxx922.sqc'  !Get GEX_RC_PAY run controls
#Include 'getcodta.sqc'  !Get-Company-Data Procedures
#Include 'getpgdta.sqc'  !Get-Paygroup-Data Procedures
#Include 'getdptnm.sqc'  !Get-Department-Name Procedures
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Get_Run_Control Procedure
#Include 'datemath.sqc'  !Does the date-math functions
#Include 'validdt.sqc'   !Validates Native Date
