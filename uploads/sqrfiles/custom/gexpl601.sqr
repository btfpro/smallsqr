!***********************************************************************
!  GEXPL601:  Local 23 Pension Interface                               *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPL601:             Local 23 Pension Interface                     *
!                                                                      *
! Narrative:		This program creates a monthly report for      *
!                       Local 23 and a file that contains employee     *
!                       information required by the union to           *
!                       administer the pension benefit.                *
!                                                                      *
! #Debugx Used:		#debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:		pay_calendar                                   *
!                       ben_defn_pgm                                   *
!                       personal_data                                  *
!                       employment                                     *
!                       job                                            *
!                       benef_defn_optn                                *
!                       benef_plan_tbl                                 *
!                       pay_line                                       *
!                       pay_check                                      *
!                       pay_deduction                                  *
!                       gex_hrs_bal                                    *
!                       earnings_bal                                   *
!                       health_benefit                                 *
!                                                                      *
! Written by:		Jim Hutchison                                  *
!                                                                      *
! Normally Run:		Monthly                                        *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Plan Type(s)                                 *
!                         Vendor ID(s)                                 *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
!  INITIALS   DATE       DESCRIPTION OF THE CHANGE                     *
!***********************************************************************
!  JDH        01/20/98   Initial Creation                              *
!  JDH        02/26/99   Add descending sort on effseq.                *
!                        Performance Tuning.                           *
!  JDH        03/01/99   Added missing line to select                  *
!  JDH        03/12/99   Fixed problem with earnings_bal select.       *
!                        Fixed problem with gex_hrs_bal select.        *
!  JDH        04/13/99   Get the effdt, action, action_reason from     *
!                          the job row that originally set the         *
!                          empl_status to its current value for        *
!                          empl_status in ('L','P','R','T').           *
!  JNB        06/10/99   Emps in benefit program C03 get 166.67 hrs    *
!  JDH        06/29/99   Changed logic to get action, action_reason,   *
!                          effdt.                                      *
!  SXK        10/21/99   changed to have union from job_labor          *
!  SXK        11/16/1999 Changed zip to postal and ssn to empli        *
!  CJH        07/25/2001 Remove JOB_LABOR and associated columns from  *
!                        main select - they are not used in the SQR    *
!                        but are hindering performance.                *
!  PSR        08/06/2002 Modified UNION_CD FROM JOB_LABOR TO JOB       * 
!                                          - Upgrade 8.3               * 
!                        Change in Column name from Provider - Vendor_id
!  PSR        08/27/2002 EMPL_RCD is now part of the key for           *
!                        PS_EARNINGS_BAL - Upgrade 8.3                 *
!  GBD        09/12/2002 v8.3 Upgrade - Provider field changed to      *
!                        Vendor ID.                                    *
!  GBD        10/16/2002 Modified code to eliminate multiple row select*
! ISDVASS     03/26/2008 Upgrade 9.0 Changes 
!  GEX-TLL    05/21/2008 HCM 9.0 Upgrade (Emplid to SSN Conversion)    *
! VENDKXY     11/19/2009 Done Changes requested for ITG 69940 			   * 
! GEXBN_848_E130211_01 2012-07-09 Vahini Katta                         *
! Take out the vendor id hardcoding while writing to the dat file      *
!                           when store changes in month being processed*
!ISDVAWD     04/22/2014     ITG# 827  - Changed file path  '\\nt5\' *
!			       with '\\corp.gianteagle.com\'`	       *
! GEX_SEC_REENG 2015-04-29 Vahini Katta                                *
! Changes related to gex_emplid_mapp/7 digit emplid                    *
! Ujwal Dyasani 5/4/2017  to fix Emplid mismatch issue                 *
!
!  GEXBN_848_E0041390_01   08/22/2016                                  * 
!                         1. Correct spelling from TOAL MTD HRS 			 *
!							to TOTAL MTD HRS           					                     *
!                        2. Insert new Rate between existing columns	 *
!							TOAL MTD HRS and PENSION CONTRIB AMT.	                   *
!							Rate has to be fetched from GEX_FLAT_RATE                *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup32.sqc'   !Printer and page-size initialization

!GEX-TLL 05/21/2008 HCM 9.0 Upgrade Changes Begin
!#define #column1     1
!#define #column2    32
!#define #column3    42
!#define #column4    51
!#define #column5    60
!#define #column6    65
!#define #column7    72
!#define #column8    79
!#define #column9    88
!#define #column10   98
!#define #column11  113
!#define #column12  128
!#define #column13  143
!#define #column14  158
#define #column1     1
#define #column2    29
#define #column2_1  39         !vendnrr 04-16-2007  ITG-42082
#define #column3    47
#define #column4    56
#define #column5    65
#define #column6    70
#define #column7    77
#define #column8    84
#define #column9    93
#define #column10  103
#define #column11  117
#define #column12  131
#define #column13  146
#define #column14  161  

!GEX-TLL 05/21/2008 HCM 9.0 Upgrade Changes End
Begin-Heading 8
#debug9 Show 'Begin-Heading'

#Include 'stdhdg01.sqc'

!  Print 'Provider:'            (3,1)
  Print 'VendorID:'            (3,1)
  Print $Provider              (3,13)

  Print $Reporting_Period      (3,) Center 

  Print 'Ben Pgm:'             (4,1)
  Print $Benefit_Program       (4,13)
  Print $BenefitProgramDescr   (4,) Center

  print 'SERVICE'              (+2,{#column3})
  print 'BIRTH'                (+0,{#column4})
  print 'EMPL'                 (+0,{#column5})
  print 'ACTION'               (+0,{#column7}) 
  print 'EFF'                  (+0,{#column8})
  print 'BENEFIT'              (+0,{#column9})
  print '          MTD'        (+0,{#column10})     !GEX-TLL 05/21/2008 Added
  print '           MTD'       (+0,{#column11})
  print '         TOTAL'       (+0,{#column12})
  print '       PENSION'       (+0,{#column13})
  print '         TOTAL'       (+0,{#column14})

  print 'EMPLOYEE NAME'        (+1,{#column1}) 
  print 'SSN'                  (+0,{#column2})
  print 'Emplid'               (+0,{#column2_1})    !GEX-TLL 05/21/2008 Added
  print 'DATE'                 (+0,{#column3})
  print 'DATE'                 (+0,{#column4})
  print 'STAT'                 (+0,{#column5})
  print 'ACTION'               (+0,{#column6}) 
  print 'REASON'               (+0,{#column7}) 
  print 'DATE'                 (+0,{#column8})
  print 'TERM DATE'            (+0,{#column9})
  print '        HOURS'        (+0,{#column10})	    !GEX-TLL 05/21/2008 Changed
  print '    VACN HOURS'       (+0,{#column11})
  print '     MTD HOURS'       (+0,{#column12})
  print '   CONTRIB AMT'       (+0,{#column13})
  print '     YTD HOURS'       (+0,{#column14})

  !print '-'                    (+1,{#column1},30)  FILL  !GEX-TLL 05/21/2008
  print '-'                    (+1,{#column1},28)  FILL   !GEX-TLL 05/21/2008
  print '-'                    (+0,{#column2},09)  FILL
  print '-'                    (+0,{#column2_1},07)FILL   !GEX-TLL 05/21/2008 Added
  print '-'                    (+0,{#column3},08)  FILL
  print '-'                    (+0,{#column4},08)  FILL
  print '-'                    (+0,{#column5},04)  FILL
  print '-'                    (+0,{#column6},06)  FILL
  print '-'                    (+0,{#column7},06)  FILL
  print '-'                    (+0,{#column8},08)  FILL
  print '-'                    (+0,{#column9},09)  FILL
  !print '-'                    (+0,{#column10},14) FILL  !GEX-TLL 05/21/2008
  !print '-'                    (+0,{#column11},14) FILL  !GEX-TLL 05/21/2008
  print '-'                    (+0,{#column10},13) FILL   !GEX-TLL 05/21/2008
  print '-'                    (+0,{#column11},13) FILL   !GEX-TLL 05/21/2008
  print '-'                    (+0,{#column12},14) FILL
  print '-'                    (+0,{#column13},14) FILL
  print '-'                    (+0,{#column14},14) FILL
End-Heading


Begin-Report
#debug9 Show 'Begin-Report'
 MOVE $CURRENT-DATE TO $CURRDT 'MMDDYY'   !VENDKXY Done the Changes for ITG 69940
 
!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - - Changed Report name 
!*****************************************************************
!  let $report-name = '\\nt5\ftproot\benefits\Local 23_GEXPL601\' ||'PL601PDF_' || $CURRDT || '.pdf'  !VENDKXY Done the changes for ITG 69940
   let $report-name = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\Local 23_GEXPL601\' ||'PL601PDF_' || $CURRDT || '.pdf'  !VENDKXY Done the changes for ITG 69940
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************
 
  new-report  $report-name   !VENDKXY Done the changes for ITG 69940
!  Do Set-Optimizer-Goal                 ! ISDVASS Upgrade 9.0 Changes

  Do Init-DateTime
  Do Init-Number
  Do Get-Current-DateTime
  Do Stdapi-Init

  Do P100-Start
  Do Openfile     !VENDKXY Done the changes for ITG 69940
  Do P200-Main-Process
  Do P300-Finish

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  Show 'Successful end of report'
End-Report


Begin-Procedure Set-Optimizer-Goal
  Begin-SQL
    Alter Session Set OPTIMIZER_GOAL=RULE; 
  End-SQL
End-Procedure

!VENDKXY Done the changes for ITG 69940-- Begin
Begin-Procedure Openfile

    encode '<009>' into $delim

!*****************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - Begin - Updated File path   
!*****************************************************************            
!    Let $file601 = '\\nt5\ftproot\benefits\Local 23_GEXPL601\' || 'PL601XLS_' ||  $CURRDT || '.xls'  
	Let $file601 = '\\corp.gianteagle.com\common\HR\HRPS\secure\benefitsk\Local 23_GEXPL601\' || 'PL601XLS_' ||  $CURRDT || '.xls'  
!******************************************************************
! ISDVAWD ITG# 827- 4/22/2014 - End  
!******************************************************************  	  		
    Open $file601 As 2 For-Writing Record=1000:Vary
    

   Move 'PL601XLS'			To $Report  
   encode '<012>' into $newline 
   String 'Report ID:' $Report  ' ' ' ' ' ' ' ' ' ' $ReportTitle by $delim into $gexpl601
   Write 2 from $gexpl601
   Write 2 from $newline 
   
!  20160822 - GEXBN_848_E0041390_01 - Commented - Begin
	
!    String 'VENDORID' 'BEN PGM' 'DEPTID' 'EMPLOYEE NAME' 'SSN' 'EMPLID' 'SERVICE DATE' 'BIRTH DATE' 'EMPL STAT' 'ACTION' 'ACTION REASON' 
!    'EFF DATE' 'BENEFIT TERM DATE' 'MTD HRS' 'MTD VACN HOURS' 'TOAL MTD HRS' 'PENSION CONTRIB AMT' 'TOTAL YTD HRS' by $delim into $gexpl601
!  20160822 - GEXBN_848_E0041390_01 - Commented - End

!  20160822 - GEXBN_848_E0041390_01 - Added Rate Column - Begin
    String 'VENDORID' 'BEN PGM' 'DEPTID' 'EMPLOYEE NAME' 'SSN' 'EMPLID' 'SERVICE DATE' 'BIRTH DATE' 'EMPL STAT' 'ACTION' 'ACTION REASON' 
    'EFF DATE' 'BENEFIT TERM DATE' 'MTD HRS' 'MTD VACN HOURS' 'TOTAL MTD HRS' 'RATE' 'PENSION CONTRIB AMT' 'TOTAL YTD HRS' by $delim into $gexpl601
!  20160822 - GEXBN_848_E0041390_01 - Added Rate Column - End
    Write 2 from $gexpl601
    
End-Procedure

!VENDKXY Done the changes for ITG 69940-- End

Begin-Procedure P100-Start
#debug9 Show 'P100-Start'

 !Move 'GEXPL601'                    To $ReportID  !VENDKXY Done the changes for ITG 69940
  Move 'PL601PDF'			To $ReportID    !VENDKXY Done the changes for ITG 69940
  move 'LOCAL 23 PENSION INTERFACE'   To $ReportTitle

  Show '$ReportID=' $ReportID
  Show '$ReportTitle=' $ReportTitle

  If $prcs_process_instance = ''

    Move '''' to $ProviderString
    Display 'Enter Vendor ID or leave blank to exit.'
    While 1=1
      Input $Provider maxlen=6 'Provider ID'
      if RTRIM($Provider, ' ') = ''
        concat '''' with $ProviderString
        break
      end-if
      if $ProviderString <> ''''
        concat ''',''' with $ProviderString
      end-if
      concat $Provider with $ProviderString
    End-While

    Let $Vendor_ID = $Provider

    if $ProviderString = ''''''
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = 'ALL'
    else
      let $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = 'BPT.VENDOR_ID In (' || $ProviderString || ')'
      let $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA = $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA
    end-if

    Move '''' to $PlanTypeString
    Display 'Enter Plan Type or leave blank to exit.'
    While 1=1
      Input $PlanType Maxlen=2 'Plan Type'
      Uppercase $PlanType
      if RTRIM($PlanType, ' ') = ''
        concat '''' with $PlanTypeString
        break
      end-if
      if $PlanTypeString <> ''''
        concat ''',''' with $PlanTypeString
      end-if
      concat $PlanType with $PlanTypeString
    End-While

    if $PlanTypeString = ''''''
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = 'ALL'
    else
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = 'bpt.plan_type In (' || $PlanTypeString || ')'
      let $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA
    end-if

  Else

    Let $GEXXX912_Plan_Type_Alias = 'bpt.plan_type'
    Let $GEXXX912_Benefit_Plan_Alias = 'bpt.benefit_plan'
    Do GEXXX912-Select-Benefit-Plan-Parameters

    Let $GEXXX927_Vendor_ID_Alias = 'BPT.VENDOR_ID'
    Do GEXXX927-Select-Vendor-ID-Parameters

  End-If

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show 'Report Began at ' &timeBegan ' on ' &dateBegan

  If $GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA = '1=1'
    Display 'At least one plan type parameter must be specified'
    Stop
  End-If

  If $GEXXX927_INCLUDE_VENDOR_ID_CRITERIA = '1=1'
    Display 'At least one vendor id parameter must be specified'
    Stop
  End-If

  Let $X000_ORDER_BY = 'Vendor ID, Benefit Program, Employee Name'

  Show '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA  = '   $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA
  Show '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA      = '   $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA

  Do Get-Calendar-Year-Id    !getbalid.sqc

  Do P140-Get-Last-Confirmed-Pay
  Do P150-Print-Cover-Page
  Do P160-Open-File
End-Procedure


Begin-Procedure P140-Get-Last-Confirmed-Pay
#debug9 Show 'P140-Get-Last-Confirmed-Pay'

Begin-Select Loops=1
pay_end_dt
To_Number(To_Char(pay_end_dt,'MM'))          &balance_period
To_Number(To_Char(pay_end_dt,'YYYY'))        &balance_year
To_Char(pay_end_dt,'fmMonth YYYY')           &period
Last_Day(pay_end_dt)                         &current_month_end
Last_Day(Add_Months(pay_end_dt,-1))          &prev_month_end
Last_Day(Add_Months(pay_end_dt,1))           &next_month_end

  Let $AsOfDate         = &pay_end_dt
  Let $pay_end_dt       = &pay_end_dt
  Let #balance_year     = &balance_year
  Let #balance_period   = &balance_period
  Let $Reporting_Period = &period

  Let $current_month_begin = '01-'||Substr($pay_end_dt,4,8)
  Let $current_month_end   = &current_month_end
  Let $prev_month_begin = '01-'||Substr(&prev_month_end,4,8)
  Let $next_month_end      = &next_month_end

  Let $current_year_begin = '01-JAN-'||Substr($pay_end_dt,8,4)
  Let $current_year_end   = '31-DEC-'||Substr($pay_end_dt,8,4)

  let #balance_qtr = #balance_period
  let #balance_qtr = ((#balance_qtr - 1)/ 3) + 1
  do Format-Number(#balance_qtr, $balance_qtr, '9.99') !avoid rounding the integer portion
  move $balance_qtr to $balance_qtr x     !save the integer portion
  let #balance_qtr = $balance_qtr

  Show 'Report will contain balances for the month of ' $Reporting_Period
  Show 'Last confirmed pay end date:  ' $pay_end_dt

  If Rtrim($pay_end_dt,' ') = ''
    Show 'No confirmed pay period found in calendar'
    Stop
  End-If

From   ps_pay_calendar

Where  pay_end_dt         =
      (Select Max(pay_end_dt)
       From   ps_pay_calendar
       Where  pay_end_dt <= $AsOfToday
       And    pay_confirm_run = 'Y')

And    pay_confirm_run = 'Y'
End-Select
End-Procedure


begin-procedure P150-Print-Cover-Page
#debug9 Show 'P150-Print-Cover-Page'

  Print 'RUN CONTROL INFORMATION FOR THIS REPORT RUN:'        (+5,1)
  Print '$Prcs_OPRID          = '                             (+2,5)
  Print $Prcs_OPRID                                           (0,+2)
  Print '$PRCS_RUN_CNTL_ID    = '                             (+1,5)
  Print $PRCS_RUN_CNTL_ID                                     (0,+2)

  Print 'SELECTION CRITERIA FOR THIS REPORT RUN:'             (+5,2)
  Print '$GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA = '  (+2,5)
  Print $GEXXX912_INCLUDE_BENEFIT_PLAN_DISPLAY_CRITERIA       (0,+2)
  Print '$GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA    = '  (+2,5)
  Print $GEXXX927_INCLUDE_VENDOR_ID_DISPLAY_CRITERIA          (0,+2)

  Print 'Report will contain balances for the month of'       (+2,5)
  Print $Reporting_Period                                     (0,+1)
  Print 'Last confirmed pay end date:'                        (+2,5)
  Print $pay_end_dt                                           (0,+2)

  Print 'SORT ORDER FOR THIS REPORT RUN:'                     (+5,2)
  Print $X000_ORDER_BY                                        (+2,5)

  Let #PAGE-COUNT = 0
End-Procedure


Begin-Procedure P160-Open-File
#debug9 Show 'P160-Open-File'

  ! Let $File1 = '{OUTFILE}' || Lower($ReportID) || '.dat'  !VENDKXY Done the changes for ITG 69940
  
   Let $File1 = '{OUTFILE}' || 'gexpl601.dat'  !VENDKXY Done the changes for ITG 69940

  Open $File1
    As 1
    For-Writing
    !Record=243:Fixed	!GEX-TLL 05/21/2008 Commented
    Record=270:Fixed    !GEX-TLL 05/21/2008 Added
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Opening output file.  Program terminating.'
    Stop
  End-If
End-Procedure

!  20160822 - GEXBN_848_E0041390_01 - Fetch rate - Begin
begin-procedure GEX-FetchRate
#debug9 Show 'GEX-FetchRate'

    Let $BENEFIT_PROGRAM_1 = ''
    Let $BENEFIT_PLAN_1 = ''
    Let $COVRG_CD_1 = ''

Begin-select
BPP.BENEFIT_PROGRAM
HB.BENEFIT_PLAN
HB.COVRG_CD

    Let $BENEFIT_PROGRAM_1 = &BPP.BENEFIT_PROGRAM
    Let $BENEFIT_PLAN_1 = &HB.BENEFIT_PLAN
    Let $COVRG_CD_1 = &HB.COVRG_CD

FROM PS_BEN_PROG_PARTIC BPP ,PS_HEALTH_BENEFIT HB
WHERE BPP.EMPLID = &j.emplid
AND BPP.EMPL_RCD = &j.empl_rcd
AND BPP.EMPLID = HB.EMPLID 
AND BPP.EMPL_RCD = HB.EMPL_RCD
AND HB.PLAN_TYPE = &bpt.plan_type
and bpp.BENEFIT_PROGRAM = &bdo.benefit_program
AND BPP.EFFDT = (SELECT MAX(BPP_ED.EFFDT) FROM PS_BEN_PROG_PARTIC BPP_ED
                WHERE BPP_ED.EMPLID = BPP.EMPLID
                AND BPP_ED.EMPL_RCD = BPP.EMPL_RCD
                AND BPP_ED.COBRA_EVENT_ID = BPP.COBRA_EVENT_ID
				AND BPP_ED.BENEFIT_PROGRAM = bpp.BENEFIT_PROGRAM
                AND BPP_ED.EFFDT <= $pay_end_dt)
AND HB.EFFDT = (SELECT MAX(HB_ED.EFFDT) FROM PS_HEALTH_BENEFIT HB_ED
                WHERE HB_ED.EMPLID = HB.EMPLID
                AND HB_ED.EMPL_RCD = HB.EMPL_RCD
                AND HB_ED.COBRA_EVENT_ID = HB.COBRA_EVENT_ID
                AND HB_ED.PLAN_TYPE = HB.PLAN_TYPE
                AND HB_ED.BENEFIT_NBR = HB.BENEFIT_NBR
				and HB_ED.coverage_elect = 'E' !Added by Ujwal on 11-Mar-2017				
                !AND HB_ED.EFFDT <= BPP.EFFDT)
				AND HB_ED.EFFDT < (select nvl(min(effdt),$pay_end_dt) from PS_BEN_PROG_PARTIC bpp_1
									where bpp_1.emplid = bpp.emplid 
									AND bpp_1.EMPL_RCD = bpp.EMPL_RCD
									and bpp_1.effdt > BPP.EFFDT ))
End-select



    show '$BENEFIT_PROGRAM_1: ' $BENEFIT_PROGRAM_1
    show '$BENEFIT_PLAN_1: '$BENEFIT_PLAN_1
    show '$COVRG_CD_1: '$COVRG_CD_1

	Let $RATE_TBL_ID = ''

Begin-select
BDC.RATE_TBL_ID

	let $RATE_TBL_ID = &BDC.RATE_TBL_ID

FROM PS_BEN_DEFN_OPTN BDO, PS_BEN_DEFN_COST BDC
WHERE BDO.BENEFIT_PROGRAM = $BENEFIT_PROGRAM_1
AND BDO.PLAN_TYPE = &bpt.plan_type
AND BDO.BENEFIT_PLAN = $BENEFIT_PLAN_1
AND BDO.COVRG_CD = $COVRG_CD_1
and BDC.BENEFIT_PROGRAM = BDO.BENEFIT_PROGRAM
AND BDC.PLAN_TYPE = BDO.PLAN_TYPE
and BDC.option_id = BDO.option_id
AND BDO.EFFDT = (SELECT MAX(EFFDT) FROM PS_BEN_DEFN_OPTN BDO_ED
                WHERE BDO_ED.BENEFIT_PROGRAM = BDO.BENEFIT_PROGRAM
                AND BDO_ED.EFFDT <= $pay_end_dt)
AND BDC.EFFDT = (SELECT MAX(EFFDT) FROM PS_BEN_DEFN_COST BDC_ED
                WHERE BDC_ED.BENEFIT_PROGRAM = BDC.BENEFIT_PROGRAM
                AND BDC_ED.EFFDT <= $pay_end_dt)
End-select

show '$RATE_TBL_ID: '$RATE_TBL_ID

	Let #GXS_Rate = 0
	Let $RateType = ''

BEGIN-SELECT
BRT.RATE_TYPE

	move &BRT.RATE_TYPE to $RateType

FROM PS_BN_RATE_TBL BRT
WHERE RATE_TBL_ID = $RATE_TBL_ID
AND BRT.EFFDT = (SELECT MAX(BRT_ED.EFFDT) FROM PS_BN_RATE_TBL BRT_ED
              WHERE BRT.RATE_TBL_ID = BRT_ED.RATE_TBL_ID
              AND BRT_ED.EFFDT <= $pay_end_dt)
AND EFF_STATUS = 'A'
END-SELECT

show '$RateType: '$RateType

if $RateType = '4'
BEGIN-SELECT
BRD.BN_EMPLR_RATE &EmployerRate1
	
	MOVE &EmployerRate1 to #GXS_Rate
	
FROM PS_BN_RATE_DATA  BRD
WHERE  BRD.RATE_TBL_ID     = $RATE_TBL_ID
AND    BRD.EFFDT            = (SELECT MAX(BRD_ED.EFFDT)
       FROM PS_BN_RATE_DATA	BRD_ED
       WHERE BRD_ED.RATE_TBL_ID = BRD.RATE_TBL_ID			
       AND    BRD_ED.EFFDT        <= $pay_end_dt)
AND to_number(BRD.BN_RATE_KEY01) = (select max(to_number(BRD1.BN_RATE_KEY01)) from PS_BN_RATE_DATA  BRD1
      where BRD1.RATE_TBL_ID = BRD.RATE_TBL_ID
      and BRD1.effdt = BRD.effdt
      AND to_number(BRD1.BN_RATE_KEY01) <= #MontsOfService)
END-SELECT

show '#GXS_Rate: '#GXS_Rate

else

if $RateType = '2'
BEGIN-SELECT
BRD.BN_EMPLR_RATE

	MOVE &BRD.BN_EMPLR_RATE to #GXS_Rate

FROM PS_BN_RATE_DATA  BRD
WHERE  BRD.RATE_TBL_ID     = $RATE_TBL_ID
AND    BRD.EFFDT            =
      (SELECT MAX(BRD_ED.EFFDT)
       FROM PS_BN_RATE_DATA	BRD_ED
       WHERE BRD_ED.RATE_TBL_ID = BRD.RATE_TBL_ID			
       AND    BRD_ED.EFFDT        <= $pay_end_dt)
END-SELECT

show '#GXS_Rate: '#GXS_Rate

end-if
end-if

if (#GXS_Rate = 0 and $RateType <> '4')

Begin-select
GFL.GEX_RATE_AMT

	move &GFL.GEX_RATE_AMT to #GXS_Rate

FROM PS_GEX_FLAT_RATE GFL
WHERE GFL.RATE_TBL_ID = $RATE_TBL_ID
AND GFL.EFFDT = (SELECT MAX(GFL_ED.EFFDT) FROM PS_GEX_FLAT_RATE GFL_ED
                  WHERE GFL.RATE_TBL_ID = GFL_ED.RATE_TBL_ID 
                  AND GFL_ED.EFFDT <= $pay_end_dt)
End-select
show '#GXS_Rate: '#GXS_Rate
End-if

end-procedure GEX-FetchRate
!  20160822 - GEXBN_848_E0041390_01 - Fetch rate - End

Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
 
  Let $Data-Found-Flag = 'N'

Begin-Select
!bpt.provider         ()     On-Break   Level=1
bpt.vendor_id         ()     On-Break   Level=1 !PSR 08/12/02 Change in Column Name
                                       Print=Never
                                       Save=$Save_Vendor_ID
                                       After=P290-After-Vendor-ID-Break

bdo.benefit_program  ()     On-Break   Level=2
                                       Print=Never
                                       Save=$Save_Benefit_Program
                                       Before=P280-Before-BP-Break
                                       After=P285-After-BP-Break


bpt.benefit_plan
bpt.plan_type
! CJH - 07/25/2001 - begin
!J.UNION_CD
! CJH - 07/25/2001 - end
e.service_dt

!  20160822 - GEXBN_848_E0041390_01 - Get months of service - Begin
MONTHS_BETWEEN($pay_end_dt,e.service_dt) &MontsOfService
!  20160822 - GEXBN_848_E0041390_01 - Get months of service - End

per.name
per.birthdate
per.emplid !SXK 11/16/1999 Changed ssn to emplid
per.sex
per.address1
per.address2
per.city
per.state
per.postal !SXK 11/16/1999 Changed from zip to postal

j.company
j.paygroup
j.emplid
j.empl_rcd
j.effdt
j.deptid
j.jobcode
j.empl_status
j.reg_temp
j.full_part_time
j.gex_volun_low_hrs

Sum(pd.ded_cur) &pd.ded_cur

  Let $Data-Found-Flag = 'Y'
  
  !  20160822 - GEXBN_848_E0041390_01 - Get months of service - Start
	move &MontsOfService to #MontsOfService
  !  20160822 - GEXBN_848_E0041390_01 - Get months of service - End
  

  Let $Provider = &BPT.VENDOR_ID
  Let $Vendor_ID = &BPT.VENDOR_ID
  Let $Benefit_Program = &bdo.benefit_program
  Let $Deptid = &j.deptid   !VENDKXY Done the changes for ITG 69940 

  Let #contribution-amount = &pd.ded_cur

  Do P220-Get-Benefit-Termination-Row
  
  

  Evaluate &j.empl_status
    When = 'L'
    When = 'P'
    When = 'R'
    When = 'T'
      Do P230-Get-Leave-Begin-Date
      Break
    When-Other
      Do P235-Get-Status-Date
  End-Evaluate

  Let #hrs_ytd = 0
  Let #hrs_mtd = 0
  Let #MTD-Hours          = 0
  Let #MTD-Vacation-Hours = 0
  Let #MTD-Total-Hours    = 0
  Let #YTD-Total-Hours    = 0

  Let $Begin-Date = $current_month_begin
  Let $End-Date   = $current_month_end
  Do P240-Gex-Hrs-Bal


  If #total-hours = 0
    Let $spcl_balance = '''Y'''
    If &bdo.benefit_program = 'G01' Or
       &bdo.benefit_program = 'G02'
      Let $erncd = '(''PD1'')'
      Do P240-Earnings-Bal
      Let #MTD-Total-Hours = #hrs_mtd
      Let #YTD-Total-Hours = #hrs_ytd
      Let $spcl_balance = '''N'''
      Let $erncd = '(''513'',''515'')'
      Do P240-Earnings-Bal
      Let #MTD-Vacation-Hours = #hrs_mtd
    Else
      If &bdo.benefit_program = 'G03' Or
         &bdo.benefit_program = 'G04'
        Let $erncd = '(''PD2'')'
        Do P240-Earnings-Bal
        Let #MTD-Total-Hours = #hrs_mtd
        Let #YTD-Total-Hours = #hrs_ytd
        Let $spcl_balance = '''N'''
        Let $erncd = '(''513'',''515'')'
        Do P240-Earnings-Bal
        Let #MTD-Vacation-Hours = #hrs_mtd
      End-If
    End-If
  Else
    Let #MTD-Total-Hours = #total-hours
    Let #MTD-Vacation-Hours = #vacation-hours
    Let $Begin-Date = $current_year_begin
    Let $End-Date   = $current_month_end
    Do P240-Gex-Hrs-Bal
    Let #YTD-Total-Hours = #total-hours
  End-If
!  JNB 06/10/99 Begin 

  If $Benefit_Program = 'C03'
     Let #MTD-Total-Hours = 166.67
  End-If            
! JNB 06/10/99 End
  Let #MTD-Hours = #MTD-Total-Hours - #MTD-Vacation-Hours

!  20160822 - GEXBN_848_E0041390_01 - Fetch rate - Begin
	Do GEX-FetchRate
	show 'Do GEX-FetchRate'
!  20160822 - GEXBN_848_E0041390_01 - Fetch rate - End
  
  Do P250-Format-Record
  Do P260-Print-Record
  
  ! =================================================== 
  ! GEXBN_848_E130211_01 2012-07-09 Vahini Katta BEGIN  
  ! =================================================== 
  !If &BPT.VENDOR_ID = '69009'
    Do P270-Write-Record
  !End-If
  ! ===================================================
  ! GEXBN_848_E130211_01 2012-07-09 Vahini Katta ENDS
  ! ===================================================

  Add #MTD-Hours           To #BP-MTD-Hours
  Add #MTD-Vacation-Hours  To #BP-MTD-Vacation-Hours
  Add #MTD-Total-Hours     To #BP-MTD-Total-Hours
  Add #Contribution-Amount To #BP-Contribution-Amount
  Add #YTD-Total-Hours     To #BP-YTD-Total-Hours

From   ps_personal_data          per,
       ps_employment             e,
       ps_job                    j,
       ps_benef_plan_tbl         bpt,
       ps_ben_defn_optn          bdo,
       ps_pay_cal_bal_id         pcbi,
       ps_pay_check              pc,
       ps_pay_deduction          pd

Where  e.emplid                  = j.emplid
And    e.empl_rcd               = j.empl_rcd
And    per.emplid                = j.emplid
And    j.effdt                   =
      (Select Max(effdt)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt             <= $pay_end_dt)

And    j.effseq                  =
      (Select Max(effseq)
       From   ps_job
       Where  emplid             = j.emplid
       And    empl_rcd          = j.empl_rcd
       And    effdt              = j.effdt)

And    bpt.effdt                 =
      (Select Max(effdt)
       From   ps_benef_plan_tbl
       Where  plan_type          = bpt.plan_type
       And    benefit_plan       = bpt.benefit_plan
       And    effdt             <= $pay_end_dt)

And    bdo.plan_type             = bpt.plan_type
And    bdo.benefit_plan          = bpt.benefit_plan

And    bdo.effdt                 =
      (Select Max(effdt)
       From   ps_ben_defn_optn
       Where  benefit_program    = bdo.benefit_program
!GBD 10/16/2002 Begin
       And    effdt             <= $pay_end_dt)
!       And    effdt             <= $pay_end_dt
!       And    plan_type          = bdo.plan_type
!       And    option_id          = bdo.option_id)
!GBD 10/16/2002 End

And    pcbi.company              = pc.company
And    pcbi.paygroup             = pc.paygroup
And    pcbi.balance_id           = 'CY'
And    pcbi.balance_year         = #balance_year
And    pcbi.balance_period       = #balance_period

And    pc.pay_end_dt             = pcbi.pay_end_dt

And    pc.emplid                 = j.emplid
And    pc.empl_rcd              = j.empl_rcd

And    pd.company                = pc.company
And    pd.paygroup               = pc.paygroup
And    pd.pay_end_dt             = pc.pay_end_dt
And    pd.off_cycle              = pc.off_cycle
And    pd.page_num                  = pc.page_num
And    pd.line_num                  = pc.line_num
And    pd.sepchk                 = pc.sepchk

And    pd.plan_type              = bpt.plan_type
And    pd.benefit_plan           = bpt.benefit_plan
And    pd.ded_class              = 'N'

And   [$GEXXX912_INCLUDE_BENEFIT_PLAN_CRITERIA]
And   [$GEXXX927_INCLUDE_VENDOR_ID_CRITERIA]

Group By BPT.VENDOR_ID,
       bdo.benefit_program,
       bpt.benefit_plan,
       bpt.plan_type,
! CJH - 07/25/2001 - begin
!       J.UNION_CD, 
! CJH - 07/25/2001 - end
       e.service_dt,
       per.name,
       per.birthdate,
       per.emplid,
       per.sex,
       per.address1,
       per.address2,
       per.city,
       per.state,
       per.postal,
       j.company,
       j.paygroup,
       j.emplid,
       j.empl_rcd,
       j.effdt,
       j.deptid,
       j.jobcode,
       j.empl_status,
       j.reg_temp,
       j.full_part_time,
       j.gex_volun_low_hrs

Order By BPT.VENDOR_ID,
       bdo.benefit_program,
       per.name

End-Select
End-Procedure


Begin-Procedure P210-Get-Benefit-Program-Name
#debug9 Show 'P210-Get-Benefit-Program-Name'

Begin-Select
bdp.descr

  Let $BenefitProgramDescr = &bdp.descr

From   ps_ben_defn_pgm   bdp

Where  bdp.benefit_program    = &bdo.benefit_program

And    bdp.effdt              =
      (Select Max(effdt)
       From   ps_ben_defn_pgm
       Where  benefit_program = bdp.benefit_program
       And    effdt          <= $pay_end_dt)
End-Select
End-Procedure


Begin-Procedure P220-Get-Benefit-Termination-Row
#debug9 Show 'P220-Get-Benefit-Termination-Row'
Begin-Select
hb.coverage_begin_dt

From   ps_health_benefit hb

Where  hb.emplid         = &j.emplid
And    hb.empl_rcd      = &j.empl_rcd
And    hb.plan_type      = &bpt.plan_type

And    hb.effdt          =
      (Select Max(effdt)
       From   ps_health_benefit
       Where  emplid     = &j.emplid
       And    empl_rcd  = &j.empl_rcd
       And    plan_type  = &bpt.plan_type)

And    hb.coverage_begin_dt Between $prev_month_begin
                                And $next_month_end

And    hb.coverage_elect = 'T'
End-Select
End-Procedure


Begin-Procedure P230-Get-Leave-Begin-Date
#debug9 Show 'P230-Get-Leave-Begin-Date'
  Let $Status-Change = 'N'
Begin-Select
JOB.EFFDT
JOB.EMPL_STATUS
JOB.ACTION
JOB.ACTION_REASON

  If &JOB.EMPL_STATUS = &J.EMPL_STATUS And
      $Status-Change = 'N'
    Let $Effective-Date = &JOB.EFFDT
    Let $Action         = &JOB.ACTION
    Let $Action_Reason  = &JOB.ACTION_REASON
    Let $Status-Change = 'N'
  Else
    Let $Status-Change = 'Y'
  End-If

FROM   PS_JOB JOB

WHERE  EMPLID     = &J.EMPLID
AND    EMPL_RCD  = &J.EMPL_RCD
AND    EFFDT     <= &J.EFFDT
ORDER BY JOB.EFFDT DESC, JOB.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P235-Get-Status-Date
#debug9 Show 'P235-Get-Status-Date'
  Let $Reg-Temp-Change = 'N'
Begin-Select
J1.EFFDT
J1.REG_TEMP
J1.ACTION
J1.ACTION_REASON
J1.EMPL_STATUS

  If &J1.REG_TEMP = &J.REG_TEMP And
      $Reg-Temp-Change = 'N'
    Let $Reg-Temp-Change = 'N'
    If &J1.EMPL_STATUS <> 'T'
      Let $Effective-Date = &J1.EFFDT
      Let $Action         = &J1.ACTION
      Let $Action_Reason  = &J1.ACTION_REASON
    End-If
  Else
    Let $Reg-Temp-Change = 'Y'
  End-If

FROM   PS_JOB J1

WHERE  EMPLID     = &J.EMPLID
AND    EMPL_RCD  = &J.EMPL_RCD
AND    EFFDT     <= &J.EFFDT
ORDER BY J1.EFFDT DESC, J1.EFFSEQ DESC
End-Select
End-Procedure


Begin-Procedure P240-Gex-Hrs-Bal
#debug9 Show 'P240-Gex-Hrs-Bal'

  Let #total-hours = 0
  Let #vacation-hours = 0

Begin-Select
Sum(ghb.total_hrs) &ghb.total_hrs
Sum(ghb.vacn_hours) &ghb.vacn_hours

  Let #total-hours = &ghb.total_hrs
  Let #vacation-hours = &ghb.vacn_hours

From   ps_gex_hrs_bal ghb

!Where ghb.company            = &j.company
!And   ghb.paygroup           = &j.paygroup
!And   ghb.emplid             = &j.emplid
Where  ghb.emplid             = &j.emplid
And    ghb.empl_rcd          = &j.empl_rcd
And    ghb.plan_type          = &bpt.plan_type
And    ghb.benefit_plan       = &bpt.benefit_plan
And    ghb.check_dt     Between $Begin-Date
                            And $End-Date
End-Select
End-Procedure


Begin-Procedure P240-Earnings-Bal
#debug9 Show 'P240-Earnings-Bal'

  Let #hrs_ytd = 0
  Let #hrs_mtd = 0

Begin-Select
eb.balance_period
eb.hrs_mtd
eb.hrs_ytd

  Add &eb.hrs_ytd To #hrs_ytd

  If &eb.balance_period = #balance_period
    Add &eb.hrs_mtd To #hrs_mtd
  End-If

From   ps_earnings_bal eb

Where  eb.emplid              = &j.emplid
And    eb.company             = &j.company
And    eb.balance_id          = 'CY'
And    eb.balance_year        = #balance_year
And    eb.empl_rcd            = &j.empl_rcd
And    eb.balance_period      =
      (Select Max(balance_period)
       From   ps_earnings_bal
       Where  emplid          = eb.emplid
       And    company         = eb.company
       And    balance_id      = eb.balance_id
       And    balance_year    = eb.balance_year
       And    balance_period <= #balance_period
       And    spcl_balance    = eb.spcl_balance
       And    empl_rcd        = eb.empl_rcd
       And    erncd           = eb.erncd)

And    eb.spcl_balance        = [$spcl_balance]
And    eb.erncd              In [$erncd]
End-Select
End-Procedure


Begin-Procedure P250-Format-Record
#debug9 Show 'P250-Format-Record'

  If &j.gex_volun_low_hrs = 'Y'
    Let $gex_volun_low_hrs = 'Y'
  Else
    Let $gex_volun_low_hrs = ''
  End-If

  Do Format-DateTime(&e.service_dt, $Service_Dt, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime(&e.service_dt, $ServiceDt, {DEFYMD}, '', '')

  Do Format-DateTime(&per.birthdate, $Birthdate, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime(&per.birthdate, $Birth_Date, {DEFYMD}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime($Effective-Date, $effdt, {DEFYMD}, '', '')

  Do Format-DateTime($Effective-Date, $eff_dt, {DEFMDY}, '', '')

  Do Format-DateTime(&hb.coverage_begin_dt, $Term_Date, {DEFMDY}, '', '')

  Let $Year4 = '1'
  Do Format-DateTime(&hb.coverage_begin_dt, $TermDate, {DEFYMD}, '', '')

  Move #contribution-amount To #Pension_contribution_amount
  Move #MTD-Hours           To #Pension_MTD_Hours
  Move #MTD-Vacation-Hours  To #Pension_MTD_Vacation_Hours
  Move #MTD-Total-Hours     To #Pension_MTD_Total_Hours
  Move #YTD-Total-Hours     To #Pension_YTD_Total_Hours

  Move #Pension_contribution_amount To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 099999999
  Let $Pension_contribution_amount = $string_int_ed || Substr($string_after_Dec,1,2)

  Move #Pension_MTD_Hours To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 09999
  Let $Pension_MTD_Hours = $string_int_ed || Substr($string_after_Dec,1,2)

  Move #Pension_MTD_Vacation_Hours To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 09999
  Let $Pension_MTD_Vacation_Hours = $string_int_ed || Substr($string_after_Dec,1,2)

  Move #Pension_MTD_Total_Hours To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 09999
  Let $Pension_MTD_Total_Hours = $string_int_ed || Substr($string_after_Dec,1,2)

  Move #Pension_YTD_Total_Hours To $string
  Unstring $string By '.' Into $string_int $string_after_Dec
  Move $string_int To $string_int_ed 09999
  Let $Pension_YTD_Total_Hours = $string_int_ed || Substr($string_after_Dec,1,2)

  If Substr($Pension_contribution_amount,1,1) != '-'
    Let $Pension_contribution_amount = '+' || Substr($Pension_contribution_amount,2,10)
  End-If

  If Substr($Pension_MTD_Hours,1,1) != '-'
    Let $Pension_MTD_Hours = '+' || Substr($Pension_MTD_Hours,2,6)
  End-If

  If Substr($Pension_MTD_Vacation_Hours,1,1) != '-'
    Let $Pension_MTD_Vacation_Hours = '+' || Substr($Pension_MTD_Vacation_Hours,2,6)
  End-If

  If Substr($Pension_MTD_Total_Hours,1,1) != '-'
    Let $Pension_MTD_Total_Hours = '+' || Substr($Pension_MTD_Total_Hours,2,6)
  End-If

  If Substr($Pension_YTD_Total_Hours,1,1) != '-'
    Let $Pension_YTD_Total_Hours = '+' || Substr($Pension_YTD_Total_Hours,2,6)
  End-If
End-Procedure


Begin-Procedure P260-Print-Record
#debug9 Show 'P260-Print-Record'
!GEX-TLL 05/21/2008 HCM 9.0 Upgrade Added Begin
  !Added by Ujwal Dyasani - 5/4/2017 - Begin
  let $emplid_out  =  &per.emplid      
  !Added by Ujwal Dyasani - 5/4/2017 - End
  do Get-Gex-Mapp-EmpIid (&per.emplid,$emplid_out)            
  do Get-Emp-Ssn (&per.emplid,$Ssn_Out) 
!GEX-TLL 05/21/2008 HCM 9.0 Upgrade Added End
  Print &per.name                  (+1,{#column1},30)	
  !Print &per.emplid                (+0,{#column2})	!GEX-TLL 05/21/2008 
  Print $Ssn_Out                   (+0,{#column2})      !GEX-TLL 05/21/2008 
  Print $emplid_out                (+0,{#column2_1}) 	!GEX-TLL 05/21/2008 
  Print $Service_Dt                (+0,{#column3})
  Print $Birthdate                 (+0,{#column4})
  Print &j.empl_status             (+0,{#column5})
  Print $Action                    (+0,{#column6})
  Print $Action_Reason             (+0,{#column7})
  Print $eff_dt                    (+0,{#column8})
  Print $Term_Date                 (+0,{#column9})
  !Print #MTD-Hours                 (+0,{#column10})  Edit 999,999,999.99	!GEX-TLL 05/21/2008 
  !Print #MTD-Vacation-Hours        (+0,{#column11})  Edit 999,999,999.99	!GEX-TLL 05/21/2008 
  Print #MTD-Hours                 (+0,{#column10})  Edit 99,999,999.99  !GEX-TLL 05/21/2008 
  Print #MTD-Vacation-Hours        (+0,{#column11})  Edit 99,999,999.99  !GEX-TLL 05/21/2008 
  Print #MTD-Total-Hours           (+0,{#column12})  Edit 999,999,999.99
  Print #contribution-amount       (+0,{#column13})  Edit 999,999,999.99
  Print #YTD-Total-Hours           (+0,{#column14})  Edit 999,999,999.99
  
     !VENDKXY Done the changes for ITG 69940-- Begin 
            
!  20160822 - GEXBN_848_E0041390_01 - Commented - Commented - Begin
   !STRING $Vendor_ID $Benefit_Program $Deptid &per.name $Ssn_Out $emplid_out &e.service_dt &per.birthdate &j.empl_status $Action  $Action_Reason  $Effective-Date &hb.coverage_begin_dt #MTD-Hours #MTD-Vacation-Hours  #MTD-Total-Hours #contribution-amount #YTD-Total-Hours BY $DELIM INTO $GEXPL601
!  20160822 - GEXBN_848_E0041390_01 - Commented - Commented - End
!  20160822 - GEXBN_848_E0041390_01 - Commented - Append Rate Column - Begin
	STRING $Vendor_ID $Benefit_Program $Deptid &per.name $Ssn_Out $emplid_out &e.service_dt &per.birthdate &j.empl_status $Action  $Action_Reason  $Effective-Date &hb.coverage_begin_dt #MTD-Hours #MTD-Vacation-Hours  #MTD-Total-Hours #GXS_Rate #contribution-amount #YTD-Total-Hours BY $DELIM INTO $GEXPL601
!  20160822 - GEXBN_848_E0041390_01 - Commented - Append Rate Column - End

   WRITE 2 FROM $GEXPL601

  !Vendkxy done the changes for ITG 69940-- End
  
End-Procedure


Begin-Procedure P270-Write-Record
#debug9 Show 'P270-Write-Record'

  Add 1 To #recs-written
!GEX-TLL 05/21/2008 HCM 9.0 Upgrade Added Begin
!GEX_SEC_REENG 2015-04-29 Vahini Katta Begins
  !do Get-Gex-Mapp-EmpIid (&per.emplid,$emplid_out)  
  !Commented by Ujwal Dyasani - 5/4/2017 - Begin
  !let $emplid_out  =  &per.emplid      
  !Commented by Ujwal Dyasani - 5/4/2017 - End
  do Get-Emp-Ssn (&per.emplid,$Ssn_Out) 
!GEX_SEC_REENG 2015-04-29 Vahini Katta Ends
!GEX-TLL 05/21/2008 HCM 9.0 Upgrade Added End 
  Write 1 From $Benefit_Program:3
             !&per.emplid:9	!GEX-TLL 05/21/2008 
             $Ssn_Out:9		!GEX-TLL 05/21/2008 
             &per.name:30
             &per.sex:1
             $gex_volun_low_hrs:1
             &j.full_part_time:1  
             &j.reg_temp:1
             $action:3
             $action_reason:3
             $Birth_Date:10
             $ServiceDt:10
             $Effdt:10
             $TermDate:10
             &per.address1:35
             &per.address2:35
             &per.city:30
             &per.state:2
             &per.postal:10
             $Pension_MTD_Hours:7
             $Pension_MTD_Vacation_Hours:7
             $Pension_MTD_Total_Hours:7
             $Pension_YTD_Total_Hours:7
             $Pension_contribution_amount:11
             $emplid_out:7		!GEX-TLL 05/21/2008 
    Status=#Filestat

  If #Filestat != 0
    Display 'Error Writing output file.  Program terminating.'
    Stop
  End-If
End-Procedure


Begin-Procedure P280-Before-BP-Break
#debug9 Show 'P280-Before-BP-Break'
  New-Page
  Do P210-Get-Benefit-Program-Name
End-Procedure


Begin-Procedure P285-After-BP-Break
#debug9 Show 'P285-After-BP-Break'

  Print 'Benefit Program'          (+3,{#column2})
  Print $Save_Benefit_Program      (0,+1)
  Print 'Totals'                   (0,+1)

  Print #BP-MTD-Hours              (+0,{#column10})   Edit 999,999,999.99
  Print #BP-MTD-Vacation-Hours     (+0,{#column11})   Edit 999,999,999.99
  Print #BP-MTD-Total-Hours        (+0,{#column12})   Edit 999,999,999.99
  Print #BP-Contribution-Amount    (+0,{#column13})   Edit 999,999,999.99
  Print #BP-YTD-Total-Hours        (+0,{#column14})   Edit 999,999,999.99

  Add #BP-MTD-Hours           To #Vendor-ID-MTD-Hours
  Add #BP-MTD-Vacation-Hours  To #Vendor-ID-MTD-Vacation-Hours
  Add #BP-MTD-Total-Hours     To #Vendor-ID-MTD-Total-Hours
  Add #BP-Contribution-Amount To #Vendor-ID-Contribution-Amount
  Add #BP-YTD-Total-Hours     To #Vendor-ID-YTD-Total-Hours

  Let #BP-MTD-Hours           = 0
  Let #BP-MTD-Vacation-Hours  = 0
  Let #BP-MTD-Total-Hours     = 0
  Let #BP-Contribution-Amount = 0
  Let #BP-YTD-Total-Hours     = 0
End-Procedure


Begin-Procedure P290-After-Vendor-ID-Break
#debug9 Show 'P290-After-Vendor-ID-Break'

!  Print 'Provider'                 (+3,{#column2})
  Print 'VendorID'                 (+3,{#column2})
  Print $Save_Vendor_ID            (0,+1)
  Print 'Totals'                   (0,+1)

  Print #Vendor-ID-MTD-Hours            (+0,{#column10})   Edit 999,999,999.99
  Print #Vendor-ID-MTD-Vacation-Hours   (+0,{#column11})   Edit 999,999,999.99
  Print #Vendor-ID-MTD-Total-Hours      (+0,{#column12})   Edit 999,999,999.99
  Print #Vendor-ID-Contribution-Amount  (+0,{#column13})   Edit 999,999,999.99
  Print #Vendor-ID-YTD-Total-Hours      (+0,{#column14})   Edit 999,999,999.99

  Let #Vendor-ID-MTD-Hours           = 0
  Let #Vendor-ID-MTD-Vacation-Hours  = 0
  Let #Vendor-ID-MTD-Total-Hours     = 0
  Let #Vendor-ID-Contribution-Amount = 0
  Let #Vendor-ID-YTD-Total-Hours     = 0
End-Procedure


Begin-Procedure P300-Finish
#debug9 Show 'P300-Finish'

  Do P310-Close-File
  Do P320-Display-File-Totals
  Do P330-Print-Report-Totals

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded
End-Procedure


Begin-Procedure P310-Close-File
#debug9 Show 'P310-Close-File'

  Close 1
End-Procedure


Begin-Procedure P320-Display-File-Totals
#debug9 Show 'P320-Display-File-Totals'

  Move #recs-written To $recs-written   888,888,888

  Display ''
  Display $recs-written          Noline
  Display ' records written to ' Noline
  Display $File1
  Display ''
End-Procedure


Begin-Procedure P330-Print-Report-Totals
#debug9 Show 'P330-Print-Report-Totals'

  If $Data-Found-Flag <> 'Y'
    New-Page
    Print 'NO DATA SELECTED FOR THIS REPORT RUN'	(25,) Center
  End-If
End-Procedure


#include 'gexxx912.sqc'  !Get plan type multiple row table
#include 'gexxx927.sqc'  !Get vendor id multiple row table
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'reset.sqc'     !Reset printer Procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'getgeid.sqc'   !Get 7 digit Emplid 	    !GEX-TLL 05/21/2008 Added
