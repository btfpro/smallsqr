!***********************************************************************
! GEXPY664.SQR:                                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-2006 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY664.SQR :                                                       *
!                                                                      *
! Narrative:            Create a program that will report check address*
!			information from a list of check numbers       *
!			supplied by the Tax Department on an Ad-Hoc    *
!			basis.  				       *
!			ITG-35691.				       *
!								       *
! #Debug Used:          x                                              *
!								       *
! SQL Tables:           PS_PAY_CHECK, PS_PERSONAL_DATA                 *
!								       *
! Written by:           Rapidigm, Deepak. S, 04/20/06                  *
!                                                                      *
! Normally Run:         Run on ad-hoc basis			       *
!                                                                      *
! Control Parms:        Reads the run control table PS_GEX_RUN_CNTL    *
!			and input file Check_Layout.txt	               *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS        DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
!GEX_PY267 SXD 08/30/2007     Sqr from 8.3 is moved to 9.0 as it is.   *                                                                *
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup02.sqc'   !Printer and Page-Size Initialization (Landscape)

#define #column1  001
#define #column2  015
#define #column3  032
#define #column4  070
#define #column5  095
#define #column6  103
#define #column7  112
#define #column8  125
#define #column9  138
#define #column10  149
#define #column11  162

!***********************************************************************
! Defines Heading of the report
!***********************************************************************
Begin-Heading 5
  let $ReportID = 'GEXPY664'
  #include 'stdhdg01.sqc'

  Print 'Employee Check Address Information for Tax Department'     (3,1) center

  Print 'LAST NAME'	(+3,{#column1}) underline
  Print 'FIRST NAME'	(0,{#column2})	underline
  Print 'ADDRESS1'	(0,{#column3})	underline
  Print 'CITY'		(0,{#column4})	underline
  Print 'STATE'		(0,{#column5})	underline
  Print 'POSTAL'	(0,{#column6})	underline	
  Print 'EMPLID'	(0,{#column7})	underline
  Print 'CHECK NBR'	(0,{#column8})  underline
  Print 'NET PAY'	(0,{#column9})	underline
  Print 'CHECK DATE'	(0,{#column10})	underline
  Print 'PAYCHECK STATUS' (0,{#column11}) underline

End-Heading


!***********************************************************************
! Start of Program
!***********************************************************************
Begin-Report

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Stdapi-Init             !stdapi.sqc
  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = '1'
  end-if

  do Get-Run-Control	     !gexrnctl.sqc  
 
  let #GEX_PAYCHKNBR_ROWS = 1
  let $ChkNbr_Alias = 'A.PAYCHECK_NBR'
  do Open-Input-File
  do Read-Input-File
  do Select-Data
  do stdapi-term

End-Report


!***********************************************************************
! Opens the Input file which has the PAY CHECK NUMBERs
!***********************************************************************
Begin-Procedure Open-Input-File

	! 'F:\GE_Work\HRMS\Checkno_layout.txt'
	move &filepath to $filepath
	open $filepath as 1  for-reading record=11 status=#filestat
	if #filestat != 0
		show 'Error Opening File: ' $filepath
		stop
	end-if

End-Procedure


!***********************************************************************
! Reads the PAY CHECK NUMBERs from the input file.
!***********************************************************************
Begin-Procedure Read-Input-File

   while 1
	read 1 into $chknum:10
	
	if #end-file
		break
	end-if

	let $chknum = rtrim($chknum,' ')

        if $chknum != ''
		If #GEX_PAYCHKNBR_ROWS = 1
			Let $CHKNUM_CRITERIA = '(' || $ChkNbr_Alias || ' In ('''     || $chknum || ''''
			let #GEX_PAYCHKNBR_ROWS = #GEX_PAYCHKNBR_ROWS + 1
		Else
			Let $CHKNUM_CRITERIA = $CHKNUM_CRITERIA || ',''' || $chknum  || ''''
		End-If
	end-if
   end-while
	
	Let $CHKNUM_CRITERIA = $CHKNUM_CRITERIA || '))'
   #debugx show $CHKNUM_CRITERIA 
End-Procedure 

!***********************************************************************
! Main Procedure which selects the data.
!***********************************************************************
Begin-Procedure Select-Data
Begin-Select 
B.LAST_NAME		(+2,{#column1})
B.FIRST_NAME		(0,{#column2})
B.ADDRESS1		(0,{#column3})
B.CITY			(0,{#column4})
B.STATE			(0,{#column5})	
B.POSTAL		(0,{#column6})	
B.EMPLID		(0,{#column7})
A.PAYCHECK_NBR		(0,{#column8})
A.NET_PAY		(0,{#column9}) edit 99999.99
A.CHECK_DT		(0,{#column10})
A.PAYCHECK_STATUS	(0,{#column11})

FROM PS_PAY_CHECK A, PS_PERSONAL_DATA B
 WHERE B.EMPLID = A.EMPLID
   AND A.NET_PAY <> 0 
  ! AND A.PAYCHECK_NBR IN ( LIST OF CHECK NUMBERS )
  AND [$CHKNUM_CRITERIA]

End-Select
End-Procedure


#include 'stdapi.sqc'    !Process Scheduler Interface
#include 'reset.sqc'     !Reset Printer Procedure
#include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date math
#include 'gexrnctl.sqc'  !Routine for Get-Run-Control
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures