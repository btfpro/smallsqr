!***********************************************************************
! GEXPY098:  Bonus Processing SQL                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-1998 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXPY098 :            Bonus Processing SQL                           *
!                                                                      *
! Narrative:            This SQR is designed to update paysheets and   *
!                        the pay calendar for bonus processing.        *
!                         (This process must be executed after the     *
!                          on-cycle paysheets are created for the      *
!                          bonus company/paygroup's.  Those paysheets  *
!                          must be created prior to any adjustments    *
!                          being processed.)                           *
!                                                                      *
! #Debugx Used:         #debug9 paragraph trace                        *
!                       #debug8 key variable values                    *
!                       #debug7 data returned from sql calls           *
!                                                                      *
! SQL Tables:           pay_calendar                                   *
!                       pay_page                                       *
!                       pay_line                                       *
!                       pay_earnings                                   *
!                       pay_oth_earns                                  *
!                                                                      *
! Written by:           Jim Hutchison                                  *
!                                                                      *
! Normally Run:         Annually                                       *
!                                                                      *
! Control Parms:        FOR WHERE CLAUSE:                              *
!                         Bonus Pay End Date                           *
!                         Paycheck Issue Date                          *
!                                                                      *
!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE       DESCRIPTION OF THE CHANGE                       *
!***********************************************************************
! JDH       07/14/99   Initial Creation                                *
!                                                                      *
! JDH       10/01/99   Set OK_TO_PAY = 'N' on PAY_EARNINGS for all     *
!                      non-bonus rows instead of deleting the rows.    *
!                      Added paycheck issue date run control parameter *
!                      in order to update check_dt on pay_line.        *
!                                                                      *
! SXK       06/20/00   Modified the initial select statement where     *
!                      clause to oth_pay > 0                           *
!                                                                      *
! CJH       06/22/00   Modified final update to set all ok_to_pay = 'N'*
!                      paysheets to off_cycle = 'Y' so that the        *
!                      off-cycle confirm will delete the sheets.       *
!                                                                      *
! AXL       10/30/01   Added JOB_PAY = 'N' to Update PS_PAY_OTH_EARNS  *
!                      procedure.                                      *
!                                                                      *
! AXL       08/28/2002 New sqc to populate Pay End Date and Check Date *
!                      according to Bonus processing changes.          *
!                      Added new joins to PAGE# in each UPDATE to      *
!                      improve efficiency.                             *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)


Begin-Report
#debug9 Show 'Begin-Report'
  Do P100-Initialization
  Do P200-Main-Process
  Do P300-Finalization
End-Report


Begin-Procedure P100-Initialization
#debug9 Show 'P100-Initialization'
  Do Init-DateTime           !datetime.sqc
  Do Init-Number             !number.sqc
  Do Get-Current-DateTime    !curdttim.sqc
  Do Stdapi-Init             !stdapi.sqc

  Let $ReportID      =   'GEXPY098'
  Let $ReportTitle   =   'Bonus Processing SQL'

  Show '$ReportID    = ' $ReportID
  Show '$ReportTitle = ' $ReportTitle

  date-time () HH:MI:SS &timeBegan
  date-time () MM/DD/YYYY &dateBegan
  show ' '
  show 'Report Began at ' &timeBegan ' on ' &dateBegan
  show ' '

  If $prcs_process_instance = ''
    Show 'Enter pay end date'
    Do Ask-As-Of-Date
    Let $pay_end_dt = $AsOfDate
    Show 'Enter paycheck issue date'
    Do Ask-As-Of-Date
    Let $check_dt = $AsOfDate
  Else
    !AXL 8/28/2002 New source for PAY END DT and CHECK DATE - Begin
    !Do GEXXX922-Select-Parameters
    !Let $pay_end_dt = $GEX_RC_PAY.Pay_End_Dt
    !Let $check_dt   = $GEX_RC_PAY.Check_Dt
    Do Select-Parameters
    Let $pay_end_dt = $RC_PAYINIT.Pay_End_Dt   
    Let $check_dt   = $RC_PAYINIT.Check_Dt
    !AXL 8/28/2002 New source for PAY END DT and CHECK DATE - End

    If Rtrim($pay_end_dt, ' ') = '' Or
       Rtrim($check_dt, ' ')   = ''
      Show 'Pay end date and paycheck issue date are both required for this process'
      Stop
    End-If
  End-If
End-Procedure


Begin-Procedure P200-Main-Process
#debug9 Show 'P200-Main-Process'
Begin-Select
company     ()  on-break  level=1  print=never
paygroup    ()  on-break  level=2  print=never
pay_end_dt  ()  on-break  level=3  print=never
off_cycle   ()  on-break  level=4  print=never

page_num       ()  on-break  level=5  print=never
!                                   after=P240-Update-Pay-Page
!line_num       ()  on-break  level=6  print=never
!                                   after=P230-Update-Pay-Line
!addl_nbr       ()  on-break  level=7  print=never
!                                   after=P220-Update-Pay-Earnings
!erncd

  Let $company   = &company
  Let $paygroup  = &paygroup
  Let $off_cycle = &off_cycle
  Let #page      = &page_num

  Do P240-Update-Pay-Page
  Do P230-Update-Pay-Line
  Do P220-Update-Pay-Earnings
  Do P210-Update-Pay-Oth-Earns

From   ps_pay_page

Where  pay_end_dt = $pay_end_dt
! CJH 06/20/2000 - comment-out criteria and use at the final ok_to_pay update
! And    erncd      = '510'
! And    oth_pay  > 0

Order By company,
       paygroup,
       pay_end_dt,
       off_cycle,
       page_num
End-Select
End-Procedure


Begin-Procedure P210-Update-Pay-Oth-Earns
#debug9 Show 'P210-Update-Pay-Oth-Earns'
  Begin-SQL On-Error=P290-SQL-Error('P210-Update-Pay-Oth-Earns')
    Update ps_pay_oth_earns poe
       Set off_cycle  = 'Y',
       job_pay         = 'N'		!AXL 10-30-2001 Add
     Where company    = $company
       And paygroup   = $paygroup
       And pay_end_dt = $pay_end_dt
       And off_cycle  = $off_cycle
       And page_num      = #page		!AXL 08/28/2002
  End-SQL

  Add #sql-count To #poe-count
End-Procedure


Begin-Procedure P220-Update-Pay-Earnings
#debug9 Show 'P220-Update-Pay-Earnings'
  Begin-SQL On-Error=P290-SQL-Error('P220-Update-Pay-Earnings')
    Update ps_pay_earnings pe
       Set off_cycle       = 'Y',
           disable_dir_dep = 'Y',
           job_pay         = 'N',
           reg_pay_hrs     = 0,
           reg_pay         = 0,
           reg_earns       = 0,
           ot_hrs          = 0,
           ded_taken       = 'S',
           ded_subset_id   = 'BON',
           ded_taken_genl  = 'S',	!AXL 04/08/2003
           ded_subset_genl = 'BON'	!AXL 04/08/2003
     Where company    = $company
       And paygroup   = $paygroup
       And pay_end_dt = $pay_end_dt
       And off_cycle  = $off_cycle
       And page_num   = #page		!AXL 08/28/2002
  End-SQL

  Add #sql-count To #pe2-count
End-Procedure


Begin-Procedure P230-Update-Pay-Line
#debug9 Show 'P230-Update-Pay-Line'
  Begin-SQL On-Error=P290-SQL-Error('P230-Update-Pay-Line')
    Update ps_pay_line pl
       Set off_cycle  = 'Y',
           check_dt   = $check_dt
     Where company    = $company
       And paygroup   = $paygroup
       And pay_end_dt = $pay_end_dt
       And off_cycle  = $off_cycle
       And page_num      = #page		!AXL 08/28/2002
  End-SQL

  Add #sql-count To #pl-count
End-Procedure


Begin-Procedure P240-Update-Pay-Page
#debug9 Show 'P240-Update-Pay-Page'
  Begin-SQL On-Error=P290-SQL-Error('P240-Update-Pay-Page')
    Update ps_pay_page pp
       Set off_cycle  = 'Y'
     Where company    = $company
       And paygroup   = $paygroup
       And pay_end_dt = $pay_end_dt
       And off_cycle  = $off_cycle
       And page_num      = #page		!AXL 08/28/2002
  End-SQL

  Add #sql-count To #pp-count
End-Procedure


Begin-Procedure P250-Update-Pay-Calendar
#debug9 Show 'P250-Update-Pay-Calendar'
  Begin-SQL On-Error=P290-SQL-Error('P250-Update-Pay-Calendar')
    Update ps_pay_calendar pc
       Set pay_sheets_run = 'N'
     Where pay_end_dt = $pay_end_dt
  End-SQL

  Add #sql-count To #pc-count
End-Procedure


Begin-Procedure P260-Delete-On-Cycle-Sheets
#debug9 Show 'P260-Delete-On-Cycle-Sheets'
  Begin-SQL
    Delete [$table_name]
     Where pay_end_dt = $pay_end_dt
       And off_cycle  = 'N'
  End-SQL

  Evaluate $table_name
    When = 'ps_pay_oth_earns'
      Add #sql-count To #dpoe-count
    When = 'ps_pay_earnings'
      Add #sql-count To #dpe-count
    When = 'ps_pay_line'
      Add #sql-count To #dpl-count
    When = 'ps_pay_page'
      Add #sql-count To #dpp-count
  End-Evaluate
End-Procedure


Begin-Procedure P290-SQL-Error($Action)
#debug9 Show 'P290-SQL-Error'
  Show $Action ' failed'
  Show 'SQL Error:  '  $_sql-error
  Show 'company ' $company
  Show 'paygroup ' $paygroup
  Show 'off_cycle ' $off_cycle
!  Show 'page_num ' #page
!  Show 'line_num ' #line
!  Show 'addl_nbr ' #addl
!  Show 'erncd ' $erncd
  Stop
End-Procedure


Begin-Procedure P300-Finalization
#debug9 Show 'P300-Finalization'
  Do P250-Update-Pay-Calendar
  Do P310-Update-Pay-Earnings

  Show ' '
  Show '------------------------------'
  Move #poe-count to $sql-count 999,999,990
  Show 'Update PAY_OTH_EARNS Table'
  Show ' off_cycle = ''Y'''
  Show $sql-count ' rows updated'
  Show '------------------------------'
  Move #pe2-count to $sql-count 999,999,990
  Show 'Update PAY_EARNINGS Table'
  Show ' off_cycle       = ''Y'''
  Show ' disable_dir_dep = ''Y'''
  Show ' job_pay set     = ''N'''
  Show ' reg_pay_hrs     = 0'
  Show ' reg_pay         = 0'
  Show ' reg_earns       = 0'
  Show ' ot_hrs          = 0'
  Show ' ded_taken       = ''S'''
  Show ' ded_subset_id   = ''BON'''
  Show $sql-count ' rows updated'
  Show '------------------------------'
  Move #pl-count to $sql-count 999,999,990
  Show 'Update PAY_LINE Table'
  Show ' off_cycle = ''Y'''
  Show $sql-count ' rows updated'
  Show '------------------------------'
  Move #pp-count to $sql-count 999,999,990
  Show 'Update PAY_PAGE Table'
  Show ' off_cycle = ''Y'''
  Show $sql-count ' rows updated'
  Show '------------------------------'
  Move #pc-count to $sql-count 999,999,990
  Show 'Update PAY_CALENDAR Table'
  Show ' pay_sheets_run = ''N'''
  Show $sql-count ' rows updated'
  Show '------------------------------'
  Move #dpe3-count to $sql-count 999,999,990
  Show 'Update PAY_EARNINGS Table'
  Show ' ok_to_pay = ''N'''
  Show $sql-count ' rows updated'
  Show '------------------------------'
  Show ' '

  Do Reset                   !reset.sqc
  Do Stdapi-Term             !stdapi.sqc

  date-time () HH:MI:SS &timeEnded
  date-time () MM/DD/YYYY &dateEnded
  show ' '
  show 'Report Ended at ' &timeEnded ' on ' &dateEnded

  Show 'Successful end of report'
End-Procedure


Begin-Procedure P310-Update-Pay-Earnings
#debug9 Show 'P310-Update-Pay-Earnings'
Begin-SQL On-Error=P290-SQL-Error('P310-Update-Pay-Earnings')
! CJH - 06/20/2000 - add criteria to the ok_to_pay update to select only those
!                    rows that do not have earnings.
    Update ps_pay_earnings pe
       Set pe.ok_to_pay = 'N'
     Where  (pe.company, pe.paygroup, pe.pay_end_dt, pe.off_cycle, pe.page_num,
             pe.line_num, pe.addl_nbr) in (select po.company, po.paygroup,
                                      po.pay_end_dt, po.off_cycle,
                                      po.page_num, po.line_num, po.addl_nbr
                                     from ps_pay_oth_earns po
                                     where ((po.erncd <> '510') or
                                            (po.erncd = '510' and po.oth_pay = 0))
                                       and po.pay_end_dt = $pay_end_dt )
!       And off_cycle  = 'N'
! CJH - end

End-SQL
  Add #sql-count To #dpe3-count

! CJH - begin
Begin-SQL On-Error=P290-SQL-Error('P310-Update-Pay-Earnings')
    Update ps_pay_earnings pep
       Set pep.ok_to_pay = 'N'
     Where  not exists (select 'X'
                        from ps_pay_oth_earns pop
                        where pop.company = pep.company and
                              pop.paygroup = pep.paygroup and
                              pop.pay_end_dt = pep.pay_end_dt and
                              pop.off_cycle = pep.off_cycle and
                              pop.page_num = pep.page_num and
                              pop.line_num = pep.line_num and
                              pop.addl_nbr = pep.addl_nbr)
     and pep.pay_end_dt = $pay_end_dt 

! CJH - end

End-SQL
  Add #sql-count To #dpe3-count
End-Procedure


!#include 'gexxx922.sqc'  AXL 9/12/2002 No longer needed
#include 'gexpyrcl.sqc'  !Get pay run ctl 
#Include 'askaod.sqc'    !Ask-As-Of-Date procedure
#Include 'stdapi.sqc'    !Process Scheduler Interface
#Include 'reset.sqc'     !Reset Printer Procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'  !Routines for date math
