!***********************************************************************
! GEXHR065.SQR:                                                        *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! Giant Eagle, Inc.; it is not to be copied, reproduced,or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of Giant Eagle.                       *
!                                                                      *
! Copyright (c) 1997-2006 Giant Eagle,Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
! GEXHR065.SQR :                                                       *
!                                                                      *
! Narrative:            Create a program for the submission and	       *
!			management of Job Training Grant Programs.     *
!			ITG-38688.				       *
!								       *
! #Debug Used:          8,9                                            *
!								       *
! SQL Tables:           PS_JOB, PS_PERSONAL_DATA, PS_JOBCODE_TBL,      *
!		        PS_POSITION_DATA                               *
!								       *
! Written by:           Rapidigm, Deepak. S, 01/25/07                  *
!                                                                      *
! Normally Run:         This process will be run Quarterly.	       *
!                                                                      *
! Control Parms:        Reads run control tables PS_GEX_RUN_CNTL,      *
!                       PS_GEX_RC_PAYGROUP, PS_GEX_RC_JOBCODE,         *
!			PS_GEX_RC_UNION_CD                             *
!***********************************************************************

!***********************************************************************
!                         MODIFICATION HISTORY                         *
!***********************************************************************
! INITIALS  DATE        DESCRIPTION OF THE CHANGE                      *
!***********************************************************************
! DXS       01/25/07    Initial Creation                               *
!                                                                      *
! ISDVDZK            05/08/2009   Added the Code as per the ITG 60308  *
!                                 for pulling description from Position *
!                                 data  Start                           *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
#include 'setup01.sqc'   !Printer and Page-Size Initialization (Portrait)

#define #column1  001
#define #column2  010
#define #column3  023
#define #column4  050
#define #column5  060

!***********************************************************************
! Defines Heading of the report
!***********************************************************************
Begin-Heading 6
  let $ReportID = 'GEXHR065'
  #include 'stdhdg01.sqc'

  Print 'Job Training Grants'     (3,1) center
  print $rpt-subtitle  (+1,1) center

  Print 'DEPTID'	(+2,{#column1})  
  Print 'EMPLID'	(0,{#column2})	
  Print 'NAME'		(0,{#column3})	
  Print 'POSITION'	(0,{#column4})	
  Print 'JOB TITLE'     (0,{#column5})	

End-Heading


!***********************************************************************
! Start of Program
!***********************************************************************
Begin-Report

  do Init-DateTime           !datetime.sqc
  do Init-Number             !number.sqc
  do Get-Current-DateTime    !curdttim.sqc
  do Stdapi-Init             !stdapi.sqc
  if isnull($prcs_process_instance)
	let $Prcs_OprID = 'ISDVDXS'
	let $Prcs_Run_Cntl_ID = 'gexhr065'
  end-if

  ! get from_date, end_date and min wage from run control page
  do Get-Run-Control	     !gexrnctl.sqc  

 if isnull(&thrudate) 
  	do Select-Thru-Date
  else
        let $thrudate = &thrudate
  end-if

  if isnull(&fromdate)
    	do Select-From-Date
  else
	let $fromdate = &fromdate
  end-if

  #debug8 show '<' $fromdate '><' $thrudate '><' &min_wage '>'

  do Build-Dynamic-Where
  do Select-Data
  do stdapi-term

End-Report


!***********************************************************************
! Fetches the end date for the quarter depending on the run date
!***********************************************************************
begin-procedure Select-Thru-Date
	let $asofdate = datenow()
	let $thrudate = dateadd(strtodate($asofdate,'DD-MON-YYYY'), 'day', -1)
	
end-procedure 


!***********************************************************************
! Fetches the begin date for the quarter depending on the run date
!***********************************************************************
begin-procedure Select-From-Date
	let $asofdate = datenow()
	let $fromdate = dateadd(strtodate($asofdate,'DD-MON-YYYY'), 'month', -3)
	
end-procedure 


!***********************************************************************
! Builds the dynamic where clause for Company/Paygroup, Jobcode and 
! Union Code. Values retrieved from the run control page
!***********************************************************************
Begin-procedure Build-Dynamic-Where

    Let $GEXXX901_Company_Alias  = 'J.COMPANY' 
    Let $GEXXX901_Paygroup_Alias = 'J.PAYGROUP'   
    Do GEXXX901-Select-Company-Paygroup-Parameters
    
    Let $GEXXX916_Jobcode_Alias = 'J.JOBCODE'
    Do GEXXX916-Select-Jobcode-Parameters
    
    Let $GEXXX913_UNION_CD_Alias = 'J.UNION_CD'
    Do GEXXX913-Select-UNION-CD-Parameters 
    
    #ifdef debug8
    show '$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA ' $GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA
    show '$GEXXX916_INCLUDE_JOBCODE_CRITERIA  ' $GEXXX916_INCLUDE_JOBCODE_CRITERIA
    show '$GEXXX913_INCLUDE_UNION_CD_CRITERIA ' $GEXXX913_INCLUDE_UNION_CD_CRITERIA
    #end-if
End-Procedure 


!***********************************************************************
! Main Select Procedure 
!***********************************************************************
begin-procedure Select-Data
begin-select
j.deptid
j.emplid
p.NAME
j.position_nbr
c.descr 
! j.hourly_rt
! j.annual_rt


   
! ISDVDZK Added the Code as per the ITG 60308 on 05/08/2009 for pulling description from Position data  Start
  Let $position_nbr= LTrim(RTrim(&j.position_nbr,' '),' ')
  
   Do Get-Position-Title				
   !show 'position_nbr' $position_nbr 'PosName' $PosName
 
 If $PosName = ' '			
   move &c.descr    to $descr_job    XXXXXXXXXXXXXXXXXXX  
 Else
   move $PosName   to $descr_job     XXXXXXXXXXXXXXXXXXX  
 End-If					

!ISDVDZK Added the Code as per the ITG 60308 on 05/08/2009 for pulling description from Position data End   

   #debug8 show '<' &j.deptid '><' &j.emplid '><' &p.NAME '><' &j.position_nbr '><' &c.descr '>' 

	Do Print-Data

FROM ps_job j, ps_personal_data p, ps_jobcode_tbl c, ps_position_data d
WHERE j.effdt = (SELECT MAX(j_ed.effdt) 
		FROM ps_job j_ed
	  	WHERE j_ed.emplid = j.emplid
		AND j_ed.empl_rcd = j.empl_rcd
		AND j_ed.effdt <= $thrudate)
AND j.effseq = (SELECT MAX(j_es.effseq) 
	  	FROM ps_job j_es
	  	WHERE j_es.emplid = j.emplid
		AND j_es.empl_rcd = j.empl_rcd
		AND j_es.effdt =j.effdt)
AND j.empl_status = 'A'
AND j.full_part_time = 'F'
AND j.emplid = p.emplid
AND p.state = 'PA'
AND c.setid = 'COMMN'
AND c.jobcode = j.jobcode
AND c.effdt = (SELECT MAX(c_ed.effdt) 
		FROM ps_jobcode_tbl c_ed
		WHERE c_ed.setid = c.setid
		AND c_ed.jobcode = c.jobcode)
AND c.eff_status = 'A'
AND d.position_nbr = j.position_nbr
AND d.action_reason = 'NEW'
AND d.effdt  = (SELECT MAX(d_ed.effdt) 
		FROM ps_position_data d_ed
		WHERE d_ed.POSITION_NBR = d.position_nbr)
AND d.eff_status = 'A'									   
AND d.action_dt BETWEEN $fromdate AND $thrudate
AND [$GEXXX901_INCLUDE_COMPANY_PAYGROUP_CRITERIA]
AND [$GEXXX916_INCLUDE_JOBCODE_CRITERIA]
AND [$GEXXX913_INCLUDE_UNION_CD_CRITERIA]
AND j.hourly_rt > (1.5*&min_wage)
end-select
end-procedure 									   


!***********************************************************************
! Procedure to Print the data
!***********************************************************************
begin-procedure Print-Data

	Print &j.deptid 	(+1,{#column1})
	Print &j.emplid 	(0,{#column2})
	Print &p.NAME		(0,{#column3})
	Print &j.position_nbr	(0,{#column4})
	!Print &c.descr 	(0,{#column5})  ! ISDVDZK Added the Code as per the ITG 60308 on 05/08/2009 for pulling description from Position data  Start
	
	Print $descr_job	(0,{#column5})  ! ISDVDZK Added the Code as per the ITG 60308 on 05/08/2009 for pulling description from Position data  Start
	

end-procedure



#include 'stdapi.sqc'    !Process Scheduler Interface
#include 'reset.sqc'     !Reset Printer Procedure
#include 'curdttim.sqc'  !Get-Current-DateTime Procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date math
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#include 'gexrnctl.sqc'  !Routine for Get-Run-Control
#include 'gexxx901.sqc'	 !Get Company/Paygroup from Run Control Page
#include 'gexxx916.sqc'  !Get Jobcode from Run Control Page
#include 'gexxx913.sqc'  !Get UnionCD from Run Control 
#include 'getposnm.sqc'  !Get-Position-Title prodecure 
! ISDVDZK Added the Code as per the ITG 60308 on 05/08/2009 for pulling description from Position data  Start