!***********************************************************************
!  HIPAA:  Medical Certification Statement                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/09/12:10:53:28                                 !
!       $Release:  HR9                                                 !
!      $Revision:  103                                                 !
!                                                                      *
!***********************************************************************
!                                                                      *
! This report is required for United States only. No Translation. -DCD *
!***********************************************************************

#Include 'setenv.sqc'  !Set environment

Begin-Setup
  #Include 'ptpsp125.sqc'
End-Setup

#Include 'usarpt.sqc'  !Variables specific to USA


#define len_plan_type           9
#define len_date                10
#define len_status              10
#define len_benefit_plan        30
#define len_provider            30
#define len_group               30
#define len_CvgCdDescr          12
#define len_dep_name            24
#define len_birthdate           10
#define len_ssn                 11
#define len_verbage             100

#define col_plan_type           2
#define col_date                13
#define col_status              13
#define col_benefit_plan        25
#define col_provider            25
#define col_group               25
#define col_CvgCdDescr          57
#define col_dep_name            71
#define col_birthdate           97
#define col_ssn                 110
#define col_txt                 3


!***********************************************************************
! Procedue Name:  Begin-Report                                         *
!***********************************************************************
Begin-Program

  do Init-Report

  do Process-Main
  do StdAPI-Term

End-Program

!************************************************************************
!* Procedure Name  Init-Report                                          *
!************************************************************************
Begin-Procedure Init-Report
 
  do Init-DateTime
  do Init-Number

  move 'HIPAA - Employees' to $ReportID
  move 'Certificate of Group Health Plan Coverage' to $ReportTitle
  display $ReportTitle
  do Get-Current-DateTime
  do StdAPI-Init

  if $prcs_process_instance = ''
    do Ask-From-Thru-Date

    do Format-DateTime('19000101',$Temp_Dt,{DEFCMP},'','native')

    if $Temp_dt = $FromDate
       do Format-DateTime('19960701',$FromDate,{DEFCMP},'','native')
    end-if
  else
    do Get-Input-Values
    do Get-Include-Options
    
  end-if
  
End-Procedure


!**************************************************************************
!* Procedure Name: Get-Input-Values                                       *
!**************************************************************************
Begin-Procedure Get-Input-Values
    
    Do Select-Parameters

    Do Get-From-Thru-Date

    if $FromDate = ''
       Do Format-DateTime('07/01/1996', $FromDate, {DEFMDY},'','native')
    end-if

    if $ThruDate = ''
       Let $ThruDate = $AsOfToday
    end-if

End-Procedure

!**************************************************************************
!* Procedure Name: Process-Main                                           *
!**************************************************************************
! The HIPAA-Employee report is driven by this SQL, which finds the most
! recent Health_Benefit (Covrg Begin) row an Employee/Empl_Rcd/PlanType.
! If this row indicates a Positive Termination of coverage (Coverage_Elect
! = 'T' or Coverage_Elect = 'W'), then a certificate will be produced for
!this employee.
!
! Note: The old-style "Coverage End" dates are not supported.
! In order to inhibit the creation of a certificate for Multi-Job employees
! who have active coverage in another Benefit_Rcd_Nbr, we will also do a check
! to see if the employee has active coverage in the same plan type, under
! a different Benefit_Rcd_Nbr.
! Note that this applies to a single plan-type only.
!
Begin-Procedure Process-Main

  Let $Old_Plan_Type    = ''
  Let $Old_Emplid       = ''
  Let $FirstPage        = 'Y'

Begin-SELECT
A.EMPLID
A.EMPL_RCD
A.COBRA_EVENT_ID
A.PLAN_TYPE
A.BENEFIT_NBR
A.EFFDT
A.COVERAGE_BEGIN_DT
A.COVERAGE_ELECT
A.BENEFIT_PLAN
A.COVRG_CD

        let $emplid_hip     = &A.EMPLID
        let #empl_rcd_hip   = &A.EMPL_RCD
        let #cobra_hip      = &A.COBRA_EVENT_ID
        let $plantype_hip   = &A.PLAN_TYPE
        let $cov_begin_hip  = &A.COVERAGE_BEGIN_DT

        let $HIPAA = 'N'
        do Find-HIPAA-Plan

!        if $HIPAA <> 'Y'
!          Do Check-For-HIPAA
!        end-if

        Do Check-For-Other-Coverage

        if ($Other_Coverage = 'N' and $HIPAA = 'Y')

                Let $Emplid         = &A.EMPLID
                Let $Plan_Type      = rtrim(&A.PLAN_TYPE, ' ')

                if  $Emplid <> $Old_Emplid
                    if $FirstPage = 'Y'
                       Let $FirstPage        = 'N'
                    else
                       IF #Creditable_Days > 0 OR $IncOptnDetaiL = 'Y'
                           new-page
                       END-IF
                    end-if
                    Do Get-Employee-Info
                    Do Get-Administrator
                    do Get-Coverage-Cutoff
                    Let $Old_Emplid = $Emplid
                    Let $Old_Plan_Type = $Plan_Type
                else
                    if $Plan_Type <> $Old_Plan_Type
                       new-page
                       Do Get-Administrator
                       Let $Old_Plan_Type = $Plan_Type
                    end-if
                end-if

                Let #Cobra_Event_Id = &A.COBRA_EVENT_ID
                Let $Effdt          = &A.EFFDT

                Let #Creditable_Days = 0
                Let #Gap_Days        = 0
                Let $Freeze_Credit   = 'N'
                Let #TermWaiveCnt    = 0
                Let $TermWaiveFreeze = 'N'

                Let $FieldName = 'PLAN_TYPE'
                Let $FieldValue = $Plan_Type
                Let $AsOfDate = $Effdt

                Do Read-Translate-Table
                

                Let $Status = rtrim(&A.COVERAGE_ELECT, ' ')
                Let $FieldName  = 'COVERAGE_ELECT'
                Let $FieldValue = $Status
                Let $AsOfDate   = $Effdt
                Do Read-Translate-Table


                do Format-DateTime(&A.COVERAGE_BEGIN_DT, $CvgDt_def, {DEFDATE}, '', '')

                let $CvgDt_Ended = $CvgDt_def

                Let #CreditDay = 0
                Let #WaveDays = 0
                Let $Gap_Flag = 'N'

                Let $Election1 = $Status
                Let $Effdt1 = &A.EFFDT

                Do Convert-To-DTU-Date(&A.COVERAGE_BEGIN_DT, $TermDate_dtu)
                Do dtu-add-months($TermDate_dtu, -18, $PeriodDate_dtu)
                Do Convert-From-DTU-Date($PeriodDate_dtu, $PeriodDate)

                Do Get-Enrolled-Health-Info
                Do Get-Enrolled-Health-Plan-Info                   

                Do Maintain-History
                
                IF #Creditable_Days > 0 OR $IncOptnDetail = 'Y'

                   Do Print-Header-Instructions
                   Do Print-Employee-Data
                   Do Print-educational-instructions
                end-if
        end-if

FROM PS_HEALTH_BENEFIT  A,
     PS_PERSON          E
WHERE A.COVERAGE_BEGIN_DT = (SELECT MAX(A1.COVERAGE_BEGIN_DT)
                               FROM PS_HEALTH_BENEFIT A1
                              WHERE A1.EMPLID             = A.EMPLID
                                AND A1.EMPL_RCD           = A.EMPL_RCD
                                AND A1.PLAN_TYPE          = A.PLAN_TYPE
                                AND A1.COVERAGE_BEGIN_DT >= $FromDate
                                AND A1.COVERAGE_BEGIN_DT <= $ThruDate
                                AND (A1.COVERAGE_ELECT = 'T' OR A1.COVERAGE_ELECT = 'W'))
  AND (A.COVERAGE_ELECT = 'T' OR A.COVERAGE_ELECT = 'W')
  AND E.EMPLID = A.EMPLID
  AND E.DT_OF_DEATH IS NULL
  AND NOT EXISTS (SELECT 'X' FROM PS_BEN_LETTER_HIST LH
                  WHERE LH.EMPLID            =  A.EMPLID
                    AND LH.DEPENDENT_BENEF   = '00'
                    AND LH.LETTER_CD         = 'HEE'
                    AND LH.COVERAGE_END_DT   = (SELECT MAX(LH1.COVERAGE_END_DT)
                                                  FROM PS_BEN_LETTER_HIST LH1
                                                  WHERE LH1.EMPLID            = LH.EMPLID
                                                    AND LH1.DEPENDENT_BENEF   = LH.DEPENDENT_BENEF
                                                    AND LH1.LETTER_CD         = LH.LETTER_CD)
                    AND A.COVERAGE_BEGIN_DT <= LH.COVERAGE_END_DT)
 ORDER BY A.EMPLID, A.PLAN_TYPE, A.EMPL_RCD
End-SELECT
end-procedure

begin-procedure  Find-HIPAA-Plan
  let $HIPAA = 'N'

  do Get-Benefit-Plan

begin-SELECT
D.HIPAA_PLAN

  let $HIPAA = rtrim(&D.HIPAA_PLAN,' ')

FROM PS_BEN_DEFN_OPTN   O,
     PS_BEN_DEFN_PLAN   D,
     PS_BEN_DEFN_PGM    C,
     PS_BEN_PROG_PARTIC B
WHERE B.EMPLID          = $emplid_hip
  AND B.EMPL_RCD        = #empl_rcd_hip
  AND B.COBRA_EVENT_ID  = #cobra_hip
  AND B.EFFDT = (SELECT MAX(B1.EFFDT)
                  FROM PS_BEN_PROG_PARTIC B1
                  WHERE B1.EMPLID          = B.EMPLID
                    AND B1.EMPL_RCD        = B.EMPL_RCD
                    AND B1.COBRA_EVENT_ID  = B.COBRA_EVENT_ID
                    AND B1.EFFDT          <= $BenPlanEffdt)
  AND B.BENEFIT_PROGRAM = C.BENEFIT_PROGRAM
  AND C.EFFDT           = (SELECT MAX(C1.EFFDT)
                           FROM PS_BEN_DEFN_PGM C1
                           WHERE C1.BENEFIT_PROGRAM = C.BENEFIT_PROGRAM
                             AND C1.EFFDT          <=  $BenPlanEffdt)
  AND C.BENEFIT_PROGRAM = D.BENEFIT_PROGRAM
  AND D.EFFDT           = C.EFFDT
  AND D.PLAN_TYPE       = $plantype_hip
  AND D.BENEFIT_PROGRAM = O.BENEFIT_PROGRAM
  AND O.PLAN_TYPE       = D.PLAN_TYPE
  AND O.EFFDT           = D.EFFDT
  AND O.BENEFIT_PLAN    = $BenPlanTerm
End-SELECT
End-Procedure

begin-procedure Get-Benefit-Plan
  let $BenPlanTerm  = ' '
  let $BenPlanEffdt = $FromDate
begin-SELECT
BP.BENEFIT_PLAN
BP.COVERAGE_BEGIN_DT

  let $BenPlanTerm  = &BP.BENEFIT_PLAN
  let $BenPlanEffdt = &BP.COVERAGE_BEGIN_DT

FROM PS_HEALTH_BENEFIT BP
WHERE BP.EMPLID            = $emplid_hip
  AND BP.EMPL_RCD          = #empl_rcd_hip
  AND BP.COBRA_EVENT_ID    = #cobra_hip
  AND BP.PLAN_TYPE         = $plantype_hip
  AND BP.COVERAGE_BEGIN_DT = (SELECT MAX(BP1.COVERAGE_BEGIN_DT)
                              FROM PS_HEALTH_BENEFIT BP1
                              WHERE BP1.EMPLID             = BP.EMPLID
                                AND BP1.EMPL_RCD           = BP.EMPL_RCD
                                AND BP1.COBRA_EVENT_ID     = BP.COBRA_EVENT_ID
                                AND BP1.PLAN_TYPE          = BP.PLAN_TYPE
                                AND BP1.COVERAGE_BEGIN_DT <= $cov_begin_hip
                                AND BP1.COVERAGE_ELECT     = 'E')
  AND BP.COVERAGE_ELECT    = 'E'
end-SELECT
end-procedure
!
! The purpose of the next procedure is to set the process cutoff date (which represents the
! latest coverage lost date (coverage termination) during the reporting period in any one of
! the plan types that the employee is enrolled in. This date is used to prevent re-generating
! certificates when program is re-run for the same period.
!
begin-procedure Get-Coverage-Cutoff
  LET $ProcessCutOff = ''
begin-SELECT
CUT.EMPLID
CUT.EMPL_RCD
CUT.PLAN_TYPE
CUT.EFFDT
CUT.COBRA_EVENT_ID
CUT.COVERAGE_BEGIN_DT

  let $emplid_hip     = &CUT.EMPLID
  let #empl_rcd_hip   = &CUT.EMPL_RCD
  let #cobra_hip      = &CUT.COBRA_EVENT_ID
  let $plantype_hip   = &CUT.PLAN_TYPE
  let $cov_begin_hip  = &CUT.COVERAGE_BEGIN_DT
  DO Find-HIPAA-Plan

  let $ProcessCutOff  = &CUT.COVERAGE_BEGIN_DT
  if $HIPAA = 'Y'
      exit-SELECT
  end-if

FROM PS_HEALTH_BENEFIT CUT
WHERE CUT.EMPLID          = &A.EMPLID
  AND (CUT.COVERAGE_ELECT = 'T'
    OR CUT.COVERAGE_ELECT = 'W')
  AND (CUT.COVERAGE_BEGIN_DT >= $FromDate
   AND CUT.COVERAGE_BEGIN_DT <= $ThruDate)
ORDER BY CUT.COVERAGE_BEGIN_DT DESC
end-SELECT
end-procedure

! The following procedure will fetch the plan administrator of the benefit plan that
! was current prior to termination/waiver.
!
begin-procedure Get-Administrator

  Let $Administrator   = ''
  Let $Admin_Address1  = ''
  Let $Admin_Address2  = ''
  Let $Admin_Address3  = ''
  Let $Admin_CityState = ''
  Let $Admin_Phone     = ''

begin-SELECT
AD.CONTACT_NAME

  Let $Administrator = rtrim(&AD.CONTACT_NAME,' ')

AD.ADDRESS1
AD.ADDRESS2
AD.ADDRESS3

  Let $Admin_Address1 = rtrim(&AD.ADDRESS1, ' ')
  Let $Admin_Address2 = rtrim(&AD.ADDRESS2, ' ')
  Let $Admin_Address3 = rtrim(&AD.ADDRESS3, ' ')

AD.CITY
AD.STATE
AD.POSTAL

  Let $Admin_CityState = rtrim(&AD.CITY, ' ')
  if rtrim(&AD.STATE,' ')  <> ''
      LET $Admin_CityState = $Admin_CityState || ',' || rtrim(&AD.STATE, ' ')
  end-if
  if rtrim(&AD.POSTAL, ' ') <> ''
     Let $Admin_CityState = $Admin_CityState || ' ' || rtrim(&AD.POSTAL, ' ')
  end-if

AD.PHONE

  Let $Admin_Phone = 'Phone#: ' || rtrim(&AD.PHONE,' ')


FROM PS_HEALTH_BENEFIT  HP,
     PS_BENEF_PLAN_TBL  BP,
     PS_BEN_PLN_CONTACT PC,
     PS_BEN_CONTACT_TBL AD
WHERE HP.EMPLID             = &A.EMPLID
AND   HP.EMPL_RCD           = &A.EMPL_RCD
AND   HP.COBRA_EVENT_ID     = &A.COBRA_EVENT_ID
AND   HP.PLAN_TYPE          = &A.PLAN_TYPE
AND   HP.BENEFIT_NBR        = &A.BENEFIT_NBR
AND   HP.COVERAGE_BEGIN_DT  = (SELECT MAX(HP1.COVERAGE_BEGIN_DT)
                               FROM PS_HEALTH_BENEFIT HP1
                               WHERE HP1.EMPLID             = HP.EMPLID
                                AND  HP1.EMPL_RCD           = HP.EMPL_RCD
                                AND  HP1.COBRA_EVENT_ID     = HP.COBRA_EVENT_ID
                                AND  HP1.BENEFIT_NBR        = HP.BENEFIT_NBR
                                AND  HP1.PLAN_TYPE          = HP.PLAN_TYPE
                                AND  HP1.EFFDT             <= &A.EFFDT
                                AND  HP1.COVERAGE_BEGIN_DT <= &A.COVERAGE_BEGIN_DT
                                AND  HP1.COVERAGE_ELECT     = 'E')
AND   HP.EFFDT              = (SELECT MAX(HP2.EFFDT)
                               FROM PS_HEALTH_BENEFIT HP2
                               WHERE HP2.EMPLID             = HP.EMPLID
                                AND  HP2.EMPL_RCD           = HP.EMPL_RCD
                                AND  HP2.COBRA_EVENT_ID     = HP.COBRA_EVENT_ID
                                AND  HP2.BENEFIT_NBR        = HP.BENEFIT_NBR
                                AND  HP2.PLAN_TYPE          = HP.PLAN_TYPE
                                AND  HP2.EFFDT              < &A.EFFDT
                                AND  HP2.COVERAGE_BEGIN_DT  = HP.COVERAGE_BEGIN_DT
                                AND  HP2.COVERAGE_ELECT     = 'E')

AND   BP.PLAN_TYPE    = HP.PLAN_TYPE
AND   BP.BENEFIT_PLAN = HP.BENEFIT_PLAN
AND   BP.EFFDT =
        (SELECT MAX(BP1.EFFDT)
           FROM PS_BENEF_PLAN_TBL BP1
          WHERE BP1.PLAN_TYPE     = BP.PLAN_TYPE
            AND BP1.BENEFIT_PLAN  = BP.BENEFIT_PLAN
            AND BP1.EFFDT        <= &A.EFFDT)
AND   PC.PLAN_TYPE          = BP.PLAN_TYPE
AND   PC.BENEFIT_PLAN       = BP.BENEFIT_PLAN
AND   PC.EFFDT              = BP.EFFDT
AND   PC.BENEF_CONTACT_TYPE = 'HIPA'
AND   AD.BENEF_CONTACT_ID   = PC.BENEF_CONTACT_ID
AND   AD.EFFDT              = (SELECT MAX(AD1.EFFDT)
                              FROM PS_BEN_CONTACT_TBL AD1
                              WHERE AD1.BENEF_CONTACT_ID = AD.BENEF_CONTACT_ID
                                AND AD1.EFFDT           <= &A.EFFDT
                                AND AD1.EFF_STATUS       = 'A')
end-SELECT
end-procedure

!**************************************************************************
!* Procedure Name: Get-Employee-Info                                      *
!**************************************************************************
Begin-Procedure Get-Employee-Info

Begin-SELECT
PD.NAME

   do Get-Empl-Address ($Emplid,$AddrType, $AddressFound)
   Let $EE_Name     = rtrim(&PD.NAME, ' ')
   Let $EE_Address1 = rtrim($GETADDR_ADDRESS1, ' ')
   Let $EE_Address2 = rtrim($GETADDR_ADDRESS2, ' ')
   Let $EE_Address3 = rtrim($GETADDR_ADDRESS3, ' ')
   Let $EE_Address  = rtrim($GETADDR_CITY, ' ') || ', ' || rtrim($GETADDR_STATE, ' ')
   Let $EE_Address  = $EE_Address || ' ' || rtrim($GETADDR_POSTAL, ' ')

   do Get-Employee-NID

FROM PS_PERSON_NAME PD
WHERE PD.EMPLID = $Emplid
End-SELECT

End-Procedure


!**************************************************************************
!* Procedure Name: Get-Employee-NID
!**************************************************************************
Begin-Procedure Get-Employee-NID

Begin-SELECT
PN.NATIONAL_ID
   if rtrim(&PN.NATIONAL_ID, ' ') <> ''
     Let $SSN = edit(&PN.NATIONAL_ID, 'xxx-xx-xxxx')
   else
     Let $SSN = ''
   end-if

FROM PS_PERS_NID PN
WHERE PN.EMPLID          = $Emplid
  AND PN.COUNTRY         = {NID_COUNTRY}
  AND PN.PRIMARY_NID     = 'Y'
End-SELECT

End-Procedure


!**************************************************************************
!* Procedure Name: Check-For-HIPAA                                        *
!**************************************************************************

Begin-Procedure Check-For-HIPAA

Begin-SELECT
DD.HIPAA_PLAN

     Let $HIPAA = 'Y'

FROM PS_BEN_PROG_PARTIC BB,
     PS_BEN_DEFN_PGM CC,
     PS_BEN_DEFN_PLAN DD

WHERE BB.EMPLID = &A.EMPLID
  AND BB.EMPL_RCD  = &A.EMPL_RCD
  AND BB.EFFDT = (SELECT MAX(BB1.EFFDT)
                   FROM PS_BEN_PROG_PARTIC BB1
                  WHERE BB1.EMPLID = BB.EMPLID
                    AND BB1.EMPL_RCD  = BB.EMPL_RCD
                    AND BB1.EFFDT = &A.EFFDT)
  AND BB.BENEFIT_PROGRAM = CC.BENEFIT_PROGRAM
  AND CC.BENEFIT_PROGRAM = DD.BENEFIT_PROGRAM
  AND DD.PLAN_TYPE = &A.PLAN_TYPE
  AND CC.EFFDT = DD.EFFDT
  AND CC.EFFDT = (SELECT MAX(CC1.EFFDT)
                   FROM PS_BEN_DEFN_PGM CC1
                  WHERE CC1.BENEFIT_PROGRAM = CC.BENEFIT_PROGRAM
                    AND CC1.EFFDT <= BB.EFFDT)
  AND DD.HIPAA_PLAN = 'Y'

END-SELECT

End-Procedure

!**************************************************************************
!* Procedure Name: Check-For-Other-Coverage                               *
!**************************************************************************
! See if the employee has active coverage in the same plan type, for a
! different Benefit_Rcd_Nbr, as of the end of the reporting period.
!
Begin-Procedure Check-For-Other-Coverage
 Let $Other_Coverage = 'N'

Begin-Select
OC.COVERAGE_ELECT
OC.EMPL_RCD

        If &OC.COVERAGE_ELECT = 'E'
          Let $Other_Coverage = 'Y'
        End-If

FROM PS_HEALTH_BENEFIT OC
WHERE OC.EMPLID     = &A.EMPLID
  AND (OC.EMPL_RCD       <> &A.EMPL_RCD OR
       OC.COBRA_EVENT_ID <> &A.COBRA_EVENT_ID)
  AND OC.PLAN_TYPE  = &A.PLAN_TYPE
  AND OC.COVERAGE_BEGIN_DT = (SELECT MAX(OC1.COVERAGE_BEGIN_DT)
                                FROM PS_HEALTH_BENEFIT OC1
                               WHERE OC1.EMPLID             = OC.EMPLID
                                 AND OC1.EMPL_RCD           = OC.EMPL_RCD
                                 AND OC1.BENEFIT_NBR        = OC.BENEFIT_NBR
                                 AND OC1.COBRA_EVENT_ID     = OC.COBRA_EVENT_ID
                                 AND OC1.PLAN_TYPE          = OC.PLAN_TYPE
                                 AND OC1.COVERAGE_BEGIN_DT <= $ThruDate)
ORDER BY OC.EMPL_RCD
End-SELECT

End-Procedure

!**************************************************************************
!* Procedure Name: Get-Enrolled-Health-Info                               *
!**************************************************************************
! Note that we need to consider enrollments in all Benefit_Rcd_Nbrs (Empl_Rcd)
! since the certificate applies to an employee, not a specific Benefit_Rcd#
! for the employee. This is why the WHERE clause does not include a match
! on Empl_Rcd.
!
Begin-Procedure Get-Enrolled-Health-Info
 Let $En_Effdt = ''
 Let $LastCvgDt_dtu   = $TermDate_dtu
 Let #Creditable_Days = 0
 Let #Gap_Days        = 0
 Let $LastStatus      = rtrim(&A.COVERAGE_ELECT, ' ')
 Let $CvgDt_Began = ''

Begin-Select
HB.EMPLID
HB.EMPL_RCD
HB.BENEFIT_NBR
HB.COBRA_EVENT_ID
HB.EFFDT
HB.COVERAGE_BEGIN_DT
HB.PLAN_TYPE
HB.BENEFIT_PLAN
HB.COVRG_CD
HB.COVERAGE_ELECT

  

                do Format-DateTime(&HB.COVERAGE_BEGIN_DT, $CvgDt_def, {DEFDATE}, '', '')

                Let $Status = rtrim(&HB.COVERAGE_ELECT, ' ')
                Let $FieldName  = 'COVERAGE_ELECT'
                Let $FieldValue = $Status
                Let $AsOfDate   = &HB.EFFDT
                Do Read-Translate-Table

                if $Status = 'E'
                   Let $CvgDt_Began = $CvgDt_def
                   let $CovDate     = &HB.COVERAGE_BEGIN_DT
                ELSE
                   Let $CvgDt_Began = ''
                   let $CovDate     = ''
                end-if


       
                Do Calc-Credit-Period

                Let $LastStatus    = $Status
                Let $LastCvgDt_dtu = $CvgDt_dtu
                if ((#Creditable_Days >= 550  or  #Gap_Days >= 63) And
                    $Status = 'E')
                  Exit-Select
                end-if

 FROM PS_HEALTH_BENEFIT HB
WHERE HB.EMPLID            = &A.EMPLID
  AND HB.EMPL_RCD          = &A.EMPL_RCD
  AND HB.PLAN_TYPE         = &A.PLAN_TYPE
  AND HB.COVERAGE_ELECT = 'E'
  AND HB.COVERAGE_BEGIN_DT = (SELECT MIN(HB1.COVERAGE_BEGIN_DT)
                              FROM PS_HEALTH_BENEFIT HB1
                              WHERE HB1.EMPLID = HB.EMPLID
                              AND HB1.EMPL_RCD = HB.EMPL_RCD
                              AND HB1.COVERAGE_BEGIN_DT < &A.COVERAGE_BEGIN_DT
                              AND HB1.PLAN_TYPE = HB.PLAN_TYPE
                              AND HB1.COVERAGE_ELECT = 'E'
                              AND NOT EXISTS (SELECT 'X'
                                             FROM PS_HEALTH_BENEFIT X
                                            WHERE X.PLAN_TYPE = HB1.PLAN_TYPE
                                              AND X.EMPLID = HB1.EMPLID
                                              AND X.EMPL_RCD = HB1.EMPL_RCD
                                              AND X.COBRA_EVENT_ID = HB1.COBRA_EVENT_ID
                                              AND X.COVERAGE_ELECT <> 'E'
                                              AND X.EFFDT > HB1.EFFDT
                                              AND X.EFFDT < &A.EFFDT))        

ORDER BY HB.COVERAGE_BEGIN_DT DESC, HB.COVERAGE_ELECT ASC

End-SELECT

 if #Creditable_Days > 550
   Let #Creditable_Days = 550
   Let $Creditable_Msg  = '(18 months)'
 else
   Let $Creditable_Msg  = ' '
 end-if

End-Procedure

!**************************************************************************
!* Procedure Name: Get-Enrolled-Health-Plan-Info                          *
!**************************************************************************
! Note that we need to consider enrollments in all Benefit_Rcd_Nbrs (Empl_Rcd)
! since the certificate applies to an employee, not a specific Benefit_Rcd#
! for the employee. This is why the WHERE clause does not include a match
! on Empl_Rcd.
!
Begin-Procedure Get-Enrolled-Health-Plan-Info

Begin-Select
HB2.EMPLID
HB2.EMPL_RCD
HB2.BENEFIT_NBR
HB2.COBRA_EVENT_ID
HB2.EFFDT
HB2.COVERAGE_BEGIN_DT
HB2.PLAN_TYPE
HB2.BENEFIT_PLAN
HB2.COVRG_CD
HB2.COVERAGE_ELECT

                Let #Cobra_Event_Id = &HB2.COBRA_EVENT_ID

                Let $Benefit_Plan = rtrim(&HB2.BENEFIT_PLAN, ' ')

                Let $Covrg_cd = rtrim(&HB2.COVRG_CD, ' ')
                Do Get-Covrg-CD-Desc

                if $Status = 'E'

                  Do Get-Provider-Info
                  do Get-Waiting-Period
                end-if

 FROM PS_HEALTH_BENEFIT HB2
WHERE HB2.EMPLID            = &A.EMPLID
  AND HB2.EMPL_RCD          = &A.EMPL_RCD
  AND HB2.PLAN_TYPE         = &A.PLAN_TYPE
  AND HB2.COVERAGE_BEGIN_DT = (SELECT MAX(HB3.COVERAGE_BEGIN_DT)
                              FROM PS_HEALTH_BENEFIT HB3
                              WHERE HB3.EMPLID = &HB.EMPLID
                              AND HB3.EMPL_RCD = &HB.EMPL_RCD
                              AND (HB3.COVERAGE_BEGIN_DT < &A.COVERAGE_BEGIN_DT
                              OR HB3.EFFDT < &A.EFFDT)
                              AND HB3.PLAN_TYPE = &HB.PLAN_TYPE
                              AND NOT EXISTS (SELECT 'X'
                                             FROM PS_HEALTH_BENEFIT X1
                                            WHERE X1.PLAN_TYPE = HB3.PLAN_TYPE
                                              AND X1.EMPLID = HB3.EMPLID
                                              AND X1.EMPL_RCD = HB3.EMPL_RCD
                                              AND X1.COBRA_EVENT_ID = HB3.COBRA_EVENT_ID
                                              AND X1.COVERAGE_ELECT = 'E'
                                              AND X1.EFFDT > HB3.EFFDT
                                              AND X1.EFFDT < &A.EFFDT))           
ORDER BY HB2.COVERAGE_BEGIN_DT DESC, HB2.COVERAGE_ELECT ASC

End-SELECT

End-Procedure

!**************************************************************************
!* Procedure Name: Get-Covrg-CD-Desc                                      *
!**************************************************************************
Begin-Procedure Get-Covrg-CD-Desc

  Let $CvgCdDescr = ' '

Begin-SELECT

        Let $CvgCdDescr = ' '

X.DESCRSHORT

        Let $CvgCdDescr = &X.DESCRSHORT

FROM  PS_COVRG_CD_TBL X
WHERE X.COVRG_CD  = &HB2.COVRG_CD
  AND X.EFFDT = (SELECT MAX(X1.EFFDT)
                 FROM PS_COVRG_CD_TBL X1
                 WHERE X1.COVRG_CD    = X.COVRG_CD
                 AND   X1.EFFDT      <= &HB2.EFFDT
                 AND   X1.EFF_STATUS  = 'A')
End-SELECT
End-Procedure


!**************************************************************************
!* Procedure Name: Get-Provider-Info                                      *
!**************************************************************************
Begin-Procedure Get-Provider-Info

Begin-SELECT
M.DESCR              
   let $GrpPlan = &M.DESCR

VN.NAME1
  Let $Provider = rtrim(&VN.NAME1, ' ')

M.GROUP_NBR                 
   let $GrpNBR = &M.GROUP_NBR

FROM PS_BENEF_PLAN_TBL M,
     PS_VENDOR VN,
     PS_VENDOR_ADDR VA
WHERE M.PLAN_TYPE    = $Plan_Type
AND   M.BENEFIT_PLAN = $Benefit_Plan
AND   M.EFFDT =
        (SELECT MAX(M1.EFFDT)
           FROM PS_BENEF_PLAN_TBL M1
          WHERE M1.PLAN_TYPE     = M.PLAN_TYPE
            AND M1.BENEFIT_PLAN  = M.BENEFIT_PLAN
            AND M1.EFFDT        <= &HB2.EFFDT)
AND  VN.SETID     = M.SETID
AND  VN.VENDOR_ID = M.VENDOR_ID
AND  VA.SETID     = VN.SETID
AND  VA.VENDOR_ID = VN.VENDOR_ID
AND  VA.ADDRESS_SEQ_NUM = VN.REMIT_ADDR_SEQ_NUM
AND  VA.EFFDT =
        (SELECT MAX(EFFDT)
           FROM PS_VENDOR_ADDR
          WHERE SETID     = VA.SETID
            AND VENDOR_ID = VA.VENDOR_ID
            AND ADDRESS_SEQ_NUM = VA.ADDRESS_SEQ_NUM
            AND EFFDT    <= $AsOfToday)
End-SELECT

End-Procedure

begin-procedure Get-Waiting-Period

  move 0 to #waiting-days

begin-SELECT
HP.WAIT_PERIOD_DD

  let #waiting-days = &HP.WAIT_PERIOD_DD

FROM PS_BENEF_PLAN_TBL  BP,
     PS_HEALTH_PLAN_TBL HP
WHERE BP.PLAN_TYPE    = $Plan_Type
AND   BP.BENEFIT_PLAN = $Benefit_Plan
AND   BP.EFFDT =
        (SELECT MAX(BP1.EFFDT)
           FROM PS_BENEF_PLAN_TBL BP1
          WHERE BP1.PLAN_TYPE     = BP.PLAN_TYPE
            AND BP1.BENEFIT_PLAN  = BP.BENEFIT_PLAN
            AND BP1.EFFDT        <= &HB2.EFFDT)
AND  HP.PLAN_TYPE    = BP.PLAN_TYPE
AND  HP.BENEFIT_PLAN = BP.BENEFIT_PLAN
AND  HP.EFFDT  =
       (SELECT MAX(HP1.EFFDT)
          FROM PS_HEALTH_PLAN_TBL HP1
         WHERE HP1.PLAN_TYPE      = HP.PLAN_TYPE
           AND HP1.BENEFIT_PLAN   = HP.BENEFIT_PLAN
           AND HP1.EFFDT         <= &HB2.EFFDT)
end-SELECT

  let $WaitingDate = ''
  if $CovDate <> '' and #waiting-days <> 0

     Do Convert-To-DTU-Date($CovDate, $CvgDate_dtu)
     Do dtu-subtract-days($CvgDate_dtu, #waiting-days, $WaitDate_dtu)
     Do Convert-From-DTU-Date($WaitDate_dtu, $WaitDate)
     do Format-DateTime($WaitDate, $WaitingDate, {DEFDATE}, '', '')
  else
     let $WaitingDate = 'Not Applicable'
  end-if


end-procedure
!**************************************************************************
!* Procedure Name: Calc-Credit-Period                                      *
!**************************************************************************
Begin-Procedure Calc-Credit-Period

 Let #Span_Days = 0
 Do Convert-To-DTU-Date(&HB.COVERAGE_BEGIN_DT, $CvgDt_dtu)
 Do DTU-Diff-Days($CvgDt_Dtu, $LastCvgDt_dtu, #Span_Days)

 if ($LastStatus = 'T' or $LastStatus = 'W')
   if $Status = 'E'
     if $Freeze_Credit = 'N'
       Let #Creditable_Days = #Creditable_Days + #Span_Days
       Let #Creditable_Days = #Creditable_Days + #Gap_Days
     end-if
     if $LastStatus = 'W' and #TermWaiveCnt = 0
        Let #TermWaiveCnt = #TermWaiveCnt + 1
     end-if
     if $Freeze_Credit = 'Y'
        Let $Freeze_Credit ='N'
     end-if
     if $TermWaiveFreeze = 'Y'
        Let #Creditable_Days = #Creditable_Days + #Span_Days
        Let $TermWaiveFreeze = 'N'
     end-if
     Let #Gap_Days = 0
   else
     Let #Gap_Days = #Gap_Days + #Span_Days
     if $Status = 'W'
        Let #TermWaiveCnt = #TermWaiveCnt + 1
        if $LastStatus = 'T'
           if #TermWaiveCnt = 1
              if #Gap_Days > 62
                 Let $TermWaiveFreeze = 'Y'
              else
                 if #Gap_Days < 63
                    Let #Gap_Days = 0 
                 end-if
              end-if
           end-if
        end-if
     end-if
   end-if
 else
   if $Status = 'E'
     if $Freeze_Credit = 'N'
       Let #Creditable_Days = #Creditable_Days + #Span_Days
     end-if
     Let #Gap_Days = 0
   else
     Let #Gap_Days = #Gap_Days + #Span_Days
   end-if
 end-if
 if #Gap_Days > 62
   ! Coverage in effect prior to a significant break is not creditable.
   Let $Freeze_Credit = 'Y'
 end-if

End-Procedure


begin-procedure Maintain-History
  let $Insert-History = 'Y'
begin-SELECT
LEH.EMPLID
   let $Insert-History = 'N'
FROM PS_BEN_LETTER_HIST LEH
WHERE LEH.EMPLID          = &A.EMPLID
  AND LEH.DEPENDENT_BENEF = '00'
  AND LEH.LETTER_CD       = 'HEE'
  AND LEH.COVERAGE_END_DT = $ProcessCutOff
end-SELECT

  if $Insert-History = 'Y'
     do Add-Letter-History
  end-if
end-procedure

begin-procedure Add-Letter-History
Begin-SQL ON-ERROR=Insert-Error

INSERT INTO PS_BEN_LETTER_HIST
(EMPLID,DEPENDENT_BENEF,LETTER_CD,LETTER_PRINTED_DT,COVERAGE_END_DT)
VALUES (&A.EMPLID,'00','HEE',$AsOfToday,$ProcessCutOff)
End-SQL
end-procedure

begin-procedure Insert-Error

  display 'ERROR Inserting history:  ' noline
  display 'Emplid -> ' noline
  display &A.EMPLID

end-procedure

!***********************************
Begin-Procedure Print-Header-Instructions
!***********************************
  print ' '                     (+2)

  uppercase $ReportTitle
  print $ReportTitle            (+1,40) font = 7

  print $ReportDate             (+1,55)

  print '_'                     (+3,2,123)       fill

  let $v1 = 'IMPORTANT — KEEP THIS CERTIFICATE. '
  let $v1 = $v1 || 'This certificate is evidence of your coverage under this plan.  Under a federal'
  print $v1 (+1, {col_txt})

  let $v2 = 'law known as HIPAA, you may need evidence of your '
  let $v2 = $v2 || 'coverage to reduce a preexisting condition exclusion period '
  print $v2 (+1, {col_txt})

  let $v3 = 'under another plan, to help you get special enrollment '
  let $v3 = $v3 || 'in another plan, or to get certain types of individual health coverage '
  print $v3 (+1, {col_txt})

  let $v4 = 'even if you have health problems.'
  print $v4 (+1, {col_txt})

  print '_'                     (+1,2,123)       fill
end-procedure

!***********************************
Begin-Procedure Print-Employee-Data
!***********************************
  let $group_printed = 'N' 
  print 'Participant (Employee):'  (+2,{col_txt})
  print $EE_Name                   (0,+2) 

 
  print 'Participant ID: '         (+1, 28)
  print $SSN                       ( 0, +2)


  if $EE_Address1 <> ''
  print $EE_Address1               (+1, 28)

  end-if

  if $EE_Address2 <> ''
     print $EE_Address2         (+1, 28)
  end-if

  if $EE_Address3 <> ''
     print $EE_Address3         (+1, 28)
  end-if

  
  if $EE_Address <> ''
  print $EE_Address             (+1, 28)

  end-if

 
  print 'Plan Administrator:'  (+2, {col_txt} )
  print $Administrator          ( 0, 28)

  print 'Group Health Plan:'    (+0,71)
  print $GrpPlan                (+0,95)

  if $Admin_Address1 <> ''
  print $Admin_Address1         (+1, 28)

  print 'Plan Provider:'        (+0,71)
  print $Provider               (+0,95)
  else
  print 'Plan Provider:'        (+1,71)
  print $Provider               (+0,95)
  end-if

  if $Admin_Address2 <> ''
     print $Admin_Address2      (+1, 28)
     let $group_printed = 'E'
  end-if

  if $group_printed = 'E'
     print 'Group ID:'          (+0,71,{len_group})
     print $GrpNBR              (+0,95)
     let $group_printed = 'Y'
  end-if


  if $Admin_Address3 <> ''
     print $Admin_Address3      (+1, 28)
     if $group_printed <> 'Y'
     let $group_printed = 'E'
     end-if
  end-if


  if $group_printed = 'E'
     print 'Group ID:'          (+0,71,{len_group})
     print $GrpNBR              (+0,95)
     let $group_printed = 'Y'
  end-if

  if $Admin_CityState <> ''
     print $Admin_CityState     (+1, 28)
     if $group_printed <> 'Y'
     let $group_printed = 'E'
     end-if
  end-if 

  if $group_printed = 'E'
     print 'Group ID:'          (+0,71,{len_group})
     print $GrpNBR              (+0,95)
     let $group_printed = 'Y'
  end-if

  if $Admin_Phone <> ''
     print $Admin_Phone         (+1, 28)
     if $group_printed <> 'Y'
     let $group_printed = 'E'
     end-if
  end-if

  if $group_printed = 'E'
     print 'Group ID:'          (+0,71,{len_group})
     print $GrpNBR              (+0,95)
     let $group_printed = 'Y'
  end-if

  if $group_printed = 'N'
     print 'Group ID:'          (+1,71,{len_group})
     print $GrpNBR              (+0,95)
     let $group_printed = 'Y'
  end-if

 Let $GrpNBR = ''
 Let $GrpPlan = ''
 Let $Provider = ''

 Print 'Coverage Begin: '       (+2,{col_txt})
 Print $CvgDt_Began             (0,28)
 Print 'Waiting Period Began: ' (+0,71)
 Print $WaitingDate             (+0,95,16)
 Print 'Coverage Ended: '       (+1,{col_txt})
 Print $CvgDt_Ended             (0,28)
 Print 'Creditable Coverage: '  (+0,71,{len_dep_name})
 
  if #Creditable_Days > 999
  Print #Creditable_Days        (+0,95) Edit 9999
  end-if
  if #Creditable_Days > 99
        if #Creditable_Days < 999
        Print #Creditable_Days  (+0,95) Edit 999
        end-if
  end-if
  if #Creditable_Days > 9
        if #Creditable_Days < 99
        Print #Creditable_Days  (+0,95) Edit 99
        end-if
  end-if

  if #Creditable_Days < 9
    Print #Creditable_Days      (+0,95) Edit 9
  end-if
 Print 'Days'                   (+0,+1)
 Print $Creditable_Msg          (+0,+1)
 Print ' '                      (+1,1)
 print '_'                      (+1,2,123)       fill

end-procedure

!***********************************
Begin-Procedure Print-educational-instructions
!***********************************

  print 'Statement of HIPAA Portability Rights' (+3,) bold font = 15 center
  let $Tmp_Text = 'Preexisting condition exclusions' 
  print $Tmp_Text                                (+3,{col_txt}) bold underline 

  let $Tmp_Text = '. Some group health plans restrict coverage for medical conditions present before an individual’s '
  print $Tmp_Text                                (+0,)  
  let $Tmp_Text = 'enrollment. These restrictions are known as “preexisting condition exclusions.”  A preexisting condition exclusion can apply only'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'to conditions for which medical advice, diagnosis, care, or treatment was recommended or received within the 6 months before your'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '“enrollment date.”  Your enrollment date is your first day of coverage under the plan, or, if there is a waiting period, the first'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'day of your waiting period (typically, your first day of work).  In addition, a preexisting condition exclusion cannot last'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'for more than 12 months after your enrollment date (18 months if you are a late enrollee).   Finally, a preexisting condition'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'exclusion cannot apply to pregnancy and cannot apply to a child who is enrolled in health coverage within 30 days after birth,'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'adoption, or placement for adoption.'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'If a plan imposes a preexisting condition exclusion, the length of the exclusion must be reduced by the amount of your prior'
  print $Tmp_Text                                (+2,{col_txt}) 
  let $Tmp_Text = 'creditable coverage.  Most health coverage is creditable coverage, including group health plan coverage, COBRA continuation'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'coverage, coverage under an individual health policy, Medicare, Medicaid, State Children'
  let $Tmp_Text = $Tmp_Text || ''''
  let $Tmp_Text = $Tmp_Text || 's Health Insurance Program (SCHIP), and'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'coverage through high-risk pools and the Peace Corps. Not all forms of creditable coverage are required to provide certificates'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'like this one. If you do not receive a certificate for past coverage, talk to your new plan administrator.'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'You can add up any creditable coverage you have, including the coverage shown on this certificate.  However, if at any'
  print $Tmp_Text                                (+2,{col_txt}) 
  let $Tmp_Text = 'time you went for 63 days or more without any coverage (called a break in coverage) a plan may not have to count the coverage you'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'had before the break.'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, once your coverage ends, you should try to obtain alternative coverage as soon as possible to avoid a 63-day '  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'break.  You may use this certificate as evidence of your creditable coverage to reduce the length of any preexisting condition '  
  print $Tmp_Text                                (+1,6) 
  let $Tmp_Text = 'exclusion if you enroll in another plan.'  
  print $Tmp_Text                                (+1,6) 

  let $Tmp_Text = 'Right to get special enrollment in another plan'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. Under HIPAA, if you lose your group health plan coverage, you may be able to get'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'into another group health plan for which you are eligible (such as a spouse’s plan), even if the plan generally does not'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'accept late enrollees, if you request enrollment within 30 days.(Additional special enrollment rights are triggered by'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'marriage, birth, adoption, and placement for adoption.)'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, once your coverage ends, if you are eligible for coverage in another plan (such as a spouse’s plan), you should '  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'request special enrollment as soon as possible.'  
  print $Tmp_Text                                (+1,6) 

  let $Tmp_Text = 'Prohibition against discrimination based on a health factor'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. Under HIPAA, a group health plan may not keep you'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = '(or your dependents) out of the plan based on anything related to your health.  Also, a group health plan may not charge you'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '(or your dependents) more for coverage, based on health, than the amount charged a similarly situated individual.'
  print $Tmp_Text                                (+1,{col_txt}) 

new-page

  let $Tmp_Text = 'Right to individual health coverage'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. Under HIPAA, if you are an “eligible individual,” you have a right to buy certain'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'individual health policies (or in some states, to buy coverage through a high-risk pool) without a preexisting condition'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'exclusion.  To be an eligible individual, you must meet the following requirements:'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = '• You have had coverage for at least 18 months without a break in coverage of 63 days or more;'  
  print $Tmp_Text                                (+2,{col_txt})
  let $Tmp_Text = '• Your most recent coverage was under a group health plan (which can be shown by this certificate);'  
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '• Your group coverage was not terminated because of fraud or nonpayment of premiums;'  
  print $Tmp_Text                                (+1,{col_txt})
  let $Tmp_Text = '• You are not eligible for COBRA continuation coverage or you have exhausted your COBRA benefits (or continuation coverage '  
  print $Tmp_Text                                (+1,{col_txt})
  let $Tmp_Text = 'under a similar state provision); and'  
  print $Tmp_Text                                (+1,6)
  let $Tmp_Text = '• You are not eligible for another group health plan, Medicare, or Medicaid, and do not have any other health insurance coverage.'  
  print $Tmp_Text                                (+1,{col_txt})

  let $Tmp_Text = 'The right to buy individual coverage is the same whether you are laid off, fired, or quit your job. '  
  print $Tmp_Text                                (+2,{col_txt})
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, if you are interested in obtaining individual coverage and you meet the other criteria to be an eligible'  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'individual, you should apply for this coverage as soon as possible to avoid losing your eligible individual'
  print $Tmp_Text                                (+1,6) 
  let $Tmp_Text = 'status due to a 63-day break.'
  print $Tmp_Text                                (+1,6) 
  let $Tmp_Text = 'Special information for people on FMLA leave'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. If you are taking leave under the Family and Medical Leave Act (FMLA) and you drop '  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'health coverage during your leave, any days without health coverage while on FMLA leave will not count towards a 63-day break in '
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'coverage. In addition, if you do not return from leave, the 30-day period to request special enrollment in another plan will not'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'start before your FMLA leave ends.'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, when you apply for other health coverage, you should tell your plan administrator of health insurer about any'  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'prior FMLA leave.'  
  print $Tmp_Text                                (+1,6) 

  let $Tmp_Text = 'State flexibility'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. This certificate describes minimum HIPAA protections under federal law.  States may require insurers and HMOs'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'to provide additional protections to individuals in that state.'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'For more information'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. If you have questions about your HIPAA rights, you may contact your state insurance department or the U.S. '  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'Department of Labor, Employee Benefits Security Administration (EBSA) toll-free at 1-866-444-3272 (for free HIPAA publications'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'ask for publications concerning changes in health care laws).  You may also contact the CMS publication hotline at'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '1-800-633-4227 (ask for “Protecting Your Health Insurance Coverage”).  These publications and other useful information are also'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'available on the Internet at: '
  print $Tmp_Text                                (+1,{col_txt})  
  let $Tmp_Text = 'http://www.dol.gov/ebsa'
  print $Tmp_Text                                (+0,+1)  underline
  let $Tmp_Text = ', the DOL’s interactive web pages - Health Elaws, or '
  print $Tmp_Text                                (,+1)    
  let $Tmp_Text = 'http://www.cms.hhs.gov/hipaa1'
  print $Tmp_Text                                (+1,{col_txt}) underline
let $Tmp_Text = '.'
  print $Tmp_Text                                (,)
end-procedure


! *-----------------------------------------------------------------------*

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getaddr.sqc'   !Get current address for specified address type
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date AND time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'askftd.sqc'    !Asks the user to enter the from and thru dates
#Include 'bnrunctl.sqc'  !Get application run control parameters
#Include 'bngetval.sqc'  !Get BEN values mask routines
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !date math
#Include 'readxlat.sqc'  !Read xlate table

! *------------------------* End Of Report *------------------------------*
