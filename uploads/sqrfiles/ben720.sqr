!***********************************************************************
! BEN720: FMLA Plan Table Report
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!----------------------------------------------------------------------
!
!          $Date:  2006/07/19:08:52:08                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'  !Set environment

Begin-Setup
  #Include 'ptpsp125.sqc'
End-Setup

!**********************************
Begin-Program
!**********************************

  do Init-Report

  display 'Report Begin at: ' noline
  do display-time

  do Process-Main

  display 'Report Ended at: ' noline
  do display-time

  do StdAPI-Term

End-Program

!**********************************
Begin-Procedure Init-Report
!**********************************

  let $ReportID    = 'BEN720'
  let $ReportTitle = 'FMLA PLAN TABLE'

  display $ReportID    NoLine
  display ': '         NoLine
  display $ReportTitle
  display ''

  do Init-DateTime
  do Get-Current-DateTime
  let $ReportYear4 = '1'

  do StdAPI-Init

End-Procedure

!***************************************
Begin-Heading 8
!***************************************

#Include 'stdhdg01.sqc'     !Standard Report header

  print 'Plan  Effective                                           ' (+2,1)
  print '                    Annual    Svc  Work  '               ()
  print 'Hours    Spcl FMLA Hrs '                                 ()

  print 'ID    Date        Status  Description                     ' (+1,1)
  print 'Calendar Type       Entitlmt  Wks  Hrs   '               ()
  print 'Source   Accumulator   '                                 ()

  print '====  ==========  ======  ==============================  ' (+1,1)
  print '==================  ========  ===  ====  '               ()
  print '=======  =============='                                 ()

End-Heading

!***************************************
Begin-Procedure Process-Main
!***************************************

Begin-Select
F.FMLA_PLAN_ID               (+1,1,4)    On-Break Print=Change/Top-Page
                                                  Skiplines=1
F.EFFDT
  do Format-DateTime(&F.EFFDT, $Out, {DEFDATE}, '', '')
  print $Out                 (0,+2,10)
F.EFF_STATUS                 (0,+4,4)
F.DESCR                      (0,+2,30)
F.DESCRSHORT
F.FMLA_CALENDAR_TYPE
  move 'FMLA_CALENDAR_TYPE'  to $FieldName
  move &F.FMLA_CALENDAR_TYPE to $FieldValue
  do Read-Translate-Table
  print $XlatShortName       (0,+2,10)
F.CALENDAR_BEGIN_MM
F.CALENDAR_BEGIN_DD
  let $Out = ''
  if (&F.FMLA_CALENDAR_TYPE = 'C')
    let $Out = '('  || Edit(&F.CALENDAR_BEGIN_MM,'09') || '/'
    let $Out = $Out || Edit(&F.CALENDAR_BEGIN_DD,'09') || ')'
  end-if
  print $Out                 (0,+1,7)
F.ANN_ENTITLEMNT_WKS         (0,+2,2) Edit 99
  print 'weeks'              (0,+1,5)
F.MIN_SVC_WKS_REQD           (0,+2,3) Edit 999
F.MIN_HOURS_REQD             (0,+2,4) Edit 9999
F.STD_OR_PAY_HRS
  move 'STD_OR_PAY_HRS'  to $FieldName
  move &F.STD_OR_PAY_HRS to $FieldValue
  do Read-Translate-Table
  print $XlatShortName       (0,+2,7)
F.ERNCD_SPCL
  if (&F.STD_OR_PAY_HRS = 'P')
    do Get-Spcl-Earns-Name
    print &F.ERNCD_SPCL      (0,+2,3)
    print '-'                ()
    print &SP.DESCRSHORT     (0,0,10)
  end-if

FROM  PS_FMLA_PLAN_TBL F
ORDER BY F.FMLA_PLAN_ID ASC, F.EFFDT DESC
End-Select

  do Reset
  New-Page

End-Procedure

!*************************************
Begin-Procedure Get-Spcl-Earns-Name
!*************************************

Begin-Select
SP.EFF_STATUS
SP.DESCRSHORT
FROM PS_SPCL_EARNS_TBL SP
WHERE SP.ERNCD_SPCL = &F.ERNCD_SPCL
AND   SP.EFFDT =
        (SELECT MAX(X.EFFDT)
         FROM   PS_SPCL_EARNS_TBL X
         WHERE  X.ERNCD_SPCL = SP.ERNCD_SPCL
         AND    X.EFFDT     <= &F.EFFDT)
End-Select
End-Procedure

!*************************************
Begin-Procedure Display-Time
!*************************************

  do Get-Current-DateTime
  display $AsOfToday NoLine
  display ', '       NoLine
  display $AsOfNow
  display ''

End-Procedure

!*************************************

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'readxlat.sqc'  !Read the Translate Table
#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API

!****************************** End Of Report *******************************

