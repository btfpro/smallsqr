!*************************************
!  PER019UK:  Terminations Analysis  *
!*************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:30:43                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
Begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-heading 9
  #Include 'stdhdg08.sqc'
  print $SubHead1                                               (+2,1)
  print '  Current   % of     Terminated  % of     Termination' (+2,21)
  print $SubHead2                                               (+1,1,20)
  print 'Population  Total    Population  Total        Rate'    ()
end-heading

begin-procedure Init-Report

  move 'PER019UK' to $ReportID
  move 'Terminations Analysis' to $ReportTitle
  display $ReportTitle

  do Init-Array

  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-From-Thru-Date
  else
    do Select-Parameters
  end-if

end-procedure

begin-procedure Get-Values

   do Get-From-Thru-Date

end-procedure

begin-procedure Init-Array
  create-array name=Counts size=33 field=Count:number:2 field=Label:char
  create-array name=Totals size=3  field=Total:number:2
  put '   Less than 21'  into Counts(0)  Label
  put '          21-25'  into Counts(1)  Label
  put '          26-30'  into Counts(2)  Label
  put '          31-35'  into Counts(3)  Label
  put '          36-40'  into Counts(4)  Label
  put '          41-45'  into Counts(5)  Label
  put '          46-50'  into Counts(6)  Label
  put '          51-55'  into Counts(7)  Label
  put '          56-60'  into Counts(8)  Label
  put '          61-65'  into Counts(9)  Label
  put 'Greater than 65'  into Counts(10) Label

  put '    Less than 1'  into Counts(11) Label
  put '              1'  into Counts(12) Label
  put '              2'  into Counts(13) Label
  put '           3- 5'  into Counts(14) Label
  put '           6-10'  into Counts(15) Label
  put '          11-15'  into Counts(16) Label
  put 'Greater than 15'  into Counts(17) Label

  put ' M    Eur-UK'     into Counts(21) Label
  put ' M    Eur-Other'  into Counts(22) Label
  put ' M    African'    into Counts(23) Label
  put ' M    Afro-Carib' into Counts(24) Label
  put ' M    Asian'      into Counts(25) Label
  put ' M    Other'      into Counts(26) Label
  put ' F    Eur-UK'     into Counts(27) Label
  put ' F    Eur-Other'  into Counts(28) Label
  put ' F    African'    into Counts(29) Label
  put ' F    Afro-Carib' into Counts(30) Label
  put ' F    Asian'      into Counts(31) Label
  put ' F    Other'      into Counts(32) Label

end-procedure

begin-procedure Process-Main

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($ThruDate, $Thru_YMD, {DEFYMD}, '', '')
  do Retrieve-Rows
  do Create-Report

end-procedure

begin-procedure Retrieve-Rows
begin-SELECT
A.SEX
DVR.ETHNIC_GRP_CD_UK
A.BIRTHDATE
B.HIRE_DT

  if not IsNULL(&A.BirthDate)
    move '1' to $Year4
    move '-' to $DDelimiter
    do Format-DateTime(&A.BirthDate, $Birth_YMD, {DEFYMD}, '', '')
    do Dtu-Diff-Years($Birth_YMD, $Thru_YMD, #Age)
  end-if

  if not IsNULL(&B.Hire_Dt)
    move '1' to $Year4
    move '-' to $DDelimiter
    do Format-DateTime(&B.Hire_Dt, $Hire_YMD, {DEFYMD}, '', '')
    do Dtu-Diff-Years($Hire_YMD, $Thru_YMD, #Service)
  end-if

  move &A.Sex                   to $Sex
  move &DVR.Ethnic_Grp_cd_uk    to $EthnicGroup
  move &A.BirthDate             to $BirthDate
  move 0                        to #col

  do Analyze-Row

FROM  PS_PERSONAL_DT_FST A,
      PS_EMPLOYMENT B,
      PS_DIVERSITY DVR
WHERE A.EMPLID = B.EMPLID
  AND DVR.EMPLID = A.EMPLID

end-SELECT

begin-SELECT
AA.EMPLID () on-break print=never procedure=Clear-Duplicates
AA.SEX,
DVRR.ETHNIC_GRP_CD_UK,
AA.BIRTHDATE
BB.HIRE_DT
CC.EFFDT

  if (#dup_term = 0)

    let #dup_term = 1

    move '1' to $Year4
    move '-' to $DDelimiter
    do Format-DateTime(&CC.EffDt, $Eff_YMD, {DEFYMD}, '', '')

    if not IsNULL(&AA.BirthDate)
      move '1' to $Year4
      move '-' to $DDelimiter
      do Format-DateTime(&AA.BirthDate, $Birth_YMD, {DEFYMD}, '', '')
      do Dtu-Diff-Years($Birth_YMD, $Eff_YMD, #Age)
    end-if

    if not IsNULL(&BB.Hire_Dt)
      move '1' to $Year4
      move '-' to $DDelimiter
      do Format-DateTime(&BB.Hire_Dt, $Hire_YMD, {DEFYMD}, '', '')
      do Dtu-Diff-Years($Hire_YMD, $Eff_YMD, #Service)
    end-if

    move &AA.Sex                to $Sex
    move &DVRR.Ethnic_Grp_cd_uk to $EthnicGroup
    move &AA.BirthDate          to $BirthDate
    move 1                      to #col

    do Analyze-Row

  end-if

FROM  PS_PERSONAL_DT_FST AA,
      PS_EMPLOYMENT BB,
      PS_JOB CC,
      PS_DIVERSITY DVRR
WHERE AA.EMPLID = BB.EMPLID
  AND BB.PER_ORG = 'EMP'
  AND CC.EMPL_STATUS NOT IN ('X','V')
  AND BB.EMPLID = CC.EMPLID
  AND DVRR.EMPLID = AA.EMPLID
  AND BB.EMPL_RCD = CC.EMPL_RCD
  AND CC.EFFDT BETWEEN $FromDate AND $ThruDate
  AND CC.ACTION IN ('TER','TWP','TWB')
order by AA.EMPLID,CC.EFFDT
end-SELECT
end-procedure

!***********************************************************************
!  PROC  Clear-Duplicates
!***********************************************************************
begin-procedure Clear-Duplicates
  let #dup_term = 0
end-procedure

!***********************************************************************
!  PROC Analyze Row
!***********************************************************************
begin-procedure Analyze-Row
  if not IsNULL($BirthDate)
    if #Age > 65
      move 10 to #row
    else
       if #Age < 16
         move 0 to #row
    else
      subtract 16 from #Age
      divide 5 into #Age
      move #Age to #row
       end-if
    end-if
    array-add 1 to Counts(#row) Count(#col)
    array-add 1 to Totals(0)    Total(#col)
  end-if

  if #Service >= 16
    array-add 1 to Counts(17) count(#col)
  else
  if #Service >= 11
    array-add 1 to Counts(16) count(#col)
  else
  if #Service >= 6
    array-add 1 to Counts(15) count(#col)
  else
  if #Service >= 3
    array-add 1 to Counts(14) count(#col)
  else
  if #Service >= 2
    array-add 1 to Counts(13) count(#col)
  else
  if #Service >= 1
    array-add 1 to Counts(12) count(#col)
  else
    array-add 1 to Counts(11) count(#col)
  end-if
  end-if
  end-if
  end-if
  end-if
  end-if
  array-add 1 to Totals(1) Total(#col)

  do Analyze-Sex-EthnicGroup

  move #EEOcol to #row
  add 20 to #row
  array-add 1 to Counts(#row) Count(#col)
  array-add 1 to Totals(2)    Total(#col)
end-procedure

!***********************************************************************
!  PROC Create-Report
!***********************************************************************
begin-procedure Create-Report
  move 'By Age'         to $SubHead1
  move '           Age' to $SubHead2
  move 0  to #category
  move 0  to #startRow
  move 10 to #endRow
  do Print-Category
  move 'By Years of Service' to $SubHead1
  move 'Years of Service'    to $SubHead2
  move 1  to #category
  move 11 to #startRow
  move 17 to #endRow
  do Print-Category
  move 'By Sex and Ethnic Group' to $SubHead1
  move 'Sex  Ethnic Group'       to $SubHead2
  move 2  to #category
  move 21 to #startRow
  move 32 to #endRow
  do Print-Category
end-procedure

begin-procedure Print-Category
  get #TotalActive from Totals(#category) Total(0)
  get #TotalTerm   from Totals(#category) Total(1)
  move #startRow to #i
  while #i <= #endRow
    get $LineName from Counts(#i) Label
    get #Active   from Counts(#i) Count(0)
    get #Term     from Counts(#i) Count(1)
    print $LineName (+1,1)
    do Format-Number(#Active, $out, '999,999')
    print $out   (0,22)
    do Format-Number(#Term, $out, '999,999')
    print $out     (0,43)
    multiply 100 times #Term
    move #Term to #TermRate
    divide #Active into #TermRate on-error=zero
    multiply 100 times #Active
    divide #TotalActive into #Active on-error=zero
    divide #TotalTerm   into #Term   on-error=zero
    do Format-Number(#TermRate, $out, '999.9')
    print $out (0,66)
    do Format-Number(#Active, $out, '999.9')
    print $out   (0,33)
    do Format-Number(#Term, $out, '999.9')
    print $out     (0,54)
    add 1 to #i
  end-while
  new-page
end-procedure

#include 'stdapi.sqc'    !Routine to update run status
#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askftd.sqc'    !From Thru Date input

#Include 'eeogrpuk.sqc'  !Analyze-Sex-EthnicGroup procedure
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
