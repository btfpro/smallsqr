!***************************************************
!  PER538:  Accomplishment Listing                 *
!***************************************************
!***********************************************************************
!                                                                      *
!  Confidentiality Information:                                        *
!                                                                      *
!  This module contains confidential and proprietary information       *
!  of Oracle; it is not to be copied, reproduced, or transmitted       *
!  in any form, by any means, in whole or in part, nor is it to        *
!  be used for any purpose other than that for which it is             *
!  expressly provided under the applicable license agreement.          *
!                                                                      *
!  Copyright (C) 2006 Oracle. All Rights Reserved.                     *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:15:09:06                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
Begin-Setup
#Include 'ptpsp125.sqc'  !Printer and page-size initialization
End-Setup

#define col1   1         !Category
#define col2   12        !Accomplishment
#define col3   19        !Accomplishment-descr
#define col4   60        !Education Level
#define col5   90        !Country
#define col6   60        !Renewal Length
#define col7   65        !Renewal Period

#define ACCOMP_TBL
#define COUNTRY_TBL

#include 'rellang.sqc'   ! Translations File    

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-heading 7
  do Get_Field_Information ('PER538',   'REPORT_TITLE' ,        $REPORTTITLE,           #DW)
  #Include 'stdhdgtr.sqc'
  do Get_Field_Information ('PER538',   'ACCOMP_CATEGORY',      $ACCOMP_CAT,            #DW)
  do Get_Field_Information ('PER538',   'ACCOMPLISHMENT',       $ACCOMPLISHMENT,        #DW)


  PRINT $ACCOMP_CAT             (6,{col1})
  print $ACCOMPLISHMENT       (+1,{col2})

end-heading

!---------------------------------------------------------------------------
begin-procedure Init-Report

  move 'PER538' to $ReportID

  do Stdapi-Init

  if $prcs_process_instance = ''
  else
    do Select-Parameters
  end-if
do Init_Printer
do Init_Report_Translation($ReportID, $language_cd)
do Append_Report_Translation ('HR')

  do Get_Field_Information ('PER538',   'EDUCATION_LVL',        $EDUCATION_LVL,   #DW)
  do Get_Field_Information ('PER538',   'COUNTRY',              $COUNTRY,               #DW)
  do Get_Field_Information ('PER538',   'RENEWAL_PERIOD',   $RENEWAL_PERIOD,    #DW)

end-procedure

!---------------------------------------------------------------------------
begin-procedure Get-Values
   let $language_cd = $prcs_language_cd
   let $DEGREE_SW  = &RUN_CNTL_HR.DEGREE_SW
   let $LICENSE_CERTIF_SW  = &RUN_CNTL_HR.LICENSE_CERTIF_SW

 LET $curr_language_cd = $language_cd 

end-procedure

!---------------------------------------------------------------------------
begin-procedure Process-Main


begin-SELECT
A.ACCOMP_CATEGORY
COUNT(*) &CAT_NBR

 LET $ACCOMP_CATEGORY = &A.ACCOMP_CATEGORY 
 IF &CAT_NBR > 0
    DO PRINT-CATEG
    DO PRINT-ACCOMP
 END-IF

FROM PS_ACCOMP_TBL A WHERE A.ACCOMP_CATEGORY <> 'NVQ'
GROUP BY A.ACCOMP_CATEGORY
END-SELECT
end-procedure

!---------------------------------------------------------------------------
begin-procedure PRINT-CATEG
LET $FieldName = 'ACCOMP_CATEGORY'  
LET $FieldValue = $ACCOMP_CATEGORY 

DO Read-Translate-Table

PRINT $XlatLongName (+3,1,58)

EVALUATE $ACCOMP_CATEGORY 
    WHEN='DEG'
        IF $DEGREE_SW = 'Y'
            print $EDUCATION_LVL                (,{col4},29)
            print $COUNTRY                    (,{col5})
          END-IF
    WHEN='LIC'
        IF $LICENSE_CERTIF_SW = 'Y'
            print $RENEWAL_PERIOD               (,{col6})
        END-IF
    END-EVALUATE 

PRINT '' (+1,)

end-procedure

!---------------------------------------------------------------------------
begin-procedure PRINT-ACCOMP

BEGIN-SELECT 
A1.ACCOMPLISHMENT
A1.DESCR
A1.EDUCATION_LVL
A1.COUNTRY
A1.RENEWAL_PERIOD
A1.RENEWAL_LENGTH
!************************Begin Resolution 306010*****************

   !PRINT &A1.ACCOMPLISHMENT (+1,{col2},6)
   PRINT &A1.ACCOMPLISHMENT (+1,{col2},8)
!************************End Resolution 306010*******************
   LET $ACCOMP_TBL-DESCR = &A1.DESCR
   DO Get_Related_ACCOMP_TBL(&A1.ACCOMPLISHMENT)
!************************Begin Resolution 306010*****************
   !PRINT $ACCOMP_TBL-DESCR (,19,40)
   PRINT $ACCOMP_TBL-DESCR (,21,40)
!************************End Resolution 306010*******************
    EVALUATE $ACCOMP_CATEGORY 
             WHEN='DEG'
                  IF $DEGREE_SW = 'Y'
                          LET $FieldName = 'EDUCATION_LVL'
                          LET $FieldValue = &A1.EDUCATION_LVL
                          DO Read-Translate-Table
                                 print $XlatLongName (,{col4},29)
                          DO COUNTRY-DESCR
                          DO Get_Related_COUNTRY_TBL(&A1.COUNTRY)
                          print $COUNTRY_TBL-DESCR (,{col5})
                   END-IF
             WHEN='LIC'
                  IF $LICENSE_CERTIF_SW = 'Y'
                          LET $out = ' '
                          IF &A1.RENEWAL_LENGTH > 0
                             do Format-Number(&A1.RENEWAL_LENGTH, $out, '999')
                          END-IF
                          print $out (,{col6},4)
                          LET $FieldName = 'RENEWAL_PERIOD'
                          LET $FieldValue = &A1.RENEWAL_PERIOD
                          DO Read-Translate-Table
                          print $XlatShortName (,{col7})
                   END-IF
    END-EVALUATE 

FROM PS_ACCOMP_TBL A1
WHERE A1.ACCOMP_CATEGORY = $ACCOMP_CATEGORY 
ORDER BY A1.ACCOMPLISHMENT
END-SELECT
end-procedure

!---------------------------------------------------------------------------
begin-procedure COUNTRY-DESCR
 LET $COUNTRY_TBL-DESCR = ' '
BEGIN-SELECT 
C.DESCR
 LET $COUNTRY_TBL-DESCR = &C.DESCR
FROM PS_COUNTRY_TBL C
WHERE C.COUNTRY = &A1.COUNTRY
END-SELECT
end-procedure

!---------------------------------------------------------------------------
#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to update run status
#Include 'readxlat.sqc'  !Read the Translate Table
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
