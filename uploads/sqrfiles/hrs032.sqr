
!***********************************************************
!  FGHR032:  Full Text Vacancy Announcement                *
!***********************************************************

!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/09/25:17:39:22                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************
!
!
!***********************************************************************
!This program accomplishes the following:
!  1.  Retrieves job posting information for requisition selected.
!  2.  Creates a full text vacancy announcement based on information
!      placed in the requisition job posting pages.
!***********************************************************************

#Include 'setenv.sqc'  !Set Database Environment
#Include 'setup01.sqc' !printer and page-size initialization

!Override max lines and page size parameters from setup01a.sqc
begin-setup
    #define PAGE_MAX_LINES   65
    page-size 65 125
end-setup

begin-report
  do Init-Report
  do Process-Main
  do StdAPI-Term
end-report

!*************************************************
begin-procedure Init-Report
!*************************************************

  move 'HRS032' to $ReportID
  move 'Federal Vacancy Announcement' to $ReportTitle
  display $ReportTitle
  display ''

  do Init-Number        !number.sqc
  do Init-DateTime      !datetime.sqc
  do StdAPI-Init        !


  if  $prcs_process_instance  =  ' '
      !*** input prompts if no run control found

      input #JOB_REQ_NBR 'Enter Job Req Number' type=number maxlen=15
      input #GVT_VA_TEMPLATE 'Enter appropriate Vacancy Template Code' type=number maxlen=15

      display  '  '
      display  'Begin Run   ---   HRS032.SQR'  noline
      display  '  '
  else
      do  Select-Parameters
      let $RUNDATE = &RUN_CNTL_FG.RUNDATE

      display  '  '
      display  'Begin Run   ---   HRS032.SQR   '  noline
      display  $RUNDATE
      display  '  '
  end-if

end-procedure


!*************************************************
begin-procedure Process-Main
!*************************************************

   let #filenumber = 0

   let $record_found = 'N'


begin-Select distinct
GFV.HRS_JOB_OPENING_ID
GFV.HRS_PST_TMPL_ID

   let $record_found = 'Y'

   let #JOB_REQ_NBR = &GFV.HRS_JOB_OPENING_ID
   let #GVT_VA_TEMPLATE = &GFV.HRS_PST_TMPL_ID

   Do Get-Vacancy-Data

from PS_HRS_RUN_FGHR032 GFV
where GFV.OPRID = $Prcs_OprID
and GFV.RUN_CNTL_ID = $Prcs_Run_Cntl_ID

end-Select

end-procedure

!*************************************************
begin-procedure Get-Vacancy-Data
!*************************************************

begin-SELECT
GJP.HRS_JOB_OPENING_ID
JR.HRS_G_JO_TYPE
JR.HRS_CONTACT_ID
GJP.GVT_VA_NBR
PSN.DESCR
RQM.HRS_JO_MIN_RT
RQM.HRS_JO_MAX_RT
RQM.GRADE_FROM
RQM.GRADE_TO
DST.HRS_JO_PST_OPN_DT
DST.HRS_JO_PST_CLS_DT
JR.DEPTID
JR.RECRUITER_ID


   let $GVT_VA_NBR = &GJP.GVT_VA_NBR

   do Get-Contact-Details
   do Get-Fax-Number
   do Get-Email-Address
   do Format-DateTime(&DST.HRS_JO_PST_OPN_DT, $GVT_ANN_OPENING_DT, {DEFMDY}, '', '')
   do Format-Full-Date($GVT_ANN_OPENING_DT)
  
  
  
   do Format-DateTime(&DST.HRS_JO_PST_CLS_DT, $GVT_ANN_CLOSING_DT, {DEFMDY}, '', '')
   do Format-Full-Date($GVT_ANN_CLOSING_DT)

   do Get-CPDF-And-Agency-Info

   do Print-Header-Information

   do Get-Full-Vacancy-Text

   do Print-Ending-Information

FROM PS_HRS_JO_POSTING GJP, PS_HRS_JO_I JR, PS_HRS_JO_JOBCD_I GRJ  
 , PS_HRS_POS_DTA_C_I PSN, PS_HRS_JO_RQMT RQM, 
 PS_HRS_JO_PST_DST DST
WHERE GJP.HRS_JOB_OPENING_ID = JR.HRS_JOB_OPENING_ID
AND JR.HRS_JOB_OPENING_ID = GRJ.HRS_JOB_OPENING_ID
AND JR.POSITION_NBR = PSN.POSITION_NBR
AND JR.HRS_JOB_OPENING_ID = RQM.HRS_JOB_OPENING_ID
AND GJP.HRS_JOB_OPENING_ID = DST.HRS_JOB_OPENING_ID
AND GJP.HRS_JO_PST_SEQ = DST.HRS_JO_PST_SEQ
AND DST.HRS_SEQ_NUM = (SELECT MAX(HRS_SEQ_NUM) FROM PS_HRS_JO_PST_DST DST1
  WHERE DST.HRS_JOB_OPENING_ID = DST1.HRS_JOB_OPENING_ID
  AND DST.HRS_JO_PST_SEQ = DST1.HRS_JO_PST_SEQ)
AND GJP.HRS_JOB_OPENING_ID = #JOB_REQ_NBR
AND GJP.HRS_JO_PST_SEQ = (SELECT MAX(HRS_JO_PST_SEQ) FROM PS_HRS_JO_POSTING
  WHERE GJP.HRS_JOB_OPENING_ID = HRS_JOB_OPENING_ID)
AND GRJ.HRS_JO_PRM_JCD_FLG = 'Y'

end-SELECT
end-procedure

!*************************************************
begin-procedure Format-Full-Date(:$date_to_convert)
!*************************************************
   let $mm = substr($date_to_convert, 1, 2)
   let $dd = substr($date_to_convert, 4, 2)
   let $yyyy = substr($date_to_convert, 7, 4)

 evaluate $mm
   when = '01'
    let $month = 'January'
    break
   when = '02'
    let $month = 'February'
    break
   when = '03'
    let $month = 'March'
    break
   when = '04'
    let $month = 'April'
    break
   when = '05'
    let $month = 'May'
    break
   when = '06'
    let $month = 'June'
    break
   when = '07'
    let $month = 'July'
    break
   when = '08'
    let $month = 'August'
    break
   when = '09'
    let $month = 'September'
    break
   when = '10'
    let $month = 'October'
    break
   when = '11'
    let $month = 'November'
    break
   when = '12'
    let $month = 'December'
    break
   when-other
    break
  end-evaluate

  let #dd = to_number($dd)
  let $dd = to_char(#dd)

  let $date_to_convert = $month || ' ' || $dd || ', ' || $YYYY

end-procedure

!*************************************************
begin-procedure Get-CPDF-And-Agency-Info
!*************************************************
begin-SELECT
DT.COMPANY
DT.GVT_SUB_AGENCY
   LET $COMPANY = substr(&DT.COMPANY, 1, 2)
   do Get-Agency-Info
   do Get-SubAgency-Info
   let $CPDF = $COMPANY || &GSA.GVT_SUB_AGENCY

FROM PS_DEPT_TBL DT
WHERE DT.DEPTID = &JR.DEPTID
end-SELECT

end-procedure

!*************************************************
begin-procedure Get-Contact-Details
!*************************************************
begin-SELECT
PHN.PHONE

  let $PHONE_NUM = &PHN.PHONE

from PS_HRS_CONTACT_PHN PHN
where PHN.HRS_CONTACT_ID = &JR.HRS_CONTACT_ID
AND PHN.PHONE_TYPE = 'BUSN'
end-SELECT
end-procedure

!*************************************************
begin-procedure Get-Fax-Number
!*************************************************
begin-SELECT
FAX.PHONE

  let $FAX_NUM = &FAX.PHONE

from PS_HRS_CONTACT_PHN FAX
where FAX.HRS_CONTACT_ID = &JR.HRS_CONTACT_ID
AND FAX.PHONE_TYPE = 'FAX'
end-SELECT
end-procedure

!*************************************************
begin-procedure Get-Email-Address
!*************************************************
begin-SELECT
EML.HRS_CONTACT_NAME
EML.EMAIL_ADDRESS

  let $EMAIL_ADDRESS = &EML.EMAIL_ADDRESS
  let $HRS_CONTACT_NAME = &EML.HRS_CONTACT_NAME

from PS_HRS_CONTACT_I EML
where EML.HRS_CONTACT_ID = &JR.HRS_CONTACT_ID
end-SELECT
end-procedure

!*************************************************
begin-procedure Get-Agency-Info
!*************************************************
begin-SELECT
CT.DESCR
CT.ADDRESS1
CT.ADDRESS2
CT.ADDRESS3
CT.CITY
CT.STATE
CT.POSTAL

  let $COMPANY_NAME = rtrim(&CT.DESCR, ' ')

from PS_COMPANY_TBL CT
where CT.COMPANY = &DT.COMPANY
end-SELECT
end-procedure
!*************************************************
begin-procedure Get-SubAgency-Info
!*************************************************
begin-SELECT
GSA.GVT_SUB_AGENCY
GSA.GVT_DESCR70

   let $GVT_SUBAGENCY_DESCR = rtrim(&GSA.GVT_DESCR70, ' ')

from PS_GVT_SUBAGCY_TBL GSA
where GSA.COMPANY = &DT.COMPANY
end-SELECT

end-procedure

!*************************************************
begin-procedure Get-Full-Vacancy-Text
!*************************************************
begin-SELECT
GFS.HRS_JO_DESCR_TYP
GFS.HRS_SEQ_NUM

   do Get-Section-Descriptor
   do Concatenate-Sections
   do Print-Full-Text

FROM PS_HRS_JO_PST_SCTN GFS
WHERE GFS.HRS_PST_TMPL_ID = #GVT_VA_TEMPLATE
AND GFS.SELECTED = 'Y'
ORDER BY HRS_SEQ_NUM

end-SELECT

end-procedure

!*************************************************
begin-procedure Get-Section-Descriptor
!*************************************************
   let $FldName = 'HRS_JO_DESCR_TYP'
   let $Language_cd = 'ENG'
   let $FldVal = &GFS.HRS_JO_DESCR_TYP

   #debugA show $FldName
   #debugA show $Language_cd
   #debugA show $FldVal
   #debugA show $AsOfDate

begin-SELECT
XLT.FIELDNAME
XLT.FIELDVALUE

  let $FieldName = &XLT.FIELDNAME
  let $FieldValue = &XLT.FIELDVALUE
  do Read-Translate-Table
  move $XlatShortName  to $ShortName
  move $XlatLongName  to $LongName
FROM PSXLATITEM XLT
WHERE XLT.EFFDT =
    (SELECT MAX(XLT2.EFFDT) FROM PSXLATITEM XLT2
     WHERE XLT.FIELDNAME = XLT2.FIELDNAME
      AND XLT.FIELDVALUE = XLT2.FIELDVALUE
      AND XLT2.EFFDT <= $AsOfToday)
AND XLT.EFF_STATUS = 'A'
AND XLT.FIELDNAME = 'HRS_JO_DESCR_TYP'
AND XLT.FIELDVALUE = &GFS.HRS_JO_DESCR_TYP
end-SELECT

   let $section_descr = RTRIM($LongName, ' ') || ':'
   #debugA show '$section_descr = ' $section_descr

end-procedure

!*************************************************
begin-procedure Concatenate-Sections
!*************************************************

 let $section_text = ''

begin-SELECT
GRD.DESCRLONG

  if $section_text = ''
    let $section_text = rtrim(&GRD.DESCRLONG, ' ')
  else
    let $section_text = $section_text  || '  ' || rtrim(&GRD.DESCRLONG, ' ')
  end-if

from PS_HRS_JO_PST_DSCR GRD
where HRS_JOB_OPENING_ID = #JOB_REQ_NBR
and HRS_JO_DESCR_TYP = &GFS.HRS_JO_DESCR_TYP

end-SELECT

   let #len_text = length($section_text)
   divide 75 into #len_text round=0
   let #max_line = #len_text
   let #max_line = #max_line + 2

   #debugA show '$section_text = ' $section_text
   #debugA show '#len_text = ' #len_text
   #debugA show '#max_line = ' #max_line

end-procedure

!*************************************************
begin-procedure Print-Header-Information
!*************************************************

print '==== BEGIN FULL VACANCY ==============' (1,1)
print $COMPANY_NAME (+2, 1)
print ', ' (, +1)
print $GVT_SUBAGENCY_DESCR (, +2)
print 'Job Opportunity Announcement: ' (+2,1)
print &GJP.GVT_VA_NBR (, +2)
print 'Opening Date: ' (+2, 1)
print $GVT_ANN_OPENING_DT (, +2)
print 'Closing Date: ' (+2, 1)
print $GVT_ANN_CLOSING_DT (, +2)

end-procedure

!*************************************************
begin-Procedure Print-Full-Text
!*************************************************

print $section_descr (+3, 1)
print $section_text (+2, 1) wrap 75   #max_line strip=<13><10>
print '       ' (+, 1)

end-procedure

!*************************************************
begin-Procedure Print-Ending-Information
!*************************************************

print '====== END FULL VACANCY ==============' (+2,1)


end-procedure

!*************************************************
#Include 'fgrunctl.sqc'  !Get run control parameter values
#Include 'fggetval.sqc'  !Get values mask routine
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedre
#Include 'reset1.sqc'    !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
!***************************** End of Report ***************************
