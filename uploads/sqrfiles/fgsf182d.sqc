!******************************************************
! FGSF182D: This is the Standard Form 182 Training Req*
!******************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:12:17:20                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

begin-procedure Get-SF182-Data
  do Get-Employee-Data

  move &TRN.GVT_TRNG_REQ_DATE to $AsOfDate
  move &EJ.REPORTS_TO to $InfoOnPos

  do Get-Position-Info
  move &J1.SETID_JOBCODE to $SETID
  move &J1.JOBCODE to $JobCode
  do Get-Job-Title

  move &P1.NAME to $Sup1stName
  !Begin Resolution 574140
  move &E1.POSITION_PHONE to $Sup1stPhone  
  !move &E1.PHONE to $Sup1stPhone 
  !End Resolution 574140
  move $JobTitle to $Sup1stJobTitle

  move &J1.REPORTS_TO to $InfoOnPos
  do Get-Position-Info

  move &J1.JOBCODE to $JobCode
  do Get-Job-Title

  move &P1.NAME to $Sup2ndName
  !Begin Resolution 574140
  !move &E1.PHONE to $Sup2ndPhone  
  move &E1.POSITION_PHONE to $Sup2ndPhone  
  !End Resolution 574140
  move $JobTitle to $Sup2ndJobTitle

end-procedure

Begin-Procedure Get-Employee-Data
!------------------------------
!get information from training!
!------------------------------
move 0 to #FoundTrainingData
begin-select
TRN.EMPLID
TRN.COURSE_START_DT
TRN.COURSE
TRN.SESSION_NBR
TRN.COURSE_TITLE
TRN.COURSE_END_DT
TRN.INTERNAL_EXTERNAL
TRN.SCHOOL_CODE
TRN.SCHOOL
TRN.DT_NEEDED
TRN.ATTENDANCE
TRN.STATUS_DT
TRN.WAIT_DT
TRN.TRAINING_REASON
TRN.COURSE_GRADE
TRN.LETTER_CD
TRN.LETTER_PRINTED_DT
TRN.PREREQ_MET
TRN.DEPTID
TRN.DEMAND_ID
TRN.GVT_TREQ_DATA_REQ
TRN.GVT_TRNG_REQNUM
TRN.GVT_TREQ_REQ_SEQ
TRN.GVT_TRNG_REQ_DATE
TRN.GVT_TRNG_OBJS
TRN.GVT_DUTY_HRS
TRN.GVT_NONDUTYHRS
TRN.GVT_TC_PURPOSE
TRN.GVT_TC_TYPE
TRN.GVT_TC_SOURCE
TRN.GVT_TC_SPECIALINT
TRN.GVT_PURCHASEORDER
TRN.GVT_STATION_SYMBOL
TRN.GVT_BILLING_INSTR
TRN.GVT_NONGOVTTRNDAYS
TRN.GVT_DC_APPRFUND
TRN.GVT_IC_APPRFUND
TRN.ADDRESS1
TRN.ADDRESS2
TRN.ADDRESS3
TRN.ADDRESS4
TRN.CITY
TRN.STATE
TRN.POSTAL
TRN.ADDRESS1_OTHER
TRN.ADDRESS2_OTHER
TRN.ADDRESS3_OTHER
TRN.ADDRESS4_OTHER
TRN.CITY_OTHER
TRN.STATE_OTHER
TRN.POSTAL_OTHER
TRN.VENDOR
TRN.DESCR
TRN.GVT_TRN_LOC_IND
        move 1 to #FoundTrainingData
FROM PS_TRAINING TRN
WHERE TRN.EMPLID = $EMPLID
  AND TRN.GVT_TRNG_REQNUM = $TRNREQNUM
end-select

if #FoundTrainingData <> 1
   display 'No data found for emplid '          noline
   display $EMPLID                              noline
   display ' and Training Request Number '      noline
   display $TRNREQNUM
   goto Endof-GetData
end-if

!------------------------------------
!get information from person, national ID and disability !
!------------------------------------

Begin-SELECT
PD.NAME
PD.BIRTHDATE
PD.HIGHEST_EDUC_LVL
 Do Get-Empl-Address ($Emplid,'HOME', $AddressFound)
          
   Let $COUNTRY  = $GETADDR_Country
   let $ADDRESS1 = $GETADDR_Address1
   let $ADDRESS2 = $GETADDR_Address2
   
   let $CITY = $GETADDR_CITY
   let $STATE =$GETADDR_STATE
   let $POSTAL = $GETADDR_POSTAL
   
    do Get-Phone-Data ($Emplid, $Pers_Phone)
    do Get-NID

FROM PS_PERSONAL_DT_FST  PD
WHERE PD.EMPLID = $Emplid
end-SELECT


!-----Begin------ R-SMOXLE-TG9NV (3/2/2000) ----------------------
!
! get the disability code for this employee.
!
! Look up the disability code for this employee
Begin-SELECT
PD.GVT_DISABILITY_CD
FROM PS_PERS_DATA_USF PD
WHERE EMPLID = $Emplid
end-SELECT

Move &PD.GVT_DISABILITY_CD To $DisCode

! see if this code qualifies as disabled


Begin-SELECT
H.DISABLED
  FROM PS_GVT_HANDICP_TBL H
  WHERE H.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_GVT_HANDICP_TBL A_ED
        WHERE H.GVT_DISABILITY_CD = A_ED.GVT_DISABILITY_CD
          AND A_ED.EFFDT <= $AsOfToday)
     AND H.EFF_STATUS = 'A'
     AND H.GVT_DISABILITY_CD = $DisCode
end-select

Move &H.DISABLED To $Disabled_Y_or_N


!-----End-------- R-SMOXLE-TG9NV (3/2/2000) -------------------
!

!--------------------------------
!Get information from EMPLOYMENT!
!--------------------------------
begin-select
E.POSITION_PHONE
E.SERVICE_DT
EJ.REPORTS_TO
FROM PS_EMPLOYMENT E, PS_EMPL_CURRJOB_VW EJ
WHERE E.EMPLID = $Emplid
and   E.EMPLID = EJ.EMPLID
and   E.EMPL_RCD = EJ.EMPL_RCD
end-select

!-------------------------
!Get information from JOB!
!-------------------------
begin-select
J.EMPLID
J.JOBCODE
J.LOCATION
J.DEPTID
JF.GVT_TYPE_OF_APPT
J.EFFDT
J.GRADE
J.STEP
J.COMPANY
JF.GVT_SUB_AGENCY
JF.GVT_POI
JF.GVT_PAY_PLAN
FROM PS_JOB J,
     PS_JOB_USF JF 
WHERE J.EMPLID = $Emplid
AND   J.EFFDT = (SELECT MAX(J2.EFFDT)
                  FROM PS_JOB J2
                  WHERE J2.EMPLID = J.EMPLID
                  AND J2.EMPL_RCD = J.EMPL_RCD
                  AND J2.EFFDT <= &TRN.GVT_TRNG_REQ_DATE)
AND   J.EFFSEQ = (SELECT MAX(J3.EFFSEQ)
                  FROM PS_JOB J3
                  WHERE J3.EMPLID = J.EMPLID
                  AND J3.EMPL_RCD = J.EMPL_RCD
                  AND   J3.EFFDT = J.EFFDT)
AND JF.EMPLID = J.EMPLID
AND JF.EFFDT = J.EFFDT
AND JF.EFFSEQ = J.EFFSEQ
end-select

!---------------------------------
!Get information from JOBCODE_TBL!
!---------------------------------
begin-select
JC.MANAGER_LEVEL
JC.DESCR
JC.GVT_OCC_SERIES
FROM PS_JOBCODE_TBL JC
WHERE JC.JOBCODE = &J.JOBCODE
AND   JC.EFFDT =  (SELECT MAX(JC2.EFFDT)
                   FROM PS_JOBCODE_TBL JC2
                   WHERE JC2.JOBCODE = JC.JOBCODE
                   AND JC2.EFFDT <= &J.EFFDT
                   AND JC2.EFF_STATUS = 'A')
AND JC.EFF_STATUS = 'A'

end-select
!---------------------------------
!Get information from GVT_SUBAGCY_TBL!
begin-select
SUB.GVT_DESCR70
FROM PS_GVT_SUBAGCY_TBL SUB
WHERE SUB.COMPANY = &J.COMPANY
!Begin Resolution 574140
!AND   SUB.GVT_SUB_AGENCY = &J.GVT_SUB_AGENCY   
AND   SUB.GVT_SUB_AGENCY  = &JF.GVT_SUB_AGENCY    
!End  Resolution 574140
AND   SUB.EFF_STATUS = 'A'
AND   SUB.EFFDT =  (SELECT MAX(SUB2.EFFDT)
                   FROM PS_GVT_SUBAGCY_TBL SUB2
                   WHERE SUB2.COMPANY = SUB.COMPANY
                   AND   SUB2.GVT_SUB_AGENCY = SUB.GVT_SUB_AGENCY
                   AND   SUB2.EFFDT <= &J.EFFDT
                   AND   SUB2.EFF_STATUS = 'A')
end-select

!---------------------------------
!Get information from DEPT_TBL!
!---------------------------------
begin-select
D.LOCATION
D.GVT_PAR_DESCR2
D.GVT_PAR_DESCR3
D.GVT_PAR_DESCR4
D.GVT_PAR_DESCR5
D.EFFDT
FROM PS_DEPT_TBL D
WHERE D.DEPTID = &J.DEPTID
AND D.EFF_STATUS = 'A'
AND   D.EFFDT =  (SELECT MAX(D2.EFFDT)
                   FROM PS_DEPT_TBL D2
                   WHERE D2.LOCATION = D.LOCATION
                   AND D2.EFFDT <= &J.EFFDT
                   AND D2.EFF_STATUS = 'A')

end-select

!---------------------------------
!Get information from LOCATION_TBL!
!---------------------------------
begin-select
LOC.DESCR
LOC.ADDRESS1
LOC.ADDRESS2
LOC.CITY
LOC.STATE
LOC.POSTAL
LOC.BUILDING
LOC.COUNTRY
FROM PS_LOCATION_TBL LOC
WHERE LOC.LOCATION = &D.LOCATION
AND   LOC.EFFDT =  (SELECT MAX(LOC2.EFFDT)
                   FROM PS_LOCATION_TBL LOC2
                   WHERE LOC2.LOCATION = LOC.LOCATION
                   AND LOC2.EFFDT <= &D.EFFDT
                   AND LOC2.EFF_STATUS = 'A')
AND LOC.EFF_STATUS = 'A'
end-select


!-------------------------------
!Get information from XLATTABLE!
!-------------------------------
!Get info for GVT_TYPE_APPT
move &TRN.GVT_TRNG_REQ_DATE to $AsOfDate
move 'GVT_TYPE_OF_APPT' to $FieldName
!Begin Resolution 574140
!move &J.GVT_TYPE_OF_APPT to $FieldValue   
move &JF.GVT_TYPE_OF_APPT to $FieldValue
!End Resolution 574140
do Read-Translate-Table
move $XlatShortName to $GVT_TYPE_OF_APPT

!Get info for MANAGER_LEVEL
move &TRN.GVT_TRNG_REQ_DATE to $AsOfDate
move 'MANAGER_LEVEL' to $FieldName
move &JC.MANAGER_LEVEL to $FieldValue
do Read-Translate-Table
move $XlatShortName to $GVT_TYPE_APPT

Endof-GetData:
end-procedure

begin-procedure GET-POSITION-INFO
begin-select
J1.POSITION_NBR
J1.EMPLID
J1.JOBCODE
J1.SETID_JOBCODE
P1.NAME
E1.POSITION_PHONE
J1.REPORTS_TO
FROM
PS_JOB J1, PS_PERSON_NAME P1, PS_EMPLOYMENT E1
WHERE J1.EMPL_STATUS IN('A','P','L','S')
AND J1.POSITION_NBR = $InfoOnPos
AND E1.EMPLID = J1.EMPLID
AND E1.EMPL_RCD = J1.EMPL_RCD
AND P1.EMPLID = J1.EMPLID
AND J1.EFFDT =
     (SELECT MAX(C1.EFFDT)
     FROM PS_JOB C1
     WHERE C1.EMPLID = J1.EMPLID
     AND C1.EMPL_RCD = J1.EMPL_RCD
     AND C1.EFFDT <= &TRN.GVT_TRNG_REQ_DATE)
AND J1.EFFSEQ =
     (SELECT MAX(D1.EFFSEQ)
     FROM PS_JOB D1
     WHERE D1.EMPLID = J1.EMPLID
     AND D1.EMPL_RCD = J1.EMPL_RCD
     AND D1.EFFDT = J1.EFFDT)
end-select
end-procedure

!************************************************************************!
Begin-Procedure Get-NID
!************************************************************************!
 let $NATIONAL_ID = ' '
begin-select
NID.NATIONAL_ID  
  move &NID.NATIONAL_ID to $NATIONAL_ID

From PS_PERS_NID  NID
where EMPLID = $EMPLID
 and  COUNTRY = $COUNTRY
   AND NID.NATIONAL_ID_TYPE = 'PR'
End-select
end-procedure

!************************************************************************!
begin-procedure Get-Phone-Data ($PersEmplid,:$Phone)
!************************************************************************!

  if rtrim( $PersEmplid,' ') = ''
    move '' to $Phone
    goto GetPhoneExit
  end-if
 
begin-SELECT
PP.PHONE
FROM  PS_PERSONAL_PHONE PP
WHERE PP.EMPLID = $PersEmplid
  AND PP.PHONE_TYPE = 'MAIN'
end-SELECT

     let $Phone      = rtrim(&PP.Phone,' ')
    
 GetPhoneExit:
  
end-procedure



#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines to format datetimes
#Include 'number.sqc'    !Routine to format numbers
#Include 'getjobtl.sqc'  !Get JobTitleInfo
#Include 'getaddr.sqc'   !Get Employee Address
