!***********************************************************************
!  PI709.SQR:INTERFACE FILE DEFINITION TABLE                           *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:02:31:21                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


!***************************
begin-procedure Init-Report
!***************************

  move 'PI709' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi709_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
!**********************************
begin-procedure Report-Translation
!**********************************

! Read from PI709 program ids
do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PI709', 'REPORT_TITLE', $Pi709_ReportTitle, #dummy_width)
  do Get_Field_Information ('PI709', 'FILEID', $Pi709_FileId, #dummy_width)
  do Get_Field_Information ('PI709', 'EFFDT', $Pi709_EffDt, #dummy_width)
  do Get_Field_Information ('PI709', 'EFFSTAT1', $Pi709_EffStat1, #dummy_width)
  do Get_Field_Information ('PI709', 'EFFSTAT2', $Pi709_EffStat2, #dummy_width)
  do Get_Field_Information ('PI709', 'DESCR', $Pi709_Description, #dummy_width)
  do Get_Field_Information ('PI709', 'DESCRSHORT', $Pi709_Short, #dummy_width)
  do Get_Field_Information ('PI709', 'SID', $Pi709_Sid, #dummy_width)
  do Get_Field_Information ('PI709', 'PROCTYPE1', $Pi709_ProcType1, #dummy_width)
  do Get_Field_Information ('PI709', 'PROCTYPE2', $Pi709_ProcType2, #dummy_width)
  do Get_Field_Information ('PI709', 'FILETYPE1', $Pi709_FileType1, #dummy_width)
  do Get_Field_Information ('PI709', 'FILETYPE2', $Pi709_FileType2, #dummy_width)
  do Get_Field_Information ('PI709', 'RECLEN1', $Pi709_RecLen1, #dummy_width)
  do Get_Field_Information ('PI709', 'RECLEN2', $Pi709_RecLen2, #dummy_width)
  do Get_Field_Information ('PI709', 'EXTFLD1', $Pi709_ExtFld1, #dummy_width)
  do Get_Field_Information ('PI709', 'EXTFLD2', $Pi709_ExtFld2, #dummy_width)
  do Get_Field_Information ('PI709', 'CONVCASE1', $Pi709_ConvCase1, #dummy_width)
  do Get_Field_Information ('PI709', 'CONVCASE2', $Pi709_ConvCase2, #dummy_width)
  do Get_Field_Information ('PI709', 'MAXFLDS1', $Pi709_MaxFlds1, #dummy_width)
  do Get_Field_Information ('PI709', 'MAXFLDS2', $Pi709_MaxFlds2, #dummy_width)
  do Get_Field_Information ('PI709', 'INCL_HEADER_IND1', $Pi709_InclHeaderInd1, #dummy_width)
  do Get_Field_Information ('PI709', 'INCL_HEADER_IND2', $Pi709_InclHeaderInd2, #dummy_width)
  do Get_Field_Information ('PI709', 'DATA_DELIMITER1', $Pi709_DataDelimiter1, #dummy_width)
  do Get_Field_Information ('PI709', 'DATA_DELIMITER2', $Pi709_DataDelimiter2, #dummy_width)

end-procedure


begin-heading 7
#Include 'stdhdgtr.sqc'

  print $Pi709_EffStat1       (7,21,67)
  print $Pi709_ProcType1      (,90,8)
  print $Pi709_FileType1      (,100,8)
  print $Pi709_RecLen1        (,110,8)
  print $Pi709_ExtFld1        (,120,8)
  print $Pi709_ConvCase1      (,130,8)
  print $Pi709_MaxFlds1       (,140,8)
  print $Pi709_InclHeaderInd1 (,150,8)
  print $Pi709_DataDelimiter1 (,160,8)

  print $Pi709_FileId         (8,1,7)
  print $Pi709_EffDt          (,10,9)
  print $Pi709_EffStat2       (,21,5)
  print $Pi709_Description    (,28,34)
  print $Pi709_Short          (,64,14)
  print $Pi709_Sid            (,80,8)
  print $Pi709_ProcType2      (,90,8)
  print $Pi709_FileType2      (,100,8)
  print $Pi709_RecLen2        (,110,8)
  print $Pi709_ExtFld2        (,120,8)
  print $Pi709_ConvCase2      (,130,8)
  print $Pi709_MaxFlds2       (,140,8)
  print $Pi709_InclHeaderInd2 (,150,8)
  print $Pi709_DataDelimiter2 (,160,8)


end-HEADING


!***************************
begin-procedure Process-Main
!***************************

begin-SELECT

A.PI_FILE_ID                       !(+2,1)  on-break level=1
A.EFFDT
    do Format-DateTime(&A.EFFDT, $out, {DEFDATE}, '', '')
   ! print $out                      (,10)
A.EFF_STATUS                       !(,21)  on-break level=1
A.DESCR                            !(,28)  on-break level=1
A.DESCRSHORT                       !(,64)
A.PI_SYSTEM_ID                     !(,80)
A.PI_PROCESS_TYPE                  !(,90)
A.PI_FILE_TYPE                     !(,100)

  let $prnt_pi_file_id      = &A.PI_FILE_ID
  let $prnt_effdt           = &A.EFFDT
  let $prnt_eff_status      = &A.EFF_STATUS
  let $prnt_descr           = &A.DESCR
  let $prnt_descrshort      = &A.DESCRSHORT
  let $prnt_pi_system_id    = &A.PI_SYSTEM_ID
  let $prnt_pi_process_type = &A.PI_PROCESS_TYPE
  let $prnt_pi_file_type    = &A.PI_FILE_TYPE
  let $basedate             = &A.EFFDT

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!
  if $Curr_language_cd <> $Psoptions_language_cd
     do Get-Rel-Lang-Pi709
  end-if

  PRINT $PRNT_PI_FILE_ID             (+2,1,7)  on-break level=1
  PRINT $OUT                         (,10,11)
  PRINT $PRNT_EFF_STATUS             (,23,3)  on-break level=1
  PRINT $PRNT_DESCR                  (,28,34)  on-break level=1
  PRINT $PRNT_DESCRSHORT             (,64,14)
  PRINT $PRNT_PI_SYSTEM_ID           (,80,8)
  PRINT $PRNT_PI_PROCESS_TYPE        (,90,8)
  PRINT $PRNT_PI_FILE_TYPE           (,100,8)

A.PI_RECORD_LEN

  if &A.PI_RECORD_LEN > 0
     print &A.PI_RECORD_LEN          (,110,8)
  end-if

A.EXT_FLDID_IND

  if &A.EXT_FLDID_IND = 'Y'
     print &A.EXT_FLDID_IND          (,120,8)
  end-if

A.CONVERT_CASE

  if &A.CONVERT_CASE = 'Y'
     print &A.CONVERT_CASE           (,130,8)
  end-if

A.MAX_FLDS_PER_REC
   if &A.MAX_FLDS_PER_REC > 0
      print &A.MAX_FLDS_PER_REC      (,140,8)
   end-if

A.INCL_HEADER_IND

  if &A.INCL_HEADER_IND = 'Y'
     print &A.INCL_HEADER_IND        (,150,8)
  end-if
A.DATA_DELIMITER                     (,160,8)

FROM PS_PI_DEFN_FILE A
ORDER BY A.PI_FILE_ID, A.EFFDT DESC

end-SELECT

end-procedure


!*********************************
begin-procedure Get-Rel-Lang-Pi709
!*********************************

begin-SELECT

L.DESCR
L.DESCRSHORT

  if rtrim(&L.DESCR,' ') <> ''
     let $prnt_descr      = &L.DESCR
  end-if

  if rtrim(&L.DESCRSHORT,' ') <> ''
     let $prnt_descrshort = &L.DESCRSHORT
  end-if

FROM PS_PI_DEFN_F_LANG L
WHERE L.PI_FILE_ID  = $PRNT_PI_FILE_ID
  AND L.LANGUAGE_CD = $Curr_language_cd
  AND L.EFFDT       =
    (SELECT MAX(EFFDT)
     FROM PS_PI_DEFN_F_LANG
     WHERE PI_FILE_ID  = L.PI_FILE_ID
       AND LANGUAGE_CD = L.LANGUAGE_CD
       AND EFFDT      <= $Basedate)

end-SELECT

end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

