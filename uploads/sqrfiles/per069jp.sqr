!***********************************************************************
! PER069JP : Employees in Inactive Departments                         *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:56:28                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!                                                                      *
!***********************************************************************
!This report will display employees who are not in the new organization*
!                                                                      *
!***********************************************************************

Begin-setup

#define  REPORTID  'PER069JP'
#include 'setenv.sqc'      !Set environment
#include 'ptset02.sqc'     !Init printer and page sizeLandscape

end-setup


begin-program
    do Init-DateTime
    do Init-Number
    do Get-Current-DateTime
    do Define-Prcs-Vars
    do Init-Report
    do Process-Main
    do Delete-From-AE-Tables
    do Update-Prcs-Run-Status
end-program

!*************************************************************************!
! Init-Report
!*************************************************************************!
begin-procedure Init-Report

!***********************************************************************
! Translations procedures & Strings Table retrievals.                  *
!***********************************************************************

  move {REPORTID} to $ReportID
  let #Error_Total = 0
  do Get-Run-Control-Parms
  do Get-Run-Cntl-Acq
  do get-org-plan-date

  do Init_Report_Translation ('PER069JP', $curr_language_cd)
  do Append_Report_Translation ('PER069JP')

  do Get_Field_Information ('PER069JP' ,'PER069JP_TITLE'    ,$PER069JP_Title          ,#DW)
  do Get_Field_Information ('PER069JP' ,'ORG_PLAN'          ,$org_plan_lbl            ,#DW)
  do Get_Field_Information ('PER069JP' ,'ORG_DATE'          ,$org_date_lbl            ,#DW)
  do Get_Field_Information ('PER069JP' ,'BUNIT'             ,$BUnit_lbl               ,#DW)
  do Get_Field_Information ('PER069JP' ,'DEPT'              ,$dept_lbl                ,#DW)
  do Get_Field_Information ('PER069JP' ,'SUPV_LVL'          ,$supv_lvl_lbl            ,#DW)
  do Get_Field_Information ('PER069JP' ,'EMPL_ID'           ,$Empl_ID_lbl             ,#DW)
  do Get_Field_Information ('PER069JP' ,'RECNO'             ,$Recno                   ,#DW)
  do Get_Field_Information ('PER069JP' ,'EMPL_NAME'         ,$Empl_name_lbl           ,#DW)
  do Get_Field_Information ('PER069JP' ,'APPT_TYPE'         ,$APPT_TYPE_lbl           ,#DW)
  
  move $PER069JP_Title to $ReportTitle
  let $ReportDate            = $AsOfToday          ! Printed in header
  let $ReportTime            = $AsOfNow            ! Printed in header

end-procedure Init-Report

!*************************************************************************!
! Get-Run-Cntl-Acq
!*************************************************************************!

!*************************************************************************!
! Retrieve Run Control Parameters.
!*************************************************************************!
begin-procedure Get-Run-Cntl-Acq

  let $org_plan_JPN  = ''
  let $org_date_JPN  = ''
  let $Business_Unit  = ''

begin-SELECT

RC.BUSINESS_UNIT
RC.ORG_PLAN_JPN

  let $org_plan_JPN  = &RC.ORG_PLAN_JPN
  let $Business_Unit = &RC.BUSINESS_UNIT
FROM
PS_RC_IDO_JPN RC

WHERE
RC.OPRID = $prcs_oprid
AND RC.RUN_CNTL_ID = $prcs_run_cntl_id

end-SELECT
end-procedure !Get-Run-Cntl-Acq

!*************************************************************************!
! get-org-plan-date
!*************************************************************************!
!*************************************************************************!
! Get ORG_DATE_PLAN from PS_IDO_ORGPLAN_JPN
!*************************************************************************!

begin-procedure get-org-plan-date
begin-select
A.ORG_DATE_JPN
 let $org_date_JPN_In  = &A.ORG_DATE_JPN
 do Convert-To-DTU-Date ($org_date_JPN_In, $org_date_JPN)

 let $AsOfDate      = &A.ORG_DATE_JPN
FROM
PS_IDO_ORGPLAN_JPN A
WHERE
A.ORG_PLAN_JPN = $org_plan_JPN
end-select
end-procedure !get-org-plan-date

!*************************************************************************!
! Process-Main
!*************************************************************************!
!*************************************************************************!
! Get Emp Detail who are not in new org plan.
!*************************************************************************!

begin-procedure Process-Main

begin-select
A.BUSINESS_UNIT
A.EMPLID
A.EMPL_RCD
A.DEPTID
A.SUPV_LVL_ID
A.APPT_TYPE

 let $EmplID        = &A.EMPLID
 let $DeptID        = &A.DEPTID
 let $SUPV_LVL_ID   = &A.SUPV_LVL_ID
 let $BU = $Business_Unit
 do Get-Employee-Name
 do get-setid-supv
 do Get-Supvlvl-Name
 do Get-Department-Name

 ! Keys to be passed for Procedure Read-Translate-Table for Field APPT_TYPE
 let $FieldName   = 'APPT_TYPE'
 let $FieldValue  = &A.APPT_TYPE
 do Read-Translate-Table
 let $APPT_TYPE_value = $XlatShortName
 
 do Print_Line
 
FROM
PS_IDO_NEW_ORG_JPN A,
PS_DEPT_TBL B

WHERE
A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
AND A.DEPTID = B.DEPTID
AND B.EFF_STATUS = 'I'
AND B.EFFDT = (SELECT MAX(DEP1.EFFDT) FROM PS_DEPT_TBL DEP1
              WHERE DEP1.DEPTID = B.DEPTID
              AND DEP1.EFFDT <= &A.ORG_DATE_JPN)
ORDER BY A.BUSINESS_UNIT, A.DEPTID, A.SUPV_LVL_ID, A.EMPLID
end-select
end-procedure !Process-Main

!*************************************************************************!
! get-setid-supv
!*************************************************************************!
!*************************************************************************!
! Retrieve setid from PS_SET_CNTRL_REC and set to $SETID_DEPT
!*************************************************************************!

begin-procedure get-setid-supv
begin-select
A5.SETID
 let $SETID_DEPT = &A5.SETID
FROM
PS_SET_CNTRL_REC A5
WHERE A5.RECNAME = 'SUPVSR_LVL_TBL'
AND A5.SETCNTRLVALUE = $Business_Unit
end-select
end-procedure !get-setid-supv

begin-heading 10
#include 'stdhdgtr.sqc'

   print $org_plan_lbl (+1,1,22)
   print $org_plan_JPN (0,24,4)
   print $org_date_lbl (0,30,15)
   print $org_date_JPN (0,46,10)
   print $BUnit_lbl (+1,1,15)
   print $Business_Unit (0,16,6)
   
   print $dept_lbl (+1,1,40)
   print $supv_lvl_lbl (0,41,30)
   print $Empl_ID_lbl (0,72,11)
   print $Recno (0,84,90)
   print $Empl_name_lbl (0,91,30)
   print $APPT_TYPE_lbl (0,125,5)
   print '-' (+1,1,160) FILL

end-heading
!*************************************************************************!
! Print_Line
!*************************************************************************!
!*************************************************************************!
! print the report body
!*************************************************************************!
begin-procedure Print_Line

  print $DeptName (+1, 1, 40) 
  print $SupvlvlName ( 0, 41, 30)
  print $emplID (0,72,11)
  print &A.EMPL_RCD (0,84,90)
  print $Name (0,91,30)
  print $APPT_TYPE_value (0,125,5)

end-procedure !Print_Line

!*************************************************************************!
! Delete-From-AE-Tables
!*************************************************************************!

begin-procedure Delete-From-AE-Tables

begin-sql
Delete from PS_IDO_NEW_ORG_JPN
Where JOBINSTANCE = #prcs_job_instance
end-sql

end-procedure !Delete-From-AE-Tables

!*************************************************************************!
! SQC Files for called procedures
!*************************************************************************!
#include 'prcsdef.sqc'       !Update Process Request variable declare
#include 'prcsapi.sqc'       !Update Process Request API
#include 'curdttim.sqc'      !Get-Current-DateTime procedure
#include 'datetime.sqc'      !Routines for date and time formatting
#include 'number.sqc'        !Routines to format numbers
#include 'sqrtrans.sqc'      !Translate SQR strings to given language
#include 'datemath.sqc'      !Date conversion procedures
#include 'getsupjp.sqc'      !Get Supervisor Level Name
#Include 'getempnm.sqc'      !Get Employee Name
#include 'getdptnm.sqc'      !Get Department Name from the Department Table
#include 'readxlat.sqc'     ! XLAT Table Value
