!**********************************************************************
!  GetEmpAd:  Get the Personal Data for an Emplid                     *
!**********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:13:02:40                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


!------------------------------------------------------------------
! Procedure Get-Personal_Data : Get datas from PERSON
!                                         and  PERS_DATA_EFFDT
!                                         and  NAMES
!                                         and  ADDRESSES
!------------------------------------------------------------------

begin-procedure Get-Personal-Data

    move '' to $Full_Name
    move '' to $Letter_Name
    move '' to $Name
    move '' to $Anrede
    move '' to $Name
    move '' to $Titel
    move '' to $Vorsatz
    move '' to $Zusatz
    move '' to $Street
    move '' to $Street2
    move '' to $Postal
    move '' to $City
    move '' to $PostalCity
    move '' to $State
    move '' to $Country

    move '' to $Sex
    move '' to $MarStatus
    move '' to $DtOfDeath
    move '' to $Birthdate

  if $EmplID = ''  !if null, bypass the SELECT
    goto GetEmpAdExit
  end-if

  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if

  if $Addr_Type = ''  !if no Address Type is specified, default to 'Home
    move 'HOME' to $Addr_Type
  end-if

! ---------------------- Select in Person
begin-SELECT

PSN.BIRTHDATE
PSN.DT_OF_DEATH

 let $Birthdate = &PSN.BIRTHDATE
 let $DtOfDeath = &PSN.DT_OF_DEATH

FROM  PS_PERSON PSN
WHERE PSN.EMPLID = $EmplID

end-SELECT

! ---------------------- Select in Pers_Data_Effdt
begin-SELECT
PDE1.SEX
PDE1.MAR_STATUS

 let $Sex = &PDE1.SEX
 let $MarStatus = &PDE1.MAR_STATUS

FROM  PS_PERS_DATA_EFFDT PDE1
WHERE PDE1.EMPLID = $EmplID
  AND PDE1.EFFDT = ( SELECT MAX(PDE2.EFFDT) 
                       FROM PS_PERS_DATA_EFFDT PDE2
                      WHERE PDE2.EMPLID = PDE1.EMPLID
                        AND ( PDE2.EFFDT <= $AsOfDate
                              OR ( PDE2.EFFDT > $AsOfDate
                                   AND $AsOfDate < ( SELECT MIN(PDE3.EFFDT)
                                                       FROM PS_PERS_DATA_EFFDT PDE3
                                                      WHERE PDE3.EMPLID = PDE2.EMPLID))))
end-SELECT

! ---------------------- Select in Names
begin-SELECT

NAME1.NAME
NAME1.NAME_PREFIX
NAME1.NAME_TITLE
NAME1.NAME_ROYAL_PREFIX
NAME1.NAME_ROYAL_SUFFIX

 let $Name = rtrim(&NAME1.NAME,' ')
  let $Anrede = rtrim(&NAME1.NAME_PREFIX,' ')
  !find last name
  let $field = rtrim($Name,' ')
  find ',' in $Name 0 #locn
  extract $LastName from $Name 0 #locn

  !find first name
  add 2 to #locn
  let $Name = {ps-substr}($Name,#locn,50 - #locn)
  find ',' in $Name 0 #locn
  if #locn <> -1
    extract $FirstName from $Name 0 #locn
    add 1 to #locn
    extract $PrefixName from $Name #locn 50
  else
    let $Firstname = $Name
    let $PrefixName = ''
  end-if
  let $Full_Name = rtrim ($FirstName, ' ')

  !**build DÜVO-formatted name string in $Full_Name

  let $Titel = rtrim(&NAME1.NAME_TITLE, ' ')
  let $Vorsatz = rtrim(&NAME1.NAME_ROYAL_PREFIX, ' ')
  let $Zusatz = rtrim(&NAME1.NAME_ROYAL_SUFFIX, ' ')

  if rtrim ($Zusatz, ' ') <> ''
    concat ' ' with $Full_Name
    concat ' ' with $Letter_Name
    concat $Zusatz with $Full_Name
    concat $Zusatz with $Letter_Name
  end-if

  if rtrim ($Vorsatz, ' ') <> ''
    concat ' ' with $Full_Name
    concat ' ' with $Letter_Name
    concat $Vorsatz with $Full_Name
    concat $Vorsatz with $Letter_Name
  end-if

  concat ' ' with $Full_Name
  concat ' ' with $Letter_Name
  concat $LastName with $Full_Name
  concat $LastName with $Letter_Name

FROM  PS_NAMES NAME1
WHERE NAME1.EMPLID = $EmplID
  AND NAME1.NAME_TYPE = 'PRI' 
  AND NAME1.EFFDT = ( SELECT MAX(NAME2.EFFDT) 
                        FROM PS_NAMES NAME2
                       WHERE NAME2.EMPLID = NAME1.EMPLID 
                         AND NAME2.NAME_TYPE = NAME1.NAME_TYPE 
                         AND ( NAME2.EFFDT <= $AsOfDate
                               OR (NAME2.EFFDT > $AsOfDate
                                   AND $AsOfDate < ( SELECT MIN(NAME3.EFFDT) 
                                                       FROM PS_NAMES NAME3 
                                                      WHERE NAME3.EMPLID = NAME2.EMPLID 
                                                        AND NAME3.NAME_TYPE = NAME2.NAME_TYPE))))
end-SELECT

! ---------------------- Select in Addresses
begin-SELECT

ADDR1.ADDRESS1
ADDR1.ADDRESS2
ADDR1.POSTAL
ADDR1.CITY
ADDR1.STATE

    let $Street = rtrim(&ADDR1.ADDRESS1,' ')
    let $Street2 = rtrim(&ADDR1.ADDRESS2,' ')
    let $Postal = rtrim(&ADDR1.POSTAL,' ')
    let $City = rtrim(&ADDR1.CITY,' ')
    let $State = rtrim(&ADDR1.STATE,' ')

    concat $Postal with $PostalCity
    concat ' ' with $PostalCity
    concat $City with $PostalCity

FROM PS_ADDRESSES ADDR1
WHERE ADDR1.EMPLID = $EmplID
  AND ADDR1.ADDRESS_TYPE = $Addr_Type
  AND ADDR1.EFFDT = ( SELECT MAX(ADDR2.EFFDT) 
                        FROM PS_ADDRESSES ADDR2
                       WHERE ADDR2.EMPLID = ADDR1.EMPLID 
                         AND ADDR2.ADDRESS_TYPE = ADDR1.ADDRESS_TYPE 
                         AND ( ADDR2.EFFDT <= $AsOfDate
                               OR ( ADDR2.EFFDT > $AsOfDate
                                    AND $AsOfDate < ( SELECT MIN(ADDR3.EFFDT) 
                                                        FROM PS_ADDRESSES ADDR3
                                                       WHERE ADDR3.EMPLID = ADDR2.EMPLID 
                                                         AND ADDR3.ADDRESS_TYPE = ADDR2.ADDRESS_TYPE))))
end-SELECT

GetEmpAdExit:

end-procedure

