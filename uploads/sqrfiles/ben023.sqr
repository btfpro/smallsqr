!***********************************************************************
!  HIPAA:  Medical Certification Statement (Dependents)                *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/09/12:11:06:15                                 !
!       $Release:  HR9                                                 !
!      $Revision:  107                                                 !
!                                                                      *
!***********************************************************************
!                                                                      *
! This report is required for United States only. No Translation. -DCD *
! This module is cloned from BEN022.SQR.                          -TBL *
!***********************************************************************

#Include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Variables specific to USA

Begin-Setup
  #Include 'ptpsp125.sqc'
End-Setup

#define len_plan_type           9
#define len_date                10
#define len_status              10
#define len_benefit_plan        30
#define len_provider            30
#define len_group               30

#define col_plan_type           3
#define col_date                14
#define col_status              14
#define col_benefit_plan        26
#define col_provider            26
#define col_group               26
#define col_txt                 3
#define col_entry               28


!***********************************************************************
! Procedue Name:  Begin-Program                                         *
!***********************************************************************
Begin-Program

  Do Init-Report
  Do Process-Main
  Do StdAPI-Term

End-Program


!************************************************************************
!* Procedure Name  Init-Report                                          *
!************************************************************************
Begin-Procedure Init-Report

  Do Init-DateTime
  Do Init-Number

  Move 'HIPAA - Dependents' to $ReportID
  Move 'Dependent Certificate of Group Health Plan Coverage' to $ReportTitle
  display $ReportTitle
  Do Get-Current-DateTime
  Do StdAPI-Init

  If   $prcs_process_instance = ''
    Do Ask-From-Thru-Date

    Do Format-DateTime('19000101',$Temp_Dt,{DEFCMP},'','native')

    If   $Temp_dt = $FromDate
       Do Format-DateTime('19960701',$FromDate,{DEFCMP},'','native')
   End-If
  else
    Do Get-Input-Values
    do Get-Include-Options
 End-If

  Do Create-Arrays

End-Procedure


!**************************************************************************
!* Procedure Name: Get-Input-Values                                       *
!**************************************************************************
Begin-Procedure Get-Input-Values

    Do Select-Parameters

    Do Get-From-Thru-Date

    If   $FromDate = ''
       Do Format-DateTime('07/01/1996', $FromDate, {DEFMDY},'','native')
   End-If

    If   $ThruDate = ''
       Let   $ThruDate = $AsOfToday
   End-If

End-Procedure


!**************************************************************************
!* Procedure Name: Delete-Table                                           *
!**************************************************************************
Begin-Procedure Delete-Table

   Let   $sql-statement = 'BEN023.SQR,Delete-Table,Delete,PS_R_BEN023'
   Begin-SQL On-Error=SQL-Error
      DELETE FROM PS_R_BEN023;
   End-SQL

End-Procedure


!**************************************************************************
!* Procedure Name: Create-Arrays                                          *
!**************************************************************************
Begin-Procedure Create-Arrays

   Create-array name=DepInfo size = 20
                field=Name:char
                field=Birthdate:char
                field=SSN:char

   Create-array name=Dependent_A size = 20
                field=A_Dependent_Benef:char

   Create-array name=Dependent_B size = 20
                field=B_Dependent_Benef:char

   Create-array name=Dependent_A2 size = 20
                field=A_Dependent_Lostc:char

   Create-array name=Dependent_B2 size = 20
                field=B_Dependent_Lostc:char

End-Procedure


!**************************************************************************
!* Procedure Name: Process-Main                                           *
!**************************************************************************
Begin-Procedure Process-Main
    Do Delete-Table
    Do Commit-Transaction
    Do Process-Dependent-Coverage
    Do Print-Report
    Do Delete-Table

End-Procedure


!**************************************************************************
!* Procedure Name: Process-Dependent-Coverage                             *
!**************************************************************************
Begin-Procedure Process-Dependent-Coverage

Begin-SELECT
S.EMPLID
S.EMPL_RCD
S.COBRA_EVENT_ID
S.PLAN_TYPE
S.BENEFIT_NBR
S.EFFDT
S.COVERAGE_BEGIN_DT
S.COVERAGE_ELECT
S.BENEFIT_PLAN
S.COVRG_CD

        let $emplid_hip     = &S.EMPLID
        let #empl_rcd_hip   = &S.EMPL_RCD
        let #cobra_hip      = &S.COBRA_EVENT_ID
        let $plantype_hip   = &S.PLAN_TYPE
        let $cov_begin_hip  = &S.COVERAGE_BEGIN_DT

        let $HIPAA = 'N'
        do Find-HIPAA-Plan

!  if $HIPAA <> 'Y'
!    Do Check-For-HIPAA
!  end-if

  if $HIPAA = 'Y'

     Let   $A_coverage_status = rtrim(&s.coverage_elect,' ')
     Let   $t_effdt = &s.effdt
     Let   $t_coverage_begin_dt = &s.coverage_begin_dt
     Let   $array = 'A'
     Let   #row_a = 0

     If &S.COVERAGE_ELECT = 'E'
        Let $A_coverage_status = 'T'
        Let $Emplid = &S.EMPLID
        Let #Empl_Rcd = &S.EMPL_RCD
        Let #Cobra_Event_Id = &S.COBRA_EVENT_ID
        Let $Plan_Type = &S.PLAN_TYPE
        Let #Benefit = &S.BENEFIT_NBR
        Let $Effdt = &S.EFFDT

        Do Select-Health-Dependnt

     End-If

     Do Select-Next-Highest-Enrollment
  end-if

FROM PS_HEALTH_BENEFIT S
WHERE S.COVERAGE_BEGIN_DT = (SELECT MAX(S1.COVERAGE_BEGIN_DT)
                             FROM PS_HEALTH_BENEFIT S1
                             WHERE S1.EMPLID            = S.EMPLID
                               AND S1.EMPL_RCD          = S.EMPL_RCD
                               AND S1.COBRA_EVENT_ID    = S.COBRA_EVENT_ID
                               AND S1.PLAN_TYPE         = S.PLAN_TYPE
                               AND S1.COVERAGE_BEGIN_DT >= $FromDate
                               AND S1.COVERAGE_BEGIN_DT <= $ThruDate)
 ORDER BY S.EMPLID, S.PLAN_TYPE
End-SELECT

End-Procedure

begin-procedure  Find-HIPAA-Plan
  let $HIPAA = 'N'

  do Get-Benefit-Plan

begin-SELECT
DV.HIPAA_PLAN

  let $HIPAA = rtrim(&DV.HIPAA_PLAN,' ')

FROM PS_BEN_DEFN_OPTN   OV,
     PS_BEN_DEFN_PLAN   DV,
     PS_BEN_DEFN_PGM    CV,
     PS_BEN_PROG_PARTIC BV
WHERE BV.EMPLID          = $emplid_hip
  AND BV.EMPL_RCD        = #empl_rcd_hip
  AND BV.COBRA_EVENT_ID  = #cobra_hip
  AND BV.EFFDT = (SELECT MAX(BV1.EFFDT)
                  FROM PS_BEN_PROG_PARTIC BV1
                  WHERE BV1.EMPLID          = BV.EMPLID
                    AND BV1.EMPL_RCD        = BV.EMPL_RCD
                    AND BV1.COBRA_EVENT_ID  = BV.COBRA_EVENT_ID
                    AND BV1.EFFDT          <= $BenPlanEffdt)
  AND BV.BENEFIT_PROGRAM = CV.BENEFIT_PROGRAM
  AND CV.EFFDT           = (SELECT MAX(CV1.EFFDT)
                           FROM PS_BEN_DEFN_PGM CV1
                           WHERE CV1.BENEFIT_PROGRAM = CV.BENEFIT_PROGRAM
                             AND CV1.EFFDT          <= $BenPlanEffdt)
  AND CV.BENEFIT_PROGRAM = DV.BENEFIT_PROGRAM
  AND DV.EFFDT           = CV.EFFDT
  AND DV.PLAN_TYPE       = $plantype_hip
  AND DV.BENEFIT_PROGRAM = OV.BENEFIT_PROGRAM
  AND OV.PLAN_TYPE       = DV.PLAN_TYPE
  AND OV.EFFDT           = DV.EFFDT
  AND OV.BENEFIT_PLAN    = $BenPlanTerm
End-SELECT
End-Procedure

begin-procedure Get-Benefit-Plan
  let $BenPlanTerm  = ' '
  let $BenPlanEffdt = $FromDate
begin-SELECT
BPV.BENEFIT_PLAN
BPV.COVERAGE_BEGIN_DT

  let $BenPlanTerm  = &BPV.BENEFIT_PLAN
  let $BenPlanEffdt = &BPV.COVERAGE_BEGIN_DT

FROM PS_HEALTH_BENEFIT BPV
WHERE BPV.EMPLID            = $emplid_hip
  AND BPV.EMPL_RCD          = #empl_rcd_hip
  AND BPV.COBRA_EVENT_ID    = #cobra_hip
  AND BPV.PLAN_TYPE         = $plantype_hip
  AND BPV.COVERAGE_BEGIN_DT = (SELECT MAX(BPV1.COVERAGE_BEGIN_DT)
                              FROM PS_HEALTH_BENEFIT BPV1
                              WHERE BPV1.EMPLID             = BPV.EMPLID
                                AND BPV1.EMPL_RCD           = BPV.EMPL_RCD
                                AND BPV1.COBRA_EVENT_ID     = BPV.COBRA_EVENT_ID
                                AND BPV1.PLAN_TYPE          = BPV.PLAN_TYPE
                                AND BPV1.COVERAGE_BEGIN_DT <= $cov_begin_hip
                                AND BPV1.COVERAGE_ELECT     = 'E')
  AND BPV.COVERAGE_ELECT    = 'E'
end-SELECT
end-procedure

!**************************************************************************
!* Procedure Name: Select-Next-Highest-Enrollment                         *
!**************************************************************************

Begin-Procedure Select-Next-Highest-Enrollment

Begin-SELECT
E.EMPLID
E.EMPL_RCD
E.COBRA_EVENT_ID
E.PLAN_TYPE
E.BENEFIT_NBR
E.EFFDT
E.BENEFIT_PLAN
E.COVERAGE_BEGIN_DT
E.COVRG_CD
E.COVERAGE_ELECT


  Let   $B_coverage_status = rtrim(&E.COVERAGE_ELECT,' ')
  Let   $E_Coverage_begin_dt = &E.COVERAGE_BEGIN_DT
  Let   $E_Benefit_Plan = &E.BENEFIT_PLAN

  Let #row_b = 0
  If   &E.COVERAGE_ELECT = 'E'
     Let $B_coverage_status = 'T'
     Let $array = 'B'
     Let $Emplid = &E.EMPLID
     Let #Empl_Rcd = &E.EMPL_RCD
     Let #Cobra_Event_Id = &E.COBRA_EVENT_ID
     Let $Plan_Type = &E.PLAN_TYPE
     Let #Benefit = &E.BENEFIT_NBR
     Let $Effdt = &E.EFFDT
     Do Select-Health-Dependnt
  End-If

  Do Compare-Arrays
  Do Shift-Arrays

  Let   $T_Effdt = &E.EFFDT
  Let   $T_Benefit_Plan = &E.BENEFIT_PLAN
  Let   $T_Coverage_begin_dt = &E.COVERAGE_BEGIN_DT

FROM PS_HEALTH_BENEFIT E
WHERE E.EMPLID            = &S.EMPLID
AND   E.EMPL_RCD          = &S.EMPL_RCD
AND   E.COBRA_EVENT_ID    = &S.COBRA_EVENT_ID
AND   E.PLAN_TYPE         = &S.PLAN_TYPE
AND   E.BENEFIT_NBR       = &S.BENEFIT_NBR
AND   E.COVERAGE_BEGIN_DT < &S.COVERAGE_BEGIN_DT
ORDER BY E.COVERAGE_BEGIN_DT DESC

End-SELECT

End-Procedure


!**************************************************************************
!* Procedure Name: Select-Health-Dependnt                                 *
!**************************************************************************
Begin-Procedure Select-Health-Dependnt

Begin-SELECT
F.DEPENDENT_BENEF

  If $array = 'A'
     Put &F.DEPENDENT_BENEF Into Dependent_A(#row_a)
     Put ' ' Into Dependent_A2(#row_a)
     Let   #row_a = #row_a + 1
  Else
     Put &F.DEPENDENT_BENEF Into Dependent_B(#row_b)
     Put ' ' Into Dependent_B2(#row_b)
     Let   #row_b = #row_b + 1
  End-If


FROM PS_HEALTH_DEPENDNT F,
     PS_DEP_BEN I
WHERE F.EMPLID          = $Emplid
  AND F.EMPL_RCD        = #Empl_Rcd
  AND F.COBRA_EVENT_ID  = #Cobra_Event_Id
  AND F.PLAN_TYPE       = $Plan_Type
  AND F.BENEFIT_NBR     = #Benefit
  AND F.EFFDT           = $Effdt
  AND F.EMPLID          = I.EMPLID
  AND F.DEPENDENT_BENEF = I.DEPENDENT_BENEF
  AND I.DT_OF_DEATH IS NULL

End-SELECT

End-Procedure


!**************************************************************************
!* Procedure Name: Compare-Arrays                                         *
!**************************************************************************
Begin-Procedure Compare-Arrays

Let #count_b = 0
While #count_b < #row_b
   Get $b_dependent_benef from Dependent_B(#count_b)
   Let #count_a = 0
   Let $lost_coverage = 'Y'
   While #count_a < #row_a
      Get $a_dependent_benef from Dependent_A(#count_a)
      Get $a_dependent_lostc from Dependent_A2(#count_a)
      Let #count_a = #count_a + 1
      If $b_dependent_benef = $a_dependent_benef
!       and $a_dependent_lostc <> 'Y'
         Let $lost_coverage = 'N'
         do Insert-CoverageB
      End-If
   End-While
   If $lost_coverage = 'Y'
      Do Insert-Table
!      Put 'Y' into Dependent_B(#count_b)
   End-If
   Let #count_b = #count_b + 1
End-While

End-Procedure


!**************************************************************************
!* Procedure Name: Shift-Arrays                                           *
!**************************************************************************
Begin-Procedure Shift-Arrays

Let #count = 0
While #count < #row_b
   Get $b_dependent_benef from Dependent_B(#count)
   Put $b_dependent_benef into Dependent_A(#count)
   Get $b_dependent_lostc from Dependent_B2(#count)
   Put $b_dependent_lostc into Dependent_A2(#count)
   Let #count = #count +1
End-While
Let #row_a = #row_b

move $b_coverage_status to $a_coverage_status

End-Procedure

!**************************************************************************
!* Procedure Name: Print-Report                                           *
!**************************************************************************
Begin-Procedure Print-Report

  Let $Old_Plan_Type    = ''
  Let $Old_Emplid       = ''
  Let $Old_Benef        = ''
  let $CvgDt_Ended      = ''
  Let $FirstPage        = 'Y'

Begin-SELECT
A.EMPLID
A.EMPL_RCD
A.DEPENDENT_BENEF
A.COBRA_EVENT_ID
A.PLAN_TYPE
A.BENEFIT_NBR
A.EFFDT
A.COVERAGE_BEGIN_DT
A.COVERAGE_ELECT
A.BENEFIT_PLAN

        Do Check-For-Other-Coverage

        If $Other_Coverage = 'N'

                Let $Emplid          = &A.EMPLID
                Let $Dependent_benef = &A.DEPENDENT_BENEF
                Let $Plan_Type       = rtrim(&A.PLAN_TYPE, ' ')

                if  ($Emplid    <> $Old_Emplid) OR
                    ($Old_Benef <> $Dependent_benef)

                    if $FirstPage = 'Y'
                       Let $FirstPage        = 'N'
                    else
                      IF #Creditable_Days > 0 OR $IncOptnDetaiL = 'Y'
                         new-page
                      END-IF
                    end-if

                    Do Get-Dependent-Info
                    Do Get-Employee-Info
                    Do Get-Administrator
                    do Get-Coverage-Cutoff

                    Let $Old_Emplid    = $Emplid
                    Let $Old_Benef     = $Dependent_benef
                    Let $Old_Plan_Type = $Plan_Type
                else
                    if $Plan_Type <> $Old_Plan_Type
                       new-page
                       Do Get-Administrator
                       Let $Old_Plan_Type = $Plan_Type
                    end-if
                end-if


                Let   #Cobra_Event_Id  = &A.COBRA_EVENT_ID
                Let   $Effdt           = &A.EFFDT

                Let   #Creditable_Days = 0
                Let   #Gap_Days        = 0

                Let   $Freeze_Credit   = 'N'

                Let   $Plan_Type = rtrim(&A.PLAN_TYPE, ' ')
                Let   $FieldName = 'PLAN_TYPE'
                Let   $FieldValue = $Plan_Type
                Let   $AsOfDate = $Effdt
                Do Read-Translate-Table



                Let   $Status = rtrim(&A.COVERAGE_ELECT, ' ')
                Let   $FieldName  = 'COVERAGE_ELECT'
                Let   $FieldValue = $Status
                Let   $AsOfDate   = $Effdt
                Do Read-Translate-Table

                Do Format-DateTime(&A.COVERAGE_BEGIN_DT, $CvgDt_def, {DEFDATE}, '', '')
                let $CvgDt_Ended = $CvgDt_def

                Let   #CreditDay = 0
                Let   #WaveDays = 0
                Let   $Gap_Flag = 'N'

                Let   $Election1 = $Status
                Let   $Effdt1 = &A.EFFDT

                Do Convert-To-DTU-Date(&A.COVERAGE_BEGIN_DT, $TermDate_dtu)
                Do dtu-add-months($TermDate_dtu, -18, $PeriodDate_dtu)
                Do Convert-From-DTU-Date($PeriodDate_dtu, $PeriodDate)

                Do Get-Enrolled-Health-Info
                Do Get-Enrolled-Health-Plan-Info

                Do Maintain-History
                 IF #Creditable_Days > 0 OR $IncOptnDetaiL = 'Y'
                    Do Print-Header-Instructions
                    Do Print-Employee-Data
                    Do Print-educational-instructions
                 End-if   
        End-If

FROM PS_R_BEN023      A
WHERE A.COVERAGE_BEGIN_DT  = (SELECT MAX(A1.COVERAGE_BEGIN_DT)
                              FROM PS_R_BEN023 A1
                              WHERE A1.EMPLID          = A.EMPLID
                                AND A1.EMPL_RCD        = A.EMPL_RCD
                                AND A1.DEPENDENT_BENEF = A.DEPENDENT_BENEF
                                AND A1.PLAN_TYPE       = A.PLAN_TYPE
                                AND (A1.COVERAGE_ELECT = 'T' OR A1.COVERAGE_ELECT = 'W')
                                AND (A1.COVERAGE_BEGIN_DT  >= $FromDate
                                AND  A1.COVERAGE_BEGIN_DT  <= $ThruDate))
  AND (A.COVERAGE_ELECT = 'T' OR A.COVERAGE_ELECT = 'W')
  AND NOT EXISTS (SELECT 'X' FROM PS_BEN_LETTER_HIST LH
                  WHERE LH.EMPLID            = A.EMPLID
                    AND LH.DEPENDENT_BENEF   = A.DEPENDENT_BENEF
                    AND LH.DEPENDENT_BENEF   <> '00'
                    AND LH.LETTER_CD         = 'HDE'
                    AND LH.COVERAGE_END_DT   = (SELECT MAX(LH1.COVERAGE_END_DT)
                                                FROM PS_BEN_LETTER_HIST LH1
                                                WHERE LH1.EMPLID            = LH.EMPLID
                                                  AND LH1.DEPENDENT_BENEF   = LH.DEPENDENT_BENEF
                                                  AND LH1.LETTER_CD         = LH.LETTER_CD)
                    AND A.COVERAGE_BEGIN_DT <= LH.COVERAGE_END_DT)
ORDER BY A.EMPLID, A.DEPENDENT_BENEF, A.PLAN_TYPE, A.EMPL_RCD
End-SELECT

End-Procedure

!
! The purpose of the next procedure is to set the process cutoff date (which represents the
! latest coverage lost date (coverage termination) during the reporting period in any one of
! the plan types that the dependent is enrolled in. This date is used to prevent re-generating
! certificates when program is re-run for the same period.
!
begin-procedure Get-Coverage-Cutoff
  LET $ProcessCutOff = ''
begin-SELECT
CUT.EMPLID
CUT.EMPL_RCD
CUT.PLAN_TYPE
CUT.EFFDT
CUT.COBRA_EVENT_ID
CUT.COVERAGE_BEGIN_DT

  let $emplid_hip     = &CUT.EMPLID
  let #empl_rcd_hip   = &CUT.EMPL_RCD
  let #cobra_hip      = &CUT.COBRA_EVENT_ID
  let $plantype_hip   = &CUT.PLAN_TYPE
  let $cov_begin_hip  = &CUT.COVERAGE_BEGIN_DT

  let $HIPAA = 'N'
  DO Find-HIPAA-Plan

  let $ProcessCutOff = &CUT.COVERAGE_BEGIN_DT

  if $HIPAA = 'Y'
      do Check-No-Other-Coverage
      if $Active-Cov-Exist = 'N'
         exit-SELECT
      end-if
  end-if

FROM PS_R_BEN023 CUT
WHERE CUT.EMPLID          = &A.EMPLID
  AND CUT.DEPENDENT_BENEF = &A.DEPENDENT_BENEF
  AND (CUT.COVERAGE_ELECT = 'T'
    OR CUT.COVERAGE_ELECT = 'W')
  AND (CUT.COVERAGE_BEGIN_DT >= $FromDate
   AND CUT.COVERAGE_BEGIN_DT <= $ThruDate)
ORDER BY CUT.COVERAGE_BEGIN_DT DESC
end-SELECT
end-procedure

begin-procedure Check-No-Other-Coverage

  move 'N' to $Active-Cov-Exist

begin-SELECT
ACE.EMPLID

  move 'Y' to $Active-Cov-Exist

FROM PS_HEALTH_DEPENDNT ACE,
     PS_HEALTH_BENEFIT  ABE
WHERE ACE.EMPLID             = &A.EMPLID
  AND ACE.EMPL_RCD          <> &CUT.EMPL_RCD
  AND ACE.DEPENDENT_BENEF    = &A.DEPENDENT_BENEF
  AND ACE.PLAN_TYPE          = &CUT.PLAN_TYPE
  AND ABE.EMPLID             = ACE.EMPLID
  AND ABE.EMPL_RCD           = ACE.EMPL_RCD
  AND ABE.COBRA_EVENT_ID     = ACE.COBRA_EVENT_ID
  AND ABE.PLAN_TYPE          = ACE.PLAN_TYPE
  AND ABE.BENEFIT_NBR        = ACE.BENEFIT_NBR
  AND ABE.EFFDT              = ACE.EFFDT
  AND ABE.COVERAGE_BEGIN_DT  = (SELECT MAX(AB1.COVERAGE_BEGIN_DT)
                                FROM PS_HEALTH_BENEFIT  AB1
                                WHERE AB1.EMPLID              = ABE.EMPLID
                                  AND AB1.EMPL_RCD            = ABE.EMPL_RCD
                                  AND AB1.PLAN_TYPE           = ABE.PLAN_TYPE
                                  AND AB1.COVERAGE_BEGIN_DT  <= $ThruDate)
  AND ABE.COVERAGE_ELECT     = 'E'
end-SELECT
end-procedure

! The following procedure will fetch the plan administrator of the benefit plan that
! was current prior to loss of coverage.
!
begin-procedure Get-Administrator

  Let $Administrator   = ''
  Let $Admin_Address1  = ''
  Let $Admin_Address2  = ''
  Let $Admin_Address3  = ''
  Let $Admin_CityState = ''
  Let $Admin_Phone     = ''

begin-SELECT
AD.CONTACT_NAME

  Let $Administrator = rtrim(&AD.CONTACT_NAME,' ')

AD.ADDRESS1
AD.ADDRESS2
AD.ADDRESS3

  Let $Admin_Address1 = rtrim(&AD.ADDRESS1, ' ')
  Let $Admin_Address2 = rtrim(&AD.ADDRESS2, ' ')
  Let $Admin_Address3 = rtrim(&AD.ADDRESS3, ' ')

AD.CITY
AD.STATE
AD.POSTAL

  Let $Admin_CityState = rtrim(&AD.CITY, ' ')
  if rtrim(&AD.STATE,' ')  <> ''
      LET $Admin_CityState = $Admin_CityState || ',' || rtrim(&AD.STATE, ' ')
  end-if
  if rtrim(&AD.POSTAL, ' ') <> ''
     Let $Admin_CityState = $Admin_CityState || ' ' || rtrim(&AD.POSTAL, ' ')
  end-if

AD.PHONE

  Let $Admin_Phone = 'Phone#: ' || rtrim(&AD.PHONE,' ')


FROM PS_HEALTH_BENEFIT  HP,
     PS_BENEF_PLAN_TBL  BP,
     PS_BEN_PLN_CONTACT PC,
     PS_BEN_CONTACT_TBL AD
WHERE HP.EMPLID             = &A.EMPLID
AND   HP.EMPL_RCD           = &A.EMPL_RCD
AND   HP.COBRA_EVENT_ID     = &A.COBRA_EVENT_ID
AND   HP.PLAN_TYPE          = &A.PLAN_TYPE
AND   HP.BENEFIT_NBR        = &A.BENEFIT_NBR
AND   HP.COVERAGE_BEGIN_DT  = (SELECT MAX(HP1.COVERAGE_BEGIN_DT)
                               FROM PS_HEALTH_BENEFIT HP1
                               WHERE HP1.EMPLID             = HP.EMPLID
                                AND  HP1.EMPL_RCD           = HP.EMPL_RCD
                                AND  HP1.COBRA_EVENT_ID     = HP.COBRA_EVENT_ID
                                AND  HP1.BENEFIT_NBR        = HP.BENEFIT_NBR
                                AND  HP1.PLAN_TYPE          = HP.PLAN_TYPE
                                AND  HP1.EFFDT             <= &A.EFFDT
                                AND  HP1.COVERAGE_BEGIN_DT <= &A.COVERAGE_BEGIN_DT
                                AND  HP1.COVERAGE_ELECT     = 'E')
AND   HP.EFFDT              = (SELECT MAX(HP2.EFFDT)
                               FROM PS_HEALTH_BENEFIT HP2
                               WHERE HP2.EMPLID             = HP.EMPLID
                                AND  HP2.EMPL_RCD           = HP.EMPL_RCD
                                AND  HP2.COBRA_EVENT_ID     = HP.COBRA_EVENT_ID
                                AND  HP2.BENEFIT_NBR        = HP.BENEFIT_NBR
                                AND  HP2.PLAN_TYPE          = HP.PLAN_TYPE
                                AND  HP2.EFFDT             <= &A.EFFDT
                                AND  HP2.COVERAGE_BEGIN_DT  = HP.COVERAGE_BEGIN_DT
                                AND  HP2.COVERAGE_ELECT     = 'E')

AND   BP.PLAN_TYPE    = HP.PLAN_TYPE
AND   BP.BENEFIT_PLAN = HP.BENEFIT_PLAN
AND   BP.EFFDT =
        (SELECT MAX(BP1.EFFDT)
           FROM PS_BENEF_PLAN_TBL BP1
          WHERE BP1.PLAN_TYPE     = BP.PLAN_TYPE
            AND BP1.BENEFIT_PLAN  = BP.BENEFIT_PLAN
            AND BP1.EFFDT        <= &A.EFFDT)
AND   PC.PLAN_TYPE          = BP.PLAN_TYPE
AND   PC.BENEFIT_PLAN       = BP.BENEFIT_PLAN
AND   PC.EFFDT              = BP.EFFDT
AND   PC.BENEF_CONTACT_TYPE = 'HIPA'
AND   AD.BENEF_CONTACT_ID   = PC.BENEF_CONTACT_ID
AND   AD.EFFDT              = (SELECT MAX(AD1.EFFDT)
                              FROM PS_BEN_CONTACT_TBL AD1
                              WHERE AD1.BENEF_CONTACT_ID = AD.BENEF_CONTACT_ID
                                AND AD1.EFFDT           <= &A.EFFDT
                                AND AD1.EFF_STATUS       = 'A')
end-SELECT
end-procedure

!**************************************************************************
!* Procedure Name: Get-Employee-Info                                      *
!**************************************************************************
Begin-Procedure Get-Employee-Info

Begin-SELECT
PD.NAME

   do Get-Empl-Address ($Emplid,$AddrType, $AddressFound)
   Let $EE_Name     = rtrim(&PD.NAME, ' ')
   Let $EE_Address1 = rtrim($GETADDR_ADDRESS1, ' ')
   Let $EE_Address2 = rtrim($GETADDR_ADDRESS2, ' ')
   Let $EE_Address3 = rtrim($GETADDR_ADDRESS3, ' ')
   Let $EE_Address  = rtrim($GETADDR_CITY, ' ') || ', ' || rtrim($GETADDR_STATE, ' ')
   Let $EE_Address  = $EE_Address || ' ' || rtrim($GETADDR_POSTAL, ' ')

   do Get-Employee-NID

FROM PS_PERSON_NAME PD
WHERE PD.EMPLID = $Emplid
End-SELECT

End-Procedure


!**************************************************************************
!* Procedure Name: Get-Employee-NID
!**************************************************************************
Begin-Procedure Get-Employee-NID

Begin-SELECT
PN.NATIONAL_ID
   if rtrim(&PN.NATIONAL_ID, ' ') <> ''
     Let $SSN = edit(&PN.NATIONAL_ID, 'xxx-xx-xxxx')
   else
     Let $SSN = ''
   end-if

FROM PS_PERS_NID PN
WHERE PN.EMPLID          = $Emplid
  AND PN.COUNTRY         = {NID_COUNTRY}
  AND PN.PRIMARY_NID     = 'Y'
End-SELECT

End-Procedure


!**************************************************************************
!* Procedure Name: Get-Dependent-Info                                     *
!**************************************************************************
Begin-Procedure Get-Dependent-Info

Begin-SELECT
DB.SAME_ADDRESS_EMPL
DN.NAME
DB.ADDRESS1
DB.ADDRESS2
DB.ADDRESS3
DB.CITY
DB.STATE
DB.POSTAL

        Do    Get-Dependent-NID

        if &DB.SAME_ADDRESS_EMPL = 'Y'
          do Get-Empl-Address ($Emplid,$AddrType, $AddressFound)
          Let   $Dep_Address1 = rtrim($GETADDR_Address1, ' ')
          Let   $Dep_Address2 = rtrim($GETADDR_Address2, ' ')
          Let   $Dep_Address3 = rtrim($GETADDR_Address3, ' ')
          Let   $Dep_Address  = rtrim($GETADDR_City, ' ')||', '||rtrim($GETADDR_State, ' ')
          Let   $Dep_Address  = $Dep_Address ||' '||rtrim($GETADDR_Postal, ' ')
        else

          Let   $Dep_Address1 = rtrim(&DB.ADDRESS1, ' ')
          Let   $Dep_Address2 = rtrim(&DB.ADDRESS2, ' ')
          Let   $Dep_Address3 = rtrim(&DB.ADDRESS3, ' ')
          Let   $Dep_Address  = rtrim(&DB.CITY, ' ')||', '||rtrim(&DB.STATE, ' ')
          Let   $Dep_Address  = $Dep_Address ||' '||rtrim(&DB.POSTAL, ' ')
        end-if

          Let   $Dep_Name     = rtrim(&DN.NAME, ' ')

FROM PS_DEP_BEN_ADDR DB,
     PS_DEP_BEN_NAME DN
WHERE DB.EMPLID          = $Emplid
  AND DB.DEPENDENT_BENEF = $Dependent_benef
  AND DB.EFFDT = ( SELECT MAX(DB1.EFFDT)
                   FROM PS_DEP_BEN_ADDR DB1
                   WHERE DB1.EMPLID = DB.EMPLID
                   AND   DB1.DEPENDENT_BENEF = DB.DEPENDENT_BENEF
                   AND   DB1.EFFDT <= $ASOFTODAY)
  AND DN.EMPLID          = DB.EMPLID
  AND DN.DEPENDENT_BENEF = DB.DEPENDENT_BENEF
  AND DN.EFFDT = ( SELECT MAX(DN1.EFFDT)
                   FROM PS_DEP_BEN_NAME DN1
                   WHERE DN1.EMPLID = DN.EMPLID
                   AND   DN1.DEPENDENT_BENEF = DN.DEPENDENT_BENEF
                   AND   DN1.EFFDT <= $ASOFTODAY)
End-SELECT
End-Procedure


!**************************************************************************
!* Procedure Name: Get-Dependent-NID                                      *
!**************************************************************************
Begin-Procedure Get-Dependent-NID
  Let   $Dep_SSN = ' '
Begin-SELECT
H.NATIONAL_ID
        If   rtrim(&H.NATIONAL_ID, ' ') <> ''
          Let   $Dep_SSN = edit(&H.NATIONAL_ID, 'xxx-xx-xxxx')
        else
          Let   $Dep_SSN = rtrim(&H.NATIONAL_ID, ' ')
        End-If
FROM PS_DEP_BENEF_NID H
WHERE H.EMPLID           = $Emplid
  AND H.DEPENDENT_BENEF  = $Dependent_benef
  AND H.COUNTRY          = {NID_COUNTRY}
  AND H.PRIMARY_NID      = 'Y'

End-SELECT


End-Procedure


!**************************************************************************
!* Procedure Name: Check-For-Other-Coverage                               *
!**************************************************************************
! See if the employee has active coverage in the same plan type, for a
! different BENEFIT_RCD_NBR, as of the end of the reporting period. If there
! is,  verify if dependent is attached to this coverage.

Begin-Procedure Check-For-Other-Coverage
 Let $Other_Coverage = 'N'

Begin-Select
OC.COVERAGE_ELECT
OC.EMPL_RCD
OC.COBRA_EVENT_ID
OC.BENEFIT_NBR
OC.EFFDT

        If &OC.COVERAGE_ELECT = 'E'

           do Check-Dep-Coverage

        End-If

 FROM PS_HEALTH_BENEFIT OC
WHERE OC.EMPLID     = &A.EMPLID
  AND (OC.EMPL_RCD       <> &A.EMPL_RCD OR
       OC.COBRA_EVENT_ID <> &A.COBRA_EVENT_ID)
  AND OC.PLAN_TYPE  = &A.PLAN_TYPE
  AND OC.COVERAGE_BEGIN_DT = (SELECT MAX(OC1.COVERAGE_BEGIN_DT)
                               FROM PS_HEALTH_BENEFIT OC1
                               WHERE OC1.EMPLID             = OC.EMPLID
                                 AND OC1.EMPL_RCD           = OC.EMPL_RCD
                                 AND OC1.BENEFIT_NBR        = OC.BENEFIT_NBR
                                 AND OC1.COBRA_EVENT_ID     = OC.COBRA_EVENT_ID
                                 AND OC1.PLAN_TYPE          = OC.PLAN_TYPE
                                 AND OC1.COVERAGE_BEGIN_DT <= $ThruDate)
ORDER BY OC.EMPL_RCD
End-SELECT

End-Procedure

begin-procedure Check-Dep-Coverage
begin-SELECT
HED.EMPLID

     Let $Other_Coverage = 'Y'

FROM PS_HEALTH_DEPENDNT HED
WHERE HED.EMPLID          = &A.EMPLID
  AND HED.EMPL_RCD        = &OC.EMPL_RCD
  AND HED.COBRA_EVENT_ID  = &OC.COBRA_EVENT_ID
  AND HED.PLAN_TYPE       = &A.PLAN_TYPE
  AND HED.BENEFIT_NBR     = &OC.BENEFIT_NBR
  AND HED.EFFDT           = &OC.EFFDT
  AND HED.DEPENDENT_BENEF = &A.DEPENDENT_BENEF
end-SELECT
end-procedure


!**************************************************************************
!* Procedure Name: Get-Enrolled-Health-Info                               *
!**************************************************************************
Begin-Procedure Get-Enrolled-Health-Info
 Let   $En_Effdt = ''
 Let   $LastCvgDt_dtu   = $TermDate_dtu
 Let   #Creditable_Days = 0
 Let   #Gap_Days        = 0
 Let   $LastStatus      = rtrim(&A.COVERAGE_ELECT, ' ')
 Let   $CvgDt_Began = ''

Begin-Select
HB.EMPLID
HB.EMPL_RCD
HB.DEPENDENT_BENEF
HB.BENEFIT_NBR
HB.COBRA_EVENT_ID
HB.EFFDT
HB.COVERAGE_BEGIN_DT
HB.PLAN_TYPE
HB.BENEFIT_PLAN
HB.COVERAGE_ELECT


                Do Format-DateTime(&HB.COVERAGE_BEGIN_DT, $CvgDt_def, {DEFDATE}, '', '')

                Let   $Status = rtrim(&HB.COVERAGE_ELECT, ' ')
                Let   $FieldName  = 'COVERAGE_ELECT'
                Let   $FieldValue = $Status
                Let   $AsOfDate   = &HB.EFFDT
                Do Read-Translate-Table

                if $Status = 'E'
                    Let $CvgDt_Began = $CvgDt_def
                    let $CovDate     = &HB.COVERAGE_BEGIN_DT
                ELSE
                    Let $CvgDt_Began = ''
                    let $CovDate     = ''
                end-if

                Do Calc-Credit-Period

                Let   $LastStatus    = $Status
                Let   $LastCvgDt_dtu = $CvgDt_dtu
                If   ((#Creditable_Days >= 550  or  #Gap_Days >= 63) And
                    $Status = 'E')
                  Exit-Select
               End-If

 FROM PS_R_BEN023 HB
WHERE HB.EMPLID            = &A.EMPLID
  AND HB.EMPL_RCD          = &A.EMPL_RCD
  AND HB.DEPENDENT_BENEF   = &A.DEPENDENT_BENEF
  AND HB.PLAN_TYPE         = &A.PLAN_TYPE
  AND HB.COVERAGE_ELECT = 'E'
  AND HB.COVERAGE_BEGIN_DT = (SELECT MIN(HB1.COVERAGE_BEGIN_DT)
                              FROM PS_R_BEN023 HB1
                              WHERE HB1.EMPLID = HB.EMPLID
                              AND HB1.EMPL_RCD = HB.EMPL_RCD
                              AND HB1.COVERAGE_BEGIN_DT < &A.COVERAGE_BEGIN_DT
                              AND HB1.DEPENDENT_BENEF = HB.DEPENDENT_BENEF
                              AND HB1.COBRA_EVENT_ID = HB.COBRA_EVENT_ID
                              AND HB1.PLAN_TYPE = HB.PLAN_TYPE
                              AND HB1.COVERAGE_ELECT = HB.COVERAGE_ELECT 
                              AND NOT EXISTS (SELECT 'X'
                                              FROM PS_R_BEN023 X 
                                              WHERE X.EMPLID = HB1.EMPLID
                                              AND X.EMPL_RCD = HB1.EMPL_RCD
                                              AND X.DEPENDENT_BENEF = HB1.DEPENDENT_BENEF 
                                              AND X.COBRA_EVENT_ID = HB1.COBRA_EVENT_ID
                                              AND X.PLAN_TYPE = HB1.PLAN_TYPE
                                              AND X.COVERAGE_ELECT <> 'E'
                                              AND X.EFFDT > HB1.EFFDT
                                              AND X.EFFDT < &A.EFFDT)
                                              AND EXISTS (SELECT 'X'
                                              FROM PS_R_BEN023 X1
                                              WHERE X1.EMPLID = HB1.EMPLID
                                              AND X1.EMPL_RCD = HB1.EMPL_RCD
                                              AND X1.DEPENDENT_BENEF = HB1.DEPENDENT_BENEF
                                              AND X1.COBRA_EVENT_ID = HB1.COBRA_EVENT_ID
                                              AND X1.PLAN_TYPE = HB1.PLAN_TYPE
                                              AND X1.COVERAGE_ELECT = HB1.COVERAGE_ELECT 
                                              AND X1.EFFDT = HB1.EFFDT
                                              AND X1.EFFDT < &A.EFFDT))
ORDER BY HB.COVERAGE_BEGIN_DT DESC,HB.COVERAGE_ELECT ASC

End-SELECT

 If   #Creditable_Days > 550
   Let   #Creditable_Days = 550
   Let   $Creditable_Msg  = '(18 months)'
 else
   Let   $Creditable_Msg  = ' '
 End-If

End-Procedure

!**************************************************************************
!* Procedure Name: Get-Enrolled-Health-Plan-Info                          *
!**************************************************************************
! Note that we need to consider enrollments in all Benefit_Rcd_Nbrs (Empl_Rcd)
! since the certificate applies to an employee, not a specific Benefit_Rcd#
! for the employee. This is why the WHERE clause does not include a match
! on Empl_Rcd.
!
Begin-Procedure Get-Enrolled-Health-Plan-Info

Begin-Select
HB2.EMPLID
HB2.EMPL_RCD
HB2.BENEFIT_NBR
HB2.COBRA_EVENT_ID
HB2.EFFDT
HB2.COVERAGE_BEGIN_DT
HB2.PLAN_TYPE
HB2.BENEFIT_PLAN
HB2.COVERAGE_ELECT

                Let #Cobra_Event_Id = &HB2.COBRA_EVENT_ID

                Let $Benefit_Plan = rtrim(&HB2.BENEFIT_PLAN, ' ')

                if $Status = 'E'

                  Do Get-Provider-Info
                  do Get-Waiting-Period
                end-if

 FROM PS_R_BEN023 HB2
WHERE HB2.EMPLID            = &A.EMPLID
  AND HB2.EMPL_RCD          = &A.EMPL_RCD
  AND HB2.PLAN_TYPE         = &A.PLAN_TYPE
  AND HB2.COVERAGE_BEGIN_DT = (SELECT MAX(HB3.COVERAGE_BEGIN_DT)
                              FROM PS_HEALTH_BENEFIT HB3
                              WHERE HB3.EMPLID = &HB.EMPLID
                              AND HB3.EMPL_RCD = &HB.EMPL_RCD
                              AND (HB3.COVERAGE_BEGIN_DT < &A.COVERAGE_BEGIN_DT
                              OR HB3.EFFDT < &A.EFFDT)
                              AND HB3.PLAN_TYPE = &HB.PLAN_TYPE
                              AND NOT EXISTS (SELECT 'X'
                                             FROM PS_HEALTH_BENEFIT X1
                                            WHERE X1.PLAN_TYPE = HB3.PLAN_TYPE
                                              AND X1.EMPLID = HB3.EMPLID
                                              AND X1.EMPL_RCD = HB3.EMPL_RCD
                                              AND X1.COBRA_EVENT_ID = HB3.COBRA_EVENT_ID
                                              AND X1.COVERAGE_ELECT = 'E'
                                              AND X1.EFFDT > HB3.EFFDT
                                              AND X1.EFFDT < &A.EFFDT))
ORDER BY HB2.COVERAGE_BEGIN_DT DESC, HB2.COVERAGE_ELECT ASC

End-SELECT

End-Procedure

!**************************************************************************
!* Procedure Name: Get-Provider-Info                                      *
!**************************************************************************
Begin-Procedure Get-Provider-Info

Begin-SELECT
M.DESCR              
   Let $GrpPlan= &M.DESCR

VN.NAME1
  Let $Provider = rtrim(&VN.NAME1, ' ')

M.GROUP_NBR                 
   Let $GrpNBR= &M.GROUP_NBR

FROM PS_BENEF_PLAN_TBL M,
     PS_VENDOR VN,
     PS_VENDOR_ADDR VA
WHERE M.PLAN_TYPE    = $Plan_Type
AND   M.BENEFIT_PLAN = $Benefit_Plan
AND   M.EFFDT =
        (SELECT MAX(M1.EFFDT)
           FROM PS_BENEF_PLAN_TBL M1
          WHERE M1.PLAN_TYPE     = M.PLAN_TYPE
            AND M1.BENEFIT_PLAN  = M.BENEFIT_PLAN
            AND M1.EFFDT        <= &A.EFFDT)
AND  VN.SETID     = M.SETID
AND  VN.VENDOR_ID = M.VENDOR_ID
AND  VA.SETID     = VN.SETID
AND  VA.VENDOR_ID = VN.VENDOR_ID
AND  VA.ADDRESS_SEQ_NUM = VN.REMIT_ADDR_SEQ_NUM
AND  VA.EFFDT =
        (SELECT MAX(EFFDT)
           FROM PS_VENDOR_ADDR
          WHERE SETID     = VA.SETID
            AND VENDOR_ID = VA.VENDOR_ID
            AND ADDRESS_SEQ_NUM = VA.ADDRESS_SEQ_NUM
            AND EFFDT    <= $AsOfToday)
End-SELECT

End-Procedure

begin-procedure Get-Waiting-Period

  move 0 to #waiting-days

begin-SELECT
HP.WAIT_PERIOD_DD

  let #waiting-days = &HP.WAIT_PERIOD_DD

FROM PS_BENEF_PLAN_TBL  BP,
     PS_HEALTH_PLAN_TBL HP
WHERE BP.PLAN_TYPE    = $Plan_Type
AND   BP.BENEFIT_PLAN = $Benefit_Plan
AND   BP.EFFDT =
        (SELECT MAX(BP1.EFFDT)
           FROM PS_BENEF_PLAN_TBL BP1
          WHERE BP1.PLAN_TYPE     = BP.PLAN_TYPE
            AND BP1.BENEFIT_PLAN  = BP.BENEFIT_PLAN
            AND BP1.EFFDT        <= &A.EFFDT)
AND  HP.PLAN_TYPE    = BP.PLAN_TYPE
AND  HP.BENEFIT_PLAN = BP.BENEFIT_PLAN
AND  HP.EFFDT  =
       (SELECT MAX(HP1.EFFDT)
          FROM PS_HEALTH_PLAN_TBL HP1
         WHERE HP1.PLAN_TYPE      = HP.PLAN_TYPE
           AND HP1.BENEFIT_PLAN   = HP.BENEFIT_PLAN
           AND HP1.EFFDT         <= &A.EFFDT)
end-SELECT

  let $WaitingDate = ''
  if $CovDate <> '' and #waiting-days <> 0

     Do Convert-To-DTU-Date($CovDate, $CvgDate_dtu)
     Do dtu-subtract-days($CvgDate_dtu, #waiting-days, $WaitDate_dtu)
     Do Convert-From-DTU-Date($WaitDate_dtu, $WaitDate)
     do Format-DateTime($WaitDate, $WaitingDate, {DEFDATE}, '', '')

  else
     let $WaitingDate = 'Not Applicable'
  end-if


end-procedure


!**************************************************************************
!* Procedure Name: Calc-Credit-Period                                     *
!**************************************************************************
Begin-Procedure Calc-Credit-Period

 Let   #Span_Days = 0
 Do Convert-To-DTU-Date(&HB.COVERAGE_BEGIN_DT, $CvgDt_dtu)
 Do DTU-Diff-Days($CvgDt_Dtu, $LastCvgDt_dtu, #Span_Days)

 If   ($LastStatus = 'T' or $LastStatus = 'W')
   If   $Status = 'E'
     If   $Freeze_Credit = 'N'
       Let   #Creditable_Days = #Creditable_Days + #Span_Days
       Let   #Creditable_Days = #Creditable_Days + #Gap_Days
     End-If
     Let   #Gap_Days = 0
   else
     Let   #Gap_Days = #Gap_Days + #Span_Days
 End-If
 else
   If   $Status = 'E'
     If   $Freeze_Credit = 'N'
       Let   #Creditable_Days = #Creditable_Days + #Span_Days
     End-If
     Let   #Gap_Days = 0
   else
     Let   #Gap_Days = #Gap_Days + #Span_Days
   End-If
 End-If
 If   #Gap_Days > 62
   ! Coverage in effect prior to a significant break is not creditable.
   Let   $Freeze_Credit = 'Y'
 End-If

End-Procedure


!**************************************************************************
!* Procedure Name: Insert-Table                                           *
!**************************************************************************
Begin-Procedure Insert-Table

  Let $sql-statement = 'BEN023.SQR,Insert-Termination-Row,Insert,PS_R_BEN023'
  let $covstat       =  $a_coverage_status

Begin-SQL On-Error=Invalid-Insert
INSERT INTO PS_R_BEN023
(EMPLID, EMPL_RCD, DEPENDENT_BENEF, COBRA_EVENT_ID,PLAN_TYPE, BENEFIT_NBR, EFFDT, COVERAGE_BEGIN_DT,
 COVERAGE_ELECT,BENEFIT_PLAN)
  VALUES (&E.EMPLID, &E.EMPL_RCD, $b_dependent_benef, &E.COBRA_EVENT_ID,
          &E.PLAN_TYPE, &E.BENEFIT_NBR, $T_Effdt, $T_Coverage_begin_dt,
          $a_coverage_status, ' ')

End-SQL

  Let $sql-statement = 'BEN023.SQR,Insert-Election-Row,Insert,PS_R_BEN023'
  let $covstat       =  'E'

Begin-SQL On-Error=Invalid-Insert
INSERT INTO PS_R_BEN023
(EMPLID, EMPL_RCD, DEPENDENT_BENEF, COBRA_EVENT_ID,PLAN_TYPE, BENEFIT_NBR, EFFDT, COVERAGE_BEGIN_DT,
 COVERAGE_ELECT,BENEFIT_PLAN)
  VALUES (&E.EMPLID, &E.EMPL_RCD, $b_dependent_benef, &E.COBRA_EVENT_ID,
          &E.PLAN_TYPE, &E.BENEFIT_NBR, &E.EFFDT, $E_Coverage_begin_dt,
          'E', $E_Benefit_Plan)
End-SQL

End-Procedure

begin-procedure Insert-CoverageB

  Let $sql-statement = 'BEN023.SQR,Insert-Current-Coverage,Insert,PS_R_BEN023'
  let $covstat       =  &E.COVERAGE_ELECT

Begin-SQL On-Error=Invalid-Insert 

INSERT INTO PS_R_BEN023
(EMPLID, EMPL_RCD, DEPENDENT_BENEF, COBRA_EVENT_ID,PLAN_TYPE, BENEFIT_NBR, EFFDT, COVERAGE_BEGIN_DT,
 COVERAGE_ELECT,BENEFIT_PLAN)
  VALUES (&E.EMPLID, &E.EMPL_RCD, $b_dependent_benef, &E.COBRA_EVENT_ID,
          &E.PLAN_TYPE, &E.BENEFIT_NBR, &E.EFFDT, $E_Coverage_begin_dt,
          &E.COVERAGE_ELECT, $E_Benefit_Plan)
End-SQL
end-procedure

begin-procedure Maintain-History
  let $Insert-History = 'Y'
begin-SELECT
LEH.EMPLID
   let $Insert-History = 'N'
FROM PS_BEN_LETTER_HIST LEH
WHERE LEH.EMPLID          = &A.EMPLID
  AND LEH.DEPENDENT_BENEF = &A.DEPENDENT_BENEF
  AND LEH.LETTER_CD       = 'HDE'
  AND LEH.COVERAGE_END_DT = $ProcessCutOff
end-SELECT

  if $Insert-History = 'Y'
     do Add-Letter-History
  end-if
end-procedure

begin-procedure Add-Letter-History
Begin-SQL ON-ERROR=Insert-Error

INSERT INTO PS_BEN_LETTER_HIST
(EMPLID,DEPENDENT_BENEF,LETTER_CD,LETTER_PRINTED_DT,COVERAGE_END_DT)
VALUES (&A.EMPLID,&A.DEPENDENT_BENEF,'HDE',$AsOfToday,$ProcessCutOff)
End-SQL
end-procedure

begin-procedure Insert-Error

  display 'ERROR Inserting history:  ' noline
  display 'Emplid -> ' noline
  display &A.EMPLID

end-procedure

begin-procedure Invalid-Insert

  display '**Invalid Insert**'
  display $sql-statement

  display 'Emplid -> '           noline
  display &E.EMPLID              noline
  display ' Empl_Rcd# -> '       noline
  display &E.EMPL_RCD            noline
  display ' Dependent -> '       noline
  display $b_dependent_benef     noline
  display ' Plan Type -> '       noline
  display &E.PLAN_TYPE           noline
  display ' Cobra EventID -> '   noline
  display &E.COBRA_EVENT_ID      noline
  display ' CoverageElect -> '   noline
  display $covstat

end-procedure

!***********************************
Begin-Procedure Print-Header-Instructions
!***********************************
  print ' '                     (+2)

  uppercase $ReportTitle
  print $ReportTitle            (+1,40) font = 7

  print $ReportDate             (+1,55)

  print '_'                     (+3,2,123)       fill

  let $v1 = 'IMPORTANT — KEEP THIS CERTIFICATE. '
  let $v1 = $v1 || 'This certificate is evidence of your coverage under this plan.  Under a federal'
  print $v1 (+1, {col_txt})

  let $v2 = 'law known as HIPAA, you may need evidence of your '
  let $v2 = $v2 || 'coverage to reduce a preexisting condition exclusion period '
  print $v2 (+1, {col_txt})

  let $v3 = 'under another plan, to help you get special enrollment '
  let $v3 = $v3 || 'in another plan, or to get certain types of individual health coverage '
  print $v3 (+1, {col_txt})

  let $v4 = 'even if you have health problems.'
  print $v4 (+1, {col_txt})

  print '_'                     (+1,2,123)       fill
end-procedure

!***********************************
Begin-Procedure Print-Employee-Data
!***********************************
  Let $group_printed = 'N' 
  print 'Dependent:'  (+2, {col_txt})
  Print $Dep_Name     ( 0, {col_entry})

  print 'Participant (Employee):'  (+2, {col_txt})
  print $EE_Name                   ( 0, {col_entry})
  print 'Participant ID: '         (+1, {col_entry})
  print $SSN                       ( 0, +2)

  print $EE_Address1               (+1, {col_entry})

 if $EE_Address2 <> ''
     print $EE_Address2            (+1, {col_entry})
  end-if
  if $EE_Address3 <> ''
     print $EE_Address3            (+1, {col_entry})
  end-if

  print $EE_Address                (+1, {col_entry})

  if $EE_Address2 = ''
     print ' '                     (+1)
  end-if

  if $EE_Address3 = ''
     print ' '                     (+1)
  end-if

  print 'Plan Administrator:'  (+2, {col_txt} )
  print $Administrator             ( 0, {col_entry})

  print 'Group Health Plan:'    (+0,71,{len_benefit_plan})
  print $GrpPlan                 (+0,95)

  print $Admin_Address1            (+1, {col_entry})

  print 'Plan Provider:'             (+0,71,{len_provider})
  Print $Provider            (+0,95,{len_provider})

  if $Admin_Address2 <> ''
     print $Admin_Address2         (+1, {col_entry})
     Let $group_printed = 'E'
  end-if

  if $group_printed = 'E'
     print 'Group ID:'              (+0,71,{len_group})
     print $GrpNBR                  (+0,95)
     Let $group_printed = 'Y'
  end-if

  if $Admin_Address3 <> ''
    print $Admin_Address3         (+1, {col_entry})
     if $group_printed <> 'Y'
     Let $group_printed = 'E'
     end-if
  end-if

  if $group_printed = 'E'
     print 'Group ID:'              (+0,71,{len_group})
     print $GrpNBR                  (+0,95)
     Let $group_printed = 'Y'
  end-if

  if $Admin_CityState <> ''
     print $Admin_CityState        (+1, {col_entry})
     if $group_printed <> 'Y'
     Let $group_printed = 'E'
     end-if
  end-if

  if $group_printed = 'E'
     print 'Group ID:'              (+0,71,{len_group})
     print $GrpNBR                  (+0,95)
     Let $group_printed = 'Y'
  end-if

  if $Admin_Phone <> ''
     print $Admin_Phone            (+1, {col_entry})
     if $group_printed <> 'Y'
     Let $group_printed = 'E'
     end-if
  end-if

  if $group_printed = 'E'
     print 'Group ID:'              (+0,71,{len_group})
     print $GrpNBR                  (+0,95)
     Let $group_printed = 'Y'
  end-if

  if $group_printed = 'N'
     print 'Group ID:'              (+1,71,{len_group})
     print $GrpNBR                  (+0,95)
     Let $group_printed = 'Y'
  end-if

  Print 'Coverage Begin: '         (+2,{col_txt})
  Print $CvgDt_Began                     ( 0, {col_entry})
  Print 'Waiting Period Began: '         (+0,71)
  Print $WaitingDate                     (+0,95,16)

  Print 'Coverage Ended: '         (+1,{col_txt})
  Print $CvgDt_Ended                     ( 0, {col_entry})

  Print 'Creditable Coverage: '         (+0,71,24)
  if #Creditable_Days > 999
  Print #Creditable_Days                 (+0,95) Edit 9999
  end-if
  if #Creditable_Days > 99
        if #Creditable_Days < 999
        Print #Creditable_Days                 (+0,95) Edit 999
        end-if
  end-if
  if #Creditable_Days > 9
        if #Creditable_Days < 99
        Print #Creditable_Days                 (+0,95) Edit 99
        end-if
  end-if

  if #Creditable_Days < 9
    Print #Creditable_Days                 (+0,95) 
  end-if
  Print 'Days'                           (+0,+1)
  Print $Creditable_Msg                  (+0,+1)
  Print ' '                              (+1,1)

  print '_'                     (+1,2,123)       fill


end-procedure

!***********************************
Begin-Procedure Print-educational-instructions
!***********************************

  print 'Statement of HIPAA Portability Rights' (+3,) bold font = 15 center
  let $Tmp_Text = 'Preexisting condition exclusions' 
  print $Tmp_Text                                (+3,{col_txt}) bold underline 

  let $Tmp_Text = '. Some group health plans restrict coverage for medical conditions present before an individual’s '
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'enrollment. These restrictions are known as “preexisting condition exclusions.”  A preexisting condition exclusion can apply only'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'to conditions for which medical advice, diagnosis, care, or treatment was recommended or received within the 6 months before your'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '“enrollment date.”  Your enrollment date is your first day of coverage under the plan, or, if there is a waiting period, the first'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'day of your waiting period (typically, your first day of work).  In addition, a preexisting condition exclusion cannot last'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'for more than 12 months after your enrollment date (18 months if you are a late enrollee).   Finally, a preexisting condition'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'exclusion cannot apply to pregnancy and cannot apply to a child who is enrolled in health coverage within 30 days after birth,'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'adoption, or placement for adoption.'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'If a plan imposes a preexisting condition exclusion, the length of the exclusion must be reduced by the amount of your prior'
  print $Tmp_Text                                (+2,{col_txt}) 
  let $Tmp_Text = 'creditable coverage.  Most health coverage is creditable coverage, including group health plan coverage, COBRA continuation'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'coverage, coverage under an individual health policy, Medicare, Medicaid, State Children'
  let $Tmp_Text = $Tmp_Text || ''''
  let $Tmp_Text = $Tmp_Text || 's Health Insurance Program (SCHIP), and'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'coverage through high-risk pools and the Peace Corps. Not all forms of creditable coverage are required to provide certificates'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'like this one. If you do not receive a certificate for past coverage, talk to your new plan administrator.'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'You can add up any creditable coverage you have, including the coverage shown on this certificate.  However, if at any'
  print $Tmp_Text                                (+2,{col_txt}) 
  let $Tmp_Text = 'time you went for 63 days or more without any coverage (called a break in coverage) a plan may not have to count the coverage you'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'had before the break.'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, once your coverage ends, you should try to obtain alternative coverage as soon as possible to avoid a 63-day '  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'break.  You may use this certificate as evidence of your creditable coverage to reduce the length of any preexisting condition '  
  print $Tmp_Text                                (+1,6) 
  let $Tmp_Text = 'exclusion if you enroll in another plan.'  
  print $Tmp_Text                                (+1,6) 

  let $Tmp_Text = 'Right to get special enrollment in another plan'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. Under HIPAA, if you lose your group health plan coverage, you may be able to get'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'into another group health plan for which you are eligible (such as a spouse’s plan), even if the plan generally does not'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'accept late enrollees, if you request enrollment within 30 days.(Additional special enrollment rights are triggered by'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'marriage, birth, adoption, and placement for adoption.)'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, once your coverage ends, if you are eligible for coverage in another plan (such as a spouse’s plan), you should '  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'request special enrollment as soon as possible.'  
  print $Tmp_Text                                (+1,6) 

  let $Tmp_Text = 'Prohibition against discrimination based on a health factor'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. Under HIPAA, a group health plan may not keep you'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = '(or your dependents) out of the plan based on anything related to your health.  Also, a group health plan may not charge you'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '(or your dependents) more for coverage, based on health, than the amount charged a similarly situated individual.'
  print $Tmp_Text                                (+1,{col_txt}) 

new-page

  let $Tmp_Text = 'Right to individual health coverage'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. Under HIPAA, if you are an “eligible individual,” you have a right to buy certain'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'individual health policies (or in some states, to buy coverage through a high-risk pool) without a preexisting condition'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'exclusion.  To be an eligible individual, you must meet the following requirements:'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = '• You have had coverage for at least 18 months without a break in coverage of 63 days or more;'  
  print $Tmp_Text                                (+2,{col_txt})
  let $Tmp_Text = '• Your most recent coverage was under a group health plan (which can be shown by this certificate);'  
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '• Your group coverage was not terminated because of fraud or nonpayment of premiums;'  
  print $Tmp_Text                                (+1,{col_txt})
  let $Tmp_Text = '• You are not eligible for COBRA continuation coverage or you have exhausted your COBRA benefits (or continuation coverage '  
  print $Tmp_Text                                (+1,{col_txt})
  let $Tmp_Text = 'under a similar state provision); and'  
  print $Tmp_Text                                (+1,6)
  let $Tmp_Text = '• You are not eligible for another group health plan, Medicare, or Medicaid, and do not have any other health insurance coverage.'  
  print $Tmp_Text                                (+1,{col_txt})

  let $Tmp_Text = 'The right to buy individual coverage is the same whether you are laid off, fired, or quit your job. '  
  print $Tmp_Text                                (+2,{col_txt})
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, if you are interested in obtaining individual coverage and you meet the other criteria to be an eligible'  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'individual, you should apply for this coverage as soon as possible to avoid losing your eligible individual'
  print $Tmp_Text                                (+1,6) 
  let $Tmp_Text = 'status due to a 63-day break.'
  print $Tmp_Text                                (+1,6) 
  let $Tmp_Text = 'Special information for people on FMLA leave'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. If you are taking leave under the Family and Medical Leave Act (FMLA) and you drop '  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'health coverage during your leave, any days without health coverage while on FMLA leave will not count towards a 63-day break in '
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'coverage. In addition, if you do not return from leave, the 30-day period to request special enrollment in another plan will not'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'start before your FMLA leave ends.'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '--'   
  print $Tmp_Text                                (+1,5) bold
  let $Tmp_Text = '>'   
  print $Tmp_Text                                (+0,6) bold
  let $Tmp_Text = ' Therefore, when you apply for other health coverage, you should tell your plan administrator of health insurer about any'  
  print $Tmp_Text                                (+0,6) 
  let $Tmp_Text = 'prior FMLA leave.'  
  print $Tmp_Text                                (+1,6) 

  let $Tmp_Text = 'State flexibility'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. This certificate describes minimum HIPAA protections under federal law.  States may require insurers and HMOs'  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'to provide additional protections to individuals in that state.'
  print $Tmp_Text                                (+1,{col_txt}) 

  let $Tmp_Text = 'For more information'  
  print $Tmp_Text                                (+2,{col_txt})  bold underline
  let $Tmp_Text = '. If you have questions about your HIPAA rights, you may contact your state insurance department or the U.S. '  
  print $Tmp_Text                                (+0,)
  let $Tmp_Text = 'Department of Labor, Employee Benefits Security Administration (EBSA) toll-free at 1-866-444-3272 (for free HIPAA publications'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'ask for publications concerning changes in health care laws).  You may also contact the CMS publication hotline at'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = '1-800-633-4227 (ask for “Protecting Your Health Insurance Coverage”).  These publications and other useful information are also'
  print $Tmp_Text                                (+1,{col_txt}) 
  let $Tmp_Text = 'available on the Internet at: '
  print $Tmp_Text                                (+1,{col_txt})  
  let $Tmp_Text = 'http://www.dol.gov/ebsa'
  print $Tmp_Text                                (+0,+1)  underline
  let $Tmp_Text = ', the DOL’s interactive web pages - Health Elaws, or '
  print $Tmp_Text                                (,+1)    
  let $Tmp_Text = 'http://www.cms.hhs.gov/hipaa1'
  print $Tmp_Text                                (+1,{col_txt}) underline
let $Tmp_Text = '.'
  print $Tmp_Text                                (,)
end-procedure
! *-----------------------------------------------------------------------*

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getaddr.sqc'   !Get current address for specified address type
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date AND time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'askftd.sqc'    !Asks the user to enter the from and thru dates
#Include 'bnrunctl.sqc'  !Get application run control parameters
#Include 'bngetval.sqc'  !Get BEN values mask routines
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !date math
#Include 'readxlat.sqc'  !Read xlate table

! *------------------------* End Of Report *------------------------------*
