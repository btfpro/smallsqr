!***********************************************************************
!  TAX712: Print contents of Tax Form Print Parameters Table           *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:31:15                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term

end-report

begin-procedure Init-Report
  move 'TAX712' to $ReportID

  do Init-DateTime
  do Get-Current-DateTime

  do Stdapi-Init

  do Get-Report-Language
  move $Tax712_ReportTitle to $ReportTitle
  display $ReportTitle

  if $Prcs_Process_Instance = ''
    do Prompts
  else
     do Select-Parameters
     do Convert-Parameters
  end-if

end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY712 program ids
  do Init_Report_Translation($ReportID,$language_cd)

  do Get_Field_Information ('TAX712', 'REPORTTITLE',  $Tax712_ReportTitle, #dummy_width)
  do Get_Field_Information ('TAX712', 'FORMID',       $Tax712_FormID,      #dummy_width)

  do Get_Field_Information ('TAX712', 'LASTPRTLN1',   $Tax712_LastPrtLn1,  #dummy_width)
  do Get_Field_Information ('TAX712', 'LOCPERCOL1',   $Tax712_LocPerCol1,  #dummy_width)

  do Get_Field_Information ('TAX712', 'EFFDT2',       $Tax712_EffDt2,      #dummy_width)
  do Get_Field_Information ('TAX712', 'FORMID2',      $Tax712_FormID2,     #dummy_width)
  do Get_Field_Information ('TAX712', 'LAYOUT_STY2',  $Tax712_Layout_Sty2, #dummy_width)
  do Get_Field_Information ('TAX712', 'TOP_MARGIN2',  $Tax712_Top_Margin2, #dummy_width)
  do Get_Field_Information ('TAX712', 'TOP_SPLIT2',   $Tax712_Top_Split2,  #dummy_width)
  do Get_Field_Information ('TAX712', 'CNTR_SPLIT2',  $Tax712_Cntr_Split2, #dummy_width)
  do Get_Field_Information ('TAX712', 'BOTM_SPLIT2',  $Tax712_Botm_Split2, #dummy_width)
  do Get_Field_Information ('TAX712', 'LASTPRTLN2',   $Tax712_LastPrtLn2,  #dummy_width)
  do Get_Field_Information ('TAX712', 'LOCPERCOL2',   $Tax712_LocPerCol2,  #dummy_width)

  do Get_Field_Information ('TAX712', 'EFFDT3',       $Tax712_EffDt3 ,     #dummy_width)
  do Get_Field_Information ('TAX712', 'FORMID3',      $Tax712_FormID3 ,    #dummy_width)
  do Get_Field_Information ('TAX712', 'LAYOUT_STY3',  $Tax712_Layout_Sty3, #dummy_width)
  do Get_Field_Information ('TAX712', 'TOP_MARGIN3',  $Tax712_Top_Margin3, #dummy_width)
  do Get_Field_Information ('TAX712', 'TOP_SPLIT3',   $Tax712_Top_Split3,  #dummy_width)
  do Get_Field_Information ('TAX712', 'CNTR_SPLIT3',  $Tax712_Cntr_Split3, #dummy_width)
  do Get_Field_Information ('TAX712', 'BOTM_SPLIT3',  $Tax712_Botm_Split3, #dummy_width)
  do Get_Field_Information ('TAX712', 'LASTPRTLN3',   $Tax712_LastPrtLn3,  #dummy_width)
  do Get_Field_Information ('TAX712', 'LOCPERCOL3',   $Tax712_LocPerCol3,  #dummy_width)
  do Get_Field_Information ('TAX712', 'BOX3',         $Tax712_Box3,        #dummy_width)
  do Get_Field_Information ('TAX712', 'DESCR3',       $Tax712_Descr3,      #dummy_width)
  do Get_Field_Information ('TAX712', 'LINE3',        $Tax712_Line3,       #dummy_width)
  do Get_Field_Information ('TAX712', 'COL3',         $Tax712_Col3,        #dummy_width)
  do Get_Field_Information ('TAX712', 'FORMAT3',      $Tax712_Format3,     #dummy_width)
  do Get_Field_Information ('TAX712', 'TEXT3',        $Tax712_Text3,       #dummy_width)

end-procedure


begin-HEADING 9
  #Include 'stdhdgtr.sqc'

  print $Tax712_FormID                (3,1)
  print $TaxForm_ID                   (,+3)

  print $Tax712_LastPrtLn1            (+3,60)
  print $Tax712_LocPerCol1            (0,66)

  print $Tax712_EffDt2                (+1,1)
  print $Tax712_FormID2               (0,12)
  print $Tax712_Layout_Sty2           (0,19)
  print $Tax712_Top_Margin2           (0,32)
  print $Tax712_Top_Split2            (0,39)
  print $Tax712_Cntr_Split2           (0,45)
  print $Tax712_Botm_Split2           (0,52)
  print $Tax712_LastPrtLn2            (0,60)
  print $Tax712_LocPerCol2            (0,66)

  print $Tax712_EffDt3                (+1,1)
  print $Tax712_FormID3               (0,12)
  print $Tax712_Layout_Sty3           (0,19)
  print $Tax712_Top_Margin3           (0,32)
  print $Tax712_Top_Split3            (0,39)
  print $Tax712_Cntr_Split3           (0,45)
  print $Tax712_Botm_Split3           (0,52)
  print $Tax712_LastPrtLn3            (0,60)
  print $Tax712_LocPerCol3            (0,66)
  print $Tax712_Box3                  (0,72)
  print $Tax712_Descr3                (0,79)
  print $Tax712_Line3                 (0,110)
  print $Tax712_Col3                  (0,116)
  print $Tax712_Format3               (0,122)
  print $Tax712_Text3                 (0,140)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
  if $CurrentOnly = 'Y'
    do Select-Current
  else
    do Select-All
  end-if
END-PROCEDURE

BEGIN-PROCEDURE Select-Current
BEGIN-SELECT

A.TAXFORM_ID              () on-break print=never level=1 after=After-Form
  move 'TAXFORM_ID'  to $FieldName
  move &A.TaxForm_ID to $FieldValue
  do Read-Translate-Table
  move $XlatShortName to $TaxForm_ID

A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out              (+1,1)
  move &A.EffDt to $EffDt

B.FORM_ID                 (0,12)
  move &B.Form_ID to $Form_ID
B.LAYOUT_STYLE            (0,20)
B.TOP_MARGIN              (0,33)
B.TOP_SPLIT               (0,40)
B.CENTER_SPLIT            (0,45)
B.BOTTOM_SPLIT            (0,52)
B.LAST_PRINT_LINE         (0,60)
B.LOCALS_PER_FORM         (0,66)

  do Get-Box

  print ' ' (+2,1)

 FROM PS_TAXFORM_TBL A, PS_TAXFORM_FORM B
WHERE A.TAXFORM_ID = B.TAXFORM_ID
  AND A.EFFDT      = (SELECT MAX(EFFDT)
                     FROM PS_TAXFORM_TBL AA
                    WHERE AA.TAXFORM_ID = A.TAXFORM_ID)
  AND B.EFFDT      = A.EFFDT
ORDER BY A.TAXFORM_ID, A.EFFDT DESC, B.FORM_ID

END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE SELECT-ALL
BEGIN-SELECT

C.TAXFORM_ID              () on-break print=never level=1 after=After-Form
  move 'TAXFORM_ID'  to $FieldName
  move &C.TaxForm_ID to $FieldValue
  do Read-Translate-Table
  move $XlatShortName to $TaxForm_ID

C.EFFDT
  do Format-DateTime(&C.EffDt, $out, {DEFDATE}, '', '')
  print $out              (+1,1)
  move &C.EffDt to $EffDt

D.FORM_ID                 (0,12)
  move &D.Form_ID to $Form_ID
D.LAYOUT_STYLE            (0,20)
D.TOP_MARGIN              (0,33)
D.TOP_SPLIT               (0,40)
D.CENTER_SPLIT            (0,45)
D.BOTTOM_SPLIT            (0,52)
D.LAST_PRINT_LINE         (0,60)
D.LOCALS_PER_FORM         (0,66)

  do Get-Box

  print ' ' (+2,1)

 FROM PS_TAXFORM_TBL C, PS_TAXFORM_FORM D
WHERE C.TAXFORM_ID = D.TAXFORM_ID
  AND C.EFFDT      = D.EFFDT
ORDER BY C.TAXFORM_ID, D.FORM_ID

END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Get-Box

  let #Line = 0

BEGIN-SELECT
E.TAXFORM_ID
E.BOX                     (+#LINE,72)
E.DESCR                   !(0,79)

        let $Prnt_Descr = &E.DESCR
          if $Curr_language_cd <> $Psoptions_Language_Cd
         do Lang-Record-Details
        end-if

        Print $Prnt_Descr               (0,79)

F.PRINT_LINE              (0,110)
F.PRINT_COL               (0,116)
F.PRINT_FORMAT            (0,122)
F.PRINT_TEXT              (0,140)  WRAP 29 2


  let #Line = 1

 FROM PS_TAXFORM_BOX E, PS_TAXFORM_PRT F
WHERE E.TAXFORM_ID = $FieldValue
  AND E.EFFDT      = $EffDt
  AND F.TAXFORM_ID = E.TAXFORM_ID
  AND F.EFFDT      = E.EFFDT
  AND F.BOX        = E.BOX
  AND F.FORM_ID    = $Form_ID

ORDER BY E.BOX

END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Lang-Record-Details
BEGIN-SELECT
LNG_R.DESCR

        let $Prnt_Descr = Cond(Rtrim(&LNG_R.DESCR,' ')='', &E.DESCR, &LNG_R.DESCR)

FROM PS_TAXFORM_BX_LANG LNG_R
WHERE TAXFORM_ID  = &E.TAXFORM_ID
  AND LANGUAGE_CD = $Curr_language_cd
  AND EFFDT          = (SELECT MAX(EFFDT) FROM PS_TAXFORM_BX_LANG
                                     WHERE TAXFORM_ID  = &E.TAXFORM_ID
                                         AND LANGUAGE_CD = $Curr_language_cd
                                         AND EFFDT      <= $Effdt)
  AND BOX = &E.BOX
END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE After-Form
  new-page
END-PROCEDURE

BEGIN-PROCEDURE Prompts
  while $CurrentOnly = ''
    input $CurrentOnly 'Current Table entries only? (Y/N)'
    uppercase $CurrentOnly
    if INSTR('YN',$CurrentOnly,1) = 0
      move '' to $CurrentOnly
    end-if
  end-while
END-PROCEDURE


begin-procedure Convert-Parameters

  move $RC_QTR_UI.Current_Only     to $CurrentOnly
end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
