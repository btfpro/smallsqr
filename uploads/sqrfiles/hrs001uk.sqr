!*************************************
!  HRS001UK:  Adverse Impact Report  *
!*************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:13:35:50                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
Begin-Setup
#Include 'ptpsp125.sqc'  !Printer and page-size initialization
End-Setup

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-heading 6
   #Include 'stdhdg08.sqc'
   print '                    <--------- U K  E T'          (+2,1)
   print ' H N I C   G R O U P ------------>   <---- S E X ---->'    ()
   print 'Actions              EU-UK  EU-Other  African '  (+1,1)
   print ' Afro Carib  Asian  Other       Male    Female      Total'             ()
end-heading

begin-procedure Init-Report

  move 'HRS001UK' to $ReportID
  move 'Adverse Impact Report' to $ReportTitle
  display $ReportTitle

  do Init-Array

  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-From-Thru-Date
  else
    do Select-Parameters
  end-if

end-procedure

begin-procedure Get-Values

   do Get-From-Thru-Date

end-procedure

begin-procedure Init-Array
  create-array name=Totals size=3 field=total:number:10 field=label:char
  put 'Total Applicants' into Totals(0) label
  put 'Total Offers'     into Totals(1) label
  put 'Total Hired'      into Totals(2) label
end-procedure

begin-procedure Process-Main

  do Develop-Totals
  do Create-Report

end-procedure

begin-procedure Develop-Totals
begin-SELECT
A.HRS_PERSON_ID,A.SEX,DVR.ETHNIC_GRP_CD_UK,A.STATUS_CODE
  move 'M'           to $Sex  !don't differentiate male/female by Ethnic Group
  move &DVR.Ethnic_Grp_cd_uk to $EthnicGroup
  do Analyze-Sex-EthnicGroup  !will return minority code (1 thru 6) in #EEOcol
  move 0 to #i  !Total Applicants row
  do Increment-Array
  do Check-For-Offer
  if &A.Status_Code = '020' or $OfferMade = 'Y'  !Hired or Offered Job
    move 1 to #i  !Applicants Offered Job row
    do Increment-Array
  end-if
  if &A.Status_Code = '020'  !Hired
    move 2 to #i  !Applicants Hired row
    do Increment-Array
  end-if
FROM  PS_HRS_APPLICANT A,
      PS_HRS_APP_DIV DVR
WHERE A.STATUS_DT BETWEEN $FromDate AND $ThruDate
  AND A.HRS_PERSON_ID = DVR.HRS_PERSON_ID
  AND A.APP_PER_STATUS <> 'E'  !Exclude Employee-Applicants
end-SELECT
end-procedure

begin-procedure Increment-Array
  array-add 1 1 to Totals(#i) total(#EEOcol) total(9)
  if &A.Sex = 'M'  !Male
    array-add 1 to Totals(#i) total(7)    !Total Males
    if #EEOcol = 1  !White
      array-add 1 to Totals(#i) total(0)  !Total White Males
    end-if
  else
    array-add 1 to Totals(#i) total(8)    !Total Females
  end-if
end-procedure

begin-procedure Check-For-Offer
  move 'N' to $OfferMade
begin-select

O.HRS_PERSON_ID
  move 'Y' to $OfferMade
  EXIT-SELECT               !Only see if any offer was made

 FROM PS_HRS_OFFER O
WHERE O.HRS_PERSON_ID = &A.HRS_PERSON_ID
end-select
end-procedure

begin-procedure Create-Report
  move 0 to #i
  while #i <= 2
    get $label from Totals(#i) label
    print $label (+3,1)
    move 20 to #startCol
    move 0 to #EEOcol
    while #EEOcol <= 9
        if #EEOcol <= 1
                add 1 to #EEOcol
        end-if
      get #count from Totals(#i) total(#EEOcol)
      do Format-Number(#count, $out, '999,999')
      print $out (0,#startCol)
      if #i > 0  !if Offered or Hired row
        get #total from Totals(0) total(#EEOcol)
        divide #total into #count on-error=zero
        multiply 100 times #count
        do Format-Number(#count, $out, '9999.99')
        print $out (+1,#startCol)
        if #EEOcol = 0  !if Control Group column
          print '% Total' (0,1)
          move #count to #controlTotal
          position (-1)
        else
          divide #controlTotal into #count on-error=zero
          multiply 100 times #count
          do Format-Number(#count, $out, '9999.99')
          print $out (+1,#startCol)

     if #total > 0 and #EEOcol <> 8  !if applicants and not Totals column

            !4/5ths measurement rule for selection
            !if (#count < 80) and (#controlTotal > 0)
            !  print '*' ()
            !end-if
          end-if
          print '% Impact' (0,1)
          position (-2)
        end-if
      end-if
      evaluate #EEOcol
        when = 0
        when = 6
        when = 8
          add 2 to #startCol
      end-evaluate
      add 9 to #startCol
      add 1 to #EEOcol
    end-while
    add 1 to #i
    if #i = 2
      position (+2)
    end-if
  end-while
  print '* Totals may include applicants which have not been given a UK ethnic status ' (18,1)

end-procedure

#include 'stdapi.sqc'    !Routine to update Run status
#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askftd.sqc'    !From Thru Date input

#Include 'eeogrpuk.sqc'  !Analyze-Sex-EthnicGroup procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
