!***********************************************************************
!  GetPrimJ:  Get Primary Job                                          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:13:12:12                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!***********************************************************************
begin-procedure Get-Primary-Job($emplid, $asofdate, :#emplrcd)
!***********************************************************************
! Keys passed in:  $Emplid, $AsOfDate
! Values returned: #EmplRcd
!
! This procedure returns the Lowest 'Active' Primary Job Record#
! for a given employee at a certain date
! When no date is provided, by default the current date is used
!***********************************************************************

  move 0 to #EmplRcd
  
  let $Emplid = Rtrim($Emplid,' ')
  if $Emplid = ''
    goto GetPrimJExit
  end-if

  if $AsOfDate = ''
    move $_AsOfToday to $AsOfDate
  end-if

  let $JOB_FOUND = 'N'
  let $ACTIVE_FOUND = 'N'
  let $PRIM_FOUND = 'N'

begin-SELECT
JOB.EMPL_RCD
JOB.EMPL_STATUS
JOB.JOB_INDICATOR

   let #CurrEmplRcd = &JOB.EMPL_RCD
   Evaluate &JOB.EMPL_STATUS
   when = 'A'
   when = 'L'
   when = 'P'
   when = 'S'
   when = 'W'
      let $CurrActive = 'Y'
      break
   when-other
      let $CurrActive = 'N'
   end-evaluate

   let $CurrIndicator = &JOB.JOB_INDICATOR
   if $JOB_FOUND = 'N'
      let #Emplrcd = #CurrEmplRcd
      let $JOB_FOUND = 'Y'
   end-if
   if $ACTIVE_FOUND = 'N' and $CurrActive = 'Y'
      let #Emplrcd = #CurrEmplRcd
      let $ACTIVE_FOUND = 'Y'
   end-if
   if $PRIM_FOUND = 'N' and $CurrIndicator = 'P'
      if $ACTIVE_FOUND = $CurrActive
         let #Emplrcd = #CurrEmplRcd
         let $PRIM_FOUND = 'Y'
      end-if
   end-if
 
FROM PS_JOB JOB
WHERE JOB.EMPLID = $Emplid
  AND JOB.EFFDT =
       (SELECT MAX(EFFDT)
        FROM PS_JOB
        WHERE EMPLID = JOB.EMPLID
          AND EMPL_RCD = JOB.EMPL_RCD
          AND EFFDT <= $AsOfDate)
  AND JOB.EFFSEQ =
       (SELECT MAX(EFFSEQ)
        FROM PS_JOB
        WHERE EMPLID = JOB.EMPLID
          AND EMPL_RCD = JOB.EMPL_RCD
          AND EFFDT = JOB.EFFDT) 
ORDER BY JOB.EMPL_RCD 
end-SELECT

GetPrimJExit:

end-procedure


!***********************************************************************
begin-procedure Define-PrimJob-Clause($Record_Alias, $asofdate, :$Prim_Job_Clause)
!***********************************************************************
! Keys passed in:  $Record_Alias, $AsOfDate
! Values returned: $Prim_Job_Clause
!
! This procedure defines a statement for selecting the primary job
! record# for employees. This statement needs to be included in a where-clause
! The procedure needs to be called before using the statement in a SQL.
! Example: 
!   do Define-PrimJob-Clause('EM', '', $PRIM_JOB_CLAUSE)
!   BEGIN-SELECT
!   EM.EMPLID,
!   EM.EMPL_RCD
!   FROM PS_EMPLOYMENT EM
!   WHERE EM.EMPL_RCD = [$PRIM_JOB_CLAUSE]
!***********************************************************************


if $AsOfDate = ''
  move $_AsOfToday to $AsOfDate
end-if

let $IS_ACTIVE = 'IN(''A'',''L'',''P'',''S'',''W'')'

let $EXCEPTIONS = 
    ' ((MJ3.EMPL_STATUS ' || $IS_ACTIVE ||
    ' AND (MJ2.EMPL_STATUS NOT ' || $IS_ACTIVE ||
    '  OR (MJ3.JOB_INDICATOR = ''P'' AND MJ2.JOB_INDICATOR <> ''P'')))
    OR (MJ3.JOB_INDICATOR = ''P'' AND MJ2.JOB_INDICATOR <> ''P''
       AND MJ2.EMPL_STATUS NOT ' || $IS_ACTIVE || '))'

let $PRIM_JOB_CLAUSE = '(SELECT MIN(MJ2.EMPL_RCD)
        FROM PS_JOB MJ2
        WHERE MJ2.EMPLID = '|| $Record_Alias || '.EMPLID

          AND MJ2.EFFDT = 
               (SELECT MAX(EFFDT)
                FROM PS_JOB
                WHERE EMPLID = MJ2.EMPLID
                  AND EMPL_RCD = MJ2.EMPL_RCD
                  AND EFFDT <= ''' || $AsOfDate || ''')

          AND MJ2.EFFSEQ = 
               (SELECT MAX(EFFSEQ)
                FROM PS_JOB
                WHERE EMPLID = MJ2.EMPLID
                  AND EMPL_RCD = MJ2.EMPL_RCD
                  AND EFFDT = MJ2.EFFDT)

          AND ((MJ2.EMPL_STATUS ' || $IS_ACTIVE ||
          ' AND MJ2.JOB_INDICATOR = ''P'')
          OR NOT EXISTS
               (SELECT MJ3.EMPL_RCD
                FROM PS_JOB MJ3
                WHERE MJ3.EMPLID = MJ2.EMPLID
                  AND MJ3.EMPL_RCD <> MJ2.EMPL_RCD
                  AND MJ3.EFFDT = 
                       (SELECT MAX(EFFDT)
                        FROM PS_JOB
                        WHERE EMPLID = MJ3.EMPLID
                          AND EMPL_RCD = MJ3.EMPL_RCD
                          AND EFFDT <= ''' || $AsOfDate || ''')
                  AND MJ3.EFFSEQ =
                       (SELECT MAX(EFFSEQ)
                        FROM PS_JOB
                        WHERE EMPLID = MJ3.EMPLID
                          AND EMPL_RCD = MJ3.EMPL_RCD
                          AND EFFDT = MJ3.EFFDT)
                  AND ' || $EXCEPTIONS || ')))'

let $_PRIM_JOB_CLAUSE = $PRIM_JOB_CLAUSE 

end-procedure

!***********************************************************************
begin-procedure Define-PrimJob-With-Field($Record_Alias, $DateField, :$Prim_Job_Clause)
!***********************************************************************
! Keys passed in:  $Record_Alias, $DateField
! Values returned: $Prim_Job_Clause
!
! This procedure defines a statement for selecting the primary job
! record# for employees. It is used by passing a date field, currently
! used in yhe sqr select, instead of a predefined date value - in case
! the date field will change.  This statement needs to be included in 
! a where-clause. The procedure needs to be called before using the 
! statement in a SQL.
! Example: 
!   let $Field = 'F.Pay_End_Dt'
!   do Define-PrimJob-With-Field('EM',$Field, $PRIM_JOB_CLAUSE)
!   BEGIN-SELECT
!   EM.EMPLID,
!   EM.EMPL_RCD
!   FROM PS_JOB EM
!   WHERE EM.EMPL_RCD = [$PRIM_JOB_CLAUSE]
!     AND EM.EFFDT    = xxx
!     AND EM.EFFSEQ   = xxx
!***********************************************************************

let $IS_ACTIVE = 'IN(''A'',''L'',''P'',''S'',''W'')'

let $EXCEPTIONS = 
    ' ((MJ3.EMPL_STATUS ' || $IS_ACTIVE ||
    ' AND (MJ2.EMPL_STATUS NOT ' || $IS_ACTIVE ||
    '  OR (MJ3.JOB_INDICATOR = ''P'' AND MJ2.JOB_INDICATOR <> ''P'')))
    OR (MJ3.JOB_INDICATOR = ''P'' AND MJ2.JOB_INDICATOR <> ''P''
       AND MJ2.EMPL_STATUS NOT ' || $IS_ACTIVE || '))'

let $PRIM_JOB_CLAUSE = '(SELECT MIN(MJ2.EMPL_RCD)
        FROM PS_JOB MJ2
        WHERE MJ2.EMPLID = '|| $Record_Alias || '.EMPLID

          AND MJ2.EFFDT = 
               (SELECT MAX(EFFDT)
                FROM PS_JOB
                WHERE EMPLID = MJ2.EMPLID
                  AND EMPL_RCD = MJ2.EMPL_RCD
                  AND EFFDT <= '||$DateField||')

          AND MJ2.EFFSEQ = 
               (SELECT MAX(EFFSEQ)
                FROM PS_JOB
                WHERE EMPLID = MJ2.EMPLID
                  AND EMPL_RCD = MJ2.EMPL_RCD
                  AND EFFDT = MJ2.EFFDT)

          AND ((MJ2.EMPL_STATUS ' || $IS_ACTIVE ||
          ' AND MJ2.JOB_INDICATOR = ''P'')
          OR NOT EXISTS
               (SELECT MJ3.EMPL_RCD
                FROM PS_JOB MJ3
                WHERE MJ3.EMPLID = MJ2.EMPLID
                  AND MJ3.EMPL_RCD <> MJ2.EMPL_RCD
                  AND MJ3.EFFDT = 
                       (SELECT MAX(EFFDT)
                        FROM PS_JOB
                        WHERE EMPLID = MJ3.EMPLID
                          AND EMPL_RCD = MJ3.EMPL_RCD
                          AND EFFDT <= '||$DateField||')
                  AND MJ3.EFFSEQ =
                       (SELECT MAX(EFFSEQ)
                        FROM PS_JOB
                        WHERE EMPLID = MJ3.EMPLID
                          AND EMPL_RCD = MJ3.EMPL_RCD
                          AND EFFDT = MJ3.EFFDT)
                  AND ' || $EXCEPTIONS || ')))'

let $_PRIM_JOB_CLAUSE = $PRIM_JOB_CLAUSE 

end-procedure
!***************************** End of Include File **************************
