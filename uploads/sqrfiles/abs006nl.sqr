!***********************************************************************
!  ABS006NL:  Ziekterapportage Afd/Lftd                                *
!  IMPORTANT : First run ABS702NL, Create Statictics, THEN ABS006NL    *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!
!          $Date:  2006/09/15:08:02:44                                 
!       $Release:  HR9                                                 
!      $Revision:  102                                                 
!                                                                      *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup31.sqc'   !Printer and page-size initialization

#define ColTot 41


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  if $doorgaan != 'N'
    do Process-Main
  else
    display 'Dit programma kan alleen opgestart worden vanuit PeopleSoft'
  end-if
  do commit-transaction
  do Reset
  do Stdapi-Term
end-report

begin-heading 5
do Get_Field_Information ('ABS006NL', 'REPORT_TITLE',    $REPORTTITLE,   #DW)
  #Include 'stdhdgtr.sqc'

end-heading

begin-procedure Init-Report
  move 'ABS006NL' to $ReportID
  move 'Ziekteverzuim totaal per afdeling per Leeftijd' to $ReportTitle
  do Stdapi-Init

  let $doorgaan = 'Y'
  if $prcs_process_instance = ''
     let $doorgaan = 'N'
  else
    do Select-Parameters
  end-if
do Init_Report_Translation ('ABS006NL', $language_cd)
do Append_Report_Translation ('ABS005NL')
do Get_Field_Information ('ABS005NL', 'DAY_COUNT',       $DAY_COUNT,   #DW)
do Get_Field_Information ('ABS005NL', 'DAYS',            $DAYS,   #DW)
do Get_Field_Information ('ABS005NL', 'DEPARTMENT',      $DEPT,   #DW)
do Get_Field_Information ('ABS005NL', 'EMPL_COUNT',      $EMPL_COUNT,   #DW)
do Get_Field_Information ('ABS005NL', 'EMPL_NOT_ILL',    $EMPL_NOT_ILL,   #DW)
do Get_Field_Information ('ABS005NL', 'FTE',             $FTE,   #DW)
do Get_Field_Information ('ABS005NL', 'HIERARCHY',       $HIERARCHY,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_COUNT',   $ILLNESS_COUNT,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_DAY',     $ILLNESS_DAY,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_ENDED1',  $ILLNESS_ENDED1,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_ENDED2',  $ILLNESS_ENDED2,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_FREQ',    $ILLNESS_FREQ,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_LENGTH1', $ILLNESS_LENGTH1,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_LENGTH2', $ILLNESS_LENGTH2,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_PERC',    $ILLNESS_PERC,   #DW)
do Get_Field_Information ('ABS005NL', 'ILLNESS_STARTED', $ILLNESS_STARTED,   #DW)
do Get_Field_Information ('ABS005NL', 'LONGTERM_ILL',    $LONGTERM_ILL,   #DW)
do Get_Field_Information ('ABS005NL', 'MATERNITY',       $MATERNITY,   #DW)
do Get_Field_Information ('ABS005NL', 'PARTIAL_ILL',     $PARTIAL_ILL,   #DW)
do Get_Field_Information ('ABS005NL', 'PERIOD1',         $PERIOD1,   #DW)
do Get_Field_Information ('ABS005NL', 'PERIOD2',         $PERIOD2,   #DW)
do Get_Field_Information ('ABS005NL', 'TOTAL',           $TOTAL,   #DW)

end-procedure

begin-procedure Get-Values
   let $language_cd = $PRCS_LANGUAGE_CD
   do get-IllStat-parameters
   let $FromYear  = to_char(#Vanjr)
   let $FromMonth = to_char(#Vanmnd)
   let $ThruYear  = to_char(#Temjr)
   let $ThruMonth = to_char(#Temmnd)
   let $IndLv  = Upper($IndLv)
   let $IndPv  = Upper($IndPv)
   let $IndZv  = Upper($IndZv)
   let $NL_Ind_hierarchy = Upper($NL_Ind_hierarchy)
   let $Dept_Id = Upper($Dept_Id)
end-procedure


begin-procedure Process-Main
 do Creeer-array
 do AantalMnd

 do Pers-Sterkte-Beschik-Kal-Dagen
 do Verzmd-Kalenderdgn
 do AangevangenZiekte
 do BeeindigeZiekte
 do Arbeidsrelaties

 do Print-Rapport
end-procedure


Begin-procedure Creeer-array

Begin-Select
ILL_LENG_CAT1_NLD        &cat1
ILL_LENG_CAT2_NLD        &cat2
ILL_LENG_CAT3_NLD        &cat3
ILL_LENG_CAT4_NLD        &cat4
ILL_LENG_CAT5_NLD        &cat5
ILL_LENG_CAT6_NLD        &cat6
ILL_LENG_CAT7_NLD        &cat7
ILL_LENG_CAT8_NLD        &cat8
AGE_CAT1_NLD             &lftd1
AGE_CAT2_NLD             &lftd2
AGE_CAT3_NLD             &lftd3
AGE_CAT4_NLD             &lftd4
AGE_CAT5_NLD             &lftd5
AGE_CAT6_NLD             &lftd6
AGE_CAT7_NLD             &lftd7
AGE_CAT8_NLD             &lftd8
from PS_ABS_PARAMETERS
End-Select

  create-array name=maand size=13 field=aantaldagen:number
  put 0  into maand(0) aantaldagen
  put 31 into maand(1) aantaldagen
  move 28  to  #Work_Month_Days
  if #Temjr=#Vanjr
    do Leap-Days(#Vanjr, #Work_Leap_Days)
  else
    if #Temmnd = 1
      do Leap-Days(#Vanjr, #Work_Leap_Days)
    else
      do Leap-Days(#Temjr, #Work_Leap_Days)
    end-if
  end-if
  let #Work_Month_Days =  #Work_Month_Days + #Work_Leap_Days
  put #Work_Month_Days into maand(2) aantaldagen
  put 31 into maand(3) aantaldagen
  put 30 into maand(4) aantaldagen
  put 31 into maand(5) aantaldagen
  put 30 into maand(6) aantaldagen
  put 31 into maand(7) aantaldagen
  put 31 into maand(8) aantaldagen
  put 30 into maand(9) aantaldagen
  put 31 into maand(10) aantaldagen
  put 30 into maand(11) aantaldagen
  put 31 into maand(12) aantaldagen

  create-array name=cat size=9  field=leng:number

  put &cat1 into cat(0)
  put &cat2 into cat(1)
  put &cat3 into cat(2)
  put &cat4 into cat(3)
  put &cat5 into cat(4)
  put &cat6 into cat(5)
  put &cat7 into cat(6)
  put &cat8 into cat(7)
  put 0     into cat(8)

  create-array name=lftdgrp size=8  field=kolom:number
                                    field=grens:number

  put 54  &lftd1 into lftdgrp(0) kolom grens
  put 64  &lftd2 into lftdgrp(1) kolom grens
  put 74  &lftd3 into lftdgrp(2) kolom grens
  put 84  &lftd4 into lftdgrp(3) kolom grens
  put 94  &lftd5 into lftdgrp(4) kolom grens
  put 104 &lftd6 into lftdgrp(5) kolom grens
  put 114 &lftd7 into lftdgrp(6) kolom grens
  put 124 &lftd8 into lftdgrp(7) kolom grens

!***********************************************************************
! Create the array's for Pers-Sterkte-Beschik-Kal-Dagen                *
!***********************************************************************

 create-array name=FTE size=1 field=totaal:number
                              field=lftdgrp:number:8

 create-array name=AV size=1  field=totaal:number
                              field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for Verzmd-Kalenderdgn                   *
!***********************************************************************

 create-array name=VK size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for NietVerzmd-Arbrel                    *
!***********************************************************************

 create-array name=NV size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for ZiekteVerzuimPerc                    *
!***********************************************************************

 create-array name=ZP size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for AangevangenZiekte                    *
!***********************************************************************

 create-array name=AZ size=1 field=totaal:number
                             field=lftdgrp:number:8

 create-array name=ZK size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for Gem-Verzuimduur                      *
!***********************************************************************

 create-array name=GV size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for BeeindigeZiekte                      *
!***********************************************************************

 create-array name=DZ size=8 field=totaal:number
                             field=lftdgrp:number:8

 create-array name=BZ size=1 field=totaal:number
                             field=lftdgrp:number:8

 create-array name=SM size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for Arbeidsrelaties                      *
!***********************************************************************

 create-array name=AB size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Create the required array's for MeldingsFreq                         *
!***********************************************************************

 create-array name=MF size=1 field=totaal:number
                             field=lftdgrp:number:8

!***********************************************************************
! Initialise the array's for the counts                                *
!***********************************************************************

 put 0 into FTE(0) totaal
 put 0 into AV(0)  totaal
 put 0 into VK(0)  totaal
 put 0 into NV(0)  totaal
 put 0 into ZP(0) totaal
 put 0 into AZ(0)  totaal
 put 0 into ZK(0)  totaal
 put 0 into GV(0)  totaal
 put 0 into BZ(0)  totaal
 put 0 into SM(0)  totaal
 put 0 into MF(0)  totaal
 put 0 into AB(0)  totaal
 let #grp = 0
  while #grp < 8
    put 0 into FTE(0) lftdgrp(#grp)
    put 0 into AV(0)  lftdgrp(#grp)
    put 0 into VK(0)  lftdgrp(#grp)
    put 0 into NV(0)  lftdgrp(#grp)
    put 0 into ZP(0)  lftdgrp(#grp)
    put 0 into AZ(0)  lftdgrp(#grp)
    put 0 into ZK(0)  lftdgrp(#grp)
    put 0 into GV(0)  lftdgrp(#grp)
    put 0 into BZ(0)  lftdgrp(#grp)
    put 0 into SM(0)  lftdgrp(#grp)
    put 0 into MF(0)  lftdgrp(#grp)
    put 0 into AB(0)  lftdgrp(#grp)
    add 1 to #grp
  end-while

 let #cat = 0
 while #cat <> 8
   put 0 into DZ(0)  totaal
   let #grp = 0
   while #grp < 8
     put 0 into DZ(0)  lftdgrp(#grp)
     add 1 to #grp
   end-while
   add 1 to #cat
 end-while

End-procedure


Begin-procedure AantalMnd

 let #AantalMnd   = (#Temjr - #Vanjr) * 12
 let #AantalMnd   = #AantalMnd - #Vanmnd + #Temmnd + 1
 let #Jaarteller  = #Vanjr
 let #Maandteller = #Vanmnd
 get #AantalDgn from maand(#Maandteller) aantaldagen
 while not(#Jaarteller = #Temjr and #Maandteller = #Temmnd)
   add 1 to #Maandteller
   if #Maandteller > 12
     let #Maandteller = 1
     add 1 to #Jaarteller
   end-if
   get #hulpdagen from maand(#Maandteller) aantaldagen
   add #hulpdagen to #AantalDgn
 end-while

End-procedure

!***********************************************************************
! Retrieve the number of available employment calendar days
!***********************************************************************
Begin-procedure Pers-Sterkte-Beschik-Kal-Dagen

begin-Select
EMPLID
EMPL_RCD
NL_FROM_YEAR
NL_FROM_MONTH
AGE_NLD                  &leeftijd_fte
FTE_ILI_NLD             &#fte_ili
FTE_ELI_NLD             &#fte_eli
ILL_AVDAYS_ILI_NLD      &av_days_ili
ILL_AVDAYS_ELI_NLD      &av_days_eli

  if $IndLv = 'I'
    let #fte = &#fte_ili
    let #av  = &av_days_ili
  else
    let #fte = &#fte_eli
    let #av  = &av_days_eli
  end-if
  do Bepaal-Lftdgrp(#grp, &leeftijd_fte)

  ARRAY-ADD #fte to FTE(0) TOTAAL
  ARRAY-ADD #av  to AV(0)  TOTAAL
  if #grp > -1
     ARRAY-ADD #fte to FTE(0) LFTDGRP(#GRP)
     ARRAY-ADD #av  to AV(0)  LFTDGRP(#GRP)
  end-if

from PS_ABS_STAT_NLD A, PS_DEPT_SEC_VW_NLD C
where C.SETID = $Setid
and C.NL_DEPTID_ROOT = $dept_id
and A.SETID = C.SETID
and A.DEPTID = C.DEPTID
and (($NL_IND_HIERARCHY = 'Y')OR(C.DEPTID = C.NL_DEPTID_ROOT))
and NOT((#VANJR >= NL_FROM_YEAR)
        and NOT(NL_FROM_YEAR = #Vanjr and NL_FROM_MONTH >= #Vanmnd))
and NOT((NL_FROM_YEAR >= #Temjr)
        and NOT(NL_FROM_YEAR = #Temjr and NL_FROM_MONTH <= #Temmnd))
End-Select

!*******************************************
! Divide all FTE's by the number of months *
!*******************************************

  let #grp = 0
  while #grp < 8
    get #fte from FTE(0) lftdgrp(#grp)
    let #fte = #fte / #AantalMnd
    put #fte into FTE(0) lftdgrp(#grp)
    add 1 to #grp
  end-while
  get #fte from FTE(0) totaal
  let #fte = #fte / #AantalMnd
  put #fte into FTE(0) totaal

End-procedure

!***********************************************************************
! Retrieve the number of illness calendar days                         *
!***********************************************************************
Begin-procedure Verzmd-Kalenderdgn

Begin-Select
AGE_NLD                  &leeftijd_vk
ILL_DAYIPI_ILI_NLD      &day_ipi_ili
ILL_DAYIPI_ELI_NLD      &day_ipi_eli
ILL_DAYEPI_ILI_NLD      &day_epi_ili
ILL_DAYEPI_ELI_NLD      &day_epi_eli

!***********************************************************************
! Determine #days Including/Excluding Partial and Longterm illness     *
!***********************************************************************

  if  $IndLv = 'I'
    if $IndPv = 'I'
      let #dagen = &day_ipi_ili
    else
      let #dagen = &day_epi_ili
    end-if
  else
    if $IndPv = 'I'
      let #dagen = &day_ipi_eli
    else
      let #dagen = &day_epi_eli
    end-if
  end-if

!*****************************************
! Add to the correct age category        *
!*****************************************

  do Bepaal-Lftdgrp(#grp, &leeftijd_vk)
  ARRAY-ADD #dagen to VK(0) TOTAAL
  if #grp > -1
     ARRAY-ADD #dagen to VK(0) LFTDGRP(#GRP)
  end-if

from PS_ABS_STAT_NLD A, PS_DEPT_SEC_VW_NLD C
where C.SETID = $Setid
and C.NL_DEPTID_ROOT = $dept_id
and A.SETID = C.SETID
and A.DEPTID = C.DEPTID
and (($NL_IND_HIERARCHY = 'Y')OR(C.DEPTID = C.NL_DEPTID_ROOT))
and NOT((#VANJR >= NL_FROM_YEAR)
        and NOT(NL_FROM_YEAR = #Vanjr and NL_FROM_MONTH >= #Vanmnd))
and NOT((NL_FROM_YEAR >= #Temjr)
        and NOT(NL_FROM_YEAR = #Temjr and NL_FROM_MONTH <= #Temmnd))
and not ($IndZv           = 'E' and
         ABSENCE_TYPE     = '502')
End-Select
 do ZiekteVerzuimPerc
 do NietVerzmd-Arbrel

End-procedure

!***********************************************************************
! Retrieve the number of employments without illness
!***********************************************************************
Begin-procedure NietVerzmd-Arbrel

Begin-Select
MIN(AGE_NLD)             &leeftijd_nv
SUM(ILL_DAYIPI_ILI_NLD) &daynv_ipi_ili
SUM(ILL_DAYIPI_ELI_NLD) &daynv_ipi_eli
SUM(ILL_DAYEPI_ILI_NLD) &daynv_epi_ili
SUM(ILL_DAYEPI_ELI_NLD) &daynv_epi_eli
SUM(EMPL_RCD_ILI_NLD)  &emplnv_rcd_ili
SUM(EMPL_RCD_ELI_NLD)  &emplnv_rcd_eli

!***********************************************************************
! Determine #days Including/Excluding Partial and Longterm illness     *
!***********************************************************************

  if  $IndLv = 'I'
    let #AantArbRel = &emplnv_rcd_ili
    if $IndPv = 'I'
      let #dagen = &daynv_ipi_ili
    else
      let #dagen = &daynv_epi_ili
    end-if
  else
    let #AantArbRel = &emplnv_rcd_eli
    if $IndPv = 'I'
      let #dagen = &daynv_ipi_eli
    else
      let #dagen = &daynv_epi_eli
    end-if
  end-if

!********************************************************
! When an employment doesn't have an illnes within      *
! the report period, then he is included in the count   *
!********************************************************

  if #dagen = 0
    do Bepaal-Lftdgrp(#grp, &leeftijd_nv)
    ARRAY-ADD #AantArbRel to NV(0) TOTAAL
    if #grp > -1
      ARRAY-ADD #AantArbRel to NV(0) LFTDGRP(#GRP)
    end-if
  end-if

from PS_ABS_STAT_NLD A, PS_DEPT_SEC_VW_NLD C
where C.SETID = $Setid
and C.NL_DEPTID_ROOT = $dept_id
and A.SETID = C.SETID
and A.DEPTID = C.DEPTID
and (($NL_IND_HIERARCHY = 'Y')OR(C.DEPTID = C.NL_DEPTID_ROOT))
and NOT((#VANJR >= NL_FROM_YEAR)
        and NOT(NL_FROM_YEAR = #Vanjr and NL_FROM_MONTH >= #Vanmnd))
and NOT((NL_FROM_YEAR >= #Temjr)
        and NOT(NL_FROM_YEAR = #Temjr and NL_FROM_MONTH <= #Temmnd))
and not ($IndZv           = 'E' and
         ABSENCE_TYPE     = '502')
GROUP by EMPLID,EMPL_RCD
End-Select

!******************************************
! Divide by the number of months          *
!******************************************

  let #grp = 0
  while #grp < 8
    get #nv from NV(0) lftdgrp(#grp)
    let #nv = #nv / #AantalMnd
    put #nv into NV(0) lftdgrp(#grp)
    add 1 to #grp
  end-while
  get #nv from NV(0) totaal
  let #nv = #nv / #AantalMnd
  put #nv into NV(0) totaal

End-procedure

!***********************************************************************
! Determine the Illness Percentage
! Illness Percentage = (Illness Days / (#FTE's * #Days in Month)) * 100
!***********************************************************************
Begin-procedure ZiekteVerzuimPerc

  let #grp = 0
  while #grp < 8
    get #fte from FTE(0) lftdgrp(#grp)
    get #vk  from VK(0)  lftdgrp(#grp)
    if #fte > 0
      let #zp = (100 * #vk)/(#fte * #AantalDgn)
    else
      let #zp = 0
    end-if
    put #zp into ZP(0) lftdgrp(#grp)
    add 1 to #grp
  end-while
  get #fte from FTE(0) totaal
  get #vk  from VK(0)  totaal
  if #fte > 0
    let #zp = (100 * #vk)/(#fte * #AantalDgn)
  else
    let #zp = 0
  end-if
  put #zp into ZP(0) totaal

End-procedure

!***********************************************************************
! Determine begin of Illness within month
!***********************************************************************
Begin-procedure AangevangenZiekte

 let $vergdatum = To_Char(#Vanmnd)
 if length($vergdatum) = 1
   let $vergdatum = '0' || $vergdatum
 end-if
 let $vergdatum = To_Char(#Vanjr) || $vergdatum || '01'

 if $IndPv = 'I'
   do AangevangenZiekteInc
 else
   do AangevangenZiekteEx
 end-if
End-procedure

!***********************************************************************
! Determine begin of Illness within month including Partial illness
!***********************************************************************
Begin-procedure AangevangenZiekteInc

Begin-Select
A.BEGIN_DT                &beginazi
MIN(A.AGE_NLD)             &leeftijd_azi
SUM(A.ILL_DAYIPI_ILI_NLD) &dayazi_ipi_ili
SUM(A.ILL_DAYIPI_ELI_NLD) &dayazi_ipi_eli

!***************************
! Determine age category   *
!***************************

  do Bepaal-Lftdgrp(#grp, &leeftijd_azi)

  do Format-DateTime(&beginazi, $begindatum, {DEFCMP}, '', '')
  if $begindatum >= $vergdatum
    ARRAY-ADD 1 to AZ(0) TOTAAL
    if #grp > -1
      ARRAY-ADD 1 to AZ(0) LFTDGRP(#GRP)
    end-if
  end-if

!*********************************
! Count number of illnesses      *
!*********************************

  if ($IndLv = 'I' and &dayazi_ipi_ili <> 0) or
     ($IndLv = 'E' and &dayazi_ipi_eli <> 0)
    ARRAY-ADD 1 to ZK(0) TOTAAL
    if #grp > -1
      ARRAY-ADD 1 to ZK(0) LFTDGRP(#GRP)
    end-if
  end-if

from PS_ABS_STAT_NLD A, PS_DEPT_SEC_VW_NLD C
where C.SETID = $Setid
and C.NL_DEPTID_ROOT = $dept_id
and A.SETID = C.SETID
and A.DEPTID = C.DEPTID
and (($NL_IND_HIERARCHY = 'Y')OR(C.DEPTID = C.NL_DEPTID_ROOT))
and NOT((#VANJR >= A.NL_FROM_YEAR)
        and NOT(A.NL_FROM_YEAR = #Vanjr and A.NL_FROM_MONTH >= #Vanmnd))
and NOT((A.NL_FROM_YEAR >= #Temjr)
        and NOT(A.NL_FROM_YEAR = #Temjr and A.NL_FROM_MONTH <= #Temmnd))
and not ($IndZv           = 'E' and
     A.ABSENCE_TYPE       = '502')
and not A.BEGIN_DT is NULL
GROUP by A.EMPLID,A.EMPL_RCD,A.BEGIN_DT
End-Select

End-procedure

!***********************************************************************
! Determine begin of Illness within month excluding partial illness
!***********************************************************************
Begin-procedure AangevangenZiekteEx

Begin-Select
B.EMPLID
B.EMPL_RCD
B.NL_FROM_YEAR            &from_yearaze
B.NL_FROM_MONTH           &from_monthaze
B.BEGIN_DT                &beginaze
B.AGE_NLD                  &leeftijd_aze
B.ILL_DAYEPI_ILI_NLD      &dayaze_epi_ili
B.ILL_DAYEPI_ELI_NLD      &dayaze_epi_eli
B.ILL_START_FULL_NLD       &start_fullaze
B.ILL_START_PART_NLD       &start_partaze

!***************************
! Determine age category   *
!***************************

  do Bepaal-Lftdgrp(#grp, &leeftijd_nv)

!**************************************
! Count number of started illnesses   *
!**************************************

  if &start_fullaze = 'Y'
    ARRAY-ADD 1 to AZ(0) TOTAAL
    if #grp > -1
      ARRAY-ADD 1 to AZ(0) LFTDGRP(#GRP)
    end-if
  end-if

!*********************************
! Count number of illnesses      *
!*********************************

  if &start_fullaze = 'Y' or (&from_yearaze = #Vanjr and &from_monthaze = #Vanmnd)
    if ($IndLv = 'I' and &dayaze_epi_ili <> 0) or
       ($IndLv = 'E' and &dayaze_epi_eli <> 0)
      ARRAY-ADD 1 to ZK(0) TOTAAL
      if #grp > -1
        ARRAY-ADD 1 to ZK(0) LFTDGRP(#GRP)
      end-if
    end-if
  end-if

from PS_ABS_STAT_NLD B, PS_DEPT_SEC_VW_NLD D
where D.SETID = $Setid
and D.NL_DEPTID_ROOT = $dept_id
and B.SETID = D.SETID
and B.DEPTID = D.DEPTID
and (($NL_IND_HIERARCHY = 'Y')OR(D.DEPTID = D.NL_DEPTID_ROOT))
and NOT((#VANJR >= B.NL_FROM_YEAR)
        and NOT(B.NL_FROM_YEAR = #Vanjr and B.NL_FROM_MONTH >= #Vanmnd))
and NOT((B.NL_FROM_YEAR >= #Temjr)
        and NOT(B.NL_FROM_YEAR = #Temjr and B.NL_FROM_MONTH <= #Temmnd))
and not ($IndZv = 'E' and B.ABSENCE_TYPE = '502')
and not B.BEGIN_DT is NULL
End-Select

End-procedure

!***********************************************************************
! Determine return from Illness within month
!***********************************************************************
Begin-procedure BeeindigeZiekte

 let #vorigelengte = 0
 let $vorigeempl = ''
 let #vorigercd = 0
 let $vorigebegindatum = ''

Begin-Select
EMPLID                  &emplidbz
EMPL_RCD               &empl_rcdbz
BEGIN_DT                &beginbz
NL_FROM_YEAR            &nl_from_yearbz
NL_FROM_MONTH           &nl_from_monthbz
AGE_NLD                  &leeftijd_bz
ILL_LENIPI_ILI_NLD      &len_ipi_ili
ILL_LENIPI_ELI_NLD      &len_ipi_eli
ILL_LENEPI_ILI_NLD      &len_epi_ili
ILL_LENEPI_ELI_NLD      &len_epi_eli


!*********************************************
! Correction calculation for double count    *
!*********************************************

 if  not(&emplidbz = $vorigeempl AND &empl_rcdbz  = #vorigercd AND &beginbz = $vorigebegindatum)
   let #vorigelengte = 0
 end-if

!***************************
! Determine age category   *
!***************************

 do Bepaal-Lftdgrp(#grp, &leeftijd_bz)

!*********************************************
! count illness length per gender            *
!--------------------------------------------*
! count number of ended illnesses            *
!*********************************************

  if $IndPv = 'I' and $IndLv = 'I'
    let #lengte = &len_ipi_ili - #vorigelengte
    let #vorigelengte = &len_ipi_ili
  else
    if $IndPv = 'I' and $IndLv = 'E'
      let #lengte = &len_ipi_eli - #vorigelengte
      let #vorigelengte = &len_ipi_eli
    else
      if $IndPv = 'E' and $IndLv = 'I'
        let #lengte = &len_epi_ili - #vorigelengte
        let #vorigelengte = &len_epi_ili
      else
        if $IndPv = 'E' and $IndLv = 'E'
          let #lengte = &len_epi_eli - #vorigelengte
          let #vorigelengte = &len_epi_eli
        end-if
      end-if
    end-if
  end-if

  ARRAY-ADD 1       to BZ(0) TOTAAL
  ARRAY-ADD #lengte to SM(0) TOTAAL
  if #grp > -1
    ARRAY-ADD 1       to BZ(0) LFTDGRP(#GRP)
    ARRAY-ADD #lengte to SM(0) LFTDGRP(#GRP)
  end-if

!**********************************************
! determine illness length category           *
!**********************************************

  let #cat = 0
  GET #onder from CAT(#CAT) LENG
  WHILE (#onder <= #lengte and #onder <> 0)
    add 1 to #cat
    GET #onder from CAT(#CAT) LENG
  END-WHILE
  let #cat = #cat - 1

  if #cat > -1
    ARRAY-ADD 1 to DZ(#CAT) TOTAAL
    if #grp > -1
      ARRAY-ADD 1 to DZ(#CAT) LFTDGRP(#GRP)
    end-if
  end-if

!**********************************************
! Remember previous key values                *
!**********************************************

 let $vorigeempl       = &emplidbz
 let #vorigercd        = &empl_rcdbz
 let $vorigebegindatum = &beginbz

from PS_ABS_STAT_NLD A, PS_DEPT_SEC_VW_NLD C
where C.SETID = $Setid
and C.NL_DEPTID_ROOT = $dept_id
and A.SETID = C.SETID
and A.DEPTID = C.DEPTID
and (($NL_IND_HIERARCHY = 'Y')OR(C.DEPTID = C.NL_DEPTID_ROOT))
and NOT((#VANJR >= NL_FROM_YEAR)
        and NOT(NL_FROM_YEAR = #Vanjr and NL_FROM_MONTH >= #Vanmnd))
and NOT((NL_FROM_YEAR >= #Temjr)
        and NOT(NL_FROM_YEAR = #Temjr and NL_FROM_MONTH <= #Temmnd))
and not ($IndZv           = 'E' and
     ABSENCE_TYPE         = '502')
and not BEGIN_DT is NULL
and (   ($IndPv = 'I' and $IndLv = 'I' and ILL_ENDIPI_ILI_NLD = 'Y')
     or ($IndPv = 'I' and $IndLv = 'E' and ILL_ENDIPI_ELI_NLD = 'Y')
     or ($IndPv = 'E' and $IndLv = 'I' and ILL_ENDEPI_ILI_NLD = 'Y')
     or ($IndPv = 'E' and $IndLv = 'E' and ILL_ENDEPI_ELI_NLD = 'Y'))
order by EMPLID, EMPL_RCD, BEGIN_DT, NL_FROM_YEAR, NL_FROM_MONTH
End-Select
 do Gem-Verzuimduur

End-procedure

!***********************************************************************
! Determine average illness length (for ended illnesses only)
!***********************************************************************
Begin-procedure Gem-Verzuimduur

  let #grp = 0
  while #grp < 8
    get #sm from SM(0) lftdgrp(#grp)
    get #bz from BZ(0) lftdgrp(#grp)
    if #bz > 0
      let #gv = #sm/#bz
    else
      let #gv = 0
    end-if
    put #gv into GV(0) lftdgrp(#grp)
    add 1 to #grp
  end-while
  get #sm from SM(0) totaal
  get #bz from BZ(0) totaal
  if #bz > 0
    let #gv = #sm/#bz
  else
    let #gv = 0
  end-if
  put #gv into GV(0) totaal

End-procedure

!***********************************************************************
! Retrieve employment records
!***********************************************************************
Begin-procedure Arbeidsrelaties

Begin-Select
EMPLID                  &emplidab
EMPL_RCD               &empl_rcdab
NL_FROM_YEAR            &yearab
NL_FROM_MONTH           &monthab
AGE_NLD                  &leeftijd_ab
EMPL_RCD_ILI_NLD       &rcd#_iliab
EMPL_RCD_ELI_NLD       &rcd#_eliab

  do Bepaal-Lftdgrp(#grp, &leeftijd_ab)

  if $IndLv = 'I'
      let #ab = &rcd#_iliab
  else
      let #ab = &rcd#_eliab
  end-if

  ARRAY-ADD #ab to AB(0) TOTAAL
  if #grp > -1
    ARRAY-ADD #ab to AB(0) LFTDGRP(#GRP)
  end-if

from PS_ABS_STAT_NLD A, PS_DEPT_SEC_VW_NLD C
where C.SETID = $Setid
and C.NL_DEPTID_ROOT = $dept_id
and A.SETID = C.SETID
and A.DEPTID = C.DEPTID
and (($NL_IND_HIERARCHY = 'Y')OR(C.DEPTID = C.NL_DEPTID_ROOT))
and NOT((#VANJR >= NL_FROM_YEAR)
        and NOT(NL_FROM_YEAR = #Vanjr and NL_FROM_MONTH >= #Vanmnd))
and NOT((NL_FROM_YEAR >= #Temjr)
        and NOT(NL_FROM_YEAR = #Temjr and NL_FROM_MONTH <= #Temmnd))
End-Select

  let #grp = 0
  while #grp < 8
    get #ab from AB(0) lftdgrp(#grp)
    let #ab = #ab / #AantalMnd
    put #ab into AB(0) lftdgrp(#grp)
    add 1 to #grp
  end-while
  get #ab from AB(0) totaal
  let #ab = #ab / #AantalMnd
  put #ab into AB(0) totaal

 do MeldingsFreq

End-procedure

!***********************************************************************
! Determine Illness frequency
! Illness Frequency = #Newly Started Illnesses / #Employments
!***********************************************************************
Begin-procedure MeldingsFreq

  let #grp = 0
  while #grp < 8
    get #az from AZ(0) lftdgrp(#grp)
    get #ab from AB(0) lftdgrp(#grp)
    if #ab > 0
      let #mf = #az/#ab
    else
      let #mf = 0
    end-if
    put #mf into MF(0) lftdgrp(#grp)
    add 1 to #grp
  end-while
  get #az from AZ(0) totaal
  get #ab from AB(0) totaal
  if #ab > 0
    let #mf = #az/#ab
  else
    let #mf = 0
  end-if
  put #mf into MF(0) totaal

End-procedure

!***********************************************************************
! Determine Illness age category
!***********************************************************************
Begin-procedure Bepaal-Lftdgrp(:#grp, #lftd)

  let #grp = 0
  get #onder from lftdgrp(#grp) grens
  while (#onder <= #lftd and #onder <> 0)
    add 1 to #grp
    get #onder from lftdgrp(#grp) grens
  end-while
  let #grp = #grp - 1

End-procedure


Begin-procedure Print-Rapport
  print $DEPT                     (+1, 1, 23)
  print $dept_id                        (0, +2,10)
  move $dept_id to $DeptID
  !************************ Begin Comented For Resolution - 618052 *********************
      !do Get-Department-Name-with-SetID
  !************************ End Comented For Resolution - 618052 *********************

  !************************ Begin Resolution - 618052 *********************
    do Get-PSOptions-Language($PSOptions_Language_Cd)
    let $SetIDLang = $SetID
    if $curr_language_cd <> $Psoptions_Language_Cd
       do Get-Department-Name-Lang
       if $DeptName = ''
          do Get-Department-Name-with-SetID
       end-if
    else
       do Get-Department-Name-with-SetID
    end-if
  !************************ End Resolution - 618052  **********************
  print $DeptName    (0,+2)
  print $PERIOD1                     (+1, 1, 23)
  let $Periode = $FromMonth || '-' || $FromYear || ' ' || $PERIOD2 || ' ' || $ThruMonth || '-' || $ThruYear
  print $periode                        (0, +2)
  print $MATERNITY         (+1, 1, 23)
  print $IndZv                          (0, +2)
  print $PARTIAL_ILL             (+1, 1, 23)
  print $IndPv                          (0, +2)
  print $LONGTERM_ILL            (+1, 1, 23)
  print $IndLv                          (0, +2)
  print $TOTAL                        (+1, 45)

  let #array-teller = 0
  get #kolom #grens from lftdgrp(0) kolom grens
  add 3 to #kolom
  print #grens                          (0, #kolom)   edit 999
  add 1 to #array-teller
  get #kolom #grens from lftdgrp(#array-teller) kolom grens
  while #array-teller <> 7 and #grens <> 0
    print '-'                           (0,0)
    let #hulpgrens = #grens - 1
    print #hulpgrens                    (0,0)         edit 999
    add 3 to #kolom
    print #grens                        (0, #kolom)   edit 999
    add 1 to #array-teller
    get #kolom #grens from lftdgrp(#array-teller) kolom grens
  end-while
  print '-'                             (0,0)
  print '999'                           (0,0)

 print $ILLNESS_PERC           (+2, 1,39)
 get #waarde from ZP(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from ZP(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from ZP(0) lftdgrp(#array-teller)
 end-while

print $ILLNESS_FREQ                (+2, 1,39)
 get #waarde from MF(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from MF(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from MF(0) lftdgrp(#array-teller)
 end-while

print $ILLNESS_LENGTH1            (+2, 1)
print $ILLNESS_LENGTH2         (+1, 1,39)
 get #waarde from GV(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from GV(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from GV(0) lftdgrp(#array-teller)
 end-while

print $ILLNESS_ENDED1  (+2, 1,39)
 get #waarde from BZ(0) totaal
 print #waarde                             (0, {ColTot}) edit 9999999999
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from BZ(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 9999999999
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from BZ(0) lftdgrp(#array-teller)
 end-while

 let $prtcat = '>>'
 print $ILLNESS_ENDED2 (+2, 1)

 let #array-teller = 0
 get #onder from cat(#array-teller)
   while #onder <> 0
     print #onder                              (+2, 25, 3) edit 999
     print '-'                                 (0, 28)
     add 1 to #array-teller
     get #onder from cat(#array-teller)
     if #onder <> 0
       let #onderh = #onder - 1
       print #onderh                           (0, 29,3) edit 999
     else
       print $prtcat                           (0, 29,3)
     end-if
     print $DAYS                            (0, 33,7)

     let #hulp-teller = #array-teller - 1
     get #waarde from DZ(#hulp-teller) totaal
     print #waarde                             (0, {ColTot}) edit 9999999999
     let #array-teller2 = 0
     get #kolom #grens from lftdgrp(#array-teller2) kolom grens
     get #waarde from DZ(#hulp-teller) lftdgrp(#array-teller2)
     while #array-teller2 <> 8 and #grens <> 0
       print #waarde                           (0, #kolom)   edit 9999999999
       add 1 to #array-teller2
       get #kolom #grens from lftdgrp(#array-teller2) kolom grens
       get #waarde from DZ(#hulp-teller) lftdgrp(#array-teller2)
     end-while
   end-while

print $ILLNESS_STARTED (+2, 1,39)
 get #waarde from AZ(0) totaal
 print #waarde                             (0, {ColTot}) edit 9999999999
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from AZ(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 9999999999
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from AZ(0) lftdgrp(#array-teller)
 end-while

print $ILLNESS_COUNT             (+2, 1,39)
 get #waarde from ZK(0) totaal
 print #waarde                             (0, {ColTot}) edit 9999999999
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from ZK(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 9999999999
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from ZK(0) lftdgrp(#array-teller)
 end-while

 print $FTE                (+2, 1,39)
 get #waarde from FTE(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from FTE(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from FTE(0) lftdgrp(#array-teller)
 end-while

print $EMPL_COUNT            (+2, 1,39)
 get #waarde from AB(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from AB(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from AB(0) lftdgrp(#array-teller)
 end-while

print $DAY_COUNT    (+2, 1,39)
 get #waarde from AV(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from AV(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from AV(0) lftdgrp(#array-teller)
 end-while

print $ILLNESS_DAY    (+2, 1,39)
 get #waarde from VK(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from VK(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from VK(0) lftdgrp(#array-teller)
 end-while

print $EMPL_NOT_ILL   (+2, 1,39)
 get #waarde from NV(0) totaal
 print #waarde                             (0, {ColTot}) edit 99999999.9
 let #array-teller = 0
 get #kolom #grens from lftdgrp(#array-teller) kolom grens
 get #waarde from NV(0) lftdgrp(#array-teller)
 while #array-teller <> 8 and #grens <> 0
   print #waarde                           (0, #kolom)   edit 99999999.9
   add 1 to #array-teller
   get #kolom #grens from lftdgrp(#array-teller) kolom grens
   get #waarde from NV(0) lftdgrp(#array-teller)
 end-while
 next-listing

End-procedure

!***********************************************************************
! Age categories from absence parameters
!***********************************************************************
Begin-procedure Leeftijdsgroepen

  print 'Totaal'                        (0, +1)

  let #array-teller = 0
  get #kolom #grens from lftdgrp(0) kolom grens
  add 3 to #kolom
  print ','                             (0, 0)
  let #grens = round(#grens, 0)
  print #grens                          (0, +1)   edit 999
  add 1 to #array-teller
  get #kolom #grens from lftdgrp(#array-teller) kolom grens
  while #array-teller <> 7 and #grens <> 0
    print '-'                           (0, 0)
    let #hulpgrens = #grens - 1
    let #hulpgrens = round(#hulpgrens, 0)
    print #hulpgrens                    (0, 0)   edit 999
    add 3 to #kolom
    print ','                           (0, 0)
    let #grens = round(#grens, 0)
    print #grens                        (0, +1)   edit 999
    add 1 to #array-teller
    get #kolom #grens from lftdgrp(#array-teller) kolom grens
  end-while
  print '+'                             (0,0)

End-procedure Leeftijdsgroepen


#include 'stdapi.sqc'    !Routine to update Run status
#include 'hrgetnld.sqc'  !Get the reportparameters (NL)
#include 'hrctlnld.sqc'  !Get run control parameter values (NL)
#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting (NL)
#Include 'number.sqc'  !Routines to format numbers (NL)
#Include 'tranctrl.sqc'  !Transaction control
#Include 'sqrtrans.sqc'
#include 'datewrk.sqc'   !Leap Date Logic
#include 'datemath.sqc'  !date logic
!************************ Begin Resolution - 618052 *********************
#Include 'prcslng.sqc'   !Get Language Codes,support for multiple language codes
 !************************ End Resolution - 618052  **********************
