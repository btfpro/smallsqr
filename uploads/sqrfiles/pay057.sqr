!******************************************
!  PAY057: FINAL CHECK RECONCILE REPORT   *
!******************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!----------------------------------------------------------------------
! PeopleSoft, Inc.; it is not to be copied, reproduced, or transmitted *
!                                                                      *
!                                                                      *
!***********************************************************************
!
!          $Date:  2006/07/20:04:40:49                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************
!***********************************************************************
! Function:                                                            *
!  This report selects the employees that will have a Final Check      *
!  processed (based on a PAY_SHEET_SRC of 'K' or 'L' in PAY_EARNINGS)  *
!  and reconciles that data with the EMPLOYMENT record.  If no         *
!  Termination Date exists, then a warning will be printed to advise   *
!  that the employee does not have a termination record.               *
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'                  ! Set environment
#Include 'setup32.sqc'                 ! Init printer and page size

BEGIN-REPORT                           ! SQR BEGIN Statement

  DO INIT-REPORT                       ! Start the SQR
  DO RESET
  DO STDAPI-TERM                       ! End the SQR

END-REPORT                             ! SQR END Statement

! *******************************************************************
!    DEFINE COLUMNS                                                 *
! *******************************************************************

#DEFINE COL01_NAME              010     ! Column #01 - Employee Name
#DEFINE COL02_EMPLID            062     ! Column #02 - Employee ID
#DEFINE COL03_EMPL_RCD         077     ! Column #03 - Employee Record #
#DEFINE COL04_OFF_CYCLE         084     ! Column #04 - Off-Cycle
#DEFINE COL05_PAGE_NUM             096     ! Column #05 - Page #
#DEFINE COL06_LINE_NUM             104     ! Column #06 - Line #
#DEFINE COL07_PAY_SHEET_SRC     112     ! Column #07 - Pay Sheet Source
#DEFINE COL08_TERM_DT           128     ! Column #08 - Termination Date

BEGIN-PROCEDURE INIT-REPORT

  MOVE 'PAY057' TO $REPORTID

  DO INIT-DATETIME                     ! Perform Date/Time Proc
  DO INIT-NUMBER                       ! Initialize Number
  DO STDAPI-INIT                       ! Standard Initialize
  DO GET-REPORT-LANGUAGE               ! Procedure that decides the report language
                                       ! and then calls 'Report-Translation' procedure
  MOVE $PAY057_ReportTitle to $ReportTitle
  DISPLAY $ReportId
  DISPLAY $ReportTitle

  DO GET-CURRENT-DATETIME              ! Get source database information
  DO PAYROLL-REPORT-INITIALIZATION

END-PROCEDURE                          ! End the Init Procedure

! *******************************************************************
!    Paragraph Begin:  REPORT-TRANSLATION                           *
!    This procedure is used to read language-dependent text from    *
!    the database                                                   *
! *******************************************************************

BEGIN-PROCEDURE REPORT-TRANSLATION
! Read from PAY057 program id

  DO INIT_REPORT_TRANSLATION($ReportID,$language_cd)
  DO GET_FIELD_INFORMATION('PAY057', 'REPORT_TITLE',    $PAY057_ReportTitle,    #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'OFF_CYCLE',       $PAY057_Off_Cycle,      #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'PAGE_NUM',           $PAY057_PAGE_NUM,          #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'LINE_NUM',           $PAY057_LINE_NUM,          #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'PAY_SHEET_SRC',   $PAY057_Pay_Sheet_Src,  #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'EMPLID',          $PAY057_Emplid,         #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'NAME',            $PAY057_Name,           #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'EMPL_RCD',       $PAY057_EMPL_RCD,      #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'TERMINATION_DT',  $PAY057_Termination_Dt, #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'NO_EMP_FND',      $PAY057_No_Emp_Fnd,     #DW)
  DO GET_FIELD_INFORMATION('PAY057', 'NO_TERM_FND',     $PAY057_No_Term_Fnd,    #DW)

END-PROCEDURE           ! End procedure Report-Translation

BEGIN-HEADING 9

#Include 'stdhtr06.sqc'
  PRINT $PAY057_Name            (+2,{COL01_NAME},)
  PRINT $PAY057_Emplid          (0,{COL02_EMPLID},)
  PRINT $PAY057_EMPL_RCD       (0,{COL03_EMPL_RCD},)
  PRINT $PAY057_Off_Cycle       (0,{COL04_OFF_CYCLE},)
  PRINT $PAY057_PAGE_NUM           (0,{COL05_PAGE_NUM},)
  PRINT $PAY057_LINE_NUM           (0,{COL06_LINE_NUM},)
  PRINT $PAY057_Pay_Sheet_Src   (0,{COL07_PAY_SHEET_SRC},)
  PRINT $PAY057_Termination_Dt  (0,{COL08_TERM_DT},)

  PRINT '------------------------------'        (+1,{COL01_NAME},)
  PRINT '----------'                            (0,{COL02_EMPLID},)
  PRINT '----'                                  (0,{COL03_EMPL_RCD},)
  PRINT '---------'                             (0,{COL04_OFF_CYCLE},)
  PRINT '-----'                                 (0,{COL05_PAGE_NUM},)
  PRINT '-----'                                 (0,{COL06_LINE_NUM},)
  PRINT '-------------'                         (0,{COL07_PAY_SHEET_SRC},)
  PRINT '----------------'                      (0,{COL08_TERM_DT},)

END-HEADING

! *******************************************************************
! *  REPORT                                                         *
! *  This is the main paragraph of the SQR.                         *
! *******************************************************************

BEGIN-PROCEDURE REPORT

  LET $Slct_Flag = 'N'

BEGIN-SELECT
A.COMPANY       () on-break print=never  level=1  procedure=page_break
  MOVE &A.COMPANY TO $company
A.PAY_END_DT    () on-break print=never  level=2  procedure=page_break
  MOVE &A.PAY_END_DT TO $PayEndDate
A.PAYGROUP      () on-break print=never  level=3  procedure=page_break
  MOVE &A.PAYGROUP TO $PayGroup
B.EMPLID        (0,{COL02_EMPLID},)
B.EMPL_RCD     (0,{COL03_EMPL_RCD},)
B.OFF_CYCLE     (0,{COL04_OFF_CYCLE},)
B.PAGE_NUM         (0,{COL05_PAGE_NUM},)
B.LINE_NUM         (0,{COL06_LINE_NUM},)
B.PAY_SHEET_SRC
  MOVE 'PAY_SHEET_SRC' TO $FieldName
  MOVE &B.PAY_SHEET_SRC TO $FieldValue
  DO READ-TRANSLATE-TABLE
  PRINT $XLATSHORTNAME  (0,{COL07_PAY_SHEET_SRC},)
C.NAME          (1,{COL01_NAME},)
  DO GET-TERMINATION-DT
  NEXT-LISTING
  MOVE 'Y' TO $Slct_Flag

FROM PS_PAY_CALENDAR A,
     PS_PAY_EARNINGS B,
     PS_NAMES        C

WHERE [$SlctCalendar]
  AND B.OFF_CYCLE IN ($SlctOffCycleA, $SlctOffCycleB)
  AND B.PAGE_NUM BETWEEN #SlctPageFrom AND #SlctPageThru
  AND A.COMPANY    = B.COMPANY
  AND B.PAYGROUP   = A.PAYGROUP
  AND B.PAY_END_DT = A.PAY_END_DT
  AND B.PAY_SHEET_SRC IN ('K', 'L')
  AND B.ADDL_NBR   = 0
  AND C.EMPLID     = B.EMPLID
  AND C.NAME_TYPE  = 'PRI'
  AND C.EFFDT =
      (SELECT MAX(CX.EFFDT)
       FROM   PS_NAMES CX
       WHERE  CX.EMPLID    = C.EMPLID
         AND  CX.NAME_TYPE = C.NAME_TYPE
         AND  CX.EFFDT    <= A.PAY_END_DT)
ORDER BY A.COMPANY,
         A.PAY_END_DT,
         A.PAYGROUP,
         C.NAME,
         B.EMPLID

END-SELECT

  IF $Slct_Flag = 'N'
     PRINT $PAY057_No_Emp_Fnd   (0,{COL01_NAME},)
  END-IF

END-PROCEDURE   ! End procedure Report

! *******************************************************************
! GET-TERMINATION-DT                                                *
!    This procedure retrieves the termination date of an employee   *
!    from the Employment table if the employee has a Final Check    *
!    pay sheet source (K or L).                                     *
! *******************************************************************
BEGIN-PROCEDURE GET-TERMINATION-DT

BEGIN-SELECT

D.TERMINATION_DT
  do Format-DateTime(&D.TERMINATION_DT, $out, {DEFDATE}, '', '')
  PRINT $out    (0,{COL08_TERM_DT},)
D.EMPLID

  If IsNULL(&D.TERMINATION_DT)
    PRINT $PAY057_No_Term_Fnd   (0,{COL08_TERM_DT},)
  End-if

FROM PS_EMPLOYMENT D
WHERE D.EMPLID = &B.EMPLID
  AND D.EMPL_RCD = &B.EMPL_RCD

END-SELECT

END-PROCEDURE           ! End procedure Get-Termination-Dt

BEGIN-PROCEDURE page_break

  new-page

END-PROCEDURE

! *******************************************************************
! *  Include Section                                                *
! *******************************************************************
#Include 'stdapi.sqc'    ! Standard API Procedure
#Include 'curdttim.sqc'  ! Get-Current-DateTime procedure
#Include 'datetime.sqc'  ! Routines for date and time formatting
#Include 'number.sqc'    ! Routines to format numbers
#Include 'getcodta.sqc'  ! Get-Company-Data procedure
#Include 'getpgdta.sqc'  ! Get-PayGroup-Data procedure
#Include 'payinit.sqc'   ! Report Initialization and Timing
#Include 'payrnctl.sqc'  ! Get-Run-Control procedure
#Include 'reset.sqc'     ! Reset printer procedure
#Include 'sqrtrans.sqc'  ! Translate SQR strings to a given language
#Include 'getrplng.sqc'  ! Gets the report language
#Include 'readxlat.sqc'  ! Routine to read the Translate Table

! *******************************************************************
!                   <<<   END OF FILE   >>>
! *******************************************************************
