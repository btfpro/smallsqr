!***************************************
!  PER022:  EEO-5 Job Analysis         *
!***************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:32:27                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************


#include 'setenv.sqc'    !Set environment
Begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-heading 8
!********BEGIN RESOLUTION 581425***********
  !#Include 'stdhdg03.sqc'
  !print 'Location Group:  '                          (+1,1)
  !print $Location                                    ()
  !print '                      Employee  Minority  ' (+2,1)
  !print '<-------------- M A L E --------------->  ' ()
  !print '<------------ F E M A L E ------------->'   ()
  !print 'EEO Category            Total     Total   ' (+1,1)
  !print '   White   Black    Hisp   Asian  Indian  ' ()
  !print '   White   Black    Hisp   Asian  Indian'   ()
  
  ALTER-PRINTER
        font=4
        point-size=15
    PRINT $ReportTitle                                 (,1) BOLD
  ALTER-PRINTER
        font=4
        point-size=10
    print $Location                                    (+2,6)
  ALTER-PRINTER
    font=4
    point-size=7
    let #RptCol = {ColR} - 2
    page-number             (0,#RptCol)  'Page No.  '
    print 'Run Date '       (+1,#RptCol)
    print $ReportDate       ()
    print 'Run Time '       (+1,#RptCol)
    print $ReportTime       ()
    print 'As Of Date: '    (0,1)
    do Format-DateTime($AsOfDate, $out, {DEFDATE}, '', '')
    print $out ()
  
  ALTER-PRINTER
        font=4
        point-size = 6
        GRAPHIC (+2,1,130) HORZ-LINE
    
        PRINT 'Total'                               (+1,25) BOLD
        PRINT 'Total'                               (,35)   BOLD
        PRINT 'MALE'                                (,61)   BOLD
        PRINT 'FEMALE'                              (,101)  BOLD
    
        PRINT 'EEO Category'                        (+1,1)  BOLD
        PRINT 'Employees'                           (,25)   BOLD
        PRINT 'Minorities'                          (,35)   BOLD
          
        PRINT 'White'                               (,45)   BOLD
        PRINT 'Black'                               (,53)   BOLD
        PRINT 'Hisp.'                               (,61)   BOLD
        !*******Begin Resolution - 619239 **********
        !PRINT 'Asian'                               (,69)   BOLD
        PRINT 'Asn/PcIs'                               (,67)   BOLD
        !*******End Resolution - 619239 ************
        PRINT 'Nat. Am'                             (,76)   BOLD
      
        PRINT 'White'                               (,87)   BOLD
        PRINT 'Black'                               (,95)   BOLD
        PRINT 'Hisp.'                               (,103)  BOLD
        !*******Begin Resolution - 619239 **********
        !PRINT 'Asian'                               (,111)  BOLD
        PRINT 'Asn/PcIs'                               (,109)  BOLD
        !*******End Resolution - 619239 ************
        PRINT 'Nat. Am'                             (,118)  BOLD
  
      GRAPHIC (,1,130) HORZ-LINE
!****************END RESOLUTION 581425***********************
end-heading

begin-procedure Init-Report
  move 'PER022' to $ReportID
  move 'EEO-5 Job Analysis' to $ReportTitle
  display $ReportTitle

  do Init-Array

  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-As-Of-date
  else
    do Select-Parameters
  end-if

end-procedure

begin-procedure Get-Values
  do Get-As-Of-Date
end-procedure

begin-procedure Init-Array
  create-array name=EEOarray  size=20 field=count:number:12 field=label:char
  create-array name=EEOtotals size=20 field=count:number:12
  put 'Officials,Admin,Mgrs' into EEOarray(1)  label
  put 'Principals'           into EEOarray(2)  label
  put 'Asst.Prin.-Teach'     into EEOarray(3)  label
  put 'Asst.Prin.-NonTeach'  into EEOarray(4)  label
  put 'Elemntry Clsrm Teach' into EEOarray(5)  label
  put 'Secondry Clsrm Teach' into EEOarray(6)  label
  put 'Other Clsrm Teach'    into EEOarray(7)  label
  put 'Guidance'             into EEOarray(8)  label
  put 'Psychological'        into EEOarray(9)  label
  put 'Librarian/Audiovsual' into EEOarray(10) label
  put 'Consultnts/Supervsrs' into EEOarray(11) label
  put 'Other Prof. Staff'    into EEOarray(12) label
  put 'Teacher Aides'        into EEOarray(13) label
  put 'Technicians'          into EEOarray(14) label
  put 'Clerical/Secretarial' into EEOarray(15) label
  put 'Service Workers'      into EEOarray(16) label
  put 'Craft Wrkers-Skilled' into EEOarray(17) label
  put 'Laborers-Unskilled'   into EEOarray(18) label
  put '   TOTAL'             into EEOarray(19) label
  move 19 to #TotRow    !Row # of TOTAL above
  move 23 to #startCol  !starting position of first column total
  move  8 to #len       !length of total (in print positions)
end-procedure

begin-procedure Process-Main
begin-SELECT
E.ESTABID () on-break print=never procedure=Print-Report
E.DESCR
JC.EEO5CODE
A.SEX
ETH.ETHNIC_GROUP
COUNT(*)
  move &A.Sex          to $Sex
  move &ETH.Ethnic_Group to $EthnicGroup
  move &E.Descr        to $Location
  do Increment-Array
FROM  PS_PERS_DATA_MAX A,
      PS_JOB J,
      PS_JOBCODE_TBL JC,
      PS_ESTAB_TBL E,
      PS_DIVERS_ETHNIC DVR,
      PS_ETHNIC_GRP_TBL ETH
WHERE J.PER_ORG = 'EMP'
  AND J.REG_TEMP = 'R'
  AND (J.HR_STATUS = 'A'                            !HP99999
           AND J.EFFDT <= $AsOfDate)
  AND DVR.EMPLID = A.EMPLID
  AND A.EMPLID = J.EMPLID
  AND E.ESTABID = J.ESTABID
  AND J.EFFDT =
      (SELECT MAX(J1.EFFDT)
       FROM PS_JOB J1
       WHERE J1.EMPLID = J.EMPLID
         AND J1.EMPL_RCD = J.EMPL_RCD
         AND J1.EFFDT <= $AsofDate)
  AND J.EFFSEQ =
      (SELECT MAX(J2.EFFSEQ)
       FROM PS_JOB J2
       WHERE J2.EMPLID = J.EMPLID
         AND J2.EMPL_RCD = J.EMPL_RCD
         AND J2.EFFDT = J.EFFDT)
  AND J.JOB_INDICATOR = 'P'
  AND JC.JOBCODE = J.JOBCODE
  AND JC.SETID = J.SETID_JOBCODE
  AND JC.EFFDT =
      (SELECT MAX(JC1.EFFDT)
       FROM PS_JOBCODE_TBL JC1
       WHERE JC1.JOBCODE = JC.JOBCODE
         AND JC1.SETID = JC.SETID
         AND JC1.EFFDT <= J.EFFDT)
  AND DVR.PRIMARY_INDICATOR = 'Y' 
  AND DVR.SETID = ETH.SETID 
  AND DVR.ETHNIC_GRP_CD = ETH.ETHNIC_GRP_CD 
  AND ETH.EFFDT = (SELECT MAX(E2.EFFDT) FROM PS_ETHNIC_GRP_TBL E2 
                  WHERE E2.SETID = ETH.SETID 
                  AND E2.ETHNIC_GRP_CD = ETH.ETHNIC_GRP_CD 
                  AND E2.EFFDT <= $AsofDate)
  AND ETH.ETHNIC_GROUP  <> '6'  !Not Applicable
  AND J.EEO_CLASS     <> 'E'  !Exclude from counts
  AND JC.EEO5CODE     <> 'N'  !Not counted for EEO reporting
  AND E.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_ESTAB_TBL
       WHERE  ESTABID = E.ESTABID
         AND  EFFDT  <= $AsofDate)
GROUP BY E.ESTABID,E.DESCR,JC.EEO5CODE,A.SEX,ETH.ETHNIC_GROUP
ORDER BY E.ESTABID
end-SELECT

do Print-Report  !Print last location's report

move 'District Summary' to $Location
move 1 to #EEOrow    !Load the "print" array with the District totals
while #EEOrow <= #TotRow
  move 0 to #EEOcol
  while #EEOcol <= 11
    get #count1 from EEOtotals(#EEOrow) count(#EEOcol)
    put #count1 into EEOarray(#EEOrow)  count(#EEOcol)
    add 1 to #EEOcol
  end-while
  add 1 to #EEOrow
end-while
do Print-Report  !Print District Summary Statistics
end-procedure

begin-procedure Print-Report
  move 2 to #line
  move 1 to #EEOrow
  while #EEOrow <= #TotRow
  !******************BEGIN RESOLUTION 581425**********************
  
  ALTER-PRINTER
        font=3
        point-size=6
    
    IF #EEOrow%2 = 0
       GRAPHIC (+1,1,130) box 3 0 15
     ELSE
       POSITION (+1,,)
  END-IF
  
  !******************END RESOLUTION 581425***********************
    get $EEOcatName from EEOarray(#EEOrow) label
    print $EEOcatName   (#line,1)
    print '% of Row'    (+1,3)
    print '% of Column' (+1,3)
    move #startCol to #col
    get #rowTotal from EEOarray(#EEOrow) count(0)
    move 0 to #EEOcol
    while #EEOcol <= 11
      get #count1 from EEOarray(#EEOrow) count(#EEOcol)
      array-add #count1 to EEOtotals(#EEOrow) count(#EEOcol)
      do Format-Number(#count1, $out, 'B999,999')
      print $out (#line,#col)
      multiply 100 times #count1
      move #count1 to #count2
      divide #rowTotal into #count1 on-error=zero
      do Format-Number(#count1, $out, 'B9999.99')
      print $out (+1,#col)
      get #colTotal from EEOarray(#TotRow) count(#EEOcol)
      divide #colTotal into #count2 on-error=zero
      do Format-Number(#count2, $out, 'B9999.99')
      print $out (+1,#col)
      put 0 into EEOarray(#EEOrow) count(#EEOcol)
      add #len to #col
      if #EEOcol <= 1 or #EEOcol = 6
        add 2 to #col
      end-if
      add 1 to #EEOcol
    end-while
    !***************BEGIN RESOLUTION 581425**************************
    !if #EEOrow = 12
    if #EEOrow = 18
            GRAPHIC (,1,130) HORZ-LINE
        end-if
        if #EEOrow = 19
        GRAPHIC (,1,130) HORZ-LINE
      !move 2 to #line
      !new-page
      move 1 to #line
    else
      !add 4 to #line
      add 3 to #line
    !***************END RESOLUTION 581425**************************  
    end-if
    add 1 to #EEOrow
  end-while
  new-page
end-procedure

begin-procedure Increment-Array
  do Analyze-Sex-EthnicGroup  !returns 1 to 10 in #EEOcol
  add 1 to #EEOcol            !convert to print column
  move &JC.EEO5code to #EEOrow
  array-add &COUNT(*) &COUNT(*) to EEOarray(#EEOrow) count(#EEOcol) count (0)
  array-add &COUNT(*) &COUNT(*) to EEOarray(#TotRow) count(#EEOcol) count (0)
  if $EthnicGroup > '1'  !if a Minority group
    array-add &COUNT(*) to EEOarray(#EEOrow) count(1)
    array-add &COUNT(*) to EEOarray(#TotRow) count(1)
  end-if
end-procedure

#include 'rgrnctl1.sqc'  !Get run control parameter values
#include 'rggetval.sqc'  !Get values mask routines
#include 'askaod.sqc'    !Ask As Of Date input

#Include 'eeogroup.sqc'  !Analyze-Sex-EthnicGroup procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
