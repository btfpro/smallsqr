!***********************************************************************
!  PI714.SQR:INTERFACE EMPLOYEE TABLE                                  *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:02:34:15                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup31.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-procedure Init-Report
  move 'PI714' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi714_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation

! Read from PI714 program ids
do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PI714', 'REPORT_TITLE', $Pi714_ReportTitle, #dummy_width)
  do Get_Field_Information ('PI714', 'EMPLID', $Pi714_Empleid, #dummy_width)
  do Get_Field_Information ('PI714', 'EMPL_REC', $Pi714_EmplRec, #dummy_width)
  do Get_Field_Information ('PI714', 'EFFDT', $Pi714_EffDt, #dummy_width)
  do Get_Field_Information ('PI714', 'SID', $Pi714_Sid, #dummy_width)
  do Get_Field_Information ('PI714', 'DESCR', $Pi714_Descr, #dummy_width)
  do Get_Field_Information ('PI714', 'PI_EMPLID', $Pi714_PIEmplid, #dummy_width)

end-procedure


begin-heading 7
#Include 'stdhdgtr.sqc'

  print $Pi714_Empleid       (+2,1,20)
  print $Pi714_EmplRec       (,23,16)
  print $Pi714_EffDt         (,41,10)
  print $Pi714_Sid           (,53,8)
  print $Pi714_Descr         (,63,15)
  print $Pi714_PIEmplid      (,80,20)

end-HEADING


begin-procedure Process-Main

begin-SELECT

A.EMPLID                     (+2,1,20)
A.EMPL_RCD                  (,23,16)
A.EFFDT
    do Format-DateTime(&A.EFFDT, $out, {DEFDATE}, '', '')
    print $out               (,41,10)
A.PI_SYSTEM_ID               (,53,8)
B.DESCRSHORT                ! (,63,15)

      Let $Prnt_PiSys_Descrshort = &B.DESCRSHORT
      if $Curr_language_cd <> $Psoptions_Language_Cd
        do Lang-Record-Details
      end-if
      Print  $Prnt_PiSys_Descrshort (,63,15)

A.PI_EMPLID                  (,80,20)

FROM PS_PI_EMPLID_TBL A, PS_PI_SYSTEM_TBL B
WHERE A.PI_SYSTEM_ID = B.PI_SYSTEM_ID  AND B.EFFDT =
    (SELECT MAX(EFFDT)
       FROM PS_PI_SYSTEM_TBL
           WHERE PI_SYSTEM_ID = A.PI_SYSTEM_ID
                 AND EFF_STATUS = 'A'
                 AND EFFDT     <= A.EFFDT)
ORDER BY A.EMPLID, A.EMPL_RCD, A.EFFDT DESC

end-SELECT

end-procedure


begin-procedure Lang-Record-Details
begin-select

LB.DESCRSHORT

   Let $Prnt_PiSys_Descrshort = Cond(Rtrim(&LB.DESCRSHORT,' ')='', $Prnt_PiSys_Descrshort, &LB.DESCRSHORT)

FROM PS_PI_SYSTEM_LANG LB
WHERE PI_SYSTEM_ID = &A.PI_SYSTEM_ID
  AND LANGUAGE_CD  = $Curr_language_cd
  AND EFFDT        = (SELECT MAX(EFFDT) FROM PS_PI_SYSTEM_LANG
                       WHERE PI_SYSTEM_ID = &A.PI_SYSTEM_ID
                         AND LANGUAGE_CD  = $Curr_language_cd
                         AND EFFDT <=&A.EFFDT )

end-select
end-procedure

#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

