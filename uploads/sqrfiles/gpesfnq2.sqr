!***********************************************************************
!  GPESFNQ2:  Termination Release Document                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!----------------------------------------------------------------------
!                                                                      
!          $Date:  2006/07/20:14:29:39                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment

 begin-setup
#include 'setupdb.sqc'               ! Database specific setup

#define PTSETxx-Included

    declare-printer DEFAULT-HP
       point-size=7.2
       pitch=17
    end-declare

    declare-printer DEFAULT-PD
       point-size=7.2
       pitch=17
    end-declare

    #ifndef EBCDIC
    declare-printer DEFAULT-LP
       init-string=<27>E<27>(0N<27>&l0O<27>&l6C<27>&l0E<27>&l95F<27>(s16.66H<27>&k2G
!               |    |      |       |       |       |        |           |
!               |    |      |       |       |       |        |            --> CR
!               |    |      |       |       |       |         --> Line Prntr font
!               |    |      |       |       |        --> Number of Lines
!               |    |      |       |        --> Top Margin
!               |    |      |        --> Vertical Motion Index (1/48" increments)
!               |    |       --> Portrait orientation
!               |     --> ISO 8859-1 symbol set
!                --> Reset
       before-bold=<27>(s3B
       after-bold=<27>(s0B
    end-declare

    #else

    declare-printer DEFAULT-LP
    end-declare
    #endif

    declare-printer DEFAULT-PS
       point-size=7.2
    end-declare

#define PAGE_ORIENTATION PORTRAIT

#Define ColR 105  !Column # referenced by Standard Headings

 !    #if {PAPER_SIZE} = 'LETTER'
 !#define PAGE_PAPER_SIZE (LETTER)
 !#define LINE_HEIGHT 13
 !#define CHAR_WIDTH  3.20
 !    #End-if

 !    #if {PAPER_SIZE} = 'A4'

#define PAGE_PAPER_SIZE (A4)
#define LINE_HEIGHT 13
#define CHAR_WIDTH 3.20
 !    #end-if

    declare-layout DEFAULT
       paper-size={PAGE_PAPER_SIZE}
       orientation={PAGE_ORIENTATION}
       line-height={LINE_HEIGHT}              ! 72/printer_point-size
       char-width={CHAR_WIDTH}                ! points, to handle max cols
       left-margin=.05
       bottom-margin=.25
    end-declare

    declare-report DEFAULT
       layout=DEFAULT
       printer-type=HP
    end-declare

 END-SETUP


!************************************************************************
! Report Section
!************************************************************************
 
begin-report
    do Init-Report
    do Process-Main
    do Stdapi-Term
end-report

!************************************************************************
! Procedure Init-Report
!************************************************************************

begin-procedure Init-Report
    do Init-DateTime
    do Init-Number
    do Get-Current-DateTime

    do Stdapi-Init
end-procedure


!************************************************************************
! Procedure Main
!************************************************************************
 begin-procedure Process-Main
#debug do Fin-Debug-Msg('Process-Main')

#define INICIO_DEVENGOS 15
#define E_D_DESCRIPCION 35
#define E_D_TOTAL 110
    Let #linea = {INICIO_DEVENGOS}

Let $sql-statement = 'GPESFNQ2.SQR,Process-Main,Select,PS_GPES_RC_FNQ2'
begin-SELECT on-error=SQL-Error

R.EMPLID
R.EMPL_RCD
    LET $Select-Emplid =   rtrim(&R.EMPLID, '')
    LET #Select-Empl-Rcd =    &R.EMPL_RCD


#debugd show 'Operation Id: ' $prcs_oprid
#debugd show 'Run Control Id: ' $prcs_run_cntl_id
#debugd show 'Report language: ' $Curr_Language_Cd
#debugd show 'Base language: ' $PSoptions_language_cd
    
    do Check-Empl-Termtn-Dt

FROM PS_GPES_RC_FNQ2 R
WHERE R.OPRID = $prcs_oprid
AND  R.RUN_CNTL_ID = $prcs_run_cntl_id

    End-SELECT


 end-procedure

!************************************************************************
! Procedure Check-Empl-Termtn-Dt
!************************************************************************
begin-procedure Check-Empl-Termtn-Dt
#debug do Fin-Debug-Msg('Check-Empl-Termtn-Dt')

Let $Empl-Terminated = 'N'

Let $sql-statement = 'GPESFNQ2.SQR,Check-Empl-Termtn-Dt,Select,PS_EMPLOYMENT'
begin-SELECT on-error=SQL-Error
{DateTimeOut-Prefix}EMPLMNT.HIRE_DT{DateTimeOut-Suffix}         &EMPLMNT.HIRE_DT
   do Format-DateTime(&EMPLMNT.HIRE_DT, $Hire_Out, {DEFDMY}, '', '')
{DateTimeOut-Prefix}EMPLMNT.TERMINATION_DT{DateTimeOut-Suffix}  &EMPLMNT.TERMINATION_DT
   If &EMPLMNT.TERMINATION_DT <> ''
     do Format-DateTime(&EMPLMNT.TERMINATION_DT, $Termination_Out, {DEFDMY}, '', '')

     do Convert-To-DTU-Date(&EMPLMNT.TERMINATION_DT, $Termination_DTU_Dt)
     do DTU-Month-Begin($Termination_DTU_Dt, $Termination-Pd-Bgn-Dt)
     do Convert-From-DTU-Date($Termination-Pd-Bgn-Dt, $Termination-Pd-Bgn-Dt)
     do DTU-Month-End($Termination_DTU_Dt, $Termination-Pd-End-Dt)
     do Convert-From-DTU-Date($Termination-Pd-End-Dt, $Termination-Pd-End-Dt)
   End-if
   
{DateTimeOut-Prefix}EMPLMNT.REHIRE_DT{DateTimeOut-Suffix}       &EMPLMNT.REHIRE_DT
 !EMPLMNT.TERMINATION_DT + 1                                      &JOB_EFFDT

   do Convert-To-DTU-Date(&EMPLMNT.TERMINATION_DT, $EffDt-Wk)
   do dtu-add-days($EffDt-Wk , 1 , $Effdt)
   do Convert-From-DTU-Date($Effdt, $Effdt)


#debugd show 'Employee: ' $Select-Emplid
#debugd show 'Employee record: ' #Select-Empl-Rcd
#debugd show 'Employee hire dt: ' &EMPLMNT.HIRE_DT
#debugd show 'Employee Termination dt: ' &EMPLMNT.TERMINATION_DT
#debugd show 'Termination Period Bgn/End Dt''s:''' $Termination-Pd-Bgn-Dt ' - ' $Termination-Pd-End-Dt
#debugd show 'Employee rehire dt: ' &EMPLMNT.REHIRE_DT

   Let $Empl-Terminated = 'Y'
   do Format-DateTime($AsOfToday,$Fecha,{DEFDMY},'','')
   do JOB
   do PERSONAL-DATA
   do COMPANY-TBL

   Let $Day = {PS-substr}($Fecha,1,2)
   Let $Month = {PS-substr}($Fecha,4,2)
   Let $Year = {PS-substr}($Fecha,7,4)

   do GET-MONTH-NAME

   Let $Linea-Fecha = 'En ' || $Cmpny-City || ' ,a ' || $Day || ' de ' || $Monthname || ' de ' || $Year

   ALTER-PRINTER
   point-size=10
   font=5
   do CABECERA
   do GET-LINE-DETAILS
   Let #Linea = #Linea + 2
   do TOTAL-NETO
   DO PIE
   
   
FROM PS_EMPLOYMENT EMPLMNT

WHERE EMPLMNT.EMPLID = $Select-Emplid
AND EMPLMNT.EMPL_RCD = #Select-Empl-Rcd


end-SELECT


end-procedure

!************************************************************************
! Procedure JOB
!************************************************************************
begin-procedure JOB
#debug do Fin-Debug-Msg('JOB')

#debugd show 'JOB EFFDT: ' $Effdt

Let $sql-statement = 'GPESFNQ2.SQR,JOB,Select,PS_JOB'
begin-SELECT on-error=SQL-Error
JOB.EMPLID
JOB.EMPL_RCD
JOB.EFFDT
JOB.COMPANY
JOB.SETID_LOCATION
JOB.LOCATION
JOB.SETID_JOBCODE
JOB.JOBCODE
JOB.REG_REGION
JOB.LABOR_AGREEMENT
JOB.EMPL_CTG
JOB.GP_PAYGROUP

    Do GET-JOBCODE-DESCR

FROM PS_JOB JOB

WHERE JOB.EMPLID = $Select-Emplid
AND JOB.EMPL_RCD = #Select-Empl-Rcd
AND JOB.EFFDT = $Effdt
AND JOB.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB JOB3
       WHERE JOB3.EMPLID = JOB.EMPLID
       AND  JOB3.EMPL_RCD = JOB.EMPL_RCD
       AND  JOB3.EFFDT = JOB.EFFDT)
end-SELECT

#debugd show 'As Of Today: ' $AsOfToday

end-procedure


!************************************************************************
! Procedure JOBCODE-TBL
!************************************************************************
 begin-procedure Get-JobCode-Descr
#debug do Fin-Debug-Msg('Get-JobCode-Descr')

    Let $sql-statement = 'GPESFNQ2.SQR,Get-JobCode-Descr,Select,PS_JOBCODE_TBL'
begin-SELECT on-error=SQL-Error
JOBCODE.EFFDT
JOBCODE.DESCR

       if $Curr_Language_Cd <> $PSoptions_language_cd
          do Get-JobCode-Lang
       end-if
       if $Empl-JobCode-Desc = ''
          Let $Empl-JobCode-Desc = rtrim(&JOBCODE.DESCR, ' ')
       end-if


FROM PS_JOBCODE_TBL JOBCODE

WHERE JOBCODE.SETID = &JOB.SETID_JOBCODE
     AND JOBCODE.JOBCODE = &JOB.JOBCODE
     AND JOBCODE.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOBCODE_TBL JOBCODE2
           WHERE JOBCODE2.SETID = JOBCODE.SETID
                AND  JOBCODE2.JOBCODE = JOBCODE.JOBCODE
               AND  JOBCODE2.EFFDT <= &JOB.EFFDT)
AND JOBCODE.EFF_STATUS <> 'I'

end-SELECT

#debugd show 'Setid: ' &JOB.SETID_JOBCODE
#debugd show 'Job Code: ' &JOB.JOBCODE
#debugd show 'Description: ' $Empl-JobCode-Desc

 end-procedure

!************************************************************************
! Procedure Get-JobCode-Lang
!************************************************************************
BEGIN-PROCEDURE Get-JobCode-Lang
#debug do Fin-Debug-Msg('Get-JobCode-Lang')

    Let $sql-statement = 'GPESFNQ2.SQR,Get-JobCode-Lang,Select,PS_JOBCODE_LANG'
BEGIN-SELECT on-error=SQL-Error
JOBCODELNG.DESCR

       LET $Empl-JobCode-Desc = &JOBCODELNG.DESCR

FROM PS_JOBCODE_LANG JOBCODELNG

WHERE JOBCODELNG.SETID = &JOB.SETID_JOBCODE
     AND JOBCODELNG.JOBCODE = &JOB.JOBCODE
     AND JOBCODELNG.EFFDT = &JOBCODE.EFFDT
     AND JOBCODELNG.LANGUAGE_CD = $Curr_Language_Cd

END-SELECT

END-PROCEDURE

!************************************************************************
! Procedure PERSONAL-DATA
!************************************************************************
begin-procedure PERSONAL-DATA
#debug do Fin-Debug-Msg('PERSONAL-DATA')

    Let $Empl-Name = ''
    Let $sql-statement = 'GPESFNQ2.SQR,PERSONAL-DATA,Select,PS_PERSONAL_DT_FST'
begin-SELECT on-error=SQL-Error
PDTA.NAME
    Let $Name = &PDTA.NAME
    Do Rotate-Name
PDTA.SEX
    Let $sex = &PDTA.SEX
    if $sex = 'M'
       Let $Linea = 'D. '
    else
       Let $Linea = 'Dña. '
    end-if

FROM PS_PERSONAL_DT_FST PDTA
    WHERE PDTA.EMPLID = $Select-Emplid

end-SELECT

#debugd show 'Employee ID: ' $Select-Emplid
#debugd show 'Employee name: ' $Name

 end-procedure

!************************************************************************
! Procedure COMPANY-TBL
!************************************************************************
begin-procedure COMPANY-TBL
#debug do Fin-Debug-Msg('COMPANY-TBL')

   Let $Cmpny-Descr = ''
   Let $Cmpny-City = ''
   Let $sql-statement = 'GPESFNQ2.SQR,COMPANY-TBL,Select,PS_COMPANY_TBL'
begin-SELECT on-error=SQL-Error
CMPNY.EFFDT
CMPNY.DESCR
       if $Curr_Language_Cd <> $PSoptions_language_cd
          do Get-Cmpny-Lang
       end-if
       if $Cmpny-Descr = ''
          Let $Cmpny-Descr = rtrim(&CMPNY.DESCR, ' ')
       end-if

CMPNY.CITY
       Let $Cmpny-City = RTRIM(&CMPNY.CITY, ' ' )

FROM PS_COMPANY_TBL CMPNY

WHERE CMPNY.COMPANY = &JOB.COMPANY
    AND CMPNY.EFFDT = (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL CMPNY2
         WHERE CMPNY2.COMPANY = CMPNY.COMPANY
             AND  CMPNY2.EFFDT <= &JOB.EFFDT)
AND CMPNY.EFF_STATUS <> 'I'

end-SELECT

#debugd show 'Company Description: ' $Cmpny-Descr
#debugd show 'Company City: ' $Cmpny-City

END-PROCEDURE

!************************************************************************
! Procedure Get-Cmpny-Lang
!************************************************************************
BEGIN-PROCEDURE Get-Cmpny-Lang
#debug do Fin-Debug-Msg('Get-Cmpny-Lang')

Let $sql-statement = 'GPESFNQ2.SQR,Get-Cmpny-Lang,Select,PS_COMPNY_TBL_LANG'
BEGIN-SELECT on-error=SQL-Error
CMPNYLNG.DESCR

       LET $Cmpny-Descr = &CMPNYLNG.DESCR

FROM PS_COMPNY_TBL_LANG CMPNYLNG

WHERE CMPNYLNG.COMPANY = &JOB.COMPANY
     AND CMPNYLNG.EFFDT = &CMPNY.EFFDT
     AND CMPNYLNG.LANGUAGE_CD = $Curr_Language_Cd

END-SELECT

END-PROCEDURE


!************************************************************************
! Procedure Get-Paylsip-TmpltID
!************************************************************************
begin-procedure Get-Paylsip-TmpltID
#debug do Fin-Debug-Msg('Get-Paylsip-TmpltID')

#debugd show 'As Of Date: ' &S.SEG_END_DT
#debugd show 'Pay Group:  ' &JOB.GP_PAYGROUP

  Let $PaySlip_ID = ''
  Let $Pslip_EFFDT = ''
         
Let $sql-statement = 'GPESFNQ2.SQR,Get-Paylsip-TmpltID,Select,PS_GPES_PSLIP_GRP'
BEGIN-SELECT on-error=SQL-Error
GRP1.GPES_PAYSLIP_ID
GRP1.EFFDT

  Let $PaySlip_ID  =  &GRP1.GPES_PAYSLIP_ID
  Let $Pslip_EFFDT =  &GRP1.EFFDT


FROM PS_GPES_PSLIP_GRP GRP1

WHERE GRP1.GP_PAYGROUP = &JOB.GP_PAYGROUP
  AND GRP1.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPES_PSLIP_GRP
                     WHERE GPES_PAYSLIP_ID = GRP1.GPES_PAYSLIP_ID
                       AND GP_PAYGROUP = GRP1.GP_PAYGROUP
                       AND EFFDT <= &S.SEG_END_DT)
end-select

#debugd show 'PaySlip Template ID: ' $PaySlip_ID
#debugd show 'PaySlip EFFDT: ' $Pslip_EFFDT

end-procedure


!************************************************************************
! Procedure GET-LINE-DETAILS
!************************************************************************
begin-procedure GET-LINE-DETAILS
#debug do Fin-Debug-Msg('GET-LINE-DETAILS')

Let $primer_devengo =  'Y'
Let #Total-Devengo  =  0
Let #E_Total        =  0
Let #D_Total        =  0

Let $sql-statement = 'GPESFNQ2.SQR,GET-LINE-DETAILS,Select,PS_GP_RSLT_ED_VW'
BEGIN-SELECT on-error=SQL-Error
A.EMPLID
A.CAL_RUN_ID
A.EMPL_RCD
A.GP_PAYGROUP
A.CAL_ID
A.ORIG_CAL_RUN_ID
A.RSLT_SEG_NUM
A.INSTANCE
A.PIN_NUM
A.CALC_RSLT_VAL
S.SEG_END_DT
       Let #E_D_Cantidad = &A.CALC_RSLT_VAL

B.PIN_TYPE
       do Get-Paylsip-TmpltID
       LET $E_D_Type = RTRIM(&B.PIN_TYPE, ' ')
       Evaluate $E_D_Type
       When = 'DD'
          Let #D_Total      = #D_Total + #E_D_Cantidad
          Let #E_D_Cantidad = #E_D_Cantidad * (-1)
          Do Get-DD-Descr
          Break
       When = 'ER'
          Let #E_Total      = #E_Total + #E_D_Cantidad
          Do Get-ER-Descr
          Break
       End-Evaluate

       If $E_D_descripcion <> ''     !Element found in payslip setup
          LET #Total-Devengo = #Total-Devengo + #e_d_cantidad
       !   Let $E_D_descripcion = '**Descr. no encontrada Grupo Pago ' || rtrim(&JOB.GP_PAYGROUP, ' ') || '**'
          if $primer_devengo = 'Y'
             Let #Linea = {inicio_devengos} + 1
             Let $primer_devengo = 'N'
          else
             Let #Linea = #Linea + 1
          end-if
          do Body-Control

          print $E_D_descripcion   (#Linea,{e_d_descripcion})
          print #E_D_cantidad      (,{E_D_TOTAL}) edit B,999,999.99
       End-If


FROM PS_GP_PIN B 
    ,PS_GP_RSLT_ERN_DED A 
    ,PS_GP_PYE_SEG_STAT S 

WHERE S.EMPLID = $Select-emplid
  AND S.CAL_RUN_ID = S.ORIG_CAL_RUN_ID
  AND S.EMPL_RCD = #Select-Empl-Rcd
  AND S.GP_PAYGROUP = &JOB.GP_PAYGROUP
  AND A.PIN_NUM = B.PIN_NUM 
  AND A.EMPLID = S.EMPLID 
  AND A.EMPL_RCD = S.EMPL_RCD 
  AND A.CAL_RUN_ID = S.CAL_RUN_ID 
  AND A.ORIG_CAL_RUN_ID = S.ORIG_CAL_RUN_ID 
  AND A.CAL_ID = S.CAL_ID 
  AND A.GP_PAYGROUP = S.GP_PAYGROUP 
  AND A.RSLT_SEG_NUM = S.RSLT_SEG_NUM 
  AND A.CALC_RSLT_VAL <> 0
  AND S.SEG_BGN_DT >= $Termination-Pd-Bgn-Dt
  AND S.SEG_END_DT <= $Termination-Pd-End-Dt
ORDER BY B.PIN_TYPE DESC

end-select

#debugd show 'Total Devengos:    ' #E_Total
#debugd show 'Total Deducciones: ' #D_Total
end-procedure


!************************************************************************
! Procedure Get-ER-Descr
!************************************************************************
begin-procedure Get-ER-Descr
#debug do Fin-Debug-Msg('Get-ER-Descr')

#debugd show 'As Of Date: ' &S.SEG_END_DT
#debugd show 'Pay Group:  ' &JOB.GP_PAYGROUP
#debugd show 'Pin Num:    ' &A.PIN_NUM

Let $E_D_Descripcion = ''
         
Let $sql-statement = 'GPESFNQ2.SQR,Get-ER-Descr,Select,PS_GPES_PAYSLIP_LN'
BEGIN-SELECT on-error=SQL-Error
LN.GPES_ELEM_DESCR
LN.DESCR

  If &LN.GPES_ELEM_DESCR = 'T'    ! User defined description
     Let $E_D_Descripcion = rtrim(&LN.DESCR,'')
     If $Curr_Language_Cd <> $PSoptions_language_cd
        do Get-PAYSLIP_LN-Lang
     End-if
  Else    ! GP_PIN element description
     do Get-GP_PIN-Descr
  end-if


FROM PS_GPES_PAYSLIP_LN LN

WHERE LN.GPES_PAYSLIP_ID = $PaySlip_ID
  AND LN.EFFDT = $Pslip_EFFDT
  AND LN.GPES_PIN_PS1_NUM = &A.PIN_NUM
end-select

end-procedure


!************************************************************************
! Procedure Get-PAYSLIP_LN-Lang
!************************************************************************
begin-procedure Get-PAYSLIP_LN-Lang
#debug do Fin-Debug-Msg('Get-PAYSLIP_LN-Lang')

Let $sql-statement = 'GPESFNQ2.SQR,Get-PAYSLIP_LN-Lang,Select,PS_GPES_PSLPLN_LNG'
BEGIN-SELECT on-error=SQL-Error
PSLPLN.DESCR
  Let $E_D_Descripcion = rtrim(&PSLPLN.DESCR,'')


FROM PS_GPES_PSLPLN_LNG PSLPLN

WHERE PSLPLN.GPES_PAYSLIP_ID = $PaySlip_ID
  AND PSLPLN.EFFDT = $Pslip_EFFDT
  AND PSLPLN.GPES_PIN_PS1_NUM = &A.PIN_NUM
  AND PSLPLN.LANGUAGE_CD = $Curr_Language_Cd
end-select

end-procedure


!************************************************************************
! Procedure Get-DD-Descr
!************************************************************************
begin-procedure Get-DD-Descr
#debug do Fin-Debug-Msg('Get-DD-Descr')

#debugd show 'As Of Date: ' &S.SEG_END_DT
#debugd show 'Pay Group:  ' &JOB.GP_PAYGROUP
#debugd show 'Pin Num:    ' &A.PIN_NUM

Let $E_D_Descripcion = ''
         
Let $sql-statement = 'GPESFNQ2.SQR,Get-DD-Descr,Select,PS_GPES_PAYSLIP_AP'
BEGIN-SELECT on-error=SQL-Error
AP.GPES_ELEM_DESCR
AP.DESCR
  Let $E_D_Descripcion = rtrim(&AP.DESCR,'')

  If &AP.GPES_ELEM_DESCR = 'T'    ! User defined description
     Let $E_D_Descripcion = rtrim(&AP.DESCR,'')
     If $Curr_Language_Cd <> $PSoptions_language_cd
        do Get-PAYSLIP_AP-Lang
     End-if
  Else    ! GP_PIN element description
     do Get-GP_PIN-Descr
  end-if


FROM PS_GPES_PAYSLIP_AP AP

WHERE AP.GPES_PAYSLIP_ID = $PaySlip_ID
  AND AP.EFFDT = $Pslip_EFFDT
  AND AP.GPES_PIN_PS1_NUM = &A.PIN_NUM
end-select

If $E_D_Descripcion = ''
   Let $sql-statement = 'GPESFNQ2.SQR,Get-DD-Descr,Select,PS_GPES_PAYSLIP_AP'
BEGIN-SELECT on-error=SQL-Error
IR.GPES_ELEM_DESCR
IR.DESCR
  Let $E_D_Descripcion = rtrim(&IR.DESCR,'')

  If &IR.GPES_ELEM_DESCR = 'T'    ! User defined description
     Let $E_D_Descripcion = rtrim(&IR.DESCR,'')
     If $Curr_Language_Cd <> $PSoptions_language_cd
        do Get-PAYSLIP_IR-Lang
     End-if
  Else    ! GP_PIN element description
     do Get-GP_PIN-Descr
  end-if

FROM PS_GPES_PAYSLIP_IR IR

WHERE IR.GPES_PAYSLIP_ID = $PaySlip_ID
  AND IR.EFFDT = $Pslip_EFFDT
  AND IR.GPES_PIN_PS1_NUM = &A.PIN_NUM
end-select
End-If

If $E_D_Descripcion = ''
   Let $sql-statement = 'GPESFNQ2.SQR,Get-DD-Descr,Select,PS_GPES_PAYSLIP_LN'
BEGIN-SELECT on-error=SQL-Error
LND.GPES_ELEM_DESCR
LND.DESCR
  Let $E_D_Descripcion = rtrim(&LND.DESCR,'')

  If &LND.GPES_ELEM_DESCR = 'T'    ! User defined description
     Let $E_D_Descripcion = rtrim(&LND.DESCR,'')
     If $Curr_Language_Cd <> $PSoptions_language_cd
        do Get-PAYSLIP_LN-Lang
     End-if
  Else    ! GP_PIN element description
     do Get-GP_PIN-Descr
  end-if

FROM PS_GPES_PAYSLIP_LN LND

WHERE LND.GPES_PAYSLIP_ID = $PaySlip_ID
  AND LND.EFFDT = $Pslip_EFFDT
  AND LND.GPES_PIN_PS1_NUM = &A.PIN_NUM
end-select
End-If

end-procedure


!************************************************************************
! Procedure Get-PAYSLIP_AP-Lang
!************************************************************************
begin-procedure Get-PAYSLIP_AP-Lang
#debug do Fin-Debug-Msg('Get-PAYSLIP_AP-Lang')

Let $sql-statement = 'GPESFNQ2.SQR,Get-PAYSLIP_AP-Lang,Select,PS_GPES_PSLPAP_LNG'
BEGIN-SELECT on-error=SQL-Error
PSLPAP.DESCR
  Let $E_D_Descripcion = rtrim(&PSLPAP.DESCR,'')


FROM PS_GPES_PSLPAP_LNG PSLPAP

WHERE PSLPAP.GPES_PAYSLIP_ID = $PaySlip_ID
  AND PSLPAP.EFFDT = $Pslip_EFFDT
  AND PSLPAP.GPES_PIN_PS1_NUM = &A.PIN_NUM
  AND PSLPAP.LANGUAGE_CD = $Curr_Language_Cd
end-select

end-procedure


!************************************************************************
! Procedure Get-PAYSLIP_IR-Lang
!************************************************************************
begin-procedure Get-PAYSLIP_IR-Lang
#debug do Fin-Debug-Msg('Get-PAYSLIP_IR-Lang')

Let $sql-statement = 'GPESFNQ2.SQR,Get-PAYSLIP_IR-Lang,Select,PS_GPES_PSLPIR_LNG'
BEGIN-SELECT on-error=SQL-Error
PSLPIR.DESCR
  Let $E_D_Descripcion = rtrim(&PSLPIR.DESCR,'')


FROM PS_GPES_PSLPIR_LNG PSLPIR

WHERE PSLPIR.GPES_PAYSLIP_ID = $PaySlip_ID
  AND PSLPIR.EFFDT = $Pslip_EFFDT
  AND PSLPIR.GPES_PIN_PS1_NUM = &A.PIN_NUM
  AND PSLPIR.LANGUAGE_CD = $Curr_Language_Cd
end-select

end-procedure


!************************************************************************
! Procedure Get-GP_PIN-Descr
!************************************************************************
begin-procedure Get-GP_PIN-Descr
#debug do Fin-Debug-Msg('Get-GP_PIN-Descr')

Let $sql-statement = 'GPESFNQ2.SQR,Get-GP_PIN-Descr,Select,PS_GP_PIN'
BEGIN-SELECT on-error=SQL-Error
GPPIN.DESCR
  Let $E_D_Descripcion = rtrim(&GPPIN.DESCR,'')


FROM PS_GP_PIN GPPIN

WHERE GPPIN.PIN_NUM = &A.PIN_NUM
end-select

  If $Curr_Language_Cd <> $PSoptions_language_cd
Let $sql-statement = 'GPESFNQ2.SQR,Get-GP_PIN-Descr,Select,PS_GP_PIN_LANG'
BEGIN-SELECT on-error=SQL-Error
PINLNG.DESCR
  Let $E_D_Descripcion = rtrim(&PINLNG.DESCR,'')


FROM PS_GP_PIN_LANG PINLNG

WHERE PINLNG.PIN_NUM = &A.PIN_NUM
  AND PINLNG.LANGUAGE_CD = $Curr_Language_Cd
end-select
  End-if

end-procedure


!************************************************************************
! Procedure total-neto
!************************************************************************
begin-procedure total-neto
#debug do Fin-Debug-Msg('total-neto')

Let $sql-statement = 'GPESFNQ2.SQR,total-neto,Select,PS_GP_PYGRP'
BEGIN-SELECT DISTINCT on-error=SQL-Error
C.CURRENCY_CD &Currency_Cd
   Let $Currency_Cd  =   rtrim(&Currency_Cd , ' ')


FROM PS_GP_PYGRP B, PS_GP_PYENT_DTL C

WHERE B.GP_PAYGROUP = &JOB.GP_PAYGROUP
  AND C.PAY_ENTITY = B.PAY_ENTITY
  AND C.EFFDT =  (SELECT MAX(C_ED.EFFDT) FROM PS_GP_PYENT_DTL C_ED
                   WHERE C.PAY_ENTITY = C_ED.PAY_ENTITY
                     AND C_ED.EFFDT <= &S.SEG_END_DT)
end-select
  Let #total-neto = #E_Total - #D_Total
  #debugd show 'Neto: ' #total-neto
  move #total-neto to $total-neto  B,999,999.99

end-procedure


!************************************************************************************
 BEGIN-PROCEDURE Get-month-name
!************************************************************************************
#debug do Fin-Debug-Msg('Get-month-name')

    evaluate $Month
    when = '01'
       move 'Enero'    to $MonthName
    when = '02'
       move 'Febrero'  to $MonthName
    when = '03'
       move 'Marzo'    to $MonthName
    when = '04'
       move 'Abril'    to $MonthName
    when = '05'
       move 'Mayo'     to $MonthName
    when = '06'
       move 'Junio'    to $MonthName
    when = '07'
       move 'Julio'    to $MonthName
    when = '08'
       move 'Agosto'    to $MonthName
    when = '09'
       move 'Septiembre'    to $MonthName
    when = '10'
       move 'Octubre'    to $MonthName
    when = '11'
       move 'Noviembre'    to $MonthName
    when = '12'
       move 'Diciembre'    to $MonthName
    end-evaluate

 end-procedure


!************************************************************************
! Procedure Body-Control
!************************************************************************
begin-procedure Body-Control
#debug do Fin-Debug-Msg('Body-Control')

  if #linea > 39
     Print 'Continua en la siguiente pagina ...' (55,15)
   !  Do print-footer
     new-page
     Print 'Continuación ...' (5,15)
     print 'CONCEPTOS' (8,{e_d_descripcion}) BOLD
     print 'IMPORTE '   (,{E_D_TOTAL}) BOLD
     let #linea = 10
  end-if

end-procedure


!************************************************************************
! Procedure Print-Certificate
!************************************************************************
 BEGIN-PROCEDURE CABECERA
#debug do Fin-Debug-Msg('CABECERA')

    Let $total-devengo = #Total-Devengo
    Let $Linea = $Linea || $name || ' certifica que ha trabajado en la empresa '
    Let $Linea = $Linea || $Cmpny-Descr || ' desde el ' ||  $Hire_Out
    Let $Linea = $Linea || ' hasta el ' || $Termination_Out
    Let $Linea = $Linea || ' con la categoría profesional de ' || $Empl-JobCode-Desc
    Let $Linea = $Linea || ', y declara que ha recibido la cantidad abajo indicada'
    Let $Linea = $Linea || ' en concepto de liquidación total por su baja en la empresa '
    Let $Linea = $Linea || 'en el día de hoy.'
    print $Linea (5,15)  wrap 100 7  line-height = 1.5

    Let $linea2 = 'La cantidad referenciada corresponde a los siguientes conceptos: '
    print $linea2 (12,15)

    print 'CONCEPTOS' (14,{e_d_descripcion}) BOLD
    print 'IMPORTE '   (,{E_D_TOTAL}) BOLD

 END-PROCEDURE

!************************************************************************
! Procedure Print-Certificate
!************************************************************************
 BEGIN-PROCEDURE PIE
#debug do Fin-Debug-Msg('PIE')


    PRINT 'Líquido a percibir ' (#Linea,40) bold
    print #Total-Devengo      (#LINEA,{E_D_TOTAL}) edit B,999,999.99 bold
    print $Currency_Cd          (#Linea, +6) bold

    Let $lineaf = 'Quedo así indemnizado y liquidado por todos los conceptos que'
    Let $lineaf = $lineaf || ' pudieran derivarse de la relación'
    Let $lineaf = $lineaf || ' laboral que unía a las partes y que queda extinguida,'
    Let $lineaf = $lineaf || ' manifestando expresamente que nada más tengo que reclamar,'
    Let $lineaf = $lineaf || ' estando de acuerdo con ello la empresa.'

    Let $linea-firma = 'Firmado: ' || $name

    print $lineaf (45,15) wrap 100 3  line-height = 1.5
    print $linea-fecha (49,15)
    print 'El trabajador, ' (50,15)
    print $linea-firma (55,15)

    NEW-PAGE

 end-procedure

!--------------------------------------------------------------------!
! Debug Msg                                                          !
!--------------------------------------------------------------------!
 begin-procedure Fin-Debug-Msg($procedure_name)
    display ' '
    display '----------------------------------'
    display $procedure_name
#debugt date-time () {Native-DateTime} &SysDateTime
#debugt move &SysDateTime to $SysDateTime
#debugt show 'TIMING, ' $procedure_name ', ' $SysDateTime
    display ' '
 end-procedure ! Fin-Debug-Msg

!***************************************************************
!Include SQCs
!***************************************************************
#Include 'getsetid.sqc'
#Include 'readxlat.sqc'
#Include 'sqlerr.sqc'
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#include 'datemath.sqc'  !Routines for date calculation
#include 'rotname1.sqc'
