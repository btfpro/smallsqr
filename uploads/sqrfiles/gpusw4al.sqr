!**********************************************************************!
! gpusw4al: W-4 IRS Report                                       (USA) !
!***********************************************************************
!                                                                      !
!               Confidentiality Information:                           !
!                                                                      !
!                                                                      !
! This module contains confidential and proprietary information        !
! of Oracle; it is not to be copied, reproduced, or transmitted        !
! in any form, by any means, in whole or in part, nor is it to         !
! be used for any purpose other than that for which it is              !
! expressly provided under the applicable license agreement.           !
!                                                                      !
! Copyright (C) 2006 Oracle. All Rights Reserved.                      !
!                                                                      !
!**********************************************************************!
!                                                                      !
!          $Date:  2006/07/21:02:02:39                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      !
!**********************************************************************!
#include 'setenv.sqc'    !Set Database Environment


begin-report

  do Init-DateTime
  do Get-Current-DateTime
  do Init-Report
  display 'Report Began: ' noline
  display $AsOfNow
  display ''

  do Build-Where


  If $GPUS_RPT_TAX_RC.GPUS_ELEC_PRCS_IND = 'Y'
     do Print-GPUSW4AL-REPORT-HEADERS
     Let $W4RptWhere = ' AND (W4.GPUS_FWT_STATUS = ''G'' OR (W4.GPUS_FWT_ALLOW > 10 AND W4.GPUS_FWT_STATUS = ''N'')) AND W4.GPUS_W4_IRS_FLAG <> ''Y'''
  end-if

  do W4IrsReport

  If $GPUS_RPT_TAX_RC.GPUS_ELEC_PRCS_IND = 'Y'
     print 'Total Employees Processed' (+2,1) bold
     print '---->' (,40)
     print #Total_Records (,47) edit 999,999,999
  end-if

  do Get-Current-DateTime
  display ''
  display 'Report Ended: ' noline
  display $AsOfNow
  do Stdapi-Term

end-report


#include 'setup31.sqc'


begin-heading 3

  #include 'stdhdg01.sqc'

end-heading


Begin-Footing 3

    If $Gpus_Update_Cd = '03'
        Print 'This form was signed electronically via ePay' (2,)  Center
        Print '' (+1,)
    End-If

End-Footing


Begin-Procedure Init-Report

  do Stdapi-Init

  do Select-Parameters

  If $GPUS_RPT_TAX_RC.GPUS_ELEC_PRCS_IND = 'Y'
     move 'GPUSW4AL' to $ReportID

     move 'W-4  Transmittal (Form 6466) - Detail Report' to $ReportTitle
     display $ReportTitle
     display ''
  Else
     move 'GPUSW4AL' to $ReportID

     move 'W-4 IRS Report' to $ReportTitle
     display $ReportTitle
     display ''
  end-if

End-Procedure


begin-procedure Build-Where

   Let $W4RptWhere = ' '

  move &GPUS_RPT_TAX_RC.Gpus_Tax_Year to $PeriodBeginDate xxxx
  move &GPUS_RPT_TAX_RC.GPUS_TAX_QTR to #GPUS_RPT_TAX_RC.Gpus_Tax_Qtr
  evaluate #GPUS_RPT_TAX_RC.Gpus_Tax_Qtr
        when = 1
          move '01' to $QtrBeginMonth 09
          break
        when = 2
          move '04' to $QtrBeginMonth 09
          break
        when = 3
          move '07' to $QtrBeginMonth 09
          break
        when = 4
          move '10' to $QtrBeginMonth 09
          break
  end-evaluate
  concat $QtrBeginMonth with $PeriodBeginDate -xx
  concat '-01' with $PeriodBeginDate
  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($PeriodBeginDate, $PeriodBeginDate, {DEFYMD}, '', 'native')
  If $GPUS_RPT_TAX_RC.GPUS_EXEMPT_ALLOW = '03'       ! Report Employees Claiming Exemption
     concat ' AND GPUS_FWT_STATUS = ''G'''     with $W4RptWhere

   else
     If $GPUS_RPT_TAX_RC.GPUS_EXEMPT_ALLOW = '04'     ! Report With more than Ten Allowances
      concat ' AND W4.GPUS_FWT_ALLOW > 10 AND GPUS_FWT_STATUS IN (''G'',''N'')' with $W4RptWhere
     else
       If $GPUS_RPT_TAX_RC.GPUS_EXEMPT_ALLOW = '02'   ! BOTH (EITHER OF E / T.)
        concat ' AND (GPUS_FWT_STATUS = ''G'' OR (W4.GPUS_FWT_ALLOW > 10 AND GPUS_FWT_STATUS = ''N''))' with $W4RptWhere
       else
         If $GPUS_RPT_TAX_RC.GPUS_EXEMPT_ALLOW = '01'   ! All BUT Exempts
          concat ' AND GPUS_FWT_STATUS IN (''G'',''N'')'     with $W4RptWhere
         End-If
       End-If
     End-If
   End-if


   If $GPUS_RPT_TAX_RC.GPUS_W4_UPD_TYPE = '03'           ! Employee Created/Updated Via Web
    concat ' AND GPUS_UPDATE_CD = ''03'''     with $W4RptWhere
   else
     If $GPUS_RPT_TAX_RC.GPUS_W4_UPD_TYPE  = '02'
        concat ' AND W4.EMPLID = '''  with $W4RptWhere
      concat $GPUS_RPT_TAX_RC.EMPLID   with $W4RptWhere
      concat '''' with $W4RptWhere
     End-If
   End-if


end-procedure

begin-procedure Print-GPUSW4AL-REPORT-HEADERS
  Print 'Employee'                  (+3,1) bold
  Print 'Employee'                  (,15) bold
  print 'Federal'                   (,50) bold
  print 'Federal'                   (,66) bold
  print 'Effective'                 (,100) bold
  Print 'Id'                        (+1,1) bold
  Print 'Name'                      (,15) bold
  print 'Allowances'                (,50) bold
  print 'Withhold Status'           (,66) bold
  print 'Date'                      (,100) bold
  print ''                          (+1,1)

end-procedure



begin-procedure W4IrsReport

  display 'Generating report . . . ' noline

begin-SELECT
W4.EMPLID
W4.GPUS_FWT_MAR_ST
W4.GPUS_FWT_ALLOW
W4.GPUS_FWT_ADD_AMT
W4.GPUS_FWT_STATUS
W4.COMPANY              () on-break level=1 print=never after=Print-Company-Totals save=$Last_Company
W4.EFFDT
W4.GPUS_UPDATE_CD
W4.GPUS_W4_NAME_CHECK
SN.NATIONAL_ID

  Let #Total_Records_Per_Company = #Total_Records_Per_Company + 1
  Let #Total_Records = #Total_Records + 1
  move &W4.EMPLID              to $Emplid
  move &W4.EFFDT               to $W4_Effdt
  move &W4.GPUS_UPDATE_CD         to $Gpus_Update_Cd

  let $FieldName    = 'GPUS_FWT_MAR_ST'
  let $FieldValue   = &W4.GPUS_FWT_MAR_ST
  do Read-Translate-Table
  let $Gpus_Fwt_Mar_St = $XLatShortName

  move &W4.GPUS_FWT_ALLOW      to $Gpus_Fwt_Allow
  move &W4.GPUS_FWT_ADD_AMT    to #Gpus_Fwt_Add_Amt

  let $FieldName    = 'GPUS_FWT_STATUS'
  let $FieldValue   = &W4.GPUS_FWT_STATUS
  do Read-Translate-Table
  let $Gpus_Fwt_Status = $XLatShortName

  if &W4.GPUS_FWT_STATUS = 'G'
     let $Special_Fwt_Status_l = 'Maintain gross, do not withhold'
  else
    let $Special_Fwt_Status_L = $XlatLongName
  end-if

  do Get-Emp-Name-Addr

  If $GPUS_RPT_TAX_RC.GPUS_ELEC_PRCS_IND = 'Y'
      do Print-W4IrsReportB
  Else
     do Print-W4IrsReport
  End-If


FROM PS_GPUS_FWT_EE  W4, PS_PERS_NID SN
WHERE W4.EFFDT >= $PeriodBeginDate
  AND W4.EFFDT <= $PeriodEndDate
  AND W4.EMPLID = SN.EMPLID
    AND W4.EFFDT = (SELECT MAX(W41.EFFDT)
                    FROM PS_GPUS_FWT_EE W41
                    WHERE W41.EMPLID = W4.EMPLID
                      AND W41.COMPANY = W4.COMPANY
                    AND W41.EFFDT <= $PeriodEndDate )
  [$W4RptWhere]
ORDER BY W4.COMPANY, SN.NATIONAL_ID

end-SELECT

end-procedure


Begin-Procedure Get-Emp-Name-Addr

Begin-Select
W4E.NAME

     Move &W4E.NAME     to $Name
     Do Get-Empl-Address ($Emplid,'HOME', $AddressFound)

   Let $COUNTRY  = $GETADDR_Country
   let $ADDRESS1 = $GETADDR_Address1
   let $ADDRESS2 = $GETADDR_Address2
   let $ADDRESS3 = $GETADDR_Address3
   let $ADDRESS4 = $GETADDR_Address4

   let $CITY = $GETADDR_CITY
   let $STATE =$GETADDR_STATE
   let $POSTAL = $GETADDR_POSTAL


     do Get-Fed-Ein

     do Get-NID

     move $Postal     to $Zip
     move $Zip            to $ZipExtsn ~~~~~~xxxx

     if instr($Zip,'-',6) = 0 and $ZipExtsn <> '    '
        let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
     end-if

     move $Zip         to $ZipExtsn ~~~~~xxxxx

     Let $StatePostal = ''
     Let $State = rtrim($State,' ')
     concat $State with $StatePostal
     if $State != ''
       if rtrim($Postal,' ') != ''
         concat ', ' with $StatePostal
       end-if
     end-if
     concat $Zip With $StatePostal


FROM PS_PERSON_NAME W4E
WHERE EMPLID = $Emplid
End-Select

End-Procedure


Begin-Procedure Get-Fed-Ein

Begin-Select
W4C.DESCR
W4C.FEDERAL_EIN
W4C.ADDRESS1
W4C.ADDRESS2
W4C.ADDRESS3
W4C.ADDRESS4
W4C.CITY
W4C.STATE
W4C.POSTAL
W4C.COUNTRY

     Move &W4C.Federal_Ein to $Federal_Ein
     Move &W4C.DESCR       to $Employer
     Move &W4C.COUNTRY     to $CountryC
     Move &W4C.ADDRESS1    to $Address1C
     Move &W4C.ADDRESS2    to $Address2C
     Move &W4C.ADDRESS3    to $Address3C
     Move &W4C.ADDRESS4    to $Address4C
     Move &W4C.CITY        to $CityC
     Move &W4C.STATE       to $StateC
     Move &W4C.POSTAL      to $PostalC

     move &W4C.Postal     to $Zip
     move $Zip            to $ZipExtsn ~~~~~~xxxx

     if instr($Zip,'-',6) = 0 and $ZipExtsn <> '    '
        let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
     end-if

     move $Zip         to $ZipExtsn ~~~~~xxxxx

     Let $StatePostalC = ''
     Let $StateC = rtrim($StateC,' ')
     concat $StateC with $StatePostalC
     if $StateC != ''
       if rtrim($PostalC,' ') != ''
         concat ', ' with $StatePostalC
       end-if
     end-if
     concat $Zip With $StatePostalC

     If $GPUS_RPT_TAX_RC.GPUS_ELEC_PRCS_IND = 'Y'
        Let #Length_of_EIN = length($Federal_ein)
        if $Federal_Ein = '' or #Length_of_EIN < 9
           display 'Federal EIN is invalid for Company '
           display &W4.Company
           stop
        end-if
     end-if

FROM
PS_COMPANY_TBL W4C
WHERE COMPANY = &W4.COMPANY
  AND EFFDT = (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL
               WHERE COMPANY = &W4.COMPANY
                 AND EFFDT <= $W4_Effdt )
End-Select
End-Procedure


Begin-Procedure Get-NID
Begin-Select
NATIONAL_ID

     Move &NATIONAL_ID     to $Ssn

FROM PS_PERS_NID
WHERE NATIONAL_ID_TYPE ='PR'
  AND EMPLID = $Emplid
  AND COUNTRY = $Country
End-Select
End-Procedure


begin-procedure Print-W4IrsReport

  print 'Signed On  '     (+2,1)
  do Format-DateTime($W4_Effdt, $out, {DEFMDY},'','')
  print $out                    ()
  print 'Employee Name                      :   '  (+3,1)
  PRINT $Name                (,50)
  Print 'Social Security No                 :   '  (+2,1)
  Print $Ssn                    (,50)       edit xxx-xx-xxxx
  Print 'Address                            :   '  (+2,1)
  Print $Address1               (,50)

  If rtrim($address2,' ') != ''
  Print $Address2               (+1,50)
  end-if

  If rtrim($address3,' ') != ''
  Print $Address3               (+1,50)
  end-if

  If rtrim($address4,' ') != ''
  Print $Address4               (+1,50)
  end-if

  If rtrim($City,' ') != ''
  Print $City                   (+1,50)
  end-if

  If rtrim($StatePostal,' ') != ''
  Print $StatePostal            (+1,50)
  end-if

  Print $Country                (+1,50)

  Print 'Marital Status                     :   '  (+2,1)
  print $Gpus_Fwt_Mar_St          (,50)
  print 'Total Number Of Allowances Claimed :   '  (+2,1)
  print $Gpus_Fwt_Allow         (,50)
  Print 'Additional Amount                  :   '  (+2,1)
  print #Gpus_Fwt_Add_Amt           (,50)
  print 'Exemption Status                   :   '  (+2,1)
  print $Gpus_Fwt_Status             (,50)

  Print 'Employer                           :   '  (+2,1)
  Print $Employer               (,50)
  Print 'Employer id                        :   '  (+1,1)
  Print $Federal_Ein            (,50)
  Print 'Address                            :   '  (+1,1)
  Print $Address1C              (,50)

  If rtrim($address2C,' ') != ''
  Print $Address2C              (+1,50)
  end-if

  If rtrim($address3C,' ') != ''
  Print $Address3C              (+1,50)
  end-if

  If rtrim($address4C,' ') != ''
  Print $Address4C              (+1,50)
  end-if

  If rtrim($CityC,' ') != ''
  Print $CityC                  (+1,50)
  end-if

  If rtrim($StatePostalC,' ') != ''
  Print $StatePostalC           (+1,50)
  end-if

  Print $CountryC                (+1,50)

  Print 'Employee last name differ from SSN : ' (+2,1)
  Print &W4.GPUS_W4_NAME_CHECK (,50)

  New-Page

end-procedure

begin-procedure Print-Company-Totals
If $GPUS_RPT_TAX_RC.GPUS_ELEC_PRCS_IND = 'Y'
   let $Employer_descr = substr($Employer, 1,25)
   print 'Totals for Company' (+2,1) bold
   print $Last_Company (,+1)
   print '-' (,,)
   print $Employer (,,)
   print '(FEIN' (,+1)
   print $Federal_Ein (,+1)
   print ')' (,,)
   print '---->' (,71)
   print #Total_Records_Per_Company  (,78) edit 999,999,999
   print ' ' (+1,1)
   print ' ' (+1,1)
   Let #Total_Records_Per_Company = 0
end-if
end-procedure

begin-procedure Print-W4IrsReportB

  Print $Emplid                 (+1,1)
  Print $Name                   (,15)
  print $Gpus_Fwt_Allow         (,50)
  print $Special_Fwt_Status_l   (,66)
  do Format-DateTime($W4_Effdt, $out, {DEFMDY},'','')
  print $out                   (,100)

end-procedure

#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Update Process API
#include 'askaod.sqc'    !Ask As Of Date
#include 'gpuspctl.sqc'  !Select Run Control Parameters
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'number.sqc'    !Routines to format numbers
#include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'getaddr.sqc'   !Get Current Person Address







