!**********************************************************************!
!  gpusfm2a:  W-2c MMREF-2 Files Audit Report                          !
!**********************************************************************!
!                                                                      !
!               Confidentiality Information:                           !
!                                                                      !
!                                                                      !
! This module contains confidential and proprietary information        !
! of Oracle; it is not to be copied, reproduced, or transmitted        !
! in any form, by any means, in whole or in part, nor is it to         !
! be used for any purpose other than that for which it is              !
! expressly provided under the applicable license agreement.           !
!                                                                      !
! Copyright (C) 2006 Oracle. All Rights Reserved.                      !
!                                                                      !
!**********************************************************************!
!                                                                      !
!          $Date:  2006/07/21:01:45:18                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      !
!**********************************************************************!

#include 'setenv.sqc' !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Select-Reports
  do Stdapi-Term
end-report

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Initialization
end-procedure


begin-heading 10
  #Include 'stdhdg01.sqc'

  print 'RCW1'                           (+2,1)
  print 'Orig SSN'                        (0,19)
  print 'O First Name'                    (0,37)
  print 'O Middle Name'                   (0,54)
  print 'O Last Name'                     (0,71)
  print 'Address 1'                       (0,94)
  print 'Address 2'                       (0,118)
  print 'City'                            (0,142)
  print 'S E'                             (0,166)
  print 'R P'                             (0,170)
  print 'TPS'                             (0,174)

  print 'RCW2'                           (+1,1)
  print 'Corr SSN'                        (0,19)
  print 'C First Name'                    (0,37)
  print 'C Middle Name'                   (0,54)
  print 'C Last Name'                     (0,71)
  print 'St'                              (0,94)
  print 'Zip'                             (0,99)
  print 'Xtnsn'                           (0,107)
  print 'Foreign State'                   (0,114)
  print 'Foreign Zip'                     (0,140)
  print 'CC'                              (0,158)
  print 'S E'                             (0,166)
  print 'R P'                             (0,170)
  print 'TPS'                             (0,174)

  print 'RCW3'                           (+1,1)
  print 'Fed Wages'                       (0,21)
  print 'Fed Tax'                         (0,39)
  print 'SS Wages'                        (0,54)
  print 'SS Tax'                          (0,72)
  print 'Med Wages'                       (0,85)
  print 'Med Tax'                         (0,103)
  print 'SS Tips'                         (0,119)
  print 'Adv EIC'                         (0,135)
  print 'Dep Care'                        (0,150)
  print '401K'                            (0,170)

  print 'RCW4'                           (+1,1)
  print '403B'                            (0,26)
  print '408K6'                           (0,41)
  print '457B'                            (0,58)
  print '501C18D-C'                       (0,69)
  print 'NQ 457 Y'                        (0,86)
  print 'NQ 457 N'                        (0,102)
  print 'Combat Pay'                      (0,116)
  print 'GT ER Cost'                      (0,134)
  print 'NS S Opt'                        (0,147)
  print 'Def NQ Comp'                     (0,163)

  print 'RCO'                            (+1,1)
  print 'Alloc Tips'                      (0,20)
  print 'Uncoll ToT'                      (0,36)
  print 'Med Svgs'                        (0,54)
  print '408P'                            (0,74)
  print 'Adopt Exp'                       (0,85)
  print 'Un SS GTL'                       (0,101)
  print 'Un Med GTL'                      (0,116)
  print '409A Inc NQ'                     (0,134)

  move '  ' to $FullLine
  print $FullLine                        (+1,176)

end-heading

begin-procedure Initialization
  display ''
  display 'W-2c MMREF-2 Files Audit Report '
  display ' '
  move 'W-2c MMREF-2 Files Audit Report' to $ReportTitle
  move 'GPUSFM2A' to $ReportID
  do Get-Current-DateTime

  If $Prcs_Process_Instance = ''
    Display 'Report cannot be run outside of PeopleSoft, please use process scheduler.'
    Stop
  end-if

  do Select-US-Year-end-Parameters
  do Convert-Parameters

  move '' to $ErName
end-procedure

begin-procedure Select-Reports

begin-SELECT

RC960.EMPLR_PIN
RC960.GPUS_XMITTER_ID
    do Get-Transmitter
RC960.CONTACT_NAME
RC960.TELEPHONE_NBR
RC960.EXTENSION
RC960.EMAIL_ADDR_MMREF
RC960.FAX
RC960.GPUS_NOTIFY_METHOD

FROM PS_GPUS_YE_MMREFPA RC960
end-SELECT

  if rtrim(&RC960.CONTACT_NAME, ' ') = ''
    Show 'MMREF Parameters must be set up on SETUP Menu.'
    Show 'Re-execute after establishing MMREF Parameters.'
    Stop
  end-if

!  move &RC960.EMAIL_ADDR_MMREF to $RC960.EMAIL_ADDR
!  uppercase $RC960.EMAIL_ADDR

  let $FileIn = 'W2CREPORT'
        do Open-File

  if #filestat = 0
      do Read-Tape-File
  end-if

end-procedure

begin-procedure Open-File

  #ifdef NT
  #define FILEPATH files\
  #end-if

  #ifdef UNIX
  #define FILEPATH files/
  #end-if

  Let $prcs_path = $prcsoutputdir
    Let $Flag ='Y'
    Let #st = 1
    Find  'log_output'  In  $prcs_path  #st   #Loc
    Let #Len = Length($prcs_path )
    Let $Str1= Substr($prcs_path, 0, #Loc)
    Let $Str2=  $Str1 || '{FILEPATH}'
    Let $FileID = $Str2 || $FileIn
    open $FileID as 1 for-reading record=1024
      status=#filestat
    if #filestat != 0
      display $FileID noline
      display ' not found '
      display ' '
  end-if

end-procedure

begin-procedure Process-RCA-Record
  do Print-RCA-Record
end-procedure

begin-procedure Process-RCE-Record
  if $ErName <> ''
    if $TotalsOnly = 'N'
      new-page
    end-if
  end-if

  next-listing  NEED = 15
  do Print-RCE-Record

  if $TotalsOnly = 'N'
    move $ErName              to $Employer
    new-page
  end-if
end-procedure

begin-procedure Process-RCW-Record

  next-listing  NEED = 6
  do Print-RCW-Record
end-procedure


begin-procedure Process-RCO-Record
  do Print-RCO-Record
end-procedure

begin-procedure Process-Employer-Record
  display 'Employer Record Found'
  extract $Employer from $TaxRecord 40 57
  extract $TaxJurisdiction from $TaxRecord 220 1
end-procedure

begin-procedure Process-RCF-Record
   do Print-RCF-Record
end-procedure


begin-procedure Process-RCT-Record
   do Print-RCT-Record
end-procedure

begin-procedure Process-RCU-Record
  do Print-RCU-Record
end-procedure

begin-procedure Print-File-Totals
  move '' to $ErName
end-procedure

begin-procedure Get-Transmitter
begin-SELECT
X.DESCR

FROM  PS_GPUS_YE_CO_TBL X
WHERE X.EFFDT = (SELECT MAX (Y.EFFDT) FROM PS_GPUS_YE_CO_TBL Y WHERE Y.EFFDT <= $AsOfDate AND Y.GPUS_YE_RPT_CO = &RC960.GPUS_XMITTER_ID)
  AND X.GPUS_YE_RPT_CO = &RC960.GPUS_XMITTER_ID

end-SELECT
  move &X.Descr to $TrName
end-procedure


begin-procedure Convert-Parameters
begin-SELECT
RC.CALENDAR_YEAR
RC.GPUS_TOTALS_ONLY

FROM  PS_GPUS_RC_MMREF RC
WHERE RC.OPRID = $Prcs_OprID
  AND RC.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-SELECT

  move &RC.CALendAR_YEAR           to $Cal_Year
  move &RC.GPUS_TOTALS_ONLY        to $TotalsOnly
end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'gpusrdw2.sqc'  !Read MMREF2-C file
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl2.sqc'  !US Year end Process Scheduler Run Controls
