!***********************************************************************
!  PI703.SQR:INTERFACE PS RECORD TABLE                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:02:27:53                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup31.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-procedure Init-Report
  move 'PI703' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi703_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation

! Read from PI703 program ids
do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PI703', 'REPORT_TITLE', $Pi703_ReportTitle, #dummy_width)
  do Get_Field_Information ('PI703', 'SYSTEMID', $Pi703_SystemID, #dummy_width)
  do Get_Field_Information ('PI703', 'RECORDNAME', $Pi703_RecordName, #dummy_width)
  do Get_Field_Information ('PI703', 'PROCTYPE', $Pi703_ProcType, #dummy_width)
  do Get_Field_Information ('PI703', 'FIELDNAME', $Pi703_FieldName, #dummy_width)
  do Get_Field_Information ('PI703', 'KEY_FIELD_IND', $Pi703_KeyFieldInd, #dummy_width)
  do Get_Field_Information ('PI703', 'EFFDT_IND', $Pi703_EffdtInd, #dummy_width)
  do Get_Field_Information ('PI703', 'EFFSEQ_IND', $Pi703_EffseqInd, #dummy_width)
  do Get_Field_Information ('PI703', 'ORDER_BY_SEQ', $Pi703_OrderBySeq, #dummy_width)
  do Get_Field_Information ('PI703', 'COLLATE_ORDER', $Pi703_CollateOrder, #dummy_width)

end-procedure


begin-heading 7
!#Include 'stdhdg01.sqc'
#Include 'stdhdgtr.sqc'
  print $Pi703_SystemID      (+2,1,8)
  print $Pi703_RecordName    (,10,20)
  print $Pi703_ProcType      (,31,14)
  print $Pi703_FieldName     (,47,21)
  print $Pi703_KeyFieldInd   (,70,10)
  print $Pi703_EffdtInd      (,82,10)
  print $Pi703_EffseqInd     (,94,10)
  print $Pi703_OrderBySeq    (,107,10)
  print $Pi703_CollateOrder  (,119,8)

end-HEADING

begin-procedure Process-Main

begin-SELECT

A.PI_SYSTEM_ID               (+1,1,4)   on-break level=1 skiplines=3
A.RECNAME                    (,10,20)   on-break level=2
A.PI_PROCESS_TYPE            (,35,11)
B.FIELDNAME                  (,47,21)
B.KEY_FIELD_IND

  if &B.KEY_FIELD_IND = 'Y'
     print &B.KEY_FIELD_IND       (,75,6)
  end-if

B.EFFDT_IND

  if &B.EFFDT_IND = 'Y'
     print &B.EFFDT_IND           (,87,6)
  end-if
B.EFFSEQ_IND

  if &B.EFFSEQ_IND = 'Y'
     print &B.EFFSEQ_IND          (,99,6)
  end-if

B.ORDER_BY_SEQ

  if &B.ORDER_BY_SEQ > 0
     print &B.ORDER_BY_SEQ        (,110,6)
  end-if

B.COLLATE_ORDER              (,122,3)

FROM  PS_PI_PS_RECORD A, PS_PI_PSREC_FLD B
WHERE A.PI_SYSTEM_ID = B.PI_SYSTEM_ID AND A.RECNAME = B.RECNAME AND A.PI_PROCESS_TYPE = B.PI_PROCESS_TYPE
ORDER BY A.PI_SYSTEM_ID, A.RECNAME, A.PI_PROCESS_TYPE, B.FIELDNAME

end-SELECT

end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

