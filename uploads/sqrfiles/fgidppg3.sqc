!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:12:05:10                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
BEGIN-PROCEDURE PRINT-IDP-DATA-PAGE-3

GRAPHIC () FONT 4 10

LET #MAX_ROWS      = 20
LET #CURRENT_ROW   = 1
PRINT ' ' (39,2)

! GET COMPETENCY DATA

BEGIN-SELECT

COMPETENCY.COMPETENCY
COMPETENCY.YR_ACQUIRED
COMPETENCY.YR_LAST_USED
COMPETENCY.PROFICIENCY

        IF #CURRENT_ROW <= #MAX_ROWS
           PRINT &COMPETENCY.COMPETENCY (+4,3)
           MOVE &COMPETENCY.COMPETENCY TO $REQD_COMP
           DO GET-COMP-DESCR
           PRINT $REQD_COMP_DESCR (,23)
           PRINT &COMPETENCY.YR_ACQUIRED (,71)
           PRINT &COMPETENCY.YR_LAST_USED (,88)
           DO GET-PROFICIENCY
           DO GET-COMP-TYPE
        END-IF
        ADD 1 TO #CURRENT_ROW

        FROM  PS_COMPETENCIES COMPETENCY
        WHERE EMPLID = $EMPLID

END-SELECT

! GET TRAINING DATA

LET #MAX_ROWS      = 15
LET #CURRENT_ROW   = 1
PRINT ' ' (139,2)

BEGIN-SELECT

TRAINING.COURSE
TRAINING.COURSE_TITLE
TRAINING.ATTENDANCE
!TRAINING.PROFICIENCY
TRAINING.GVT_TC_TYPE
TRAINING.GVT_TC_PURPOSE

        IF #CURRENT_ROW <= #MAX_ROWS
           PRINT &TRAINING.COURSE (+4,3)
           PRINT &TRAINING.COURSE_TITLE (,23)
           MOVE 'ATTENDANCE' TO $FIELDNAME
           MOVE &TRAINING.ATTENDANCE TO $FIELDVALUE
           DO READ-TRANSLATE-TABLE
           PRINT $XLATLONGNAME (,75)
           MOVE &TRAINING.COURSE TO $REQD_COURSE
           DO GET-DURATION
           PRINT $REQD_DUR_TIME (,110)
           PRINT $REQD_DUR_INT  (,115)
           MOVE 'GVT_TC_TYPE' TO $FIELDNAME
           MOVE &TRAINING.GVT_TC_TYPE TO $FIELDVALUE
           DO READ-TRANSLATE-TABLE
           PRINT $XLATLONGNAME (,128)
           MOVE 'GVT_TC_PURPOSE' TO $FIELDNAME
           MOVE &TRAINING.GVT_TC_PURPOSE TO $FIELDVALUE
           DO READ-TRANSLATE-TABLE
           PRINT $XLATLONGNAME (,183)
        END-IF
        ADD 1 TO #CURRENT_ROW

        FROM  PS_TRAINING TRAINING
        WHERE EMPLID = $EMPLID

END-SELECT

END-PROCEDURE

BEGIN-PROCEDURE GET-COMP-DESCR

BEGIN-SELECT

COMP_A.DESCR
     MOVE &COMP_A.DESCR TO $REQD_COMP_DESCR

     FROM  PS_COMPETENCY_TBL COMP_A
     WHERE COMP_A.COMPETENCY =  $REQD_COMP

END-SELECT

END-PROCEDURE

BEGIN-PROCEDURE RATNG_SCAL
BEGIN-SELECT
EMP_RVW.RATING_SCALE
EMP_RVW.EFFDT
      DO GET_RATNG_MDL
FROM  PS_EMPLOYEE_REVIEW EMP_RVW
      WHERE EMP_RVW.EMPLID    = $EMPLID
      AND   EMP_RVW.EMPL_RCD = 0
ORDER BY EMP_RVW.EFFDT DESC

END-SELECT

END-PROCEDURE

BEGIN-PROCEDURE GET_RATNG_MDL
BEGIN-SELECT
SCL_TBL.RATING_MODEL
FROM PS_REVW_SCALE_TBL SCL_TBL
WHERE SCL_TBL.RATING_SCALE = &EMP_RVW.RATING_SCALE AND
      SCL_TBL.EFFDT = (SELECT MAX(SCL_TBL1.EFFDT) FROM PS_REVW_SCALE_TBL SCL_TBL1
                           WHERE SCL_TBL1.RATING_SCALE = SCL_TBL.RATING_SCALE AND
                           SCL_TBL1.EFFDT <= $IDPDATE)
END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE GET-PROFICIENCY
DO RATNG_SCAL

BEGIN-SELECT
COMP_RATING_A.DESCR (,97)
       FROM  PS_REVW_RATING_TBL COMP_RATING_A
       WHERE COMP_RATING_A.REVIEW_RATING = &COMPETENCY.PROFICIENCY
       AND   COMP_RATING_A.RATING_MODEL =  &SCL_TBL.RATING_MODEL
       AND   COMP_RATING_A.EFFDT = (SELECT MAX(COMP_RATING_A1.EFFDT)
       FROM PS_REVW_RATING_TBL COMP_RATING_A1
             WHERE COMP_RATING_A1.RATING_MODEL = COMP_RATING_A.RATING_MODEL AND
                   COMP_RATING_A1.REVIEW_RATING = COMP_RATING_A.REVIEW_RATING AND
                   COMP_RATING_A1.EFFDT <= $IDPDATE)

END-SELECT

END-PROCEDURE

BEGIN-PROCEDURE GET-COMP-TYPE
let #Cnt1 = 0
BEGIN-SELECT  LOOPS = 2

CM_TYPE_TBL.DESCR
    if #Cnt1 = 0
      print &CM_TYPE_TBL.DESCR (,140)
    else
      print ';'               (,194)
      print &CM_TYPE_TBL.DESCR (,)
    end-if
      let #Cnt1 = #Cnt1 + 1
      FROM  PS_COMPETENCY_TYPS COMPETENCY_TYPS,PS_CM_TYPE_TBL CM_TYPE_TBL
      WHERE COMPETENCY_TYPS.COMPETENCY = &COMPETENCY.COMPETENCY
      AND   CM_TYPE_TBL.CM_TYPE = COMPETENCY_TYPS.CM_TYPE

END-SELECT

END-PROCEDURE

BEGIN-PROCEDURE GET-DURATION

BEGIN-SELECT

COURSE_TBL.DURATION_TIME
           MOVE &COURSE_TBL.DURATION_TIME TO $REQD_DUR_TIME
COURSE_TBL.COST_UNIT_CD
           !MOVE 'DURATION_INTERVAL' TO $FIELDNAME
           !MOVE &COURSE_TBL.DURATION_INTERVAL TO $FIELDVALUE
           !DO READ-TRANSLATE-TABLE
           !MOVE $XLATLONGNAME TO $REQD_DUR_INT
              MOVE &COURSE_TBL.COST_UNIT_CD TO $REQD_DUR_INT
           FROM  PS_COURSE_TBL COURSE_TBL
           WHERE COURSE_TBL.COURSE = $REQD_COURSE

END-SELECT

END-PROCEDURE
