!***********************************************************************
!  BNPriJob: Find Primary Job.                                         *
!                                                                      *
!  Description: This SQC contains the Get-Primary-Job function.        *
!               Get-Primary-Job() returns the Empl_Rcd of the Primary  *
!               Job for benefits, as of the indicated Effdt.           *
!                                                                      *
!    Call:                                                             *
!  Get-Primary-Job($Emplid,#Benefit_Rcd_Nbr,$Effdt,#Empl_Rcd,$Found)   *
!                                                                      *
!              Following fields are populated by calling program:      *
!                    $Emplid                                           *
!                    #Benefit_Rcd_Nbr                                  *
!                    $Effdt (in native database format)                *
!                                                                      *
!              Following fields are returned by this function:         *
!                    #Empl_Rcd of the Primary Job                      *
!                    $Found (Y/N indicating whether Primary Job was    *
!                            found.                                    *
!                                                                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:08:55:45                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#ifndef BNPRIJOB ! Guards against this SQC being included multiple times
#define BNPRIJOB  

!****************
! Get-Primary-Job
!****************

Begin-Procedure Get-Primary-Job($GPJ_Emplid, #GPJ_BRN, $GPJ_Effdt, :#GPJ_ERN, :$GPJ_Found)

 If $GPJ_Emplid  <>  $Last_Emplid Or
    #GPJ_BRN     <>  #Last_BRN    Or
    $GPJ_Effdt   <>  $Last_Effdt

    do GPJ-Select-PJ($GPJ_Emplid, #GPJ_BRN, $GPJ_Effdt, #GPJ_ERN, $GPJ_Found)
    Move #GPJ_ERN    to  #Last_ERN
    Move $GPJ_Found  to  $Last_Found
    Move $GPJ_Emplid to  $Last_Emplid
    Move #GPJ_BRN    to  #Last_BRN
    Move $GPJ_Effdt  to  $Last_Effdt
 Else
    Move #Last_ERN   to  #GPJ_ERN
    Move $Last_Found to  $GPJ_Found
 End-If

End-Procedure



!*************************************
Begin-Procedure GPJ-Select-PJ($GPJ_Emplid, #GPJ_BRN, $GPJ_Effdt, :#GPJ_ERN, :$GPJ_Found)
!*************************************

 Let $GPJ_Found  = 'N'
 Let #GPJ_ERN    =  0

Begin-Select
GPJ.EMPL_RCD

                          Move 'Y'            to  $GPJ_Found
                          Move &GPJ.EMPL_RCD  to  #GPJ_ERN

 FROM PS_PRIMARY_JOBS GPJ,
      PS_EMPLOYMENT GEM
WHERE GPJ.EMPLID          = GEM.EMPLID
  AND GPJ.EMPL_RCD        = GEM.EMPL_RCD
  AND GEM.EMPLID          = $GPJ_Emplid
  AND GEM.BENEFIT_RCD_NBR = #GPJ_BRN
  AND GPJ.PRIMARY_JOB_IND = 'Y'
  AND GPJ.PRIMARY_JOB_APP = 'BN'
  AND GPJ.EFFDT = (SELECT MAX(GPJ1.EFFDT)
                     FROM PS_PRIMARY_JOBS GPJ1
                    WHERE GPJ1.EMPLID          = GPJ.EMPLID
                      AND GPJ1.EMPL_RCD        = GPJ.EMPL_RCD
                      AND GPJ1.PRIMARY_JOB_APP = 'BN'
                      AND GPJ1.EFFDT          <= $GPJ_Effdt)
End-Select


End-Procedure


#Endif           ! Guards against SQC being included multiple times



