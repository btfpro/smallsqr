!***********************************************************************
!  TAX720: Print contents of Tax Treaty Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:33:00                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
! This program is specific to HRMS for Public Sector.                  *
!***********************************************************************

#include 'setenv.sqc'   ! set environment
#include 'setup32.sqc'  ! printer and page-size initialization

!***********************************************************************
begin-report
  do Init-Report
  do Get-Treaty-Data
  do Reset
  do Stdapi-Term
end-report

!***********************************************************************
begin-procedure Init-Report
  move 'TAX720' to $ReportID
  move 'Tax Treaty Table' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
end-procedure


!***********************************************************************
begin-heading 7
  #include 'stdhdg01.sqc'

  print 'Country/'                  (+2,  1)
  print 'Effective'                 ( 0, 13)
  print 'Short'                     ( 0, 58)
  print ' # Allow.'                 ( 0, 70)
  print '  Months'                  ( 0, 81)
  print 'Max Earnings'              ( 0,123)
  print 'Tax Rate'                  ( 0,140)
  print 'Tax Rate'                  ( 0,156)

  print 'Treaty ID'                 (+1,  1)
  print 'Date'                      ( 0, 13)
  print 'Description'               ( 0, 26)
  print 'Descr.'                    ( 0, 58)
  print 'Permitted'                 ( 0, 70)
  print 'Eligible'                  ( 0, 81)
  print 'Income Code (for 1042-S)'  ( 0, 91)
  print '    Eligible'              ( 0,123)
  print '(after form)'              ( 0,138)
  print '(before form)'             ( 0,153)

  print '-'  (+1,  1,10) fill
  print '-'  ( 0, 13,11) fill
  print '-'  ( 0, 26,30) fill
  print '-'  ( 0, 58,10) fill
  print '-'  ( 0, 70, 9) fill
  print '-'  ( 0, 81, 8) fill
  print '-'  ( 0, 91,30) fill
  print '-'  ( 0,123,12) fill
  print '-'  ( 0,138,12) fill
  print '-'  ( 0,153,13) fill

end-heading


!***********************************************************************
! Procedure:  Get-Treaty-Data
! Description:
!   This procedure selects data from the TREATY_TBL, in country/treaty
!   order.  For each row found, another routine is called to report
!   on the treaty earnings allowances (if any).
!***********************************************************************
begin-procedure Get-Treaty-Data

let $Last_Country = ''

begin-SELECT
NRA.COUNTRY
NRA.TREATY_ID
NRA.EFFDT
NRA.EFF_STATUS
NRA.DESCR
NRA.DESCRSHORT
NRA.NO_ALLOWANCES
NRA.MONTHS_ELIGIBLE

  if &NRA.COUNTRY <> $Last_Country
    do Country-Break
    move &NRA.COUNTRY to $Last_Country
  end-if

  do Format-DateTime(&NRA.EFFDT, $EffDt, {DEFDATE}, '', '')

  if &NRA.EFF_STATUS = 'I'
    let $Status = 'I'
  else
    let $Status = ' '
  end-if

  print &NRA.TREATY_ID          (+2,  1)
  print $EffDt                   (0, 13)
  print $Status                  (0, 23)
  print &NRA.DESCR               (0, 26)
  print &NRA.DESCRSHORT          (0, 58)
  print &NRA.NO_ALLOWANCES       (0, 76) edit 999
  print &NRA.MONTHS_ELIGIBLE     (0, 86) edit 999

  do Get-Treaty-Earnings

FROM PS_TREATY_NRA_TBL NRA

WHERE NRA.EFFDT = (SELECT MAX(NRA2.EFFDT)
                 FROM PS_TREATY_NRA_TBL NRA2
                WHERE NRA2.COUNTRY   = NRA.COUNTRY
                  AND NRA2.TREATY_ID = NRA.TREATY_ID
                  AND NRA2.EFFDT    <= $AsOfToday)
ORDER BY NRA.COUNTRY,
         NRA.TREATY_ID

end-SELECT

end-procedure


!***********************************************************************
! Procedure:  Get-Treaty-Earnings
! Description:
!   This procedure looks for any matching rows from the TREATY_EARN_TBL
!   and prints them, in Income Code order.  If there are no treaty
!   earnings records, a message will be printed instead.
!***********************************************************************
begin-procedure Get-Treaty-Earnings

let #Line = 0
let $Last_Income_Cd = ''

begin-SELECT
ERN.INCOME_CD_1042
ERN.MAX_EARNS_ELIG
ERN.TAX_RT
ERN.TAX_RT_NOFORM

  ! this will cause the income code description to only print for the first earnings line
  ! for each different income code
  if &ERN.INCOME_CD_1042 <> $Last_Income_Cd
    move &ERN.INCOME_CD_1042 to $Last_Income_Cd

    move 'INCOME_CD_1042'    to $FieldName
    move &ERN.INCOME_CD_1042 to $FieldValue
    do Read-Translate-Table
    let $Income_Cd_Name = $XlatLongName
  else
    let $Income_Cd_Name = ''
  end-if

  print $Income_Cd_Name       (+#Line, 91)
  print &ERN.MAX_EARNS_ELIG        (0,123) edit 9,999,999.99
  print &ERN.TAX_RT                (0,140) edit 0.999999
  print &ERN.TAX_RT_NOFORM         (0,156) edit 0.999999

  move 1 to #Line

FROM PS_TREATY_EARN_TBL ERN
WHERE ERN.COUNTRY   = &NRA.COUNTRY
  AND ERN.TREATY_ID = &NRA.TREATY_ID
  AND ERN.EFFDT     = &NRA.EFFDT
ORDER BY ERN.INCOME_CD_1042,
         ERN.MAX_EARNS_ELIG

end-SELECT

if #line = 0
  print 'There are no earnings records for this treaty' (0, 91)
end-if

end-procedure


!***********************************************************************
! Procedure:  Country-Break
! Description:
!***********************************************************************
begin-procedure Country-Break

let #Count = 0

begin-SELECT
COU.DESCR
  add 1 to #Count
FROM PS_COUNTRY_TBL COU
WHERE COU.COUNTRY = &NRA.COUNTRY
end-SELECT

if #Count > 0
  print &COU.DESCR   (+3,1)
else
  print &NRA.COUNTRY (+3,1)
end-if
print '-'            (+1,1,30) fill

end-procedure


!***********************************************************************
#Include 'readxlat.sqc'  ! Read-Translate-Table procedure
#Include 'reset.sqc'     ! Reset printer procedure
#Include 'curdttim.sqc'  ! Get-Current-DateTime procedure
#Include 'datetime.sqc'  ! Routines for date and time formatting
#Include 'number.sqc'    ! Routines to format numbers
#Include 'stdapi.sqc'    ! Update Process API
