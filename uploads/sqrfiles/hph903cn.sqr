!***********************************************************************
!  HPH903CN:   Extract CATS - FULL-TIME DATA                           *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:13:20:32                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
! Extract FT Canadian Academic Teaching Survey data for submission to  *
! Statistics Canada.                                                   *
!                                                                      *
! Input: PS_HP_FT_SURVEY                                               *
!                                                                      *
! Output :  <user specify>                                             *
!                                                                      *
!***********************************************************************
!***********************************************************************
! This program is specific to HRMS for Education & Government          *
!***********************************************************************


#include 'setenv.sqc'     !Set environment
#include 'setup31.sqc'    !Printer and page-size initialization


begin-REPORT

  do INITIALIZATION
  do OPEN-FILE

  display 'Report began at: ' NoLine
  display &SysDateTime
  display ''

  do PROCESS-MAIN

  close 1

  do UPDATE-RPTNG-PERIOD-TABLE
  do DELETE-RUN-CONTROL
  do RESET
  do STDAPI-TERM

  do GET-CURRENT-DATETIME
  display 'Report ended at: ' NoLine
  display &SysDateTime
  display ''

end-REPORT


!***********************************************************************
! INITIALIZATION
! Initializes variables.
!***********************************************************************
begin-procedure INITIALIZATION

  move 'HPH903CN' to $ReportID
  move 'Extract Canadian Academic Teaching Survey - Full-Time Data' to $ReportTitle

  display $ReportID     NoLine
  display ':'           NoLine
  display $ReportTitle
  display ' '

  do INIT-DATETIME
  do GET-CURRENT-DATETIME
  do STDAPI-INIT

  do SELECT-PARAMETERS               ! Get E&G parameters
  do SELECT-REPORT-PARAMETERS        ! Get StatsCan parameters

  move '          ' to $Blank
  move 0 to #NoofFTLine

end-procedure INITIALIZATION


!***********************************************************************
! MAINLINE                                                             *
! This is the main procedure                                           *
!***********************************************************************
begin-procedure PROCESS-MAIN

  display 'Started Extracting FT Canadian Academic Teaching Survey Line Data .... '

  do EXTRACT-FT-SURVEY-LINE-DATA

  display 'Finished Extracting FT Canadian Academic Teaching Survey Line Data.'
  display ' '

  do Format-Number(#NoofFTLine, $out, '99,999,999')

  display 'Number of FT Survey line records: ' noline
  display $out
  display ' '

end-procedure PROCESS-MAIN


!***********************************************************************
! PROCESS SURVEY DATA
! Extracts the data from the survey table.
!***********************************************************************
begin-procedure EXTRACT-FT-SURVEY-LINE-DATA

  let $err-statement1 = 'HPH903CN, Select Error - EXTRACT-FT-SURVEY-LINE-DATA'
  let $err-statement2 = 'Bind Variable: #CURRRPT_YR: '||edit(#CurrRpt_Yr,'0000')

begin-SELECT On-Error=Error-Display
FTS.EMPLID,
FTS.HP_STATS_CAN_YR,
FTS.HP_STATS_INSTN_CD,
FTS.HP_STATS_DEPT_CD,
FTS.HP_STATS_SUBJ_CD,
FTS.SEX,
FTS.HP_YEAR_OF_BIRTH,
FTS.HP_YEAR_OF_APPTMNT,
FTS.HP_PREV_EMPLYMNT,
FTS.HP_PREV_EMPLT_LOC,
FTS.HP_FIRST_DEGREE_YR,
FTS.HP_FIRST_DEG_PROV,
FTS.HP_STATS_DEG_LVL,
FTS.HP_HIGHEST_DEG_YR,
FTS.HP_HIGH_DEG_PROV,
FTS.HP_STATS_LOC_CD,
FTS.HP_IMMIGRTN_STATUS,
FTS.HP_STATS_RANK_CD,
FTS.HP_STATS_RESP_CD,
FTS.HP_APPT_TO_RANK_YR,
FTS.HP_MED_DENT_CAT,
FTS.HP_FT_APPTMNT_TYPE,
FTS.HP_SALARY_STATUS,
FTS.HP_ACTUAL_SALARY,
FTS.HP_ANNUAL_SALARY,
FTS.HP_ADMIN_STIPENDS,
FTS.HP_MM_NOT_ON_LEAVE,
FTS.HP_MM_ON_SABBATICL,
FTS.HP_MM_ON_OTHER_PD,
FTS.HP_MM_ON_UNPAID_LV,
FTS.HP_REDUCED_LOAD,
FTS.HP_ONT_SALARY_SRCE,
FTS.HP_ONT_FINAL_SALRY,
PER.NATIONAL_ID

   let $Emplid = substr(&FTS.EMPLID,1,9)
   let $DegLvl = substr(&FTS.HP_STATS_DEG_LVL,2,1)
   let $Rank = &FTS.HP_STATS_RANK_CD
   let $Resp = &FTS.HP_STATS_RESP_CD
   let $RankResp = $Rank|| $Resp

   If &FTS.SEX = 'M'
      let $Gender = '1'
   Else
      let $Gender = '2'
   End-If

   do Format-Number(&FTS.HP_ACTUAL_SALARY, $Actual_Salary, '099999')
   do Format-Number(&FTS.HP_ANNUAL_SALARY, $Annual_Salary, '099999')
   do Format-Number(&FTS.HP_ADMIN_STIPENDS, $Stipends_Salary, '09999')

   do Format-Number(&FTS.HP_MM_NOT_ON_LEAVE, $Mm_Not_On_Leave, '09')
   do Format-Number(&FTS.HP_MM_ON_SABBATICL, $Mm_Sabbaticl, '09')
   do Format-Number(&FTS.HP_MM_ON_OTHER_PD, $Mm_Other_Paid, '09')
   do Format-Number(&FTS.HP_MM_ON_UNPAID_LV, $Mm_Unpd_Lve, '09')
   do Format-Number(&FTS.HP_REDUCED_LOAD, $Reduced_Load, '09')
   do Format-Number(&FTS.HP_ONT_FINAL_SALRY, $Final_Salary, '099999')

   do WRITE-FTS-TRANSACTIONS

FROM PS_HP_FT_SURVEY FTS, PS_PERS_NID PER
WHERE FTS.HP_STATS_CAN_YR = #CurrRpt_Yr
  AND PER.EMPLID = FTS.EMPLID
  AND PER.COUNTRY = 'CAN'
  AND PER.NATIONAL_ID_TYPE = 'PR'
end-SELECT

end-procedure EXTRACT-FT-SURVEY-LINE-DATA


!***********************************************************************
! WRITE FTS TRANSACITONS
! Writes the data to the output file.
!***********************************************************************
begin-procedure WRITE-FTS-TRANSACTIONS

  Write 1 from
         &FTS.HP_STATS_INSTN_CD:6
         &PER.NATIONAL_ID:9
         &FTS.HP_STATS_DEPT_CD:3
         &FTS.HP_STATS_SUBJ_CD:5
         $Gender:1
         &FTS.HP_YEAR_OF_BIRTH:4
         &FTS.HP_YEAR_OF_APPTMNT:4
         &FTS.HP_PREV_EMPLYMNT:2
         &FTS.HP_PREV_EMPLT_LOC:5
         &FTS.HP_FIRST_DEGREE_YR:4
         &FTS.HP_FIRST_DEG_PROV:5
         $DegLvl:1
         &FTS.HP_HIGHEST_DEG_YR:4
         &FTS.HP_HIGH_DEG_PROV:5
         &FTS.HP_STATS_LOC_CD:5
         &FTS.HP_IMMIGRTN_STATUS:1
         $RankResp:2
         &FTS.HP_APPT_TO_RANK_YR:4
         &FTS.HP_MED_DENT_CAT:1
         &FTS.HP_FT_APPTMNT_TYPE:1
         &FTS.HP_SALARY_STATUS:1
         $Actual_Salary:6
         $Annual_Salary:6
         $Stipends_Salary:5
         $Blanks:2
         $Mm_Not_On_Leave:2
         $Mm_Sabbaticl:2
         $Mm_Other_Paid:2
         $Mm_Unpd_Lve:2
         $Reduced_Load:2
         &FTS.HP_ONT_SALARY_SRCE:2
         $Final_Salary:6
         $Blanks:10

  Add 1 to #NoofFTLine

  do UPDATE-FT-SURVEY-DATA

end-procedure WRITE-FTS-TRANSACTIONS

!***********************************************************************
! UPDATE SURVEY DATA with a last reported date
! Updates the survey data with a last reported date for those records used to
! create the flat file.
!***********************************************************************
begin-procedure UPDATE-FT-SURVEY-DATA


  let $err-statement1 = 'HPH903CN, Select Error - UPDATE-FT-SURVEY-DATA'
  let $err-statement2 = 'Bind Variable: #CURRPT_YR: '||edit(#CurrRpt_Yr,'0000') || ' $Emplid: '
                         || $Emplid

begin-SQL On-Error=Error-Display
 UPDATE PS_HP_FT_SURVEY
  SET HP_LAST_DT_RPRTED = $AsOfToday
 WHERE HP_STATS_CAN_YR = #CurrRpt_Yr
   AND EMPLID = $Emplid
end-SQL

end-procedure UPDATE-FT-SURVEY-DATA


!***********************************************************************
! GET PARAMETER VALUES
! Gets the run control parameters.
!***********************************************************************
begin-procedure GET-PARAMETER-VALUES

  do CONVERT-TO-DTU-DATE($FT_Rpt_Start_Dt,$FT_Start_Dtu_Dt)
  do CONVERT-TO-DTU-DATE($FT_Rpt_End_Dt,$FT_End_Dtu_Dt)

  do DTU-PARSE-DATE($FT_Start_Dtu_Dt,#dtu_yr, #dtu_mo, #dtu_da)
  let #CurrRpt_Yr = #dtu_yr
  display #CurrRpt_Yr

  let $Year_Head = 'Reporting Year ' || edit(#dtu_yr,'0000')
  display $Year_Head

  let $Date_Head = 'From ' || $FT_Start_Dtu_Dt || ' To ' || $FT_End_Dtu_Dt
  display $Date_Head

end-procedure GET-PARAMETER-VALUES


!***********************************************************************
!GET VALUES
!***********************************************************************
begin-procedure GET-VALUES
end-procedure GET-VALUES


!***********************************************************************
!OPEN FILE
!Opens the output file.
!***********************************************************************
begin-procedure OPEN-FILE

  !************************************************************
  !  CODE INSERTED TO ACCEPT INTERNAL EXPORT FILE NAME
  !  {IMPORTPREFIX} is defined in setenv.sqc
  !************************************************************

  let $Runctl_FT_FileName = ltrim(rtrim($Runctl_FT_FileName, ' '), ' ')
  let $OpenName = '{IMPORTPREFIX}' || $Runctl_FT_FileName || '.DAT'

  open $OpenName as 1 for-writing record=120:fixed Status=#filestat

  display 'Output File : ' noline
  display $OpenName
  display ' '
  display 'File Status : ' noline
  display #filestat

end-procedure OPEN-FILE


!***********************************************************************
! DELETE RUN CONTROL
! Deletes run control information from run control table.
!***********************************************************************
begin-procedure DELETE-RUN-CONTROL

  let $err-statement1 = 'HPH903CN, Delete Error - DELETE-RUN-CONTROL'
  let $err-statement2 = 'Bind Variables: $PRCS_OPRID: '||$Prcs_OprID ||
                        ', $PRCS_RUN_CNTL_ID: '||$Prcs_Run_Cntl_ID

begin-SQL On-Error=Error-Display
DELETE FROM PS_RUN_CNTL_EG
WHERE OPRID       = $Prcs_OprID
  AND RUN_CNTL_ID = $Prcs_Run_Cntl_ID

end-SQL
end-procedure DELETE-RUN-CONTROL


!***********************************************************************
! SQCs                                                                 *
!***********************************************************************
#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Date math routine
#Include 'number.sqc'    !Routines to format numbers
#Include 'stderror.sqc'  !Routine for error display.
#Include 'hprnctl1.sqc'  !Routine for EG parameters
#Include 'egrnctl1.sqc'  !Get E&G run control parameters
