!***********************************************
!  CMP014:  Salary History by Group            *
!***********************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:11:35:41                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!Procedures ending with "Scrty" and Record alias ending with "SC" are  *
!using SQR Security.                                                   *
!***********************************************************************
#include 'setenv.sqc'     !Set environment
begin-setup
  #Include 'ptset02.sqc'   !Printer and page-size initialization

#define col1 1
#define col2 14
#define col3 45
#define col4 57
#define col5 64
#define col5_1 70
#define col6 86
#define col6_1 85
#define col7 93
#define col7_2 94
#define col8 99
#define col8_1 102
#define col9 107
#define col9_1 115
#define col10 127
#define col10_1 130
#define col11 148
#define col12 157
#define col13 163
#define col14 169

end-setup


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  if $prcs_oprid=''
    goto last2
  end-if
  
  do Process-Main
 
  do Reset
  do Stdapi-Term
LAST2:
end-report

begin-heading 8
do Get_Field_Information ('CMP014',     'REPORT_TITLE',  $REPORTTITLE,    #DW)

  #Include 'stdhtr08.sqc'

do Get_Field_Information ('CMP014',     'GB_GROUP_ID',   $GROUP_LBL,  #DW)
do Get_Field_Information ('CMP014',     'EMPLID',    $EMPL_LBL,   #DW)
do Get_Field_Information ('CMP014',     'NAME',    $NAME_LBL,        #DW)
do Get_Field_Information ('HR',      'EFFDT1',    $EFFDT1_LBL,        #DW)
do Get_Field_Information ('HR',      'EFFDT2',    $EFFDT2_LBL,        #DW)
do Get_Field_Information ('HR',         'JOBCODE1',    $JOBCODE_LBL,        #DW)
do Get_Field_Information ('HR',      'ANNUAL',    $ANNUAL_LBL,        #DW)
do Get_Field_Information ('HR',      'COMP_RATE2',    $RATE_LBL,       #DW)
do Get_Field_Information ('HR',      'ACTION',    $ACTION_LBL,        #DW)
do Get_Field_Information ('HR',      'ACTION_REASON2',   $ACTN_REAS_LBL,        #DW)
do Get_Field_Information ('CMP014',     'CHANGE',    $CHANGE_LBL,        #DW)
do Get_Field_Information ('HR',      'PERCENT_TYPE1',   $PERCENT_LBL,        #DW)
do Get_Field_Information ('HR',      'AMOUNT',    $AMOUNT_LBL,        #DW)
do Get_Field_Information ('CMP014',     'COMPRATE',    $COMPRATE_LBL,        #DW)
do Get_Field_Information ('CMP014',     'CURRENCY_CD',   $CUR_LBL,        #DW)
do Get_Field_Information ('HR',      'COMP2',    $CODE_LBL,        #DW)
do Get_Field_Information ('CMP014',     'FREQUENCY_ID',   $FREQ_LBL,        #DW)
do Get_Field_Information ('CMP014',     'PLAN',    $PLAN_LBL,        #DW)
do Get_Field_Information ('HR',      'GRADE2',    $GRADE_LBL,        #DW)

PRINT   $GROUP_LBL             (+3,{col1})
PRINT   $GROUP          (,{col2})

print   $EFFDT1_LBL            (+1,{col3})
print   $JOBCODE_LBL            (,{col4})
print   $ANNUAL_LBL            (,{col5_1})
print   $ACTION_LBL            (,{col7})
print   $CHANGE_LBL            (,{col8_1})
print   $CHANGE_LBL            (,{col9_1})
print   $CUR_LBL               (,{col11})

print   $EMPL_LBL  (+1,{col1})
print   $NAME_LBL               (,{col2})
print   $EFFDT2_LBL             (,{col3})
print   $CODE_LBL               (,{col4})
print   $RATE_LBL               (,{col5_1})
print   $ACTION_LBL             (,{col6_1})
print   $ACTN_REAS_LBL          (,{col7})
print   $PERCENT_LBL            (,{col8_1})
print   $AMOUNT_LBL             (,{col9_1})
print   $COMPRATE_LBL           (,{col10_1})
print   $CODE_LBL               (,{col11})
print   $FREQ_LBL               (,{col12},4)
print   $PLAN_LBL               (,{col13})
print   $GRADE_LBL              (,{col14})
end-heading


begin-procedure Init-Report

  move 'CMP014' to $ReportID
  do Stdapi-Init
  if $prcs_oprid=''
    display ''
    display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
    display ''
    goto last1
  end-if

  if $prcs_process_instance = ''
    do Ask-From-Thru-Date
  else
    do Select-Parameters
  end-if
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')
  LAST1:
end-procedure

begin-procedure Get-Values
   let $language_cd = $prcs_language_cd
   do Get-As-Of-Date
   do Get-From-Thru-Date
   let $GROUP = &RUN_CNTL_HR.GB_GROUP_ID
   let $print_comp = &RUN_CNTL_HR.MULT_COMP_FLAG

end-procedure

begin-procedure Process-Main
  if $language_cd = 'JPN'
    let $qualifier = 'A.NAME_AC'
  else
    let $qualifier = 'A.NAME'
  end-if
begin-SELECT
A.EMPLID
  print &A.EMPLID                      (+2,{col1})
A.NAME
  let $NAME = substr(&A.NAME, 1, 30)
  print $NAME                      (,{col2})
A.EMPL_RCD
A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                      (,{col3})
A.EFFSEQ
A.JOBCODE
  print &A.JOBCODE                      (,{col4})
A.ANNUAL_RT
  do Format-Number(&A.ANNUAL_RT, $out, '999999999999999.999')
  print $out                      (,{col5})
A.ACTION
  print &A.ACTION                      (,{col6})
A.ACTION_REASON
  print &A.ACTION_REASON                      (,{col7_2})
A.CHANGE_PCT
  do Format-Number(&A.CHANGE_PCT, $out, '999.99mi')
  print $out                      (,{col8})
A.CHANGE_AMT
  do Format-Number(&A.CHANGE_AMT, $out, '999999999999.9999mi')
  print $out                      (,{col9})
A.COMPRATE
  do Format-Number(&A.CompRate, $out, '999999999999.999999')
  print $out                      (,{col10})
A.CURRENCY_CD
  print &A.CURRENCY_CD                      (,{col11})
A.COMP_FREQUENCY
  print &A.COMP_FREQUENCY                      (,{col12})
A.SAL_ADMIN_PLAN
  print &A.SAL_ADMIN_PLAN                      (,{col13})
A.GRADE
  print &A.GRADE                               (,{col14})

 if $print_comp = 'Y'
    do Get-Compensation-Components
 end-if

FROM  PS_SAL_HIST_GRP_VW A, PS_SAL_PLN_GRP_RES B
WHERE B.GB_GROUP_ID = $GROUP 
AND   A.EMPLID = B.EMPLID
AND   A.EMPL_RCD = B.EMPL_RCD
AND   A.EFFDT = B.JOB_EFFDT
AND   A.EFFSEQ = B.JOB_EFFSEQ
AND   B.VERSIONGBQDM = (SELECT MAX(B2.VERSIONGBQDM) FROM PS_SAL_PLN_GRP_RES B2 
                        WHERE B2.GB_GROUP_ID = B.GB_GROUP_ID 
   AND   B2.EMPLID = B.EMPLID
   AND   B2.EMPL_RCD = B.EMPL_RCD
   AND   B2.JOB_EFFDT = B.JOB_EFFDT
   AND   B2.JOB_EFFSEQ = B.JOB_EFFSEQ)
AND   A.EFFDT = (SELECT MAX(A2.EFFDT) FROM PS_SAL_HIST_GRP_VW A2 
                 WHERE A.EMPLID = A2.EMPLID AND A.EMPL_RCD = A2.EMPL_RCD)
AND   A.EFFSEQ = (SELECT MAX(A3.EFFSEQ) FROM PS_SAL_HIST_GRP_VW A3 
                 WHERE A.EMPLID = A3.EMPLID AND A.EMPL_RCD = A3.EMPL_RCD AND A.EFFDT = A3.EFFDT)
ORDER BY A.EMPLID, [$qualifier], A.EFFDT
end-SELECT
end-procedure

begin-procedure Get-Compensation-Components
begin-SELECT
D.COMP_EFFSEQ
  do Format-Number(&D.COMP_EFFSEQ, $out, '999')
D.COMP_RATECD
 STRING &D.COMP_RATECD $out BY ' ' INTO $RATECD
  print $RATECD                      (+1,{col9})
D.COMPRATE
  do Format-Number(&D.CompRate, $out, '999999999999.999999')
  print $out                      (,{col10})
D.CURRENCY_CD                     (,{col11})
D.COMP_FREQUENCY                  (,{col12})
FROM  PS_COMPENSATION D
WHERE D.EMPLID    = &A.EmplID
  AND D.EMPL_RCD =  &A.EMPL_RCD
  AND D.EFFDT     = &A.Effdt
  AND D.EFFSEQ    = &A.Effseq
ORDER BY D.COMP_RATECD, D.COMP_EFFSEQ
end-SELECT
end-procedure

#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askftd.sqc'    !From Thru Date input

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines to format datetimes
#Include 'number.sqc'    !Routine to format numbers
#include 'stdapi.sqc'    !Routine to update run status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
