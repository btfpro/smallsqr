! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
! TLSMSG: Message Catalog Listing without explain text                 *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************

#Define C_Language_cd          14
#Define C_Message_set_nbr       1

#Define C_Message_nbr           1
#Define C_Message_text          8
#Define C_Last_update_dttm    110
#Define C_Descrlong            16
#Define C_Descrlong_wrap_col  100
#Define C_Descrlong_wrap_line 100

#include 'setenv.sqc' !Set environment

#include 'setup01.sqc'     ! Init printer and page size

begin-report
    do Initialize
    do Report
    do Terminate
    do Commit-Transaction
end-report

begin-heading 8
#include 'stdhdg01.sqc'

print 'Language Cd: '                  (3,1)
print $Language_Cd                     (0,+1)
move 'LANGUAGE_CD' to $FieldName
move $language_Cd  to $FieldValue
do   Read-Translate-Table
print $XlatLongName                    (0, +3)
print 'Message Set: '                  (4,1)
print #Message_set_nbr                 (0,0)  edit '99999'
do Get_Set_Descr

print '  Set'                          (7,{C_Message_set_nbr})
print 'Number'                         (7,{C_Message_nbr})
print 'Message Text'                   (7,{C_Message_text},100)
print 'Last Update'                    (7,{C_Last_update_dttm})

print '------'                         (8,{C_Message_set_nbr})
print '------'                         (8,{C_Message_nbr})
print '----------------------------------------------------------------------------------------------------' (8,{C_Message_text})
print '-----------'                    (8,{C_Last_update_dttm})

end-heading

begin-procedure Initialize           ! Get Source Database Information
    do Init-DateTime
    do Define-Prcs-Vars
    do Get-Run-Control-Parms
    input $Message_Explanations 'Print message explanations also? (Y/N)'
    uppercase $Message_Explanations
    move 'TLSMSG' to $ReportID
    move 'Message Catalog Listing' to $ReportTitle
    display $ReportTitle
    do Get-Current-DateTime

end-procedure

begin-procedure Terminate
    if #prcs_process_instance > 0
    do Update-Prcs-Run-Status
    end-if
    do Reset
end-procedure

!***********************************************************************
! Produce the Report -- Retrieve entries from PSMSGCATDEFN             *
!***********************************************************************
begin-procedure Report         ! Main report processing

! Get Base Language Messages
begin-SELECT
LANGUAGE_CD &Base_Language
FROM PSOPTIONS
end-SELECT

move &Base_Language to $Language_cd

begin-SELECT

MESSAGE_SET_NBR                 ()                   on-break PRINT=NEVER PROCEDURE=PAGE_BREAK LEVEL=1
  move &Message_set_nbr  to #Message_set_nbr
MESSAGE_NBR                     (+1,{C_MESSAGE_NBR}) EDIT '999999'
MESSAGE_TEXT                    (0,{C_MESSAGE_TEXT})
LAST_UPDATE_DTTM                &Last_update_dttm
    if RTRIM(&Last_update_dttm, ' ') <> ''
    do Format-DateTime(&Last_update_dttm, $out, {DEFDATE}, '', '')
    print  $out             (0,{C_Last_update_dttm})
    end-if

    if $Message_Explanations =  'Y'
DESCRLONG                      &Descrlong
    if &Descrlong <> ''
        print &Descrlong       (+2,{C_Descrlong})
                   WRAP {C_DESCRLONG_WRAP_COL}
                    {C_DESCRLONG_WRAP_LINE}
#ifndef EBCDIC
                    ON=<10> STRIP=<13>
#else
                    ON=<37> STRIP=<13>
#endif
        print  ''               (+1)
    end-if
    end-if

from PSMSGCATDEFN
order by
     MESSAGE_SET_NBR,
     MESSAGE_NBR
end-SELECT

! Now get the non-base language messages
begin-SELECT

LANGUAGE_CD              ()    on-break PRINT=NEVER PROCEDURE=PAGE_BREAK LEVEL=1
  move &Language_cd to $Language_cd
MESSAGE_SET_NBR    &Lang_Message_Set_nbr ()                   on-break PRINT=NEVER PROCEDURE=PAGE_BREAK LEVEL=2
  move &Message_set_nbr  to #Message_set_nbr
MESSAGE_NBR  &Lang_Message_Nbr        (+1,{C_MESSAGE_NBR}) EDIT '999999'
MESSAGE_TEXT &Lang_Message_Text       (0,{C_MESSAGE_TEXT})
    if $Message_Explanations =  'Y'
DESCRLONG                      &Lang_Descrlong
    if &Descrlong <> ''
        print &Lang_Descrlong       (+2,{C_Descrlong})
                   WRAP {C_DESCRLONG_WRAP_COL}
                    {C_DESCRLONG_WRAP_LINE}
#ifndef EBCDIC
                    ON=<10> STRIP=<13>
#else
                    ON=<37> STRIP=<13>
#endif
        print  ''               (+1)
    end-if
    end-if

from PSMSGCATLANG
order by LANGUAGE_CD,
     MESSAGE_SET_NBR,
     MESSAGE_NBR
end-SELECT


end-procedure



!***********************************************************************
! Get Message Set Descriptive Titles                                   *
!***********************************************************************
begin-procedure Get_Set_Descr
begin-SELECT

DESCR                   (0,+2)

from PSMSGSETDEFN
  where MESSAGE_SET_NBR = #Message_Set_Nbr
end-SELECT
end-procedure


!***********************************************************************
! Page Break Logic                                                     *
!***********************************************************************
begin-procedure page_break
    if $End = 'Y'
      do Reset
    end-if
    new-page
end-procedure

#include 'reset.sqc'        ! Reset Printer
#include 'curdttim.sqc'     ! Current Date and Time
#include 'readxlat.sqc'     ! Read Translate Table
#Include 'datetime.sqc'     ! Routines for date and time formatting
!#Include 'Number.sqc'       ! Routines to format numbers
#include 'prcsapi.sqc'   !Update Process Request API
#include 'prcsdef.sqc'   !Update Process Request variable declaration
#include 'tranctrl.sqc'     ! Transaction control
