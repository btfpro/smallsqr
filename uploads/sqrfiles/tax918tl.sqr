

!***********************************************************************
!  TAX918TL:  W-3 Transmittal - Totals Report - Puerto Rico            *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2011, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2011/12/21:16:23:46                        
!       $Release:  HR9                                        
!    $Resolution:  872510                                     
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP00003       Add PublicSector APRD Changes                          *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

begin-report
  display ''
  display 'Begin W-3PR Transmittal - Totals Report'
  do Init-Report
  do Process-Main
  do Stdapi-Term
  display ''
  display 'End   W-3PR Transmittal - Totals Report'
end-report

begin-procedure Init-Report
  move 'TAX918TL' to $ReportID
  move 'W-3PR Transmittal - Totals Report' to $ReportTitle
  move 'P' to $TaxForm_ID
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Get-Current-DateTime
  do Initialization
  do Assign-Year-End-Variables
end-procedure

begin-heading 7
  #Include 'stdhdg01.sqc'
  print 'Company'              (3,1)
  print $A.W2_Reporting_Co     (,+2)
  let $TaxYear = 'Tax Year ' || $CalYear
  print $TaxYear               (3,)    center
  print 'W-3PR'                (+3,3)
  print 'Box'                  (+1,3)
  print 'Description'          (,10)
  print 'Contents / Amount'    (,65)
end-heading

begin-procedure Initialization
  do Get-Current-DateTime
  move '1' to $MMLZero
  move '1' to $Year4
  do Format-DateTime($AsOfToday, $910Date, {DEFYMD},'','')

  move $910Date to $AsOfYear xxxx   !extract the current year
  move $AsOfYear  to #AsOfYear
  move #AsOfYear  to #saveAsOfYear
  subtract 1    from #AsOfYear
  do Get-Tax-Reporting-Run-Controls

  if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''
    display ''
    display 'Current Year is not one greater than Tax Reporting Year.'
    display 'Current Year is    ' noline
    display  #saveAsOfYear  9999
    display 'Tax Reporting Year ' noline
    display &TX.Balance_Year
    input $Answer maxlen=1 'Do you want to continue? (Y/N)'
    uppercase $Answer
    if $Answer <> 'Y'
      stop
    end-if
  end-if
end-procedure

begin-procedure Process-Main
  move &TX.Balance_Year to $CalYear 9999
begin-SELECT
A.W2_REPORTING_CO
A.DESCR
A.FEDERAL_EIN
A.ADDRESS1
A.ADDRESS2
A.CITY
A.STATE
A.POSTAL
A.COUNTRY
A.OTHER_EIN
A.MQGE_EMPLOYEES                                                !HP00003

  move &A.W2_Reporting_Co to $A.W2_Reporting_Co
  move &A.MQGE_EMPLOYEES  to $A.MQGE_Employees                  !HP00003
  if $A.MQGE_Employees = 'Y'                                    !HP00003
    let $Process_MQGE = 'Y'                                     !HP00003
                                                                !HP00003
    do Get-W2-Count                                             !HP00003
    move #W2_Count  to  #W2_MQGE_Count                          !HP00003
                                                                !HP00003
    if #W2_MQGE_Count > 0                                       !HP00003
      do Load-Amounts                                           !HP00003
      do Print-W3                                               !HP00003
      NEW-PAGE                                                  !HP00003
    end-if                                                      !HP00003
  end-if                                                        !HP00003

  let $Process_MQGE = 'N'                                       !HP00003

  do Get-W2-Count

  if #W2_Count > 0
    do Load-Amounts
    do Print-W3
    NEW-PAGE
  end-if

FROM  PS_W2_COMPANY A
WHERE A.CALENDAR_YEAR   = &TX.Balance_Year
  AND A.W2_REPORTING_CO = A.COMPANY
  AND A.W2_REPORTING_CO <> ' '
ORDER BY A.W2_REPORTING_CO
end-SELECT
end-procedure

begin-procedure Get-W2-Count
  move 0 to #W2_Count
begin-SELECT DISTINCT
EE.EMPLID
  add 1 to #W2_Count
FROM  PS_YE_EE EE, PS_W2_COMPANY C1, PS_YE_DATA WD1             !HP00003
WHERE C1.W2_REPORTING_CO = &A.W2_Reporting_Co
  AND EE.COMPANY         = C1.COMPANY
  AND EE.CALENDAR_YEAR   = C1.CALENDAR_YEAR
  AND EE.CALENDAR_YEAR   = &TX.Balance_Year
  AND EE.TAXFORM_ID      = 'P'
  AND EE.PROCESS_FLAG   <> 'V'
  AND WD1.COMPANY        = C1.COMPANY                           !HP00003
  AND WD1.CALENDAR_YEAR  = C1.CALENDAR_YEAR                     !HP00003
  AND WD1.COMPANY        = EE.COMPANY                           !HP00003
  AND WD1.EMPLID         = EE.EMPLID                            !HP00003
  AND WD1.CALENDAR_YEAR  = EE.CALENDAR_YEAR                     !HP00003
  AND WD1.TAXFORM_ID     = EE.TAXFORM_ID                        !HP00003
  AND WD1.SEQUENCE_NUMBER = 1                                   !HP00003
  AND WD1.MEDICARE_ONLY   = $Process_MQGE                       !HP00003
end-SELECT
end-procedure

begin-procedure Load-Amounts
  do Zero-Amounts
begin-SELECT
C2.W2_REPORTING_CO
D.BOX
#ifdef DB2ALL
DECIMAL(SUM(D.W2_AMOUNT),15,3) &D.W2_Amount
#else
SUM(D.W2_AMOUNT) &D.W2_Amount
#endif
  move &D.W2_Amount to #Amount

   let $D.Box = RTRIM(&D.Box,' ')
   evaluate $D.Box
     when = $YE_Cost_Pension_Annuity             !Cost of Pension or Annuity
       add  #Amount to #06
       break
     when = $YE_ER_Health_Cost                   !ER Health Cost
       add  #Amount to #06A
       break
     when = $YE_Charitable_Contrib               !Charitable Contribution
       add  #Amount to #06B
       break
     when = $YE_Wages                            !PR wages
       add  #Amount to #07
       break
     when = $YE_Commissions                      !Commissions
       add  #Amount to #08
       break
     when = $YE_Allowance                        !Allowances
       add  #Amount to #09
       break
     when = $YE_Tips                             !Tips
       add  #Amount to #10
       break
     when = $YE_Reimbursed_Expense               !Reimbursed Expenses
       add  #Amount to #12
       break
     when = $YE_Tax_Withheld                     !PR Tax Withheld
       add  #Amount to #13
       break
     when = $YE_Retire_Fund                      !Retirement fund
       add  #Amount to #14
       break
     when = $YE_CODA_Plans                       !CODA Contributions
       add  #Amount to #15
       break
     when = $YE_Sal_Act_No_324                   !Salaries under Act no 324 2004
       add  #Amount to #16
       break
     when = $YE_Save_Money_Pgm                   !Save Money Program
       add  #Amount to #16A
       break 
     when = $YE_SS_Wages                         !Soc Sec Wages
       add  #Amount to #17
       break
     when = $YE_SS_Tax                           !Soc Sec Tax Withheld
       add  #Amount to #18
       break
     when = $YE_Med_Wages                        !Medicare Wages and Tips
       add  #Amount to #19
       break
     when = $YE_Med_Tax                          !Medicare Tax Withheld
       add  #Amount to #20
       break
     when = $YE_SS_Tips                          !Soc Sec Tips
       add  #Amount to #21
       break
     when = $YE_SS_Tax_Tips                      !Uncollected Soc Sec
       add  #Amount to #22                       ! Tax on Tips
       break
     when = $YE_Med_Tax_Tips                     !Uncollected Medicare
       add  #Amount to #23                       ! Tax on Tips
       break
     !when = $YE_HIRE_Act_Wages                   !HIRE Act of 1020
     !  add  #Amount to #24
     !  break
   end-evaluate

FROM  PS_YE_AMOUNTS D, PS_YE_EE E1, PS_W2_COMPANY C2,
      PS_YE_DATA WD2                                            !HP00003
WHERE C2.W2_REPORTING_CO = &A.W2_Reporting_Co
  AND D.COMPANY          = C2.COMPANY
  AND D.CALENDAR_YEAR    = C2.CALENDAR_YEAR
  AND D.CALENDAR_YEAR    = &TX.Balance_Year
  AND D.SEQUENCE_NUMBER  = 1
  !AND D.BOX IN($YE_Cost_Pension_Annuity,
  !             $YE_Wages,
  !             $YE_Commissions,
  !             $YE_Allowance,
  !             $YE_Tips,
  !             $YE_Reimbursed_Expense,
  !             $YE_Tax_Withheld,
  !             $YE_Retire_Fund,
  !             $YE_CODA_Plans,
  !             $YE_Sal_Act_No_324,
  !             $YE_SS_Wages,
  !             $YE_SS_Tax,
  !             $YE_Med_Wages,
  !             $YE_Med_Tax,
  !             $YE_SS_Tips,
  !             $YE_SS_Tax_Tips,
  !             $YE_Med_Tax_Tips,
  !             $YE_HIRE_Act_Wages)
  AND D.COMPANY          = E1.COMPANY
  AND D.EMPLID           = E1.EMPLID
  AND D.TAXFORM_ID       = E1.TAXFORM_ID
  AND D.CALENDAR_YEAR    = E1.CALENDAR_YEAR
  AND D.TAXFORM_ID       = 'P'
  AND E1.PROCESS_FLAG   <> 'V'
  AND WD2.COMPANY        = C2.COMPANY                           !HP00003
  AND WD2.CALENDAR_YEAR  = C2.CALENDAR_YEAR                     !HP00003
  AND WD2.COMPANY        = E1.COMPANY                           !HP00003
  AND WD2.EMPLID         = E1.EMPLID                            !HP00003
  AND WD2.CALENDAR_YEAR  = E1.CALENDAR_YEAR                     !HP00003
  AND WD2.TAXFORM_ID     = E1.TAXFORM_ID                        !HP00003
  AND WD2.COMPANY        = D.COMPANY                            !HP00003
  AND WD2.EMPLID         = D.EMPLID                             !HP00003
  AND WD2.CALENDAR_YEAR  = D.CALENDAR_YEAR                      !HP00003
  AND WD2.TAXFORM_ID     = D.TAXFORM_ID                         !HP00003
  AND WD2.SEQUENCE_NUMBER = 1                                   !HP00003
  AND WD2.MEDICARE_ONLY   = $Process_MQGE                       !HP00003
GROUP BY C2.W2_REPORTING_CO, D.BOX
end-SELECT
end-procedure

begin-procedure Zero-Amounts
  move '' to $FedEIN
  move '' to $OtherEIN
  move 0 to #06
  move 0 to #06A
  move 0 to #06B
  move 0 to #07
  move 0 to #08
  move 0 to #09
  move 0 to #10
  move 0 to #11
  move 0 to #12
  move 0 to #13
  move 0 to #14
  move 0 to #15
  move 0 to #16
  move 0 to #16A
  move 0 to #17
  move 0 to #18
  move 0 to #19
  move 0 to #20
  move 0 to #21
  move 0 to #22
  move 0 to #23
  move 0 to #24
end-procedure

begin-procedure Print-W3
  print 'b     Total number of statements     '  (+2,4)
  print #W2_Count                                (,76)   edit 999999

  move &A.Federal_EIN to $FedEIN 099999999
  if $Process_MQGE = 'Y'                                        !HP00003
    print 'c     Employer identification number (MQGE)'  (+2,4) !HP00003
  else                                                          !HP00003
    print 'c     Employer identification number '  (+2,4)
  end-if                                                        !HP00003
  print $FedEIN                                  (,65)   edit xx-xxxxxxx

  print 'd     Employer name                  '  (+2,4)
  print &A.Descr                                 (,65)

  let $City = RTRIM(&A.City,' ')
  print 'e     Employer address               '  (+2,4)
  print &A.Address1                               (,65)
  print $City                                    (+1,65)
  print &A.State                                 ()       edit ,bxx

  Move &A.Postal to $Zip
  move $Zip            to $ZipExtsn ~~~~~~xxxx
  if &A.Country <> 'USA'
    print $Zip                                   (,+1)
    print &A.Country                             (,+1)
  else
    if instr($Zip,'-',6) = 0
    and $ZipExtsn <> '    '
      let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
    end-if
    print $Zip                                   (,+1)
  end-if

  print 'f     Other EIN used this year'         (+2,4)
  if &A.Other_EIN <> 0
    move &A.Other_EIN to $OtherEIN 099999999
    print $OtherEIN                              (,65)    edit xx-xxxxxxx
  end-if

  move '99999999999999.99' to $Fmt

  print ' 6    Cost of Pension or Annuity     '  (+2,4)
  do Format-Number(#06, $out, $Fmt)
  print $out  (,65)

  print '6A    ER Health Cost                 '  (+2,4)
  do Format-Number(#06A, $out, $Fmt)
  print $out  (,65)

  print '6B    Charitable Contribution        '  (+2,4)
  do Format-Number(#06B, $out, $Fmt)
  print $out  (,65)

  print ' 7    Wages                          '  (+2,4)
  do Format-Number(#07, $out, $Fmt)
  print $out  (,65)

  print ' 8    Commissions                    '  (+2,4)
  do Format-Number(#08, $out, $Fmt)
  print $out  (,65)

  print ' 9    Allowances                     '  (+2,4)
  do Format-Number(#09, $out, $Fmt)
  print $out  (,65)

  print '10    Tips                           '  (+2,4)
  do Format-Number(#10, $out, $Fmt)
  print $out  (,65)

  print '11    Total 7+8+9+10                 '  (+2,4)
  let #11 = #07+#08 + #09 + #10
  do Format-Number(#11, $out, $Fmt)
  print $out  (,65)

  print '12    Reimbursed Expenses            '  (+2,4)
  do Format-Number(#12, $out, $Fmt)
  print $out  (,65)

  print '13    Tax Withheld                   '  (+2,4)
  do Format-Number(#13, $out, $Fmt)
  print $out  (,65)

  print '14    Retirement Fund                '  (+2,4)
  do Format-Number(#14, $out, $Fmt)
  print $out  (,65)

  print '15    Contributions to CODA Plans    '  (+2,4)
  do Format-Number(#15, $out, $Fmt)
  print $out  (,65)

  print '16    Salaries under Act No.324, 2004'  (+2,4)
  do Format-Number(#16, $out, $Fmt)
  print $out  (,65)

  print '16A   Save Money Plan                '  (+2,4)
  do Format-Number(#16A, $out, $Fmt)
  print $out  (,65)

  print '17    Social Security Wages          '  (+2,4)
  do Format-Number(#17, $out, $Fmt)
  print $out  (,65)

  print '18    Social Security Tax Withheld   '  (+2,4)
  do Format-Number(#18, $out, $Fmt)
  print $out  (,65)

  print '19    Medicare Wages and Tips                     '   (+2,4)
  do Format-Number(#19, $out, $Fmt)
  print $out  (,65)

  print '20    Medicare Tax Withheld                       '   (+2,4)
  do Format-Number(#20, $out, $Fmt)
  print $out  (,65)

  print '21    Social Security Tips                        '   (+2,4)
  do Format-Number(#21, $out, $Fmt)
  print $out  (,65)

  print '22    Uncollected Social Security Tax on Tips     '   (+2,4)
  do Format-Number(#22, $out, $Fmt)
  print $out  (,65)

  print '23    Uncollected Medicare Tax on Tips            '   (+2,4)
  do Format-Number(#23, $out, $Fmt)
  print $out  (,65)

  !print '24    Wages and Tips under HIRE Act of 2010       '   (+2,4)
  !do Format-Number(#24, $out, $Fmt)
  !print $out  (,65)

end-procedure

#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'w2.sqc'        !Routine for W2 Year End Box variables
