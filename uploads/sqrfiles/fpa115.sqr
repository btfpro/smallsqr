!***********************************************************************
!  FPA115: Update the Collective Order Number and deletion of          *
!          FPAEEFLAT...                                                *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/07/19:12:33:47                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
#Define FPMACTION_TBL
#Include 'rellang.sqc'      ! Related Language File


#include 'setenv.sqc'       ! Set environment
#include 'setup01.sqc'      ! Printer and page-size initialization


begin-report
    do Init-DateTime
    do Init-Number
    do Get-Current-DateTime
    do Init-Report
    do PROCESS-MAIN
    do Reset
    do Stdapi-Term
end-report



begin-heading 8
    #Include 'stdhdg01.sqc'

    do Get_Field_Information($ReportID, 'COLL_ORDER_NBR' ,  $COLL_ORDER_NBR_LBL ,   #DW)
    do Get_Field_Information($ReportID, 'UPD_INT_REF' ,     $UPD_INT_REF_LBL ,      #DW)
    do Get_Field_Information($ReportID, 'EMPLID' ,          $EMPLID_LBL ,           #DW)
    do Get_Field_Information($ReportID, 'NAME' ,            $NAME_LBL ,             #DW)
    do Get_Field_Information($ReportID, 'EMPL_RCD' ,        $EMPL_RCD_LBL ,         #DW)
    do Get_Field_Information($ReportID, 'ORDER_NBR' ,       $ORDER_NBR_LBL ,        #DW)
    do Get_Field_Information($ReportID, 'ACTION_TYPE' ,     $ACTION_TYPE_LBL ,      #DW)
    do Get_Field_Information($ReportID, 'EDIT_ORDER_DT' ,   $EDIT_ORDER_DT_LBL ,    #DW)

    print $COLL_ORDER_NBR_LBL       (+1,22)
    print $Coldec                   (,+1)
    print $UPD_INT_REF_LBL          (,+2)
    print #RefcolPS                 (,+1)  edit 99999999
    print $EMPLID_LBL               (+2,1)
    print $NAME_LBL                 (,12)
    print $EMPL_RCD_LBL             (,51)
    print $ORDER_NBR_LBL            (,58)
    print $ACTION_TYPE_LBL          (,71)
    print $EDIT_ORDER_DT_LBL        (,96)

!YVG    print 'Record'           (+1,48)
!YVG    print 'Notification'     (,57)
!YVG    print 'Career'           (,71)
!YVG    print 'Order'            (,98)

end-heading



begin-procedure Init-Report

  move 'FPA115' to $ReportID
  display $ReportTitle

  let $language_cd = $CURR_LANGUAGE_CD

  do Init_Report_Translation($ReportID, $language_cd)
  do Append_Report_Translation($ReportID)

  do Get_Field_Information($ReportID, 'REPORT_TITLE', $ReportTitle, #DW)

  do Security-Param                !SQR security

! Process Scheduler setup
  do Stdapi-Init

  if $prcs_process_instance = ''
     do Input-Variables
  else
     do FP-Select-Parameters
     do Select-Parameters
  end-if

end-procedure



!*********   Parameters *********************
!-------------------------------!
begin-procedure Input-Variables

Input $Orig             'Origin = ''C'''
Input $RefcolPS         'Collective Intern Order Reference'
Input $Coldec           'Collective Order Number'

move $RefcolPS to #RefcolPS
! let #RefcolPS = To_Number($RefcolPS)

end-procedure Input-Variables

!-------------------------------!
begin-procedure Get-Values

Do Get-Orig                      !  FPgetval
Do Get-RefColPS                  !  FPgetval
Do Get-ColDec                    !  FPgetval

end-procedure Get-Values



!---------- Main process -------------!
begin-procedure PROCESS-MAIN

do Get-Notice

end-procedure PROCESS-MAIN



!-------- Process notifications  ----------!
begin-procedure Get-Notice

let $sql-statement = 'FPA115.SQR,Get-Notice,Select,PS_FPAEENOTICE A'
begin-select On-Error=SQL-Error

A.EMPLID
B.NAME
A.EMPL_RCD
A.ACTION
A.FP_NOTIF_REF
{DATEOUT-PREFIX}A.FP_COLEDIT_DT{DATEOUT-SUFFIX} &A.FP_COLEDIT_DT
A.FP_REFCOL_PS

   Do Update-Notice
   Do Delete-FPAEEFLAT
   Do Print-Notice

from  PS_FPAEENOTICE A,
      PS_PERSON_NAME B
      ,PS_FPFAST_PERS_VW2 SCRTY         !SQR security

where A.EMPLID=B.EMPLID
  and A.FP_REFCOL_PS  =  #RefColPS
  and A.FP_COLDEC_NBR  = ' '

  and B.EMPLID = SCRTY.EMPLID           !
  and A.EMPL_RCD = SCRTY.EMPL_RCD       !SQR security
  [$SecurityClause]                     !

order by A.EMPLID

end-select

end-procedure Get-Notice



!---------- Update Notification Number -------------!
begin-procedure Update-Notice

let $sql-statement = 'FPA115.SQR,Update-Notice,Update,PS_FPAEENOTICE'
begin-SQL On-Error=SQL-Error

Update PS_FPAEENOTICE

   Set FP_COLDEC_NBR=$ColDec, LAST_UPDATE_DATE={DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX}

Where EMPLID        = &A.EMPLID
  and EMPL_RCD     = &A.EMPL_RCD
  and FP_REFCOL_PS  = &A.FP_REFCOL_PS

end-SQL

end-procedure Update-Notice



!-------------------------------!
begin-procedure Delete-FPAEEFLAT

let $sql-statement = 'FPA115.SQR,Delete-FPAEEFLAT,Delete,PS_FPAEEFLAT_TBL'
Begin-SQL On-Error=SQL-Error

Delete From PS_FPAEEFLAT_TBL

Where  FP_ORIG       = $Orig        ! "C" = Collective...
  and  EMPLID        = &A.EMPLID
  and  FP_REFCOL_PS  = #RefColPS

end-SQL

End-Procedure Delete-FPAEEFLAT



!---------- Edit concerned IDs -------------!
begin-procedure Print-Notice

let $Name=substr(&B.NAME,1,35)
do Get-Descr

move '1' to $Year4
move '-' to $DDelimiter
do Format-DateTime(&A.FP_COLEDIT_DT,$ColDt,{DEFDMY},'','')

print &A.EMPLID                 (+1,1)
Print $Name                     (,12)
print &A.EMPL_RCD               (,51)
print &A.FP_NOTIF_REF           (,59)
print &A.ACTION                 (,73)
print $FPMACTION_TBL-DESCR      (,80)
print $ColDt                    (,97)

end-procedure Print-Notice



begin-procedure Get-Descr

let $sql-statement = 'FPA115.SQR,Get-Descr,Select,PS_FPMACTION C'
begin-select On-Error=SQL-Error

C.DESCR
C.ACTION
C.EFFDT

        Let $FPMACTION_TBL-DESCR = &C.DESCR
        Do Get_Related_FPMACTION_TBL(&C.ACTION,&C.EFFDT)        !Translation function

from  PS_FPMACTION_TBL C

where C.ACTION = &A.ACTION
and   C.EFF_STATUS = 'A'
and   C.EFFDT = (select max(W.EFFDT)
                 from  PS_FPMACTION_TBL W
                 where W.ACTION=C.ACTION
                 and   W.EFF_STATUS = 'A'
                 and   W.EFFDT <= {DATEIN-PREFIX}&A.FP_COLEDIT_DT{DATEIN-SUFFIX})

end-select

end-procedure Get-Descr



#include 'stdapi.sqc'    !Routine to update run status
#include 'hrrnctl1.sqc'  !Get run control parameter values (select parameter)
#include 'fprnctl1.sqc'  !Get run control parameter values (FPS)
#include 'hrgetval.sqc'  !Get values mask routines
#include 'fpgetval.sqc'  !Get values mask routines (FPS)
#include 'askftd.sqc'    !From Thru Date input

#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#include 'hrsecty.sqc'   !Get SQR Security parameters
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
