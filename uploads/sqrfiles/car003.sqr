!************************************************************************
! Report Name: CAR003.SQR - List of Vehicles Report
!************************************************************************
!
! Confidentiality Information:
!
! This module contains confidential and proprietary information 
! of Oracle; it is not to be copied, reproduced, or transmitted 
! in any form, by any means, in whole or in part, nor is it to  
! be used for any purpose other than that for which it is       
! expressly provided under the applicable license agreement.    
!                                                               
! Copyright (C) 2006 Oracle. All Rights Reserved.               
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2006/07/19:11:28:52                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!
!************************************************************************


#include 'setenv.sqc'    ! Set environment
Begin-Setup
#include 'ptpsl177.sqc'   !Printer and page-size initialization
End-Setup

#include 'stdapi.sqc'    ! Update process request API
#include 'readxlat.sqc'  ! Get XLAT Values
#include 'datemath.sqc'  ! SQR date arithmetic procedures
#include 'datetime.sqc'  ! Routines for date and time formatting
#include 'curdttim.sqc'  ! Get-Current-DateTime procedure
#include 'number.sqc'    ! Routines to format numbers
#include 'hrrnctl1.sqc'  ! Get run control parameter values
#include 'hrgetval.sqc'  ! Get values mask routines
#include 'sqrtrans.sqc'  ! Globalization


#define ACTIVE        'A'
#define C_CAR_ID       1
#define C_REGNUM      10
#define C_VIN         24
#define C_TYPE        41
#define C_SIZE        56
#define C_COLOR       71
#define C_MODEL       86


!------------------------------------------------------------------------
! Start Report
!------------------------------------------------------------------------
begin-report
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Stdapi-Init
   do Init-Report
   do Process-Main
   do Stdapi-Term
end-report


!------------------------------------------------------------------------
! Page heading
!------------------------------------------------------------------------
begin-heading 8
   do Get_Field_Information('CAR003','REPORT_TITLE',$REPORTTITLE,#DW)
#include 'stdhdgtr.sqc'
   do Get_Field_Information('CAR003','CAR_ID',$CarId,#DW)
   do Get_Field_Information('CAR003','COLOUR_CD',$Colour,#DW)
   do Get_Field_Information('CAR003','DESCRIPTION',$Description,#DW)
   do Get_Field_Information('CAR003','ENGINE_SIZE1',$EngineSize1,#DW)
   do Get_Field_Information('CAR003','ENGINE_SIZE2',$EngineSize2,#DW)
   do Get_Field_Information('CAR003','ENGINE_TYPE1',$EngineType1,#DW)
   do Get_Field_Information('CAR003','ENGINE_TYPE2',$EngineType2,#DW)
   do Get_Field_Information('CAR003','REGISTRATION_NUM1',$RegNum1,#DW)
   do Get_Field_Information('CAR003','REGISTRATION_NUM2',$RegNum2,#DW)
   do Get_Field_Information('CAR003','VIN',$VIN,#DW)

   print $CarId                     (+3, {C_CAR_ID},8)
   print $RegNum1                   (0,  {C_REGNUM},13)
   print $VIN                       (0,  {C_VIN},16)
   print $EngineType1               (0,  {C_TYPE},14)
   print $EngineSize1               (0,  {C_SIZE},14)
   print $Colour                    (0,  {C_COLOR},14)
   print $Description               (0,  {C_MODEL})
   print $RegNum2                   (+1, {C_REGNUM})
   print $EngineType2               (0,  {C_TYPE},30)
   print $EngineSize2               (0,  {C_SIZE},14)

end-heading


!------------------------------------------------------------------------
! Main processing
!------------------------------------------------------------------------
begin-procedure Process-Main

begin-select
CAR_ID
REGISTRATION_NUM
VIN
MANUFACT_CD
   let $ManufactCd = &Manufact_Cd
   let $FieldName  = 'MANUFACT_CD'
   let $FieldValue = $ManufactCd
   do Read-Translate-Table
   let $Manufact   = $XLATShortName
MODEL_DESCR
   let $ModelDescr = &Model_Descr
   let $CarModelFullDescr = $Manufact || ' ' || $ModelDescr
   do Get-Car-Model-Details
COLOUR_CD
   let $FieldName  = 'COLOUR_CD'
   let $FieldValue = &Colour_Cd
   do Read-Translate-Table
   let $Colour = $XLATShortName

   do Print-Report-Line

FROM PS_CAR_DATA
WHERE EFF_STATUS  = {ACTIVE}
and EFFDT <= $AsOfToday
ORDER BY CAR_ID

end-select

end-procedure Process-Main


!------------------------------------------------------------------------
! Report initialization
!------------------------------------------------------------------------
begin-procedure Init-Report
    #define Year4 '1'
   let $ReportID = 'CAR003'

   if $prcs_process_instance = ''
      !No Prompt
   else
      do Select-Parameters
   end-if

   do Init_Report_Translation($ReportID,$LanguageCode)
   do Append_Report_Translation('CAR003')

end-procedure Init-Report

! Called by Select-Parameters
begin-procedure Get-Values
  let  $LanguageCode = $PRCS_LANGUAGE_CD
end-procedure


!------------------------------------------------------------------------
! Get the car model details
!------------------------------------------------------------------------
begin-procedure Get-Car-Model-Details
begin-select
ENGINE_TYPE_CD
   let $FieldName  = 'ENGINE_TYPE_CD'
   let $FieldValue = &Engine_Type_Cd
   do Read-Translate-Table
   let $EngineType = $XLATShortName
ENGINE_SIZE
from PS_CAR_MODEL
where MANUFACT_CD = $ManufactCd
  and MODEL_DESCR = $ModelDescr
  and EFF_STATUS  = {ACTIVE}
  and EFFDT = (select max(EFFDT) from PS_CAR_MODEL
               where MANUFACT_CD  = $ManufactCD
                 and MODEL_DESCR  = $ModelDescr
                 and EFF_STATUS  = {ACTIVE}
                 and EFFDT <= $AsOfToday)
end-select

end-procedure Get-Car-Model-Details


!------------------------------------------------------------------------
! Output row to report
!------------------------------------------------------------------------
begin-procedure Print-Report-Line

   print &Car_Id                        (+1,{C_CAR_ID},8)
   print &Registration_Num              (0, {C_REGNUM},13)
   print &VIN                           (0, {C_VIN},16)
   print $EngineType                    (0, {C_TYPE},14)
   print &Engine_Size                   (0, {C_SIZE},14)
   print $Colour                        (0, {C_COLOR},14)
   print $CarModelFullDescr             (0, {C_MODEL})

end-procedure Print-Report-Line

