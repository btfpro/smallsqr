!***********************************************************************
!  FGHR025: Position Listing                                          *
!          (Select Option 1 for Active, 2 for Inactive, 3 for All)     *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2006/07/19:11:59:38                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'   !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Init-Report
   do Process-Main
   do Reset
   do Stdapi-Term
end-report


!********************************************
begin-procedure Init-Report

  move 'FGHR025' to $ReportID

  do Get-Report-Language

  display $FGHR025-REPORTTITLE
  move $FGHR025-REPORTTITLE to $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-As-Of-Date
    do Ask-Active-Option
  else
    do Select-Parameters
    do Get-As-Of-Date
    do Get-Active-Option
  end-if

end-procedure


!**********************************
! This procedure is used to read language-dependent text from the Strings table
!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from FGHR025 program IDS
do Init_Report_Translation($ReportID, $Language_Cd)
do Append_Report_Translation('HR')

do Get_Field_Information ('FGHR025', 'REPORT_TITLE',   $FGHR025-REPORTTITLE,    #CW)
do Get_Field_Information ('FGHR025', 'DEPT_ID',        $FGHR025-DEPTID,         #CW)
do Get_Field_Information ('HR',      'HEAD_CT1',       $FGHR025-MAX,            #CW)
do Get_Field_Information ('HR',      'POSITION_STAT1', $FGHR025-POSITION,       #CW)
do Get_Field_Information ('HR',      'JOBCODE1',       $FGHR025-JOB,            #CW)
do Get_Field_Information ('HR',      'EFFDT1',         $FGHR025-EFFECTIVE,      #CW)
do Get_Field_Information ('HR',      'REPORT_TO1',     $FGHR025-REPORTS,        #CW)
do Get_Field_Information ('HR',      'HEAD_CT2',       $FGHR025-HEAD,           #CW)
do Get_Field_Information ('HR',      'REG_TEMP1',      $FGHR025-REG,            #CW)
do Get_Field_Information ('HR',      'FULL_PART_TM1',  $FGHR025-FULL,           #CW)
do Get_Field_Information ('HR',      'STD_HRS1',       $FGHR025-STD,            #CW)
do Get_Field_Information ('HR',      'STATUS',         $FGHR025-STATUS,         #CW)
do Get_Field_Information ('HR',      'BUDGET_POS1',    $FGHR025-BUDG,           #CW)
do Get_Field_Information ('HR',      'JOBCODE2',       $FGHR025-CODE,           #CW)
do Get_Field_Information ('HR',      'POSITION_NBR2',  $FGHR025-NO,             #CW)
do Get_Field_Information ('HR',      'EFFDT2',         $FGHR025-DATE,           #CW)
do Get_Field_Information ('HR',      'POSITION_TITLE', $FGHR025-POSTITLE,       #CW)
do Get_Field_Information ('HR',      'REPORT_TO2',     $FGHR025-TO,             #CW)
do Get_Field_Information ('HR',      'AGENCY',         $FGHR025-AGENCY,         #CW)
do Get_Field_Information ('HR',      'LOCATION',       $FGHR025-LOCATION,       #CW)
do Get_Field_Information ('HR',      'HEAD_CT3',       $FGHR025-COUNT,          #CW)
do Get_Field_Information ('HR',      'REG_TEMP2',      $FGHR025-TEMP,           #CW)
do Get_Field_Information ('HR',      'FULL_PART_TM2',  $FGHR025-PART,           #CW)
do Get_Field_Information ('HR',      'STD_HRS2',       $FGHR025-HOURS,          #CW)
do Get_Field_Information ('HR',      'BUDGET_POS2',    $FGHR025-POSN,           #CW)

End-Procedure

begin-HEADING 9
#Include 'stdhdg03.sqc'
!******************************
!     column headings         *
!******************************
   print $FGHR025-MAX        (+3,110)
   print $FGHR025-POSITION   (+1,21)
   print $FGHR025-JOB        (0,13)
   print $FGHR025-EFFECTIVE  (0,31)
   print $FGHR025-REPORTS    (0,80)
   print $FGHR025-HEAD       (0,110)
   print $FGHR025-REG        (0,117)
   print '/'                 (0,122)
   print $FGHR025-FULL       (0,123)
   print '/'                 (0,128)
   print $FGHR025-STD        (0,130)
   print $FGHR025-POSITION   (0,137)
   print $FGHR025-STATUS     (0,147)
   print $FGHR025-BUDG       (0,159)
!*******************************
!     column headings          *
!*******************************
   print $FGHR025-DEPTID         (+1,1)
   print $FGHR025-CODE           (0,13)
   print $FGHR025-NO             (0,21)
   print $FGHR025-DATE           (0,31)
   print $FGHR025-POSTITLE       (0,46)
   print $FGHR025-TO             (0,80)
   print $FGHR025-AGENCY         (0,90)
   print $FGHR025-LOCATION       (0,99)
   print $FGHR025-COUNT          (0,110)
   print $FGHR025-TEMP           (0,117)
   print $FGHR025-PART           (0,123)
   print $FGHR025-HOURS          (0,130)
   print $FGHR025-STATUS         (0,137)
   print $FGHR025-DATE           (0,147)
   print $FGHR025-POSN           (0,159)
end-HEADING

!***********************************************************************
!  PROC Process-Main
!         Main Procedure
!***********************************************************************
begin-procedure Process-Main
begin-SELECT
A.DEPTID                 (+1,1) on-break level=1 skiplines=1
A.JOBCODE                (0,13)
A.POSITION_NBR           (0,21)
A.EFFDT
   do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
   print $out            (0,31)
A.DESCR                  (0,46)
A.REPORTS_TO             (0,80)
A.COMPANY                (0,90)
A.LOCATION               (0,99)
A.MAX_HEAD_COUNT         (0,113)
A.REG_TEMP               (0,117)
A.FULL_PART_TIME         (0,123)
A.STD_HOURS
    do Format-Number(&A.Std_hours, $out, 'bbb9.99')
    print $out           (0,128)
A.POSN_STATUS
    move 'POSN_STATUS' to $Fieldname
    move &A.Posn_Status to  $FieldValue
    do Read-Translate-Table
    print $XlatShortName (0,137)
A.STATUS_DT
    if RTRIM(&A.Status_Dt, ' ') <> ''
       do Format-DateTime(&A.Status_Dt, $out, {DEFDATE}, '', '')
       print $out        (0,147)
    end-if
A.BUDGETED_POSN          (0,159)
A.EFF_STATUS
    if $option = '3'
       and &A.Eff_Status = 'I'
       print 'I'         (0,43)
    end-if

FROM  PS_POSITION_DATA A
WHERE A.EFF_STATUS [$Instatus]
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_POSITION_DATA
       WHERE POSITION_NBR = A.POSITION_NBR
         AND EFFDT     <= $AsOfDate)

ORDER BY A.DEPTID,A.JOBCODE,A.POSITION_NBR

end-SELECT
end-procedure

!***********************************************************************

#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines
#include 'askaod.sqc'    !Ask As Of Date input

#include 'askactop.sqc'  !Ask Active Option
#Include 'readxlat.sqc'  !Read the Translate Table
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
