!*****************************************************
!  PAY017:  Print Earnings Distributions - Paysheets *
!*****************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:31:12                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-setup

!page-size 58 182
DECLARE-LAYOUT PAY017
MAX-COLUMNS = 182
MAX-LINES = 58
END-DECLARE

begin-SQL On-Error=Skip
DELETE FROM PS_R_PAY009;
end-SQL

end-setup


begin-report
  do Init-Report
  do Stdapi-Term
end-report

begin-procedure Init-Report

  move 'PAY017' to $ReportID 

  do Get-Report-Language          ! Procedure that decides the report language
                                  ! and then calls 'Report-Translation' procedure
                                  ! that reads the language dep. text.

  move $Pay017_Title to $ReportTitle

  do Init-DateTime
  do Init-Number
  move 'N' to $CyclePrompts     !Don't prompt for On/Off cycle in PAYINIT
  do Stdapi-Init
  do Payroll-Report-Initialization
  do Delete-EmplType-Table
end-procedure


!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
!Read language-dependent text from the database.

begin-procedure Report-Translation        ! Read from PAY017  program ids

   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('PAY017',    'REPORT_TITLE'       , $Pay017_Title           , #dummy_width)
   do Get_Field_Information ('PAY017',    'PAY_PERIOD'         , $Pay017_Pay_Period      , #dummy_width)
   do Get_Field_Information ('PAY017',    'RUN_ID'             , $Pay017_Run_Id          , #dummy_width)
   do Get_Field_Information ('PAY017',    'EMPLOYEE'           , $Pay017_Emplid          , #dummy_width)
   do Get_Field_Information ('PAY017',    'NAME'               , $Pay017_Name            , #dummy_width)
   do Get_Field_Information ('PAY017',    'BUSINESS_UNIT'      , $Pay017_BU              , #dummy_width)
   do Get_Field_Information ('PAY017',    'TYPE'               , $Pay017_Tp              , #dummy_width)
   do Get_Field_Information ('PAY017',    'DEPTID'             , $Pay017_Dept            , #dummy_width)
   do Get_Field_Information ('PAY017',    'PAGE'               , $Pay017_Pg              , #dummy_width)
   do Get_Field_Information ('PAY017',    'LINE'               , $Pay017_Ln              , #dummy_width)
   do Get_Field_Information ('PAY017',    'OK_TO_PAY'          , $Pay017_Ok_Pay          , #dummy_width)
   do Get_Field_Information ('PAY017',    'PAY'                , $Pay017_Pay             , #dummy_width)
   do Get_Field_Information ('PAY017',    'REG_PAY'            , $Pay017_Reg             , #dummy_width)
   do Get_Field_Information ('PAY017',    'HOURS'              , $Pay017_Hrs             , #dummy_width)
   do Get_Field_Information ('PAY017',    'OT'                 , $Pay017_OT              , #dummy_width)
   do Get_Field_Information ('PAY017',    'HOURLY'             , $Pay017_Hourly          , #dummy_width)
   do Get_Field_Information ('PAY017',    'RATE'               , $Pay017_Rate            , #dummy_width)
   do Get_Field_Information ('PAY017',    'SHIFT'              , $Pay017_Sh              , #dummy_width)
   do Get_Field_Information ('PAY017',    'JOBCODE'            , $Pay017_JobCd           , #dummy_width)
   do Get_Field_Information ('PAY017',    'POSITION_NBR'       , $Pay017_PosNbr          , #dummy_width)
   do Get_Field_Information ('PAY017',    'REASON'             , $Pay017_Reas            , #dummy_width)
   do Get_Field_Information ('PAY017',    'STATE'              , $Pay017_St              , #dummy_width)
   do Get_Field_Information ('PAY017',    'PROVINCE'           , $Pay017_Prv             , #dummy_width)
   do Get_Field_Information ('PAY017',    'LOCALITY'           , $Pay017_Lclty           , #dummy_width)
   do Get_Field_Information ('PAY017',    'TAX'                , $Pay017_Tax             , #dummy_width)
   do Get_Field_Information ('PAY017',    'PERIODS'            , $Pay017_Per             , #dummy_width)
   do Get_Field_Information ('PAY017',    'FREQUENCY'          , $Pay017_Freq            , #dummy_width)
   do Get_Field_Information ('PAY017',    'METHOD'             , $Pay017_Mtd             , #dummy_width)
   do Get_Field_Information ('PAY017',    'OTHER'              , $Pay017_Other           , #dummy_width)
   do Get_Field_Information ('PAY017',    'ERNCD'              , $Pay017_ErnCd           , #dummy_width)
   do Get_Field_Information ('PAY017',    'AMOUNT'             , $Pay017_ErnAmt          , #dummy_width)
   do Get_Field_Information ('PAY017',    'SEPCHK'             , $Pay017_Sep             , #dummy_width)
   do Get_Field_Information ('PAY017',    'CHK'                , $Pay017_Chk             , #dummy_width)


end-procedure


begin-heading 10
  #Include 'stdhdg02.sqc'

    do Format-DateTime($AsOfDate, $out, {DEFDATE}, '', '')
  
    print $Pay017_Pay_Period             (+1,1)
    print  $out                          (0,18)
    print $Pay017_Run_Id                 (+1,1)
    print  $SlctRunID                    (0,13)
    print $Pay017_Emplid                 (+2,1)
    print $Pay017_Emplid                 (0,12,3)
    print $Pay017_Dept                   (0,16,4)
    print $Pay017_Pg                     (0,26,2)
    print $Pay017_Ln                     (0,29,2)
    print $Pay017_ok_Pay                 (0,35,3)
    print $Pay017_Reg                    (0,41,3)
    print $Pay017_OT                     (0,50,3)
    print $Pay017_Other                  (0,56,14)
    print $Pay017_Hourly                 (0,77,6)
    print $Pay017_Reg                    (0,87,7)
    print $Pay017_Other                  (0,98,14)
    print $Pay017_Sh                     (0,114,5)
    print $Pay017_Dept                   (0,120,4)
    print $Pay017_JobCd                  (0,131,3)
    print $Pay017_PosNbr                 (0,138,7)
    print $Pay017_Sep                    (0,147,3)
    print $Pay017_Pay                    (0,151,3)
    print $Pay017_Prv                    (0,156,5)
    print $Pay017_Lclty                  (0,163,5)
    print $Pay017_Tax                    (0,170,3)
    print $Pay017_Freq                   (0,174,3)
    print $Pay017_Tax                    (0,178,3)

    print $Pay017_Name                   (+1,1)
    print $Pay017_Tp                     (0,12,3)
    print $Pay017_Pay                    (0,35,3)
    print $Pay017_Hrs                    (0,41,3)
    print $Pay017_Hrs                    (0,50,3)
    print $Pay017_ErnCd                  (0,57,4)
    print $Pay017_Other                  (0,62,3)
    print $Pay017_Hrs                    (0,66,3)
    print $Pay017_Rate                   (0,78,4)
    print $Pay017_Pay                    (0,89,3)
    print $Pay017_Erncd                  (0,99,4)
    print $Pay017_Other                  (0,105,3)
    print $Pay017_ErnAmt                 (0,109,3)
    print $Pay017_ErnCd                  (0,131,4)
    print $Pay017_Chk                    (0,147,3)
    print $Pay017_Reas                   (0,151,3)
    print $Pay017_St                     (0,156,5)
    print $Pay017_Per                    (0,170,3)
    print $Pay017_Mtd                    (0,178,3)

    print $Pay017_BU                     (+1,1)

end-heading


begin-procedure Report

begin-SELECT
A.COMPANY,
A.PAYGROUP,
A.PAY_END_DT

  move &A.Company    to $Company

  if $Company <> $priorCompany
    do Get-Company-Data
    move $Company to $priorCompany
    move $CompanyName to $priorCompanyName
  end-if

  move &A.PayGroup   to $PayGroup
  move &A.Pay_End_Dt to $AsOfDate
  do Update-EmplType-Table
  do Get-Earnings

FROM  PS_PAY_CALENDAR A
WHERE [$SlctCalendar]
  AND PAY_SHEETS_RUN = 'Y'
ORDER BY A.COMPANY, A.PAY_END_DT, A.PAYGROUP
end-SELECT

end-procedure


begin-procedure Get-Earnings
  move 'Y' to $First_One
begin-SELECT
B.ADDL_NBR,
B.OFF_CYCLE,
B.ACCT_CD

    if $PayGroup <> $PriorPayGroup and $PriorPayGroup <> ''
      NEW-PAGE
    end-if
    move $PayGroup    to $PriorPayGroup      !for use in the Heading

    if &B.Acct_cd <> $Acct_cd

      if  $First_One = 'Y'          !if first record
        move 'N' to $First_One
      else
        NEW-PAGE
      end-if

      move &B.Acct_cd to $Acct_cd        !for use in the Heading
      move &B.Off_Cycle to $Off_Cycle    !for use in getting Footing
      move -1 to #priorADDL_NBR
    end-if

!Skip a PaySheet line if there are no additional lines for a particular LINE_NUM.

  if &B.ADDL_NBR = 0 and #priorADDL_NBR = 0
    NEXT-LISTING SKIPLINES=2
  end-if

  move &B.ADDL_NBR to #priorADDL_NBR

B.PAGE_NUM            (1,25)   EDIT 999
  move &B.PAGE_NUM to #PAGE_NUM
B.LINE_NUM            (1,29)   EDIT 99

  if &B.ADDL_NBR <> 0
    do Format-Number(&B.ADDL_NBR, $Num_out, '099')
    let $num_out = LTRIM($num_out, ' ')
    print '-'      ()
    print $num_out ()
  end-if

C.EMPLID           (0,1,11)  on-break level=1 print=change/top-page
C.EMPL_TYPE        (0,+1,1)
C.DEPTID           (0,+1,10)
C.NAME             (2,1)     on-break level=1 print=change/top-page
C.BUSINESS_UNIT    (3,1)     on-break level=1 print=change/top-page
B.OK_TO_PAY        (1,36,1)
B.REG_PAY_HRS      (0,+1)    EDIT    B,999.99
B.OT_HRS           (0,+1)    EDIT    B,999.99
B.HOURLY_RT        (0,+14)   EDIT b99999,999.999999  ! change to 12,6 if necessary
B.REG_PAY          (0,+1)    EDIT B99,999.99
B.SHIFT            (0,+19,1)
B.DEPTID           (0,+3,10)
B.JOBCODE          (0,+1,6)
B.POSITION_NBR     (0,+1,8)
B.SEPCHK           (0,+2,1)  EDIT 9
B.ADDLPAY_REASON   (0,+2,3)
B.STATE            (0,+3,2)
B.LOCALITY         (0,+3,7)
B.TAX_PERIODS      (0,+2)    EDIT 99
F.FREQUENCY_TYPE   (0,+2,1)
B.TAX_METHOD       (0,+3,1)

  do Get-Other-Hours-Earnings

  NEXT-LISTING SKIPLINES=1 NEED=5

FROM  PS_PAY_EARNINGS B,PS_PAY_LINE C, PS_FREQUENCY_TBL F
WHERE B.COMPANY    = $Company
  AND B.PAYGROUP   = $PayGroup
  AND B.PAY_END_DT = $AsOfDate
  AND B.OFF_CYCLE  = 'N'
  AND B.SINGLE_CHECK_USE IN ('C', 'N')
  AND C.COMPANY    = $Company
  AND C.PAYGROUP   = $PayGroup
  AND C.PAY_END_DT = $AsOfDate
  AND C.OFF_CYCLE  = B.OFF_CYCLE
  AND C.PAGE_NUM      = B.PAGE_NUM
  AND C.LINE_NUM      = B.LINE_NUM
  AND C.SINGLE_CHECK_USE IN ('C', 'N')
  AND C.EMPL_TYPE IN
      (SELECT PRINT_EMPLTYPE
       FROM   PS_R_PAY009)
  AND B.PAY_FREQUENCY = F.FREQUENCY_ID
  AND F.EFF_STATUS    = 'A'
  AND F.EFFDT       =
    (SELECT MAX(F1.EFFDT)
       FROM PS_FREQUENCY_TBL F1
      WHERE F1.FREQUENCY_ID   = F.FREQUENCY_ID
        AND F1.EFF_STATUS     = F.EFF_STATUS
        AND F1.EFFDT         <= B.PAY_END_DT)
ORDER BY B.ACCT_CD,B.PAGE_NUM,B.LINE_NUM,B.ADDL_NBR
end-SELECT

end-procedure


begin-procedure Get-Other-Hours-Earnings

  move 1 to #HrsLine
  move 1 to #PayLine

begin-SELECT
ERNCD,OTH_HRS,OTH_PAY

  if &Oth_Hrs <> 0
    print &ErnCd     (#HrsLine,56,3)
    print &Oth_Hrs   (0,+1) edit 9,999.99
    add 2 to #HrsLine
  end-if

  if &Oth_Pay <> 0
    print &ErnCd     (#PayLine,98,3)
    print &Oth_Pay   (0,+1)   edit 9999,999.99
    add 2 to #PayLine
  end-if

FROM  PS_PAY_OTH_EARNS
WHERE COMPANY    = $Company
  AND PAYGROUP   = $PayGroup
  AND PAY_END_DT = $AsOfDate
  AND OFF_CYCLE  = &B.Off_Cycle
  AND PAGE_NUM      = &B.PAGE_NUM
  AND LINE_NUM      = &B.LINE_NUM
  AND ADDL_NBR      = &B.ADDL_NBR
ORDER BY ERNCD
end-SELECT

  if #HrsLine > 4 or #PayLine > 4
    position (+1)
    move -1 to #priorADDL_NBR
  end-if

end-procedure


begin-procedure Update-EmplType-Table

  do Delete-EmplType-Table

begin-SELECT
Y.EMPL_TYPE
  do Insert-EmplType
FROM   PS_PAYGROUP_TBL X,PS_PAYGRP_EMPLTYPE Y
WHERE  X.COMPANY         = $Company
  AND  X.PAYGROUP        = $PayGroup
  AND  X.EFFDT =
       (SELECT MAX(EFFDT)
        FROM   PS_PAYGROUP_TBL
        WHERE  COMPANY   = $Company
          AND  PAYGROUP  = $PayGroup
          AND  EFFDT    <= $AsOfDate)
  AND  Y.COMPANY         = $Company
  AND  Y.PAYGROUP        = $PayGroup
  AND  Y.EFFDT           = X.EFFDT
  AND  Y.PRINT_PAY_SHEET = 'Y'
end-SELECT

end-procedure


begin-procedure Insert-EmplType

begin-SQL
INSERT INTO PS_R_PAY009 (PRINT_EMPLTYPE) VALUES (&Y.Empl_Type)
end-SQL

end-procedure


begin-procedure Delete-EmplType-Table

begin-SQL on-error=Ignore
DELETE FROM PS_R_PAY009;
end-SQL

end-procedure


begin-procedure Ignore
! dummy procedure to avoid SQL error message
end-procedure


#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'payinit.sqc'   !Report Initialization and Timing
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language

