!***********************************************************************
!  BEN711: Retirement Plan Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:08:49:22                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

#define col_sep                  1   !Column Separator
#define col_plan_type            5   !Plan Type
#define col_plan_name           11   !Plan Name
#define col_ben_plan             8   !Ben Plan
#define col_ben_pln_name        31   !Benefit Plan Name
#define col_eff_date            11   !Eff Date
#define col_spec_accum          12   !Special Accumulator
#define col_empler_percnt        9   !Employer Percent
#define col_emplyee_percnt       9   !Employee Percent
#define col_empler_contrb       13   !Employer Contribution
#define col_coord_with_fica     12   !Coordinate with FICA?

begin-program
  do Init-Report
!--- 'RETIREMENT PLAN TABLE'
  do Get-Report-Language
  let $ReportTitle  = $BEN711-REPORTTITLE
  display ' '
  display $ReportTitle
  display ' '

  do Process-Main

  do Reset
  do StdAPI-Term
end-program

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  let $ReportID = 'BEN711'

  do Get-Current-DateTime
  do StdAPI-Init
end-procedure

!**********************************
! This procedure is used to read language-dependent text from the database.
!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BEN711 program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN711', 'REPORT_TITLE', $BEN711-REPORTTITLE, #CW)
  do Get_Field_Information ('BEN711', 'COORDINATE',   $BEN711-COORDINATE,  #CW)
  do Get_Field_Information ('BEN711', 'ACCUMULATOR',  $BEN711-ACCUMULATOR, #CW)
  do Get_Field_Information ('BEN711', 'SPECIAL',      $BEN711-SPECIAL,     #CW)
  do Get_Field_Information ('BEN711', 'CONTRIBUTION', $BEN711-CONTRIBUTION,#CW)
  do Get_Field_Information ('BEN711', 'WITH_FICA',    $BEN711-WITH-FICA,   #CW)

  do Get_Field_Information ('BEN1',   'EFFECTIVE',    $BEN1-EFFECTIVE,     #CW)
  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,          #CW)
  do Get_Field_Information ('BEN1',   'BENEFIT',      $BEN1-BENEFIT,       #CW)
  do Get_Field_Information ('BEN1',   'DATE',         $BEN1-DATE,          #CW)
  do Get_Field_Information ('BEN1',   'PERCENT',      $BEN1-PERCENT,       #CW)
  do Get_Field_Information ('BEN1',   'NAME',         $BEN1-NAME,          #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-TYPE,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN_NAME',    $BEN1-PLAN-NAME ,    #CW)
  do Get_Field_Information ('BEN1',   'EMPLOYEE',     $BEN1-EMPLOYEE,      #CW)
  do Get_Field_Information ('BEN1',   'EMPLOYER',     $BEN1-EMPLOYER,      #CW)

End-Procedure

!***************
begin-HEADING 8
!***************
  #Include 'stdhdgtr.sqc'

!Column Heading line 1
  print $BEN1-PLAN                 (+2,1,{col_plan_type})
  print $BEN1-PLAN                 (0,+{col_sep},{col_plan_name})
  print $BEN1-BENEFIT              (0,+{col_sep},{col_ben_plan})
  print $BEN1-BENEFIT              (0,+{col_sep},{col_ben_pln_name})
  print $BEN1-EFFECTIVE            (0,+{col_sep},{col_eff_date})
  print $BEN711-SPECIAL            (0,+{col_sep},{col_spec_accum})
  print $BEN1-EMPLOYER             (0,+{col_sep},{col_empler_percnt})
  print $BEN1-EMPLOYEE             (0,+{col_sep},{col_emplyee_percnt})
  print $BEN1-EMPLOYER             (0,+{col_sep},{col_empler_contrb})
  print $BEN711-COORDINATE         (0,+{col_sep},{col_coord_with_fica})

!Column Heading line 2
  print $BEN1-TYPE                 (+1,1,{col_plan_type})
  print $BEN1-NAME                 (0,+{col_sep},{col_plan_name})
  print $BEN1-PLAN                 (0,+{col_sep},{col_ben_plan})
  print $BEN1-PLAN-NAME            (0,+{col_sep},{col_ben_pln_name})
  print $BEN1-DATE                 (0,+{col_sep},{col_eff_date})
  print $BEN711-ACCUMULATOR        (0,+{col_sep},{col_spec_accum})
  print $BEN1-PERCENT              (0,+{col_sep},{col_empler_percnt})
  print $BEN1-PERCENT              (0,+{col_sep},{col_emplyee_percnt})
  print $BEN711-CONTRIBUTION       (0,+{col_sep},{col_empler_contrb})
  print $BEN711-WITH-FICA          (0,+{col_sep},{col_coord_with_fica})

!Column Heading line 3
  print '='                        (+1,1,{col_plan_type})               FILL
  print '='                        (0,+{col_sep},{col_plan_name})       FILL
  print '='                        (0,+{col_sep},{col_ben_plan})        FILL
  print '='                        (0,+{col_sep},{col_ben_pln_name})    FILL
  print '='                        (0,+{col_sep},{col_eff_date})        FILL
  print '='                        (0,+{col_sep},{col_spec_accum})      FILL
  print '='                        (0,+{col_sep},{col_empler_percnt})   FILL
  print '='                        (0,+{col_sep},{col_emplyee_percnt})  FILL
  print '='                        (0,+{col_sep},{col_empler_contrb})   FILL
  print '='                        (0,+{col_sep},{col_coord_with_fica}) FILL

end-HEADING

!***********************************************************************
!  PROC Process Main
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

PLAN_TYPE                  (+1,1,4)
  move 'PLAN_TYPE' to $FieldName
  move &Plan_Type to $FieldValue
  do Read-Translate-Table
  print $XlatShortName     (0,+2,10)
BENEFIT_PLAN               (0,+2,7)
  move &benefit_plan to $benefit_plan
  move &plan_type    to $plan_type
  do Get-Benefit-Name
  print $BenefitName       (0,+2,30)
EFFDT  &EffDt
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out               (0,+2,10)
ERNCD_SPCL                 (0,+2,11)
EMPLR_PCT   &emplr_pct
  do Format-Number(&emplr_pct,$out,'b99.999')
  print $out               (0,+3,7)
EMPL_PCT    &empl_pct
  do Format-Number(&empl_pct,$out,'b99.999')
  print $out               (0,+3,7)
EMPLR_EMPL_PCT  &emplr_empl_pct
  do Format-Number(&emplr_empl_pct,$out,'b99.999')
  print $out               (0,+7,7)
COORDINATE_FICA            (0,+4,10)

FROM PS_RTRMNT_PLAN_TBL

ORDER BY PLAN_TYPE, BENEFIT_PLAN, EFFDT DESC

END-SELECT
END-PROCEDURE

!********************************************************************
#Include 'getbennm.sqc'  !Get-Benefit-Name procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!************************ End Of Report *****************************
