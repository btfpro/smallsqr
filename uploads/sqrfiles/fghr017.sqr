!***************************************
!  FGHR017:  Personnel Actions History  *



!***************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/08/05:23:09:09                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  689848                                              !
!                                                                      *
!***********************************************************************
!Porcedures ending with "Scrty" and Record alias ending with "SC" are
!using SQR Security.
!***********************************************************************
#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do Init-Report

  if $prcs_oprid=''
  goto last2
 end-if

  do Process-Main

  do Reset
  do Stdapi-Term
LAST2:
end-report

begin-heading 7
do Get_Field_Information ('FGHR017',    'REPORT_TITLE',         $REPORTTITLE,   #DW)
  #Include 'stdhtr08.sqc'
do Get_Field_Information ('FGHR017',    'PERSONNEL',            $PERSONNEL,     #DW)
do Get_Field_Information ('FGHR017',    'YRS_SERVICE1',         $YRS_SERVICE1,  #DW)
do Get_Field_Information ('FGHR017',    'YRS_SERVICE2',         $YRS_SERVICE2,  #DW)
do Get_Field_Information ('HR',         'EMP_TYP1',             $EMPL_TYPE1,    #DW)
do Get_Field_Information ('HR',         'EMP_TYP2',             $EMPL_TYPE2,    #DW)
do Get_Field_Information ('HR',         'REG_TMP1',             $REG_TEMP1,     #DW)
do Get_Field_Information ('HR',         'REG_TMP2',             $REG_TEMP2,     #DW)
do Get_Field_Information ('HR',         'FULL_PART_TM1',        $FULL_PART_TM1, #DW)
do Get_Field_Information ('HR',         'FULL_PART_TM2',        $FULL_PART_TM2, #DW)
do Get_Field_Information ('HR',         'CURRENCY_CD1',         $CURRENCY_CD1,  #DW)
do Get_Field_Information ('HR',         'CURRENCY_CD2',         $CURRENCY_CD2,  #DW)
do Get_Field_Information ('HR',         'COMPRATE',             $COMPRATE,      #DW)
do Get_Field_Information ('HR',         'EFFDT1',               $EFFDT1,        #DW)
do Get_Field_Information ('HR',         'EFFDT2',               $EFFDT2,        #DW)
do Get_Field_Information ('HR',         'EMPLID',               $EMPLID,        #DW)
do Get_Field_Information ('HR',         'ACTION_REASON1',       $ACT_RSN1,      #DW)
do Get_Field_Information ('HR',         'ACTION_REASON2',       $ACT_RSN2,      #DW)
do Get_Field_Information ('HR',         'EMPL_NAME',            $NAME,          #DW)
do Get_Field_Information ('HR',         'HIRE_DT1',             $HIRE_DT1,      #DW)
do Get_Field_Information ('HR',         'HIRE_DT2',             $HIRE_DT2,      #DW)
do Get_Field_Information ('HR',         'JOBCODE1',             $JOBCODE1,      #DW)
do Get_Field_Information ('HR',         'JOBCODE2',             $JOBCODE2,      #DW)
do Get_Field_Information ('HR',         'JOBTITLE',             $JOBTITLE,      #DW)
do Get_Field_Information ('HR',         'GRADE1',               $GRADE1,        #DW)
do Get_Field_Information ('HR',         'GRADE2',               $GRADE2,        #DW)
do Get_Field_Information ('HR',         'SUPERVISOR_NAME',      $SUPER,         #DW)


  if $Action <> $priorAction
    move 'ACTION'  to $FieldName
    move $Action   to $FieldValue
    do Read-Translate-Table
    move $XlatLongName to $ActionName  !for use in Heading
    move $Action to $priorAction
  end-if

PRINT $PERSONNEL                (3,1)
  print $Action                 (0,+2) edit xxx--
  print $ActionName             ()
LET $REG1 = $REG_TEMP1 || '/'
LET $FULL1 = $FULL_PART_TM1 || '/'

PRINT $EFFDT1                   (+3,1)
PRINT $ACT_RSN1                 (0,12)
PRINT $HIRE_DT1                 (0,72)
PRINT $YRS_SERVICE1             (0,84)
PRINT $EMPL_TYPE1               (0,88)
PRINT $REG1                     (0,92)
PRINT $FULL1                    (0,97)
PRINT $JOBCODE1                 (0,103)
PRINT $GRADE1                   (0,121)

if #MultiCurr
PRINT $CURRENCY_CD1             (0,129)
end-if

PRINT $EFFDT2                   (+1,1)
PRINT $ACT_RSN2                 (0,12)
PRINT $NAME                     (0,23)
PRINT $EMPLID                   (0,60)
PRINT $HIRE_DT2                 (0,72)
PRINT $YRS_SERVICE2             (0,84)
PRINT $EMPL_TYPE2               (0,88)
PRINT $REG_TEMP2                (0,92)
PRINT $FULL_PART_TM2            (0,97)
PRINT $JOBCODE2                 (0,103)
PRINT $JOBTITLE                 (0,110)
PRINT $GRADE2                   (0,121)

if #MultiCurr
PRINT $CURRENCY_CD2             (0,129)
end-if

PRINT $COMPRATE                 (0,142)
PRINT $SUPER                    (0,156)

end-heading

begin-procedure Init-Report
Let $_TableAlias = 'B'
  move 'FGHR017' to $ReportID
  do Stdapi-Init
  if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto last1
  end-if

  do Security-Param

  if $prcs_process_instance = ''
    do Ask-From-Thru-Date
    do Ask-Actions
  else
    do Select-Parameters
    do Get-Values
  end-if
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')
LAST1:
end-procedure

begin-procedure Get-Values
   let $language_cd = $prcs_language_cd
   do Get-From-Thru-Date
   do Get-Actions

end-procedure

!***************************
begin-procedure Process-Main
begin-SELECT
C.BUSINESS_UNIT
  LET $BU = RTRIM(&C.BUSINESS_UNIT,' ')
C.SETID_DEPT
  Let $SetidDept = &C.SETID_DEPT
C.ACTION ()     on-break print=never level=1 procedure=Action-Change
  move &C.Action         to $Action
C.DEPTID ()     on-break print=never procedure=Dept-Change
  if $firstRec <> 'N'
    do Initialize-Subheading
    move 'N' to $firstRec
  end-if
C.EFFDT

  do Format-DateTime(&C.EffDt, $effdate, {DEFDATE}, '', '')
  print $effdate                    (+1,1)
C.ACTION_REASON
  move &C.Action_Reason  to $Action_Reason
  move &C.EffDt          to $AsOfDate
  do Get-Action-Reason
  print $Action_Reason_DescrShort   (0,12)
!*********BEGIN SQR NAME DISPLAY CHANGE*********************
!A.NAME                              (0,23)
A.NAME_DISPLAY                     (0,23)
!*********BEGIN SQR NAME DISPLAY CHANGE*********************
A.EMPLID                            (0,60)

B.HIRE_DT
  do Format-DateTime(&B.Hire_Dt, $out, {DEFDATE}, '', '')
  print $out                         (0,72)

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime(&B.Hire_Dt, $Hire_YMD, {DEFYMD}, '', '')
  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime(&C.EffDt, $Eff_YMD, {DEFYMD}, '', '')
  do Dtu-Diff-Months($Hire_YMD, $Eff_YMD, #Months)
  Let #YearsSvc = round((#Months / 12),1)
  do Format-Number(#YearsSvc, $out, '99.9')
  print $out                        (0,83)

C.EMPL_TYPE                         (0,88)
C.REG_TEMP                          (0,92)
C.FULL_PART_TIME                    (0,97)
C.JOBCODE                           (0,103)
C.SETID_JOBCODE
  move &C.SETID_JOBCODE to $SETID
  move &C.JobCode to $Jobcode
  do Get-Job-Title
  print $JobTitleAbbrv              (0,110)
C.SAL_ADMIN_PLAN                    (0,121) EDIT XXX/
C.GRADE                             ()
C.CURRENCY_CD
  if #MultiCurr
     print &C.Currency_cd           (0,129)
  end-if
C.COMPRATE
  do Format-Number(&C.CompRate, $out, '999999999999.999999')
  print $out                        (0,134)
C.COMP_FREQUENCY                    (0,154)
C.SUPERVISOR_ID
  move &C.Supervisor_ID to $EmplID
  do Get-Employee-Name
  print $Name                        (0,156,21)
FROM  PS_PERSON_NAME A,
      PS_EMPLOYMENT B,
      PS_JOB C

WHERE A.EMPLID = B.EMPLID
  AND B.EMPLID = C.EMPLID
  AND B.EMPL_RCD = C.EMPL_RCD
  AND C.EFFDT BETWEEN $FromDate AND $ThruDate
  AND C.ACTION IN ([$ACTIONSTRING])
  [$SecurityClausewithERN]

ORDER BY C.ACTION,C.DEPTID,C.EFFDT DESC
end-SELECT
end-procedure Process-Main


!-----------------------------
begin-procedure Action-Change
  new-page
end-procedure

begin-procedure Dept-Change
  if #current-line >= 51  !ensure a minimum of 2 employees is listed
    new-page
  else
    position (+3)
  end-if
  do Initialize-Subheading
end-procedure

begin-procedure Initialize-Subheading
  move &C.DeptID to $DeptID
  do Get-Department-Name
  do Print-Subheading
end-procedure

begin-procedure Print-Subheading
  print 'Department' (+1,1)
  print $DeptID      (0,+1)
  print $DeptName    (0,+2)
  print 'SetID -'    (+1,1)
  print $SetidDept   (0,+1)
  position (+1)

end-procedure

#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines
#include 'askftd.sqc'    !From Thru Date input
#include 'askactn.sqc'   !Action input
#include 'hrsecty.sqc'   !Get SQR Security parameters
#include 'fgrnctl2.sqc'  !Get Run-Control Actions
#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'getjobtl.sqc'  !Get-Job-Title procedure
#Include 'getempnm.sqc'  !Get-Employee-Name procedure
#Include 'getactrs.sqc'  !Get-Action-Reason procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset1.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
