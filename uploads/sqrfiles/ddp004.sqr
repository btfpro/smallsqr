!*************************************
!  DDP004:  Payroll Advice Register  *
!*************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:03:36:22                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'DDP004' to $ReportID

  do Init-DateTime
  do Init-Number
  do Stdapi-Init

  do Get-Report-Language
  move $Ddp004_ReportTitle to $ReportTitle
  display $ReportTitle

  do Payroll-Report-Initialization
end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from DDP004 program ids
  do Init_Report_Translation($ReportID,$language_cd)
  do Append_Report_Translation('HR')

  do Get_Field_Information ('DDP004', 'REPORTTITLE',  $Ddp004_ReportTitle, #dummy_width)
  do Get_Field_Information ('DDP004', 'FORM_ID',      $Ddp004_Form_ID,     #dummy_width)
  do Get_Field_Information ('DDP004', 'SEP_CHK1',     $Ddp004_Sep_Chk1,    #dummy_width)
  do Get_Field_Information ('DDP004', 'LOCN_CD1',     $Ddp004_Locn_Cd1,    #dummy_width)
  do Get_Field_Information ('DDP004', 'PAYGRP1',      $Ddp004_PayGrp1,     #dummy_width)
  do Get_Field_Information ('DDP004', 'ADDRESS1',     $Ddp004_Address1,    #dummy_width)
  do Get_Field_Information ('DDP004', 'ADVICE2',      $Ddp004_Advice2,     #dummy_width)
  do Get_Field_Information ('DDP004', 'CHKDT2',       $Ddp004_ChkDt2,      #dummy_width)
  do Get_Field_Information ('DDP004', 'DEPOSIT2',     $Ddp004_Deposit2,    #dummy_width)
  do Get_Field_Information ('DDP004', 'SEP_CHK2',     $Ddp004_Sep_Chk2,    #dummy_width)
  do Get_Field_Information ('DDP004', 'EMPNAME2',     $Ddp004_EmpName2,    #dummy_width)
  do Get_Field_Information ('DDP004', 'EMPID2',       $Ddp004_EmpId2,      #dummy_width)
  do Get_Field_Information ('DDP004', 'DEPT2',        $Ddp004_Dept2,       #dummy_width)
  do Get_Field_Information ('DDP004', 'DEPT_ID2',     $Ddp004_Dept_Id2,    #dummy_width)
  do Get_Field_Information ('DDP004', 'LOCN2',        $Ddp004_Locn2,       #dummy_width)
  do Get_Field_Information ('DDP004', 'LOCN_CD2',     $Ddp004_Locn_Cd2,    #dummy_width)
  do Get_Field_Information ('DDP004', 'PAYGRP2',      $Ddp004_PayGrp2,     #dummy_width)
  do Get_Field_Information ('DDP004', 'ADDRESS2',     $Ddp004_Address2,    #dummy_width)
  do Get_Field_Information ('DDP004', 'GRANDTOT',     $Ddp004_GrandTot,    #dummy_width)
  do Get_Field_Information ('DDP004', 'VOUCHERS',     $Ddp004_Vouchers,    #dummy_width)
  do Get_Field_Information ('DDP004', 'VOUCHER',      $Ddp004_Voucher,     #dummy_width)
  do Get_Field_Information ('DDP004', 'ONLINE',       $Ddp004_Online,      #dummy_width)
  do Get_Field_Information ('DDP004', 'FINAL',        $Ddp004_Final,       #dummy_width)
  do Get_Field_Information ('DDP004', 'ONLN_FNL',     $Ddp004_Onln_Fnl,    #dummy_width)
  do Get_Field_Information ('DDP004', 'SOURCE',       $Ddp004_Source2,     #dummy_width)
  do Get_Field_Information ('DDP004', 'COMPTOT',      $Ddp004_CompTot,     #dummy_width)
  do Get_Field_Information ('DDP004', 'LEGEND',       $Ddp004_Legend,      #dummy_width)

  do Get_Field_Information ('HR', 'BUS_UNIT1',  $HR_Bus_Unit1, #dummy_width)
  do Get_Field_Information ('HR', 'BUS_UNIT2',  $HR_Bus_Unit2, #dummy_width)

end-procedure

begin-heading 7
  #Include 'stdhtrpi.sqc'
  #Include 'stdhtr02.sqc'
  print $Ddp004_Form_ID               (+1,1)
  print $Form_ID                      (0,13)

  print $Ddp004_Sep_Chk1              (+2,50)
  print $Hr_Bus_Unit1                 (0,121)
  print $Ddp004_Locn_Cd1              (0,142)
  print $Ddp004_PayGrp1               (0,148)
  print $Ddp004_Address1              (0,155)

  print $Ddp004_Advice2               (+1,1)
  print $Ddp004_Source2               (0,17)
  print $Ddp004_ChkDt2                (0,25)
  print $Ddp004_Deposit2              (0,37)
  print $Ddp004_Sep_Chk2              (0,50)
  print $Ddp004_EmpName2              (0,56)
  print $Ddp004_EmpId2                (0,84)
  print $Ddp004_Dept2                 (0,97)
  print $Ddp004_Dept_Id2              (0,109)
  print $Hr_Bus_Unit2                 (0,123)
  print $Ddp004_Locn2                 (0,130)
  print $Ddp004_Locn_Cd2              (0,142)
  print $Ddp004_PayGrp2               (0,148)
  print $Ddp004_Address2              (0,155)

end-heading


begin-procedure Report

begin-SELECT
A.COMPANY () on-break print=never procedure=Company-Change
B.FORM_ID () on-break print=never procedure=FormID-Change
B.PAYCHECK_NBR
B.PAYGROUP
B.PAY_END_DT
B.OFF_CYCLE,
B.PAGE_NUM,
B.LINE_NUM
B.SEPCHK

#ifdef DB2ALL
DECIMAL(SUM(B.DEPOSIT_AMOUNT), 15, 3) &NetDeposit
#else
SUM(B.DEPOSIT_AMOUNT)        &NetDeposit
#end-if

  move &B.Form_ID to $Form_ID
  do Get-Check-Data
  do Print-Line

FROM  PS_PAY_CALENDAR   A,
      PS_PAY_DISTRIBUTN B
WHERE [$SlctCalendar]
  AND B.OFF_CYCLE IN ($SlctOffCycleA, $SlctOffCycleB)
  AND B.PAGE_NUM BETWEEN #SlctPageFrom AND #SlctPageThru
  AND B.COMPANY          = A.COMPANY
  AND B.PAYGROUP         = A.PAYGROUP
  AND B.PAY_END_DT       = A.PAY_END_DT
  AND B.ACCOUNT_TYPE    <> '$'
GROUP BY A.COMPANY,
         B.FORM_ID,
         B.PAYCHECK_NBR,
         B.PAY_END_DT,
         B.PAYGROUP,
         B.PAGE_NUM,
         B.LINE_NUM,
         B.OFF_CYCLE,
         B.SEPCHK
ORDER BY A.COMPANY,
         B.FORM_ID,
         B.PAYCHECK_NBR
end-SELECT

  move 1 to #level
  do Company-Change
  print $Ddp004_GrandTot   (+2,1)
  do Format-Number(#GrandNetDeposit, $out, '$,$$$,$$$,$$9.99')
  print $out  (0,33)
  do Format-Number(#GrandVouchers, $out, '999,999')
  print $out      (0,+2)
  if #GrandVouchers <> 1
    print $Ddp004_Vouchers        (0,+1)
  else
    print $Ddp004_Voucher         (0,+1)
  end-if
  do Reset
end-procedure

begin-procedure Get-Check-Data
begin-SELECT
C.NAME
C.EMPLID
C.DEPTID
C.LOCATION
C.CHECK_DT
C.PAYCHECK_OPTION
C.PAYCHECK_ADDR_OPTN
C.PAY_SHEET_SRC
C.BUSINESS_UNIT

  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn

#ifdef DB2ALL
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif
#ifdef MICROSOFT
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif
!The is a workaround due to a bug in the current version of SQRW when using
!the -TB flag. This will be removed once the MITI fix is available...

FROM  PS_PAY_CHECK C
WHERE C.COMPANY          = &A.Company
  AND C.PAYGROUP         = &B.PayGroup
  AND C.PAY_END_DT       = &B.Pay_End_Dt
  AND C.OFF_CYCLE        = &B.Off_Cycle
  AND C.PAGE_NUM            = &B.PAGE_NUM
  AND C.LINE_NUM            = &B.LINE_NUM
  AND C.SEPCHK           = &B.SepChk
end-SELECT

end-procedure


begin-procedure Print-Line

  print &B.PAYCHECK_NBR            (+1,1)  edit 099999999999999
  evaluate &C.Pay_Sheet_Src
    when = 'O'
      print $Ddp004_Online   (0,17)
      break
    when = 'K'
      print $Ddp004_Final    (0,17)
      break
    when = 'L'
      print $Ddp004_Onln_Fnl (0,17)
      break
  end-evaluate

  do Format-DateTime(&C.Check_Dt, $out, {DEFDATE}, '', '')
  print $out       (0,25)

  print &B.SepChk          (0,50)  edit b9
  print &C.Name            (0,54,30)
  print &C.EmplID          (0,85)
  print &C.DeptID          (0,109)
  print &C.Business_Unit   (0,123)
  print &C.Location        (0,142)
  print &B.PayGroup        (0,149)

  move &A.Company  to $Company
  move &C.Check_Dt to $AsOfDate  !for included procedures
  move &C.DeptID   to $DeptID
  move &C.Business_unit to $BU
  do Get-Department-Name
  print $DeptNameAbbrv            (0,97)
  move &C.Location to $Location
  move &C.Business_unit to $Busunit
  let $Recname  = 'LOCATION_TBL'
  do Get-Setid($Busunit, $Recname, $Setid)
  do Get-Location-Name
  print $LocationAbbrv            (0,130)

#ifdef DB2ALL
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif
#ifdef MICROSOFT
  move &C.PayCheck_Addr_Optn to $PayCheck_Addr_Optn xxxx
#endif
!The is a workaround due to a bug in the current version of SQRW when using
!the -TB flag. This will be removed once the MITI fix is available...

  let $FieldName  = 'PAYCHECK_ADDR_OPTN'
  let $FieldValue = $PayCheck_Addr_Optn
  do Read-Translate-Table
  move $XLatShortName to $AddressOption

  print $AddressOption            (0,155)

  print &NetDeposit               (0,36)  edit 99,999,999.99
  add &NetDeposit to #TotalNetDeposit
  add 1           to #TotalVouchers
end-procedure


begin-procedure FormID-Change
  new-page
end-procedure


begin-procedure Company-Change
  print $Ddp004_CompTot    (+2,1)
  do Format-Number(#TotalNetDeposit, $out, '$,$$$,$$$,$$9.99')
  print $out  (0,33)
  do Format-Number(#TotalVouchers, $out, '999,999')
  print $out      (0,+2)
  if #TotalVouchers <> 1
    print $Ddp004_Vouchers   (0,+1)
  else
    print $Ddp004_Voucher    (0,+1)
  end-if
  add #TotalVouchers   to #GrandVouchers
  add #TotalNetDeposit to #GrandNetDeposit
  move 0 to #TotalVouchers
  move 0 to #TotalNetDeposit
  if #level = 0
    new-page
  end-if
end-procedure

!******************************************************
!  RESET: Reset printer, print 'End of Report'        *
!         Note: Uncomment ENCODE statement to force   *
!               a printer reset at end of report      *
!******************************************************

begin-procedure Reset
  move 'Y' to $End_of_Report
  if #MsgFlagW
     print $MsgTextW (+2,1)
  end-if
end-procedure

begin-footing 2
  print $Ddp004_Legend (1,1)
  if $End_of_Report = 'Y'
    !*******************************************************************
    !  Use a globalized string if one was gotten from stdhdgtr         *
    !*******************************************************************
    if $stdhdg_end_rep = ''
      print 'End of Report' (+1,1) center
    else
      print $stdhdg_end_rep (+1,1) center
    end-if
 !  encode '<27>E' into $Reset
    print $Reset (+0,+1)
  end-if
end-footing


#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'getsetid.sqc'  !Get-Setid procedure
#Include 'getlocnm.sqc'  !Get-Location-Name procedure
#Include 'payinit.sqc'   !Report Initialization and Timing
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read-Translate-Table procedure
