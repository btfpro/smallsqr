!***********************************************************************
!  BEN717CN: Pension Plan Table (Part 2) for Canada
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:08:51:02                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

!*****************************
begin-program
!*****************************

  do Init-Report
  do Process-Main
  do Reset
  do StdAPI-Term

end-program

!*****************************
begin-procedure Init-Report
!*****************************

  do Init_Printer
  do Init-DateTime
  do Init-Number
  move 'BEN717CN' to $ReportID

  do Get-Current-DateTime
  do StdAPI-Init
  do Get-Report-Language   ! Procedure that decides the report language
                           ! and then calls 'Report-Translation' procedure
                           ! that reads the language dep. text.
  display ' '
  move $Ben717cn_Title   to $ReportTitle
  display $ReportTitle
  display ' '

end-procedure


!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
!Read language-dependent text from the database.

begin-procedure Report-Translation        ! Read from BEN717CN  program ids

    do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('BEN717CN', 'TITLE'  ,     $Ben717cn_Title  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'PNS_RATE'  ,  $Ben717cn_Pns_Rate  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'CNTR'  ,      $Ben717cn_Cntr  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'CNTR_OVER_YMPE' , $Ben717cn_Cntr_Over_YMPE , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'CNTR_UP_YMPE' ,   $Ben717cn_Cntr_Up_YMPE  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'PLAN'  ,      $Ben717cn_Plan  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'BEN'  ,       $Ben717cn_Ben  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'EFF'  ,       $Ben717cn_Eff  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'TYP'  ,       $Ben717cn_Typ  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'NAME'  ,      $Ben717cn_Name  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'BEN_PL_NAME' , $Ben717cn_Ben_Pl_Name  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'DATE'  ,      $Ben717cn_Date  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'RATE_TYP'  ,  $Ben717cn_Rate_Typ  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'EARN_LIM'  ,  $Ben717cn_Earn_Lim  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'EMPL%'  ,     $Ben717cn_Empl%  , #dummy_width)
   do Get_Field_Information ('BEN717CN', 'EMPLR%'  ,    $Ben717cn_Emplr%  , #dummy_width)
end-procedure

!*****************************
begin-HEADING 8
!*****************************

  #Include 'stdhdgtr.sqc'

  print $Ben717cn_Pns_Rate           (+2,95)
  print $Ben717cn_Cntr_Up_YMPE       (0,113)
  print $Ben717cn_Cntr_Over_YMPE     (0,137)
  print $Ben717cn_Plan               (+1,1)
  print $Ben717cn_Plan               (0,7)
  print $Ben717cn_Ben                (0,19)
  print $Ben717cn_Eff                (0,59)
  print $Ben717cn_Cntr               (0,72)
  print '-'                        (0,94,14)  fill
  print '-'                        (0,112,20) fill
  print '-'                        (0,135,20) fill
 print $Ben717cn_Typ                 (+1,1)
 print $Ben717cn_Name                (0,7)
 print $Ben717cn_Plan                (0,19)
 print $Ben717cn_Ben_Pl_Name         (0,27)
 print $Ben717cn_Date                (0,59)
 print $Ben717cn_Rate_Typ            (0,72)
 print $Ben717cn_Earn_Lim            (0,94)
 print $Ben717cn_Empl%               (0,112)
 print $Ben717cn_Emplr%               (0,123)
 print $Ben717cn_Empl%               (0,135)
 print $Ben717cn_Emplr%               (0,146)


end-HEADING

!*****************************
BEGIN-PROCEDURE Process-Main
!*****************************

BEGIN-SELECT

A.PLAN_TYPE                    (+1,1)  on-break level=1 skiplines=1
  move 'PLAN_TYPE'  to $FieldName
  move &A.Plan_Type to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,7)   on-break level=1

A.BENEFIT_PLAN                 (0,19)  on-break level=2 skiplines=2
  move &A.Benefit_Plan to $Benefit_Plan
  move &A.Plan_Type    to $Plan_Type
  do Get-Benefit-Name
  print $BenefitName           (0,27)  on-break level=2

A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                   (0,59)  on-break level=3

A.PENS_RT_TBL_TYPE
  move 'PENS_RT_TBL_TYPE'  to $FieldName
  move &A.Pens_Rt_Tbl_Type to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,72)  on-break level=3

  do Get-Rate-Info

FROM  PS_PENSION_PLN_TBL A
ORDER BY A.PLAN_TYPE,A.BENEFIT_PLAN,A.EFFDT

END-SELECT
END-PROCEDURE

!*****************************
BEGIN-PROCEDURE Get-Rate-Info
!*****************************

!Note: This Select must be separate from the parent since there is the
!      possibility of no child records. We want to make sure we report
!      the parent in any case, so a join can not be used since the absence
!      of a child would suppress the parent.

let #line=0

BEGIN-SELECT

B.ANNUAL_RT
  do Format-Number(&B.Annual_Rt,$out,'999,999,999,999,999.999')
  print $out                   (+#line,85)
  let #line=1
B.EE_PCT_UNDER_YMPE
  do Format-Number(&B.EE_Pct_Under_YMPE,$out,'999.999')
  print $out                   (0,114)
B.ER_PCT_UNDER_YMPE
  do Format-Number(&B.ER_Pct_Under_YMPE,$out,'999.999')
  print $out                   (0,125)
B.EE_PCT_OVER_YMPE
  do Format-Number(&B.EE_Pct_Over_YMPE,$out,'999.999')
  print $out                   (0,137)
B.ER_PCT_OVER_YMPE
  do Format-Number(&B.ER_Pct_Over_YMPE,$out,'999.999')
  print $out                   (0,148)

FROM  PS_PENSION_RTE_TBL B
WHERE B.PLAN_TYPE    = &A.Plan_Type
  and B.BENEFIT_PLAN = &A.Benefit_Plan
  and B.EFFDT        = &A.Effdt
ORDER BY B.ANNUAL_RT

END-SELECT
END-PROCEDURE

!*********************************************************************
#Include 'getbennm.sqc'  !Get-Benefit-Name procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
#Include 'stdapi.sqc'    !Update Process API
#Include 'useprntr.sqc'  !Select printer type
!*********************************************************************

