!***********************************************************************
!  FPA015:  Update Step Increment                                     *
!                                                                      *
!  Insert into FPAEECAREER table from FPAEECAREERBIS table             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/07/19:12:26:38                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'           ! Set environment
#include 'setup01.sqc'          ! Printer and page-size initialization

BEGIN-REPORT

Do Init-DateTime
Do Init-Number

    do Get-Current-DateTime()
    display  'AsOfToday :' noline
    display  $AsOfToday

do Init-Report
do Proc-Main
do Stdapi-Term

END-REPORT



!***************************************************************
!
!
!       Init-Report
!
!
!***************************************************************
BEGIN-PROCEDURE  Init-Report

do Stdapi-Init
do Security-Param       !SQR security

if $prcs_process_instance = ''
    INPUT $AsOfDate 'Put the Promotion Simulation date'
else
    do Select-Parameters
end-if

END-PROCEDURE Init-Report



!***************************************************************
!
!
!       Get-Values
!
!
!***************************************************************
BEGIN-PROCEDURE  Get-Values

    do Get-As-Of-Date

END-PROCEDURE Get-Values



!***************************************************************
!
!
!       Proc-Main
!
!
!***************************************************************
BEGIN-PROCEDURE Proc-Main

do Select-System
do Select-Popul               ! Impacted Civil Servants selected

END-PROCEDURE Proc-Main



!*******************************************************************************************************
!
!       Function :      Select-System
!
!
!       Description :   Select default action for the Automatic Increment from PS_FPMSYSTEM_TBL table
!                       and select values about Individual Orders Editing from PS_FPMACTION_TBL table
!
!*******************************************************************************************************
BEGIN-PROCEDURE Select-System

display 'Select-System debut'

BEGIN-SELECT

A.FP_EDITIDVDEC &EditIndiv
A.FP_EDITCOLDEC &EditCollec
B.FP_COLDECINCR &DerNumNotif
B.FP_REFCOL_PS  &RefArretCol
B.FP_ACTN_STEP  &ActCarriere

        #IFDEF DEBUGi
        display 'EditIndiv: ' noline
        display &EditIndiv
        display 'EditCollec: ' noline
        display &EditCollec
        display 'DerNumNotif: ' noline
        display &DerNumNotif
        display 'RefArretCol: ' noline
        display &RefArretCol
        display 'ActCarriere: ' noline
        display &ActCarriere
        #ENDIF

from    PS_FPMACTION_TBL A, PS_FPMSYSTEM_TBL B

where   A.ACTION = B.FP_ACTN_STEP
and     A.EFF_STATUS = 'A'
and     A.EFFDT =
            (select max(EFFDT)
            from    PS_FPMACTION_TBL
            where   ACTION = B.FP_ACTN_STEP
            and     EFFDT <= {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX})

END-SELECT

let #DerNumNotif = &DerNumNotif
let #RefArretCol = &RefArretCol

If &EditIndiv = 'Y' or  &EditCollec = 'Y'
    let #DerNumNotif = #DerNumNotif + 1
    let $DerNumNotif = to_char(#DerNumNotif)
    Let $DerNumNotif = Lpad($DerNumNotif,8,'0')
Else
    Let $DerNumNotif = ' '
End-if

If  &EditCollec = 'Y'
    let #RefArretCol = #RefArretCol + 1
End-if

!System Table Update
!-------------------
If #DerNumNotif != &DerNumNotif  OR   #RefArretCol != &RefArretCol

BEGIN-SQL

update PS_FPMSYSTEM_TBL
set
FP_COLDECINCR   =   #DerNumNotif,
FP_REFCOL_PS    =   #RefArretCol,
LAST_UPDATE_DATE =  {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX}

END-SQL

End-if

display 'Select-System fin'

END-PROCEDURE Select-System



!***************************************************************
!
!       Function :      Select-Popul
!
!       Description :   Impacted Civil Servants selected
!
!***************************************************************
BEGIN-PROCEDURE Select-Popul

display 'Select-Popul begin'

BEGIN-SELECT

EMPLID          &Agent-id
EMPL_RCD        &Dossier
{DATEOUT-PREFIX}EFFDT{DATEOUT-SUFFIX}  &Date-avancement
EFFSEQ          &Sequence

    ! Init Variables
    !---------------
    let $Emplid = rtrim(&Agent-id,' ')
    let #Empl_rcd = &Dossier
    Do Convert-To-DTU-Date(&Date-avancement,$Effdt_dtu)             ! SQR format
    let $Effdt_nat = &Date-avancement                               ! Native format

    #IFDEF DEBUGi
    display 'Emplid:' noline
    display $Emplid
    #ENDIF

    do Maj-Carriere                                                 ! CAREER Update to close the previous occurence

    do Insert-Fpaeeupdrow($Emplid,#Empl_rcd,$Effdt_nat,$AsOfToday)  ! FPAEEUPDROW Update
    display 'FPAEEUPDROW update'

    do Insert-Carriere                                              ! Copy from FPAEECAREERBIS into FPAEECAREER

    If &EditIndiv = 'Y' or  &EditCollec = 'Y'
        do Insert-Notif                                             ! Row insert into FPAEENOTIF
    end-if

from    PS_FPAEECAREERBIS
where   LAST_UPDATE_DATE = {DATEIN-PREFIX}$AsOfDate{DATEIN-SUFFIX}

END-SELECT

display 'Select-Popul end'

END-PROCEDURE Select-Popul



!***************************************************************
!
!
!   Maj-Carriere : update previous step datas :
!       Update Career Instance Terminated (FP_CLD_INST = 'Y')
!       Update (Sub)Step Actual End Date
!
!
!***************************************************************

BEGIN-PROCEDURE Maj-Carriere

display 'Maj-Carriere debut'

do Substr-Date($Effdt_dtu,0,0,1,$Date-fin-reelle)
do convert-from-DTU-Date($Date-fin-reelle,$Date-fin-reelle-nat)

#IFDEF DEBUGi
display  'Actual End Date: ' noline
display  $Date-fin-reelle
#ENDIF

! Select Max Sequence from Max Effdt found
!------------------------------------------
BEGIN-SELECT
A.EFFDT         &MaxDtEffet
A.EFFSEQ        &MaxSequence

   Do Convert-To-DTU-Date(&MaxDtEffet,$MaxDtEffet_dtu)           ! SQR format
   let $MaxDtEffet_nat = &MaxDtEffet                             ! Native format
   let #MaxSequence = &MaxSequence

from PS_FPAEECAREER A,
     PS_FPFAST_PERS_VW2 SCRTY       !SQR security
where A.EMPLID = $Emplid
and A.EMPL_RCD = #Empl_rcd

and A.EFFDT = (select max(A1.EFFDT)
               from PS_FPAEECAREER A1
               where A1.EMPLID = $Emplid
                 and A1.EMPL_RCD = #Empl_rcd
                 and A1.EFFDT <= {DATEIN-PREFIX}$Effdt_nat{DATEIN-SUFFIX})

and A.EFFSEQ = (select max (A2.EFFSEQ)
                from PS_FPAEECAREER A2
                where A2.EMPLID = $Emplid
                  and A2.EMPL_RCD = #Empl_rcd
                  and A2.EFFDT = A.EFFDT)

and A.EMPLID = SCRTY.EMPLID       !
and A.EMPL_RCD = SCRTY.EMPL_RCD !SQR security
[$SecurityClause]               !

END-SELECT


let $sql-statement = 'FPA015.SQR,Maj-Carriere,Update,PS_FPAEECAREER'
BEGIN-SQL On-Error=SQL-Error

update  PS_FPAEECAREER

set     FP_CLD_INST = 'Y',
        FP_CLD_STEP = 'Y',
        FP_STEP_END_DT = {DATEIN-PREFIX}$Date-fin-reelle-nat{DATEIN-SUFFIX},   ! $Effdt_nat - 1 jour
        LAST_UPDATE_DATE = {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX}

where   EMPLID =    $Emplid
and     EMPL_RCD =  #Empl_rcd
and     EFFDT =     {DATEIN-PREFIX}$MaxDtEffet_nat{DATEIN-SUFFIX}
and     EFFSEQ =    #MaxSequence

END-SQL

display 'Maj-Carriere End'

END-PROCEDURE Maj-Carriere



!***************************************************************
!
!
! Insert-Carriere : Insert row into Career from Career Bis
!
!
!***************************************************************

BEGIN-PROCEDURE Insert-Carriere

display 'Insert-Carriere debut'

If $MaxDtEffet_dtu = $Effdt_dtu
    let #MaxSequence = #MaxSequence + 1
end-if


! Retrieve fields belonging to FPAEECAREER
!-----------------------------------------
do gnr-fieldnames-career


!Copy row from PS_FPAEECAREER to PS_FPAEECAREERBIS
!--------------------------------------------------------------------------------
let $sql-statement = 'FPA015.SQR, Insert-Carriere ,Insert,PS_FPAEECAREER'
BEGIN-SQL On-Error=SQL-Error

INSERT INTO PS_FPAEECAREER ([$field_list])

SELECT [$field_list] from PS_FPAEECAREERBIS B

where B.EMPLID =    $Emplid
and   B.EMPL_RCD =  #Empl_rcd
and   B.EFFDT =     {DATEIN-PREFIX}$Effdt_nat{DATEIN-SUFFIX}
and   B.EFFSEQ =    &Sequence

END-SQL


! Some fields have to be updated
!--------------------------------
let $sql-statement = 'FPA015.SQR,Insert-Carriere ,Update,PS_FPAEECAREER'
BEGIN-SQL On-Error=SQL-Error

update  PS_FPAEECAREER

set     FP_INST_STAT =      'R',
        FP_NOTIF_REF =      $DerNumNotif,
        LAST_UPDATE_DATE =  {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX}

where   EMPLID =    $Emplid
and     EMPL_RCD =  #Empl_rcd
and     EFFDT =     {DATEIN-PREFIX}$Effdt_nat{DATEIN-SUFFIX}
and   EFFSEQ = &Sequence

END-SQL


! Delete in PS_FPAEECAREERBIS of the copied row
!-----------------------------------------------------
let $sql-statement = 'FPA015.SQR, Insert-Carriere ,Delete,PS_FPAEECAREERBIS'
BEGIN-SQL On-Error=SQL-Error

delete from PS_FPAEECAREERBIS
where EMPLID = $Emplid
and   EMPL_RCD = #Empl_rcd
and   EFFDT = {DATEIN-PREFIX}$Effdt_nat{DATEIN-SUFFIX}
and   EFFSEQ = &Sequence

END-SQL

display 'Insert-Carriere end'

END-PROCEDURE Insert-Carriere



!***************************************************************
!
!
! Insert-Notif
!
!
!***************************************************************

BEGIN-PROCEDURE Insert-Notif

display 'Insert-Notif '
display $Emplid
display #DerNumNotif
display &EditIndiv
display &EditCollec
display #RefArretCol
display &ActCarriere
display $AsOfToday

let $sql-statement = 'FPA015.SQR,Insert-Notif ,Insert,PS_FPAEENOTICE'
BEGIN-SQL On-Error=SQL-Error

INSERT
INTO PS_FPAEENOTICE
        (EMPLID,
        EMPL_RCD,
        FP_NOTIF_REF,
        FP_RETURN_R,
        FP_TBL_CD,
        FP_EDITIDVDEC,
        FP_EDITCOLDEC,
        FP_IDV_ON_REQ,
        FP_COL_ON_REQ,
        FP_IDVEDIT_DT,
        FP_COLEDIT_DT,
        FP_COLDEC_NBR,
        FP_REFCOL_PS,
        FP_DEC_ABROG,
        FP_DEC_ABROG_DT,
        FP_EDIT_ABROG,
        ACTION,
        FP_EDT_TMP,
        FP_EDT_FNL,
        FP_IDVDEC_NBR,
        LAST_UPDATE_DATE)
VALUES
        ($Emplid ,                      ! EMPLID,
        #Empl_rcd,                      ! EMPL_RCD,
        $DerNumNotif,                   ! FP_NOTIF_REF,
        ' ',                            ! FP_RETURN_R,
        'C',                            ! FP_TBL_CD,
        &EditIndiv,                     ! FP_EDITIDVDEC,
        &EditCollec,                    ! FP_EDITCOLDEC,
        'N',                            ! FP_IDV_ON_REQ,
        'N',                            ! FP_COL_ON_REQ,
        null,                           ! FP_IDVEDIT_DT,
        null,                           ! FP_COLEDIT_DT,
        '0',                            ! FP_COLDEC_NBR,
        #RefArretCol,                   ! FP_REFCOL_PS,
        'N',                            ! FP_DEC_ABROG,
        null,                           ! FP_DEC_ABROG_DT,
        'N',                            ! FP_EDIT_ABROG,
        &ActCarriere,                   ! ACTION,
        'N',                            ! FP_EDT_TMP,
        'N',                            ! FP_EDT_FNL,
        ' ',                            ! FP_IDVDEC_NBR,
        {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX})   ! LAST_UPDATE_DATE

END-SQL

display 'Insert-Notif end'

END-PROCEDURE Insert-Notif



!*************************************************************
!
!       Function :      gnr-fieldnames-career
!
!*************************************************************

BEGIN-PROCEDURE gnr-fieldnames-career

let $_gnr-field-line-suffix = ','
let $_gnr-field-line-prefix = ''

let $_field_list = $_gnr-field-line-prefix
do list-record-career

END-PROCEDURE gnr-fieldnames-career



!*************************************************************
!
!       Function :      list-record-career
!
!       Description :   retrieve fields belonging to FPAEECAREER.
!                       Fields belonging to subrecords included in FPAEECAREER are retrieved as well.
!
!*************************************************************

BEGIN-PROCEDURE list-record-career

do list-fields('FPAEECAREER')

BEGIN-SELECT

FIELDNAME &subrecord
    let $recname = &subrecord
    do list-fields($recname)

from    PSRECFIELD

where   RECNAME =   'FPAEECAREER'
and     SUBRECORD = 'Y'

order by FIELDNAME

END-SELECT

END-PROCEDURE list-record-career



!********************************************************************
!
!       Function :      list-fields
!
!       Description :   retrieve fields belonging to a given record
!
!********************************************************************

BEGIN-PROCEDURE list-fields($recname)

BEGIN-SELECT

RF2.FIELDNAME            &RF2.FIELDNAME
DF.FIELDTYPE             &DF.FIELDTYPE

    if &DF.FIELDTYPE <> 1

        let $gnr-fieldname =  rtrim(&RF2.FIELDNAME,' ')

        let $_field_list = $_field_list || $_gnr-field-line-prefix || $gnr-fieldname
        let $_gnr-field-line-prefix = ','

    end-if

from PSRECFIELD RF2,
PSDBFIELD DF
where DF.FIELDNAME  = RF2.FIELDNAME
and   RF2.RECNAME = $recname
and   RF2.SUBRECORD  = 'N'
order by RF2.FIELDNAME

END-SELECT
END-PROCEDURE



#Include 'fpparam.sqc'          ! Get parameter values in FPM record (FPS)
#Include 'fptestdt.sqc'       ! Routines for date and time formatting (FPS)
#Include 'curdttim.sqc'         ! Get-Current-DateTime procedure
#Include 'stdapi.sqc'           ! Routines to update run status
#Include 'hrrnctl1.sqc'         ! Get run control parameter values
#Include 'hrgetval.sqc'         ! Get values mask routines
#Include 'number.sqc'           ! Routines to format numbers
#Include 'fpjob.sqc'            ! Insert-Fpaeeupdrow procedure (FPS)
#Include 'fpsalary.sqc'         ! Routines for salary (FPS)
#Include 'hrsecty.sqc'          ! Get SQR Security parameters
