!***********************************************************************
!                                                                      *
!  FPA316sqc :  Seniority Calculator for one EMPLID                    *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!                                                                      
!          $Date:  2006/07/19:12:41:15                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!***************************************************************
!
!
!       FPA316-Main
!
!
!***************************************************************


BEGIN-PROCEDURE FPA316-Main

let $sql-statement = 'FPA316.SQR,,Select,Proc-Main'

        do Get-Cal-Thirtieth($Cal-Thirtieth,$Termdt-cd)    !Calendar or Thirtieth
        do Va-Reinit

        #IFDEF DEBUGI
        display 'start date'
        display $FromDate


        display 'thru date'
        display $ThruDate


        display 'FromDate_dtu'
        display $FromDate_dtu


        display '$ThruDate_dtu'
        display $ThruDate_dtu

        display $Emplid
        display $Empl_rcd
        display #empl_rcd
        #ENDIF

do Search-Pos-Stat1
do Search-Pos-Stat2
do DELETE-FPASENDUR-CALC
do Insert-FPASENDUR_CALC


END-PROCEDURE FPA316-Main



!***************************************************************
!
!
!       Va-Reinit
!       Reinit variables for each employee
!
!
!***************************************************************
BEGIN-PROCEDURE Va-Reinit

    DO Convert-To-DTU-Date ($ThruDate,$Thrudate_dtu)
    DO Convert-To-DTU-Date ($FromDate,$FromDate_dtu)

    !Number of days End of civil service position
    !---------------------------------------------
    IF $Cal-Thirtieth ='T'  !Thirtieth
        let #Days-Number=30
    else                    !Calendar
        Do Days-Number($FromDate_dtu,#Days-Number)
    end-if


    let #Anc-Dy-Rel = 0                         !Actual seniority
    let #Anc-Mn-Rel = 0
    let #Anc-An-Rel = 0

    let #Dur-Dy-Rel = 0                         !Actual Service Length
    let #Dur-Mn-Rel = 0
    let #Dur-An-Rel = 0


END-PROCEDURE  Va-Reinit


!*******************************************************************************
!
!       Search-Pos-Stat1 : select the last of the civil service position which
!                          is linked to the previous step, when the end date of
!                          this last civil service position is greater than the
!                          current step begin date.
!
!********************************************************************************

BEGIN-PROCEDURE Search-Pos-Stat1

#IFDEF DEBUGI
display 'FPA316 / search-pos-stat1 start'
#ENDIF

!Fetch the last civil service position of the start date
!----------------------------------------------------------

let $sql-statement = 'FPA316.SQR,,Select,Search-Pos-Stat1'
BEGIN-SELECT On-Error=SQL-Error

{DATEOUT-PREFIX}A.EFFDT{DATEOUT-SUFFIX} &Posit-Beg1
{DATEOUT-PREFIX}A.FP_FOREND_DT{DATEOUT-SUFFIX} &ForEnd_Dt1
{DATEOUT-PREFIX}A.FP_END_DT{DATEOUT-SUFFIX} &Posit-End1
B.FP_PROM_STEP      &AutoProm1
B.FP_CAREFRZPCT     &Coeff1
B.FP_DURATION_PCT   &Coeff_dur1


        let $Posit-Beg = $FromDate_dtu
        do Convert-To-DTU-Date(&ForEnd_Dt1,$ForEnd_Dt)
        do Convert-To-DTU-Date(&Posit-End1,$Posit-End)
        let $AutoProm = rtrim(&AutoProm1,' ')
        let #Coeff = &Coeff1
        let #Coeff_dur = &Coeff_dur1

        display '#Coeff'
        display #Coeff
        display '#Coeff_dur'
        display #Coeff_dur

        do CalcAnc-Posit-End       !Calculation of the civil service position end-date
        Display '$FromDate_dtu' noline
        Display $FromDate_dtu
        Display '$Posit-End ' noline
        Display $Posit-End
        Display '$StepEnd_dt' noline
        Display $StepEnd_dt
        display $Posit-Beg

        If $Posit-End >= $FromDate_dtu
                do Calc-Anc
        end-if

from  PS_FPAEELEGALSTAT A, PS_FPMLEGALSTA_TBL B
     ,PS_FPFAST_PERS_VW2 SCRTY       !SQR security

where A.EMPLID = $Emplid
and   A.EMPL_RCD = #Empl_rcd
and   A.FP_INST_STAT = 'R'
and   A.EFFDT  =
        (select max(EFFDT)
         from  PS_FPAEELEGALSTAT
         where EMPLID = $Emplid
         and   FP_INST_STAT = 'R'
         and   EMPL_RCD = #Empl_rcd
         and   EFFDT < {DATEIN-PREFIX}$FromDate{DATEIN-SUFFIX})
and   A.FP_LEGALSTAT_CD  = B.FP_LEGALSTAT_CD
and   B.EFF_STATUS    = 'A'
and   B.EFFDT =
        (select max(EFFDT)
         from  PS_FPMLEGALSTA_TBL
         where FP_LEGALSTAT_CD = A.FP_LEGALSTAT_CD
         and   EFFDT <= A.EFFDT)

and A.EMPLID = SCRTY.EMPLID     !
and A.EMPL_RCD = SCRTY.EMPL_RCD !SQR security
[$SecurityClause]               !

END-SELECT

#IFDEF DEBUGI
display 'FPA316.SQC / END-PROCEDURE Search-Pos-Stat1 end'
#ENDIF

END-PROCEDURE  Search-Pos-Stat1



BEGIN-PROCEDURE Search-Pos-Stat2

display 'FPA316.SQC / search-pos-stat2 start'

let $sql-statement = 'FPA316SQR,,Select,Search-Pos-Stat2'
BEGIN-SELECT On-Error=SQL-Error

{DATEOUT-PREFIX}W.EFFDT{DATEOUT-SUFFIX} &Posit-Beg2
{DATEOUT-PREFIX}W.FP_FOREND_DT{DATEOUT-SUFFIX} &ForEnd_Dt2
{DATEOUT-PREFIX}W.FP_END_DT{DATEOUT-SUFFIX} &Posit-End2
X.FP_PROM_STEP      &AutoProm2
X.FP_CAREFRZPCT     &Coeff2
X.FP_DURATION_PCT   &Coeff_dur2

        do Convert-To-DTU-Date(&Posit-Beg2,$Posit-Beg)
        do Convert-To-DTU-Date(&ForEnd_Dt2,$ForEnd_Dt)
        do Convert-To-DTU-Date(&Posit-End2,$Posit-End)
        let $AutoProm = rtrim(&AutoProm2,' ')
        let #Coeff = &Coeff2
        let #Coeff_dur = &Coeff_dur2

        #IFDEF DEBUGI
        display '$Posit-Beg Search-Pos-Stat2'
        display '$Posit-Beg'
        display $Posit-Beg
        display '#Coeff'
        display #Coeff
        display '#Coeff_dur'
        display #Coeff_dur
        #ENDIF

        do CalcAnc-Posit-End         !Calculation of the civil service position end-date

        #IFDEF DEBUGI
        display '$Posit-End'
        display $Posit-End
        #ENDIF


        do Calc-Anc                  !Senirioty calculation

from  PS_FPAEELEGALSTAT W, PS_FPMLEGALSTA_TBL X
     ,PS_FPFAST_PERS_VW2 SCRTY       !SQR security

where W.EMPLID = $Emplid
and   W.EMPL_RCD = #Empl_rcd
and   W.EFFDT >= {DATEIN-PREFIX}$FromDate{DATEIN-SUFFIX}
and   W.EFFDT < {DATEIN-PREFIX}$ThruDate{DATEIN-SUFFIX}
and   W.FP_INST_STAT = 'R'
and   W.FP_LEGALSTAT_CD  = X.FP_LEGALSTAT_CD
and   X.EFF_STATUS    = 'A'
and   X.EFFDT =
        (select max(EFFDT)
         from  PS_FPMLEGALSTA_TBL
         where FP_LEGALSTAT_CD = W.FP_LEGALSTAT_CD
         and   EFFDT <= W.EFFDT)

and W.EMPLID = SCRTY.EMPLID     !
and W.EMPL_RCD = SCRTY.EMPL_RCD !SQR security
[$SecurityClause]               !

order by W.EFFDT

END-SELECT

display 'FPA316.SQC / Search-Pos-Stat2 end'
END-PROCEDURE  Search-Pos-Stat2



!***************************************************************
!
!    Calc-Anc  : iterative calculation of actual seniority and
!                career seniority
!
!***************************************************************

BEGIN-PROCEDURE Calc-Anc

        #IFDEF DEBUGI
        display 'Calc-Anc Debut'
        Display '$Calcul-Anc-Re' noline
        Display $Calcul-Anc-Re
        #ENDIF

        !Actual seniority calculation
        !--------------------------------------------------------------
        !If $Calcul-Anc-Re = 'Y'
                !if $Posit-End > $EndCar
                if $Posit-End > $ThruDate_dtu
                        let $Posit-End = $ThruDate_dtu  !$EndCar
                        !let $Calcul-Anc-Re='N'
                end-if
                do Calc-Anc-Re           !Actual seniority calculation
        !end-if

        #IFDEF DEBUGI
        Display '$Calcul-Anc-Car' noline
        Display $Calcul-Anc-Car
        display  'FPA316.SQC / Calc-Anc end :'
        #ENDIF


END-PROCEDURE Calc-Anc


!**************************************************************************************
!
!    Calc-Anc-Re : Iterative calculation of actual seniority and actual service length
!
!**************************************************************************************

BEGIN-PROCEDURE Calc-Anc-Re

#IFDEF DEBUGI
display 'Calc-Anc-Re start:'
display 'Posit-Beg ' noline
display $Posit-Beg
display 'Posit-End ' noline
display $Posit-End
display 'Coeff ' noline
display #Coeff
display 'Coeff_dur' noline
display #Coeff_dur
#ENDIF

!Number of days End of civil service position
!---------------------------------------------
IF $Cal-Thirtieth ='T'  !Thirtieth
        let #Days-Number=30
else                    !Calendar
        Do Days-Number($Posit-End,#Days-Number )
end-if


!Calculation of the civil service position duration
!--------------------------------------------------
do FP-Calc-Dur($Cal-Thirtieth,$Posit-Beg,$Posit-End,#A,#M,#J)
display 'diff entre debut et fin en A/M/J'
display #A
display #M
display #J

!Total seniority with % advancement career (actual seniority)
!-------------------------------------------------------------
let #Anc-Dy-Rel = #Anc-Dy-Rel + #J*(#Coeff/100)
let #Anc-Mn-Rel = #Anc-Mn-Rel + #M*(#Coeff/100)
let #Anc-An-Rel = #Anc-An-Rel + #A*(#Coeff/100)


!Total service length with % advancement career (actual service length)
!-----------------------------------------------------------------------
let #Dur-Dy-Rel = #Dur-Dy-Rel + #J*(#Coeff_dur/100)
let #Dur-Mn-Rel = #Dur-Mn-Rel + #M*(#Coeff_dur/100)
let #Dur-An-Rel = #Dur-An-Rel + #A*(#Coeff_dur/100)



!Needed adjustement due to %
!---------------------------------------------------
let #AA = #Anc-An-Rel - trunc(#Anc-An-Rel,0)
let #Anc-An-Rel = #Anc-An-Rel - #AA
let #Anc-Mn-Rel = #Anc-Mn-Rel + (#AA * 12)

let #AM = #Anc-Mn-Rel - trunc(#Anc-Mn-Rel,0)
let #Anc-Mn-Rel = #Anc-Mn-Rel - #AM
let #Anc-Dy-Rel = #Anc-Dy-Rel + (#AM * 30)
!let #Anc-Dy-Rel = #Anc-Dy-Rel + (#AM * #Days-Number)

let #Anc-Dy-Rel = round(#Anc-Dy-Rel,0)

While #Anc-Dy-Rel >= #Days-Number
        let #Anc-Mn-Rel = #Anc-Mn-Rel + 1
        let #Anc-Dy-Rel = #Anc-Dy-Rel - #Days-Number
End-While

While #Anc-Mn-Rel >= 12
        let #Anc-An-Rel = #Anc-An-Rel + 1
        let #Anc-Mn-Rel = #Anc-Mn-Rel - 12
End-While

#IFDEF DEBUGI
display 'service length'
display #Dur-An-Rel
display #Dur-Mn-Rel
display #Dur-Dy-Rel
#ENDIF


!Needed adjustement due to %
!---------------------------
let #DA = #Dur-An-Rel - trunc(#Dur-An-Rel,0)
let #Dur-An-Rel = #Dur-An-Rel - #DA
let #Dur-Mn-Rel = #Dur-Mn-Rel + (#DA * 12)

let #DM = #Dur-Mn-Rel - trunc(#Dur-Mn-Rel,0)
let #Dur-Mn-Rel = #Dur-Mn-Rel - #DM
let #Dur-Dy-Rel = #Dur-Dy-Rel + (#DM * 30)


let #Dur-Dy-Rel = round(#Dur-Dy-Rel,0)

While #Dur-Dy-Rel >= #Days-Number
        let #Dur-Mn-Rel = #Dur-Mn-Rel + 1
        let #Dur-Dy-Rel = #Dur-Dy-Rel - #Days-Number
End-While

While #Dur-Mn-Rel >= 12
        let #Dur-An-Rel = #Dur-An-Rel + 1
        let #Dur-Mn-Rel = #Dur-Mn-Rel - 12
End-While



#IFDEF DEBUGI
display 'Seniority'
display #Anc-An-Rel
display #Anc-Mn-Rel
display #Anc-Dy-Rel

display 'FPA316.SQC / Calc-Anc-Rel end:'
#ENDIF

END-PROCEDURE Calc-Anc-Re



!***********************************************************************************
!
!   Insert-FPACareer :  Insert row into PS_FPASENDUR_CALC record
!
!***********************************************************************************
BEGIN-PROCEDURE Insert-FPASENDUR_CALC

let $sql-statement = 'FPA316.SQC, Insert-PS_FPASENDUR_CALC ,Insert,PS_FPASENDUR_CALC'
BEGIN-SQL On-Error=SQL-Error

insert into PS_FPASENDUR_CALC (EMPLID, EMPL_RCD, START_DATE, END_DATE, FP_TRUESEN_DY, FP_TRUESEN_MN, FP_TRUESEN_YR, FP_DURSTEP_DY, FP_DURSTEP_MN, FP_DURSTEP_YR)
values( $Emplid,
        #Empl_rcd,
        {DATEIN-PREFIX}$FromDate{DATEIN-SUFFIX},
        {DATEIN-PREFIX}$FromDate{DATEIN-SUFFIX},
        #Anc-Dy-Rel,
        #Anc-Mn-Rel,
        #Anc-An-Rel,
        #Dur-Dy-Rel,
        #Dur-Mn-Rel,
        #Dur-An-Rel)

END-SQL

#IFDEF DEBUGI
Display 'FPA316.SQC / Insert-FPASENDUR_CALC end'
#ENDIF
END-PROCEDURE Insert-FPASENDUR_CALC



!-------------------------------------------
! Procedure DELETE-FPASENDUR-CALC
!-------------------------------------------
! delete FPASENDUR_CALC
! In  :
! Out :
!-------------------------------------------
begin-procedure DELETE-FPASENDUR-CALC

let $sql-statement = 'FPA316.SQC,DELETE_FPASENDUR_CALC,Delete,PS_FPASENDUR_CALC'
Begin-SQL On-Error=SQL-Error
Delete From PS_FPASENDUR_CALC
end-SQL
#IFDEF DEBUGI
Display 'FPA316.SQC / DELETE-FPASENDUR-CALC end'
#ENDIF

END-PROCEDURE DELETE-FPASENDUR-CALC




!***********************************************************************
!
!   CalcAnc-Posit-End: determine the thru date of position
!
!***********************************************************************

BEGIN-PROCEDURE CalcAnc-Posit-End

if $Posit-End = ''
        Let $Posit-End = $ThruDate_dtu
end-if

END-PROCEDURE CalcAnc-Posit-End

#IFNDEF FPJOB
    !first call of fpjob.sqc
    #Include 'fpjob.sqc'      ! Insert-Fpaeeupdrow procedure
#ENDIF

#IFNDEF FPSALARY
    !first call of fpsalary.sqc
    #include 'fpsalary.sqc'   ! Routines for salary
#ENDIF

#Include 'fptestdt.sqc'     ! Routines for date and time formatting
#Include 'fpparam.sqc'        ! Routines for  Parameters Tables    FPS
#include 'curdttim.sqc'       ! Get-Current-DateTime procedure
#Include 'number.sqc'         ! Routines to format numbers

