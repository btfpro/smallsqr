!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:37:24                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
! -----------------------------------------------------------------------------
! Report ID:     PAY050.SQR
!
! Description:   Prints the Tips Establishment Post information.
!
! Purpose:       For audit and/or control purposes.
!
! Created:       26-Aug-1997
!
! Run Frequency: On request.
!
! Input:         PeopleSoft database & user parameters.
!
! Output:
!   ¯  Report - Audit/Control Report (PAY050.LIS)
!
! NOTES:
! ------
!   (1)  Specifications based on Tips Project (summer 1997)
!
! ---------------------------------------------------------------------
!                      <<<  MAINTENANCE LOG  >>>
! ---------------------------------------------------------------------
! |   WHEN    |  WHO  |  Maintenance performed?
! ---------------------------------------------------------------------
! | dd-mmm-yy |  ???  |
! |           |       |
! ---------------------------------------------------------------------
! \/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/
! /\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\/\

#Include 'setenv.sqc'                  ! Set environment

#Include 'setup32.sqc'                 ! Printer Controls

BEGIN-REPORT                           ! SQR BEGIN Statement

  DO INIT-REPORT                       ! Start the SQR
  DO STDAPI-TERM                       ! End the SQR

END-REPORT                             ! SQR END Statement

! *******************************************************************
! ** Section Begin:  DEFINES <Standard SQR>
! *******************************************************************

#DEFINE  COL01 001     ! Business Unit
#DEFINE  COL1A 003     ! Business Unit Descr
#DEFINE  COL02 011     ! Tips Establishment
#DEFINE  COL2A 032     ! Tips Establishment Descr
#DEFINE  COL03 062     ! Gross Receipts
#DEFINE  COL04 075     ! Gross Tips
#DEFINE  COL05 088     ! Gross Indirect Tips
#DEFINE  COL06 101     ! Allocation Base
#DEFINE  COL07 114     ! Direct Tipped Allocation Base
#DEFINE  COL08 127     ! Total Shortfall
#DEFINE  COL09 140     ! Total Allcoation
#DEFINE  COL10 153     ! YTD Tippable Hours
#DEFINE  COL11 166     ! Employee Count

! *******************************************************************
! ** Section End:  DEFINES <Standard SQR>
! *******************************************************************

! *******************************************************************
! ** Section Begin:  BEGIN-HEADING  <STANDARD SQR>
! *******************************************************************

BEGIN-HEADING   8                        ! Begin Headings

#INCLUDE 'stdhdg02.sqc'                  ! Standard Heading #02

  PRINT 'Post Date:'                (+1,{COL01},)
  PRINT $POST_DATE                  (,+1,)

  PRINT 'Bus'                       (+2,{COL01},)
  PRINT 'Tips'                      (,{COL02},)
  PRINT 'Gross'                     (,{COL03},)
  PRINT 'Gross'                     (,{COL04},)
  PRINT 'Gross'                     (,{COL05},)
  PRINT 'Allocation'                (,{COL06},)
  PRINT 'Direct Tip'                (,{COL07},)
  PRINT 'Total'                     (,{COL08},)
  PRINT 'Total'                     (,{COL09},)
  PRINT 'YTD'                       (,{COL10},)
  PRINT 'Employee'                  (,{COL11},)

  PRINT 'Unit'                      (+1,{COL01},)
!  PRINT 'Descr'                     (,{COL1A},)
  PRINT 'Establishment'             (,{COL02},)
  PRINT 'Descr'                     (,{COL2A},)
  PRINT 'Receipts'                  (,{COL03},)
  PRINT 'Dir. Tips'                 (,{COL04},)
  PRINT 'Ind. Tips'                 (,{COL05},)
  PRINT 'Base'                      (,{COL06},)
  PRINT 'Alloc Base'                (,{COL07},)
  PRINT 'Shortfall'                 (,{COL08},)
  PRINT 'Allocation'                (,{COL09},)
  PRINT 'Tip Hours'                 (,{COL10},)
  PRINT 'Count'                     (,{COL11},)

  PRINT '----'                      (+1,{COL01},)
!  PRINT '----------------'          (,{COL1A},)
  PRINT '--------------------'      (,{COL02},)
  PRINT '----------------'          (,{COL2A},)
  PRINT '------------'              (,{COL03},)
  PRINT '------------'              (,{COL04},)
  PRINT '------------'              (,{COL05},)
  PRINT '------------'              (,{COL06},)
  PRINT '------------'              (,{COL07},)
  PRINT '------------'              (,{COL08},)
  PRINT '------------'              (,{COL09},)
  PRINT '------------'              (,{COL10},)
  PRINT '--------'                  (,{COL11},)

END-HEADING     !  End Headings

! *******************************************************************
! ** Section End:  BEGIN-HEADING  <STANDARD SQR>
! *******************************************************************


! *******************************************************************
! ** Paragraph Begin:  INIT-REPORT
!    Called by the SQR-required section [BEGIN-REPORT]
! *******************************************************************

BEGIN-PROCEDURE INIT-REPORT            ! Main Procedure Section

  MOVE 'PAY050'                        TO $REPORTID
  MOVE 'Allocation by Establishment Report' TO $REPORTTITLE

  DO GET-CURRENT-DATETIME

  DISPLAY $REPORTID
  DISPLAY $REPORTTITLE

  DISPLAY ' '

  DISPLAY 'CURRENT DATE : ' NOLINE
  DISPLAY $ASOFTODAY

  DISPLAY 'CURRENT TIME : ' NOLINE
  DISPLAY $ASOFNOW

  DISPLAY ' '

  DO INIT-DATETIME                     ! Perform Date/Time Proc

  DO INIT-NUMBER                       ! Initialize Number

  DO STDAPI-INIT                       ! Standard Initialize

  DO SELECT-PARAMETERS                 ! Standard Select Parameters

  DO REPORT                            ! MAIN PARAGRAPH

END-PROCEDURE                          ! End the Init Procedure

! *******************************************************************
! ** Paragraph End:  INIT-REPORT
! *******************************************************************


! *****************************************************************
! ** Paragraph Begin:  REPORT
!    This is the main paragraph of the SQR.
!    Common routine for version 6.01 of PeopleSoft Payroll.
! *******************************************************************

BEGIN-PROCEDURE REPORT

BEGIN-SELECT

TEP.BUSINESS_UNIT (+1,{COL01},) on-break print=always level=1 before=Bus-Unit-Change

TEP.TIPS_ESTAB    (,{COL02},)
  LET $COMPANY   =  &TEP.COMPANY      ! Load the lookup value
  LET $ESTAB     =  &TEP.TIPS_ESTAB   ! Load the lookup value
  LET $ASOFDATE  =  &TEP.POST_DT      ! Load the lookup date
  DO Get-Establishment-Name       ! Perform the procedure
  PRINT $ESTABDESCR (,{COL2A},28) ! Print retrieved name

TEP.COMPANY
TEP.POST_DT

  LET $ASOFDATE  =  $RC_TIP.POST_DT  ! Load the lookup date
  LET $COMPANY   =  &TEP.COMPANY     ! Load the lookup value
  DO GET-COMPANY-DATA

  DO FORMAT-DATETIME(&TEP.POST_DT, $OUT, {DEFDATE}, '', '')
  MOVE $OUT TO $POST_DATE

TEP.GRS_RCPTS             (,{COL03},)  EDIT 9,999,999.99
TEP.GRS_TIPS              (,{COL04},)  EDIT 9,999,999.99
TEP.GRS_IND_TIPS          (,{COL05},)  EDIT 9,999,999.99
TEP.ALLOCATION_BSE        (,{COL06},)  EDIT 9,999,999.99
TEP.DT_ALLOC_BSE          (,{COL07},)  EDIT 9,999,999.99
TEP.TOT_SHORTFALL         (,{COL08},)  EDIT 9,999,999.99
TEP.TOT_ALLOCATION        (,{COL09},)  EDIT 9,999,999.99
TEP.GRS_TIP_HRS           (,{COL10},)  EDIT 9,999,999.99
TEP.EMPL_CNT              (,{COL11},)  EDIT 99,999

FROM
      PS_TIPS_ESTAB_PD  TEP

WHERE
      TEP.POST_DT  =  $RC_TIP.POST_DT   ! Filter - Post Date
  AND TEP.COMPANY  =  $RC_TIP.COMPANY   ! Filter - Company

ORDER BY
      TEP.COMPANY,
      TEP.BUSINESS_UNIT,
      TEP.TIPS_ESTAB

END-SELECT

END-PROCEDURE   !  REPORT

! *******************************************************************
! ** Paragraph End:  REPORT
! *******************************************************************


! *******************************************************************
! ** Paragraph Begin:  BUS-UNIT-CHANGE
!    This is called whenever the Business Unit changes in the main
!    select in the REPORT paragraph via an on-break command.
! *******************************************************************

BEGIN-PROCEDURE BUS-UNIT-CHANGE

  LET $BU =  &TEP.BUSINESS_UNIT  ! Load the lookup value

  DO Get-Business-Unit-Name      ! Perform the procedure

  PRINT $BU_DESCR (+1,{COL1A},30)  ! Print retrieved name

END-PROCEDURE   !  BUS-UNIT-CHANGE

! *******************************************************************
! ** Paragraph End:  BUS-UNIT-CHANGE
! *******************************************************************


! *******************************************************************
! *  Include Section
! *******************************************************************
! *    This is where the SQC's are  included into the body of
! *    the program.
! *    NOTE:  All SQC's are called in lower-case.  For UNIX
! *           environment, make sure that the actual SQC files
! *           are stored in lower-case, or they will not be found.
! *******************************************************************

#Include 'stdapi.sqc'    ! Standard API Procedure
#Include 'tiprnctl.sqc'  ! Tips Run Control
#Include 'curdttim.sqc'  ! Get-Current-DateTime procedure
#Include 'datetime.sqc'  ! Routines for date and time formatting
#Include 'number.sqc'    ! Routines to format numbers
#Include 'readxlat.sqc'  ! Routine to read the Translate Table
#Include 'getcodta.sqc'  ! Routine to retrieve Company data

#Include 'getbunam.sqc'  ! Routine to retrieve the Business Unit Name
#Include 'getestnm.sqc'  ! Routine to retrieve the Establishment Name

