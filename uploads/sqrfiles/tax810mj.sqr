!***********************************************************************
!  TAX810MJ:  Qtrly Wage List - Tape - MICHIGAN 72-byte format         *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:42:56                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
! HP00001       Add $PublicSector indicator                            *
! HP00007       E&G 72-byte format                                     *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup31.sqc' !Printer and page-size initialization
#include 'usarpt.sqc'  ! NID_COUNTRY defined

begin-report
  Let $t1 = datenow()
  Show 'Report Started at : ' $t1
  do Init-Report

! These variables are used by rptsmmry.sqc
  Let $Prnt_CoTxGrs = 'N'
  Let $Prnt_CoExGrs = 'N'

  do Process-Main

  if $RecordWritten = 'Y'
        display ''
        display 'Upon successful conclusion of this program, one or more'
        display 'files will have been created, as follows:'
        display ''
        display '  TAX810MI  contains 72-character records in the format'
        display '            required for Web or FTP submission to the Michigan'
        display '            Employment Security Commission.'
        display ''
        display 'Follow instructions from the MESC for preparing and'
        display 'submitting the tape.'
        display ''

  else

        do Print-Summary-Data('NoData')

  end-if

  close 10

! do Delete-Work-File                                                   !HP00007

  do Stdapi-Term
  Let $t2 = datenow()
  Show 'Report Ended at : ' $t2

end-report

begin-procedure Init-Report
  move 'TAX810MJ' to $ReportID
  move 'Michigan Quarterly UI Wage Report - Tape' to $ReportTitle
  display ''
  display 'Creating Tape File for Michigan UI Wage Reporting'

  do Init-DateTime
  do Init-Number
  do Get-EandG                                                          !HP00001
  do Get-Current-DateTime
  do Get-Calendar-Year-Id
  do Stdapi-Init
  do Initialization

end-procedure

begin-procedure Initialization
#ifdef TAXTEST
  move 'CA'              to $State
#else
  move 'MI'              to $State
#endif

  do Get-Tax-Reporting-Run-Controls
  move &TX.Balance_Year to $RptYear 9999
  move &TX.Balance_Qtr  to $RptQtr 9
  move $PeriodEndDate   to $AsOfDate


! Use these routines if a run control page is setup for Michigan
! These routines will bring in the reporting medium.  If and when
! Michigan UI Tax reporting is combined into one program for the 72-byte format and 276-byte format
! and a file format needs to be selected.
!
!  if $PRCS_Process_Instance = ''
!     do Prompts
!  else
!     do Select-Parameters
!     do Convert-Parameters
!  end-if

  display ' '
  display 'Processing balances for '   noline
  display $RptQtrYr
  do Open-File

  do Delete-Work-File

end-procedure

begin-procedure Prompts

  while $FileType = ''
    input $FileType 'Tape, cartridge, Web or diskette file? (T, C, D or Q to quit)'
    uppercase $FileType
    if INSTR('TCDQ',$FileType,1) = 0
      display ' '
      display '***** Enter T, C, D or Q *****'
      display ' '
      move '' to $FileType
    end-if
  end-while
end-procedure

begin-procedure Convert-Parameters

  move $RC_QTR_UI.Reporting_Medium to $FileType

end-procedure

begin-procedure Open-File
if $FileType = 'D'
      move 0 to #RecordCount
      if #FileExtension <> 0
         close 10
      end-if

      add 1 to #FileExtension
      move #FileExtension to $FileExtension 099

      #ifdef OS400
         let $FileExtension = '(D' || $FileExtension || ')'
         let $FileID = '{IMPORTPREFIX}' || 'TAX810MI' || $FileExtension
      #else
         let $FileID = '{IMPORTPREFIX}' || 'TAX810MI.' || $FileExtension
      #endif

      open $FileID as 10 for-writing record=72:fixed
else
      open '{IMPORTPREFIX}TAX810MI{IMPORTSUFFIX}' as 10 for-writing record=72:fixed

end-if
end-procedure

begin-procedure Process-Main
begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
A.COMPANY
A.DESCR
A.ADDRESS1
A.CITY
A.STATE
A.POSTAL
A.FEDERAL_EIN

  move &A.Company to $Company
  move &A.Descr   to $CompanyName

  move '' to $priorEmplID

  move 'N'        to $Process_Company
  if $PublicSector <> 'Y'                                               !HP00007
     do Read-Employee-Data-72
     if $Process_Company = 'Y'
        do Generate-Tax-Tape-72
     end-if
  else                                                                  !HP00007
    do Read-Employee-Data-HP                                            !HP00007
    if $Process_Company = 'Y'                                           !HP00007
       do Generate-Tax-Tape                                             !HP00007
    end-if                                                              !HP00007
  end-if                                                                !HP00007

FROM  PS_COMPANY_TBL A
WHERE A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
  (SELECT MAX(EFFDT)
     FROM PS_COMPANY_TBL
     WHERE  COMPANY = A.COMPANY
       AND  EFFDT  <= $AsOfDate)
ORDER BY COMPANY
end-SELECT
  if #Count_F > 0
    do Print-Summary-Data('FileTotal')
  end-if
end-procedure


begin-procedure Read-Employee-Data-72
begin-SELECT
C.COMPANY    () on-break print=never before=Format-Employer-ID-72
C.EMPLID
C.STATE
C.LOCALITY
C.TAX_CLASS
C.NLGRS_QTD
C.TXGRS_QTD
DD1.NATIONAL_ID
DD.LAST_NAME
DD.FIRST_NAME
DD.MIDDLE_NAME
PP.HIRE_DT


  if &C.NlGrs_QTD > 0
    do Process-Employee-72
  else
    do Print-Summary-Data('NegWage')
  end-if


FROM  PS_TAX_BALANCE C, PS_PERSON_NAME DD, PS_PERS_NID DD1, PS_EMPLOYMENT PP
WHERE  C.COMPANY       = &A.Company
  AND DD.EMPLID        =  C.EMPLID
  AND PP.EMPLID        =  C.EMPLID
  AND  C.TAX_CLASS     = 'U'
  AND  C.BALANCE_ID    = $Calendar_Year_Id
  AND  C.BALANCE_YEAR  = &TX.Balance_Year
  AND  C.BALANCE_QTR   = &TX.Balance_Qtr
  AND  C.STATE         = $State
  AND  C.LOCALITY      = ' '
  AND  C.BALANCE_PERIOD  =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE
       WHERE  EMPLID        = C.EMPLID
         AND  COMPANY       = C.COMPANY
         AND  BALANCE_ID    = C.BALANCE_ID
         AND  BALANCE_YEAR  = C.BALANCE_YEAR
         AND  BALANCE_QTR   = C.BALANCE_QTR
         AND  STATE         = C.STATE
         AND  LOCALITY      = C.LOCALITY
         AND  TAX_CLASS     = C.TAX_CLASS)
  AND PP.EMPL_RCD  =
      (SELECT MIN(EMPL_RCD)
       FROM   PS_EMPLOYMENT
       WHERE  EMPLID        = C.EMPLID)
  AND C.NLGRS_QTD <> 0
  AND DD.EMPLID            = DD1.EMPLID
  AND DD1.COUNTRY = {NID_Country}
  AND DD1.NATIONAL_ID_TYPE = $Payroll_NID_Type
ORDER BY DD1.NATIONAL_ID
end-SELECT
end-procedure

begin-procedure Format-Employer-ID-72

  do Get-State-Tax-Data

 let $TestStateEIN = &Employer_ID_SUT
 let #Length = LENGTH(RTRIM(&Employer_ID_SUT,' '))

  evaluate #Length
    when = 10
      extract $StateEIN from $TestStateEIN 0 10
    when = 07
      extract $StateEIN from $TestStateEIN 0 7
      concat '000'    with $StateEIN
    when = 00
      do Employer_ID_missing_72
    when-Other
      do Employer_ID_error-72
  end-evaluate

end-procedure

begin-procedure Employer_ID_missing_72

    display '*********************** Processing stopped *********************'
    display '*** State Unemployment ID missing in Company State Tax Table ***'
    display '*********************** Processing stopped *********************'
    display 'Company - '    noline
    display $Company
    stop

end-procedure

begin-procedure Employer_ID_error-72


  display '*********************** Processing stopped *********************'
  display '*** Incorrect Format for State Unemployment in Company State Tax Table ***'
  display '*** Michigan State Unemployment ID is 7 digits in length ***'
  display '*** positions 8 - 10 should hold 3 digit multi-unit number or blanks if not requested ***'
  display '*********************** Processing stopped *********************'
  display 'Company - '  noline
  display $Company
  display 'Employer ID - ' noline
  display  $TestStateEIN
  stop

end-procedure

begin-procedure Process-Employee-72

  move 'Y'           to $Process_Company
  move &C.Company    to $Company
  move &C.EmplID     to $EmplID
  move &C.State      to $Emp_State
  move &C.Locality   to $Locality
  move &C.Tax_Class  to $Tax_Class

  let $Employer_ID    = $StateEIN            !from Format-Employer-ID-72


  move &DD1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits
  if $S123 = &Temp_SSN_Mask or RTRIM(&DD1.NATIONAL_ID, ' ') = ''
     move '000000000'   to $NATIONAL_ID
  else
     move &DD1.NATIONAL_ID        to $NATIONAL_ID
  end-if

  let $LastName   = rtrim(&DD.LAST_NAME, ' ')
  let $FirstName  = rtrim(&DD.FIRST_NAME, ' ')
  let $MiddleName = {ps-substr}(&DD.MIDDLE_NAME,1,1)
  uppercase $LastName
  uppercase $FirstName
  uppercase $MiddleName

  move &C.Txgrs_QTD  to #Txgrs_QTD
  move &C.NlGrs_QTD  to #NlGrs_QTD
  add #NlGrs_QTD     to #Co_NlGrs_QTD



  let #max_amt_NLG = 9999999999
  let #NLGrs_orig = #NlGrs_QTD

  add 1 to #Count_S

  let $done1 = 'N'
  while $done1 <> 'Y'
    do split-s-record (#NLGrs_orig,#NlGrs_QTD,#max_amt_NLG,$done1)
    do Insert-Work-Record
    add 1 to #Count_R
    add 1 to #Count_S1
  end-while

end-procedure


begin-procedure Generate-Tax-Tape-72
begin-SELECT
RR.COMPANY        () on-break print=never level=1 before=Before-Company-72
                                                 after=AFTER-COMPANY-72
RR.EMPLOYER_ID
RR.STATE
RR.EMPLID
RR.TAX_CLASS
RR.LOCALITY
RR.TXGRS_QTD
RR.NLGRS_QTD
RR.NATIONAL_ID
RR.LAST_NAME
RR.FIRST_NAME
RR.MIDDLE_NAME

  move &RR.EMPLOYER_ID  to $StateEIN
  move &RR.NATIONAL_ID  to $NATIONAL_ID
  move &RR.LAST_NAME    to $LastName
  move &RR.FIRST_NAME   to $FirstName
  move &RR.MIDDLE_NAME  to $MidInitial
  move &RR.NLGRS_QTD    to #NlGrs_QTD
  multiply 100 times #NlGrs_QTD
  do Format-Number(#NlGrs_QTD,  $NlGrs_QTD,  '0999999999')


  do Write-S-Record-72

FROM  PS_R_TAX810MI RR
WHERE RR.COMPANY       = &A.Company
ORDER BY RR.COMPANY, RR.NATIONAL_ID
end-SELECT
end-procedure

begin-procedure Before-Company-72
  do Get-Company-Data

  do E-Record-totals-72
  do Write-E-Record-72

end-procedure



begin-procedure After-Company-72

  do Print-Summary-Data('CoTotal')

  add #Co_NlGrs_QTD to #Tot_NlGrs_QTD
  add #Count_S to #Count_F
  add #Count_R to #Count_R_Total

  move 0 to #Co_NlGrs_QTD
  move 0 to #Count_S
  move 0 to #Count_S1
  move 0 to #Count_R
  new-page

end-procedure

begin-procedure E-Record-totals-72
begin-SELECT

COUNT(*)          &EmplCount
#ifdef DB2ALL
DECIMAL(SUM(TB.NLGRS_QTD),15,3) &CompanyTotal
#else
SUM(TB.NLGRS_QTD) &CompanyTotal
#endif

  move &A.Federal_EIN  to $FedEIN 099999999

  move &EmplCount to $Count_S 0999999
  move &CompanyTotal to #Temp
  multiply 100 times #Temp
  move #Temp to $Co_NlGrs_QTD 0999999999999


FROM  PS_R_TAX810MI TB
WHERE TB.COMPANY       = &A.Company
GROUP BY TB.COMPANY
end-SELECT

end-procedure


begin-procedure Write-E-Record-72

                                      !Add cols
  write 10 from 'E':1
          $StateEIN:10
           $RptYear:4
            $RptQtr:1
                $Sp:24
           $Count_S:7
      $Co_NlGrs_QTD:13
                $Sp:12

  move 'Y' to $RecordWritten

end-procedure

begin-procedure Write-S-Record-72
  write 10 from 'S':1
          $StateEIN:10
           $RptYear:4
            $RptQtr:1
       $NATIONAL_ID:9
                $Sp:7
          $LastName:16
         $FirstName:12
        $MidInitial:1
         $NlGrs_QTD:10
                $Sp:1


  move '' to $NlGrs_QTD
  move 0  to #NlGrs_QTD
end-procedure

begin-procedure Read-Employee-Data-HP                                   !HP00007
begin-SELECT                                                            !HP00007
RC.COMPANY        () on-break print=never level=1                       !HP00007
                                     before=Read-Employer-ID-Default    !HP00007
RC.EMPLID                                                               !HP00007
RC.TAX_CLASS                                                            !HP00007
RC.STATE                                                                !HP00007
RC.LOCALITY                                                             !HP00007
RC.TXGRS_QTD                                                            !HP00007
RC.NLGRS_QTD                                                            !HP00007
RD1.NATIONAL_ID                                                         !HP00007
RD.LAST_NAME                                                            !HP00007
RD.FIRST_NAME                                                           !HP00007
RD.MIDDLE_NAME                                                          !HP00007
RP.HIRE_DT                                                              !HP00007
                                                                        !HP00007
  move 'Y' to $Process_Company                                          !HP00007
  move &RC.Company to $Company                                          !HP00007
  move &RC.EmplID  to $EmplID                                           !HP00007
  move &RC.State   to $Emp_State                                        !HP00007
  move &RC.Locality   to $Locality                                      !HP00007
  move &RC.Tax_Class  to $Tax_Class                                     !HP00007
  move &RC.Txgrs_QTD  to #Txgrs_QTD                                     !HP00007
  move &RC.Nlgrs_QTD  to #Nlgrs_QTD                                     !HP00007
  move &RD1.National_ID to $National_ID                                 !HP00007
  move &RD.LAST_NAME  to $LastName                                      !HP00007
  move &RD.FIRST_NAME to $FirstName                                     !HP00007
  move &RD.MIDDLE_NAME to $MiddleName                                   !HP00007
  do Get-Employer-ID                                                    !HP00007
  do Insert-Work-Record                                                 !HP00007
                                                                        !HP00007
FROM  PS_TAX_BALANCE RC, PS_PERSON_NAME RD,  PS_PERS_NID RD1            !HP00007
      , PS_EMPLOYMENT RP                                 !HP00007
WHERE RC.COMPANY       = &A.Company                                     !HP00007
  AND RD.EMPLID        = RC.EMPLID                                      !HP00007
  AND RP.EMPLID        = RC.EMPLID                                      !HP00007
  AND RC.TAX_CLASS     = 'U'                                            !HP00007
  AND RC.BALANCE_ID    = $Calendar_Year_Id                              !HP00007
  AND RC.BALANCE_YEAR  = &TX.Balance_Year                               !HP00007
  AND RC.BALANCE_QTR   = &TX.Balance_Qtr                                !HP00007
  AND RC.STATE         = $State                                         !HP00007
  AND RC.LOCALITY      = ' '                                            !HP00007
  AND RC.BALANCE_PERIOD  =                                              !HP00007
      (SELECT MAX(BALANCE_PERIOD)                                       !HP00007
       FROM   PS_TAX_BALANCE                                            !HP00007
       WHERE  EMPLID        = RC.EMPLID                                 !HP00007
         AND  COMPANY       = RC.COMPANY                                !HP00007
         AND  BALANCE_ID    = RC.BALANCE_ID                             !HP00007
         AND  BALANCE_YEAR  = RC.BALANCE_YEAR                           !HP00007
         AND  BALANCE_QTR   = RC.BALANCE_QTR                            !HP00007
         AND  STATE         = RC.STATE                                  !HP00007
         AND  LOCALITY      = RC.LOCALITY                               !HP00007
         AND  TAX_CLASS     = RC.TAX_CLASS)                             !HP00007
  AND RP.EMPL_RCD  =                                                    !HP00007
      (SELECT MIN(EMPL_RCD)                                             !HP00007
       FROM   PS_EMPLOYMENT                                             !HP00007
       WHERE  EMPLID        = RC.EMPLID)                                !HP00007
  AND RC.NLGRS_QTD <> 0                                                 !HP00007
  AND RD.EMPLID            = RD1.EMPLID                                 !HP00007
  AND RD1.COUNTRY = {NID_Country}                                       !HP00007
  AND RD1.NATIONAL_ID_TYPE = $Payroll_NID_Type                          !HP00007
ORDER BY RD1.NATIONAL_ID                                                !HP00007
end-SELECT                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Generate-Tax-Tape                                       !HP00007
begin-SELECT                                                            !HP00007
R.COMPANY        () on-break print=never level=1 before=Before-Company  !HP00007
                                                 after=AFTER-COMPANY    !HP00007
R.EMPLOYER_ID    () on-break print=never level=2                        !HP00007
                                             before=Bef-Empler-ID-Chg   !HP00007
                                              after=Empler-ID-Chg       !HP00007
R.STATE                                                                 !HP00007
R.EMPLID                                                                !HP00007
R.TAX_CLASS                                                             !HP00007
R.LOCALITY                                                              !HP00007
R.TXGRS_QTD                                                             !HP00007
R.NLGRS_QTD                                                             !HP00007
R.NATIONAL_ID                                                           !HP00007
R.LAST_NAME                                                             !HP00007
R.FIRST_NAME                                                            !HP00007
R.MIDDLE_NAME                                                           !HP00007
                                                                        !HP00007
  move &R.EMPLOYER_ID  to $Employer_ID                                  !HP00007
                                                                        !HP00007
  if &R.NlGrs_QTD > 0                                                   !HP00007
    do Process-Employee-HP                                              !HP00007
  else                                                                  !HP00007
    do Print-Summary-Data('NegWage')                                    !HP00007
  end-if                                                                !HP00007
                                                                        !HP00007
FROM  PS_R_TAX810MI R                                                   !HP00007
WHERE R.COMPANY       = &A.Company                                      !HP00007
ORDER BY R.EMPLOYER_ID, R.NATIONAL_ID                                   !HP00007
end-SELECT                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Process-Employee-HP                                     !HP00007
  move &R.NATIONAL_ID to $S123 xxx       !isolate first 3 digits        !HP00007
  if $S123 = &Temp_SSN_Mask or RTRIM(&R.NATIONAL_ID, ' ') = ''          !HP00007
    move '000000000'   to $NATIONAL_ID                                  !HP00007
  else                                                                  !HP00007
    move &R.NATIONAL_ID        to $NATIONAL_ID                          !HP00007
  end-if                                                                !HP00007
  let $LastName   = rtrim(&R.LAST_NAME, ' ')                            !HP00007
  let $FirstName  = rtrim(&R.FIRST_NAME, ' ')                           !HP00007
  let $MidInitial = {ps-substr}(&R.MIDDLE_NAME,1,1)                     !HP00007
  uppercase $LastName                                                   !HP00007
  uppercase $FirstName                                                  !HP00007
  uppercase $$MidInitial                                                !HP00007
                                                                        !HP00007
  move &R.NlGrs_QTD to #NlGrs_QTD                                       !HP00007
  add #NlGrs_QTD to #Acct_NlGrs_QTD                                     !HP00007
  multiply 100 times #NlGrs_QTD                                         !HP00007
                                                                        !HP00007
  add 1 to #Count_ID                                                    !HP00007
                                                                        !HP00007
  do Format-Number(#NlGrs_QTD,  $NlGrs_QTD,  '0999999999')              !HP00007
  do Write-S-Record-72                                                  !HP00007
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Before-Company                                          !HP00007
  do Get-Company-Data                                                   !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure After-Company                                           !HP00007
  do Print-Summary-Data('CoTotal')                                      !HP00007
                                                                        !HP00007
  add #Co_NlGrs_QTD to #Tot_NlGrs_QTD                                   !HP00007
  add #Count_S to #Count_F                                              !HP00007
  add #Count_R to #Count_R_Total                                        !HP00007
                                                                        !HP00007
  move 0 to #Co_NlGrs_QTD                                               !HP00007
  move 0 to #Count_S                                                    !HP00007
  move 0 to #Count_S1                                                   !HP00007
  move 0 to #Count_R                                                    !HP00007
  new-page                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Bef-Empler-ID-Chg                                       !HP00007
  let $StateEIN = EDIT(RTRIM(&R.Employer_ID,' '),'0999999999')          !HP00007
  do E-Record-totals-72-HP                                              !HP00007
  do Write-E-Record-72-HP                                               !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Empler-ID-Chg                                           !HP00007
  if #Count_ID > 0                                                      !HP00007
    do Print-Summary-Data('AcctTotal')                                  !HP00007
                                                                        !HP00007
    add #Acct_NlGrs_QTD to #Co_NlGrs_QTD                                !HP00007
    add #Count_ID to #Count_S                                           !HP00007
                                                                        !HP00007
    move 0 to #Acct_NlGrs_QTD                                           !HP00007
    move 0 to #Count_ID                                                 !HP00007
    move 0 to #Count_ID1                                                !HP00007
    new-page                                                            !HP00007
  end-if                                                                !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure E-Record-totals-72-HP                                   !HP00007
begin-SELECT                                                            !HP00007
                                                                        !HP00007
COUNT(*)          &EmplCount_HP                                         !HP00007
#ifdef DB2ALL                                                           !HP00007
DECIMAL(SUM(TR.NLGRS_QTD),15,3) &CompanyTotal_HP                        !HP00007
#else                                                                   !HP00007
SUM(TR.NLGRS_QTD) &CompanyTotal_HP                                      !HP00007
#endif                                                                  !HP00007
                                                                        !HP00007
  move &EmplCount_HP to $Count_S_72 0999999                             !HP00007
  move &CompanyTotal_HP to #Temp                                        !HP00007
  multiply 100 times #Temp                                              !HP00007
  move #Temp to $NlGrs_QTD_S_72 0999999999999                           !HP00007
                                                                        !HP00007
                                                                        !HP00007
FROM  PS_R_TAX810MI TR                                                  !HP00007
WHERE TR.COMPANY       = &A.Company                                     !HP00007
AND   TR.EMPLOYER_ID   = &R.Employer_ID                                 !HP00007
AND   TR.NLGRS_QTD > 0                                                  !HP00007
end-SELECT                                                              !HP00007
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Write-E-Record-72-HP                                    !HP00007
                                                                        !HP00007
  write 10 from 'E':1                                                   !HP00007
          $StateEIN:10                                                  !HP00007
           $RptYear:4                                                   !HP00007
            $RptQtr:1                                                   !HP00007
                $Sp:24                                                  !HP00007
        $Count_S_72:7                                                   !HP00007
    $NlGrs_QTD_S_72:13                                                  !HP00007
                $Sp:12                                                  !HP00007
                                                                        !HP00007
  move 'Y' to $RecordWritten                                            !HP00007
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Read-Employer-ID-Default                                !HP00007
                                                                        !HP00007
  do Get-State-Tax-Data                                                 !HP00007
                                                                        !HP00007
  if RTRIM(&Employer_ID_SUT,' ') = ''                                   !HP00007
    do Employer_ID_missing_72                                           !HP00007
  end-if                                                                !HP00007
                                                                        !HP00007
  let $StateEIN = RTRIM(&Employer_ID_SUT, ' ')                          !HP00007
                                                                        !HP00007
  let $TestStateEIN = $StateEIN                                         !HP00007
  let $ExtReq = 'Y'                                                     !HP00007
  find ' ' in $TestStateEIN 0 #Spaces                                   !HP00007
                                                                        !HP00007
  evaluate #Spaces                                                      !HP00007
    when = 10                                                           !HP00007
      extract $StateEIN from $TestStateEIN 0 10                         !HP00007
      let $ExtReq = 'N'                                                 !HP00007
    when = 07                                                           !HP00007
      extract $StateEIN from $TestStateEIN 0 7                          !HP00007
    when-Other                                                          !HP00007
      do Employer_ID_warning                                            !HP00007
  end-evaluate                                                          !HP00007
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Employer-ID                                         !HP00007
                                                                        !HP00007
  let $Employer_ID = $StateEIN                                          !HP00007
  let $Employer_ID_Ext = ''                                             !HP00007
                                                                        !HP00007
  if $ExtReq = 'Y'                                                      !HP00007
      let $Job_Found = 'N'                                              !HP00007
      do Get-Active-Job                                                 !HP00007
                                                                        !HP00007
      if $Job_Found = 'N'                                               !HP00007
        do Get-Inactive-Job                                             !HP00007
      end-if                                                            !HP00007
                                                                        !HP00007
      if $Job_Found = 'Y'                                               !HP00007
        do Get-Employer-ID-Extension                                    !HP00007
        CONCAT $Employer_ID_Ext with $Employer_ID                       !HP00007
      else                                                              !HP00007
        CONCAT '000' with $Employer_ID                                  !HP00007
      end-if                                                            !HP00007
  end-if                                                                !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Active-Job                                          !HP00007
                                                                        !HP00007
begin-select                                                            !HP00007
JOB.BUSINESS_UNIT                                                       !HP00007
JOB.TAX_LOCATION_CD                                                     !HP00007
                                                                        !HP00007
  let $Business_Unit = &JOB.BUSINESS_UNIT                               !HP00007
  let $Tax_Location  = &JOB.TAX_LOCATION_CD                             !HP00006
  let $Job_Found     = 'Y'                                              !HP00007
                                                                        !HP00007
FROM PS_JOB JOB                                                         !HP00007
WHERE JOB.EMPLID     = $EmplID                                          !HP00007
  AND JOB.COMPANY    = $Company                                         !HP00007
  AND JOB.EMPL_RCD  =                                                   !HP00007
      (SELECT MIN(EMPL_RCD)                                             !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB.EMPLID                                    !HP00007
          AND COMPANY   = JOB.COMPANY                                   !HP00007
          AND EMPL_STATUS = 'A')                                        !HP00007
  AND JOB.EFFDT    =                                                    !HP00007
      (SELECT MAX(EFFDT)                                                !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB.EMPLID                                    !HP00007
          AND COMPANY   = JOB.COMPANY                                   !HP00007
          AND EMPL_RCD = JOB.EMPL_RCD                                   !HP00007
          AND EFFDT    <= $AsOfDate)                                    !HP00007
  AND JOB.EFFSEQ   =                                                    !HP00007
      (SELECT MAX(EFFSEQ)                                               !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB.EMPLID                                    !HP00007
          AND COMPANY   = JOB.COMPANY                                   !HP00007
          AND EMPL_RCD = JOB.EMPL_RCD                                   !HP00007
          AND EFFDT     = JOB.EFFDT)                                    !HP00007
end-select                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Inactive-Job                                        !HP00007
                                                                        !HP00007
begin-select                                                            !HP00007
JOB1.BUSINESS_UNIT                                                      !HP00007
JOB1.TAX_LOCATION_CD                                                    !HP00007
                                                                        !HP00007
  let $Business_Unit = &JOB1.BUSINESS_UNIT                              !HP00007
  let $Tax_Location  = &JOB1.TAX_LOCATION_CD                            !HP00006
  let $Job_Found     = 'Y'                                              !HP00007
                                                                        !HP00007
FROM PS_JOB JOB1                                                        !HP00007
WHERE JOB1.EMPLID     = $EmplID                                         !HP00007
  AND JOB1.COMPANY    = $Company                                        !HP00007
  AND JOB1.EMPL_RCD  =                                                  !HP00007
      (SELECT MIN(EMPL_RCD)                                             !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB1.EMPLID                                   !HP00007
          AND COMPANY   = JOB1.COMPANY)                                 !HP00007
  AND JOB1.EFFDT    =                                                   !HP00007
      (SELECT MAX(EFFDT)                                                !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB1.EMPLID                                   !HP00007
          AND COMPANY   = JOB1.COMPANY                                  !HP00007
          AND EMPL_RCD = JOB1.EMPL_RCD                                  !HP00007
          AND EFFDT    <= $AsOfDate)                                    !HP00007
  AND JOB1.EFFSEQ   =                                                   !HP00007
      (SELECT MAX(EFFSEQ)                                               !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB1.EMPLID                                   !HP00007
          AND COMPANY   = JOB1.COMPANY                                  !HP00007
          AND EMPL_RCD = JOB1.EMPL_RCD                                  !HP00007
          AND EFFDT     = JOB1.EFFDT)                                   !HP00007
end-select                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Employer-ID-Extension                               !HP00007
begin-select                                                            !HP00007
LOC.EMPLOYER_ID_EXT                                                     !HP00007
                                                                        !HP00007
  let $Employer_ID_Ext = &LOC.Employer_ID_Ext                           !HP00007
                                                                        !HP00007
FROM PS_CO_UI_RPTCD_TBL LOC                                             !HP00007
WHERE LOC.COMPANY = $Company                                            !HP00007
  AND LOC.LOCATION = $Tax_Location                                      !HP00006
  AND LOC.EFFDT =                                                       !HP00007
      (SELECT MAX(EFFDT)                                                !HP00007
         FROM PS_CO_UI_RPTCD_TBL                                        !HP00007
        WHERE COMPANY  = LOC.COMPANY                                    !HP00007
          AND LOCATION = LOC.LOCATION                                   !HP00007
          AND EFFDT   <= $AsOfDate)                                     !HP00007
  AND LOC.EFF_STATUS = 'A'                                              !HP00007
end-select                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Employer_ID_warning                                                      !HP00007
                                                                                         !HP00007
  display '********************************* Warning **********************************' !HP00007
  display '*** Incorrect Format for State Unemployment in Company State Tax Table.  ***' !HP00007
  display '*** Michigan State Unemployment ID consists of an assigned 7 digits      ***' !HP00007
  display '*** account number plus 3 digit multi-unit number.                       ***' !HP00007
  display '****************************************************************************' !HP00007
  display 'Company - '  noline                                                           !HP00007
  display $Company                                                                       !HP00007
  display 'Employer ID - ' noline                                                        !HP00007
  display  $TestStateEIN                                                                 !HP00007
end-procedure                                                                            !HP00007

begin-procedure Insert-Work-Record                                      !HP99999
                                                                        !HP99999
!  let $Txgrs_QTD = #Txgrs_QTD
!  let $Nlgrs_QTD = #Nlgrs_QTD
   move #Txgrs_QTD to $Txgrs_QTD 99999999.99mi
   move #Nlgrs_QTD to $Nlgrs_QTD 99999999.99mi
                                                                        !HP99999
  let $err-statement1 = 'TAX810MJ, Insert-Error - INSERT-WORK-RECORD'   !HP99999
  let $err-statement2 = 'Key Values: TABLE PS_R_TAX810MI' ||            !HP99999
                        ', COMPANY ' || $Company ||                     !HP99999
                        ', EMPLOYER_ID ' || $Employer_ID ||             !HP99999
                        ', NATIONAL_ID ' || $National_ID ||             !HP99999
                        ', STATE ' || $Emp_State ||                     !HP99999
                        ', EMPLID ' || $Emplid ||                       !HP99999
                        ', LAST_NAME ' || $LastName ||
                        ', FIRST_NAME ' || $FirstName ||
                        ', MIDDLE_NAME ' || $MiddleName ||
                        ', TAX_CLASS ' || $Tax_Class ||                 !HP99999
                        ', LOCALITY ' || $Locality ||                   !HP99999
                        ', TXGRS_QTD ' || $Txgrs_QTD ||                 !HP99999
                        ', NLGRS_QTD ' || $Nlgrs_QYD                    !HP99999
                                                                        !HP99999
begin-SQL on-error=Error-Display                                        !HP99999
  INSERT INTO PS_R_TAX810MI                                             !HP99999
        (COMPANY,                                                       !HP99999
         EMPLOYER_ID,                                                   !HP99999
         NATIONAL_ID,                                                   !HP99999
         STATE,                                                         !HP99999
         EMPLID,                                                        !HP99999
         LAST_NAME,
         FIRST_NAME,
         MIDDLE_NAME,
         TAX_CLASS,                                                     !HP99999
         LOCALITY,                                                      !HP99999
         TXGRS_QTD,                                                     !HP99999
         NLGRS_QTD)                                                     !HP99999
 VALUES ($Company,                                                      !HP99999
         $Employer_ID,                                                  !HP99999
         $National_ID,                                                  !HP99999
         $Emp_State,                                                    !HP99999
         $EmplID,                                                       !HP99999
         $LastName,
         $FirstName,
         $MiddleName,
         $Tax_Class,                                                    !HP99999
         $Locality,                                                     !HP99999
         #Txgrs_QTD,                                                    !HP99999
         #Nlgrs_QTD)                                                    !HP99999
end-SQL                                                                 !HP99999
end-procedure                                                           !HP99999
                                                                        !HP99999
begin-procedure Delete-Work-File                                        !HP99999
                                                                        !HP99999
  let $err-statement1 = 'TAX810MJ, Delete-Error - DELETE-WORK-FILE'     !HP99999
  let $err-statement2 = ' '                                             !HP99999
                                                                        !HP99999
begin-SQL On-Error=Error-Display                                        !HP99999
DELETE FROM PS_R_TAX810MI                                               !HP99999
end-SQL                                                                 !HP99999                                                                 !PUBSEC
                                                                        !HP99999
end-procedure                                                           !HP99999
                                                                        !HP99999
#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'txrnctl1.sqc'  !Select-Parameters procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'geteandg.sqc'  !Get-EandG procedure                           !HP00001
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getfrmat.sqc'  !Get-Diskette-Format procedure
#Include 'rptsmmry.sqc'  !Print-Summary-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
#Include 'stderror.sqc'  !Routine for error display                     !HP99999
#Include 'txovrflw.sqc'  !Split-S-Record
