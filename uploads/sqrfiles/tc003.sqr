!**************************************
! TC003: Employee Compensation Report
!**************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:15:39:12                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'   !Set environment
#Include 'setup32.sqc'  !Printer and page-size initialization

#define TC_COMP_DEFN
#define GB_GROUP_TBL

!************
Begin-Report
!************

  do Init-Report
  do Process-Main
  do StdAPI-Term

End-Report

!******************************
Begin-Procedure Init-Report
!******************************

  do Init-DateTime
  do Init-Number

  move 'TC003' to $ReportID
  do Get-Report-Language

  do Get-Current-DateTime
  do StdAPI-Init
  if $prcs_process_instance = ''
!    do Ask-Runcntrl
  else
    do Get-Run-Control-Value
  end-if

End-Procedure

!***********************************
Begin-Procedure Report-Translation
!***********************************

   do Init_Report_Translation($ReportID,$language_cd)

   do Get_Field_Information ('TC003', 'REPORT_TITLE', $ReportTitle, #CW)
   do Get_Field_Information ('TC003', 'GROUP', $Group, #CW)
   do Get_Field_Information ('TC003', 'EMPLOYEE', $Employee, #CW)
   do Get_Field_Information ('TC003', 'FROM', $From, #CW)
   do Get_Field_Information ('TC003', 'TO', $To, #CW)
   do Get_Field_Information ('TC003', 'ASOFDATE', $AsofDate, #CW)
   do Get_Field_Information ('TC003', 'AMOUNT_TYPE', $AmountType, #CW)
   do Get_Field_Information ('TC003', 'EE_AMOUNT', $EmployeeAmount, #CW)
   do Get_Field_Information ('TC003', 'ER_AMOUNT', $EmployerAmount, #CW)
   do Get_Field_Information ('TC003', 'TOTAL_COMP', $TotalComp, #CW)

 !    $ReportTitle    -- 'Employee Compensation Report'
 !    $Group          -- 'Group: '
 !    $Employee       -- 'Employee: '
 !    $From           -- 'Period: From '
 !    $To             -- ' To '
 !    $AsofDate       -- ' As of Date: '
 !    $AmountType     -- '(From Actual Payroll Amounts)'
 !    $EmployeeAmount -- 'Employee Amount'
 !    $EmployerAmount -- 'Employer Amount'
 !    $TotalComp      -- 'Total Compensation for'

End-Procedure

!***************
Begin-Heading 9
!***************
   print '            '    (1,1)
   print 'PeopleSoft'      (+1, 1)          center
   print $ReportTitle      (+1, 1)          center
   let #RptCol = {ColR} - 7
   Print $From   (+1, 1)
!   Print $From_Date        (0, +1)   Edit {Native-DateMask}
   Print $From_Date        (0, +1)
   Print $To               (0, +1)
!   Print $Thru_Date        (0, +1)   Edit {Native-DateMask}
   Print $Thru_Date        (0, +1)

   If $Amount_Type = 'P'
     Print $AmountType (0, #RptCol)
   End-if

   Do Change-Page

End-Heading

!*******************************
Begin-Procedure Process-Main
!*******************************

  Do Init-Field

Begin-SELECT
L.EMPLID                      () bold on-break level=1 print=Never
                                  Procedure = Print-Total-Compensation
    Let $Emplid = &L.EMPLID
H.NAME
    Let $Name = rtrim(&H.NAME, ' ')
L.GB_GROUP_ID                 () bold on-break level=1 print = Never
                                 Procedure = Print-Total-Compensation
    Let $Group_id = &L.GB_GROUP_ID
G.EFFDT
    Let $Group_Effdt = &G.EFFDT
G.DESCR
    Let $Group_Descr = &G.DESCR
L.TC_COMP_SOURCE_ID       (+1, 5) on-break level= 2 print=change

SUM(L.TO_COMP_AMT)       &TC_COMP_AMT_VC
SUM(L.TO_PAYROLL_AMT)    &TC_PAYROLL_AMT
  if $Amount_Type = 'C'
     Let #TC_AMT = &TC_COMP_AMT_VC
  else
     Let #TC_AMT = &TC_PAYROLL_AMT
  end-if
D.ORG_COST
   Let $Org_Cost = &D.ORG_COST

   If $Org_Cost = 'Y'
        Let #TC_ER = #TC_AMT

        Let #TotalER = #TotalER + #TC_ER
    else
        Let #TC_EE = #TC_AMT

        Let #TotalEE = #TotalEE + #TC_EE
    End-If

L.COMPENSATION_ID

    Let $CompID = rtrim(&L.COMPENSATION_ID, ' ')

D.DESCR50
   Let $Comp_Descr = rtrim(&D.DESCR50, ' ')

   Let $_TC_COMP_DEFN-DESCR50 = $Comp_Descr

   Do Get_Related_TC_COMP_DEFN($CompID)

   Let $Comp_Descr = $_TC_COMP_DEFN-DESCR50


   If $CompID <> $Old_CompID
       Print $CompID                 (0, 25) Bold
       Print $Comp_Descr             (0, 45)
       Let $Old_CompID = $CompID
    End-if
L.THRU_DATE

    Let $Amt_Date = &L.THRU_DATE

!    PRINT $Amt_Date (0, 100) Edit {Native-DateMask}
    PRINT $Amt_Date (0, 100)

    Display #TC_ER
    If #TC_EE > 0.0
       Print #TC_EE ( 0, 120) Edit '999,999,999.99'
    End-if
    If #TC_ER > 0.0
       Print #TC_ER ( 0, 140) Edit '999,999,999.99'
    End-if

    Let #TC_EE = 0.0
    Let #TC_ER = 0.0


FROM PS_TC_EE_DETAIL L,
     PS_TC_BEN_GRP P,
     PS_TC_COMP_SOURCE S,
     PS_TC_COMP_DEFN D,
     PS_GB_GROUP_TBL G,
     PS_PERSON_NAME H
WHERE  P.OPRID = $prcs_oprid
AND    P.RUN_CNTL_ID = $prcs_run_cntl_id
AND    P.TC_PLAN_ID      = $Tc_Plan_id
AND    P.TC_PLAN_EFFDT    = $Tc_Plan_Effdt
AND    L.TC_PLAN_ID      = P.TC_PLAN_ID
AND    L.EFFDT = P.TC_PLAN_EFFDT
AND    L.GB_GROUP_ID = P.GB_GROUP_ID
AND    (L.FROM_DATE >= $From_Date and L.FROM_DATE <= $Thru_Date)
AND    (L.THRU_DATE >= $From_Date and L.THRU_DATE <= $Thru_Date)
AND    S.TC_MEASURE = $Amount_Type
AND    L.TC_COMP_SOURCE_ID = S.TC_COMP_SOURCE_ID
AND    D.COMPENSATION_ID = L.COMPENSATION_ID
AND    G.GB_GROUP_ID = P.GB_GROUP_ID
AND    G.EFFDT = (SELECT MAX(G1.EFFDT)
                  FROM PS_GB_GROUP_TBL G1
                  WHERE G1.GB_GROUP_ID = P.GB_GROUP_ID
                  AND   G1.EFFDT <= $Tc_Plan_Thru_Dt)
AND    H.EMPLID = L.EMPLID
Group By L.GB_GROUP_ID,
         G.EFFDT,
         G.DESCR,
         L.EMPLID,
         H.NAME,
         L.COMPENSATION_ID,
         D.DESCR50,
         L.TC_COMP_SOURCE_ID,
         D.ORG_COST,
         L.THRU_DATE
ORDER BY L.EMPLID,
         L.GB_GROUP_ID,
         L.TC_COMP_SOURCE_ID,
         L.COMPENSATION_ID,
         L.THRU_DATE
End-Select

   Do Print-Total-Compensation

End-Procedure Process-Main


!*******************************
begin-procedure Change-Page
!*******************************
   Print $Group     (+2, 1)
   print $Group_id  (0, +1) Bold

   Let $_GB_GROUP_TBL-DESCR = $Group_Descr
   Do Get_Related_GB_GROUP_TBL($Group_Effdt,$Group_id)
   Let $Group_Descr = $_GB_GROUP_TBL-DESCR


   Print $Group_Descr (0, +5)

   Print $Employee     (+1, 1)
   Print  $Emplid      (0, +1)  Bold
   Print  $Name        (0, +5)

   Print $AsofDate       (+2, 100)
   Print $EmployeeAmount (0, 120)
   Print $EmployerAmount (0,  145)

end-procedure Change-Page

!********************************************
Begin-Procedure Print-Total-Compensation
!********************************************

   print $TotalComp (+2,1)  bold
   Print  $Name                 (0, +3)
   Print  ':'                       (0, +1)
   print #TotalEE (,110) Edit '999,999,999,999.99' bold
   print $Base_Currency (,+2) bold
   print #TotalER (,137) Edit '999,999,999,999.99' bold
   print $Base_Currency (,+2) bold

   Do Init-Field

   new-page

End-Procedure Emplyee-Total

!********************************************
Begin-Procedure Init-Field
!********************************************

   Let #TotalEE = 0
   Let #TotalER = 0
   Let $Old_CompID = ''

End-Procedure Init-Field

! *----------------------------------------------------------------------*
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Format-DateTime procedures
#Include 'stdapi.sqc'    !Update Process API
#Include 'number.sqc'    !Routines to format numbers
#Include 'adformat.sqc'  !Global address format
#Include 'tccomp.sqc'    !Read Total Compensation
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'rellang.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
! *------------------------* End Of Report *-----------------------------*
