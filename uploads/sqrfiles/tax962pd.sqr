!***********************************************************************
!  TAX962PD:  Puerto Rico W-2 Audit Report                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2012, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2012/01/09:12:28:08                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  872510                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Get-Transmitter
  do Select-Reports
  do Stdapi-Term
end-report

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Initialization
end-procedure


begin-heading 9
  #Include 'stdhdg01.sqc'
  print 'RW'                             (+2,1)
  print 'SSN'                             (0,11)
  print 'FICA Gross'                      (0,26)
  print 'FICA Tax'                        (0,44)
  print 'Medi Wages'                      (0,58)
  print 'Medi Tax'                        (0,76)
  print 'FICA Tips'                       (0,91)
  print 'ER Hlth Care'                    (0,104)

  print 'RO'                             (+1,1)
  print 'MS'                              (0,15)
  print 'Fed WG - PR'                     (0,26)
  print 'Comm - PR'                       (0,41)
  print 'Allow - PR'                      (0,59)
  print 'TIPS - PR'                       (0,74)
  print 'Tot Comp-PR'                     (0,89)
  print 'Tax - PR'                        (0,108)
  print 'Retire - PR'                     (0,121)

  print 'RS1'                             (+1,1)
  print 'SSN '                            (0,11)
  print 'Last'                            (0,23)
  print 'First'                           (0,46)
  print 'Middle'                          (0,64)
  print 'Phone'                           (0,82)
  print 'Close Dt'                        (0,95)
  print 'Serial #'                        (0,106)
  print 'Access Cd'                       (0,118)

  print 'RS2'                             (+1,1)
  print 'Char Contr'                      (0,25)
  print 'Money Plan'                      (0,43)
  print 'Cost Pen'                        (0,60)
  print 'CODA plans'                      (0,74)
  print 'Reimb Exp'                       (0,91)
  print 'Uncoll SS'                       (0,107)
  print 'Uncoll Med'                      (0,122)
  print 'Sal Act 324'                     (0,137)

  move '  ' to $FullLine
  print $FullLine                        (+1,176)

end-heading

begin-procedure Initialization
  display ''
  display 'Puerto Rico W-2 Audit Report '
  display ' '
  move 'TAX962PD' to $ReportID
  let $State-Process = 'Y'
  do Get-Current-DateTime

  If $Prcs_Process_Instance = ''
    Display 'Report cannot be run outside of PeopleSoft, please use process scheduler.'
    Stop
  End-if

  do Select-US-Year-End-Parameters
  do Convert-Parameters

  move '' to $ErName
end-procedure

begin-procedure Select-Reports

Begin-Select

RC960.REPORTING_MEDIUM
RC960.CONTACT_NAME_SSA

FROM PS_TAX960_PARM RC960
End-Select

  if rtrim(&RC960.CONTACT_NAME_SSA, ' ') = ''
    Show 'Electronic Parameters must be set up on SETUP Menu.'
    Show 'Re-execute after establishing Electronic Parameters.'
    Stop
  End-if

  move &RC960.Reporting_Medium to $FileType

  let $FileIn = 'TAX960PR'
  if $FileType = 'D'
    do Open-Diskette
  else
    do Open-File
  end-if

  if #filestat = 0
    if $FileType = 'D'
      do Read-Diskette-File
    else
      do Read-Tape-File
    end-if
  end-if

end-procedure

begin-procedure Open-Diskette
  move 1 to #FileExtension
  move #FileExtension to $FileExtension 099
#ifdef OS400
   let $FileExtension = '(D' || $FileExtension || ')'
   let $FileID = '{IMPORTPREFIX}' || $FileIn || $FileExtension
#else
   let $FileID = '{IMPORTPREFIX}' || $FileIn || '.' || $FileExtension
#endif
  open $FileID as 1 for-reading record=512:fixed
    status=#filestat
  if #filestat != 0
    display $FileID noline
    display ' not found '
    display ' '
  end-if
end-procedure

begin-procedure Open-File
  let $FileID = '{IMPORTPREFIX}' || $FileIn || '{IMPORTSUFFIX}'
  open $FileID as 1 for-reading record=512
    status=#filestat
  if #filestat != 0
    display $FileID noline
    display ' not found '
    display ' '
  end-if
end-procedure

begin-procedure Process-RA-Record
  do Print-RA-Record
end-procedure

begin-procedure Process-RE-Record
  if $ErName <> ''
    if $TotalsOnly = 'N'
      new-page
      move ' ' to $Col1Hdr1
      move ' ' to $Col2Hdr1
      move ' ' to $Col3Hdr1
      move ' ' to $Col4Hdr1
      move ' ' to $Col5Hdr1
      move ' ' to $Col6Hdr1
      move ' ' to $Col1Hdr2
      move ' ' to $Col2Hdr2
      move ' ' to $Col3Hdr2
      move ' ' to $Col4Hdr2
      move ' ' to $Col5Hdr2
      move ' ' to $Col6Hdr2
    end-if
  end-if

  next-listing  NEED = 24
  do Print-RE-Record

  if $TotalsOnly = 'N'
    move $ErName              to $Employer
    new-page
    move 'Last'             to $Col1Hdr1
    move 'Name'             to $Col1Hdr2
    move 'First'            to $Col2Hdr1
    move 'Name'             to $Col2Hdr2
    move 'Middle'           to $Col3Hdr1
    move 'Name'             to $Col3Hdr2
    move 'SSN'              to $Col4Hdr2
    move '              '   to $Col5Hdr1
    move '   State Wages'   to $Col5Hdr2
    move '              '   to $Col6Hdr1
    move '   State Wages'   to $Col6Hdr2
  end-if
end-procedure

begin-procedure Process-RS-Record
  do Print-RS-Record
end-procedure


begin-procedure Process-RO-Record
  do Print-RO-Record
end-procedure

begin-procedure Process-Employer-Record
  display 'Employer Record Found'
  extract $Employer from $TaxRecord 40 57
  extract $TaxJurisdiction from $TaxRecord 220 1
end-procedure

begin-procedure Process-RF-Record
   do Print-RF-Record
end-procedure


begin-procedure Process-RT-Record
   do Print-RT-Record
end-procedure

begin-procedure Process-RV-Record
  do Print-RV-Record
end-procedure

begin-procedure Process-RU-Record
  do Print-RU-Record
end-procedure

begin-procedure Print-Employer-Totals
   !Dummy
end-procedure

begin-procedure Print-File-Totals
  print 'File Totals - Wages: ' (+2,1)
  print  #F_StateWages (0,22) edit 999,999,999.99   !State Taxable Wages
  print 'Taxes: ' (+1,15)
  print  #F_StateTax   (0,22) edit 999,999,999.99   !State Taxes
  move 0 to #F_StateWages
  move 0 to #F_StateTax
  move '' to $ErName
end-procedure

begin-procedure Get-Transmitter
begin-SELECT
TX.COMPANY, TX.BALANCE_YEAR

FROM PS_TAXRPT_RUNCTL TX
end-SELECT

begin-SELECT
X.DESCR

FROM  PS_W2_COMPANY X
WHERE COMPANY = &TX.Company
  AND X.CALENDAR_YEAR = &TX.Balance_Year
end-SELECT
  move &X.Descr to $TrName
end-procedure


begin-procedure Convert-Parameters

  move $RC_US_YE.Media              to $FileType
  move $RC_US_YE.Totals_Only        to $TotalsOnly

end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'rdtp962p.sqc'  !Read PR mag tape file
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl2.sqc'  !US Year End Process Scheduler Run Controls
