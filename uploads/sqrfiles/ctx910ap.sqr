!***********************************************************************
!  CTX910AP:   PRINT T4A Supplementary from Tax Extract Table          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2012, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2012/11/21:16:42:25                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  872695                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup07.sqc'
#Include 'ctxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#define TAXFORM_NOTES
#include 'rellang.sqc'

begin-report

  do Init-Report

  do Process-Main

  if $SelectEEs = 'R'
    close 1

  end-if

!**** XML Publisher
  if $XMLPublisher = 'Y'

    do Capture-OS
    do Insert-YE-Runcontrol

    do XML-Close-Tag('CAN_YearEnd', $closeTag)
    do Write-XML-Line(#Fnum2, $closeTag)
    do Close-XML-File(#FNum2)

    if $RC_CAN_YE.T4A_COPY_TYPE = '2' and #FNum <> 0
      do Write-XML-Line(#Fnum, $closeTag)
      do Close-XML-File(#FNum)
    end-if

  else
    display ''
    display 'Upon successful conclusion of this program, one file will have'
    display 'been created:'
    display ''
    display '{IMPORTPREFIX}CTX910AP.LIS contains print images for paper T4A.'
    display ''
  end-if

#ifdef PRCSSCHD
  do StdAPI-Term
#endif

end-report


begin-procedure Init-Report

  display 'PRINT T4A SLIP'
  do Init-DateTime
  do Init-Number
#ifdef PRCSSCHD
  do StdAPI-Init
#endif

  do Initialization

     use-printer-type HPLASERJET
     do Declare-Printer-T4A

     let #StubPtr = 0
     do Set-T4A-Row-Pointers

end-procedure


begin-setup

declare-layout  default
paper-size      =(150,150)
formfeed        =no
orientation     =portrait
top-margin      =0.0
left-margin     =0.01
line-height     =12pt
char-width      =7.2pt
end-declare


#ifdef TAXTEST

#ifndef MVS
#ifndef OS400
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<27>E<27>(10U<27>&l0O<27>&l6D<27>&l0E<27>&l80F<27>(s10.0H
  end-declare
#else
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<39>E<39>(10U<39>&l0O<39>&l6D<39>&l0E<39>&l80F<39>(s10.0H
  end-declare
#endif
#else
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<39>E<39>(10U<39>&l0O<39>&l6D<39>&l0E<39>&l80F<39>(s10.0H
  end-declare
#endif

#endif

#ifndef MVS
#ifndef OS400
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<27>E<27>&l0L
  end-declare
#else
  declare-printer DEFAULT-HP
  end-declare
  declare-printer DEFAULT-LP
  init-string=<39>E<39>&l0L
  end-declare
#endif
#else
   declare-printer DEFAULT-HP
   end-declare
   declare-printer DEFAULT-LP
   init-string=<39>E<39>&l0L
   end-declare
#endif

   declare-procedure
     before-report = Init-Page-Size
   end-declare

end-setup


!***********************************************************************
begin-procedure Init-Page-Size
!***********************************************************************
! This sends a 'Perforation Skip' command to disable any bottom
! margin and obtain the maximum PCL page.  This is required in
! the printing of the T4A laser form.

#ifndef MVS
#ifndef OS400
  encode '<27>&l0L<27>&l3E' into $perforation_skip
#else
  encode '<39>&l0L<39>&l3E' into $perforation_skip
#endif
#else
  encode '<39>&l0L<39>&l3E' into $perforation_skip
#endif

  print $perforation_skip () code

end-procedure


begin-procedure Initialization

  let $PriorReportID = ' '
  move 'Y' to $T4A_Assigned

  move 'Y' to $FirstRecord
  move 'N' to $BurstSet
  do Get-Current-DateTime

  do Array-Create

  move 0  to #OtherInfo_i

  do Initialize-OtherInfo-Array

  move ' ' to $PlanA
  move ' ' to $PlanB
  move ' ' to $PlanC


  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($AsOfToday, $AsOfDateYMD, {DEFYMD},'','')
  move $AsOfDateYMD to $AsOfYear xxxx

  move $AsOfYear  to #AsOfYear
  subtract 1 from #AsOfYear

  do Get-Can-Tax-Processing-Params

  if &TX.Balance_Year < 2010
     display ' '
     display 'Reporting Year < 2010 - Processing stopped'
     stop quiet
  end-if

  move &TX.Balance_Year to #TaxYear
  move &TX.Balance_Year to $TaxYear
  move &TX.Balance_Year to $CalYear 9999
  let $AsOfDate = $CalYear || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP}, '', 'native')

  if &TX.Balance_Year <> #AsOfYear  and $Prcs_Process_Instance = ''

    display ''
    display 'Current Year is ' noline
    display $AsOfYear
    display 'Tax Reporting Year is ' noline
    display $TaxYear
    display 'Current Year is not one greater than Tax Reporting Year.'
    input $Answer maxlen=1 'Current tax year is not one greater than Tax Reporting Year -  Do you want to continue? (Y/N)'
    uppercase $Answer

    if $Answer <> 'Y'
      stop
    end-if
  end-if

  if $PRCS_Process_Instance = ''
     do Prompts
  else
#ifdef PRCSSCHD
           do Select-Canadian-YrEnd-Parameters
           do Convert-Parameters
#endif
  end-if

  if $SelectEEs = 'R'
    open '{IMPORTPREFIX}CTX910A1{IMPORTSUFFIX}' as 1 for-writing record=253:fixed
  end-if

  move $RC_CAN_YE.T4A_Final_Print to $Slip_Final_Print

!**** XML Publisher
  if $XMLPublisher = 'Y'
    do Get-XMLP-Definitions

    do Format-DateTime($AsOfToday, $out, {DEFYMD},'','')
    do Format-DateTime($out, $IssueDate, {DEFYMD},'','native')

    move 0 to #BatchSize
    move 1 to #BatchId
    let $BurstValueSave = ''

    let #FNum  = 0
    let #FStat = 0

    do Clear-Guide-Temp

    let $FName2 = 'CTXT4AXM.xml'
    let $sourceFile2     = $FName2 || '{IMPORTSUFFIX}'
    let $sourceLocation2 = '{IMPORTPREFIX}' || ''
    let $sourceFileName2 = rtrim($sourceLocation2,' ') || rtrim($sourceFile2,' ')
    let #FNum2  = 4
    let #FStat2 = 0
    do Open-XML-File($FName2, #FNum2, #FStat2)
    do XML-Open-Tag('CAN_YearEnd', $openTag2)
    do Write-XML-Line(#Fnum2, $openTag2)

  end-if

end-procedure


begin-procedure Prompts

  display ' '
  display ' '
  display ' '
  display 'Select the type of form to use for your T4A printing '
  display ' '

  while $FormType = ''
    input $FormType 'Enter L(Laser Form), or X(XML Publisher) or enter Q to quit'
    uppercase $FormType
    if INSTR('LXQ',$FormType, 1) = 0
      display ' '
      display '***** Enter L, X or Q to quit *****'
      display ' '
      move '' to $FormType
    end-if
  end-while

  if $FormType = 'X'
      let $XMLPublisher = 'Y'
  else
      let $XMLPublisher = 'N'
  end-if

  if $FormType = 'Q'
     stop
  else
        display ' '
        display ' '
        display ' '
        display 'Select the T4A copy to print'
        display ' '

        while $CopyType = ''
    input $CopyType 'Enter 1(Govt Copy), or 2(Employee Copy), or 4(Company Copy), or enter Q to quit'
          uppercase $CopyType
          if INSTR('124Q',$CopyType, 1) = 0
             display ' '
             display '***** Enter 1, 2, 4, or Q to quit *****'
             display ' '
             move '' to $CopyType
          end-if
        end-while

        if $CopyType = 'Q'
           stop
        end-if
  end-if

  if $CopyType = '1'
     let $SortInd = '1'
  else
    display ' '
    display ' '
    display ' '
    display 'If this job will print forms to send to the Canadian '
    display 'government, enter 1 and the system will establish the '
    display 'correct sort sequence.'
    display ' '
    display 'If this job will print forms for another use (such as '
    display 'laser forms, employer copy, etc., you may select the '
    display 'sort sequence you prefer.  Enter 2 to see the sort options. '
    display 'You will be shown three levels of sorting options, one level '
    display 'at a time.'
    display ' '

    while $SortInd = ''
      input $SortInd 'Enter 1 (Standard Govt Sort Sequence), or 2 (Other Sort Options) , or enter Q to quit'
      uppercase $SortInd
      if INSTR('12Q',$SortInd, 1) = 0
        display ' '
        display '***** Enter 1, 2, or Q to quit *****'
        display ' '
        move '' to $SortInd
      end-if
    end-while

    if $SortInd = 'Q'
       stop
    end-if
  end-if

  if $SortInd = '1'
    let $SortSequence = 'SL.REPORTING_ID ASC, EE.COUNTRY ASC, ' ||
       'EE.SLIP_SURNAME ASC , EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC'

  else

    display ' '
    display ' '
    display ' '
    display 'Select the Primary Sort Sequence for your T4A slips.'
    display 'To sequence by Business number, enter 1.'
    display 'To sequence by company, enter 2.'
    display ' '

    while $SortInd1 = ''
      input $SortInd1 'For 1st sort field : enter 1 (Sort by Business Number), or 2 (Sort by Company), or enter Q to quit'
      uppercase $SortInd1
      if INSTR('12Q',$SortInd1, 1) = 0
        display ' '
        display '***** Enter 1, 2, or Q to quit *****'
        display ' '
        move '' to $SortInd1
      end-if
    end-while

  if $SortInd1 = 'Q'
     stop
  end-if

  if $SortInd1 = '1'
     let $Sort1 = 'SL.REPORTING_ID ASC'
     let $DispSort1 = 'Business Number; '
  end-if

  if $SortInd1 = '2'
     let $Sort1 = 'SL.COMPANY ASC'
     let $DispSort1 = 'Company; '
  end-if

  display ' '
  display ' '
  display ' '
  display 'Within the previous sort, select the second sort sequence.'
  display '    To sort by Location Code           - enter 1'
  display '    To sort by Department Code         - enter 2'
  display '    To sort by Mail Drop               - enter 3'
  display '    To sort by Postal Code             - enter 4'
  display '    To sort by none of the above       - enter 5'
  display '    To quit                            - enter Q'
  display ' '

  while $SortInd2 = ''
    input $SortInd2 'For 2nd sort : enter 1 (Loc Code), 2 (Dpt Code), 3 (Mail Drp), 4 (Post Code), 5 (None), or Q to quit'
    uppercase $SortInd2
    if INSTR('12345Q',$SortInd2, 1) = 0
      display ' '
      display '***** Enter 1, 2, 3, 4, 5, or Q to quit *****'
      display ' '
      move '' to $SortInd2
    end-if
  end-while

  if $SortInd2 = 'Q'
     stop
  end-if

  if $SortInd2 = '1'
     let $Sort2 = 'EE.LOCATION ASC'
     let $DispSort2 = 'Location; '
  end-if

  if $SortInd2 = '2'
     let $Sort2 = 'EE.DEPTID ASC'
     let $DispSort2 = 'Department; '
  end-if

  if $SortInd2 = '3'
     let $Sort2 = 'EE.MAIL_DROP ASC'
     let $DispSort2 = 'Mail Drop; '
  end-if

  if $SortInd2 = '4'
     let $Sort2 = 'EE.POSTAL ASC'
     let $DispSort2 = 'Postal Code; '
  end-if

  if $SortInd2 = '5'
     let $Sort2 = 'N'
  end-if

  display ' '
  display ' '
  display ' '
  display 'Within the previous sorts, select the third sort sequence.'
  display '   To sort by Employee Name  - enter 1'
  display '   To sort by Employee ID    - enter 2'
  display '   To sort by Employee SIN   - enter 3'
  display '   To quit                   - enter Q'
  display ' '

  while $SortInd3 = ''
    input $SortInd3 'For 3rd sort field : enter 1 (Empl Name), 2 (Empl ID), 3 (Empl SIN), or enter Q to quit'
    uppercase $SortInd3
    if INSTR('123Q',$SortInd3, 1) = 0
      display ' '
      display '***** Enter 1, 2, 3, or Q to quit *****'
      display ' '
      move '' to $SortInd3
    end-if
  end-while

  if $SortInd3 = 'Q'
     stop
  end-if

  if $SortInd3 = '1'
     let $Sort3 = 'EE.SLIP_SURNAME ASC, EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC'
     let $DispSort3 = 'Employee Name; Employee ID'
  end-if

  if $SortInd3 = '2'
     let $Sort3 = 'EE.EMPLID ASC'
     let $DispSort3 = 'Employee ID'
  end-if

  if $SortInd3 = '3'
     let $Sort3 = 'EE.SIN ASC'
     let $DispSort3 = 'Employee SIN'
  end-if

  if $Sort2 = 'N'
     let $SortSequence =  $Sort1 || ', ' ||  $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort3
  else
     let $SortSequence = $Sort1 || ', ' || $Sort2 || ', ' || $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort2 || $DispSort3
  end-if
 end-if

   if $SortInd = '2' and $SortInd1 = '1'
      move 'R' to $SortCode
   else
      if $SortInd = '2' and $SortInd1 = '2'
         move 'C' to $SortCode
      else
         move ' ' to $SortCode
      end-if
   end-if

   if $SortInd <> '1'
      display '  '
      display '  '
      display 'Your sort sequence is ' noline
      display $DisplaySeq
      display '  '
      display '  '
     display 'Is this sequence correct?'
     display 'If the sequence is correct, enter Y'

      while $Response = ''
        input $Response ' Enter Y to continue or enter Q to quit'
        uppercase $Response
        if INSTR('YQ',$Response, 1) = 0
          display ' '
          display '***** Enter Y or Q to quit *****'
          display ' '
          move '' to $Response
        end-if
      end-while

     if $Response = 'Q'
        stop
     end-if
   else
     display 'Standard Government sort selected.'
   end-if

  while $SelectEEs = ''
    display ''
    display 'Regular processing or Select employees?'
    input $SelectEEs 'Enter R or S'
    uppercase $SelectEEs
    if INSTR('RS',$SelectEEs,1) = 0
      display 'Enter R or S'
      move '' to $SelectEEs
    end-if
  end-while

  if $SelectEEs = 'S'
    display 'Enter EmplID or hit ENTER to stop selecting'
    move 'AND EE.EMPLID in (''' to $E.SelectedEEs
    move ' ' to $SelectedEmplID
    while $SelectedEmplID <> ''
      input $SelectedEmplID ' '
      if $SelectedEmplID <> ''
        uppercase $SelectedEmplID
        let $E.SelectedEEs = $E.SelectedEEs || $SelectedEmplID || ''','''
        move 'Y' to $EE_Selected
      end-if
    end-while

    if $EE_Selected <> 'Y'
      display ''
      display 'No employees selected. Program stopped.'
      display ''
      stop
    end-if

    let $E.SelectedEEs = SUBSTR($E.SelectedEEs,1,LENGTH($E.SelectedEEs) - 2)
    let $E.SelectedEEs = $E.SelectedEEs || ')'
  else
    move '' to $E.SelectedEEs
  end-if

end-procedure

begin-procedure Array-Create
create-array name=OtherInfo size=45     -
    field=OtherInfo_Box_Num:char        -
    field=OtherInfo_Box_Amt:number      -
    field=OtherInfo_Text:char

end-procedure

begin-procedure Initialize-OtherInfo-Array

  move 0 to #load
  move 0 to #loadamt
  move ' ' to $loadspace
  move 45 to #Max_OtherInfo

  while #load < #Max_OtherInfo

    put $loadspace #loadamt $loadspace into OtherInfo(#load)
        OtherInfo_Box_Num OtherInfo_Box_Amt OtherInfo_Text

    add 1 to #load

  end-while

end-procedure


begin-procedure  Process-Main

begin-SELECT
SL.REPORTING_ID
SL.COMPANY
SL.EMPLID
SL.CALENDAR_YEAR
SL.UI_EXEMPT
SL.WAGE_LOSS_PLAN
SL.PROVINCE
SL.SEQUENCE_NUMBER
SL.YE_SLIP_PROCESS
SL.CAN_YE_SLIP_SEQ
EE.SIN
EE.EMPLID
EE.SLIP_SURNAME
EE.SLIP_FIRST_NAME
EE.SLIP_INITIAL
EE.ADDRESS1
EE.ADDRESS2
EE.CITY
EE.PROVINCE
EE.COUNTRY
EE.POSTAL
EE.LOCATION
EE.DEPTID
EE.MAIL_DROP

 if $Proc_Amend_Cancel_New = 'Y'
   do Check-Status
 end-if

 if $StatusOpen = 'Y'

  if $FirstRecord = 'N'
    if $SortInd = '1'

      if &EE.EmplID <> $EmplID or
         &SL.Reporting_ID <> $ReportID
         do Print-T4A-Data
         do Finish-Printing
         move 'Y' to $BurstSet

         if &SL.Reporting_ID <> $ReportID
            if $SelectEEs = 'R'
              do Write-Summary-Record
            end-if
         end-if
         do Initialize-Employee-Data
         move 0 to #j
      end-if
    else
      if $SortInd1 = '1'
         if &SL.Reporting_ID <> $ReportID or
            &EE.EmplID <> $EmplID
            do Print-T4A-Data
            do Finish-Printing
            move 'Y' to $BurstSet

            if &SL.Reporting_ID <> $ReportID
               if $SelectEEs = 'R'

                 do Write-Summary-Record
               end-if
            end-if
            do Initialize-Employee-Data
            move 0 to #j
         end-if
      else
         if &SL.Company <> $Company or
            &SL.Reporting_ID <> $ReportID or
            &EE.EmplID <> $EmplID
            do Print-T4A-Data
            do Finish-Printing
            move 'Y' to $BurstSet

            if &SL.Reporting_ID <> $ReportID
               if $SelectEEs = 'R'

                 do Write-Summary-Record
               end-if
            end-if
            do Initialize-Employee-Data
            move 0 to #j
         end-if
      end-if
    end-if
  else
    move 'N' to $FirstRecord
    move 'Y' to $BurstSet
  end-if

  do Format-Employee-Data

  if $XMLPublisher = 'Y' and $BurstSet = 'Y'

     move 'N' to $BurstSet

! create burst value for this slip
     let $BurstValue = rtrim(&EE.EMPLID, ' ') || $CalYear
     let $BurstValue = $BurstValue || rtrim(&SL.Company, ' ') || 'A'
     let $BurstValue = $BurstValue || rtrim(&SL.Reporting_ID, ' ')
     let $BurstValue = $BurstValue || rtrim(&SL.WAGE_LOSS_PLAN, ' ')

     do format-number(&SL.SEQUENCE_NUMBER, $T4ASeqNo, '09')
     let $BurstValue = $BurstValue || $T4ASeqNo
!
     if $RC_CAN_YE.T4A_Copy_Type = '2'

        if $BurstValue <> $BurstValueSave

           if $BurstValueSave <> ''
!              do XML-Close-Tag('T4_Data',$closeTag)
!              do Write-XML-Line(#Fnum, $closeTag)
              do XML-Close-Tag('CAN_YearEnd', $closeTag)
              do Write-XML-Line(#Fnum, $closeTag)
              do Close-XML-File(#FNum)
           end-if

           move $BurstValue to $BurstValueSave

           let $FName = rtrim($BurstValue,' ') || '.xml'
           let $sourceFile     = $FName || '{IMPORTSUFFIX}'
           let $sourceLocation = '{IMPORTPREFIX}' || ''
           let $sourceFileName = rtrim($sourceLocation,' ') || rtrim($sourceFile,' ')
           let #FNum  = 3
           let #FStat = 0
           do Open-XML-File($FName, #FNum, #FStat)
           do XML-Open-Tag('CAN_YearEnd', $openTag)
           do Write-XML-Line(#Fnum, $openTag)

        end-if
     end-if

     do XML-Open-Tag('T4A_Data', $openTag)

     if $RC_CAN_YE.T4A_Copy_Type = '2' and #FNum <> 0
        do Write-XML-Line(#Fnum, $openTag)
     end-if

     do Get-SS-Consent
     if $UserFlag = 'Y'
        do Write-XML-Line(#Fnum2, $openTag)
     end-if

!     if ($RC_CAN_YE.T4A_COPY_TYPE  = '2' and $Consent <> 'C') or
!         $RC_CAN_YE.T4A_COPY_TYPE <> '2'
!        do Write-XML-Line(#Fnum2, $openTag)
!     end-if

     if #OPT_BLK.PY_RPT_BATCH_SIZE > 0
        add 1 to #BatchSize
        if #BatchSize > #OPT_BLK.PY_RPT_BATCH_SIZE
           move 0 to #BatchSize
           add 1 to #BatchId
        end-if
     end-if

     do Build-XML-Line('BURST_VALUE', $BurstValue, 'Y', #FNum, #FNum2, $UserFlag)
     do Build-XML-Line('EMPLID', &EE.EMPLID, 'Y', #FNum, #FNum2, $UserFlag)

     do format-number(#BatchId, $T4ABatch, '09999')
     do Build-XML-Line('BATCH_ID', $T4ABatch, 'Y', #FNum, #FNum2, $UserFlag)


  end-if


  do Get-Slip-Detail

  if &SL.Company <> $PriorCompany and $Compbrk = 'Y'
       if #StubPtr <> 0
          do Insert-FormFeed
          new-page
          let #StubPtr = 0
          do Set-T4A-Row-Pointers
       end-if
  end-if

  if &SL.Company <> $PriorCompany
      move &SL.Company to $Company
      do Get-Setup-Viewing-Date
      do Get-Company-Data
      move $Company to $PriorCompany
      do convert-to-char($CompanyName, $CompanyName)
      move $CompnyAdd1 to $CoStreet
      move $CompnyCity to $CoCity
      let $CoProvince = rtrim(&CT.State, ' ')
      unstring &CT.Postal by ' ' into $PCD1 $PCD2
      let $PostCode = $PCD1 || $PCD2
      let $CoPostalCD = edit($PostCode, 'xxxbxxx')
      do convert-to-char($CoStreet, $CoStreet)
      do convert-to-char($CoCity, $CoCity)
      let $CompCity = RTRIM($CoCity, ' ')
      let $CoCity = $CompCity || ','
      let $CoCityProv = $CoCity || ' ' || $CoProvince || '  ' || $CoPostalCD
      move 'Y'      to $Compbrk
  end-if


  if $Proc_Amend_Cancel_New = 'Y' and $RC_CAN_YE.T4A_Final_Print = 'Y'
    do Close-Amend-Cancel-T4A
  end-if

  if $XMLPublisher = 'Y' and $CopyType = '2'

     do Insert-Guide-Data
  end-if


 end-if


FROM   PS_CAN_YE_SLIP  SL,
       PS_CAN_YE_EMPL  EE
WHERE  SL.CALENDAR_YEAR = &TX.Balance_Year
  AND  SL.TAXFORM_ID = 'A'
  AND  SL.PROCESS_FLAG <> 'V'
  AND  EE.CALENDAR_YEAR = &TX.Balance_Year
  AND  EE.EMPLID = SL.EMPLID
#ifdef MVS
  \$E.SelectedEEs\
#else
  [$E.SelectedEEs]             !NULL string if SELECT EEs option not used
#endif
  AND  EE.COMPANY = SL.COMPANY
  AND  EE.SEQUENCE_NUMBER = SL.SEQUENCE_NUMBER
  AND  SL.SEQUENCE_NUMBER = (SELECT MAX(SL1.SEQUENCE_NUMBER)
               FROM PS_CAN_YE_SLIP SL1
               WHERE SL1.COMPANY = SL.COMPANY
                 AND SL1.EMPLID  = SL.EMPLID
                 AND SL1.CALENDAR_YEAR = SL.CALENDAR_YEAR
#ifdef MVS
  \$seq_number\
#else
  [$seq_number]
#endif
#ifdef MVS
  \$ye_slip_process\
#else
  [$ye_slip_process]
#endif
#ifdef MVS
  ORDER BY \$SORTSEQUENCE\
#else
  ORDER BY [$SortSequence]
#endif
end-SELECT

   if $EmplID <> ''
     do Print-T4A-Data
     do Finish-Printing
   end-if
   if $SelectEEs = 'R'
     do Write-Summary-Record
   end-if

end-procedure


begin-procedure Get-SLIP-Detail

  move 12 to #MaxOtherInfoBx

  let $Print_EmplID = 'Y'

begin-SELECT
DE.BOX
DE.CAN_YE_BOX_TEXT
DE.CAN_YE_BOX_AMT


  do Accumulate-Employee-Data

FROM  PS_CAN_YE_DETAIL DE
WHERE EMPLID            = &EE.EmplID
  AND TAXFORM_ID        = 'A'
  AND WAGE_LOSS_PLAN    = &SL.Wage_Loss_Plan
  AND PROVINCE          = &SL.Province
  AND CALENDAR_YEAR     = &TX.Balance_Year
  AND COMPANY           = &SL.Company
  AND SEQUENCE_NUMBER   = &SL.Sequence_Number
  AND CAN_YE_SLIP_SEQ   = &SL.Can_YE_Slip_Seq

end-SELECT

end-procedure


begin-procedure Accumulate-Employee-Data

    if $Print_EmplID = 'Y'
       do Process-Other-Info-Bx
    end-if
 
    let $Box = RTRIM(&DE.Box, ' ')
    let #BoxAmt = &DE.Can_YE_Box_Amt
    let $BoxText = &DE.Can_YE_Box_Text

    evaluate $Box

       when = '016'
         if #BoxAmt > 0
            add #BoxAmt to #EmpPensionAnnuity
            add #BoxAmt to #TotPensionAnnuity
         end-if

       when = '018'
         if #BoxAmt > 0
            add #BoxAmt to #EmpLumpSumPayment
            add #BoxAmt to #TotLumpSumPayment
         end-if

       when = '020'
         if #BoxAmt > 0
            add #BoxAmt to #EmpCommissions
            add #BoxAmt to #TotCommissions
         end-if

       when = '022'
         if #BoxAmt > 0
            add #BoxAmt to #EmpIncomeTax
            add #BoxAmt to #TotIncomeTax
         end-if

       when = '024'
         if #BoxAmt > 0
            add #BoxAmt to #EmpAnnuities
            add #BoxAmt to #TotAnnuities
         end-if

       when = '048'
         if #BoxAmt > 0
            add #BoxAmt to #EmpFeesServices
            add #BoxAmt to #TotFeesServices
         end-if

       when-other
           do Process-Other-Info-Bx

       end-evaluate

end-procedure


begin-procedure Process-Other-Info-Bx

  if $Print_EmplID = 'Y'
     let $Box = '014'
     let #BoxAmt = 0
     let $BoxText = &EE.EmplID
     let $Print_EmplID = 'N'
  else
     let $Box = RTRIM(&DE.Box, ' ')
     let #BoxAmt = &DE.Can_YE_Box_Amt
     let $BoxText = &DE.Can_YE_Box_Text
  end-if

  move 0 to #srch_idx


  evaluate $Box

    when = '014'
    when = '026'
    when = '027'
    when = '028'
    when = '030'
    when = '032'
    when = '034'
    when = '036'
    when = '040'
    when = '042'
    when = '046'
    when = '102'
    when = '104'
    when = '105'
    when = '106'
    when = '107'
    when = '108'
    when = '109'
    when = '110'
    when = '111'
    when = '115'
    when = '116'
    when = '117'
    when = '118'
    when = '119'
    when = '122'
    when = '123'
    when = '124'
    when = '125'
    when = '126'
    when = '127'
    when = '129'
    when = '130'
    when = '131'
    when = '132'
    when = '133'
    when = '134'
    when = '135'
    when = '142'
    when = '143'
    when = '144'
    when = '146'
    when = '148'
    when = '150'
    when = '152'
    when = '154'
    when = '156'
    when = '158'
    when = '180'
    when = '190'

   while #srch_idx < #Max_OtherInfo
     get $i_Box from OtherInfo(#srch_idx) OtherInfo_Box_Num

     if rtrim($i_Box, ' ') = ''

       put $Box #BoxAmt $BoxText into OtherInfo(#OtherInfo_i)
        OtherInfo_Box_Num OtherInfo_Box_Amt OtherInfo_Text

        add 1 to #OtherInfo_i

        do Add-Other-Box-Summary
        break

     else

       if rtrim($i_Box, ' ') = $Box

         array-add #BoxAmt to OtherInfo(#srch_idx) OtherInfo_Box_Amt
         do Add-Other-Box-Summary
         break

       end-if

     end-if

    add 1 to #srch_idx

   end-while

    when-other
      break
  end-evaluate

end-procedure


begin-procedure Add-Other-Box-Summary

  evaluate $Box
    when = '028'
      if #BoxAmt > 0
        add #BoxAmt to #EmpOtherIncome
        add #BoxAmt to #TotOtherIncome
      end-if

    when = '030'
      if #BoxAmt > 0
        add #BoxAmt to #EmpPatronageAlloc
        add #BoxAmt to #TotPatronageAlloc
      end-if

    when = '032'
      if #BoxAmt > 0
        add #BoxAmt to #EmpPastPension
        add #BoxAmt to #TotPastPension
      end-if

    when = '036'
      if $BoxText <> ''
        let $Box36Text = RTRIM($BoxText, ' ')
        do Save-RPP-ID
      end-if

    when = '034'
      if #BoxAmt > 0
        add #BoxAmt to #EmpPensionAdj
        add #BoxAmt to #TotPensionAdj
      end-if

    when = '040'
      if #BoxAmt > 0
        add #BoxAmt to #EmpRESPIncome
        add #BoxAmt to #TotRESPIncome
      end-if

    when = '042'
      if #BoxAmt > 0
        add #BoxAmt to #EmpRESPAssistPay
        add #BoxAmt to #TotRESPAssistPay
      end-if

    when-other
      break
  end-evaluate

end-procedure


begin-procedure Set-Other-Info-Row-Pointers

      let $BoxCaseBox  = ''
      let $BoxCaseAmt  = ''

 evaluate #NumOfOtherInfo
    when = 0
      let #LaserRow    = #Row20
      let #LaserC2Row  = #Row52
      let #BoxCol      = 46
      let #AmtCol      = 50
      let $BoxCaseBox  = 'other1'
      let $BoxCaseAmt  = 'Other1Amt'
    when = 1
      let #LaserRow    = #Row20
      let #LaserC2Row  = #Row52
      let #BoxCol      = 64
      let #AmtCol      = 68
      let $BoxCaseBox  = 'other2'
      let $BoxCaseAmt  = 'Other2Amt'
    when = 2
      let #LaserRow    = #Row23
      let #LaserC2Row  = #Row55
      let #BoxCol      = 46
      let #AmtCol      = 50
      let $BoxCaseBox  = 'other3'
      let $BoxCaseAmt  = 'Other3Amt'
    when = 3
      let #LaserRow    = #Row23
      let #LaserC2Row  = #Row55
      let #BoxCol      = 64
      let #AmtCol      = 68
      let $BoxCaseBox  = 'other4'
      let $BoxCaseAmt  = 'Other4Amt'
    when = 4
      let #LaserRow    = #Row25
      let #LaserC2Row  = #Row57
      let #BoxCol      = 8
      let #AmtCol      = 11
      let $BoxCaseBox  = 'other5'
      let $BoxCaseAmt  = 'Other5Amt'
    when = 5
      let #LaserRow    = #Row25
      let #LaserC2Row  = #Row57
      let #BoxCol      = 26
      let #AmtCol      = 30
      let $BoxCaseBox  = 'other6'
      let $BoxCaseAmt  = 'Other6Amt'
    when = 6
      let #LaserRow    = #Row25
      let #LaserC2Row  = #Row57
      let #BoxCol      = 46
      let #AmtCol      = 50
      let $BoxCaseBox  = 'other7'
      let $BoxCaseAmt  = 'Other7Amt'
    when = 7
      let #LaserRow    = #Row25
      let #LaserC2Row  = #Row57
      let #BoxCol      = 64
      let #AmtCol      = 68
      let $BoxCaseBox  = 'other8'
      let $BoxCaseAmt  = 'Other8Amt'
    when = 8
      let #LaserRow    = #Row28
      let #LaserC2Row  = #Row60
      let #BoxCol      = 8
      let #AmtCol      = 11
      let $BoxCaseBox  = 'other9'
      let $BoxCaseAmt  = 'Other9Amt'
    when = 9
      let #LaserRow    = #Row28
      let #LaserC2Row  = #Row60
      let #BoxCol      = 26
      let #AmtCol      = 30
      let $BoxCaseBox  = 'other10'
      let $BoxCaseAmt  = 'Other10Amt'
    when = 10
      let #LaserRow    = #Row28
      let #LaserC2Row  = #Row60
      let #BoxCol      = 46
      let #AmtCol      = 50
      let $BoxCaseBox  = 'other11'
      let $BoxCaseAmt  = 'Other11Amt'
    when = 11
      let #LaserRow    = #Row28
      let #LaserC2Row  = #Row60
      let #BoxCol      = 64
      let #AmtCol      = 68
      let $BoxCaseBox  = 'other12'
      let $BoxCaseAmt  = 'Other12Amt'

    end-evaluate

end-procedure


begin-procedure Save-RPP-ID

  if $PlanA = ' '
     move $Box36Text to $PlanA
  else
     if $PlanA <> $Box36Text
       if $PlanB = ' '
          move $Box36Text to $PlanB
      else
         if $PlanB <> $Box36Text
           if $PlanC = ' '
              move $Box36Text to $PlanC
           end-if
         end-if
       end-if
     end-if
  end-if

end-procedure


begin-procedure Print-T4A-Data

         if #EmpPensionAnnuity > 0
            do Format-Number (#EmpPensionAnnuity, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('Pension', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
            print $BoxAmt (#Row07,49)
            if $CopyType = '2'
              print $BoxAmt (#Row39,49)
            end-if
         end-if


         if #EmpLumpSumPayment > 0
            do Format-Number (#EmpLumpSumPayment, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('Lump_sum_pay', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
            print $BoxAmt (#Row10,49)
            if $CopyType = '2'
              print $BoxAmt (#Row42,49)
            end-if
         end-if


         if #EmpCommissions > 0
            do Format-Number (#EmpCommissions, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('Self_Commissions', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
            print $BoxAmt (#Row10,66)
            if $CopyType = '2'
              print $BoxAmt (#Row42,66)
            end-if
         end-if


         if #EmpIncomeTax > 0
            do Format-Number (#EmpIncomeTax, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('Income_Tax_ded', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
            print $BoxAmt (#Row07,66)
            if $CopyType = '2'
              print $BoxAmt (#Row39,66)
            end-if
         end-if


         if #EmpAnnuities > 0
            do Format-Number (#EmpAnnuities, $BoxAmt, '99999999.99')
         else
            move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('Annuities', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
            print $BoxAmt (#Row14,49)
            if $CopyType = '2'
              print $BoxAmt (#Row46,49)
            end-if
         end-if


         if #EmpFeesServices > 0
            do Format-Number (#EmpFeesServices, $BoxAmt, '99999999.99')
         else
             move ' ' to $BoxAmt
         end-if

         if $XMLPublisher = 'Y'
            do Build-XML-Line('Fees_For_Services', $BoxAmt, 'Y', #FNum, #FNum2, $UserFlag)
         else
            print $BoxAmt (#Row14,66)
            if $CopyType = '2'
              print $BoxAmt (#Row46,66)
            end-if
         end-if

         do Print-Other-Info-Bx

end-procedure


begin-procedure Print-Other-Info-Bx

  let #j = 0

  let #OtherInfoCount = 0

  while #j < #OtherInfo_i
    get $OtherInfo_Box_Num #OtherInfo_Box_Amt $OtherInfo_Text from OtherInfo(#j)
        OtherInfo_Box_Num OtherInfo_Box_Amt OtherInfo_Text

    evaluate #j
      when = 0
      when = 12
      when = 24
      when = 36
        move 0  to #NumOfOtherInfo
      when = 1
      when = 13
      when = 25
      when = 37
        move 1  to #NumOfOtherInfo
      when = 2
      when = 14
      when = 26
      when = 38
        move 2  to #NumOfOtherInfo
      when = 3
      when = 15
      when = 27
      when = 39
        move 3  to #NumOfOtherInfo
      when = 4
      when = 16
      when = 28
      when = 40
        move 4  to #NumOfOtherInfo
      when = 5
      when = 17
      when = 29
      when = 41
        move 5  to #NumOfOtherInfo
      when = 6
      when = 18
      when = 30
      when = 42
        move 6  to #NumOfOtherInfo
      when = 7
      when = 19
      when = 31
      when = 43
        move 7  to #NumOfOtherInfo
      when = 8
      when = 20
      when = 32
      when = 44
        move 8  to #NumOfOtherInfo
      when = 9
      when = 21
      when = 33
        move 9  to #NumOfOtherInfo
      when = 10
      when = 22
      when = 34
        move 10 to #NumOfOtherInfo
      when = 11
      when = 23
      when = 35
        move 11 to #NumOfOtherInfo
      when-other
        break
    end-evaluate

    add 1 to #OtherInfoCount


    if #NumOfOtherInfo = 0 and #OtherInfoCount > #MaxOtherInfoBx     ! more than 1 T4A slip

      do Finish-Printing

      if $XMLPublisher = 'Y'

        do XML-Open-Tag('T4A_Data', $openTag)

        if $RC_CAN_YE.T4A_Copy_Type = '2' and #FNum <> 0
           do Write-XML-Line(#Fnum, $openTag)
        end-if

        if $UserFlag = 'Y'
           do Write-XML-Line(#Fnum2, $openTag)
        end-if

        do Build-XML-Line('BURST_VALUE', $BurstValue, 'Y', #FNum, #FNum2, $UserFlag)
        do Build-XML-Line('EMPLID', &EE.EMPLID, 'Y', #FNum, #FNum2, $UserFlag)
        do Build-XML-Line('BATCH_ID', $T4ABatch, 'Y', #FNum, #FNum2, $UserFlag)

      end-if
    end-if

    do Set-Other-Info-Row-Pointers

    if #OtherInfo_Box_Amt > 0

        if $XMLPublisher = 'Y'
          do Format-Number (#OtherInfo_Box_Amt, $OtherInfo_Box_Amt, '99,999,999.99')
        else
          do Format-Number (#OtherInfo_Box_Amt, $OtherInfo_Box_Amt, '99999999.99')
        end-if

        if $XMLPublisher = 'Y'
           do Build-XML-Line($BoxCaseBox, $OtherInfo_Box_Num, 'Y', #FNum, #FNum2, $UserFlag)
           do Build-XML-Line($BoxCaseAmt, $OtherInfo_Box_Amt, 'Y', #FNum, #FNum2, $UserFlag)
        else

           do Alter-Printer-T4A-Small-P9
           print $OtherInfo_Box_Num (#LaserRow,#BoxCol)
           do Alter-Printer-T4A-Reg

           print $OtherInfo_Box_Amt (#LaserRow,#AmtCol)
           if $CopyType = '2'

             do Alter-Printer-T4A-Small-P9
             print $OtherInfo_Box_Num (#LaserC2Row,#BoxCol)
             do Alter-Printer-T4A-Reg

             print $OtherInfo_Box_Amt (#LaserC2Row,#AmtCol)
           end-if
        end-if
    else
      if rtrim($OtherInfo_Text, '') <> ''

        if $XMLPublisher = 'Y'
           do Build-XML-Line($BoxCaseBox, $OtherInfo_Box_Num, 'Y', #FNum, #FNum2, $UserFlag)
           do Build-XML-Line($BoxCaseAmt, $OtherInfo_Text   , 'Y', #FNum, #FNum2, $UserFlag)
        else
           do Alter-Printer-T4A-Small-P9
           print $OtherInfo_Box_Num (#LaserRow,#BoxCol)
           do Alter-Printer-T4A-Reg

           let #TextCol = #AmtCol + 3
           print $OtherInfo_Text    (#LaserRow,#TextCol)
           if $CopyType = '2'

             do Alter-Printer-T4A-Small-P9
             print $OtherInfo_Box_Num (#LaserC2Row,#BoxCol)
             do Alter-Printer-T4A-Reg

             let #TextCol = #AmtCol + 3
             print $OtherInfo_Text    (#LaserC2Row,#TextCol)
           end-if
        end-if

      end-if
    end-if

    add 1 to #j
  end-while

  do Initialize-OtherInfo-Array

  move 0  to #OtherInfo_i

end-procedure


begin-procedure Format-Employee-Data

  let $SIN = edit(&EE.SIN, 'xxxbxxxbxxx')          ! Box 12

  let $LastName = substr(&EE.SLIP_Surname, 1, 25)
  do convert-to-char($LastName, $LastName)
  let $FirstName = &EE.SLIP_First_Name
  do convert-to-char($FirstName, $FirstName)
  let $MI = &EE.SLIP_Initial
  do convert-to-char($MI, $MI)
  let $Name = $LastName || ', ' || $FirstName || ' ' || $MI

  let $Street1 =  &EE.Address1
  do convert-to-char($Street1,$Street1)
  let $Street2 = RTRIM(&EE.Address2, ' ')
  do convert-to-char($Street2,$Street2)
  let $EeAddr = RTRIM(&EE.City, ' ')
  do convert-to-char($EeAddr, $EeAddr)
  let $ProvCd = rtrim(&EE.Province,' ')
  if $ProvCd = 'ZZ'
     move ' ' to $ProvCd
  end-if
  concat $ProvCd with $EeAddr ,bxx

  let $Country = ' '
  if &EE.Country <> 'CAN'
     move &EE.Country to $Country
     do convert-to-char($Country, $Country)
     let $Country_print = rtrim($Country, ' ')
  else
     let $Country_print = ' '
  end-if

  if $Country = 'USA'
    add 1 to #USACount
  end-if

  let $TempAddr = RTRIM($EeAddr, ' ')

  unstring &EE.POSTAL by ' ' into $ZIP1 $ZIP2
  let $ZIP1 = $ZIP1  || $ZIP2

  if &EE.Country = 'CAN'
    let $ZipValue = edit($ZIP1, 'xxxbxxx')
  else
    let $ZipValue = rtrim(&EE.POSTAL, ' ')
  end-if

  let $PrintAddr = $TempAddr || '  ' || $ZipValue

  let $ReportID = &SL.Reporting_ID
  let $WageLossPlan = &SL.Wage_Loss_Plan
  let $EmplID =  &EE.EmplID
  let $Slip_Process = &SL.YE_Slip_Process

  let $Prior_LastName = &EE.SLIP_Surname
end-procedure


begin-procedure Finish-Printing

     if $XMLPublisher = 'Y'
        do Build-XML-Line('sin', $SIN, 'Y', #FNum, #FNum2, $UserFlag)
     else
        print $SIN (#Row10,9)
        if $CopyType = '2'
          print $SIN (#Row42,9)
        end-if
     end-if


     if $XMLPublisher = 'Y'
        do XML-Open-Tag('EE_Name', $eeNameTag)

        if $RC_CAN_YE.T4A_Copy_Type = '2' and #FNum <> 0
           do Write-XML-Line(#Fnum, $eeNameTag)
        end-if

        if $UserFlag = 'Y'
           do Write-XML-Line(#Fnum2, $eeNameTag)
        end-if

!        if ($RC_CAN_YE.T4A_Copy_Type = '2' and $Consent <> 'C') or
!            $RC_CAN_YE.T4A_Copy_Type <> '2'
!          do Write-XML-Line(#Fnum2, $eeNameTag)
!        end-if

        do Build-XML-Line('Lastname', $LastName, 'Y', #FNum, #FNum2, $UserFlag)
        do Build-XML-Line('Firstname', $FirstName, 'Y', #FNum, #FNum2, $UserFlag)
        do Build-XML-Line('MI', $MI, 'Y', #FNum, #FNum2, $UserFlag)

        do XML-Close-Tag('EE_Name', $eeNameTag)

        if $RC_CAN_YE.T4A_Copy_Type = '2' and #FNum <> 0
           do Write-XML-Line(#Fnum, $eeNameTag)
        end-if

        if $UserFlag = 'Y'
           do Write-XML-Line(#Fnum2, $eeNameTag)
        end-if

!        if ($RC_CAN_YE.T4A_Copy_Type = '2' and $Consent <> 'C') or
!            $RC_CAN_YE.T4A_Copy_Type <> '2'
!          do Write-XML-Line(#Fnum2, $eeNameTag)
!        end-if

     else

        do Alter-Printer-T4A-Small-P9

        print $LastName (#Row15,07)
        print $FirstName (#Row15,29)
        print $MI (#Row15,40)
        if $CopyType = '2'
           print $LastName (#Row47,07)
           print $FirstName (#Row47,29)
           print $MI (#Row47,40)
        end-if

     end-if


     if $XMLPublisher = 'Y'
        do Build-XML-Line('EE_Street1', $Street1, 'Y', #FNum, #FNum2, $UserFlag)
     else

        print $Street1 (#Row17,5)
        if $CopyType = '2'
           print $Street1 (#Row49,5)
        end-if
     end-if

  if $Street2 <> ''

    if $XMLPublisher = 'Y'
       do Build-XML-Line('EE_Street2', $Street2, 'Y', #FNum, #FNum2, $UserFlag)
       do Build-XML-Line('EE_Street3', $PrintAddr, 'Y', #FNum, #FNum2, $UserFlag)
       do Build-XML-Line('EE_Street4', $Country_print, 'Y', #FNum, #FNum2, $UserFlag)
    else
       print $Street2 (#Row18,5)
       print $PrintAddr (#Row19,5)
       print $Country_print (#Row20,5)
       if $CopyType = '2'
          print $Street2 (#Row50,5)
          print $PrintAddr (#Row51,5)
          print $Country_print (#Row52,5)
       end-if
    end-if
  else

    if $XMLPublisher = 'Y'
       do Build-XML-Line('EE_Street2', $PrintAddr, 'Y', #FNum, #FNum2, $UserFlag)
       do Build-XML-Line('EE_Street3', $Country_print, 'Y', #FNum, #FNum2, $UserFlag)
    else
       print $PrintAddr (#Row18,5)
       print $Country_print (#Row19,5)
       if $CopyType = '2'
          print $PrintAddr (#Row50,5)
          print $Country_print (#Row51,5)
       end-if
    end-if
  end-if

  do Alter-Printer-T4A-Reg


  if $SelectEEs = 'S' or $SelectEEs = 'E'

      if $XMLPublisher = 'Y'
         do Build-XML-Line('DUP_Copy', 'DUPLICATE COPY', 'Y', #FNum, #FNum2, $UserFlag)
      else
         print 'DUPLICATE COPY'   (#Row04,19)
         if $CopyType = '2'
            print 'DUPLICATE COPY' (#Row36,19)
         end-if
      end-if
  end-if

  if ($SelectEEs = 'A' or $SelectEEs = 'E') and $Slip_Process = 'A'

      if $XMLPublisher = 'Y'
         do Build-XML-Line('Amend_Cancel', 'AMENDED', 'Y', #FNum, #FNum2, $UserFlag)
      else
         print 'AMENDED'        (#Row03, 35)
         if $CopyType = '2'
            print 'AMENDED'     (#Row35, 35)
         end-if
      end-if
  end-if

  if ($SelectEEs = 'C' or $SelectEEs = 'E') and $Slip_Process = 'C'

      if $XMLPublisher = 'Y'
         do Build-XML-Line('Amend_Cancel', 'CANCELLED', 'Y', #FNum, #FNum2, $UserFlag)
      else
         print 'CANCELLED'        (#Row03, 35)
         if $CopyType = '2'
            print 'CANCELLED'     (#Row35, 35)
         end-if
      end-if
  end-if


     if $CopyType <> '2'
        if $XMLPublisher = 'Y'
          do Build-XML-Line('Reporting_Id', $ReportID, 'Y', #FNum, #FNum2, $UserFlag)
        else
          print $ReportID (#Row06,9)
        end-if
     end-if

  do convert-to-char($CompanyName, $CompanyName)

     if $XMLPublisher = 'Y'
        do Build-XML-Line('Payer_Name', $CompanyName, 'Y', #FNum, #FNum2, $UserFlag)
        do Build-XML-Line('Payer_Street', $CoStreet, 'Y', #FNum, #FNum2, $UserFlag)
        do Build-XML-Line('Payer_CityProv', $CoCityProv, 'Y', #FNum, #FNum2, $UserFlag)
     else
        print $CompanyName (#Row01,5)
        print $CoStreet (#Row02,5)
        print $CoCity (#Row03,5)
        print $CoProvince (#Row03,+1)
        print $CoPostalCD (#Row03,+2)
        if $CopyType = '2'
           print $CompanyName (#Row33,5)
           print $CoStreet (#Row34,5)
           print $CoCity (#Row35,5)
           print $CoProvince (#Row35,+1)
           print $CoPostalCD (#Row35,+2)
        end-if
     end-if

     if $XMLPublisher = 'Y'
        do Build-XML-Line('Tax_Year', $CalYear, 'Y', #FNum, #FNum2, $UserFlag)

     else
        print $CalYear      (#Row01,41)
        if $CopyType = '2'
           print $CalYear   (#Row33,41)
        end-if
     end-if


  if $XMLPublisher = 'Y'
     do XML-Close-Tag('T4A_Data', $closeTag)

     if $UserFlag = 'Y'
        do Write-XML-Line(#Fnum2, $closeTag)
     end-if

!     if ($RC_CAN_YE.T4A_Copy_Type = '2' and $Consent <> 'C') or
!         $RC_CAN_YE.T4A_Copy_Type <> '2'
!
!         do Write-XML-Line(#Fnum2, $closeTag)
!     end-if

     if $RC_CAN_YE.T4A_Copy_Type = '2' and #Fnum <> 0
       do Write-XML-Line(#Fnum, $closeTag)
     end-if

  else
     if $CopyType = '1' or $CopyType = '4'

        if #StubPtr = 32
           do Insert-FormFeed
           new-page
           move 0 to #StubPtr
        else

           add 32 to #StubPtr
        end-if
        do Set-T4A-Row-Pointers
     else
        do Insert-FormFeed
        new-page
     end-if
  end-if

   add 1 to #T4ACount

end-procedure


begin-procedure Initialize-Employee-Data

  move 0 to #EmpPensionAnnuity
  move 0 to #EmpLumpSumPayment
  move 0 to #EmpCommissions
  move 0 to #EmpIncomeTax
  move 0 to #EmpAnnuities
  move 0 to #EmpFeesServices
  move 0 to #EmpOtherIncome
  move 0 to #EmpPatronageAlloc
  move 0 to #EmpPastPension
  move 0 to #EmpPensionAdj
  move ' ' to $Box36Text
  move 0 to #EmpRESPIncome
  move 0 to #EmpRESPAssistPay

end-procedure

begin-procedure Set-T4A-Row-Pointers

  let #Row01 = #StubPtr + 01
  let #Row02 = #StubPtr + 02
  let #Row03 = #StubPtr + 03
  let #Row04 = #StubPtr + 04
  let #Row05 = #StubPtr + 05
  let #Row06 = #StubPtr + 06
  let #Row07 = #StubPtr + 07
  let #Row08 = #StubPtr + 08
  let #Row09 = #StubPtr + 09
  let #Row10 = #StubPtr + 10
  let #Row11 = #StubPtr + 11
  let #Row12 = #StubPtr + 12
  let #Row13 = #StubPtr + 13
  let #Row14 = #StubPtr + 14
  let #Row15 = #StubPtr + 15
  let #Row16 = #StubPtr + 16
  let #Row17 = #StubPtr + 17
  let #Row18 = #StubPtr + 18
  let #Row19 = #StubPtr + 19
  let #Row20 = #StubPtr + 20
  let #Row21 = #StubPtr + 21
  let #Row22 = #StubPtr + 22
  let #Row23 = #StubPtr + 23
  let #Row24 = #StubPtr + 24
  let #Row25 = #StubPtr + 25
  let #Row26 = #StubPtr + 26
  let #Row27 = #StubPtr + 27
  let #Row28 = #StubPtr + 28
  let #Row29 = #StubPtr + 29
  let #Row30 = #StubPtr + 30
  let #Row31 = #StubPtr + 31
  let #Row32 = #StubPtr + 32
  let #Row33 = #StubPtr + 33
  let #Row34 = #StubPtr + 34
  let #Row35 = #StubPtr + 35
  let #Row36 = #StubPtr + 36
  let #Row37 = #StubPtr + 37
  let #Row38 = #StubPtr + 38
  let #Row39 = #StubPtr + 39

  if $CopyType = '2'
     let #Row34 = 34
     let #Row35 = 35
     let #Row36 = 36
     let #Row37 = 37
     let #Row38 = 38
     let #Row39 = 39
     let #Row42 = 42
     let #Row43 = 43
     let #Row45 = 45
     let #Row47 = 47
     let #Row46 = 46
     let #Row48 = 48
     let #Row49 = 49
     let #Row50 = 50
     let #Row51 = 51
     let #Row52 = 52
     let #Row53 = 53
     let #Row55 = 55
     let #Row56 = 56     
     let #Row57 = 57
     let #Row58 = 58
     let #Row59 = 59
     let #Row60 = 60
     let #Row61 = 61
     let #Row63 = 63
     let #Row64 = 64
     let #Row68 = 68
     let #Row69 = 69
     let #Row70 = 70
     let #Row71 = 71
     let #Row72 = 72
     let #Row73 = 73
     let #Row74 = 74
     let #Row75 = 75
     let #Row76 = 76
     let #Row77 = 77
     let #Row78 = 78
  end-if

end-procedure


begin-procedure Insert-FormFeed

#ifndef MVS
#ifndef OS400
  encode '<27>&k2G' into $FormFeed
#else
  encode '<39>&k2G' into $FormFeed
#endif
#else
  encode '<39>&k2G' into $FormFeed
#endif

 print $FormFeed () code
 print ' ' (140,01)

end-procedure


begin-procedure Write-Summary-Record

  move '301' to $TypeCode

   do format-number(#T4ACount, $T4ACount, '0999999')
   do format-number(#USACount, $USACount, '0999999')
   do format-number(#TotPensionAnnuity, $TotPensionAnnuity, '0999999999999.99')
   do format-number(#TotLumpSumPayment, $TotLumpSumPayment, '0999999999999.99')
   do format-number(#TotCommissions, $TotCommissions, '0999999999999.99')
   do format-number(#TotPatronageAlloc, $TotPatronageAlloc, '0999999999999.99')
   do format-number(#TotPastPension, $TotPastPension, '0999999999999.99')
   do format-number(#TotAnnuities, $TotAnnuities, '0999999999999.99')
   do format-number(#TotOtherIncome, $TotOtherIncome, '0999999999999.99')
   do format-number(#TotFeesServices, $TotFeesServices, '0999999999999.99')
   do format-number(#TotIncomeTax, $TotIncomeTax, '0999999999999.99')
   do format-number(#TotPensionAdj, $TotPensionAdj, '0999999999999.99')
   do format-number(#TotRESPIncome, $TotRESPIncome, '0999999999999.99')
   do format-number(#TotRESPAssistPay, $TotRESPAssistPay, '0999999999999.99')

   write 1 from   $TypeCode:3               -
                  $WageLossPlan:3           -
                  $T4ACount:7               -
                  $TotPensionAnnuity:16     -
                  $TotLumpSumPayment:16     -
                  $TotCommissions:16        -
                  $TotPatronageAlloc:16     -
                  $TotPastPension:16        -
                  $TotAnnuities:16          -
                  $TotOtherIncome:16        -
                  $TotFeesServices:16       -
                  $TotIncomeTax:16          -
                  $TotPensionAdj:16         -
                  $SortCode:1               -
                  $ReportID:16              -
                  $Company:3                -
                  $USACount:7               -
                  $PlanA:7                  -
                  $PlanB:7                  -
                  $PlanC:7                  -
                  $TotRESPIncome:16         -
                  $TotRESPAssistPay:16



  do Init-Summary-Values

end-procedure


begin-procedure Init-Summary-Values

  move 0 to #TotPensionAnnuity
  move 0 to #TotLumpSumPayment
  move 0 to #TotCommissions
  move 0 to #TotIncomeTax
  move 0 to #TotAnnuities
  move 0 to #TotOtherIncome
  move 0 to #TotPatronageAlloc
  move 0 to #TotPastPension
  move 0 to #TotPensionAdj
  move 0 to #TotFeesServices
  move 0 to #TotRESPIncome
  move 0 to #TotRESPAssistPay

  move 0 to #T4ACount
  move 0 to #USACount

  move ' ' to $PlanA
  move ' ' to $PlanB
  move ' ' to $PlanC

end-procedure


begin-procedure Convert-Parameters

  let $FormType = $RC_CAN_YE.T4A_Form_Type

  if $FormType = 'X'
    let $XMLPublisher = 'Y'
  else
    let $XMLPublisher = 'N'
  end-if

  let $CopyType = $RC_CAN_YE.T4A_Copy_Type
  let $SelectEEs = $RC_CAN_YE.T4A_Processing_Flg

  if $SelectEEs = 'S' or  $SelectEEs = 'E'
    do Read-EEs
  end-if

  let $ye_slip_process = ' '
  let $seq_number      = ' '

  evaluate $SelectEEs
  when = 'A'                    ! Amended slips
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS = ''A'' '
    let $seq_number      = ')'
    break
  when = 'C'                    ! Cancelled slips
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS = ''C'' '
    let $seq_number      = ')'
    break
  when = 'N'                    ! Reissued slips
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS = ''N'' '
    let $seq_number      = ')'
    break
  when = 'E'                    ! Reprint Amended/Cancelled/Reissued slips
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS IN (''A'', ''C'', ''N'') '
    let $seq_number      = ' AND SL1.TAXFORM_ID = SL.TAXFORM_ID AND SL1.YE_SLIP_PROCESS = SL.YE_SLIP_PROCESS) '
    break
  when = 'R'                    ! Original slips
  when = 'S'                    ! Reprint Original slips
    let $ye_slip_process = ' AND SL.YE_SLIP_PROCESS = ''O'' '
    let $seq_number      = ' AND SL1.YE_SLIP_PROCESS = SL.YE_SLIP_PROCESS) '
    break
  end-evaluate

  if $SelectEEs = 'A' or $SelectEEs = 'C' or $SelectEEs = 'N'
    let $Proc_Amend_Cancel_New = 'Y'
  else
    let $StatusOpen = 'Y'
  end-if


  if $RC_CAN_YE.T4A_Primary_Sort  = '1'
       let $SortInd = '1'
       let $SortSequence = 'SL.REPORTING_ID ASC,EE.COUNTRY ASC, ' ||
       'EE.SLIP_SURNAME ASC, EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC'

  else

      if $RC_CAN_YE.T4A_Primary_Sort  = '2'
         let $Sort1 = 'SL.REPORTING_ID ASC'
         let $DispSort1 = 'Business Number ; '
      end-if

      if $RC_CAN_YE.T4A_Primary_Sort  = '3'
         let $Sort1 = 'SL.COMPANY ASC'
         let $DispSort1 = 'Company; '
      end-if

      if $RC_CAN_YE.T4A_Sec_Sort  = '1'
          let $Sort2 = 'EE.LOCATION ASC'
          let $DispSort2 = 'Location; '
      end-if

      if $RC_CAN_YE.T4A_Sec_Sort  = '2'
         let $Sort2 = 'EE.DEPTID ASC'
         let $DispSort2 = 'Department; '
      end-if

      if $RC_CAN_YE.T4A_Sec_Sort  = '3'
         let $Sort2 = 'EE.MAIL_DROP ASC'
         let $DispSort2 = 'Mail Drop; '
      end-if

      if $RC_CAN_YE.T4A_Sec_Sort  = '4'
          let $Sort2 = 'EE.POSTAL ASC'
          let $DispSort2 = 'Postal Code; '
      end-if

      if $RC_CAN_YE.T4A_Sec_Sort  = '5'
          let $Sort2 = 'N'
      end-if

      if $RC_CAN_YE.T4A_Third_Sort  = '1'
          let $Sort3 = 'EE.SLIP_SURNAME ASC, EE.SLIP_FIRST_NAME ASC, EE.EMPLID ASC'
          let $DispSort3 = 'Employee Name; Employee ID'
      end-if

      if $RC_CAN_YE.T4A_Third_Sort  = '2'
         let $Sort3 = 'EE.EMPLID ASC'
         let $DispSort3 = 'Employee ID'
      end-if

      if $RC_CAN_YE.T4A_Third_Sort  = '3'
         let $Sort3 = 'EE.SIN ASC'
         let $DispSort3 = 'Employee SIN'
      end-if

  if $Sort2 = 'N'
     let $SortSequence =  $Sort1 || ', ' ||  $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort3
  else
     let $SortSequence = $Sort1 || ', ' || $Sort2 || ', ' || $Sort3
     let $DisplaySeq = $DispSort1 || $DispSort2 || $DispSort3
  end-if

end-if

    if $RC_CAN_YE.T4A_Primary_Sort  = '2'
          move 'R' to $SortCode
   else
      if $RC_CAN_YE.T4A_Primary_Sort  = '3'
         move 'C' to $SortCode
      else
         move ' ' to $SortCode
      end-if
   end-if

end-procedure


begin-procedure Declare-Printer-T4A

  alter-printer
    symbol-set    = 10U
    font          = 3
    point-size    = 12

end-procedure


begin-procedure Alter-Printer-T4A-Small-P9

  alter-printer
    symbol-set    = 10U
    font          = 4
    point-size    = 9

end-procedure


begin-procedure Alter-Printer-T4A-Reg

  alter-printer
    symbol-set    = 10U
    font          = 3
    point-size    = 12

end-procedure


begin-procedure Read-EEs

move 'AND EE.EMPLID in (''' to $E.SelectedEEs
move ' ' to $SelectedEmplID

begin-SELECT
CT.EMPLID

  move &CT.EMPLID     to $SelectedEmplID
  let $E.SelectedEEs = $E.SelectedEEs || $SelectedEmplID || ''','''
  move 'Y' to $EE_Selected

FROM  PS_RC_CTX910AP CT
WHERE CT.OPRID         = $Prcs_OprID
  AND CT.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
end-SELECT

let $E.SelectedEEs = SUBSTR($E.SelectedEEs,1,LENGTH($E.SelectedEEs) - 2)
let $E.SelectedEEs = $E.SelectedEEs || ')'

end-procedure


begin-procedure Check-Status

  move 'N' to $StatusOpen

begin-SELECT
CS.EMPLID

  move 'Y' to $StatusOpen

FROM PS_CAN_AMEND_T4A_S CS
WHERE CS.COMPANY         = &SL.COMPANY
  AND CS.EMPLID          = &SL.EMPLID
  AND CS.CALENDAR_YEAR   = &SL.CALENDAR_YEAR
  AND CS.SEQUENCE_NUMBER = &SL.SEQUENCE_NUMBER
  AND CS.WAGE_LOSS_PLAN  = &SL.WAGE_LOSS_PLAN
  AND CS.PROVINCE        = &SL.PROVINCE
  AND CS.CAN_YE_SLIP_SEQ = &SL.CAN_YE_SLIP_SEQ
  AND CS.AMEND_STATUS    = 'O'

end-SELECT

end-procedure


begin-procedure Close-Amend-Cancel-T4A

begin-SQL

  UPDATE PS_CAN_AMEND_T4A_S
  SET AMEND_STATUS       = 'C'
  WHERE COMPANY          = &SL.COMPANY
    AND EMPLID           = &SL.EMPLID
    AND CALENDAR_YEAR    = &SL.CALENDAR_YEAR
    AND SEQUENCE_NUMBER  = &SL.SEQUENCE_NUMBER
    AND WAGE_LOSS_PLAN   = &SL.WAGE_LOSS_PLAN
    AND PROVINCE         = &SL.PROVINCE
    AND CAN_YE_SLIP_SEQ  = &SL.CAN_YE_SLIP_SEQ

end-SQL

end-procedure


begin-procedure Get-XMLP-Definitions

  do Get-XML-URLID
  do Get-Report-Definitions

  move 'N'  to $RC_CAN_YE.Py_Ye_Rpt_Burst
  move 'N'  to $Py_Ye_Rpt_Burst
  if $OPT_SS.PY_YE_RPT_SS = 'Y'
     move $OPT_SS.REPORT_DEFN_ID to $Report_Defn_ID

!     do Check-Burst-Value
!     move $Py_Ye_Rpt_Burst  to $RC_CAN_YE.Py_Ye_Rpt_Burst

!     if $Py_Ye_Rpt_Burst <> 'Y'
!        move 'N' to $OPT_SS.PY_YE_RPT_SS
!        display 'No burst fieldname given for self-service report definition ' noline
!        display $OPT_SS.REPORT_DEFN_ID
!     end-if
  end-if

  move 'N'  to $Py_Ye_Rpt_Burst

  if #OPT_BLK.PY_RPT_BATCH_SIZE <> 0
     move $OPT_BLK.REPORT_DEFN_ID to $Report_Defn_ID
     do Check-Burst-Value

     if $Py_Ye_Rpt_Burst <> 'Y'
        display 'No burst fieldname given for batched report definition ' noline
        display $OPT_BLK.REPORT_DEFN_ID
     end-if
  end-if

end-procedure

begin-procedure Get-XML-URLID

! fetch XML data files URLID

  move ' '  to $OPT_XML.PY_YE_XMLURLID

begin-SELECT
OPT_XML.PY_YE_XMLURLID

  move &OPT_XML.PY_YE_XMLURLID      to $OPT_XML.PY_YE_XMLURLID

  exit-SELECT

FROM PS_PY_YE_OPT_TAX OPT_XML
WHERE OPT_XML.PY_TAX_YEAR     = &TX.BALANCE_YEAR
  AND OPT_XML.TAXFORM_ID      = 'A'
end-SELECT

  if $OPT_XML.PY_YE_XMLURLID = ' '
     display 'XML data file URLID is missing.'
  end-if

end-procedure

begin-procedure Get-Report-Definitions

! fetch definitions for self-service

  move 'N'  to $OPT_SS.PY_YE_RPT_SS
  move ' '  to $OPT_SS.REPORT_DEFN_ID
  move ' '  to $OPT_SS.TMPLDEFN_ID

begin-SELECT
OPT_SS.PY_YE_RPT_SS
OPT_SS.REPORT_DEFN_ID
OPT_SS.TMPLDEFN_ID
OPT_SS.PY_RPT_BATCH_SIZE

  move &OPT_SS.PY_YE_RPT_SS      to $OPT_SS.PY_YE_RPT_SS
  move &OPT_SS.REPORT_DEFN_ID    to $OPT_SS.REPORT_DEFN_ID
  move &OPT_SS.TMPLDEFN_ID       to $OPT_SS.TMPLDEFN_ID
!  move &OPT_SS.PY_RPT_BATCH_SIZE to #OPT_SS.PY_RPT_BATCH_SIZE

  exit-SELECT

FROM PS_PY_YE_OPT_RPT OPT_SS
WHERE OPT_SS.PY_TAX_YEAR     = &TX.BALANCE_YEAR
  AND OPT_SS.TAXFORM_ID      = 'A'
  AND OPT_SS.PY_W2_COPY_TYPE = $RC_CAN_YE.T4A_Copy_Type
  AND OPT_SS.PY_YE_RPT_SS    = 'Y'
end-SELECT

! fetch definitions for bulk report

  move ' '  to $OPT_BLK.REPORT_DEFN_ID
  move ' '  to $OPT_BLK.TMPLDEFN_ID
  move 0    to #OPT_BLK.PY_RPT_BATCH_SIZE

begin-SELECT
OPT_BLK.REPORT_DEFN_ID
OPT_BLK.TMPLDEFN_ID
OPT_BLK.PY_RPT_BATCH_SIZE

  move &OPT_BLK.REPORT_DEFN_ID    to $OPT_BLK.REPORT_DEFN_ID
  move &OPT_BLK.TMPLDEFN_ID       to $OPT_BLK.TMPLDEFN_ID
  move &OPT_BLK.PY_RPT_BATCH_SIZE to #OPT_BLK.PY_RPT_BATCH_SIZE

  exit-SELECT

FROM PS_PY_YE_OPT_RPT OPT_BLK
WHERE OPT_BLK.PY_TAX_YEAR     = &TX.BALANCE_YEAR
  AND OPT_BLK.TAXFORM_ID      = 'A'
  AND OPT_BLK.PY_W2_COPY_TYPE = $RC_CAN_YE.T4A_Copy_Type
  AND OPT_BLK.PY_YE_RPT_SS    <> 'Y'
end-SELECT

end-procedure

begin-procedure Check-Burst-Value
  let $Py_Ye_Rpt_Burst = 'N'
begin-SELECT
burst.BURST_FIELDNAME

  if rtrim(&burst.BURST_FIELDNAME, ' ') = ''
     let $Py_Ye_Rpt_Burst = 'N'
  else
     let $Py_Ye_Rpt_Burst = 'Y'
  end-if

  exit-SELECT

FROM PSXPRPTDEFN burst
WHERE burst.REPORT_DEFN_ID = $Report_Defn_ID
end-SELECT
end-procedure


begin-procedure Clear-Guide-Temp
  let $err-statement = 'Clear-Guide-Temp, DELETE PS_PY_SS_CAN_TMP'
begin-SQL on-error=Sql-Statement-Error
DELETE FROM PS_PY_SS_CAN_TMP
WHERE PY_YE_UPD_ACTION <> 'F'
AND TAXFORM_ID = 'A'
end-SQL
end-procedure


begin-procedure Insert-Guide-Data

  let $updateAction = 'I'
  let $NotifyFlag = 'N'
  do Get-Guide-Seqnum

  let $err-statement = 'Insert-Guide-Data, INSERT PS_PY_SS_CAN_TMP'
begin-SQL on-error=Sql-Statement-Error
INSERT INTO PS_PY_SS_CAN_TMP
(OPRID
,RUN_CNTL_ID
,PRCSINSTANCE
,PRCSNAME
,JOBINSTANCE
,EMPLID
,PY_TAX_YEAR
,COMPANY
,TAXFORM_ID
,SEQUENCE_NUMBER
,WAGE_LOSS_PLAN
,PROVINCE
,PY_YE_FORM_SEQNUM
,YE_FORM_ISSUE_DT
,YE_FORM_VIEWING_DT
,ATTACHSYSFILENAME
,ATTACHUSERFILE
,PY_YE_FILEURLID
,YE_SLIP_PROCESS
,PY_YE_REPORT_URL
,PY_YE_BURST_ID
,PROCESS_INSTANCE
,REPORT_DEFN_ID
,BURST_FIELDNAME
,PY_YE_NOTIFY_FLG
,PY_YE_UPD_ACTION
,PY_SLIP_FINAL_PRT)
Values
($Prcs_OprID
,$Prcs_Run_Cntl_ID
,#prcs_process_instance
,'CTX910AP'
,#prcs_job_instance
,&SL.EMPLID
,&SL.CALENDAR_YEAR
,&SL.COMPANY
,'A'      ! taxform id
,&SL.SEQUENCE_NUMBER
,&SL.WAGE_LOSS_PLAN
,' '                ! province
,0                  ! form sequence number
,$IssueDate
,$ViewingDate1             ! viewing date
,$FName                    ! attachsysfilename
,$FName                    ! attachuserfile
,$OPT_XML.PY_YE_XMLURLID   ! urlid
,&SL.YE_SLIP_PROCESS
,' '                ! report url
,$BurstValue
,0
,$OPT_SS.REPORT_DEFN_ID
,'BURST_VALUE'
,$NotifyFlag
,$updateAction
,$Slip_Final_Print)
end-SQL

end-procedure

begin-procedure Insert-YE-Runcontrol
  let $err-statement = 'Insert-YE-Runcontrol, INSERT PS_PY_YE_SLIP_RCTL'
begin-SQL on-error=Sql-Statement-Error
INSERT INTO PS_PY_YE_SLIP_RCTL
(OPRID
,RUN_CNTL_ID
,PRCSINSTANCE
,PRCSNAME
,JOBINSTANCE
,REPORT_DEFN_ID
,TMPLDEFN_ID
,PY_YE_SOURCEFILE
,PY_YE_SOURCELOC
,PY_YE_SRC_FILENAME
,PY_YE_OSPLATFORM
,FORM_ID
,T4_FINAL_PRINT
,T4A_FINAL_PRINT
,T4_COPY_TYPE
,PY_YE_RPT_BURST
,PY_YE_RPT_SS
,PY_RPT_DEFN_ID_SS
,PY_RPT_TMPL_ID_SS
,TAXFORM_ID
,PY_YE_SOURCEFILE2
,PY_YE_SRC_FILENAM2
,PY_SLIP_FINAL_PRT
,PY_YE_FILE_COUNT
,PY_YE_XMLURLID
,PY_TAX_YEAR
)
Values
($Prcs_OprID
,$Prcs_Run_Cntl_ID
,#prcs_process_instance
,'CTX910AP'
,#prcs_job_instance
,$OPT_BLK.REPORT_DEFN_ID
,$OPT_BLK.TMPLDEFN_ID
,$sourceFile2
,$sourceLocation2
,$sourceFileName2
,$yeOS
,'XMLP'
,$RC_CAN_YE.T4_Final_Print
,$RC_CAN_YE.T4A_Final_print
,$RC_CAN_YE.T4A_Copy_Type
,$RC_CAN_YE.Py_Ye_Rpt_Burst
,$OPT_SS.PY_YE_RPT_SS
,$OPT_SS.REPORT_DEFN_ID
,$OPT_SS.TMPLDEFN_ID
,'A'
,$sourceFile2
,$sourceFileName2
,$Slip_Final_Print
,0
,$OPT_XML.PY_YE_XMLURLID
,&TX.Balance_Year
)
end-SQL
end-procedure

begin-procedure Get-Setup-Viewing-Date
  let $yeopt.YE_FORM_VIEWING_DT = ''
  let $yeopt.PY_YE_SS_NOTE_DT   = ''

  let $Year4 = '1'
  do Format-DateTime('20491231', $ViewingDate, {DEFCMP},'','native')

begin-SELECT
yeopt.YE_FORM_VIEWING_DT
yeopt.PY_YE_SS_NOTE_DT

  let $yeopt.YE_FORM_VIEWING_DT = &yeopt.YE_FORM_VIEWING_DT
  let $yeopt.PY_YE_SS_NOTE_DT   = &yeopt.PY_YE_SS_NOTE_DT

  if $yeopt.YE_FORM_VIEWING_DT <> '' and
     $yeopt.YE_FORM_VIEWING_DT >= $IssueDate

     let $ViewingDate = $yeopt.YE_FORM_VIEWING_DT

  else
     if $yeopt.YE_FORM_VIEWING_DT <> '' and
        $yeopt.YE_FORM_VIEWING_DT < $IssueDate

        let $ViewingDate = $IssueDate

     end-if
  end-if

from PS_PY_YEOPT_TX_CAN yeopt
WHERE yeopt.COMPANY     = &SL.COMPANY
  AND yeopt.PY_TAX_YEAR = &SL.CALENDAR_YEAR
  AND yeopt.TAXFORM_ID  = 'A'
end-SELECT
end-procedure

begin-procedure Get-Guide-Seqnum
  let #gde.PY_YE_FORM_SEQNUM =  0
  let $ViewingDate1 = $ViewingDate

begin-SELECT
gde.PY_YE_FORM_SEQNUM
gde.YE_FORM_VIEWING_DT
gde.PY_YE_NOTIFY_FLG

  let #gde.PY_YE_FORM_SEQNUM = &gde.PY_YE_FORM_SEQNUM
  let $NotifyFlag = &gde.PY_YE_NOTIFY_FLG
  let $ViewingDate1 = &gde.YE_FORM_VIEWING_DT
  let $updateAction = 'C'

  exit-SELECT

FROM PS_PY_SS_CAN_GDE gde
WHERE gde.EMPLID          = &SL.EMPLID
  AND gde.PY_TAX_YEAR     = &SL.Calendar_Year
  AND gde.COMPANY         = &SL.COMPANY
  AND gde.TAXFORM_ID      = 'A'
  AND gde.SEQUENCE_NUMBER = &SL.SEQUENCE_NUMBER
  AND gde.WAGE_LOSS_PLAN  = &SL.WAGE_LOSS_PLAN
ORDER BY gde.PY_YE_FORM_SEQNUM DESC
end-SELECT

  if $updateAction <> 'C'
     let #gde.PY_YE_FORM_SEQNUM = #gde.PY_YE_FORM_SEQNUM + 1
  end-if

! It is possible that an Original is run more than once. Therefore,
! the forms must not be viewable until released.
  if $updateAction = 'C' and $NotifyFlag = 'N'

     let $ViewingDate1 = $ViewingDate
  end-if

end-procedure

begin-procedure Capture-OS
  let $yeOS = ' '

  #ifdef NT
     let $yeOS = 'NT'
  #end-if

  #ifdef MVS
     let $yeOS = 'MVS'
  #end-if

  #ifdef UNIX
     let $yeOS = 'UNIX'
  #end-if
end-procedure

begin-procedure Get-SS-Consent
  let $Consent = 'N'
begin-SELECT
CONS.YE_CONSENT_CURRENT

  move &CONS.YE_CONSENT_CURRENT to $Consent

FROM PS_PY_YE_CONS_CAN CONS
WHERE CONS.EMPLID = &SL.EMPLID
  AND CONS.LASTUPDDTTM =
      (SELECT MAX(CONS1.LASTUPDDTTM)
       FROM PS_PY_YE_CONS_CAN CONS1
       WHERE CONS1.EMPLID = CONS.EMPLID)
end-SELECT

  if $Consent = 'C' and $RC_CAN_YE.T4A_COPY_TYPE = '2'
     let $UserFlag = 'N'
  else
     let $UserFlag = 'Y'
  end-if
end-procedure


!----------------------------------
begin-procedure Sql-Statement-Error
!----------------------------------
  show $err-statement
  show ' Error : ' $sql-error
end-procedure


#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getdptnm.sqc'  !Get-Department-Name
#ifdef PRCSSCHD
#Include 'ctxrctl1.sqc' ! Get-Can-Tax YE Report Parameters
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls
#Include 'pyxmlbld.sqc'  !Routines for XML formatting
#endif
