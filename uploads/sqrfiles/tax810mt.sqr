!***********************************************************************
!  TAX810MT:  Qtrly Wage List - Tape/Diskette - MONTANA                *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:44:42                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup31.sqc' !Printer and page-size initialization
#include 'usarpt.sqc'  ! NID_COUNTRY defined

begin-report
  do Init-Report

! These variables are used by rptsmmry.sqc
  Let $Prnt_CoTxGrs = 'Y'
  Let $Prnt_CoExGrs = 'Y'

  do Process-Main

  close 10

  if #Count_F > 0
    display 'Upon successful conclusion of this program, one or more'
    display 'files will have been created, as follows:'
    display ''

    display '  TAX810MT.nnn  contain 275-character records in the format required'
    display '            for tape or diskette submission to the state of Montana,'

    display '            where "nnn" represents a volume number.'

    display ''
    display 'Follow the instructions from the department'
    display 'for submission of tapes or diskettes.'
    display ''
  else
    do Print-Summary-Data('NoData')
  end-if

  do Stdapi-Term

end-report

begin-procedure Init-Report
  move 'TAX810MT' to $ReportID
  move 'Montana Quarterly UI Wage Report - Tape/Diskette' to $ReportTitle
  display ''
  display 'Creating Tape/Diskette File for Montana UI Wage Reporting'

  do Init-DateTime
  do Init-Number

  do Get-Current-DateTime
  do Get-Calendar-Year-Id
  do Stdapi-Init

  do Initialization
end-procedure

begin-procedure Initialization
#ifdef TAXTEST
  move 'CA'              to $State
#else
  move 'MT'              to $State
#endif

  do Get-Tax-Reporting-Run-Controls
  move &TX.Balance_Year to $RptYear 9999
  move $RptYear          to $CalendarYr
  move $RptYear          to $RptYear ~~xx
  move $PeriodEndDate    to $AsOfDate

  move '0' to $Year4
  move '' to $DDelimiter
  do Format-DateTime($AsOfToday,  $CreationDate, {DEFMDY}, '', '')

  display ' '
  display 'Processing balances for '   noline
  display $RptQtrYr

  move 275 to #RecordLength

  if $PRCS_Process_Instance = ''
     do Prompts
  else
     do Select-Parameters
     do Convert-Parameters
  end-if

  do Evaluate-File-Type
  do Calculate-Diskette-Capacity
  do Open-File
end-procedure

begin-procedure Process-Main
begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

  move &TX.Company       to $Company  !TX.Company is the Transmitter
  if RTRIM($Company,' ') = ''
    display '*** Company field blank on Tax Run Control Record ***'
    display '***************** Processing stopped ****************'
    stop
  end-if
  do Get-Company-Data                 !Transmitter identifying data
  do Write-A-Record
  do Write-B-Record

begin-SELECT
A.COMPANY
A.DESCR
A.FEDERAL_EIN
A.ADDRESS1
A.CITY
A.STATE
A.POSTAL

  move &A.Company to $Company
  move &A.Descr   to $CompanyName

  do Read-Employee-Data

FROM  PS_COMPANY_TBL A
WHERE A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
  (SELECT MAX(EFFDT)
     FROM PS_COMPANY_TBL
     WHERE  COMPANY = A.COMPANY
       AND  EFFDT  <= $AsOfDate)
ORDER BY COMPANY
end-SELECT
  if #Count_F > 0
    do Write-F-Record
    do Print-Summary-Data('FileTotal')
  end-if
end-procedure

begin-procedure Read-Employee-Data
begin-SELECT
C.COMPANY    () on-break print=never before=Before-Company
                                      AFTER=AFTER-COMPANY
C.EMPLID
C.NLGRS_QTD
C.TXGRS_QTD
D1.NATIONAL_ID
D.LAST_NAME
D.FIRST_NAME
D.MIDDLE_NAME

  if &C.NlGrs_QTD > 0 and &C.TxGrs_QTD >= 0
    do Process-Employee
  else
    do Print-Summary-Data('NegWage')
  end-if

  if $FileType = 'D' and #RecordCount >= #MaxRecordsPerDiskette
    do Open-File
  end-if

FROM  PS_TAX_BALANCE C, PS_PERSON_NAME D, PS_PERS_NID D1
WHERE C.COMPANY       = &A.Company
  AND D.EMPLID        = C.EMPLID
  AND C.TAX_CLASS     = 'U'
  AND C.BALANCE_ID    = $Calendar_Year_Id
  AND C.BALANCE_YEAR  = &TX.Balance_Year
  AND C.BALANCE_QTR   = &TX.Balance_Qtr
#ifdef TAXTEST
  AND C.STATE         = 'CA'
#else
  AND C.STATE         = 'MT'
#endif
  AND C.LOCALITY      = ' '
  AND C.BALANCE_PERIOD  =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE
       WHERE  EMPLID        = C.EMPLID
         AND  COMPANY       = C.COMPANY
         AND  BALANCE_ID    = C.BALANCE_ID
         AND  BALANCE_YEAR  = C.BALANCE_YEAR
         AND  BALANCE_QTR   = C.BALANCE_QTR
         AND  STATE         = C.STATE
         AND  LOCALITY      = C.LOCALITY
         AND  TAX_CLASS     = C.TAX_CLASS)
  AND C.NLGRS_QTD <> 0
  AND D.EMPLID            = D1.EMPLID
  AND D1.COUNTRY = {NID_Country}
  AND D1.NATIONAL_ID_TYPE = $Payroll_NID_Type
ORDER BY D1.NATIONAL_ID
end-SELECT
end-procedure

begin-procedure Process-Employee
  move &D1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits
  if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
    move 'I        '   to $NATIONAL_ID
  else
    move &D1.NATIONAL_ID        to $NATIONAL_ID
  end-if

  let $LastName   = rtrim(&D.LAST_NAME, ' ')
  let $FirstName  = rtrim(&D.FIRST_NAME, ' ')
  let $MidInitial = {ps-substr}(&D.MIDDLE_NAME,1,1)
  uppercase $LastName
  uppercase $FirstName

  move &C.NlGrs_QTD to #NlGrs_QTD
  multiply 100 times #NlGrs_QTD
  do Format-Number(#NlGrs_QTD,  $NlGrs_QTD,  '09999999999999')
  add #NlGrs_QTD to #Co_NlGrs_QTD

  move &C.TxGrs_QTD to #TxGrs_QTD
  multiply 100 times #TxGrs_QTD
  do Format-Number(#TxGrs_QTD,  $TxGrs_QTD,  '09999999999999')
  add #TxGrs_QTD to #Co_TxGrs_QTD

  let #ExGrs_QTD =  #NlGrs_QTD - #TxGrs_QTD
  do Format-Number(#ExGrs_QTD,  $ExGrs_QTD,  '09999999999999')
  add #ExGrs_QTD to #Co_ExGrs_QTD

  do Write-S-Record
end-procedure

begin-procedure Before-Company
  do Get-Company-Data
  #ifdef TAXTEST
    move 'CA' to $State
  #else
    move 'MT' to $State
  #endif
  do Get-State-Tax-Data             !Employer ID
  if RTRIM(&Employer_ID_SUT,' ') = ''
    display '*** State Unemployment ID missing in Company State Tax Table ***'
    display '*********************** Processing stopped *********************'
    stop
  end-if
  move &Employer_ID_SUT to $StateEIN
  move &SUT_Experience_Rt to #Experience_Rt
  divide 100 into #Experience_Rt
  move #Experience_Rt to $Experience_Rt 0.99999
  let $Experience_Rt = SUBSTR($Experience_Rt,2,6)
  if $Prcs_Process_Instance  =  ''
     do Employer-Prompts
  else
     do Company-Run-Control
  end-if
  do Write-E-Record
end-procedure

begin-procedure After-Company
  if #Count_S > 0
    do Write-T-Record
  end-if

  divide 100 into #Co_NlGrs_QTD
  divide 100 into #Co_TxGrs_QTD
  divide 100 into #Co_ExGrs_QTD
  add #Co_NlGrs_QTD to #Tot_NlGrs_QTD
  add #Co_TxGrs_QTD to #Tot_TxGrs_QTD
  add #Co_ExGrs_QTD to #Tot_ExGrs_QTD

  do Print-Summary-Data('CoTotal')

  add #Count_S to #Count_F

  move 0 to #Co_NlGrs_QTD
  move 0 to #Co_TxGrs_QTD
  move 0 to #Co_ExGrs_QTD
  move 0 to #Count_S
  new-page
end-procedure

begin-procedure Write-A-Record
  move $CompanyName    to $TransmitterName
  move &CT.Federal_EIN to $FedEIN 099999999
  move $CompnyAdd1    to $TransmitterAddress
  move $CompnyCity        to $TransmitterCity
  let $TransmitterState = rtrim(&CT.State, ' ')
  move &CT.Postal      to $Zip

  move $Zip            to $ZipExtsn ~~~~~~xxxx
  if instr($Zip,'-',6) = 0
  and $ZipExtsn <> '    '
    let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if
  move $Zip            to $ZipExtsn ~~~~~xxxxx

  uppercase $TransmitterName
  uppercase $TransmitterAddress
  uppercase $TransmitterCity
  uppercase $TransmitterContact

  write 10 from    'A':1
           $CalendarYr:4
               $FedEIN:9
                'UTAX':4
                   $Sp:5
      $TransmitterName:50
   $TransmitterAddress:40
      $TransmitterCity:25
     $TransmitterState:2
                   $Sp:13
                  $Zip:5
             $ZipExtsn:5
   $TransmitterContact:30
     $TransmitterPhone:10
  $TransmitterPhoneExt:4
                   $Sp:6
                   $Sp:1
                   $Sp:5
                   $Sp:1
                   $Sp:9
                   $Sp:13
         $CreationDate:6
                   $Sp:27

  add 1 to #RecordCount
end-procedure

begin-procedure Write-B-Record

  write 10 from   'B':1
          $CalendarYr:4
              $FedEIN:9
        $Computer_Mfr:8
          $Tape_Label:2
                  $Sp:1
             $Density:2
        $Recording_Cd:3
              $Tracks:2
     $Blocking_Factor:2
               'UTAX':4
                  $Sp:108
     $TransmitterName:44
  $TransmitterAddress:35
     $TransmitterCity:20
    $TransmitterState:2
                  $Sp:5
                 $Zip:5
            $ZipExtsn:5
                  $Sp:13

  add 1 to #RecordCount
end-procedure

begin-procedure Write-E-Record
  move &A.Federal_EIN  to $FedEIN 099999999
  move &A.Descr        to $ErName
  move &A.Address1     to $ErAddress
  move &A.City         to $ErCity
  let $ErState = rtrim(&A.State, ' ')
  move &A.Postal       to $Zip

  move $Zip            to $ZipExtsn ~~~~~~xxxx
  if instr($Zip,'-',6) = 0
  and $ZipExtsn <> '    '
    let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if
  move $Zip            to $ZipExtsn ~~~~~xxxxx

  uppercase $ErName
  uppercase $ErAddress
  uppercase $ErCity

  move '1' to $NWNW         !E record will be followed by S record

  if #TaxesDue > 0
    move 'B' to $TypeofInfo
  else
    move 'W' to $TypeofInfo
  end-if

  write 10 from 'E':1
   &TX.Balance_Year:4
            $FedEIN:9
                $Sp:9
            $ErName:50
         $ErAddress:40
            $ErCity:25
           $ErState:2
                $Sp:8
          $ZipExtsn:5
               $Zip:5
                $Sp:1
                $Sp:1
   $Blocking_Factor:2
                $Sp:4
             'UTAX':4
               '30':2
          $StateEIN:15
       $QtrEndMonth:2
              $NWNW:1
                $Sp:1
                $Sp:5
                $Sp:7
                $Sp:5
                $Sp:47
                $Sp:1
        $TypeofInfo:1
                $Sp:18

  move 'Y' to $RecordWritten
  add 1 to #Count_E
  add 1 to #RecordCount
end-procedure

begin-procedure Write-S-Record
  add 1 to #Count_S

  write 10 from 'S':1
       $NATIONAL_ID:9
          $LastName:20
         $FirstName:12
        $MidInitial:1
               '30':2
       $QtrEndMonth:2
           $RptYear:2
   '00000000000000':14
         $NlGrs_QTD:14
         $ExGrs_QTD:14
         $TxGrs_QTD:14
  '000000000000000':15
        '000000000':9
                $Sp:2
                $Sp:3
                $Sp:4
                $Sp:4
             'UTAX':4
          $StateEIN:15
                $Sp:114

  add 1 to #RecordCount
end-procedure

begin-procedure Write-T-Record
  move #Count_S to $Count_S 0999999
  move #Co_NlGrs_QTD to $Co_NlGrs_QTD 09999999999999
  move #Co_TxGrs_QTD to $Co_TxGrs_QTD 09999999999999
  move #Co_ExGrs_QTD to $Co_ExGrs_QTD 09999999999999

  write 10 from 'T':1
           $Count_S:7
             'UTAX':4
   '00000000000000':14
      $Co_NlGrs_QTD:14
      $Co_ExGrs_QTD:14
      $Co_TxGrs_QTD:14
    '0000000000000':13
     $Experience_Rt:6
          $TaxesDue:13
   $PrevQtrUnderpay:11
          $Interest:11
           $Penalty:11
            $Credit:11
                $Sp:4
      '00000000000':11
                $Sp:4
      '00000000000':11
      $TotalPayment:11
    '0000000000000':13
   '00000000000000':14
   '00000000000000':14
       $Month1Count:7
       $Month2Count:7
       $Month3Count:7
                $Sp:3
                $Sp:7
               $DCN:10
                $Sp:8

  add 1 to #RecordCount
  move 0  to #T_Gross
end-procedure

begin-procedure Write-F-Record

  write 10 from 'F':1
       '0000000000':10
       '0000000000':10
                $Sp:4
  '000000000000000':15
  '000000000000000':15
  '000000000000000':15
  '000000000000000':15
  '000000000000000':15
  '000000000000000':15
                $Sp:8
                $Sp:8
                $Sp:8
                $Sp:136
end-procedure

begin-procedure Prompts
  display 'Enter Transmitter Contact Information:'
  input $TransmitterContact  maxlen=30 '  Contact name (30 char max)'
  input $TransmitterPhone    maxlen=10 '  Contact phone (e.g. 9995551212)'
  input $TransmitterPhoneExt maxlen=4  '  Contact 4-digit extension'

  while $FileType = ''
    input $FileType 'Tape, cartridge or diskette file? (T, C, D or Q to quit)'
    uppercase $FileType
    if INSTR('TDCQ',$FileType,1) = 0
      display ' '
      display '***** Enter T, D, C or Q *****'
      display ' '
      move '' to $FileType
    end-if
  end-while
end-procedure

begin-procedure Convert-Parameters

  move $RC_QTR_UI.Contact_Person   to $TransmitterContact
  move $RC_QTR_UI.Contact_Phone    to $TransmitterPhone
  move $RC_QTR_UI.Contact_Phone_Ext to $TransmitterPhoneExt
  move $RC_QTR_UI.Diskette_Type    to $DisketteType
  move $RC_QTR_UI.Reporting_Medium to $FileType

  unstring  $TransmitterPhone by '/' into $str1 $str2
  unstring $str2 by '-' into $str3 $str4
  let $TransmitterPhone = $str1 || $str3 || $str4
end-procedure

begin-procedure Evaluate-File-Type

  evaluate $FileType
    when = 'Q'
      stop

    when = 'D'
      if $Prcs_Process_Instance = ''
         do Get-Diskette-Format
      end-if
      move &TX.Computer_Mfr      to $Computer_Mfr
      move &TX.Recording_Cd      to $Recording_Cd
      move &TX.Blocking_Factor   to $Blocking_Factor 09
      break

    when = 'T'
      move &TX.Tape_Density      to $Density
      move &TX.Recording_Cd      to $Recording_Cd
      move 'SL'                  to $Tape_Label
      move &TX.Computer_Mfr      to $Computer_Mfr
      move &TX.Blocking_Factor   to $Blocking_Factor 09
      move 'MT'                  to $FilingType
      move '09'                  to $Tracks
      break

    when = 'C'
      move &TX.Tape_Density      to $Density
      move &TX.Recording_Cd      to $Recording_Cd
      move 'SL'                  to $Tape_Label
      move &TX.Computer_Mfr      to $Computer_Mfr
      move &TX.Blocking_Factor   to $Blocking_Factor 09
      move 'MC'                  to $FilingType
      move '18'                  to $Tracks

  end-evaluate
end-procedure

begin-procedure Employer-Prompts
  display ' '
  display 'Enter quarterly data for ' noline
  display $Company noline
  display ':'
  display '(Numbers should include decimal and cents, but no commas, e.g. 1234.00)'
  input $TaxesDue        '   Quarterly Contribution Due'
  input $PrevQtrUnderpay '   Previous Quarter Underpayment'
  input $Interest        '   Interest Due'
  input $Penalty         '   Penalty Due'
  input $Credit          '   Previous Credit to apply to balance due'
  input $TotalPayment    '   Total Payment Due'
  input $Month1Count     '   Month 1 employee count'
  input $Month2Count     '   Month 2 employee count'
  input $Month3Count     '   Month 3 employee count'
  input $DCN             '   Document Control Number'
  uppercase $DCN

  move $TaxesDue        to #TaxesDue
  move $PrevQtrUnderpay to #PrevQtrUnderpay
  move $Interest        to #Interest
  move $Penalty         to #Penalty
  move $Credit          to #Credit
  move $TotalPayment    to #TotalPayment
  move $Month1Count     to #Month1Count
  move $Month2Count     to #Month2Count
  move $Month3Count     to #Month3Count

  let $TaxesDue        = EDIT(#TaxesDue        * 100,'0999999999999')
  let $PrevQtrUnderpay = EDIT(#PrevQtrUnderpay * 100,'09999999999')
  let $Interest        = EDIT(#Interest        * 100,'09999999999')
  let $Penalty         = EDIT(#Penalty         * 100,'09999999999')
  let $Credit          = EDIT(#Credit          * 100,'09999999999')
  let $TotalPayment    = EDIT(#TotalPayment    * 100,'09999999999')

  move #Month1Count     to $Month1Count     0999999
  move #Month2Count     to $Month2Count     0999999
  move #Month3Count     to $Month3Count     0999999
end-procedure

begin-procedure Company-Run-Control
begin-select
MT.UI_TAXES_DUE
MT.UI_PREV_QTR_UNDRPY
MT.UI_INTEREST_DUE
MT.UI_PENALTY_DUE
MT.UI_PREVIOUS_CREDIT
MT.UI_TOTAL_PAYMENT
MT.UI_MONTH1_COUNT
MT.UI_MONTH2_COUNT
MT.UI_MONTH3_COUNT
MT.STATE_DOC_CNTL_NBR
FROM PS_RC_TAX810MT MT
WHERE MT.OPRID = $Prcs_OprID
  AND MT.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
  AND MT.COMPANY = &C.Company
end-SELECT

  move &MT.UI_Taxes_Due        to #TaxesDue
  move &MT.UI_Prev_Qtr_Undrpy  to #PrevQtrUnderpay
  move &MT.UI_Interest_Due     to #Interest
  move &MT.UI_Penalty_Due      to #Penalty
  move &MT.UI_Previous_Credit  to #Credit
  move &MT.UI_Total_Payment    to #TotalPayment
  move &MT.UI_Month1_Count     to #Month1Count
  move &MT.UI_Month2_Count     to #Month2Count
  move &MT.UI_Month3_Count     to #Month3Count
  move &MT.State_Doc_Cntl_Nbr     to $DCN
  uppercase $DCN

  let $TaxesDue        = EDIT(#TaxesDue        * 100,'0999999999999')
  let $PrevQtrUnderpay = EDIT(#PrevQtrUnderpay * 100,'09999999999')
  let $Interest        = EDIT(#Interest        * 100,'09999999999')
  let $Penalty         = EDIT(#Penalty         * 100,'09999999999')
  let $Credit          = EDIT(#Credit          * 100,'09999999999')
  let $TotalPayment    = EDIT(#TotalPayment    * 100,'09999999999')

  move #Month1Count     to $Month1Count     0999999
  move #Month2Count     to $Month2Count     0999999
  move #Month3Count     to $Month3Count     0999999
end-procedure


begin-procedure Open-File
if $FileType='D'
  move 0 to #RecordCount
  if #FileExtension <> 0
    close 10
  end-if
  add 1 to #FileExtension
  move #FileExtension to $FileExtension 099
  #ifdef OS400
     let $FileExtension = '(D' || $FileExtension || ')'
     let $FileID = '{IMPORTPREFIX}' || 'TAX810MT' || $FileExtension
  #else
     let $FileID = '{IMPORTPREFIX}' || 'TAX810MT.' || $FileExtension
  #endif
  open $FileID as 10 for-writing record=275:fixed
else
  open '{IMPORTPREFIX}TAX810MT{IMPORTSUFFIX}' as 10 for-writing record=275:fixed
end-if
end-procedure

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'rptsmmry.sqc'  !Print-Summary-Data procedure
#Include 'getfrmat.sqc'  !Get-Diskette-Format procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Get-Run-Control procedure
