!***********************************************************************
!  PER036.SQR                                                          *
!  PRWORA - New Hire Reporting                                         *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/07/11:02:50:14                           !
!             $Release:  HR9                                           !
!            $Revision:  102                                           !
!                                                                      *
!***********************************************************************
! ACTION:                                                              *
!     (1) NEW HIRE REPORTING TO CONFORM TO THE PERSONAL RESPONSIBILITY *
!      AND WORK OPPORTUNITY ACT OF 1996.                               *
!                                                                      *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'        ! SET ENVIRONMENT
begin-setup
#Include 'ptpsl177.sqc'   !Printer and page-size initialization
end-setup

BEGIN-REPORT

  DO INIT-DATETIME
  DO INIT-NUMBER
  DO GET-CURRENT-DATETIME
  DO INIT-REPORT
  DO PROCESS-MAIN
  DO RESET
  DO STDAPI-TERM
  
 !BEGIN Incident 633432
 
  DO DELETE-SQR-RECORD
  
 !END Incident 633432

END-REPORT

BEGIN-PROCEDURE INIT-REPORT

  DO STDAPI-INIT

  IF $PRCS_PROCESS_INSTANCE = ''
     DO ASK-FROM-THRU-DATE
     DO GET-PER036-PARM
  ELSE
     DO SELECT-PARAMETERS
  END-IF

  MOVE 'PER036' TO $REPORTID
 !Begin Incident 1529628002
 ! IF $REPORTING_STATE > ' ' 
  IF $REPORTING_STATE <> '$U' 
 !End Incident 1529628002
    MOVE 'PRWORA - State New Hire Reporting' TO $REPORTTITLE
  ELSE
 !Begin Incident 1529628002
 !   MOVE 'PRWORA - New Hire Reporting' TO $REPORTTITLE
    MOVE 'PRWORA - Federal New Hire Reporting' TO $REPORTTITLE
 !End Incident 1529628002
 END-IF

  DISPLAY $REPORTTITLE

END-PROCEDURE INIT-REPORT

!***********************************************************************
BEGIN-PROCEDURE GET-VALUES
!***********************************************************************

    DO GET-FROM-THRU-DATE
    DO GET-PER036-PARM
    LET $REPORTING_STATE = $STATE

END-PROCEDURE

BEGIN-HEADING 6

 #include 'stdhdg08.sqc'

  PRINT 'EMPLOYER'             (+3,1,8)
  
  !BEGIN Incident 633432
  !ADDED THREE MORE HEADERS AND MODIFIED THE RCW
   
  PRINT 'FEDERAL EIN'          (0,32,0)
  PRINT 'LAST_NAME'            (0,44,0)
  PRINT 'FIRST_NAME'           (0,70,0)
  PRINT 'MIDDLE_NAME'          (0,96,0)
  PRINT 'STREET'               (0,122,0)
  PRINT 'CITY'                 (0,144,0)
  PRINT 'STATE'                (0,158,0)
  PRINT 'POSTAL'               (0,164,0)
  PRINT 'NATIONAL ID'          (0,172,0)
  
  !END Incident 633432
  

END-HEADING

!************************************************************************************
BEGIN-PROCEDURE PROCESS-MAIN
!************************************************************************************

   Do Get-New-Hire-Data

END-PROCEDURE

!************************************************************************************
BEGIN-PROCEDURE Get-New-Hire-Data
!************************************************************************************

BEGIN-SELECT 
A.COMPANY              
   Let $CompName     =  rtrim(&A.COMPANY, ' ')
   
!BEGIN  Incident 633432    
! Adding 3 more fields to the sql and  the RCW also to been modified for  the fields
    If $Old_Comp <> $CompName
         Do Print-Company-Info
   end-if
   
A.LAST_NAME           &A.LAST_NAME
   let $Last_Name         = rtrim(&A.LAST_NAME, ' ')
   PRINT $Last_Name        (+1,44,25)  
   
A.FIRST_NAME           &A.FIRST_NAME 
   let $First_Name         = rtrim(&A.FIRST_NAME , ' ')
   PRINT $First_Name        (0,70,25)  
   
A.MIDDLE_NAME           &A.MIDDLE_NAME
   let $Middle_Name         = rtrim(&A.MIDDLE_NAME, ' ')
   PRINT $Middle_Name       (0,96,25)  
   
A.NATIONAL_ID
   Let $SSN = &A.NATIONAL_ID

A.ADDRESS1
   Let $Address1 = rtrim(&A.ADDRESS1, ' ')
   PRINT $Address1         (0,122,21)
   
! END Incident 633432 

A.ADDRESS2
   Let $Address2 = rtrim(&A.ADDRESS2, ' ')
A.ADDRESS3
   Let $Address3 = rtrim(&A.ADDRESS3, ' ')
A.CITY
   Let $City     = rtrim(&A.CITY, ' ')
A.STATE
   Let $State    = rtrim(&A.STATE, ' ')
A.POSTAL
   Let $Postal = {ps-substr}(&A.POSTAL,1,5)
   
! BEGIN Incident 633432
! RCW has been modified

   PRINT $City             (0,144,13)
   Print $State            (0,158,5)
   Print $Postal           (0,164,7)
   PRINT $SSN              (0,172)
   
!END Incident 633432
   
   if $Address2 <> ''
      PRINT $Address2      (+1,63)
   end-if
   if $Address3 <> ''
      PRINT $Address3      (+1,63)
   end-if

! BEGIN Incident 633432
! Record has been changed

FROM PS_HR_RFWPRW_SQR A

!END Incident 633432

 ORDER BY A.COMPANY, A.STATE

END-SELECT

END-PROCEDURE Get-New-Hire-Data
!***********************************************************************
BEGIN-PROCEDURE Print-Company-Info
!***********************************************************************

BEGIN-SELECT 
C1.COMPANY              &C1.COMPANY
C1.DESCR
   Let $CompDesc     =  rtrim(&C1.DESCR, ' ')
C1.ADDRESS1
   Let $CompAddress1 =  rtrim(&C1.ADDRESS1, ' ')
C1.ADDRESS2
   Let $CompAddress2 =  rtrim(&C1.ADDRESS2, ' ')
C1.ADDRESS3
   Let $CompAddress3 =  rtrim(&C1.ADDRESS3, ' ')
C1.ADDRESS4
   Let $CompAddress4 =  rtrim(&C1.ADDRESS4, ' ')
C1.CITY
   Let $CompCity     =  rtrim(&C1.CITY, ' ')
C1.STATE
   Let $CompState    =  rtrim(&C1.STATE, ' ')
C1.POSTAL
   Let $CompPostal   = {ps-substr}(&C1.POSTAL,1,5)
C1.FEDERAL_EIN
   Let $FEIN         = edit(&C1.FEDERAL_EIN, '999999999')

  Let $CompZip = $CompCity || ', ' || $CompState || ' ' || $CompPostal
  
 !BEGIN Incident 633432
 
  print $CompDesc              (+1, 1,30)
  
 !END Incident 633432
 
  PRINT $CompAddress1          (+1,1)
  if $CompAddress2 <> ''
       PRINT $CompAddress2     (+1,1)
  end-if
  if $CompAddress3 <> ''
       PRINT $CompAddress3     (+1,1)
  end-if
  PRINT $CompZip               (+1,1,28)
  
 !BEGIN Incident 633432
  PRINT $FEIN                  (0,31)       Edit 099999999
  
 !END Incident 633432

From PS_COMPANY_TBL C1
where C1.COMPANY       = $CompName AND
      C1.EFFDT = (SELECT MAX(C2.EFFDT)
                 FROM PS_COMPANY_TBL C2
                 WHERE C2.COMPANY = C1.COMPANY AND
                  C2.EFFDT <= $THRUDATE)
End-Select

Let $Old_Comp = $CompName

END-PROCEDURE Print-Company-Info

!BEGIN Incident 633432

!***********************************************************************
BEGIN-PROCEDURE DELETE-SQR-RECORD
!***********************************************************************
begin-SQL 
 DELETE FROM PS_HR_RFWPRW_SQR
end-SQL
END-PROCEDURE DELETE-SQR-RECORD

!END Incident 633432

#include 'hrrnctl1.sqc'  ! GET RUN CONTROL PARAMETER VALUES
#include 'hrgetval.sqc'  ! GET VALUES MASK ROUTINES
#include 'askftd.sqc'    ! FROM THRU DATE INPUT
#include 'reset.sqc'     ! RESET PRINTER PROCEDURE
#include 'stdapi.sqc'    ! UPDATE PROCESS API
#include 'curdttim.sqc'  ! GET-CURRENT-DATETIME PROCEDURE
#include 'datetime.sqc'  ! ROUTINES FOR DATE AND TIME FORMATTING
#include 'number.sqc'    ! ROUTINES TO FORMAT NUMBERS
#include 'datemath.sqc'  ! ROUTINES FOR DATEMATH
