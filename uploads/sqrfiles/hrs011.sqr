!***********************************************************************
!  HRS011:  Applicant Summary By Job Opening                           *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/09/27:00:09:28                                 !
!       $Release:  HR9                                                 !
!      $Revision:  104                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
Begin-Setup
#Include 'ptpsl177.sqc'  !Printer and page-size initialization
End-Setup

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-Heading 8
  do Get_Field_information ('HRS011',   'REPORT_TITLE', $REPORTTITLE,   #DW)
  #Include 'stdhdgtr.sqc'
  do Get_Field_information ('HRS011', 'REQ_NBR',$REQ_NBR,                       #DW)
  do Get_Field_information ('HRS011', 'SOURCE',$SOURCE,                                 #DW)
  do Get_Field_information ('HRS011', 'EMPL_NAME',$EMPL_NAME,                   #DW)
  do Get_Field_information ('HRS011', 'SPEC_SOURCE', $SPEC_SOURCE,      #DW)
  do Get_Field_information ('HRS011', 'APPLICANT_TOT', $APPLICANT_TOT,  #DW)
  do Get_Field_information ('HRS011', 'APPL_SPC_SRC', $APPL_SPC_SRC,    #DW)
  do Get_Field_information ('HRS011', 'APPL_PERC', $APPL_PERC,                  #DW)

  print $REQ_NBR                (+3, 1,11)
  print $SOURCE                 (  ,13,30)
  print $EMPL_NAME              (  ,44,51)
  print $SPEC_SOURCE            (,  95,30)
  print $APPLICANT_TOT          (,  126,27)
  print $APPL_PERC              (,  154)
  print $APPL_SPC_SRC           (+1,126,27)
  print $APPL_SPC_SRC           (  ,154)

end-Heading

begin-procedure Init-Report

  move 'HRS011' to $ReportID
  move $BaseCurr to $ReportCurr
  move $BaseRtType to $ReportRtType
  move 'W' to $ReportMsgType  !Warning

  do Stdapi-Init

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if
  do Init_Printer
  do Get-Values
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

end-procedure

begin-procedure Get-Values
do Get-Current-Language
let $language_cd = $curr_language_cd
end-procedure

!***********************************************************************
!  PROC Process-Main
!         Main Procedure
!***********************************************************************

Begin-Procedure Process-Main
Begin-SELECT
A.HRS_PERSON_ID
A.HRS_PROFILE_SEQ
A.HRS_JOB_OPENING_ID                                                   
    print &A.HRS_JOB_OPENING_ID                       (1,1,11)

D.HRS_SOURCE_ID,

  if $Curr_Language_Cd = $PSOptions_Language_Cd
     do Get-Src-Descr
     print &S.HRS_SOURCE_DESCR                           ( ,13,30)
  else
     do Get-Src-Descr-Lang
     print &SL.HRS_SOURCE_DESCR                          ( ,13,30)
  end-if

!A.EMPLID
 ! !if (&A.REFERRAL_SOURCE = 'EE')
  do Get-Emplid
  let $EMPLID = RTRIM(&E1.EMPLID, ' ')
  DO Get-Employee-Name
  PRINT $Name                                            ( ,44,50)
  !end-if

D.HRS_SUBSOURCE_ID,

  if $Curr_Language_Cd = $PSOptions_Language_Cd
     do Get-Sub-Cat-Descr
     print &SS.HRS_SUBSOURCE_DESC                        ( ,95,34)
  else
     do Get-SubDescr-Lng
     print &SSL.HRS_SUBSOURCE_DESC                       ( ,95,34)
  end-if

COUNT(*)        &APPL_SPCSRC
  LET #APPL_SPCSRC =  &APPL_SPCSRC
  Do Format-Number(#APPL_SPCSRC, $out, '99999999')
  print $out (0,130)

  do Get-APPL-REQSOURCE
  if &TotalApplSrc <> 0 
  LET #PercApplSrc = (&APPL_SPCSRC / &TotalApplSrc)
  LET #PercApplSrc =  #PercApplSrc * 100
  Do Format-Number(#PercApplSrc, $out, '999.99')
  print $out (0,160)
  LET $Percent =  '%'
  print $Percent                                                                (0,167)
  end-if
  NEXT-LISTING SKIPLINES=1 NEED=8

FROM !PS_HRS_EE_REF_JO A,
PS_HRS_RCMNT_I A,
PS_HRS_APP_PROF_I B,
PS_HRS_AL_LOG C,
PS_HRS_AL_LOG_SRCE D
WHERE A.HRS_PERSON_ID = B.HRS_PERSON_ID
AND A.HRS_PROFILE_SEQ = B.HRS_PROFILE_SEQ
AND B.HRS_AL_LOG_ID = C.HRS_AL_LOG_ID
AND B.HRS_AL_LOG_ID = D.HRS_AL_LOG_ID
GROUP BY A.HRS_JOB_OPENING_ID, D.HRS_SOURCE_ID, D.HRS_SUBSOURCE_ID, A.HRS_PERSON_ID, A.HRS_PROFILE_SEQ
Order BY A.HRS_JOB_OPENING_ID, A.HRS_PERSON_ID, A.HRS_PROFILE_SEQ
END-SELECT

END-PROCEDURE

!***********************************************************************
!  PROC Get-APPL-REQSOURCE
!***********************************************************************

BEGIN-PROCEDURE Get-APPL-REQSOURCE

BEGIN-SELECT
COUNT(*) &TotalApplSrc

FROM PS_HRS_RCMNT_I E,
PS_HRS_APP_PROF_I B1,
PS_HRS_AL_LOG C1
WHERE E.HRS_JOB_OPENING_ID = &A.HRS_JOB_OPENING_ID
AND E.HRS_PERSON_ID = B1.HRS_PERSON_ID
AND E.HRS_PROFILE_SEQ = B1.HRS_PROFILE_SEQ
AND B1.HRS_AL_LOG_ID = C1.HRS_AL_LOG_ID
and C1.HRS_SOURCE_ID = &D.HRS_SOURCE_ID

END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-Emplid
!***********************************************************************

BEGIN-PROCEDURE Get-Emplid

BEGIN-SELECT
E1.EMPLID

FROM PS_HRS_EE_REFERRAL E1
WHERE E1.HRS_PERSON_ID = &A.HRS_PERSON_ID
AND E1.HRS_PROFILE_SEQ = &A.HRS_PROFILE_SEQ

END-SELECT
END-PROCEDURE


!***********************************************************************
!  PROC Get-Sub-Cat-Descr
!***********************************************************************

BEGIN-PROCEDURE Get-Sub-Cat-Descr

BEGIN-SELECT
SS.HRS_SUBSOURCE_DESC
FROM PS_HRS_SUBSOURCE SS
WHERE SS.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
AND   SS.HRS_SUBSOURCE_ID = &D.HRS_SUBSOURCE_ID
AND  SS.EFFDT = (SELECT MAX(SS1.EFFDT)
               FROM PS_HRS_SUBSOURCE SS1
               WHERE SS1.HRS_SOURCE_ID = SS.HRS_SOURCE_ID
               AND SS1.HRS_SUBSOURCE_ID = SS.HRS_SUBSOURCE_ID
               AND SS1.EFFDT <= $AsOfToday)
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-SubDescr-Lng
!***********************************************************************

BEGIN-PROCEDURE Get-SubDescr-Lng

BEGIN-SELECT
SSL.HRS_SUBSOURCE_DESC
FROM PS_HRS_SUBSRCE_LNG SSL
WHERE SSL.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
AND   SSL.HRS_SUBSOURCE_ID = &D.HRS_SUBSOURCE_ID
AND  SSL.EFFDT = (SELECT MAX(SS2.EFFDT)
               FROM PS_HRS_SUBSRCE_LNG SS2
               WHERE SS2.HRS_SOURCE_ID = SSL.HRS_SOURCE_ID
               AND SS2.HRS_SUBSOURCE_ID = SSL.HRS_SUBSOURCE_ID
               AND SS2.LANGUAGE_CD = $Curr_Language_Cd
               AND SS2.EFFDT <= $AsOfToday)
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-Src-Descr
!***********************************************************************
BEGIN-PROCEDURE Get-Src-Descr
BEGIN-SELECT
S.HRS_SOURCE_DESCR 
FROM PS_HRS_SOURCE S
WHERE S.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
AND S.EFFDT = (SELECT MAX(S1.EFFDT)
               FROM PS_HRS_SOURCE S1
               WHERE S1.HRS_SOURCE_ID = S.HRS_SOURCE_ID
               AND S1.EFFDT <= $AsOfToday)
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-Src-Descr-Lang
!***********************************************************************
BEGIN-PROCEDURE Get-Src-Descr-Lang
BEGIN-SELECT
SL.HRS_SOURCE_DESCR 
FROM PS_HRS_SOURCE_LNG SL
WHERE SL.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
AND SL.EFFDT = (SELECT MAX(S2.EFFDT)
               FROM PS_HRS_SOURCE_LNG S2
               WHERE S2.HRS_SOURCE_ID = SL.HRS_SOURCE_ID
               AND S2.LANGUAGE_CD = $Curr_Language_Cd
               AND S2.EFFDT <= $AsOfToday)
END-SELECT
END-PROCEDURE


#include 'prcslng.sqc'   !Get language code
#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines

#Include 'getempnm.sqc'  !Get-Employee-Name and Get-Applicant-Name
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'  !Routines to Translate SQR
#Include 'useprntr.sqc'
