! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
! ICONV:   Convert Pre PT 8.1 Panels to PIA Pages                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'          ! Set environment

begin-setup
#include 'ptset01.sqc'       ! Init printer and page size
end-setup

BEGIN-REPORT

  DISPLAY 'ICONV.SQR - PeopleSoft Panel to Page Conversion Program...'

  LET $DEBUG = 'N'

  DO GET-CURRENT-DATETIME
  DISPLAY 'Start Time:'  NOLINE
  DISPLAY $SYSDATETIME

  DO INITIALIZE

  IF $RUNCONTROLFOUND = 'Y'

    DO LOAD-AND-PROCESS-PANELS

    IF $ICONV_DOPNLGROUPS = 'Y'
      DO SELECT-PNLGRPS-FOR-PNLGRP-DEFAULTS
    END-IF

  ELSE
    DISPLAY '*** --- Error - No Run Control Parameters Found --- ***'
  END-IF

  DO UPDATE-SYS-VERSION
  DO FORCE-CACHE-REFRESH
  DO PROCESS-STATUS-UPDATE

  DO GET-CURRENT-DATETIME
  DISPLAY 'End Time: ' NOLINE
  DISPLAY $SYSDATETIME

END-REPORT

!-----------------------------------------------------------------

BEGIN-PROCEDURE INITIALIZE

IF $DEBUG = 'Y'
  DISPLAY 'INITIALIZE'
END-IF

  LET $SQ = ''''

  DO ICONV-PROCESS-SCHEDULER

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE ICONV-PROCESS-SCHEDULER

IF $DEBUG = 'Y'
  DISPLAY 'ICONV-PROCESS-SCHEDULER'
END-IF

  CREATE-ARRAY NAME=PANELS SIZE=10000 FIELD=PNLNAME:CHAR
        FIELD=PNLTYPE:NUMBER

  DO INIT-DATETIME
  DO GET-CURRENT-DATETIME
  DO DEFINE-PRCS-VARS
  DO GET-RUN-CONTROL-PARMS

  LET $PRCSINSTANCE = $PRCS_PROCESS_INSTANCE
  LET #PRCSINSTANCE = TO_NUMBER($PRCS_PROCESS_INSTANCE)

  DISPLAY '$PRCSINSTANCE'
  DISPLAY $PRCSINSTANCE

  LET $RUNCONTROLFOUND = 'N'

BEGIN-SELECT
RUNN.ICONV_DOSCROLLS
RUNN.ICONV_DOBTN_SCROLL
RUNN.ICONV_DOPNL_LEVEL1
RUNN.ICONV_DOPNL_LEVEL2
RUNN.ICONV_DOPNL_LEVEL3
RUNN.ICONV_DOLEVEL1
RUNN.ICONV_DOLEVEL2
RUNN.ICONV_DOLEVEL3
RUNN.ICONV_SCROLLLIMIT
RUNN.ICONV_SHOWPROMPT
RUNN.ICONV_GRIDODDEVEN
RUNN.ICONV_FRAMETOHORIZ
RUNN.ICONV_DELETEFRAMES
RUNN.ICONV_DFLT_SHEET
RUNN.ICONV_DOPANELSIZE
RUNN.ICONV_PANELSIZE
RUNN.ICONV_DOPNLGROUPS
RUNN.ICONV_DOSTYLES
RUNN.ICONV_GRIDLINESOFF

  LET $ICONV_DOSCROLLS        = RTRIM(&RUNN.ICONV_DOSCROLLS     , ' ')
  LET $ICONV_DOBTN_SCROLL     = RTRIM(&RUNN.ICONV_DOBTN_SCROLL  , ' ')
  LET $ICONV_DOPNL_LEVEL1     = RTRIM(&RUNN.ICONV_DOPNL_LEVEL1  , ' ')
  LET $ICONV_DOPNL_LEVEL2     = RTRIM(&RUNN.ICONV_DOPNL_LEVEL2  , ' ')
  LET $ICONV_DOPNL_LEVEL3     = RTRIM(&RUNN.ICONV_DOPNL_LEVEL3  , ' ')
  LET $ICONV_DOLEVEL1         = RTRIM(&RUNN.ICONV_DOLEVEL1      , ' ')
  LET $ICONV_DOLEVEL2         = RTRIM(&RUNN.ICONV_DOLEVEL2      , ' ')
  LET $ICONV_DOLEVEL3         = RTRIM(&RUNN.ICONV_DOLEVEL3      , ' ')
  LET #ICONV_SCROLLLIMIT      = &RUNN.ICONV_SCROLLLIMIT
  LET $ICONV_SHOWPROMPT       = RTRIM(&RUNN.ICONV_SHOWPROMPT    , ' ')
  LET $ICONV_GRIDODDEVEN      = RTRIM(&RUNN.ICONV_GRIDODDEVEN   , ' ')
  LET $ICONV_FRAMETOHORIZ     = RTRIM(&RUNN.ICONV_FRAMETOHORIZ  , ' ')
  LET $ICONV_DELETEFRAMES     = RTRIM(&RUNN.ICONV_DELETEFRAMES  , ' ')
  LET $ICONV_DFLT_SHEET       = RTRIM(&RUNN.ICONV_DFLT_SHEET    , ' ')
  LET $ICONV_DOPANELSIZE      = RTRIM(&RUNN.ICONV_DOPANELSIZE   , ' ')
  LET $ICONV_PANELSIZE        = RTRIM(&RUNN.ICONV_PANELSIZE     , ' ')
  LET $ICONV_DOPNLGROUPS      = RTRIM(&RUNN.ICONV_DOPNLGROUPS   , ' ')
  LET $ICONV_DOSTYLES         = RTRIM(&RUNN.ICONV_DOSTYLES      , ' ')
  LET $ICONV_TURNOFFGRIDLINES = RTRIM(&RUNN.ICONV_GRIDLINESOFF  , ' ')
  DO DISPLAY-PARAMETERS

  LET $RUNCONTROLFOUND = 'Y'

FROM PS_ICONV_RUNCNTL RUNN
WHERE RUNN.OPRID       = $PRCS_OPRID
  AND RUNN.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE DISPLAY-PARAMETERS

IF $DEBUG = 'Y'
  DISPLAY 'DISPLAY-PARAMETERS'
END-IF

  DISPLAY '.......... INPUT PARAMETERS ..........'

  DISPLAY '$ICONV_DOSCROLLS:'     NOLINE
  DISPLAY  $ICONV_DOSCROLLS
  DISPLAY '$ICONV_DOBTN_SCROLL:'  NOLINE
  DISPLAY  $ICONV_DOBTN_SCROLL
  DISPLAY '$ICONV_DOPNL_LEVEL1:'  NOLINE
  DISPLAY  $ICONV_DOPNL_LEVEL1
  DISPLAY '$ICONV_DOPNL_LEVEL2:'  NOLINE
  DISPLAY  $ICONV_DOPNL_LEVEL2
  DISPLAY '$ICONV_DOPNL_LEVEL3:'  NOLINE
  DISPLAY  $ICONV_DOPNL_LEVEL3
  DISPLAY '$ICONV_DOLEVEL1:'      NOLINE
  DISPLAY  $ICONV_DOLEVEL1
  DISPLAY '$ICONV_DOLEVEL2:'      NOLINE
  DISPLAY  $ICONV_DOLEVEL2
  DISPLAY '$ICONV_DOLEVEL3:'      NOLINE
  DISPLAY  $ICONV_DOLEVEL3
  DISPLAY '#ICONV_SCROLLLIMIT:'   NOLINE
  DISPLAY  #ICONV_SCROLLLIMIT
  DISPLAY '$ICONV_SHOWPROMPT:'    NOLINE
  DISPLAY  $ICONV_SHOWPROMPT
  DISPLAY '$ICONV_GRIDODDEVEN:'   NOLINE
  DISPLAY  $ICONV_GRIDODDEVEN
  DISPLAY '$ICONV_FRAMETOHORIZ:'  NOLINE
  DISPLAY  $ICONV_FRAMETOHORIZ
  DISPLAY '$ICONV_DELETEFRAMES:'  NOLINE
  DISPLAY  $ICONV_DELETEFRAMES
  DISPLAY '$ICONV_DFLT_SHEET:'    NOLINE
  DISPLAY  $ICONV_DFLT_SHEET
  DISPLAY '$ICONV_DOPANELSIZE:'   NOLINE
  DISPLAY  $ICONV_DOPANELSIZE
  DISPLAY '$ICONV_PANELSIZE:'     NOLINE
  DISPLAY  $ICONV_PANELSIZE
  DISPLAY '$ICONV_DOPNLGROUPS:'   NOLINE
  DISPLAY  $ICONV_DOPNLGROUPS
  DISPLAY '$ICONV_DOSTYLES:'      NOLINE
  DISPLAY  $ICONV_DOSTYLES
  DISPLAY '$ICONV_TURNOFFGRIDLINES:'   NOLINE
  DISPLAY  $ICONV_TURNOFFGRIDLINES
  DISPLAY '.'

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE LOAD-AND-PROCESS-PANELS

IF $DEBUG = 'Y'
  DISPLAY 'LOAD-AND-PROCESS-PANELS'
END-IF

  LET #ARRAYPOS = 1

BEGIN-SELECT DISTINCT
PD.PNLNAME
PD.PNLTYPE
  LET $PNLNAME = RTRIM(&PD.PNLNAME,' ')
  LET #PNLTYPE =       &PD.PNLTYPE
  PUT $PNLNAME #PNLTYPE INTO PANELS(#ARRAYPOS)
  ADD 1 TO #ARRAYPOS
FROM PSPNLDEFN PD
WHERE ((PD.PNLNAME IN (SELECT PNLNAME FROM PS_ICONV_PANELLIST
WHERE PS_ICONV_PANELLIST.OPRID       = $PRCS_OPRID
AND   PS_ICONV_PANELLIST.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID))
OR
(PD.PNLNAME IN (SELECT PSPROJECTITEM.OBJECTVALUE1 FROM PSPROJECTITEM, PS_ICONV_PROJLIST
    WHERE  PS_ICONV_PROJLIST.OPRID = $PRCS_OPRID
    AND    PS_ICONV_PROJLIST.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
    AND    PS_ICONV_PROJLIST.PROJECTNAME = PSPROJECTITEM.PROJECTNAME
    AND    PSPROJECTITEM.OBJECTTYPE      = 5
    AND    PSPROJECTITEM.PROJECTNAME     = PS_ICONV_PROJLIST.PROJECTNAME))
)
AND PD.PNLNAME NOT IN  (SELECT PNLNAME FROM PS_ICONV_PANELNOT
WHERE PS_ICONV_PANELNOT.OPRID       = $PRCS_OPRID
AND   PS_ICONV_PANELNOT.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID)
AND PD.PNLNAME NOT IN  (SELECT PSPROJECTITEM.OBJECTVALUE1 FROM PSPROJECTITEM, PS_ICONV_PROJNOT
WHERE  PS_ICONV_PROJNOT.OPRID       = $PRCS_OPRID
AND    PS_ICONV_PROJNOT.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
AND    PS_ICONV_PROJNOT.PROJECTNAME = PSPROJECTITEM.PROJECTNAME
AND    PSPROJECTITEM.OBJECTTYPE     = 5
AND    PSPROJECTITEM.PROJECTNAME    = PS_ICONV_PROJNOT.PROJECTNAME)

END-SELECT

LET #PANELCOUNT = #ARRAYPOS
LET #I = 1

WHILE #I < #PANELCOUNT
  GET $PNLNAME #PNLTYPE FROM PANELS(#I)
  ADD 1 TO #I
  DO PROCESS-PANEL
  IF $ICONV_DOSTYLES = 'Y'
    DO SELECT-SCROLLAREAS-FOR-STYLE-UPDATES
  END-IF
END-WHILE

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PROCESS-PANEL

IF $DEBUG = 'Y'
  DISPLAY 'PROCESS-PANEL'
END-IF

IF $DEBUG = 'Y'
  DISPLAY '-----------------------------------------------------------------'
  DISPLAY $PNLNAME
ELSE
  DISPLAY $PNLNAME
END-IF

  LET $NEED-TO-RENUMBER-PSPNLFIELD = 'N'

  IF $ICONV_DOSCROLLS = 'Y'
    DO CONVERT-SCROLLS-TO-SCROLLAREAS
  END-IF

  IF $ICONV_FRAMETOHORIZ = 'Y'
    DO CONVERT-FRAMES-TO-HORIZONTAL
  END-IF

  IF $ICONV_DELETEFRAMES = 'Y'
    DO SELECT-FRAMES-FOR-DELETE
  END-IF

  IF $NEED-TO-RENUMBER-PSPNLFIELD = 'Y'
    DO RENUMBER-PSPNLFIELD
  END-IF

  IF $ICONV_DFLT_SHEET = 'Y'
    DO APPLY-PANEL-STYLESHEET
  END-IF

  IF $ICONV_DOPANELSIZE = 'Y' AND #PNLTYPE = 0
    DO UPDATE-PANELSIZE
  END-IF

  IF $ICONV_GRIDODDEVEN = 'Y'
    DO CHECK-GRIDODDEVEN-OPTIONS
  END-IF

  IF $ICONV_SHOWPROMPT = 'Y'
    DO CHECK-SHOWPROMPT-OPTIONS
  END-IF

  IF $ICONV_TURNOFFGRIDLINES = 'Y'
    DO PROCESS-GRID-GRIDLINES
  END-IF

  DO UPDATE-PSPNLDEFN
  Do Commit-Transaction

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE CONVERT-SCROLLS-TO-SCROLLAREAS

IF $DEBUG = 'Y'
  DISPLAY 'CONVERT-SCROLLS-TO-SCROLLAREAS'
END-IF

  LET #LEVEL1SCROLLS = 0
  LET #LEVEL2SCROLLS = 0
  LET #LEVEL3SCROLLS = 0

BEGIN-SELECT
FIELDUSE
OCCURSLEVEL

  LET #FIELDUSE = &FIELDUSE

  DO PARSE-PSPNLFIELD-FIELDUSE

  IF $INVISIBLE <> 'Y'
    IF &OCCURSLEVEL = 1
      LET #LEVEL1SCROLLS = #LEVEL1SCROLLS + 1
    END-IF
    IF &OCCURSLEVEL = 2
      LET #LEVEL2SCROLLS = #LEVEL2SCROLLS + 1
    END-IF
    IF &OCCURSLEVEL = 3
      LET #LEVEL3SCROLLS = #LEVEL3SCROLLS + 1
    END-IF
  END-IF

FROM PSPNLFIELD
WHERE PNLNAME = $PNLNAME
AND   FIELDTYPE = 10
END-SELECT

IF $DEBUG = 'Y'
  DISPLAY '.'
  DISPLAY '#LEVEL1SCROLLS:'   NOLINE
  DISPLAY  #LEVEL1SCROLLS
  DISPLAY '#LEVEL2SCROLLS:'   NOLINE
  DISPLAY  #LEVEL2SCROLLS
  DISPLAY '#LEVEL3SCROLLS:'   NOLINE
  DISPLAY  #LEVEL3SCROLLS
END-IF

  LET #TOTALSCROLLS = #LEVEL1SCROLLS + #LEVEL2SCROLLS + #LEVEL3SCROLLS

  LET $CONVERT-SCROLLS = 'Y'

  IF #TOTALSCROLLS < 1
    LET $CONVERT-SCROLLS = 'N'
  END-IF
  IF #TOTALSCROLLS > #ICONV_SCROLLLIMIT
    LET $CONVERT-SCROLLS = 'N'
  END-IF
  IF #LEVEL3SCROLLS > 0 AND $ICONV_DOPNL_LEVEL3 <> 'Y'
    LET $CONVERT-SCROLLS = 'N'
  END-IF
  IF #LEVEL2SCROLLS > 0 AND $ICONV_DOPNL_LEVEL2 <> 'Y'
    LET $CONVERT-SCROLLS = 'N'
  END-IF
  IF #LEVEL1SCROLLS > 0 AND $ICONV_DOPNL_LEVEL1 <> 'Y'
    LET $CONVERT-SCROLLS = 'N'
  END-IF

  IF $CONVERT-SCROLLS = 'Y'
    DO SELECT-SCROLLS-FOR-CONVERSION
  END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE SELECT-SCROLLS-FOR-CONVERSION

IF $DEBUG = 'Y'
  DISPLAY 'SELECT-SCROLLS-FOR-CONVERSION'
END-IF

BEGIN-SELECT
PF1.FIELDLEFT
PF1.FIELDTOP
PF1.FIELDRIGHT
PF1.FIELDBOTTOM
PF1.PNLFLDID
PF1.OCCURSLEVEL
PF1.FIELDUSE

  LET #ORIGINALSCROLL-FIELDLEFT   = &PF1.FIELDLEFT
  LET #ORIGINALSCROLL-FIELDTOP    = &PF1.FIELDTOP
  LET #ORIGINALSCROLL-FIELDRIGHT  = &PF1.FIELDRIGHT
  LET #ORIGINALSCROLL-FIELDBOTTOM = &PF1.FIELDBOTTOM
  LET #SCROLLAREA-FIELDLEFT       = &PF1.FIELDLEFT
  LET #SCROLLAREA-FIELDTOP        = &PF1.FIELDTOP
  LET #SCROLLAREA-FIELDRIGHT      = &PF1.FIELDLEFT + 140
  LET #SCROLLAREA-FIELDBOTTOM     = &PF1.FIELDBOTTOM
  LET #SCROLLAREA-FIELDNUM        = &PF1.PNLFLDID
  LET #SCROLLAREA-OCCURSLEVEL     = &PF1.OCCURSLEVEL
  LET #SCROLLAREA-FIELDUSE        = &PF1.FIELDUSE
  LET #FIELDUSE                   = &PF1.FIELDUSE

  LET $CONVERT-THIS-SCROLL = 'Y'

  IF #SCROLLAREA-OCCURSLEVEL = 1 AND $ICONV_DOLEVEL1 <> 'Y'
    LET $CONVERT-THIS-SCROLL = 'N'
  END-IF
  IF #SCROLLAREA-OCCURSLEVEL = 2 AND $ICONV_DOLEVEL2 <> 'Y'
    LET $CONVERT-THIS-SCROLL = 'N'
  END-IF
  IF #SCROLLAREA-OCCURSLEVEL = 3 AND $ICONV_DOLEVEL3 <> 'Y'
    LET $CONVERT-THIS-SCROLL = 'N'
  END-IF

  DO PARSE-PSPNLFIELD-FIELDUSE

  IF $INVISIBLE = 'Y'
    LET $CONVERT-THIS-SCROLL = 'N'
  END-IF

  IF $ICLIENTBUTTONSEXIST = 'Y' AND $ICONV_DOBTN_SCROLL <> 'Y'
    LET $CONVERT-THIS-SCROLL = 'N'
  END-IF

  IF $CONVERT-THIS-SCROLL = 'Y'

    IF $ICLIENTBUTTONSEXIST = 'Y'
      DO DELETE-SCROLLBAR-BUTTONS
    END-IF

    DO GET-SCROLLAREA-POSITIONS

    IF $ICONV_DELETEFRAMES <> 'Y'
      DO DELETE-SCROLLAREA-FRAMES
    END-IF

    DO DELETE-SCROLLAREA-GROUPBOXES
    DO UPDATE-PSPNLFIELD-SCROLLBAR-TO-SCROLLAREA
    DO INSERT-PSPNLCNTRLDATA-SCROLLAREA-ROW

  END-IF

FROM PSPNLFIELD PF1
WHERE PF1.PNLNAME     = $PNLNAME
AND   PF1.FIELDTYPE   = 10
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE GET-SCROLLAREA-POSITIONS

IF $DEBUG = 'Y'
  DISPLAY 'GET-SCROLLAREA-POSITIONS'
END-IF

LET #NEXTSCROLLFIELDNUM = 0

BEGIN-SELECT
MIN(PF2.PNLFLDID) &NEXTSCROLLFIELDNUM
  LET #NEXTSCROLLFIELDNUM = &NEXTSCROLLFIELDNUM
FROM PSPNLFIELD PF2
WHERE PF2.PNLNAME = $PNLNAME
AND   PF2.FIELDTYPE   = 10
AND   PF2.PNLFLDID > #SCROLLAREA-FIELDNUM
END-SELECT

IF #NEXTSCROLLFIELDNUM > 0
  LET $NEXTSCROLLFIELDNUM    = TO_CHAR(#NEXTSCROLLFIELDNUM)
  LET $SCROLLFIELDSELECT3-SQL = 'AND PF3.PNLFLDID < ' || $NEXTSCROLLFIELDNUM
ELSE
  LET $SCROLLFIELDSELECT3-SQL = ' '
END-IF

LET #MINFIELDLEFT   = 999999
LET #MINFIELDTOP    = 999999
LET #MAXFIELDBOTTOM = 0

BEGIN-SELECT
PF3.PNLFLDID
PF3.FIELDLEFT
PF3.EDITLBLLEFT
PF3.FIELDTOP
PF3.FIELDBOTTOM
PF3.FIELDTYPE
PF3.FIELDUSE
PF3.LBLTYPE
PF3.OCCURSCOUNT1
PF3.OCCURSOFFSET1
PF3.OCCURSCOUNT2
PF3.OCCURSOFFSET2
PF3.OCCURSCOUNT3
PF3.OCCURSOFFSET3

  LET #FIELDLEFT   = &PF3.FIELDLEFT
  LET #EDITLBLLEFT = &PF3.EDITLBLLEFT
  LET #FIELDTOP    = &PF3.FIELDTOP
  LET #FIELDBOTTOM = &PF3.FIELDBOTTOM
  LET #FIELDTYPE   = &PF3.FIELDTYPE
  LET #FIELDUSE    = &PF3.FIELDUSE
  LET #LBLTYPE     = &PF3.LBLTYPE

  IF #SCROLLAREA-OCCURSLEVEL = 1
    LET #OCCURSCOUNT  = &PF3.OCCURSCOUNT1
    LET #OCCURSOFFSET = &PF3.OCCURSOFFSET1
  END-IF
  IF #SCROLLAREA-OCCURSLEVEL = 2
    LET #OCCURSCOUNT  = &PF3.OCCURSCOUNT2
    LET #OCCURSOFFSET = &PF3.OCCURSOFFSET2
  END-IF
  IF #SCROLLAREA-OCCURSLEVEL = 3
    LET #OCCURSCOUNT  = &PF3.OCCURSCOUNT3
    LET #OCCURSOFFSET = &PF3.OCCURSOFFSET3
  END-IF

  DO PARSE-PSPNLFIELD-FIELDUSE

  IF $INVISIBLE <> 'Y'

    IF #FIELDLEFT <> 0 AND #FIELDLEFT < #MINFIELDLEFT
      LET #MINFIELDLEFT = #FIELDLEFT
    END-IF
    IF #LBLTYPE <> 0 AND #EDITLBLLEFT <> 0 AND #EDITLBLLEFT < #MINFIELDLEFT
      LET #MINFIELDLEFT = #EDITLBLLEFT
    END-IF

    IF #FIELDTOP <> 0 AND #FIELDTOP < #MINFIELDTOP
      LET #MINFIELDTOP = #FIELDTOP
    END-IF

    IF #FIELDBOTTOM = 0
      DO GET-Y-AXIS-EXTENT
    ELSE
      LET #Y-AXIS-EXTENT = #FIELDBOTTOM - #FIELDTOP
    END-IF

    LET #FIELDBOTTOM = #FIELDTOP + ((#OCCURSCOUNT - 1) * #OCCURSOFFSET) + #Y-AXIS-EXTENT

    IF #FIELDBOTTOM <> 0 AND #FIELDBOTTOM > #MAXFIELDBOTTOM
      LET #MAXFIELDBOTTOM = #FIELDBOTTOM
    END-IF

  END-IF

FROM PSPNLFIELD PF3
WHERE PF3.PNLNAME = $PNLNAME
AND   PF3.PNLFLDID > #SCROLLAREA-FIELDNUM
AND   PF3.FIELDLEFT <> 0
[$SCROLLFIELDSELECT3-SQL]
END-SELECT

  IF #MINFIELDLEFT = 999999
    LET #MINFIELDLEFT = 0
  END-IF

  IF #MINFIELDLEFT > 9
    LET #SCROLLAREA-FIELDLEFT = #MINFIELDLEFT - 8
  END-IF

  IF #MINFIELDLEFT = 9 OR #MINFIELDLEFT = 8
    LET #SCROLLAREA-FIELDLEFT = 5
  END-IF
  IF #MINFIELDLEFT = 7 OR #MINFIELDLEFT = 6
    LET #SCROLLAREA-FIELDLEFT = 3
  END-IF
  IF #MINFIELDLEFT = 5 OR #MINFIELDLEFT = 4
    LET #SCROLLAREA-FIELDLEFT = 2
  END-IF
  IF #MINFIELDLEFT < 4
    LET #SCROLLAREA-FIELDLEFT = 1
  END-IF

  IF #MINFIELDTOP = 99999
    LET #MINFIELDTOP = #SCROLLAREA-FIELDTOP
  END-IF
  IF #MINFIELDTOP = 0
    LET #MINFIELDTOP = #SCROLLAREA-FIELDTOP
  END-IF

  IF #MINFIELDTOP <> 0
    IF #MINFIELDTOP < #SCROLLAREA-FIELDTOP
      LET #SCROLLAREA-FIELDTOP = #MINFIELDTOP - 20
    ELSE
      IF #MINFIELDTOP - #SCROLLAREA-FIELDTOP < 20
        LET #SCROLLAREA-FIELDTOP = #MINFIELDTOP - 20
      END-IF
    END-IF
  END-IF

  IF #MAXFIELDBOTTOM <> 0
    IF #MAXFIELDBOTTOM > #SCROLLAREA-FIELDBOTTOM
      IF #MAXFIELDBOTTOM - #SCROLLAREA-FIELDBOTTOM < 17
        LET #SCROLLAREA-FIELDBOTTOM = #MAXFIELDBOTTOM + 16
      ELSE
        LET #SCROLLAREA-FIELDBOTTOM = #SCROLLAREA-FIELDBOTTOM + 16
      END-IF
    ELSE
      IF #SCROLLAREA-FIELDBOTTOM - #MAXFIELDBOTTOM < 17
        LET #SCROLLAREA-FIELDBOTTOM = #MAXFIELDBOTTOM + 16
      END-IF
    END-IF
  END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE GET-Y-AXIS-EXTENT

IF $DEBUG = 'Y'
  DISPLAY 'GET-Y-AXIS-EXTENT'
END-IF

  IF #FIELDTYPE = 0 !Text
    LET #Y-AXIS-EXTENT = 18
  END-IF
  IF #FIELDTYPE = 4 !EditBox
    IF $DISPLAYONLY = 'Y'
      LET #Y-AXIS-EXTENT = 18
    ELSE
      LET #Y-AXIS-EXTENT = 22
    END-IF
  END-IF
  IF #FIELDTYPE = 5 !DropDown
    LET #Y-AXIS-EXTENT = 17
  END-IF
  IF #FIELDTYPE = 7 !CheckBox
    LET #Y-AXIS-EXTENT = 18
  END-IF
  IF #FIELDTYPE = 8 !RadioButton
    LET #Y-AXIS-EXTENT = 18
  END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-PSPNLFIELD-SCROLLBAR-TO-SCROLLAREA

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-PSPNLFIELD-SCROLLBAR-TO-SCROLLAREA'
END-IF

LET #SCROLLAREA-FIELDTYPE = 27

BEGIN-SQL
UPDATE PSPNLFIELD SET FIELDTYPE   = #SCROLLAREA-FIELDTYPE,
      FIELDUSE    = #SCROLLAREA-FIELDUSE,
      FIELDLEFT   = #SCROLLAREA-FIELDLEFT,
      FIELDTOP    = #SCROLLAREA-FIELDTOP,
      FIELDRIGHT  = #SCROLLAREA-FIELDRIGHT,
      FIELDBOTTOM = #SCROLLAREA-FIELDBOTTOM,
      LBLTYPE     = #SCROLLAREA-LBLTYPE,
      DSPLFORMAT  = #SCROLLAREA-DSPLFORMAT,
      LBLTEXT     = $SCROLLAREA-LBLTEXT
WHERE PNLNAME = $PNLNAME
AND   PNLFLDID = #SCROLLAREA-FIELDNUM
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE INSERT-PSPNLCNTRLDATA-SCROLLAREA-ROW

IF $DEBUG = 'Y'
  DISPLAY 'INSERT-PSPNLCNTRLDATA-SCROLLAREA-ROW'
END-IF

BEGIN-SQL
DELETE FROM PSPNLCNTRLDATA
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #SCROLLAREA-FIELDNUM
END-SQL

BEGIN-SQL
INSERT INTO PSPNLCNTRLDATA (PNLNAME
                           , PNLFLDID
                           , PTFINDSTRING
                           , PTHDGSHOW
                           , PTHDGROWACTBTNS
                           , PTHDGROWACTALGN
                           , PTHDGNAVBARBTNS
                           , PTHDGNAVBARALGN
                           , PTBAREASTYLENAME
                           , PTBAREAROWACTALGN
                           , PTBAREAHIDEROWSEP
                           , PTBAREASHWGRDLINES
                           , PTFTGSHOW
                           , PTFTGSTYLENAME
                           , PTFTGSHOWROWACT
                           , PTFTGROWACTALGN
                           , PTFTGSHOWNAVBAR
                           , PTFTGNAVBARALGN
                           , PTSEPSTARTOFFSET
                           , PTSEPOCCURSOFFSET
                           , PTBTNSTARTOFFSET
                           , PTBTNOCCURSOFFSET
                           , ALLOWMULTIROWINS)
VALUES ($PNLNAME                     !PNLNAME
      , #SCROLLAREA-FIELDNUM         !PNLFLDID
      , 'O'                          !PTFINDSTRING
      , 'X'                          !PTHDGSHOW
      , 'X'                          !PTHDGROWACTBTNS
      , 3                            !PTHDGROWACTALGN
      , 'X'                          !PTHDGNAVBARBTNS
      , 3                            !PTHDGNAVBARALGN
      , ' '                          !PTBAREASTYLENAME
      , 0                            !PTBAREAROWACTALGN
      , 'O'                          !PTBAREAHIDEROWSEP
      , 'O'                          !PTBAREASHWGRDLINES
      , 'O'                          !PTFTGSHOW
      , ' '                          !PTFTGSTYLENAME
      , 'O'                          !PTFTGSHOWROWACT
      , 1                            !PTFTGROWACTALGN
      , 'O'                          !PTFTGSHOWNAVBAR
      , 3                            !PTFTGNAVBARALGN
      , 0                            !PTSEPSTARTOFFSET
      , 0                            !PTSEPOCCURSOFFSET
      , 0                            !PTBTNSTARTOFFSET
      , 0                            !PTBTNOCCURSOFFSET
      , 0)                           !ALLOWMULTIROWINS
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE DELETE-SCROLLBAR-BUTTONS

IF $DEBUG = 'Y'
  DISPLAY 'DELETE-SCROLLBAR-BUTTONS'
END-IF

BEGIN-SELECT
PFB.PNLFLDID
PFB.FIELDTYPE
PFB.FIELDUSE

  LET #SCROLLBUTTON-FIELDNUM  = &PFB.PNLFLDID
  LET #SCROLLBUTTON-FIELDTYPE = &PFB.FIELDTYPE
  LET #SCROLLBUTTON-FIELDUSE  = &PFB.FIELDUSE

  LET #FIELDUSE = #SCROLLBUTTON-FIELDUSE

  DO PARSE-PSPNLFIELD-FIELDUSE
  DO DELETE-BUTTON

FROM PSPNLFIELD PFB
WHERE PFB.PNLNAME       = $PNLNAME
AND   PFB.ASSOCFIELDNUM = #SCROLLAREA-FIELDNUM - 1
ORDER BY PFB.PNLFLDID DESC
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE DELETE-BUTTON

IF $DEBUG = 'Y'
  DISPLAY 'DELETE-BUTTON'
END-IF

  IF $ROWCOUNTER = 'Y'
    LET #SCROLLAREA-FIELDUSE = #SCROLLAREA-FIELDUSE - 33554432
  END-IF

  IF $PREVIOUSPAGE = 'Y'
    LET #SCROLLAREA-FIELDUSE = #SCROLLAREA-FIELDUSE - 8388608
  END-IF

  IF $NEXTPAGE = 'Y'
    LET #SCROLLAREA-FIELDUSE = #SCROLLAREA-FIELDUSE - 4194304
  END-IF

  IF $NEWROWBUTTON = 'Y'
    LET #SCROLLAREA-FIELDUSE = #SCROLLAREA-FIELDUSE - 2097152
  END-IF

  IF $DELROWBUTTON = 'Y'
    LET #SCROLLAREA-FIELDUSE = #SCROLLAREA-FIELDUSE - 1048576
  END-IF

  IF $TOPBUTTON = 'Y'
    LET #SCROLLAREA-FIELDUSE = #SCROLLAREA-FIELDUSE - 1073741824
  END-IF

  IF $BOTTOMBUTTON = 'Y'
    LET #SCROLLAREA-FIELDUSE = #SCROLLAREA-FIELDUSE + 2147483648
  END-IF

  LET #DELETE-FIELDNUM = #SCROLLBUTTON-FIELDNUM

  DO DELETE-PNLFIELD

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE DELETE-SCROLLAREA-FRAMES

IF $DEBUG = 'Y'
  DISPLAY 'DELETE-SCROLLAREA-FRAMES'
END-IF

  LET #FRAMECOUNT = 0

BEGIN-SELECT
PF4.PNLFLDID
  LET #DELETE-FIELDNUM = &PF4.PNLFLDID
  ADD 1 TO #FRAMECOUNT
  DO DELETE-PNLFIELD
FROM PSPNLFIELD PF4
WHERE PF4.PNLNAME     = $PNLNAME
AND   PF4.FIELDTYPE = 1
AND   PF4.FIELDTOP    > #ORIGINALSCROLL-FIELDTOP    - 11
AND   PF4.FIELDTOP    < #ORIGINALSCROLL-FIELDTOP    + 11
AND   PF4.FIELDRIGHT  > #ORIGINALSCROLL-FIELDLEFT   - 11
AND   PF4.FIELDRIGHT  < #ORIGINALSCROLL-FIELDRIGHT  + 11
ORDER BY PF4.PNLFLDID DESC
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE DELETE-SCROLLAREA-GROUPBOXES

IF $DEBUG = 'Y'
  DISPLAY 'DELETE-SCROLLAREA-GROUPBOXES'
END-IF

  LET #GROUPBOXCOUNT      = 0
  LET $LBLTEXT            = ''
  LET $SCROLLAREA-LBLTEXT = ''

BEGIN-SELECT
PF5.PNLFLDID
PF5.LBLTYPE
PF5.LBLTEXT
PF5.RECNAME
PF5.FIELDNAME
  LET #DELETE-FIELDNUM = &PF5.PNLFLDID
  LET $LBLTEXT         = &PF5.LBLTEXT
  ADD 1 TO #GROUPBOXCOUNT
  DO DELETE-PNLFIELD
FROM PSPNLFIELD PF5
WHERE PF5.PNLNAME     = $PNLNAME
AND   PF5.FIELDTYPE = 2
AND   PF5.FIELDTOP    > #ORIGINALSCROLL-FIELDTOP    - 11
AND   PF5.FIELDTOP    < #ORIGINALSCROLL-FIELDTOP    + 11
AND   PF5.FIELDRIGHT  > #ORIGINALSCROLL-FIELDLEFT   - 11
AND   PF5.FIELDRIGHT  < #ORIGINALSCROLL-FIELDRIGHT  + 11
ORDER BY PF5.PNLFLDID DESC
END-SELECT


  LET #SCROLLAREA-LBLTYPE    = 1
  LET #SCROLLAREA-DSPLFORMAT = 0
  LET $SCROLLAREA-LBLTEXT    = 'Scroll Area'
  LET $PSHDGSHOWTITLE        = 'X'


IF #GROUPBOXCOUNT > 0

  IF #GROUPBOXCOUNT = 1
    IF $LBLTEXT <> ''
      LET #SCROLLAREA-LBLTYPE    = 1
      LET #SCROLLAREA-DSPLFORMAT = 524288
      LET $SCROLLAREA-LBLTEXT    = $LBLTEXT
    END-IF
  END-IF
ELSE
  IF #FRAMECOUNT > 0
    LET #SCROLLAREA-LBLTYPE    = 0
    LET #SCROLLAREA-DSPLFORMAT = 0
    LET $SCROLLAREA-LBLTEXT    = ' '
    LET $PSHDGSHOWTITLE        = 'O'
  END-IF
END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE DELETE-PNLFIELD

IF $DEBUG = 'Y'
  DISPLAY 'DELETE-PNLFIELD'
END-IF

  LET $NEED-TO-RENUMBER-PSPNLFIELD = 'Y'

BEGIN-SQL
DELETE FROM PSPNLFIELD
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #DELETE-FIELDNUM
END-SQL

BEGIN-SQL
DELETE FROM PSPNLFIELDLANG
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #DELETE-FIELDNUM
END-SQL

BEGIN-SQL
UPDATE PSPNLFIELD
SET ASSOCFIELDNUM = ASSOCFIELDNUM - 1
WHERE PNLNAME     = $PNLNAME
AND   ASSOCFIELDNUM > 0
AND   ASSOCFIELDNUM >= #DELETE-FIELDNUM
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE RENUMBER-PSPNLFIELD

IF $DEBUG = 'Y'
  DISPLAY 'RENUMBER-PSPNLFIELD'
END-IF

LET #NEWFIELDNUM = 0

BEGIN-SELECT
OLDTBL.PNLFLDID
OLDTBL.FIELDTYPE
  LET #OLDFIELDNUM = &OLDTBL.PNLFLDID
  LET #NEWFIELDNUM = #NEWFIELDNUM + 1
  DO UPDATE-NEWFIELDNUM-ON-PSPNLFIELD
FROM PSPNLFIELD OLDTBL
WHERE OLDTBL.PNLNAME = $PNLNAME
ORDER BY OLDTBL.PNLFLDID
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-NEWFIELDNUM-ON-PSPNLFIELD

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-NEWFIELDNUM-ON-PSPNLFIELD'
END-IF

BEGIN-SQL
UPDATE PSPNLFIELD
SET PNLFLDID = #NEWFIELDNUM
WHERE PNLNAME = $PNLNAME
AND PNLFLDID = #OLDFIELDNUM
END-SQL

BEGIN-SQL
UPDATE PSPNLFIELDLANG
SET PNLFLDID = #NEWFIELDNUM
WHERE PNLNAME = $PNLNAME
AND PNLFLDID = #OLDFIELDNUM
END-SQL

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-NEWFIELDNUM-ON-PSPNLCNTRLDATA'
  DISPLAY '#NEWFIELDNUM: '   NOLINE
  DISPLAY  #NEWFIELDNUM
  DISPLAY '$PNLNAME: '    NOLINE
  DISPLAY  $PNLNAME
  DISPLAY '#OLDFIELDNUM: '   NOLINE
  DISPLAY  #OLDFIELDNUM
END-IF

BEGIN-SQL
UPDATE PSPNLCNTRLDATA
SET PNLFLDID = #NEWFIELDNUM
WHERE PNLNAME = $PNLNAME
AND PNLFLDID = #OLDFIELDNUM
END-SQL

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-NEWFIELDNUM-ON-PSPNLBTNDATA'
END-IF

BEGIN-SQL
UPDATE PSPNLBTNDATA
SET PNLFLDID = #NEWFIELDNUM
WHERE PNLNAME = $PNLNAME
AND PNLFLDID = #OLDFIELDNUM
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE SELECT-FRAMES-FOR-DELETE

IF $DEBUG = 'Y'
  DISPLAY 'SELECT-FRAMES-FOR-DELETE'
END-IF

BEGIN-SELECT
PF10.PNLFLDID
  LET #DELETE-FIELDNUM = &PF10.PNLFLDID
  DO DELETE-PNLFIELD
FROM PSPNLFIELD PF10
WHERE PF10.PNLNAME     = $PNLNAME
AND   PF10.FIELDTYPE   = 1
ORDER BY PF10.PNLFLDID DESC
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE APPLY-PANEL-STYLESHEET

IF $DEBUG = 'Y'
  DISPLAY 'APPLY-PANEL-STYLESHEET'
END-IF

BEGIN-SQL
UPDATE PSPNLDEFN
SET STYLESHEETNAME = ' '
WHERE PNLNAME     = $PNLNAME
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-PANELSIZE

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-PANELSIZE'
END-IF


! Settings for 800 x 600 page without portal (760x330)
IF $ICONV_PANELSIZE = 'A'

BEGIN-SQL
UPDATE PSPNLDEFN
SET PNLUSE = 67, PANELTOP = 0, PANELLEFT = 0, PANELRIGHT = 760, PANELBOTTOM = 330
WHERE PNLNAME     = $PNLNAME
AND   PNLTYPE     = 0
END-SQL

END-IF


! Settings for 800 x 600 page inside portal (570x330)
IF $ICONV_PANELSIZE = 'B'

BEGIN-SQL
UPDATE PSPNLDEFN
SET PNLUSE = 35, PANELTOP = 0, PANELLEFT = 0, PANELRIGHT = 570, PANELBOTTOM = 330
WHERE PNLNAME     = $PNLNAME
AND   PNLTYPE     = 0
END-SQL

END-IF


! Settings for 1024 x 768 page without portal (984x498)
IF $ICONV_PANELSIZE = 'C'

BEGIN-SQL
UPDATE PSPNLDEFN
SET PNLUSE = 131, PANELTOP = 0, PANELLEFT = 0, PANELRIGHT = 984, PANELBOTTOM = 498
WHERE PNLNAME     = $PNLNAME
AND   PNLTYPE     = 0
END-SQL

END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE CHECK-GRIDODDEVEN-OPTIONS

IF $DEBUG = 'Y'
  DISPLAY 'CHECK-GRIDODDEVEN-OPTIONS'
END-IF

BEGIN-SELECT
PF6.PNLFLDID
PF6.FIELDUSE

  LET #FIELDNUM  = &PF6.PNLFLDID
  LET #FIELDUSE  = &PF6.FIELDUSE

  DO PARSE-PSPNLFIELD-FIELDUSE
  IF $GRIDODDEVEN <> 'Y'
    DO UPDATE-GRIDODDEVEN-CHECKBOX
  END-IF

FROM PSPNLFIELD PF6
WHERE PF6.PNLNAME = $PNLNAME
AND   PF6.FIELDTYPE = 19
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-GRIDODDEVEN-CHECKBOX

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-GRIDODDEVEN-CHECKBOX'
END-IF

BEGIN-SQL
UPDATE PSPNLFIELD
SET FIELDUSE = FIELDUSE + 4096
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #FIELDNUM
AND   FIELDTYPE   = 19
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE CHECK-SHOWPROMPT-OPTIONS

IF $DEBUG = 'Y'
  DISPLAY 'CHECK-SHOWPROMPT-OPTIONS'
END-IF

BEGIN-SELECT
PF7.PNLFLDID
PF7.FIELDUSE
PF7.DSPLFORMAT

  LET #FIELDNUM   = &PF7.PNLFLDID
  LET #FIELDUSE   = &PF7.FIELDUSE
  LET #DSPLFORMAT = &PF7.DSPLFORMAT

  DO PARSE-PSPNLFIELD-FIELDUSE
  IF $INVISIBLE <> 'Y' AND $DISPLAYONLY <> 'Y'
    DO PARSE-PSPNLFIELD-DSPLFORMAT
    IF $SHOWPROMPTBUTTON <> 'Y'
      DO UPDATE-SHOWPROMPT-CHECKBOX
    END-IF
  END-IF

FROM PSPNLFIELD PF7
WHERE PF7.PNLNAME = $PNLNAME
AND   PF7.FIELDTYPE = 4
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-SHOWPROMPT-CHECKBOX

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-SHOWPROMPT-CHECKBOX'
END-IF

BEGIN-SQL
UPDATE PSPNLFIELD
SET DSPLFORMAT = DSPLFORMAT - 8388608
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #FIELDNUM
AND   FIELDTYPE   = 4
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE CONVERT-FRAMES-TO-HORIZONTAL

IF $DEBUG = 'Y'
  DISPLAY 'CONVERT-FRAMES-TO-HORIZONTAL'
END-IF

BEGIN-SELECT
PF8.PNLFLDID
PF8.FIELDBOTTOM
PF8.FIELDTOP
  LET #UPDATE-FIELDNUM = &PF8.PNLFLDID
  LET #FIELDBOTTOM     = &PF8.FIELDBOTTOM
  LET #FIELDTOP        = &PF8.FIELDTOP
  LET #FIELDHEIGHT     = &PF8.FIELDBOTTOM - &PF8.FIELDTOP
  DO UPDATE-FRAME-TO-HORIZONTAL
FROM PSPNLFIELD PF8
WHERE PF8.PNLNAME     = $PNLNAME
AND   PF8.FIELDTYPE   = 1
AND   PF8.FIELDBOTTOM - PF8.FIELDTOP < 9
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-FRAME-TO-HORIZONTAL

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-FRAME-TO-HORIZONTAL'
END-IF

IF #FIELDHEIGHT > 7
  LET #FIELDTOP    = #FIELDTOP + 3
END-IF
IF #FIELDHEIGHT > 5
  LET #FIELDTOP    = #FIELDTOP + 2
END-IF
IF #FIELDHEIGHT > 3
  LET #FIELDTOP    = #FIELDTOP + 1
END-IF
IF #FIELDHEIGHT > 1
  LET #FIELDTOP    = #FIELDTOP + 0
END-IF

  LET #FIELDBOTTOM = #FIELDTOP + 2

BEGIN-SQL
UPDATE PSPNLFIELD   SET FIELDTYPE   = 23,
FIELDTOP    = #FIELDTOP,
FIELDBOTTOM = #FIELDBOTTOM,
LBLTEXT     = ' '
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #UPDATE-FIELDNUM
AND   FIELDTYPE   = 1
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PROCESS-GRID-GRIDLINES

IF $DEBUG = 'Y'
  DISPLAY 'PROCESS-GRID-GRIDLINES'
END-IF

BEGIN-SELECT
PF60.PNLFLDID

  LET #GRIDAREA-FIELDNUM  = &PF60.PNLFLDID
  DO UPDATE-GRID-GRIDLINES

FROM PSPNLFIELD PF60
WHERE PF60.PNLNAME = $PNLNAME
AND   PF60.FIELDTYPE = 19
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE  UPDATE-GRID-GRIDLINES

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-GRID-GRIDLINES'
END-IF

BEGIN-SQL
UPDATE PSPNLCNTRLDATA
SET PTBAREASHWGRDLINES = 'O'
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #GRIDAREA-FIELDNUM
END-SQL

IF #SQL-COUNT = 0
  DO INSERT-PSPNLCNTRLDATA-GRID-ROW
END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE INSERT-PSPNLCNTRLDATA-GRID-ROW

IF $DEBUG = 'Y'
  DISPLAY 'INSERT-PSPNLCNTRLDATA-GRID-ROW'
END-IF

BEGIN-SQL
INSERT INTO PSPNLCNTRLDATA (
  PNLNAME
, PNLFLDID
, PTFINDSTRING
, PTHDGSHOW
, PTHDGROWACTBTNS
, PTHDGROWACTALGN
, PTHDGNAVBARBTNS
, PTHDGNAVBARALGN
, PTBAREASTYLENAME
, PTBAREAROWACTALGN
, PTBAREAHIDEROWSEP
, PTBAREASHWGRDLINES
, PTFTGSHOW
, PTFTGSTYLENAME
, PTFTGSHOWROWACT
, PTFTGROWACTALGN
, PTFTGSHOWNAVBAR
, PTFTGNAVBARALGN
, PTSEPSTARTOFFSET
, PTSEPOCCURSOFFSET
, PTBTNSTARTOFFSET
, PTBTNOCCURSOFFSET
, ALLOWMULTIROWINS)
VALUES (
  $PNLNAME             !PNLNAME
,#GRIDAREA-FIELDNUM    !PNLFLDID
, 'O'                  !PTFINDSTRING
, 'X'                  !PTHDGSHOW
, 'O'                  !PTHDGROWACTBTNS
, 3                    !PTHDGROWACTALGN
, 'X'                  !PTHDGNAVBARBTNS
, 3                    !PTHDGNAVBARALGN
, ' '                  !PTBAREASTYLENAME
, 0                    !PTBAREAROWACTALGN
, 'O'                  !PTBAREAHIDEROWSEP
, 'O'                  !PTBAREASHWGRDLINES
, 'O'                  !PTFTGSHOW
, ' '                  !PTFTGSTYLENAME
, 'O'                  !PTFTGSHOWROWACT
, 1                    !PTFTGROWACTALGN
, 'O'                  !PTFTGSHOWNAVBAR
, 3                    !PTFTGNAVBARALGN
, 0                    !PTSEPSTARTOFFSET
, 0                    !PTSEPOCCURSOFFSET
, 0                    !PTBTNSTARTOFFSET
, 0                    !PTBTNOCCURSOFFSET
, 0)                   !ALLOWMULTIROWINS

END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-PSPNLDEFN

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-PSPNLDEFN'
END-IF

  DO GET-FIELDCOUNT
  DO GET-CURRENT-DATETIME
  DO PDM-VERSION-UPDATE

BEGIN-SQL
UPDATE PSPNLDEFN
SET FIELDCOUNT = #FIELDCOUNT, VERSION = #VERSION, LASTUPDDTTM = {DATETIMEIN-PREFIX}$SYSDATETIME{DATETIMEIN-SUFFIX}
WHERE PNLNAME = $PNLNAME
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE GET-FIELDCOUNT

IF $DEBUG = 'Y'
  DISPLAY 'GET-FIELDCOUNT'
END-IF

BEGIN-SELECT
COUNT(*) &FIELDCOUNT
  LET #FIELDCOUNT = &FIELDCOUNT
FROM PSPNLFIELD
WHERE PNLNAME     = $PNLNAME
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE SELECT-SCROLLAREAS-FOR-STYLE-UPDATES

IF $DEBUG = 'Y'
  DISPLAY 'SELECT-SCROLLAREAS-FOR-STYLE-UPDATES'
END-IF

BEGIN-SELECT
PF50.PNLFLDID
PF50.OCCURSLEVEL
PF50.FIELDLEFT
PF50.FIELDTOP
PF50.FIELDRIGHT
PF50.FIELDBOTTOM
  LET #SA-FIELDNUM    = &PF50.PNLFLDID
  LET #SA-OCCURSLEVEL = &PF50.OCCURSLEVEL
  LET #SA-FIELDLEFT   = &PF50.FIELDLEFT
  LET #SA-FIELDTOP    = &PF50.FIELDTOP
  LET #SA-FIELDRIGHT  = &PF50.FIELDRIGHT
  LET #SA-FIELDBOTTOM = &PF50.FIELDBOTTOM
  DO SELECT-OBJECTS-FOR-LEVEL-SPECIFIC-UPDATE
FROM PSPNLFIELD PF50
WHERE PF50.PNLNAME     = $PNLNAME
AND   PF50.FIELDTYPE   = 27
ORDER BY PF50.OCCURSLEVEL DESC
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE SELECT-OBJECTS-FOR-LEVEL-SPECIFIC-UPDATE

IF $DEBUG = 'Y'
  DISPLAY 'SELECT-OBJECTS-FOR-LEVEL-SPECIFIC-UPDATE'
END-IF

BEGIN-SELECT
PF51.PNLFLDID
PF51.FIELDTYPE
  LET #OBJ-FIELDNUM  = &PF51.PNLFLDID
  LET #OBJ-FIELDTYPE = &PF51.FIELDTYPE
  DO UPDATE-LEVEL-SPECIFIC-STYLE
FROM PSPNLFIELD PF51
WHERE PF51.PNLNAME     = $PNLNAME
AND   PF51.FIELDTYPE   IN (1, 2, 23)
AND   PF51.FIELDLEFT   > #SA-FIELDLEFT
AND   PF51.FIELDTOP    > #SA-FIELDTOP
AND   PF51.FIELDRIGHT  < #SA-FIELDRIGHT
AND   PF51.FIELDBOTTOM < #SA-FIELDBOTTOM
AND   PF51.FIELDSTYLE  = ' '
END-SELECT

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-LEVEL-SPECIFIC-STYLE

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-LEVEL-SPECIFIC-STYLE'
END-IF

LET $FIELDSTYLE = ' '
LET $LABELSTYLE = ' '

IF #OBJ-FIELDTYPE = 1
  IF #SA-OCCURSLEVEL = 1
    LET $FIELDSTYLE = 'PAFRAMELEVEL1'
  END-IF
  IF #SA-OCCURSLEVEL = 2
    LET $FIELDSTYLE = 'PAFRAMELEVEL2'
  END-IF
  IF #SA-OCCURSLEVEL = 3
    LET $FIELDSTYLE = 'PAFRAMELEVEL3'
  END-IF
END-IF

IF #OBJ-FIELDTYPE = 2
  IF #SA-OCCURSLEVEL = 1
    LET $FIELDSTYLE = 'PAGROUPBOXLEVEL1'
    LET $LABELSTYLE = 'PAGROUPBOXLABELLEVEL1'
  END-IF
  IF #SA-OCCURSLEVEL = 2
    LET $FIELDSTYLE = 'PAGROUPBOXLEVEL2'
    LET $LABELSTYLE = 'PAGROUPBOXLABELLEVEL2'
  END-IF
  IF #SA-OCCURSLEVEL = 3
    LET $FIELDSTYLE = 'PAGROUPBOXLEVEL3'
    LET $LABELSTYLE = 'PAGROUPBOXLABELLEVEL3'
  END-IF
END-IF

IF #OBJ-FIELDTYPE = 23
  IF #SA-OCCURSLEVEL = 1
    LET $FIELDSTYLE = 'PAHORIZONTALRULELEVEL1'
  END-IF
  IF #SA-OCCURSLEVEL = 2
    LET $FIELDSTYLE = 'PAHORIZONTALRULELEVEL2'
  END-IF
  IF #SA-OCCURSLEVEL = 3
    LET $FIELDSTYLE = 'PAHORIZONTALRULELEVEL3'
  END-IF
END-IF

BEGIN-SQL
UPDATE PSPNLFIELD
SET FIELDSTYLE = $FIELDSTYLE,
    LABELSTYLE = $LABELSTYLE
WHERE PNLNAME     = $PNLNAME
AND   PNLFLDID    = #OBJ-FIELDNUM
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE SELECT-PNLGRPS-FOR-PNLGRP-DEFAULTS

IF $DEBUG = 'Y'
  DISPLAY 'SELECT-PNLGRPS-FOR-PNLGRP-DEFAULTS'
END-IF

BEGIN-SELECT DISTINCT
PGD.PNLGRPNAME
PGD.MARKET
PGD.ACTIONS
PGD.SEARCHRECNAME
PGD.DISABLESAVE
PGD.PRIMARYACTION
PGD.DFLTACTION
PGD.DFLTSRCHTYPE
PGD.DEFERPROC
PGD.PNLNAVFLAGS
PGD.TBARBTNS
PGD.SHOWTBAR

  LET $PNLGRPNAME    = RTRIM(&PGD.PNLGRPNAME    ,' ')
  LET $MARKET        = RTRIM(&PGD.MARKET        ,' ')
  LET $SEARCHRECNAME = RTRIM(&PGD.SEARCHRECNAME ,' ')
  LET #ACTIONS       = &PGD.ACTIONS
  LET #DISABLESAVE   = &PGD.DISABLESAVE
  LET #PRIMARYACTION = &PGD.PRIMARYACTION
  LET #DFLTACTION    = &PGD.DFLTACTION
  LET #DFLTSRCHTYPE  = &PGD.DFLTSRCHTYPE
  LET #DEFERPROC     = &PGD.DEFERPROC
  LET #PNLNAVFLAGS   = &PGD.PNLNAVFLAGS
  LET #TBARBTNS      = &PGD.TBARBTNS
  LET #SHOWTBAR      = &PGD.SHOWTBAR

  DO PARSE-PSPNLGRPDEFN-ACTIONS
  DO CHECK-IF-PNLGRP-HAS-SRCHRECORD
  DO UPDATE-PNLGRP-DEFAULTS

FROM PSPNLGRPDEFN PGD
WHERE EXISTS(SELECT 'X' FROM PSPROJECTITEM PI1 WHERE PI1.PROJECTNAME IN (SELECT PROJECTNAME FROM PS_ICONV_PROJLIST
WHERE OPRID       = $PRCS_OPRID
AND   RUN_CNTL_ID = $PRCS_RUN_CNTL_ID)
AND PI1.OBJECTTYPE = 7
AND PI1.OBJECTVALUE1 = PGD.PNLGRPNAME
AND PI1.OBJECTVALUE2 = PGD.MARKET)
AND NOT EXISTS(SELECT 'X' FROM PSPROJECTITEM PI2 WHERE PI2.PROJECTNAME IN (SELECT PROJECTNAME FROM PS_ICONV_PROJNOT
WHERE OPRID       = $PRCS_OPRID
AND   RUN_CNTL_ID = $PRCS_RUN_CNTL_ID)
AND PI2.OBJECTTYPE = 7
AND PI2.OBJECTVALUE1 = PGD.PNLGRPNAME
AND PI2.OBJECTVALUE2 = PGD.MARKET)
END-SELECT

Do Commit-Transaction  !T-HLAM-299NN

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE CHECK-IF-PNLGRP-HAS-SRCHRECORD

IF $DEBUG = 'Y'
  DISPLAY 'CHECK-IF-PNLGRP-HAS-SRCHRECORD'
END-IF

  LET $PNLGRP_HAS_SRCHRECORD = 'N'

BEGIN-SELECT
USEEDIT
  LET #USEEDIT = &USEEDIT
  DO PARSE-RECFIELD-USEEDIT
  IF $PNLGRP_HAS_SRCHRECORD = 'Y'
    EXIT-SELECT
  END-IF
FROM PSRECFIELD
WHERE RECNAME = $SEARCHRECNAME
END-SELECT

IF $DEBUG = 'Y'
  DISPLAY '$PNLGRP_HAS_SRCHRECORD:'   NOLINE
  DISPLAY $SEARCHRECNAME              NOLINE
  DISPLAY ':'                         NOLINE
  DISPLAY $PNLGRP_HAS_SRCHRECORD
END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-PNLGRP-DEFAULTS

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-PNLGRP-DEFAULTS'
END-IF

! Default Search Type
  LET #NEWDFLTSRCHTYPE = 1

! Primary Action
  IF $PNLGRP_ADD = 'Y' AND $PNLGRP_NOTADDONLY = 'N'
    LET #NEWPRIMARYACTION = 0
  ELSE
    LET #NEWPRIMARYACTION = 1
  END-IF

! Default Search Action
  LET #NEWDFLTACTION   = 0
  IF $PNLGRP_CORRECTION = 'Y'
    LET #NEWDFLTACTION = 3
  END-IF
  IF $PNLGRP_DISPLAYALL = 'Y'
    LET #NEWDFLTACTION = 2
  END-IF
  IF $PNLGRP_DISPLAY = 'Y'
    LET #NEWDFLTACTION = 1
  END-IF
  IF #NEWDFLTACTION = 0
    LET #NEWDFLTACTION = 1
  END-IF

! Deferred Mode Processing
  LET #NEWDEFERPROC = 0

! Multi-Panel Navigation
  LET #NEWPNLNAVFLAGS = 3

! Disable Toolbar
  LET #NEWSHOWTBAR = 1

! Toolbar Actions
  LET #NEWTBARBTNS = 0

  IF #DISABLESAVE = 0
    LET #NEWTBARBTNS = #NEWTBARBTNS + 1            !Save
  END-IF

  IF $PNLGRP_ADD        = 'Y'
    LET #NEWTBARBTNS = #NEWTBARBTNS + 16           !Add
  END-IF

  IF $PNLGRP_DISPLAY    = 'Y'
    LET #NEWTBARBTNS = #NEWTBARBTNS + 32           !Update/Display
  END-IF

  IF $PNLGRP_DISPLAYALL = 'Y'
    LET #NEWTBARBTNS = #NEWTBARBTNS + 64           !Update/Display All
  END-IF

  IF $PNLGRP_CORRECTION = 'Y'
    LET #NEWTBARBTNS = #NEWTBARBTNS + 128          !Correction
  END-IF

  IF $PNLGRP_HAS_SRCHRECORD = 'Y'
    LET #NEWTBARBTNS = #NEWTBARBTNS + 2            !Return to List
    LET #NEWTBARBTNS = #NEWTBARBTNS + 256          !Next in List
    LET #NEWTBARBTNS = #NEWTBARBTNS + 512          !Previous in List
  END-IF

  IF $DEBUG = 'Y'
    DISPLAY 'Panel Group Updates...'
    DISPLAY '#NEWDFLTSRCHTYPE :'    NOLINE
    DISPLAY  #NEWDFLTSRCHTYPE
    DISPLAY '#NEWPRIMARYACTION:'    NOLINE
    DISPLAY  #NEWPRIMARYACTION
    DISPLAY '#NEWDFLTACTION   :'    NOLINE
    DISPLAY  #NEWDFLTACTION
    DISPLAY '#NEWDEFERPROC    :'    NOLINE
    DISPLAY  #NEWDEFERPROC
    DISPLAY '#NEWPNLNAVFLAGS  :'    NOLINE
    DISPLAY  #NEWPNLNAVFLAGS
    DISPLAY '#NEWSHOWTBAR     :'    NOLINE
    DISPLAY  #NEWSHOWTBAR
    DISPLAY '#NEWTBARBTNS     :'    NOLINE
    DISPLAY  #NEWTBARBTNS
  END-IF

  DO GET-CURRENT-DATETIME
  DO PGM-VERSION-UPDATE

BEGIN-SQL
UPDATE PSPNLGRPDEFN
SET DFLTSRCHTYPE  = #NEWDFLTSRCHTYPE ,
    PRIMARYACTION = #NEWPRIMARYACTION,
    DFLTACTION    = #NEWDFLTACTION   ,
    DEFERPROC     = #NEWDEFERPROC    ,
    PNLNAVFLAGS   = #NEWPNLNAVFLAGS  ,
    SHOWTBAR      = #NEWSHOWTBAR     ,
    TBARBTNS      = #NEWTBARBTNS     ,
    VERSION       = #VERSION,
    LASTUPDDTTM   = {DATETIMEIN-PREFIX}$SYSDATETIME{DATETIMEIN-SUFFIX}
WHERE PNLGRPNAME = $PNLGRPNAME
AND   MARKET     = $MARKET
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PARSE-PSPNLFIELD-FIELDUSE

IF $DEBUG = 'Y'
  DISPLAY 'PARSE-PSPNLFIELD-FIELDUSE'
END-IF

  LET $ROWCOUNTER     = ''
  LET $PREVIOUSPAGE   = ''
  LET $HIDEBORDER     = ''
  LET $NEXTPAGE       = ''
  LET $NEWROWBUTTON   = ''
  LET $DELROWBUTTON   = ''
  LET $GRIDODDEVEN    = ''
  LET $NOROWDELETE    = ''
  LET $NOROWINSERT    = ''
  LET $NOAUTOUPDATE   = ''
  LET $NOAUTOSELECT   = ''
  LET $INVISIBLE      = ''
  LET $DISPLAYONLY    = ''
  LET $TOPBUTTON      = ''
  LET $BOTTOMBUTTON   = ''

  IF #FIELDUSE < 0
    LET $BOTTOMBUTTON = 'Y'
    LET #FIELDUSE = #FIELDUSE + 2147483648
  ELSE
    LET $BOTTOMBUTTON = 'N'
  END-IF

  IF #FIELDUSE > 17179869183
    LET #FIELDUSE = #FIELDUSE - 17179869184
  END-IF
  IF #FIELDUSE > 8589934591
    LET #FIELDUSE = #FIELDUSE - 8589934592
  END-IF
  IF #FIELDUSE > 4294967295
    LET #FIELDUSE = #FIELDUSE - 4294967296
  END-IF

  IF #FIELDUSE > 2147483647
    LET #FIELDUSE = #FIELDUSE - 2147483648
  END-IF

  IF #FIELDUSE > 1073741823
    LET $TOPBUTTON = 'Y'
    LET #FIELDUSE = #FIELDUSE - 1073741824
  ELSE
    LET $TOPBUTTON = 'N'
  END-IF

  IF #FIELDUSE > 536870911
    LET #FIELDUSE = #FIELDUSE - 536870912
  END-IF
  IF #FIELDUSE > 268435455
    LET #FIELDUSE = #FIELDUSE - 268435456
  END-IF
  IF #FIELDUSE > 134217727
    LET #FIELDUSE = #FIELDUSE - 134217728
  END-IF

  IF #FIELDUSE > 67108863
    LET $HIDEBORDER = 'Y'
    LET #FIELDUSE = #FIELDUSE - 67108864
  ELSE
    LET $HIDEBORDER = 'N'
  END-IF

  IF #FIELDUSE > 33554431
    LET $ROWCOUNTER = 'Y'
    LET #FIELDUSE = #FIELDUSE - 33554432
  ELSE
    LET $ROWCOUNTER = 'N'
  END-IF

  IF #FIELDUSE > 16777215
    LET #FIELDUSE = #FIELDUSE - 16777216
  END-IF

  IF #FIELDUSE > 8388607
    LET $PREVIOUSPAGE = 'Y'
    LET #FIELDUSE = #FIELDUSE - 8388608
  ELSE
    LET $PREVIOUSPAGE = 'N'
  END-IF

  IF #FIELDUSE > 4194303
    LET $NEXTPAGE = 'Y'
    LET #FIELDUSE = #FIELDUSE - 4194304
  ELSE
    LET $NEXTPAGE = 'N'
  END-IF

  IF #FIELDUSE > 2097151
    LET $NEWROWBUTTON = 'Y'
    LET #FIELDUSE = #FIELDUSE - 2097152
  ELSE
    LET $NEWROWBUTTON = 'N'
  END-IF

  IF #FIELDUSE > 1048575
    LET $DELROWBUTTON = 'Y'
    LET #FIELDUSE = #FIELDUSE - 1048576
  ELSE
    LET $DELROWBUTTON = 'N'
  END-IF

  IF #FIELDUSE > 524287
    LET #FIELDUSE = #FIELDUSE - 524288
  END-IF
  IF #FIELDUSE > 262143
    LET #FIELDUSE = #FIELDUSE - 262144
  END-IF
  IF #FIELDUSE > 131071
    LET #FIELDUSE = #FIELDUSE - 131072
  END-IF
  IF #FIELDUSE > 65535
    LET #FIELDUSE = #FIELDUSE - 65536
  END-IF
  IF #FIELDUSE > 32767
    LET #FIELDUSE = #FIELDUSE - 32768
  END-IF
  IF #FIELDUSE > 16383
    LET #FIELDUSE = #FIELDUSE - 16384
  END-IF
  IF #FIELDUSE > 8191
    LET #FIELDUSE = #FIELDUSE - 8192
  END-IF
  IF #FIELDUSE > 4095
    LET $GRIDODDEVEN = 'Y'
    LET #FIELDUSE = #FIELDUSE - 4096
  ELSE
    LET $GRIDODDEVEN = 'N'
  END-IF
  IF #FIELDUSE > 2047
    LET #FIELDUSE = #FIELDUSE - 2048
  END-IF
  IF #FIELDUSE > 1023
    LET #FIELDUSE = #FIELDUSE - 1024
  END-IF

  IF #FIELDUSE > 511
    LET $NOROWDELETE = 'Y'
    LET #FIELDUSE = #FIELDUSE - 512
  ELSE
    LET $NOROWDELETE = 'N'
  END-IF

  IF #FIELDUSE > 255
    LET $NOROWINSERT = 'Y'
    LET #FIELDUSE = #FIELDUSE - 256
  ELSE
    LET $NOROWINSERT = 'N'
  END-IF

  IF #FIELDUSE > 127
    LET $NOAUTOUPDATE = 'Y'
    LET #FIELDUSE = #FIELDUSE - 128
  ELSE
    LET $NOAUTOUPDATE = 'N'
  END-IF

  IF #FIELDUSE > 63
    LET $NOAUTOSELECT = 'Y'
    LET #FIELDUSE = #FIELDUSE - 64
  ELSE
    LET $NOAUTOSELECT = 'N'
  END-IF

  IF #FIELDUSE > 31
    LET #FIELDUSE = #FIELDUSE - 32
  END-IF
  IF #FIELDUSE > 15
    LET #FIELDUSE = #FIELDUSE - 16
  END-IF
  IF #FIELDUSE > 7
    LET #FIELDUSE = #FIELDUSE - 8
  END-IF

  IF #FIELDUSE > 3
    LET #FIELDUSE = #FIELDUSE - 4
  END-IF

  IF #FIELDUSE > 1
    LET $INVISIBLE = 'Y'
    LET #FIELDUSE = #FIELDUSE - 2
  ELSE
    LET $INVISIBLE = 'N'
  END-IF

  IF #FIELDUSE > 0
    LET $DISPLAYONLY = 'Y'
  ELSE
    LET $DISPLAYONLY = 'N'
  END-IF

  IF $DELROWBUTTON = 'Y' OR $NEWROWBUTTON = 'Y' OR $NEXTPAGE = 'Y' OR $PREVIOUSPAGE = 'Y' OR $ROWCOUNTER = 'Y' OR $BOTTOMBUTTON = 'Y' OR $TOPBUTTON = 'Y'
     LET $ICLIENTBUTTONSEXIST = 'Y'
  ELSE
     LET $ICLIENTBUTTONSEXIST = 'N'
  END-IF

IF $DEBUG = 'Y'
  DISPLAY '.'
  DISPLAY '$DISPLAYONLY:'           NOLINE
  DISPLAY $DISPLAYONLY
  DISPLAY '$INVISIBLE:'             NOLINE
  DISPLAY $INVISIBLE
  DISPLAY '$NOAUTOSELECT:'          NOLINE
  DISPLAY $NOAUTOSELECT
  DISPLAY '$NOAUTOUPDATE:'          NOLINE
  DISPLAY $NOAUTOUPDATE
  DISPLAY '$NOROWINSERT:'           NOLINE
  DISPLAY $NOROWINSERT
  DISPLAY '$NOROWDELETE:'           NOLINE
  DISPLAY $NOROWDELETE
  DISPLAY '.'
  DISPLAY '$HIDEBORDER:'            NOLINE
  DISPLAY $HIDEBORDER
  DISPLAY '$GRIDODDEVEN:'           NOLINE
  DISPLAY $GRIDODDEVEN
  DISPLAY '$DELROWBUTTON:'          NOLINE
  DISPLAY $DELROWBUTTON
  DISPLAY '$NEWROWBUTTON:'          NOLINE
  DISPLAY $NEWROWBUTTON
  DISPLAY '$NEXTPAGE:'              NOLINE
  DISPLAY $NEXTPAGE
  DISPLAY '$PREVIOUSPAGE:'          NOLINE
  DISPLAY $PREVIOUSPAGE
  DISPLAY '$ROWCOUNTER:'            NOLINE
  DISPLAY $ROWCOUNTER
  DISPLAY '$TOPBUTTON:'             NOLINE
  DISPLAY $TOPBUTTON
  DISPLAY '$BOTTOMBUTTON:'          NOLINE
  DISPLAY $BOTTOMBUTTON
  DISPLAY '$ICLIENTBUTTONSEXIST:'   NOLINE
  DISPLAY $ICLIENTBUTTONSEXIST
END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PARSE-PSPNLFIELD-DSPLFORMAT

IF $DEBUG = 'Y'
  DISPLAY 'PARSE-PSPNLFIELD-DSPLFORMAT'
END-IF

  LET #DSPLFORMAT = #DSPLFORMAT

  IF #DSPLFORMAT > 17179869183
    LET #DSPLFORMAT = #DSPLFORMAT - 17179869184
  END-IF
  IF #DSPLFORMAT > 8589934591
    LET #DSPLFORMAT = #DSPLFORMAT - 8589934592
  END-IF
  IF #DSPLFORMAT > 4294967295
    LET #DSPLFORMAT = #DSPLFORMAT - 4294967296
  END-IF

  IF #DSPLFORMAT > 2147483647
    LET #DSPLFORMAT = #DSPLFORMAT - 2147483648
  END-IF

  IF #DSPLFORMAT > 1073741823
    LET #DSPLFORMAT = #DSPLFORMAT - 1073741824
  END-IF

  IF #DSPLFORMAT > 536870911
    LET #DSPLFORMAT = #DSPLFORMAT - 536870912
  END-IF
  IF #DSPLFORMAT > 268435455
    LET #DSPLFORMAT = #DSPLFORMAT - 268435456
  END-IF
  IF #DSPLFORMAT > 134217727
    LET #DSPLFORMAT = #DSPLFORMAT - 134217728
  END-IF

  IF #DSPLFORMAT > 67108863
    LET #DSPLFORMAT = #DSPLFORMAT - 67108864
  END-IF

  IF #DSPLFORMAT > 33554431
    LET #DSPLFORMAT = #DSPLFORMAT - 33554432
  END-IF

  IF #DSPLFORMAT > 16777215
    LET #DSPLFORMAT = #DSPLFORMAT - 16777216
  END-IF

  IF #DSPLFORMAT > 8388607
    LET $SHOWPROMPTBUTTON = 'N'
    LET #DSPLFORMAT = #DSPLFORMAT - 8388608
  ELSE
    LET $SHOWPROMPTBUTTON = 'Y'
  END-IF

  IF #DSPLFORMAT > 4194303
    LET #DSPLFORMAT = #DSPLFORMAT - 4194304
  END-IF

  IF #DSPLFORMAT > 2097151
    LET #DSPLFORMAT = #DSPLFORMAT - 2097152
  END-IF

  IF #DSPLFORMAT > 1048575
    LET #DSPLFORMAT = #DSPLFORMAT - 1048576
  END-IF

  IF #DSPLFORMAT > 524287
    LET #DSPLFORMAT = #DSPLFORMAT - 524288
  END-IF
  IF #DSPLFORMAT > 262143
    LET #DSPLFORMAT = #DSPLFORMAT - 262144
  END-IF
  IF #DSPLFORMAT > 131071
    LET #DSPLFORMAT = #DSPLFORMAT - 131072
  END-IF
  IF #DSPLFORMAT > 65535
    LET #DSPLFORMAT = #DSPLFORMAT - 65536
  END-IF
  IF #DSPLFORMAT > 32767
    LET #DSPLFORMAT = #DSPLFORMAT - 32768
  END-IF
  IF #DSPLFORMAT > 16383
    LET #DSPLFORMAT = #DSPLFORMAT - 16384
  END-IF
  IF #DSPLFORMAT > 8191
    LET #DSPLFORMAT = #DSPLFORMAT - 8192
  END-IF
  IF #DSPLFORMAT > 4095
    LET #DSPLFORMAT = #DSPLFORMAT - 4096
  END-IF
  IF #DSPLFORMAT > 2047
    LET #DSPLFORMAT = #DSPLFORMAT - 2048
  END-IF
  IF #DSPLFORMAT > 1023
    LET #DSPLFORMAT = #DSPLFORMAT - 1024
  END-IF

  IF #DSPLFORMAT > 511
    LET #DSPLFORMAT = #DSPLFORMAT - 512
  END-IF

  IF #DSPLFORMAT > 255
    LET #DSPLFORMAT = #DSPLFORMAT - 256
  END-IF

  IF #DSPLFORMAT > 127
    LET #DSPLFORMAT = #DSPLFORMAT - 128
  END-IF

  IF #DSPLFORMAT > 63
    LET #DSPLFORMAT = #DSPLFORMAT - 64
  END-IF

  IF #DSPLFORMAT > 31
    LET #DSPLFORMAT = #DSPLFORMAT - 32
  END-IF
  IF #DSPLFORMAT > 15
    LET #DSPLFORMAT = #DSPLFORMAT - 16
  END-IF
  IF #DSPLFORMAT > 7
    LET #DSPLFORMAT = #DSPLFORMAT - 8
  END-IF

  IF #DSPLFORMAT > 3
    LET #DSPLFORMAT = #DSPLFORMAT - 4
  END-IF

  IF #DSPLFORMAT > 1
    LET #DSPLFORMAT = #DSPLFORMAT - 2
  END-IF

  IF #DSPLFORMAT > 0
  END-IF

IF $DEBUG = 'Y'
  DISPLAY '.'
  DISPLAY '$SHOWPROMPTBUTTON:'      NOLINE
  DISPLAY $SHOWPROMPTBUTTON
  DISPLAY '.'
END-IF


END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PARSE-PSPNLGRPDEFN-ACTIONS

IF $DEBUG = 'Y'
  DISPLAY 'PARSE-PSPNLGRPDEFN-ACTIONS'
  DISPLAY '.'
  DISPLAY $PNLGRPNAME   NOLINE
  DISPLAY '->'          NOLINE
  DISPLAY $MARKET
END-IF

  LET $PNLGRP_ADD         = ''
  LET $PNLGRP_DISPLAY     = ''
  LET $PNLGRP_DISPLAYALL  = ''
  LET $PNLGRP_CORRECTION  = ''
  LET $PNLGRP_DATAENTRY   = ''
  LET $PNLGRP_NOTADDONLY  = ''

  IF #ACTIONS > 1023
    LET #ACTIONS = #ACTIONS - 1024
  END-IF

  IF #ACTIONS > 511
    LET #ACTIONS = #ACTIONS - 512
  END-IF

  IF #ACTIONS > 255
    LET #ACTIONS = #ACTIONS - 256
  END-IF

  IF #ACTIONS > 127
    LET $PNLGRP_DATAENTRY = 'Y'
    LET #ACTIONS = #ACTIONS - 128
  ELSE
    LET $PNLGRP_DATAENTRY = 'N'
  END-IF

  IF #ACTIONS > 63
    LET #ACTIONS = #ACTIONS - 64
  END-IF

  IF #ACTIONS > 31
    LET #ACTIONS = #ACTIONS - 32
  END-IF

  IF #ACTIONS > 15
    LET #ACTIONS = #ACTIONS - 16
  END-IF

  IF #ACTIONS > 7
    LET $PNLGRP_CORRECTION = 'Y'
    LET #ACTIONS = #ACTIONS - 8
  ELSE
    LET $PNLGRP_CORRECTION = 'N'
  END-IF

  IF #ACTIONS > 3
    LET $PNLGRP_DISPLAYALL = 'Y'
    LET #ACTIONS = #ACTIONS - 4
  ELSE
    LET $PNLGRP_DISPLAYALL = 'N'
  END-IF

  IF #ACTIONS > 1
    LET $PNLGRP_DISPLAY = 'Y'
    LET #ACTIONS = #ACTIONS - 2
  ELSE
    LET $PNLGRP_DISPLAY = 'N'
  END-IF

  IF #ACTIONS > 0
    LET $PNLGRP_ADD = 'Y'
  ELSE
    LET $PNLGRP_ADD = 'N'
  END-IF

  IF $PNLGRP_DISPLAY = 'Y' OR $PNLGRP_DISPLAYALL = 'Y' OR $PNLGRP_CORRECTION = 'Y'
    LET $PNLGRP_NOTADDONLY = 'Y'
  ELSE
    LET $PNLGRP_NOTADDONLY = 'N'
  END-IF

IF $DEBUG = 'Y'
  DISPLAY '$PNLGRP_ADD:'            NOLINE
  DISPLAY  $PNLGRP_ADD
  DISPLAY '$PNLGRP_DISPLAY:'        NOLINE
  DISPLAY  $PNLGRP_DISPLAY
  DISPLAY '$PNLGRP_DISPLAYALL:'     NOLINE
  DISPLAY  $PNLGRP_DISPLAYALL
  DISPLAY '$PNLGRP_CORRECTION:'     NOLINE
  DISPLAY  $PNLGRP_CORRECTION
  DISPLAY '$PNLGRP_DATAENTRY:'      NOLINE
  DISPLAY  $PNLGRP_DATAENTRY
  DISPLAY '$PNLGRP_NOTADDONLY:'     NOLINE
  DISPLAY  $PNLGRP_NOTADDONLY
END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PARSE-RECFIELD-USEEDIT

IF $DEBUG = 'Y'
  DISPLAY 'PARSE-RECFIELD-USEEDIT'
END-IF

  IF #USEEDIT > 2147483647
    LET #USEEDIT = #USEEDIT - 2147483648
  END-IF

  IF #USEEDIT > 1073741823
    LET #USEEDIT = #USEEDIT - 1073741824
  END-IF

  IF #USEEDIT > 536870911
    LET #USEEDIT = #USEEDIT - 536870912
  END-IF
  IF #USEEDIT > 268435455
    LET #USEEDIT = #USEEDIT - 268435456
  END-IF
  IF #USEEDIT > 134217727
    LET #USEEDIT = #USEEDIT - 134217728
  END-IF

  IF #USEEDIT > 67108863
    LET #USEEDIT = #USEEDIT - 67108864
  END-IF

  IF #USEEDIT > 33554431
    LET #USEEDIT = #USEEDIT - 33554432
  END-IF

  IF #USEEDIT > 16777215
    LET #USEEDIT = #USEEDIT - 16777216
  END-IF

  IF #USEEDIT > 8388607
    LET #USEEDIT = #USEEDIT - 8388608
  END-IF

  IF #USEEDIT > 4194303
    LET #USEEDIT = #USEEDIT - 4194304
  END-IF

  IF #USEEDIT > 2097151
    LET #USEEDIT = #USEEDIT - 2097152
  END-IF

  IF #USEEDIT > 1048575
    LET #USEEDIT = #USEEDIT - 1048576
  END-IF

  IF #USEEDIT > 524287
    LET #USEEDIT = #USEEDIT - 524288
  END-IF
  IF #USEEDIT > 262143
    LET #USEEDIT = #USEEDIT - 262144
  END-IF
  IF #USEEDIT > 131071
    LET #USEEDIT = #USEEDIT - 131072
  END-IF
  IF #USEEDIT > 65535
    LET #USEEDIT = #USEEDIT - 65536
  END-IF
  IF #USEEDIT > 32767
    LET #USEEDIT = #USEEDIT - 32768
  END-IF
  IF #USEEDIT > 16383
    LET #USEEDIT = #USEEDIT - 16384
  END-IF
  IF #USEEDIT > 8191
    LET #USEEDIT = #USEEDIT - 8192
  END-IF
  IF #USEEDIT > 4095
    LET #USEEDIT = #USEEDIT - 4096
  END-IF
  IF #USEEDIT > 2047
    LET $PNLGRP_HAS_SRCHRECORD = 'Y'
    LET #USEEDIT = #USEEDIT - 2048
  END-IF
  IF #USEEDIT > 1023
    LET #USEEDIT = #USEEDIT - 1024
  END-IF

  IF #USEEDIT > 511
    LET #USEEDIT = #USEEDIT - 512
  END-IF

  IF #USEEDIT > 255
    LET #USEEDIT = #USEEDIT - 256
  END-IF

  IF #USEEDIT > 127
    LET #USEEDIT = #USEEDIT - 128
  END-IF

  IF #USEEDIT > 63
    LET #USEEDIT = #USEEDIT - 64
  END-IF

  IF #USEEDIT > 31
    LET #USEEDIT = #USEEDIT - 32
  END-IF
  IF #USEEDIT > 15
    LET #USEEDIT = #USEEDIT - 16
  END-IF
  IF #USEEDIT > 7
    LET #USEEDIT = #USEEDIT - 8
  END-IF

  IF #USEEDIT > 3
    LET #USEEDIT = #USEEDIT - 4
  END-IF

  IF #USEEDIT > 1
    LET #USEEDIT = #USEEDIT - 2
  END-IF

  IF #USEEDIT > 0
  END-IF

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PDM-VERSION-UPDATE

IF $DEBUG = 'Y'
  DISPLAY 'PDM-VERSION-UPDATE'
END-IF

BEGIN-SELECT
PDM.VERSION
  LET #VERSION = &PDM.VERSION + 1
FROM PSLOCK PDM
WHERE PDM.OBJECTTYPENAME = 'PDM'
END-SELECT

BEGIN-SQL
UPDATE PSLOCK SET VERSION = #VERSION
WHERE OBJECTTYPENAME = 'PDM'
END-SQL

  Do Commit-Transaction

BEGIN-SELECT
PDMPSVERSION.VERSION
  LET #VERSION = &PDMPSVERSION.VERSION + 1
FROM PSVERSION PDMPSVERSION
WHERE PDMPSVERSION.OBJECTTYPENAME = 'PDM'
END-SELECT

BEGIN-SQL
UPDATE PSVERSION SET VERSION = #VERSION
WHERE OBJECTTYPENAME = 'PDM'
END-SQL

  Do Commit-Transaction

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PGM-VERSION-UPDATE

IF $DEBUG = 'Y'
  DISPLAY 'PGM-VERSION-UPDATE'
END-IF

!T-TMISHR-YB97T - remove select & commit

BEGIN-SQL
UPDATE PSLOCK SET VERSION = VERSION + 1
WHERE OBJECTTYPENAME = 'PGM'
END-SQL


END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE UPDATE-SYS-VERSION

IF $DEBUG = 'Y'
  DISPLAY 'UPDATE-SYS-VERSION'
END-IF

BEGIN-SQL
UPDATE PSVERSION SET VERSION = VERSION + 1
WHERE OBJECTTYPENAME = 'SYS'
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE FORCE-CACHE-REFRESH

IF $DEBUG = 'Y'
  DISPLAY 'FORCE-CACHE-REFRESH'
END-IF

  DO GET-CURRENT-DATETIME
  DISPLAY $SYSDATETIME

BEGIN-SQL
UPDATE PSSTATUS
   SET LASTREFRESHDTTM = {DATETIMEIN-PREFIX}$SYSDATETIME{DATETIMEIN-SUFFIX}
END-SQL

END-PROCEDURE

!-----------------------------------------------------------------

BEGIN-PROCEDURE PROCESS-STATUS-UPDATE

IF $DEBUG = 'Y'
  DISPLAY 'PROCESS-STATUS-UPDATE'
END-IF

IF $PRCSINSTANCE <> ''
  LET $PRCS_RUN_STATUS='S'
  LET $PRCS_MESSAGE_PARM1 = 'SUCCESSFUL'
  DO UPDATE-PRCS-RUN-STATUS
END-IF

END-PROCEDURE


#include 'curdttim.sqc'  ! Current Date and Time
#include 'datetime.sqc'  ! Routines for date and time formatting
#include 'prcsapi.sqc'   ! Update Process Request API
#include 'prcsdef.sqc'   ! Update Process Request Variable Declare
