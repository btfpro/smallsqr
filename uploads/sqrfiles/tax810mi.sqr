!***********************************************************************
!  TAX810MI:  Qtrly Wage List - MICHIGAN - Bulk format                 *
!                                        - 72-byte format              *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/03/07:17:04:18                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  885420                                              !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
! HP00001       Add $PublicSector indicator                            *
! HP00007       E&G 72-byte format                                     *
! BULK          Bulk Report Filing Format                              *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup31.sqc' !Printer and page-size initialization
#include 'usarpt.sqc'  ! NID_COUNTRY defined

begin-report
  Let $t1 = datenow()
  Show 'Report Started at : ' $t1

! Report Format '72-BYTE' OR 'BULK'
  Let $Format = 'BULK'

  do Init-Report

! These variables are used by rptsmmry.sqc
  Let $Prnt_CoTxGrs = 'N'
  Let $Prnt_CoExGrs = 'N'

  do Process-Main

  if $RecordWritten = 'Y'
        display ''
        display 'Upon successful conclusion of this program, one or more'
        display 'files will have been created, as follows:'
        display ''
        if $Format = '72-BYTE'
           display '  TAX810MI  contains 72-character records in the format'
           display '            required for Web or FTP submission to the Michigan'
           display '            Employment Security Commission.'
        else
           display '  TAX810MI  contains records in the format required for web filing'
           display '            to the Michigan Unemployment Insurance Agency.'
        end-if
        display ''
        display 'Follow instructions from the Michigan Unemployment Insurance Agency '
        display 'for preparing and submitting the file(s) electronically.'
        display ''
  else

        do Print-Summary-Data('NoData')

  end-if

  close 10

! do Delete-Work-File                                                   !HP00007

  do Stdapi-Term
  Let $t2 = datenow()
  Show 'Report Ended at : ' $t2

end-report

begin-procedure Init-Report
  move 'TAX810MI' to $ReportID
  move 'Michigan Quarterly UI Wage Report' to $ReportTitle
  display ''
  display 'Creating Electronic File for Michigan UI Wage Reporting'

  do Init-DateTime
  do Init-Number
  do Get-EandG                                                          !HP00001
  do Get-Current-DateTime
  do Get-Calendar-Year-Id
  do Stdapi-Init
  do Initialization

end-procedure

begin-procedure Initialization
#ifdef TAXTEST
  move 'CA'              to $State
#else
  move 'MI'              to $State
#endif

  do Get-Tax-Reporting-Run-Controls
  move &TX.Balance_Year to $RptYear 9999
  move &TX.Balance_Qtr  to $RptQtr 9
  move $PeriodEndDate   to $AsOfDate
  do Get-Quarter-Dates


! Use these routines if a run control page is setup for Michigan
! These routines will bring in the reporting medium.  If and when
! Michigan UI Tax reporting is combined into one program for the 72-byte format and 276-byte format
! and a file format needs to be selected.
!
!  if $PRCS_Process_Instance = ''
!     do Prompts
!  else
!     do Select-Parameters
!     do Convert-Parameters
!  end-if

  display ' '
  display 'Processing balances for '   noline
  display $RptQtrYr
  let $FileType = ' '
  do Open-File

  do Delete-Work-File

  let #Seq      = 0
  let #Count_ID = 0
  let #Count_S  = 0
  let #Count_F  = 0
  let #Count_R  = 0
  let #Count_R_Total  = 0
  let #Acct_NlGrs_QTD = 0
  let #Co_NlGrs_QTD   = 0
  let #Tot_NlGrs_QTD  = 0
  let #Neg_TxGrs_QTD  = 0
  let #Neg_NlGrs_QTD  = 0
  let #MonEmpFlg1     = 0
  let #MonEmpFlg2     = 0
  let #MonEmpFlg3     = 0
  let #CoMonth1Count  = 0
  let #CoMonth2Count  = 0
  let #CoMonth3Count  = 0
  let $RecordWritten  = 'N'
  let $Program        = 'TAX810MI'

end-procedure

begin-procedure Prompts

  while $FileType = ''
    input $FileType 'Tape, cartridge, Web or diskette file? (T, C, D or Q to quit)'
    uppercase $FileType
    if INSTR('TCDQ',$FileType,1) = 0
      display ' '
      display '***** Enter T, C, D or Q *****'
      display ' '
      move '' to $FileType
    end-if
  end-while
end-procedure

begin-procedure Convert-Parameters

  move $RC_QTR_UI.Reporting_Medium to $FileType

end-procedure

begin-procedure Open-File
if $FileType = 'D'
      move 0 to #RecordCount
      if #FileExtension <> 0
         close 10
      end-if

      add 1 to #FileExtension
      move #FileExtension to $FileExtension 099

      #ifdef OS400
         let $FileExtension = '(D' || $FileExtension || ')'
         let $FileID = '{IMPORTPREFIX}' || 'TAX810MI' || $FileExtension
      #else
         let $FileID = '{IMPORTPREFIX}' || 'TAX810MI.' || $FileExtension
      #endif

      open $FileID as 10 for-writing record=72:fixed
else
   if $Format = '72-BYTE'
      open '{IMPORTPREFIX}TAX810MI{IMPORTSUFFIX}' as 10 for-writing record=72:fixed
   else
      open '{IMPORTPREFIX}TAX810MI{IMPORTSUFFIX}' as 10 for-writing record=86:fixed
   end-if
end-if
end-procedure

begin-procedure Process-Main

  if $PublicSector <> 'Y'
    if $Format = 'BULK'
       do Record-F-Control
       do Record-F-Totals
       do Delete-Work-File
    end-if
  else
    do Record-F-Control
    if $Format = 'BULK'
       do Record-F-Totals-HP
    end-if
  end-if

  do Process-Report
end-procedure

begin-procedure Process-Report
begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
A.COMPANY
A.DESCR
A.ADDRESS1
A.CITY
A.STATE
A.POSTAL
A.FEDERAL_EIN

  move &A.Company to $Company
  move &A.Descr   to $CompanyName

  move '' to $priorEmplID

  move 'N'        to $Process_Company
  if $PublicSector <> 'Y'
     do Read-Employee-Data
     if $Process_Company = 'Y'
        do Generate-Tax
     end-if
  else                                                                  !HP00007
    do Get-SW-Record-Count-HP                                           !HP00007
    if $Process_Company = 'Y'                                           !HP00007
       do Generate-Tax-HP                                               !HP00007
    end-if                                                              !HP00007
  end-if                                                                !HP00007

FROM  PS_COMPANY_TBL A
WHERE A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
  (SELECT MAX(EFFDT)
     FROM PS_COMPANY_TBL
     WHERE  COMPANY = A.COMPANY
       AND  EFFDT  <= $AsOfDate)
ORDER BY COMPANY
end-SELECT
  if #Count_F > 0
    do Print-Summary-Data('FileTotal')
  end-if
end-procedure

begin-procedure Record-F-Control

begin-SELECT
BA.COMPANY
BA.DESCR
BA.ADDRESS1
BA.CITY
BA.STATE
BA.POSTAL
BA.FEDERAL_EIN

  move &BA.Company to $Company
  move &BA.Descr   to $CompanyName

  move '' to $priorEmplID

  if $PublicSector <> 'Y'
    do Read-Employee-Data-BULK
  else
    do Read-Employee-Data-HP
  end-if

FROM  PS_COMPANY_TBL BA
WHERE BA.EFF_STATUS = 'A'
  AND BA.TAX_REPORT_TYPE = '2'
  AND BA.EFFDT =
  (SELECT MAX(EFFDT)
     FROM PS_COMPANY_TBL
     WHERE  COMPANY = BA.COMPANY
       AND  EFFDT  <= $AsOfDate)
ORDER BY COMPANY
end-SELECT
end-procedure

begin-procedure Read-Employee-Data
begin-SELECT
C.COMPANY    () on-break print=never before=Format-Employer-ID
C.EMPLID
C.STATE
C.LOCALITY
C.TAX_CLASS
C.NLGRS_QTD
C.TXGRS_QTD
DD1.NATIONAL_ID
DD.LAST_NAME
DD.FIRST_NAME
DD.MIDDLE_NAME
PP.HIRE_DT


  if &C.NlGrs_QTD > 0
    do Process-Employee
  else
    do Print-Summary-Data('NegWage')
  end-if


FROM  PS_TAX_BALANCE C, PS_PERSON_NAME DD, PS_PERS_NID DD1, PS_EMPLOYMENT PP
WHERE  C.COMPANY       = &A.Company
  AND DD.EMPLID        =  C.EMPLID
  AND PP.EMPLID        =  C.EMPLID
  AND  C.TAX_CLASS     = 'U'
  AND  C.BALANCE_ID    = $Calendar_Year_Id
  AND  C.BALANCE_YEAR  = &TX.Balance_Year
  AND  C.BALANCE_QTR   = &TX.Balance_Qtr
  AND  C.STATE         = $State
  AND  C.LOCALITY      = ' '
  AND  C.BALANCE_PERIOD  =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE
       WHERE  EMPLID        = C.EMPLID
         AND  COMPANY       = C.COMPANY
         AND  BALANCE_ID    = C.BALANCE_ID
         AND  BALANCE_YEAR  = C.BALANCE_YEAR
         AND  BALANCE_QTR   = C.BALANCE_QTR
         AND  STATE         = C.STATE
         AND  LOCALITY      = C.LOCALITY
         AND  TAX_CLASS     = C.TAX_CLASS)
  AND PP.EMPL_RCD  =
      (SELECT MIN(EMPL_RCD)
       FROM   PS_EMPLOYMENT
       WHERE  EMPLID        = C.EMPLID)
  AND C.NLGRS_QTD <> 0
  AND DD.EMPLID            = DD1.EMPLID
  AND DD1.COUNTRY = {NID_Country}
  AND DD1.NATIONAL_ID_TYPE = $Payroll_NID_Type
ORDER BY DD1.NATIONAL_ID
end-SELECT
end-procedure

begin-procedure Format-Employer-ID

  do Get-State-Tax-Data

 let $TestStateEIN = &Employer_ID_SUT
 let #Length = LENGTH(RTRIM(&Employer_ID_SUT,' '))

  evaluate #Length
    when = 10
      extract $StateEIN from $TestStateEIN 0 10
    when = 07
      extract $StateEIN from $TestStateEIN 0 7
      concat '000'    with $StateEIN
    when = 00
      do Employer_ID_missing
    when-Other
      do Employer_ID_error
  end-evaluate

end-procedure

begin-procedure Employer_ID_missing

    display '*********************** Processing stopped *********************'
    display '*** State Unemployment ID missing in Company State Tax Table ***'
    display '*********************** Processing stopped *********************'
    display 'Company - '    noline
    display $Company
    stop

end-procedure

begin-procedure Employer_ID_error


  display '*********************** Processing stopped *********************'
  display '*** Incorrect Format for State Unemployment in Company State Tax Table ***'
  display '*** Michigan State Unemployment ID is 7 digits in length ***'
  display '*** positions 8 - 10 should hold 3 digit multi-unit number or blanks if not requested ***'
  display '*********************** Processing stopped *********************'
  display 'Company - '  noline
  display $Company
  display 'Employer ID - ' noline
  display  $TestStateEIN
  stop

end-procedure

begin-procedure Process-Employee

  move 'Y'           to $Process_Company
  move &C.Company    to $Company
  move &C.EmplID     to $EmplID
  move &C.State      to $Emp_State
  move &C.Locality   to $Locality
  move &C.Tax_Class  to $Tax_Class

  let $Employer_ID    = $StateEIN            !from Format-Employer-ID


  move &DD1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits
  if $S123 = &Temp_SSN_Mask or RTRIM(&DD1.NATIONAL_ID, ' ') = ''
     move '000000000'   to $NATIONAL_ID
  else
     move &DD1.NATIONAL_ID        to $NATIONAL_ID
  end-if

  let $LastName   = rtrim(&DD.LAST_NAME, ' ')
  let $FirstName  = rtrim(&DD.FIRST_NAME, ' ')
  let $MiddleName = {ps-substr}(&DD.MIDDLE_NAME,1,1)
  uppercase $LastName
  uppercase $FirstName
  uppercase $MiddleName

  let #MonEmpFlg1 = 0
  let #MonEmpFlg2 = 0
  let #MonEmpFlg3 = 0
  if $Format = 'BULK'
    do Get-Monthly-Employment-Indicator
  end-if

  move &C.Txgrs_QTD  to #Txgrs_QTD
  move &C.NlGrs_QTD  to #NlGrs_QTD
  multiply 100 times #NlGrs_QTD

  let #max_amt_NLG = 9999999999
  let #NLGrs_orig = #NlGrs_QTD

  add 1 to #Count_S

  let $done1 = 'N'
  while $done1 <> 'Y'
    do split-s-record (#NLGrs_orig,#NlGrs_QTD,#max_amt_NLG,$done1)
    divide 100 into #NlGrs_QTD
    do Insert-Work-Record
    let #MonEmpFlg1 = 0
    let #MonEmpFlg2 = 0
    let #MonEmpFlg3 = 0
    let #Txgrs_QTD  = 0
    add 1 to #Count_R
    add 1 to #Count_S1
  end-while

end-procedure


begin-procedure Generate-Tax
begin-SELECT
RR.COMPANY        () on-break print=never level=1 before=Before-Company
                                                 after=AFTER-COMPANY
RR.EMPLOYER_ID
RR.STATE
RR.EMPLID
RR.TAX_CLASS
RR.LOCALITY
RR.TXGRS_QTD
RR.NLGRS_QTD
RR.NATIONAL_ID
RR.LAST_NAME
RR.FIRST_NAME
RR.MIDDLE_NAME

  move &RR.EMPLOYER_ID  to $StateEIN
  move &RR.NATIONAL_ID  to $NATIONAL_ID
  move &RR.LAST_NAME    to $LastName
  move &RR.FIRST_NAME   to $FirstName
  move &RR.MIDDLE_NAME  to $MidInitial
  move &RR.NLGRS_QTD    to #NlGrs_QTD
  add  #NlGrs_QTD       to #Co_NlGrs_QTD
  multiply 100 times #NlGrs_QTD
  do Format-Number(#NlGrs_QTD,  $NlGrs_QTD,  '0999999999')

  if $Format = '72-BYTE'
     do Write-S-Record-72
  else
     do Write-W-Record-Bulk
  end-if

FROM  PS_R_TAX810MI RR
WHERE RR.COMPANY       = &A.Company
ORDER BY RR.COMPANY, RR.NATIONAL_ID
end-SELECT
end-procedure

begin-procedure Before-Company
  do Get-Company-Data
  do Record-Totals

  if $Format = '72-BYTE'
     do Write-E-Record-72
  else
     add  1    to   #Seq
     move #Seq to   $Seq   099
     do Write-H-Record-Bulk
  end-if
end-procedure

begin-procedure After-Company

! move #CoMonth1Count to #Month1Count
! move #CoMonth2Count to #Month2Count
! move #CoMonth3Count to #Month3Count
  do Print-Summary-Data('CoTotal')

  add #Co_NlGrs_QTD to #Tot_NlGrs_QTD
  add #Count_S to #Count_F
  add #Count_R to #Count_R_Total

  move 0 to #Co_NlGrs_QTD
  move 0 to #CoMonth1Count
  move 0 to #CoMonth2Count
  move 0 to #CoMonth3Count
  move 0 to #Count_S
  move 0 to #Count_S1
  move 0 to #Count_R
  new-page

end-procedure

begin-procedure Read-Employee-Data-BULK
begin-SELECT
BC.COMPANY
BC.NLGRS_QTD

  if &BC.NlGrs_QTD > 0
    do Process-Employee-BULK
  end-if

FROM  PS_TAX_BALANCE BC, PS_PERSON_NAME BDD, PS_PERS_NID BDD1, PS_EMPLOYMENT BPP
WHERE  BC.COMPANY       = &BA.Company
  AND BDD.EMPLID        =  BC.EMPLID
  AND BPP.EMPLID        =  BC.EMPLID
  AND  BC.TAX_CLASS     = 'U'
  AND  BC.BALANCE_ID    = $Calendar_Year_Id
  AND  BC.BALANCE_YEAR  = &TX.Balance_Year
  AND  BC.BALANCE_QTR   = &TX.Balance_Qtr
  AND  BC.STATE         = $State
  AND  BC.LOCALITY      = ' '
  AND  BC.BALANCE_PERIOD  =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE
       WHERE  EMPLID        = BC.EMPLID
         AND  COMPANY       = BC.COMPANY
         AND  BALANCE_ID    = BC.BALANCE_ID
         AND  BALANCE_YEAR  = BC.BALANCE_YEAR
         AND  BALANCE_QTR   = BC.BALANCE_QTR
         AND  STATE         = BC.STATE
         AND  LOCALITY      = BC.LOCALITY
         AND  TAX_CLASS     = BC.TAX_CLASS)
  AND BPP.EMPL_RCD  =
      (SELECT MIN(EMPL_RCD)
       FROM   PS_EMPLOYMENT
       WHERE  EMPLID        = BC.EMPLID)
  AND BC.NLGRS_QTD <> 0
  AND BDD.EMPLID            = BDD1.EMPLID
  AND BDD1.COUNTRY = {NID_Country}
  AND BDD1.NATIONAL_ID_TYPE = $Payroll_NID_Type
ORDER BY BDD1.NATIONAL_ID
end-SELECT
end-procedure

begin-procedure Process-Employee-BULK

  move &BC.Company    to $Company
  move &BC.NlGrs_QTD  to #NlGrs_QTD

  move ' ' to   $Employer_ID
  move ' ' to   $National_ID
  move ' ' to   $Emp_State
  move ' ' to   $EmplID
  move ' ' to   $LastName
  move ' ' to   $FirstName
  move ' ' to   $MiddleName
  move ' ' to   $Tax_Class
  move ' ' to   $Locality
  move 0   to   #Txgrs_QTD

  do Insert-Work-Record

end-procedure

begin-procedure Record-F-Totals
begin-SELECT

COUNT(DISTINCT TC.COMPANY)  &CompCount
SUM(TC.NLGRS_QTD)           &TotalGrs

  move &CompCount to $Count_Co 0999999
  move &TotalGrs  to #TempTot

  multiply 100 times #TempTot

  move #TempTot to $TotCoNlGrs_QTD 0999999999999

  do Write-F-Record-Bulk

FROM  PS_R_TAX810MI TC
end-SELECT
end-procedure

begin-procedure Record-Totals
begin-SELECT

COUNT(*)          &EmplCount
SUM(TB.MONTH1_COUNT6)           &CoMon1Total
SUM(TB.MONTH2_COUNT6)           &CoMon2Total
SUM(TB.MONTH3_COUNT6)           &CoMon3Total
#ifdef DB2ALL
DECIMAL(SUM(TB.NLGRS_QTD),15,3) &CompanyTotal
DECIMAL(SUM(TB.TXGRS_QTD),15,3) &CoTaxTotal
#else
SUM(TB.NLGRS_QTD) &CompanyTotal
SUM(TB.TXGRS_QTD) &CoTaxTotal
#endif

  move &A.Federal_EIN  to $FedEIN 099999999

  move &EmplCount to $Count_S 0999999
  move &EmplCount to $Count_W 0999999
  move &CoMon1Total  to #Month1Count
  move &CoMon2Total  to #Month2Count
  move &CoMon3Total  to #Month3Count
  move &CompanyTotal to #TempGrs
  move &CoTaxTotal   to #TempTax

  let #TempExc = #TempGrs - #TempTax

  multiply 100 times #TempGrs
  multiply 100 times #TempExc

  move #TempGrs to $CoNlGrs        0999999999999
  move #TempGrs to $Co_NlGrs_QTD   0999999999999
  move #TempExc to $CoExcWages_QTD 0999999999999

FROM  PS_R_TAX810MI TB
WHERE TB.COMPANY       = &A.Company
GROUP BY TB.COMPANY
end-SELECT

end-procedure

begin-procedure Write-E-Record-72

  write 10 from 'E':1
          $StateEIN:10
           $RptYear:4
            $RptQtr:1
                $Sp:24
           $Count_S:7
      $Co_NlGrs_QTD:13
                $Sp:12

  move 'Y' to $RecordWritten

end-procedure

begin-procedure Write-S-Record-72
  write 10 from 'S':1
          $StateEIN:10
           $RptYear:4
            $RptQtr:1
       $NATIONAL_ID:9
                $Sp:7
          $LastName:16
         $FirstName:12
        $MidInitial:1
         $NlGrs_QTD:10
                $Sp:1


  move '' to $NlGrs_QTD
  move 0  to #NlGrs_QTD
end-procedure

begin-procedure Write-F-Record-Bulk
  write 10 from 'F':1
          $Count_Co:7
    $TotCoNlGrs_QTD:13

  move 'Y' to $RecordWritten
end-procedure

begin-procedure Write-H-Record-Bulk

  move #Month1Count  to  $Month1Count 099999
  move #Month2Count  to  $Month2Count 099999
  move #Month3Count  to  $Month3Count 099999
  move '0'           to  $Final_Rep
  move '0'           to  $Apportionm
  move '0'           to  $Amended_Flg

  write 10 from 'H':1
               $Seq:3
          $StateEIN:10
            $RptQtr:1
           $RptYear:4
           $CoNlGrs:13
    $CoExcWages_QTD:13
       $Month1Count:6
       $Month2Count:6
       $Month3Count:6
         $Final_Rep:1
        $Apportionm:1
       $Amended_Flg:1
           $Count_W:7
           $CoNlGrs:13

  move '' to $CoNlGrs
  move '' to $CoExcWages_QTD
  move '' to $Month1Count
  move '' to $Month2Count
  move '' to $Month3Count
  move 0  to #CoNlGrs
  move 0  to #CoExcWages_QTD
  add #Month1Count to #CoMonth1Count
  add #Month2Count to #CoMonth2Count
  add #Month3Count to #CoMonth3Count
  move 0  to #Month1Count
  move 0  to #Month2Count
  move 0  to #Month3Count
end-procedure

begin-procedure Write-W-Record-Bulk

  move '0'           to     $Fam_Status

  write 10 from 'W':1
          $StateEIN:10
            $RptQtr:1
           $RptYear:4
       $NATIONAL_ID:9
          $LastName:16
         $FirstName:12
        $MidInitial:1
         $NlGrs_QTD:10
        $Fam_Status:1

  move '' to $NlGrs_QTD
  move 0  to #NlGrs_QTD
end-procedure

begin-procedure Read-Employee-Data-HP                                   !HP00007
begin-SELECT                                                            !HP00007
RC.COMPANY        () on-break print=never level=1                       !HP00007
                                     before=Read-Employer-ID-Default    !HP00007
RC.EMPLID                                                               !HP00007
RC.TAX_CLASS                                                            !HP00007
RC.STATE                                                                !HP00007
RC.LOCALITY                                                             !HP00007
RC.TXGRS_QTD                                                            !HP00007
RC.NLGRS_QTD                                                            !HP00007
RD1.NATIONAL_ID                                                         !HP00007
RD.LAST_NAME                                                            !HP00007
RD.FIRST_NAME                                                           !HP00007
RD.MIDDLE_NAME                                                          !HP00007
RP.HIRE_DT                                                              !HP00007
                                                                        !HP00007
  move &RC.Company to $Company                                          !HP00007
  move &RC.EmplID  to $EmplID                                           !HP00007
  move &RC.State   to $Emp_State                                        !HP00007
  move &RC.Locality   to $Locality                                      !HP00007
  move &RC.Tax_Class  to $Tax_Class                                     !HP00007
  move &RC.Txgrs_QTD  to #Txgrs_QTD                                     !HP00007
  move &RC.Nlgrs_QTD  to #Nlgrs_QTD                                     !HP00007
  move &RD1.National_ID to $National_ID                                 !HP00007
  move &RD.LAST_NAME  to $LastName                                      !HP00007
  move &RD.FIRST_NAME to $FirstName                                     !HP00007
  move &RD.MIDDLE_NAME to $MiddleName                                   !HP00007
  do Get-Employer-ID                                                    !HP00007
  let #MonEmpFlg1 = 0
  let #MonEmpFlg2 = 0
  let #MonEmpFlg3 = 0
  if &RC.NLGRS_QTD > 0 and $Format = 'BULK'
    do Get-Monthly-Employment-Indicator
  end-if
  do Insert-Work-Record                                                 !HP00007
                                                                        !HP00007
FROM  PS_TAX_BALANCE RC, PS_PERSON_NAME RD,  PS_PERS_NID RD1            !HP00007
      , PS_EMPLOYMENT RP                                                !HP00007
WHERE RC.COMPANY       = &BA.Company                                    !HP00007
  AND RD.EMPLID        = RC.EMPLID                                      !HP00007
  AND RP.EMPLID        = RC.EMPLID                                      !HP00007
  AND RC.TAX_CLASS     = 'U'                                            !HP00007
  AND RC.BALANCE_ID    = $Calendar_Year_Id                              !HP00007
  AND RC.BALANCE_YEAR  = &TX.Balance_Year                               !HP00007
  AND RC.BALANCE_QTR   = &TX.Balance_Qtr                                !HP00007
  AND RC.STATE         = $State                                         !HP00007
  AND RC.LOCALITY      = ' '                                            !HP00007
  AND RC.BALANCE_PERIOD  =                                              !HP00007
      (SELECT MAX(BALANCE_PERIOD)                                       !HP00007
       FROM   PS_TAX_BALANCE                                            !HP00007
       WHERE  EMPLID        = RC.EMPLID                                 !HP00007
         AND  COMPANY       = RC.COMPANY                                !HP00007
         AND  BALANCE_ID    = RC.BALANCE_ID                             !HP00007
         AND  BALANCE_YEAR  = RC.BALANCE_YEAR                           !HP00007
         AND  BALANCE_QTR   = RC.BALANCE_QTR                            !HP00007
         AND  STATE         = RC.STATE                                  !HP00007
         AND  LOCALITY      = RC.LOCALITY                               !HP00007
         AND  TAX_CLASS     = RC.TAX_CLASS)                             !HP00007
  AND RP.EMPL_RCD  =                                                    !HP00007
      (SELECT MIN(EMPL_RCD)                                             !HP00007
       FROM   PS_EMPLOYMENT                                             !HP00007
       WHERE  EMPLID        = RC.EMPLID)                                !HP00007
  AND RC.NLGRS_QTD <> 0                                                 !HP00007
  AND RD.EMPLID            = RD1.EMPLID                                 !HP00007
  AND RD1.COUNTRY = {NID_Country}                                       !HP00007
  AND RD1.NATIONAL_ID_TYPE = $Payroll_NID_Type                          !HP00007
ORDER BY RD1.NATIONAL_ID                                                !HP00007
end-SELECT                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Generate-Tax-HP                                         !HP00007
begin-SELECT                                                            !HP00007
R.COMPANY     () on-break print=never level=1 before=Before-Company-HP  !HP00007
                                                 after=AFTER-COMPANY-HP !HP00007
R.EMPLOYER_ID () on-break print=never level=2                           !HP00007
                                             before=Bef-Empler-ID-Chg   !HP00007
                                              after=Empler-ID-Chg       !HP00007
R.STATE                                                                 !HP00007
R.EMPLID                                                                !HP00007
R.TAX_CLASS                                                             !HP00007
R.LOCALITY                                                              !HP00007
R.TXGRS_QTD                                                             !HP00007
R.NLGRS_QTD                                                             !HP00007
R.NATIONAL_ID                                                           !HP00007
R.LAST_NAME                                                             !HP00007
R.FIRST_NAME                                                            !HP00007
R.MIDDLE_NAME                                                           !HP00007
                                                                        !HP00007
  move &R.EMPLOYER_ID  to $Employer_ID                                  !HP00007
                                                                        !HP00007
  if &R.NlGrs_QTD > 0                                                   !HP00007
    do Process-Employee-HP                                              !HP00007
  else                                                                  !HP00007
    move &R.EMPLID    to $NegWageEmplID
    move &R.TXGRS_QTD to #NegWageTxGrs
    move &R.NLGRS_QTD to #NegWageNlGrs
    do Print-Summary-Data('NegWage')                                    !HP00007
  end-if                                                                !HP00007
                                                                        !HP00007
FROM  PS_R_TAX810MI R                                                   !HP00007
WHERE R.COMPANY       = &A.Company                                      !HP00007
ORDER BY R.EMPLOYER_ID, R.NATIONAL_ID                                   !HP00007
end-SELECT                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Process-Employee-HP                                     !HP00007
  move &R.NATIONAL_ID to $S123 xxx       !isolate first 3 digits        !HP00007
  if $S123 = &Temp_SSN_Mask or RTRIM(&R.NATIONAL_ID, ' ') = ''          !HP00007
    move '000000000'   to $NATIONAL_ID                                  !HP00007
  else                                                                  !HP00007
    move &R.NATIONAL_ID        to $NATIONAL_ID                          !HP00007
  end-if                                                                !HP00007
  let $LastName   = rtrim(&R.LAST_NAME, ' ')                            !HP00007
  let $FirstName  = rtrim(&R.FIRST_NAME, ' ')                           !HP00007
  let $MidInitial = {ps-substr}(&R.MIDDLE_NAME,1,1)                     !HP00007
  uppercase $LastName                                                   !HP00007
  uppercase $FirstName                                                  !HP00007
  uppercase $$MidInitial                                                !HP00007
                                                                        !HP00007
  move &R.NlGrs_QTD to #NlGrs_QTD                                       !HP00007
  add #NlGrs_QTD to #Acct_NlGrs_QTD                                     !HP00007
  multiply 100 times #NlGrs_QTD                                         !HP00007
                                                                        !HP00007
  add 1 to #Count_ID                                                    !HP00007
                                                                        !HP00007
  let #max_amt_NLG = 9999999999
  let #NLGrs_orig = #NlGrs_QTD
  let $done1 = 'N'
  while $done1 <> 'Y'
    do split-s-record (#NLGrs_orig,#NlGrs_QTD,#max_amt_NLG,$done1)
    do Format-Number(#NlGrs_QTD,  $NlGrs_QTD,  '0999999999')            !HP00007
    if $Format = 'BULK'
      do Write-W-Record-Bulk
    else
      do Write-S-Record-72                                              !HP00007
    end-if
    add 1 to #Count_R
  end-while
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Before-Company-HP                                       !HP00007
  do Get-Company-Data                                                   !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure After-Company-HP                                        !HP00007
! move #CoMonth1Count to #Month1Count
! move #CoMonth2Count to #Month2Count
! move #CoMonth3Count to #Month3Count
  do Print-Summary-Data('CoTotal')                                      !HP00007
                                                                        !HP00007
  add #Co_NlGrs_QTD to #Tot_NlGrs_QTD                                   !HP00007
  add #Count_S to #Count_F                                              !HP00007
  add #Count_R to #Count_R_Total                                        !HP00007
                                                                        !HP00007
  move 0 to #Co_NlGrs_QTD                                               !HP00007
  move 0 to #CoMonth1Count
  move 0 to #CoMonth2Count
  move 0 to #CoMonth3Count
  move 0 to #Count_S                                                    !HP00007
  move 0 to #Count_R                                                    !HP00007
  new-page                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Bef-Empler-ID-Chg                                       !HP00007
  let $StateEIN = EDIT(RTRIM(&R.Employer_ID,' '),'0999999999')          !HP00007
  do Record-totals-HP                                                   !HP00007
  if #Count_S_W > 0
    if $Format = 'BULK'
      add 1     to #Seq
      move #Seq to $Seq 099
      do Write-H-Record-Bulk
    else
      do Write-E-Record-72-HP                                           !HP00007
    end-if
  end-if
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Empler-ID-Chg                                           !HP00007
  if #Count_ID > 0                                                      !HP00007
    do Print-Summary-Data('AcctTotal')                                  !HP00007
                                                                        !HP00007
    add #Acct_NlGrs_QTD to #Co_NlGrs_QTD                                !HP00007
    add #Count_ID to #Count_S                                           !HP00007
                                                                        !HP00007
    move 0 to #Acct_NlGrs_QTD                                           !HP00007
    move 0 to #Count_ID                                                 !HP00007
    new-page                                                            !HP00007
  end-if                                                                !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Record-totals-HP                                        !HP00007

  let #Count_S_W   = 0
  let #Month1Count = 0
  let #Month2Count = 0
  let #Month3Count = 0
  let #TempGrs = 0
  let #TempTax = 0
  let #max_amt_NLG = 9999999999

begin-SELECT                                                            !HP00007
TR.MONTH1_COUNT6
TR.MONTH2_COUNT6
TR.MONTH3_COUNT6
TR.NLGRS_QTD                                                            !HP00007
TR.TXGRS_QTD
                                                                        !HP00007
  let #Month1Count = #Month1Count + &TR.MONTH1_COUNT6
  let #Month2Count = #Month2Count + &TR.MONTH2_COUNT6
  let #Month3Count = #Month3Count + &TR.MONTH3_COUNT6
  let #TempGrs     = #TempGrs + &TR.NLGRS_QTD
  let #TempTax     = #TempTax + &TR.TXGRS_QTD

  move &TR.NLGRS_QTD to #EmplGrs
  multiply 100 times #EmplGrs
  let #NLGrs_orig = #EmplGrs
  let $done1 = 'N'
  while $done1 <> 'Y'
    do split-s-record (#NLGrs_orig,#EmplGrs,#max_amt_NLG,$done1)
    add 1 to #Count_S_W
  end-while

FROM  PS_R_TAX810MI TR                                                  !HP00007
WHERE TR.COMPANY       = &A.Company                                     !HP00007
AND   TR.EMPLOYER_ID   = &R.Employer_ID                                 !HP00007
AND   TR.NLGRS_QTD > 0                                                  !HP00007
end-SELECT                                                              !HP00007
                                                                        !HP00007
  move #Count_S_W to $Count_S_72 0999999                                !HP00007
  move #Count_S_W to $Count_W 0999999

  multiply 100 times #TempGrs                                           !HP00007
  multiply 100 times #TempTax                                           !HP00007
  move #TempGrs to $NlGrs_QTD_S_72 0999999999999                        !HP00007
  move #TempGrs to $CoNlGrs 0999999999999

  let #TempExc = #TempGrs - #TempTax
  move #TempExc to $CoExcWages_QTD 0999999999999
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Write-E-Record-72-HP                                    !HP00007
                                                                        !HP00007
  write 10 from 'E':1                                                   !HP00007
          $StateEIN:10                                                  !HP00007
           $RptYear:4                                                   !HP00007
            $RptQtr:1                                                   !HP00007
                $Sp:24                                                  !HP00007
        $Count_S_72:7                                                   !HP00007
    $NlGrs_QTD_S_72:13                                                  !HP00007
                $Sp:12                                                  !HP00007
                                                                        !HP00007
  move 'Y' to $RecordWritten                                            !HP00007
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Read-Employer-ID-Default                                !HP00007
                                                                        !HP00007
  do Get-State-Tax-Data                                                 !HP00007
                                                                        !HP00007
  if RTRIM(&Employer_ID_SUT,' ') = ''                                   !HP00007
    do Employer_ID_missing                                              !HP00007
  end-if                                                                !HP00007
                                                                        !HP00007
  let $StateEIN = RTRIM(&Employer_ID_SUT, ' ')                          !HP00007
  let #ID_Length = length($StateEIN)                                    !HP00007
                                                                        !HP00007
  let $ExtReq = 'Y'                                                     !HP00007
                                                                        !HP00007
  evaluate #ID_Length                                                   !HP00007
    when = 10                                                           !HP00007
      let $ExtReq = 'N'                                                 !HP00007
    when = 07                                                           !HP00007
      break                                                             !HP00007
    when-Other                                                          !HP00007
      do Employer_ID_warning                                            !HP00007
  end-evaluate                                                          !HP00007
                                                                        !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Employer-ID                                         !HP00007
                                                                        !HP00007
  let $Employer_ID = $StateEIN                                          !HP00007
  let $Employer_ID_Ext = ''                                             !HP00007
                                                                        !HP00007
  if $ExtReq = 'Y'                                                      !HP00007
      let $Job_Found = 'N'                                              !HP00007
      do Get-Active-Job                                                 !HP00007
                                                                        !HP00007
      if $Job_Found = 'N'                                               !HP00007
        do Get-Inactive-Job                                             !HP00007
      end-if                                                            !HP00007
                                                                        !HP00007
      if $Job_Found = 'Y'                                               !HP00007
        do Get-Employer-ID-Extension                                    !HP00007
        CONCAT $Employer_ID_Ext with $Employer_ID                       !HP00007
      else                                                              !HP00007
        CONCAT '000' with $Employer_ID                                  !HP00007
      end-if                                                            !HP00007
  end-if                                                                !HP00007

  let $Employer_ID = rtrim($Employer_ID,' ')
  if length($Employer_ID) > 10
    let $Employer_ID = substr($Employer_ID, 1, 10)
    display ' '
    display '********************************* Warning **********************************'
    display '*** Employer Number is longer than 10 digits.                            ***'
    display '*** Only first 10 digits will be reported on the output file.            ***'
    display '****************************************************************************'
    display 'Company - '  noline
    display $Company noline
    display '    Emplid - '  noline
    display $EmplID noline
    display '    Tax Location Code - ' noline
    display $Tax_Location
    display 'Employer ID - ' noline
    display $StateEIN noline
    display '    Extension - ' noline
    display $Employer_ID_Ext
    display 'Reported Employer Number - ' noline
    display $Employer_ID
  end-if

end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Active-Job                                          !HP00007
                                                                        !HP00007
begin-select                                                            !HP00007
JOB.BUSINESS_UNIT                                                       !HP00007
JOB.TAX_LOCATION_CD                                                     !HP00007
                                                                        !HP00007
  let $Business_Unit = &JOB.BUSINESS_UNIT                               !HP00007
  let $Tax_Location  = &JOB.TAX_LOCATION_CD                             !HP00006
  let $Job_Found     = 'Y'                                              !HP00007
                                                                        !HP00007
FROM PS_JOB JOB                                                         !HP00007
WHERE JOB.EMPLID     = $EmplID                                          !HP00007
  AND JOB.COMPANY    = $Company                                         !HP00007
  AND JOB.EMPL_RCD  =                                                   !HP00007
      (SELECT MIN(EMPL_RCD)                                             !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB.EMPLID                                    !HP00007
          AND COMPANY   = JOB.COMPANY                                   !HP00007
          AND EMPL_STATUS = 'A')                                        !HP00007
  AND JOB.EFFDT    =                                                    !HP00007
      (SELECT MAX(EFFDT)                                                !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB.EMPLID                                    !HP00007
          AND COMPANY   = JOB.COMPANY                                   !HP00007
          AND EMPL_RCD = JOB.EMPL_RCD                                   !HP00007
          AND EFFDT    <= $AsOfDate)                                    !HP00007
  AND JOB.EFFSEQ   =                                                    !HP00007
      (SELECT MAX(EFFSEQ)                                               !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB.EMPLID                                    !HP00007
          AND COMPANY   = JOB.COMPANY                                   !HP00007
          AND EMPL_RCD = JOB.EMPL_RCD                                   !HP00007
          AND EFFDT     = JOB.EFFDT)                                    !HP00007
end-select                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Inactive-Job                                        !HP00007
                                                                        !HP00007
begin-select                                                            !HP00007
JOB1.BUSINESS_UNIT                                                      !HP00007
JOB1.TAX_LOCATION_CD                                                    !HP00007
                                                                        !HP00007
  let $Business_Unit = &JOB1.BUSINESS_UNIT                              !HP00007
  let $Tax_Location  = &JOB1.TAX_LOCATION_CD                            !HP00006
  let $Job_Found     = 'Y'                                              !HP00007
                                                                        !HP00007
FROM PS_JOB JOB1                                                        !HP00007
WHERE JOB1.EMPLID     = $EmplID                                         !HP00007
  AND JOB1.COMPANY    = $Company                                        !HP00007
  AND JOB1.EMPL_RCD  =                                                  !HP00007
      (SELECT MIN(EMPL_RCD)                                             !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB1.EMPLID                                   !HP00007
          AND COMPANY   = JOB1.COMPANY)                                 !HP00007
  AND JOB1.EFFDT    =                                                   !HP00007
      (SELECT MAX(EFFDT)                                                !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB1.EMPLID                                   !HP00007
          AND COMPANY   = JOB1.COMPANY                                  !HP00007
          AND EMPL_RCD = JOB1.EMPL_RCD                                  !HP00007
          AND EFFDT    <= $AsOfDate)                                    !HP00007
  AND JOB1.EFFSEQ   =                                                   !HP00007
      (SELECT MAX(EFFSEQ)                                               !HP00007
         FROM PS_JOB                                                    !HP00007
        WHERE EMPLID    = JOB1.EMPLID                                   !HP00007
          AND COMPANY   = JOB1.COMPANY                                  !HP00007
          AND EMPL_RCD = JOB1.EMPL_RCD                                  !HP00007
          AND EFFDT     = JOB1.EFFDT)                                   !HP00007
end-select                                                              !HP00007
end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Get-Employer-ID-Extension                               !HP00007
begin-select                                                            !HP00007
LOC.EMPLOYER_ID_EXT                                                     !HP00007
                                                                        !HP00007
  let $Employer_ID_Ext = &LOC.Employer_ID_Ext                           !HP00007
                                                                        !HP00007
FROM PS_CO_UI_RPTCD_TBL LOC                                             !HP00007
WHERE LOC.COMPANY = $Company                                            !HP00007
  AND LOC.LOCATION = $Tax_Location                                      !HP00006
  AND LOC.EFFDT =                                                       !HP00007
      (SELECT MAX(EFFDT)                                                !HP00007
         FROM PS_CO_UI_RPTCD_TBL                                        !HP00007
        WHERE COMPANY  = LOC.COMPANY                                    !HP00007
          AND LOCATION = LOC.LOCATION                                   !HP00007
          AND EFFDT   <= $AsOfDate)                                     !HP00007
  AND LOC.EFF_STATUS = 'A'                                              !HP00007
end-select                                                              !HP00007

  if rtrim($Employer_ID_Ext,' ') = ''
    let $Employer_ID_Ext = '000'
  end-if

end-procedure                                                           !HP00007
                                                                        !HP00007
begin-procedure Employer_ID_warning                                                      !HP00007
                                                                                         !HP00007
  display '********************************* Warning **********************************' !HP00007
  display '*** Incorrect Format for State Unemployment in Company State Tax Table.  ***' !HP00007
  display '*** Michigan State Unemployment ID consists of an assigned 7 digits      ***' !HP00007
  display '*** account number plus 3 digit multi-unit number.                       ***' !HP00007
  display '****************************************************************************' !HP00007
  display 'Company - '  noline                                                           !HP00007
  display $Company                                                                       !HP00007
  display 'Employer ID - ' noline                                                        !HP00007
  display $StateEIN                                                                      !HP00007
end-procedure                                                                            !HP00007

begin-procedure Insert-Work-Record                                      !HP99999
                                                                        !HP99999
   move #MonEmpFlg1 to $MonEmpFlg1 9
   move #MonEmpFlg2 to $MonEmpFlg2 9
   move #MonEmpFlg3 to $MonEmpFlg3 9
   move #Txgrs_QTD to $Txgrs_QTD 99999999999.99mi
   move #Nlgrs_QTD to $Nlgrs_QTD 99999999999.99mi
                                                                        !HP99999
  let $err-statement1 = 'TAX810MI, Insert-Error - INSERT-WORK-RECORD'   !HP99999
  let $err-statement2 = 'Key Values: TABLE PS_R_TAX810MI' ||            !HP99999
                        ', COMPANY ' || $Company ||                     !HP99999
                        ', EMPLOYER_ID ' || $Employer_ID ||             !HP99999
                        ', NATIONAL_ID ' || $National_ID ||             !HP99999
                        ', STATE ' || $Emp_State ||                     !HP99999
                        ', EMPLID ' || $Emplid ||                       !HP99999
                        ', LAST_NAME ' || $LastName ||
                        ', FIRST_NAME ' || $FirstName ||
                        ', MIDDLE_NAME ' || $MiddleName ||
                        ', TAX_CLASS ' || $Tax_Class ||                 !HP99999
                        ', LOCALITY ' || $Locality ||                   !HP99999
                        ', MONTH1_COUNT6 ' || $MonEmpFlg1 ||
                        ', MONTH2_COUNT6 ' || $MonEmpFlg2 ||
                        ', MONTH3_COUNT6 ' || $MonEmpFlg3 ||
                        ', TXGRS_QTD ' || $Txgrs_QTD ||                 !HP99999
                        ', NLGRS_QTD ' || $Nlgrs_QYD                    !HP99999
                                                                        !HP99999
begin-SQL on-error=Error-Display                                        !HP99999
  INSERT INTO PS_R_TAX810MI                                             !HP99999
        (COMPANY,                                                       !HP99999
         EMPLOYER_ID,                                                   !HP99999
         NATIONAL_ID,                                                   !HP99999
         STATE,                                                         !HP99999
         EMPLID,                                                        !HP99999
         LAST_NAME,
         FIRST_NAME,
         MIDDLE_NAME,
         TAX_CLASS,                                                     !HP99999
         LOCALITY,                                                      !HP99999
         MONTH1_COUNT6,
         MONTH2_COUNT6,
         MONTH3_COUNT6,
         TXGRS_QTD,                                                     !HP99999
         NLGRS_QTD)                                                     !HP99999
 VALUES ($Company,                                                      !HP99999
         $Employer_ID,                                                  !HP99999
         $National_ID,                                                  !HP99999
         $Emp_State,                                                    !HP99999
         $EmplID,                                                       !HP99999
         $LastName,
         $FirstName,
         $MiddleName,
         $Tax_Class,                                                    !HP99999
         $Locality,                                                     !HP99999
         #MonEmpFlg1,
         #MonEmpFlg2,
         #MonEmpFlg3,
         #Txgrs_QTD,                                                    !HP99999
         #Nlgrs_QTD)                                                    !HP99999
end-SQL                                                                 !HP99999
end-procedure                                                           !HP99999
                                                                        !HP99999
begin-procedure Delete-Work-File                                        !HP99999
                                                                        !HP99999
  let $err-statement1 = 'TAX810MI, Delete-Error - DELETE-WORK-FILE'     !HP99999
  let $err-statement2 = ' '                                             !HP99999
                                                                        !HP99999
begin-SQL On-Error=Error-Display                                        !HP99999
DELETE FROM PS_R_TAX810MI                                               !HP99999
end-SQL                                                                 !HP99999
                                                                        !HP99999
end-procedure                                                           !HP99999
                                                                        !HP99999
begin-procedure Record-F-Totals-HP

  let #Count_Co       = 0
  let #TotCoNlGrs_QTD = 0

begin-SELECT
PF.COMPANY
PF.EMPLOYER_ID
#ifdef DB2ALL
DECIMAL(SUM(PF.NLGRS_QTD),15,2)  &TotalGrs_HP
#else
SUM(PF.NLGRS_QTD)                &TotalGrs_HP
#endif

  let #Count_Co       = #Count_Co + 1
  let #TotCoNlGrs_QTD = #TotCoNlGrs_QTD + &TotalGrs_HP

FROM  PS_R_TAX810MI PF
WHERE PF.NLGRS_QTD > 0
GROUP BY PF.COMPANY, PF.EMPLOYER_ID
end-SELECT

  if #Count_Co > 0
    move #Count_Co to $Count_Co 0999999
    multiply 100 times #TotCoNlGrs_QTD
    move #TotCoNlGrs_QTD to $TotCoNlGrs_QTD 0999999999999
    do Write-F-Record-Bulk
  end-if

end-procedure

begin-procedure Get-SW-Record-Count-HP
begin-SELECT
COUNT(*)  &SWCount

  if &SWCount > 0
    move 'Y' to $Process_Company
  end-if

FROM  PS_R_TAX810MI CC
WHERE CC.COMPANY = &A.COMPANY
end-SELECT
end-procedure

begin-procedure Get-Quarter-Dates

  evaluate $RptQtr
    when = '1'
      move '01' to $1stmonth
      move '02' to $2ndmonth
      move '03' to $3rdmonth
      move '31' to $Lastday
      break
    when = '2'
      move '04' to $1stmonth
      move '05' to $2ndmonth
      move '06' to $3rdmonth
      move '30' to $Lastday
      break
    when = '3'
      move '07' to $1stmonth
      move '08' to $2ndmonth
      move '09' to $3rdmonth
      move '30' to $Lastday
      break
    when-other
      move '10' to $1stmonth
      move '11' to $2ndmonth
      move '12' to $3rdmonth
      move '31' to $Lastday
  end-evaluate

  let $Date_Field  = $RptYear || $1stMonth || '01'
  do Format-DateTime($Date_Field, $First_Day_Of_Quarter, {DEFCMP},'','native')

  let $Date_Field  = $RptYear || $3rdMonth || $Lastday
  do Format-DateTime($Date_Field, $Last_Day_Of_Quarter, {DEFCMP},'','native')

  let $12thOfMonth1 = $RptYear || $1stMonth || '12'
  do Format-DateTime($12thOfMonth1, $12th_Of_1st_Month, {DEFCMP},'','native')

  let $12thOfMonth2 = $RptYear || $2ndMonth || '12'
  do Format-DateTime($12thOfMonth2, $12th_Of_2nd_Month, {DEFCMP},'','native')

  let $12thOfMonth3 = $RptYear || $3rdMonth || '12'
  do Format-DateTime($12thOfMonth3, $12th_Of_3rd_Month, {DEFCMP},'','native')

end-procedure

begin-procedure Get-Monthly-Employment-Indicator
begin-SELECT
PE.EARNS_BEGIN_DT
PE.EARNS_END_DT

  move &PE.EARNS_BEGIN_DT to $EarnsBD
  move &PE.EARNS_END_DT   to $EarnsED

  do Format-DateTime($EarnsBD, $EarnsBeginDt, {DEFCMP},'','')
  do Format-DateTime($EarnsED, $EarnsEndDt, {DEFCMP},'','')

  if $EarnsBeginDt <= $12thOfMonth1 and $EarnsEndDt >= $12thOfMonth1
    let #MonEmpFlg1 = 1
  end-if

  if $EarnsBeginDt <= $12thOfMonth2 and $EarnsEndDt >= $12thOfMonth2
    let #MonEmpFlg2 = 1
  end-if

  if $EarnsBeginDt <= $12thOfMonth3 and $EarnsEndDt >= $12thOfMonth3
    let #MonEmpFlg3 = 1
  end-if

FROM  PS_PAY_EARNINGS PE, PS_PAY_CHECK PC
WHERE PE.COMPANY    = $Company
  AND PE.EMPLID     = $EmplID
  AND PE.PAY_END_DT BETWEEN $First_Day_Of_Quarter AND $Last_Day_Of_Quarter
  AND PE.PAY_LINE_STATUS = 'F'
  AND (($12th_Of_1st_Month BETWEEN PE.EARNS_BEGIN_DT AND PE.EARNS_END_DT)
    OR ($12th_Of_2nd_Month BETWEEN PE.EARNS_BEGIN_DT AND PE.EARNS_END_DT)
    OR ($12th_Of_3rd_Month BETWEEN PE.EARNS_BEGIN_DT AND PE.EARNS_END_DT))
  AND PE.COMPANY    = PC.COMPANY
  AND PE.PAYGROUP   = PC.PAYGROUP
  AND PE.PAY_END_DT = PC.PAY_END_DT
  AND PE.OFF_CYCLE  = PC.OFF_CYCLE
  AND PE.PAGE_NUM   = PC.PAGE_NUM
  AND PE.LINE_NUM   = PC.LINE_NUM
  AND PC.PAYCHECK_STATUS IN ('F','A','R')
end-SELECT
end-procedure

#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'txrnctl1.sqc'  !Select-Parameters procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'geteandg.sqc'  !Get-EandG procedure                           !HP00001
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getfrmat.sqc'  !Get-Diskette-Format procedure
#Include 'rptsmmry.sqc'  !Print-Summary-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
#Include 'stderror.sqc'  !Routine for error display                     !HP99999
#Include 'txovrflw.sqc'  !Split-S-Record
