!***********************************************************************
!  PAY715: Print contents of Garnishment Rule/Exempt/Operands Table    *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/06:10:57:25                           
!             $Release:  HR9                                           
!            $Revision:  102                                           
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY715' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language
  move $Pay715_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY715 program ids
  do Init_Report_Translation($ReportID,$language_cd)

  do Get_Field_Information ('PAY715', 'REPORT_TITLE',  $Pay715_ReportTitle,   #dummy_width)
  do Get_Field_Information ('PAY715', 'REPORT_TITLE2', $Pay715_ReportTitle2,  #dummy_width)
  do Get_Field_Information ('PAY715', 'REPORT_TITLE3', $Pay715_ReportTitle3,  #dummy_width)
  do Get_Field_Information ('PAY715', 'GARN_DE_DEFN1', $Pay715_Garn_DE_Defn1, #dummy_width)
  do Get_Field_Information ('PAY715', 'MAINT_RESP1',   $Pay715_Maint_Resp1,   #dummy_width)
  do Get_Field_Information ('PAY715', 'EFFDT1',        $Pay715_EffDt1,        #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_CALC1',      $Pay715_Ex_Calc1,      #dummy_width)
  do Get_Field_Information ('PAY715', 'STATUS',        $Pay715_Status,        #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_HRS1',       $Pay715_Ex_Hrs1,       #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_PCT1',       $Pay715_Ex_Pct1,       #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_AMT1',       $Pay715_Ex_Amt1,       #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_DEP_EXCL1',  $Pay715_Ex_Dep_Excl1,  #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_DEP_AMT1',   $Pay715_Ex_Dep_Amt1,   #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_ALW_AMT1',   $Pay715_Ex_Alw_Amt1,   #dummy_width)
  do Get_Field_Information ('PAY715', 'STATE',         $Pay715_State,         #dummy_width)
  do Get_Field_Information ('PAY715', 'RULE_ID2',      $Pay715_Rule_Id2,      #dummy_width)
  do Get_Field_Information ('PAY715', 'DESCRLONG2',    $Pay715_DescrLong2,    #dummy_width)
  do Get_Field_Information ('PAY715', 'GARN_DE_DEFN2', $Pay715_Garn_DE_Defn2, #dummy_width)
  do Get_Field_Information ('PAY715', 'MAINT_RESP2',   $Pay715_Maint_Resp2,   #dummy_width)
  do Get_Field_Information ('PAY715', 'EFFDT2',        $Pay715_EffDt2,        #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_CALC2',      $Pay715_Ex_Calc2,      #dummy_width)
  do Get_Field_Information ('PAY715', 'VAR_FREQ2',     $Pay715_Var_Freq2,     #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_HRS21',      $Pay715_Ex_Hrs21,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_HRS22',      $Pay715_Ex_Hrs22,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_PCT21',      $Pay715_Ex_Pct21,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_PCT22',      $Pay715_Ex_Pct22,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_PCT23',      $Pay715_Ex_Pct23,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_PCT24',      $Pay715_Ex_Pct24,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_AMT21',      $Pay715_Ex_Amt21,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_AMT22',      $Pay715_Ex_Amt22,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_AMT23',      $Pay715_Ex_Amt23,      #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_DEP_EXCL2',  $Pay715_Ex_Dep_Excl2,  #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_DEP_AMT2',   $Pay715_Ex_Dep_Amt2,   #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_ALW_AMT2',   $Pay715_Ex_Alw_Amt2,   #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_MIN',        $Pay715_Ex_Min,        #dummy_width)
  do Get_Field_Information ('PAY715', 'EX_MAX',        $Pay715_Ex_Max,        #dummy_width)
  do Get_Field_Information ('PAY715', 'STEP',          $Pay715_Step,          #dummy_width)
  do Get_Field_Information ('PAY715', 'CALC_TYPE',     $Pay715_Calc_Type,     #dummy_width)
  do Get_Field_Information ('PAY715', 'ELEMENT1',      $Pay715_Element1,      #dummy_width)
  do Get_Field_Information ('PAY715', 'AMOUNT',        $Pay715_Amount,        #dummy_width)
  do Get_Field_Information ('PAY715', 'PERCENT',       $Pay715_Percent,       #dummy_width)
  do Get_Field_Information ('PAY715', 'MATHEMATICAL',  $Pay715_Math,          #dummy_width)
  do Get_Field_Information ('PAY715', 'OPERATOR',      $Pay715_Operator,      #dummy_width)
  do Get_Field_Information ('PAY715', 'ELEMENT2',      $Pay715_Element2,      #dummy_width)
  do Get_Field_Information ('PAY715', 'CONDITION',     $Pay715_Condition,     #dummy_width)
  do Get_Field_Information ('PAY715', 'REFERENCED',    $Pay715_Referenced,    #dummy_width)

end-procedure

begin-HEADING 9

  #Include 'stdhdgtr.sqc'

  move 'United States' to $CountryName
  print $CountryName          (3,)        center

  if $Print-References-Hdr = 'Y'
      print $Pay715_Referenced     (6,1)
      print $Pay715_Referenced     (0,15)
      print $Pay715_EffDt1         (0,65)
      print $Pay715_EffDt1         (0,155)

  else
      if $Print-Operands-Hdr = 'Y'
          print $Pay715_Amount         (5,106)
          print $Pay715_Amount         (0,139)
          print $Pay715_Garn_DE_Defn1  (+1,52)
          print $Pay715_EffDt1         (0,64)
          print $Pay715_Percent        (0,106)
          print $Pay715_Math           (0,118)
          print $Pay715_Percent        (0,139)
      else
          print $Pay715_Garn_DE_Defn1  (6,49)
          print $Pay715_Maint_Resp1    (0,60)
          print $Pay715_EffDt1         (0,66)
          print $Pay715_Ex_Hrs1        (0,97)
          print $Pay715_Ex_Pct1        (0,105)
          print $Pay715_Ex_Amt1        (0,113)
          print $Pay715_Ex_Dep_Excl1   (0,122)
          print $Pay715_Ex_Dep_Amt1    (0,131)
          print $Pay715_Ex_Alw_Amt1    (0,142)
          print $Pay715_Ex_Amt1        (0,154)
          print $Pay715_Ex_Amt1        (0,165)
      end-if
  end-if

      print $Pay715_State          (+1,1)


  if $Print-References-Hdr = 'Y'
      print $Pay715_Rule_Id2       (0,15)
      print $Pay715_DescrLong2     (0,30)
      print $Pay715_EffDt2         (0,65)
      print $Pay715_State          (0,85)
      print $Pay715_Rule_Id2       (0,95)
      print $Pay715_DescrLong2     (0,110)
      print $Pay715_EffDt2         (0,155)

  else
      if $Print-Operands-Hdr = 'Y'
          print $Pay715_Rule_Id2       (0,8)
          print $Pay715_DescrLong2     (0,20)
          print $Pay715_Garn_DE_Defn2  (0,52)
          print $Pay715_EffDt2         (0,64)
          print $Pay715_Step           (0,75)
          print $Pay715_Calc_Type      (0,81)
          print $Pay715_Element1       (0,94)
          print $Pay715_Step           (0,106)
          print $Pay715_Operator       (0,118)
          print $Pay715_Element2       (0,128)
          print $Pay715_Step           (0,139)
          print $Pay715_Condition      (0,151)
      else
          print $Pay715_Rule_Id2       (0,7)
          print $Pay715_DescrLong2     (0,18)
          print $Pay715_Garn_DE_Defn2  (0,49)
          print $Pay715_Maint_Resp2    (0,60)
          print $Pay715_EffDt2         (0,66)
          print $Pay715_Status         (0,78)
          print $Pay715_Var_Freq2      (0,86)
          print $Pay715_Ex_Hrs21       (0,97)
          print $Pay715_Ex_Pct21       (0,105)
          print $Pay715_Ex_Amt21       (0,113)
          print $Pay715_Ex_Dep_Excl2   (0,122)
          print $Pay715_Ex_Dep_Amt2    (0,131)
          print $Pay715_Ex_Alw_Amt2    (0,142)
          print $Pay715_Ex_Min         (0,154)
          print $Pay715_Ex_Max         (0,165)

          print $Pay715_Ex_Hrs1        (+2,100)
          print $Pay715_Ex_Pct1        (0 ,110)
          print $Pay715_Ex_Pct1        (0 ,120)
          print $Pay715_Ex_Pct1        (0 ,130)
          print $Pay715_Ex_Amt1        (0 ,140)
          print $Pay715_Ex_Amt1        (0 ,160)

          print $Pay715_Ex_Hrs22       (+1,100)
          print $Pay715_Ex_Pct22       (0 ,110)
          print $Pay715_Ex_Pct23       (0 ,120)
          print $Pay715_Ex_Pct24       (0 ,130)
          print $Pay715_Ex_Amt22       (0 ,140)
          print $Pay715_Ex_Amt23       (0 ,160)
      end-if
  end-if

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main

 Move 'N' to $Print-Operands-Hdr
 Move 'N' to $Print-References-Hdr
 do Process-Rules

 NEW-PAGE
 move $Pay715_ReportTitle2 to $ReportTitle
 move 'Y' to $Print-Operands-Hdr
 do Process-Garn-Operands


 NEW-PAGE
 move $Pay715_ReportTitle3 to $ReportTitle
 Move 'N' to $Print-Operands-Hdr
 move 'Y' to $Print-References-Hdr
 do Process-Garn-References

END-PROCEDURE



BEGIN-PROCEDURE Process-Rules
BEGIN-SELECT

S.COUNTRY
A.STATE_GARN_LAW
  if RTRIM(&A.State_Garn_Law, ' ') = '$U'
    print 'FED'             (+1,1,5)   on-break level=1
  else
    print &A.State_Garn_Law (+1,1,5)   on-break level=1
  end-if
A.GARN_RULE_ID             (0,+1,10)   on-break level=1
A.DESCR                   !(0,+2,30)   on-break level=1
A.GARN_DE_DEFNID          !(0,+2,10)   on-break level=1
A.DESCRLONG

          move &A.STATE_GARN_LAW to $StateGarnLaw
          move &A.GARN_RULE_ID   to $GarnRule
          move &A.DESCR          to $RuleDescr
          move &A.DESCR          to $PRNT_DESCR
          move &A.GARN_DE_DEFNID to $PRNT_GARN_DE_DID
          move &A.DESCRLONG      to $RuleDescrLong
          move &A.DESCRLONG      to $PRNT_DESCRLONG
          move &A.EFFDT          to $Effdt
        if $Curr_language_cd <> $Psoptions_Language_Cd
        do Lang-Record-Details
        end-if
          print $PRNT_DESCR        (0,+1,30)   on-break level=1
          print $PRNT_GARN_DE_DID  (0,+1,10)   on-break level=1

A.MAINT_RESPONSBLTY        (0,+3,3)    on-break level=1
A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out               (0,+1)

A.EFF_STATUS
  print &A.Eff_Status      (0,+4)

  do Get-Exempt-Records


  if RTRIM($PRNT_DescrLong,' ') <> ''
    print $PRNT_DescrLong  (+1,18)      wrap 140 20
                                        ON=<10> STRIP=<13>
    POSITION (+1)
  end-if

FROM  PS_GARN_RULE_TBL A, PS_STATE_NAMES_TBL S
WHERE A.STATE_GARN_LAW = S.STATE
  AND S.COUNTRY = 'USA'
ORDER BY A.STATE_GARN_LAW, A.GARN_RULE_ID, A.EFFDT DESC
END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Get-Exempt-Records
  move 0 to #LineIncr
BEGIN-SELECT
B.GARN_VAR_FREQUENCY
  move 'GARN_VAR_FREQUENCY'   to $FieldName
  move &B.Garn_Var_Frequency  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName     (+#LineIncr,86,10)
B.GARN_EX_HRS
  do Format-Number(&B.Garn_Ex_Hrs, $out, 'b99.99')
  print $out               (0,+1,6)
B.PY_GARN_EX_HRS2
B.GARN_EX_PCT
  do Format-Number(&B.Garn_Ex_Pct, $out, 'b99.99')
  print $out               (0,+2,6)
B.PY_GARN_EX_PCT2
B.PY_GARN_EX_PCT3
B.PY_GARN_EX_PCT4
B.GARN_EX_AMT
  do Format-Number(&B.Garn_Ex_Amt,     $out, 'b99999.99')
  print $out               (0,+2,9)
B.PY_GARN_EX_AMT2
B.PY_GARN_EX_AMT3
B.GARN_EX_DEP_EXCL         (0,+3,6)     EDIT B99
B.GARN_EX_DEP_AMT
  do Format-Number(&B.Garn_Ex_Dep_Amt, $out, 'b99999.99')
  print $out               (0,+3,9)
B.GARN_EX_ALW_AMT
  do Format-Number(&B.Garn_Ex_Alw_Amt, $out, 'b99999.99')
  print $out               (0,+2,9)
B.GARN_EX_MIN
  do Format-Number(&B.Garn_Ex_Min, $out, 'b99999.99')
  print $out               (0,+3,9)
B.GARN_EX_MAX
  do Format-Number(&B.Garn_Ex_Max, $out, 'b99999.99')
  print $out               (0,+2,9)

  move 1 to #LineIncr

  if &B.Py_Garn_Ex_Hrs2 > 0   or
     &B.Py_Garn_Ex_Pct2 > 0   or
     &B.Py_Garn_Ex_Pct3 > 0   or
     &B.Py_Garn_Ex_Pct4 > 0   or
     &B.Py_Garn_Ex_Amt2 > 0   or
     &B.Py_Garn_Ex_Amt3 > 0

   print ' '                (+1,10)
   if &B.Py_Garn_Ex_Hrs2 > 0
     do Format-Number(&B.Py_Garn_Ex_Hrs2, $out, 'b99.99')
     print $out               (0,100,6)
   end-if

   if &B.Py_Garn_Ex_Pct2 > 0
     do Format-Number(&B.Py_Garn_Ex_Pct2, $out, 'b99.99')
     print $out               (0,110,6)
   end-if

   if &B.Py_Garn_Ex_Pct3 > 0
    do Format-Number(&B.Py_Garn_Ex_Pct3, $out, 'b99.99')
    print $out               (0,120,6)
   end-if

   if &B.Py_Garn_Ex_Pct4 > 0
     do Format-Number(&B.Py_Garn_Ex_Pct4, $out, 'b99.99')
     print $out               (0,130,6)
   end-if

   if &B.Py_Garn_Ex_Amt2 > 0
     do Format-Number(&B.Py_Garn_Ex_Amt2, $out, 'b99999.99')
     print $out              (0,140,9)
   end-if

   if &B.Py_Garn_Ex_Amt3 > 0
     do Format-Number(&B.Py_Garn_Ex_Amt3, $out, 'b99999.99')
     print $out              (0,160,9)
   end-if

  end-if

FROM  PS_GARN_EXEMPT_TBL B
WHERE B.STATE_GARN_LAW    = &A.State_Garn_Law
  AND B.GARN_RULE_ID      = &A.Garn_Rule_ID
  AND B.EFFDT             = &A.EffDt
ORDER BY B.GARN_EX_CALC
END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Process-Garn-Operands
BEGIN-SELECT
C.STATE_GARN_LAW
C.GARN_RULE_ID
C.DESCR
C.GARN_DE_DEFNID
C.EFFDT

  print &C.STATE_GARN_LAW  (+1,1)    on-break level=1
  print &C.GARN_RULE_ID    (0,8)     on-break level=1


  move &C.STATE_GARN_LAW to $StateGarnLaw
  move &C.GARN_RULE_ID   to $GarnRule
  move &C.DESCR          to $PRNT_DESCR
  move &C.DESCR          to $RuleDescr
  move &C.EFFDT          to $Effdt

  if $Curr_language_cd <> $Psoptions_Language_Cd
    do Lang-Record-Details
  end-if

  print $PRNT_DESCR        (0,20)    on-break level=1

  print &C.GARN_DE_DEFNID  (0,52)    on-break level=1

  do Format-DateTime(&C.EFFDT, $out, {DEFDATE}, '', '')
  print $out                   (0,64)

  do Get-Garn-Operands

D.COUNTRY

FROM  PS_GARN_RULE_TBL C, PS_STATE_NAMES_TBL D
WHERE C.STATE_GARN_LAW = D.STATE
  AND D.COUNTRY = 'USA'
  AND C.PY_GARN_RULE_COPY = 'N'
ORDER BY C.STATE_GARN_LAW, C.GARN_RULE_ID, C.EFFDT DESC
END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Get-Garn-Operands
  move 0 to #LineIncr
BEGIN-SELECT
E.GARN_STEP
E.GARN_CALC_TYPE
E.GARN_ELEMENT1
E.GARN_ELEMENT2
E.GARN_OPERATOR
E.GARN_AMOUNT1
E.GARN_AMOUNT2
E.GARN_PERCENT1
E.GARN_PERCENT2
E.GARN_STEP1
E.GARN_STEP2
E.GARN_CONDITION

  do Format-Number(&E.GARN_STEP, $out, '999')
  print $out                   (+#LineIncr,76)

  if &E.GARN_CALC_TYPE = 'D'
      print 'DE for Rule'      (0,81)
  else
      move 'GARN_CALC_TYPE'   to $FieldName
      move &E.GARN_CALC_TYPE  to $FieldValue
      do Read-Translate-Table
      print $XlatShortName     (0,81)
  end-if

  move 'GARN_ELEMENT1'   to $FieldName
  move &E.GARN_ELEMENT1  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,94)

  If RTRIM(&E.GARN_ELEMENT1,' ') = 'P'
      do Format-Number(&E.GARN_PERCENT1, $out, '999.99')
      print $out               (0,110)
  else
      if RTRIM(&E.GARN_ELEMENT1,' ') = 'Q'
          do Format-Number(&E.GARN_AMOUNT1, $out, 'B999999.99')
          print $out           (0,106)
      else
          if RTRIM(&E.GARN_ELEMENT1,' ') = 'S'
              do Format-Number(&E.GARN_STEP1, $out, '999')
              print $out       (0,110)
          end-if
      end-if
  end-if

  move 'GARN_OPERATOR'   to $FieldName
  move &E.GARN_OPERATOR  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,120)

  move 'GARN_ELEMENT2'   to $FieldName
  move &E.GARN_ELEMENT2  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,128)

  If RTRIM(&E.GARN_ELEMENT2,' ') = 'P'
      do Format-Number(&E.GARN_PERCENT2, $out, '999.99')
      print $out               (0,143)
  else
      if RTRIM(&E.GARN_ELEMENT2,' ') = 'Q'
          do Format-Number(&E.GARN_AMOUNT2, $out, 'B9999999.99')
          print $out           (0,138)
      else
          if RTRIM(&E.GARN_ELEMENT2,' ') = 'S'
              do Format-Number(&E.GARN_STEP2, $out, '999')
              print $out       (0,143)
          end-if
      end-if
  end-if

  move 'GARN_CONDITION'   to $FieldName
  move &E.GARN_CONDITION  to $FieldValue
  do Read-Translate-Table
  print $XlatLongName          (0,151,26)

  move 1 to #LineIncr

E.STATE_GARN_LAW
E.GARN_RULE_ID
E.EFFDT

FROM  PS_GARN_OPERANDS E
WHERE E.STATE_GARN_LAW = &C.STATE_GARN_LAW
  AND E.GARN_RULE_ID = &C.GARN_RULE_ID
  AND E.EFFDT = &C.EFFDT
ORDER BY E.STATE_GARN_LAW, E.GARN_RULE_ID, E.EFFDT, E.GARN_STEP
END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Process-Garn-References
BEGIN-SELECT
R.PY_GARN_LAW_FROM
R.PY_GARN_RULE_FROM
R.EFFDT_FROM
R.STATE_GARN_LAW
R.GARN_RULE_ID
R.DESCR
R.EFFDT

  print &R.PY_GARN_LAW_FROM  (+1,1)    on-break level=1
  print &R.PY_GARN_RULE_FROM (0,15)    on-break level=1


  move &R.PY_GARN_LAW_FROM  to $StateGarnLaw
  move &R.PY_GARN_RULE_FROM to $GarnRule
  move &R.EFFDT_FROM        to $Effdt

  do Process-GarnRef-Descr

  print $PRNT_DESCR          (0,30)    on-break level=1

  do Format-DateTime(&R.EFFDT_FROM, $out, {DEFDATE}, '', '')
  print $out                 (0,65)    on-break level=1

  print &R.STATE_GARN_LAW    (0,85)
  print &R.GARN_RULE_ID      (0,95)

  move &R.STATE_GARN_LAW to $StateGarnLaw
  move &R.GARN_RULE_ID   to $GarnRule
  move &R.DESCR          to $PRNT_DESCR
  move &R.DESCR          to $RuleDescr
  move &R.EFFDT          to $Effdt

  if $Curr_language_cd <> $Psoptions_Language_Cd
    do Lang-Record-Details
  end-if

  print $PRNT_DESCR          (0,110)

  do Format-DateTime(&R.EFFDT, $out, {DEFDATE}, '', '')
  print $out                 (0,155)

FROM  PS_GARN_RULE_TBL R, PS_STATE_NAMES_TBL T
WHERE R.STATE_GARN_LAW = T.STATE
  AND T.COUNTRY = 'USA'
  AND R.PY_GARN_RULE_COPY = 'Y'
ORDER BY R.PY_GARN_LAW_FROM, R.PY_GARN_RULE_FROM, R.EFFDT_FROM DESC, R.STATE_GARN_LAW, R.GARN_RULE_ID, R.EFFDT DESC
END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Process-GarnRef-Descr
BEGIN-SELECT
RD.DESCR

  move &RD.DESCR         to $PRNT_DESCR
  move &RD.DESCR         to $RuleDescr

  if $Curr_language_cd <> $Psoptions_Language_Cd
    do Lang-Record-Details
  end-if

FROM  PS_GARN_RULE_TBL RD
WHERE RD.STATE_GARN_LAW = &R.PY_GARN_LAW_FROM
  AND RD.GARN_RULE_ID = &R.PY_GARN_RULE_FROM
  AND RD.EFFDT = &R.EFFDT_FROM
END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Lang-Record-Details
BEGIN-SELECT
LA.DESCR
LA.DESCRLONG

    let $PRNT_DESCR = Cond(Rtrim(&LA.DESCR,' ')='', $RuleDescr, &LA.DESCR)
    let $PRNT_DESCRLONG = Cond(Rtrim(&LA.DESCRLONG,' ')='', $RuleDescrLong, &LA.DESCRLONG)

FROM PS_GARN_RULE_LANG LA
WHERE LA.STATE_GARN_LAW = $StateGarnLaw
  AND LA.GARN_RULE_ID   = $GarnRule
  AND LA.LANGUAGE_CD    = $Curr_language_cd
  AND LA.EFFDT          = (SELECT MAX(EFFDT) FROM PS_GARN_RULE_LANG
                            WHERE STATE_GARN_LAW = $StateGarnLaw
                              AND GARN_RULE_ID   = $GarnRule
                              AND LANGUAGE_CD    = $Curr_language_cd
                              AND EFFDT         <= $Effdt)
END-SELECT
END-PROCEDURE

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
