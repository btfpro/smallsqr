!***********************************************************************
!  PER718NL : Eenmalig laden Postcode bestand                          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!                                                                      
!          $Date:  2006/07/19:15:11:23                                 
!       $Release:  HR9                                                 
!      $Revision:  102                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
Begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-REPORT
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  if #filestat != 0
     Display 'Input file can not be opened Successfully....'
     Display 'Please Enter the correct Path and Input file name for the postal code'
     Display 'and Rerun the Process'
     let #prcs_run_status = 3
     do Update-Prcs-Run-Status
     goto last2
  end-if
  do Process-Main
  do Reset
  do Stdapi-Term
last2:
end-REPORT

begin-Heading 7
#Include 'stdhdg01.sqc'
end-heading

begin-procedure Init-Report
  move 'PER718NL' to $ReportID
  move 'Eenmalig laden Postcodebestand' to $ReportTitle
  display $ReportTitle
  do Stdapi-Init
  if $prcs_process_instance = ''
    do Ask-Init-File
  else
    do Select-Parameters
  end-if
  open $Filename as 1 for-reading record=275 status=#filestat


end-procedure

begin-procedure Get-Values
   do Get-Init-File
end-procedure

begin-procedure ask-init-file
  move 'N' to $Got-File-Name
  while $Got-File-Name='N'
    Input $Filename 'Wat Is De Naam v.h. Postcode-Laadbestand ?'
    If Rtrim($Filename,' ')<>''
       Move 'Y' to $Got-File-Name
    end-if
  end-while
end-procedure

begin-procedure Process-Main
move 'N' to $Header-Checked
let #RecordCount=0

let #commitNbr        = 0     !number of records processed since last commit
let #performCommit    = 1000  !perform a commit every #performCommit records
let #TimesCommit      = 0     !Times program performed a Commit

while 1 = 1 !Need a while to be able to BREAK
  read 1 into $ZipCdRecord:184
  if #end-file = 1
     break
  end-if
  !Check for Header or Trailer and do processing on it
  let $HeaderTrailerCheck = {ps-substr}($ZipCdRecord,1,3)
  if $HeaderTrailerCheck = '***'  !either header or trailer record
    do Process-Header-and-Trailer
  else !normal record
     do process-record
     add 1 to #recordcount
     add 1 to #commitNbr
     if #commitNbr = #performCommit
       do Commit-Transaction
       add 1 to #TimesCommit
       let #commitNbr = 0 
     end-if
  end-if
end-while
end-procedure

begin-procedure Process-Header-and-Trailer
if {ps-substr}($ZipCdRecord,1,4)='*** ' !header record has 3 stars
   if {ps-substr}($ZipCdRecord,1,32) = '*** POSTCODETABEL PTT REEKS VAN ' !correct header
      do Process-Header    ! in ziphdr.sqc
   else
      display 'Incorrect Bestandstype. Verwerking afgebroken'
      display $displ
      print 'Incorrect Bestandstype. Verwerking afgebroken' (+2,1,80)
      display $displ (+1,1,12)
      stop quiet
   end-if
end-if
if {ps-substr}($ZipCdRecord,1,4)='****'  !trailer record has 4 stars
   if {ps-substr}($ZipCdRecord,1,27)='**** AANTAL BESTANDRECORDS '  !correct trailer
      do process-Trailer  !in ziptrl.sqc
   else
      display 'Incorrect Bestandstype. Verwerking afgebroken'
      print 'Incorrect Bestandstype. Verwerking afgebroken' (+2,1,80)
      stop quiet
   end-if
end-if
end-procedure

begin-procedure Process-Header
let $FileMonth = {ps-substr}($ZipCdRecord,33,3)
let $Fileyear = {ps-substr}($ZipCdRecord,37,4)
do Convert-FileDate
begin-select
LAST_CHNG_DT_NLD &LastDate
  do Format-Datetime(&LastDate, $DBDate, {DEFDMY}, '', '')
  if $FileDate > $DBDate
     Display 'Bestandsdatum Geverifieerd.'
  else
     Display 'Dit Laadbestand is al eerder verwerkt. Verwerking wordt afgebroken.'
     Print 'Dit Laadbestand is al eerder verwerkt. Verwerking wordt afgebroken.' (+2,1,80)
     STOP QUIET
  end-if
from
PS_POSTAL_CHG_NLD
end-select
end-procedure

begin-procedure Convert-FileDate
evaluate $FileMonth
  when = 'JAN'
    let $Month = '01'
  when = 'FEB'
    let $Month = '02'
  when = 'MAA'
    let $Month = '03'
  when = 'APR'
    let $Month = '04'
  when = 'MEI'
    let $Month = '05'
  when = 'JUN'
    let $Month = '06'
  when = 'JUL'
    let $Month = '07'
  when = 'AUG'
    let $Month = '08'
  when = 'SEP'
    let $Month = '09'
  when = 'OKT'
    let $Month = '10'
  when = 'NOV'
    let $Month = '11'
  when = 'DEC'
    let $Month = '12'
  when-other
end-evaluate
move $Month to #FileMonth
let $FileDate = '01/'||$Month||'/'||$Fileyear
let $displ='Bestandsdatum : '||$FileDate
display $displ
end-procedure

begin-procedure Check-FileDate
let $DBMonth = {ps-substr}($DBDate,5,2)
move $DBMonth to #DBMonth
let $DBYear = {ps-substr}($DBDate,1,4)
move $DBYear to #DBYear

!display $DBDate
!display $DBYear
!display $DBMonth
!display $FileDate
!display $Month
!display $Fileyear

if #DBMonth <12
   if #FileMonth = #DBMonth+1 and #FileYear = #DBYear
      Display 'Bestandsdatum Geverifieerd.'
   else
      Display 'Foutieve Bestandsdatum. Verwerking wordt afgebroken.'
      Print 'Foutieve Bestandsdatum. Verwerking wordt afgebroken.' (+2,1,80)
      stop quiet
   end-if
else
   if #Filemonth = 1 and #Fileyear=#Dbyear+1
      Display 'Bestandsdatum Geverifieerd.'
   else
      display 'Foutieve Bestandsdatum. Verwerking Wordt Afgebroken.'
      Print 'Foutieve Bestandsdatum. Verwerking wordt afgebroken.' (+2,1,80)
      stop quiet
   end-if
end-if
end-procedure

begin-procedure Process-Record
let $POSTAL_NLD = {ps-substr}($ZipCdRecord,1,4)||' '|| {ps-substr}($ZipCdRecord,5,2)
let $RANGE_IND_NLD = {ps-substr}($ZipCdRecord,7,1)
let $BRKPNT_FROM_NLD = {ps-substr}($ZipCdRecord,8,5)
 move $BRKPNT_FROM_NLD to #BRKPNT_FROM_NLD
let $BRKPNT_UNTIL_NLD = {ps-substr}($ZipCdRecord,13,5)
 move $BRKPNT_UNTIL_NLD to #BRKPNT_UNTIL_NLD
let $POSTAL_CITY_NLD = {ps-substr}($ZipCdRecord,36,24)
let $POSTAL_CITY_NLD = rtrim($POSTAL_CITY_NLD,' ')
let $POSTAL_STREET_NLD = {ps-substr}($ZipCdRecord,77,24)
let $POSTAL_STREET_NLD = rtrim($POSTAL_STREET_NLD,' ')

if $RANGE_IND_NLD <>' '
   do Insert-Record !insert the record in the table
   do Insert-Lookuptable !insert also in the lookuptable
   add 1 to #RecordsInserted
end-if
end-procedure

begin-procedure Insert-Record
begin-sql on-error=sql-error
INSERT INTO PS_POSTAL_TBL_NLD
(POSTAL_NLD
,RANGE_IND_NLD
,BRKPNT_FROM_NLD
,BRKPNT_UNTIL_NLD
,POSTAL_CITY_NLD
,POSTAL_STREET_NLD)
values
($POSTAL_NLD
,$RANGE_IND_NLD
,#BRKPNT_FROM_NLD
,#BRKPNT_UNTIL_NLD
,$POSTAL_CITY_NLD
,$POSTAL_STREET_NLD)
end-sql
end-procedure

begin-procedure Insert-Lookuptable

LET #EO_NUM = #BRKPNT_FROM_NLD
While #EO_NUM <= #BRKPNT_UNTIL_NLD
  LET $EO_NUM1 = to_char(#EO_NUM)
  do Insert-Record-Lookuptable
  LET #EO_NUM = #EO_NUM + 2
end-while

end-procedure

begin-procedure Insert-Record-Lookuptable
begin-sql on-error=sql-error
INSERT INTO PS_EO_ADDR_VALIDAT
(COUNTRY
,POSTAL
,STATE
,CITY
,COUNTY
,ADDRESS1
,ADDRESS2
,ADDRESS3
,ADDRESS4
,ADDR_FIELD1
,ADDR_FIELD2
,ADDR_FIELD3
,NUM1
,NUM2
,HOUSE_TYPE)
values
('NLD'
,$POSTAL_NLD
,' '
,$POSTAL_CITY_NLD
,' '
,' '
,' '
,$POSTAL_STREET_NLD
,' '
,' '
,' '
,' '
,$EO_NUM1
,' '
,' ')
end-sql
end-procedure

begin-procedure Process-Trailer
move #RecordCount to $RecordCount 9999999
unstring $RecordCount by '.' into $RC
let $DisplRC = 'Aantal Bestandsrecords Gelezen  = '||$RC
Display $DisplRC
move #Recordsinserted to $RecordsInserted 9999999
unstring $RecordsInserted by '.' into $RI
let $DisplRI = 'Aantal Bestandsrecords Verwerkt = '||$RI
Display $DisplRI
let $File-RecordCount = {ps-substr}($ZipCdRecord,61,7)
move $File-RecordCount to #File-RecordCount

if #Recordsinserted = #File-RecordCount
   do Update-Chng-Data
else
   Display 'Verwerking Incorrect. Verwerking Afgebroken'
   Print 'Verwerking Incorrect. Verwerking Afgebroken' (+2,1,80)
   stop quiet
end-if
end-procedure

begin-procedure Process-Mut-Trailer
move #RecordCount to $RecordCount 9999999
unstring $RecordCount by '.' into $RC
let $DisplRC = 'Aantal Bestandsrecords Gelezen  = '||$RC
Display $DisplRC
move #InsertedRecordCount to $InsertedRecordCount 9999999
unstring $InsertedRecordCount by '.' into $RI
let $DisplRI = 'Aantal Nieuwe Records           = '||$RI
display $DisplRI
move #DeletedRecordCount to $DeletedRecordCount 9999999
unstring $DeletedRecordCount by '.' into $RD
let $DisplRD = 'Aantal Verwijderde Records      = '||$RD
display $DisplRD
move #UpdatedRecordCount to $UpdatedRecordCount 9999999
unstring $UpdatedRecordCount by '.' into $RU
let $DisplRU = 'Aantal Gemuteerde Records       = '||$RU
display $DisplRU
let $File-Updated-RecordCount={ps-substr}($ZipCdRecord,22,7)
move $File-Updated-RecordCount to #File-Updated-RecordCount
let $File-RecordCount = {ps-substr}($ZipCdRecord,86,7)
move $File-RecordCount to #File-RecordCount
if #RecordCount=#File-Updated-Recordcount
   do Update-Chng-Data
else
   display 'Verwerking Incorrect, Wordt Afgebroken.'
   Print 'Verwerking Incorrect. Verwerking Afgebroken' (+2,1,80)
   stop quiet
end-if
end-procedure

begin-procedure Update-Chng-Data
move '/' to $DDelimiter
  do Format-DateTime($FileDate,$FileDate,{DEFDMY},'','native')

begin-sql on-error=sql-error
DELETE FROM PS_POSTAL_CHG_NLD
end-sql

begin-sql on-error=sql-error
INSERT INTO PS_POSTAL_CHG_NLD
(LAST_CHNG_DT_NLD,
POSTAL_RCD_CNT_NLD)
Values
($FileDate
,#File-RecordCount)
end-sql

end-procedure

!begin-procedure Update-Chng-Data
!begin-sql on-error=sql-error
!UPDATE PS_POSTAL_CHG_NLD
!SET LAST_CHNG_DT_NLD = $FileDate
!, POSTAL_RCD_CNT_NLD = #File-RecordCount
!end-sql
!end-procedure

#include 'hrctlnld.sqc'  !Get run control parameter values
#include 'hrgetnld.sqc'  !Get values mask routines

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'tranctrl.sqc'  !Transaction control
