!***********************************************************************
!  TAX107 : W4 - EMPLOYEE WITHHOLDING ALLOWANCE AUDIT REPORT.         *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!
!                                                                      *
!          $Date:  2006/07/20:06:20:04                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set Database Environment

begin-report
  do Stdapi-Init
  move 'TAX107' to $ReportID
  move 'EMPLOYEE W4 AUDIT REPORT' to $ReportTitle
  display $ReportTitle
  display ''

  do Get-Run-ID

  do Init-DateTime
  do Get-Current-DateTime
  do Format-DateTime($AsOfToday, $out, {DEFMDY},'','')
  move $out to $ReportDate
  display 'Report Began: ' noline
  display $AsOfNow
  display ''

  do Get-From-Thru-Date

  do Order_clause

  do get-w4audit

  do Get-Current-DateTime
  display ''
  display 'Report Ended: ' noline
  display $AsOfNow
  do Stdapi-Term
end-report

#include 'setup31.sqc'   !Printer and page-size initialization


begin-heading 7

#include 'stdhdg01.sqc'

  do Format-DateTime($Fromdate, $FmDate, {DEFMDY},'','')
  do Format-DateTime($ThruDate, $TuDate, {DEFMDY},'','')

  print 'List Of W4 updated via the web from ' (+1,1)
  print $FmDate ()
  print '  to  '  ()
  print $TuDate ()

  print 'Employee Id and Name                      Marital Status'    (+2,1)
  print 'Total No Of Allowances  Addl Amount   Exempt Status'         (0,59)
  print '----------------------------------------- --------------'    (+1,1)
  print '----------------------  ------------  -------------'         (0,59)


end-heading


begin-procedure Get-Run-ID
  if $Prcs_Process_Instance = ''
     do Prompt
  else
    do Select-Parameters
  end-if
end-procedure


Begin-Procedure Prompt

while 1 = 1
    input $RunID maxlen=3 'Enter Valid Run ID' type=char

    if RTRIM($RunID, ' ') = ''
      display 'Exiting....'
      Stop
    end-if

    do edit-Run

    if $Break_YN = 'Y'
      Break
    End-If
End-While

End-Procedure


begin-procedure Edit-Run

  move 0 to #Found

begin-select

Z.FROMDATE
Z.THRUDATE
Z.ORDER_OPTION

  add 1 to #Found

FROM PS_RC_PAY Z
where Z.RUN_CNTL_ID = $RunID
end-select
  if #Found = 0
    display 'Error --  Not a Valid Run ID.'
    move 'N' to $Break_YN
  else

    if #Found = 1
    move 'Y' to $Break_YN

         Let $RC_PAY.FROMDATE = &Z.FROMDATE
         Let $RC_PAY.THRUDATE = &Z.THRUDATE
         Let $RC_PAY.ORDER_OPTION = &Z.ORDER_OPTION

    end-if

  end-if
end-procedure


Begin-Procedure Order_clause

DISPLAY 'ORDER_OPTION'
DISPLAY $RC_PAY.ORDER_OPTION
  Let $OrderByClause = ''
  If $RC_PAY.ORDER_OPTION = 'N'
      Let $OrderByClause = 'ORDER BY PERS.NAME'
  else
      Let $OrderByClause = 'ORDER BY W4.EMPLID'   !ORDER_OPTION = 'I'
  End-If

End-Procedure


begin-procedure get-w4audit
  display 'Generating report . . . ' noline

begin-SELECT
W4.EMPLID
W4.FWT_MAR_STATUS
W4.FWT_ALLOWANCES
W4.FWT_ADDL_AMT
W4.SPECIAL_FWT_STATUS
PERS.NAME

  move &W4.EMPLID              to $Emplid
  move &W4.FWT_ALLOWANCES      to #Fwt_Allowances
  move &W4.FWT_ADDL_AMT        to #Fwt_Addl_Amt


  let $FieldName    = 'FWT_MAR_STATUS'
  let $FieldValue   = &W4.FWT_MAR_STATUS
  do Read-Translate-Table
  let $Fwt_Mar_Status = $XLatShortName

  let $FieldName    = 'SPECIAL_FWT_STATUS'
  let $FieldValue   = &W4.SPECIAL_FWT_STATUS
  do Read-Translate-Table
  let $Special_Fwt_Status = $XLatShortName

  let $Name = rtrim(&PERS.NAME,' ')

  do  Print-W4Audit

FROM PS_FED_TAX_DATA  W4, PS_NAMES PERS
WHERE W4.EMPLID = PERS.EMPLID
  AND PERS.NAME_TYPE = 'PRI'
  AND PERS.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PERS.EMPLID
         AND  PX.NAME_TYPE = PERS.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfToday)
  AND LAST_UPDATE_DATE BETWEEN $FromDate AND $ThruDate
  AND LAST_ACTION = 'W'
[$OrderByClause]

end-SELECT
  display 'Done'
end-procedure


begin-procedure Print-W4Audit

  print $Emplid                 (+1,1)
  print $Name                   (0,13)
  print $Fwt_Mar_Status         (0,43)
  print #Fwt_Allowances         (0,59)
  print #Fwt_Addl_Amt           (0,83)
  print $Special_Fwt_Status     (0,97)

end-procedure


#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Update Process API
#include 'payrctl2.sqc'  !Select Run Control Parameters
#include 'pygetval.sqc'  !To Get as Of date
#include 'readxlat.sqc'  !Read-Translate-Table procedure
