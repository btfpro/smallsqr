!***********************************************************************
!  FGHR027: Active Position History Report                              *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2006/07/19:12:00:50                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'   !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-Report
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Init-Report
   do Process-Main
   do Reset
   do Stdapi-Term
end-report


!**********************************
! This procedure is used to read language-dependent text from the Strings table
!**********************************
Begin-Procedure Report-Translation
!**********************************
move 'POS004' to $ReportID
do Init_Report_Translation ($ReportID, $language_cd)
do Append_Report_Translation ('HR')

do Get_Field_Information ('POS004',     'REPORT_TITLE', $REPORTTITLE,   #DW)

do Get_Field_Information ('POS004',     'ACTION_DT1',   $ACT1,          #DW)
do Get_Field_Information ('POS004',     'ACTION_DT2',   $ACT2,          #DW)
do Get_Field_Information ('POS004',     'ACT_RSN1',     $ACTRSN1,       #DW)
do Get_Field_Information ('POS004',     'ACT_RSN2',     $ACTRSN2,       #DW)
do Get_Field_Information ('POS004',     'REG1',         $REGTMP1,       #DW)
do Get_Field_Information ('POS004',     'REG2',         $REGTMP2,       #DW)
do Get_Field_Information ('POS004',     'REG3',         $REGTMP3,       #DW)

do Get_Field_Information ('HR', 'BUDGET_POS1',  $BUDGET1,       #DW)
do Get_Field_Information ('HR', 'BUDGET_POS2',  $BUDGET2,       #DW)
do Get_Field_Information ('HR', 'JOB_SHR1',     $JOB_SHR1,      #DW)
do Get_Field_Information ('HR', 'JOB_SHR2',     $JOB_SHR2,      #DW)
do Get_Field_Information ('HR', 'STD_HRS1',     $HOURS1,        #DW)
do Get_Field_Information ('HR', 'STD_HRS2',     $HOURS2,        #DW)
do Get_Field_Information ('HR', 'STD_HRS_FREQ1',$HRS_FREQ1,     #DW)
do Get_Field_Information ('HR', 'STD_HRS_FREQ2',$HRS_FREQ2,     #DW)
do Get_Field_Information ('HR', 'HEAD_CT1',     $COUNT1,        #DW)
do Get_Field_Information ('HR', 'HEAD_CT2',     $COUNT2,        #DW)
do Get_Field_Information ('HR', 'HEAD_CT3',     $COUNT3,        #DW)
do Get_Field_Information ('HR', 'LOCATION2',    $LOC2,          #DW)
do Get_Field_Information ('HR', 'AGENCY',       $AGENCY,        #DW)
do Get_Field_Information ('HR', 'REPORT_TO1',   $RPT1,          #DW)
do Get_Field_Information ('HR', 'REPORT_TO2',   $RPT2,          #DW)
do Get_Field_Information ('HR', 'DEPTID',       $DEPTID,        #DW)
do Get_Field_Information ('HR', 'JOBCODE1',     $JOBCODE1,      #DW)
do Get_Field_Information ('HR', 'JOBCODE2',     $JOBCODE2,      #DW)

do Get_Field_Information ('HR', 'POSITION_STAT1',$POSSTAT1,     #DW)
do Get_Field_Information ('HR', 'POSITION_STAT2',$POSSTAT2,     #DW)
do Get_Field_Information ('HR', 'EFFDT1',       $EFFDT1,        #DW)
do Get_Field_Information ('HR', 'EFFDT2',       $EFFDT2,        #DW)
do Get_Field_Information ('HR', 'POSITION_TITLE',$POSTITLE,     #DW)
do Get_Field_Information ('HR', 'REG_TEMP1',    $REG1,          #DW)
do Get_Field_Information ('HR', 'REG_TEMP2',    $REG2,          #DW)
do Get_Field_Information ('HR', 'FULL_PART_TM1',$FULL1,         #DW)
do Get_Field_Information ('HR', 'FULL_PART_TM2',$FULL2,         #DW)
do Get_Field_Information ('HR', 'STATUS_DT1',   $STATDT1,       #DW)
do Get_Field_Information ('HR', 'STATUS_DT2',   $STATDT2,       #DW)
do Get_Field_Information ('HR', 'POSITION_NBR1',$POSITION,      #DW)
do Get_Field_Information ('HR', 'POSITION_NBR2',$POSITION2,     #DW)
do Get_Field_Information ('HR', 'LOCATION',     $LOCATION,      #DW)

End-Procedure

begin-Heading 8
#Include 'stdhdg03.sqc'
!******************************
!     column headings         *
!******************************
LET $REG = $REG1 || '/'
LET $FULL = $FULL1 || '/'

   print $COUNT1        (+2,122)
   print $REGTMP1       (0,128)

   print $POSITION      (+1,1)
   print $EFFDT1        (0,11)
   print $ACT1          (0,23)
   print $ACTRSN1       (0,35)
   print $JOBCODE1      (0,82)
   print $RPT1          (0,91)
   print $COUNT2        (0,122)
   print $REGTMP2       (0,128)
   print $FULL          (0,133)
   print $HOURS1        (0,141)
   print $STAT1         (0,158)
   print $BUDGET1       (0,169)

!*******************************
!     column headings          *
!*******************************
   print $POSITION2     (+1,1)
   print $EFFDT2        (0,11)
   print $ACT2          (0,23)
   print $ACTRSN2       (0,35)
   print $POSTITLE      (0,39)
   print $DEPTID        (0,73)
   print $JOBCODE2      (0,82)
   print $RPT2          (0,91)
   print $AGENCY        (0,101)
   print $LOCATION      (0,110)
   print $COUNT3        (0,122)
   print $REGTMP3       (0,128)
   print $FULL2         (0,133)
   print $HOURS2        (0,141)
   print $STATDT1       (0,148)
   print $EFFDT2        (0,158)
   print $BUDGET2       (0,169)

end-Heading
!********************************************


!********************************************
begin-procedure Init-Report

  do Get-Report-Language

  do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-As-Of-Date
  else
    do Select-Parameters
    do Get-As-Of-Date
  end-if

  move 'FGHR027' to $ReportID

  display $ReportTitle
end-procedure



!***********************************************************************
!  PROC Process-Main
!         Main Procedure
!***********************************************************************
begin-procedure Process-Main
begin-SELECT

A.POSITION_NBR             (+1,1) on-break level=1 skiplines=1
A.EFFDT
   do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
    print $out             (0,11)
A.ACTION_DT
   do Format-DateTime(&A.Action_Dt, $out, {DEFDATE}, '', '')
    print $out             (0,23)
A.ACTION_REASON            (0,35)
A.DESCR                    (0,39)
A.DEPTID                   (0,73)
A.JOBCODE                  (0,82)
A.REPORTS_TO               (0,91)
A.COMPANY                  (0,101)
A.LOCATION                 (0,110)
A.MAX_HEAD_COUNT           (0,124)
A.REG_TEMP                 (0,129)
A.FULL_PART_TIME           (0,133)
A.STD_HOURS
    do Format-Number(&A.Std_Hours, $out, 'bbb9.99')
    print $out             (0,139)
A.POSN_STATUS
    move 'POSN_STATUS' to $Fieldname
    move &A.Posn_Status to  $FieldValue
    do Read-Translate-Table
    print $XlatShortName   (0,148)
A.STATUS_DT
    if RTRIM(&A.Status_Dt, ' ') <> ''
       do Format-DateTime(&A.Status_Dt, $out, {DEFDATE}, '', '')
       print $out          (0,158)
    end-if
A.BUDGETED_POSN            (0,170)
  do Get-History-Records

FROM  PS_POSITION_DATA A
WHERE A.EFF_STATUS = 'A'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
        FROM PS_POSITION_DATA
       WHERE POSITION_NBR = A.POSITION_NBR
         AND EFFDT <= $AsOfDate)

ORDER BY A.POSITION_NBR,A.EFFDT DESC
end-SELECT
end-procedure

begin-procedure Get-History-Records
begin-SELECT
B.POSITION_NBR             (+1,1) on-break level=1 print=never
B.EFFDT
   do Format-DateTime(&B.EffDt, $out, {DEFDATE}, '', '')
   print $out              (0,11)
B.ACTION_DT
   do Format-DateTime(&B.Action_Dt, $out, {DEFDATE}, '', '')
   print $out              (0,23)
B.ACTION_REASON            (0,35)
B.DESCR                    (0,39)
B.DEPTID                   (0,73)
B.JOBCODE                  (0,82)
B.REPORTS_TO               (0,91)
B.COMPANY                  (0,101)
B.LOCATION                 (0,110)
B.MAX_HEAD_COUNT           (0,124)
B.REG_TEMP                 (0,129)
B.FULL_PART_TIME           (0,133)
B.STD_HOURS
    do Format-Number(&B.Std_Hours, $out, 'bbb9.99')
    print $out             (0,139)
B.POSN_STATUS
    move 'POSN_STATUS' to $Fieldname
    move &B.Posn_Status to  $FieldValue
    do Read-Translate-Table
    print $XlatShortName   (0,148)
B.STATUS_DT
    if RTRIM(&B.Status_Dt, ' ') <> ''
       do Format-DateTime(&B.Status_Dt, $out, {DEFDATE}, '', '')
       print $out          (0,158)
    end-if
B.BUDGETED_POSN            (0,170)

FROM PS_POSITION_DATA B
WHERE &A.Position_Nbr = B.POSITION_NBR
  AND &A.EffDt <> B.EFFDT

ORDER BY B.POSITION_NBR,B.EFFDT DESC

end-SELECT
end-procedure

!***********************************************************************
#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines
#include 'askaod.sqc'    !Ask As Of Date input

#Include 'readxlat.sqc'  !Read the Translate Table
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
