!***********************************************************************
!  BEN708: Calculation Rules Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/01/26:18:39:58                           
!             $Release:  HR9                                           
!            $Revision:  101                                           
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'   !Set environment

Begin-Setup
  #Include 'ptpsp125.sqc'
End-Setup

#define col_sep                  2   !Column Separator
#define col_calc_rule            5
#define col_eff_date            10
#define col_biographics         11
#define col_ref_dates           26
#define col_ben_base            10
#define col_abbr                4
#define col_mj_options          32
#define col_ded_lim_of_gr       11

#define RefDateCol              33

!***************************************
begin-program
!***************************************
  do Init-Report
!--- 'CALCULATION RULES TABLE'
  do Get-Report-Language
  let $ReportTitle  = $BEN708-REPORTTITLE
  display ' '
  display $ReportTitle
  display ' '


  do Process-Main
  do Reset
  do StdAPI-Term
end-program

!***************************************
begin-procedure Init-Report
!***************************************
  do Init-DateTime
  do Get-Current-DateTime
  do Init-Number

  let $ReportID = 'BEN708'

  do StdAPI-Init
end-procedure

!**********************************
! This procedure is used to read language-dependent text from the database.
!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BEN708 program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN708', 'REPORT_TITLE', $BEN708-REPORTTITLE, #CW)
  do Get_Field_Information ('BEN708', 'CALC',         $BEN708-CALC,        #CW)
  do Get_Field_Information ('BEN708', 'SOURCE_FOR',   $BEN708-SRC-FOR,     #CW)
  do Get_Field_Information ('BEN708', 'BIOGRAPHICS',  $BEN708-BIOGRAPHICS, #CW)
  do Get_Field_Information ('BEN708', 'BENEFITS',     $BEN708-BENEFITS,    #CW)
  do Get_Field_Information ('BEN708', 'BASE',         $BEN708-BASE,        #CW)
  do Get_Field_Information ('BEN708', 'ABBR',         $BEN708-ABBR,        #CW)
  do Get_Field_Information ('BEN708', 'CODE',         $BEN708-CODE,        #CW)
  do Get_Field_Information ('BEN708', 'REFER_DATES',  $BEN708-REFER-DATES, #CW)
  do Get_Field_Information ('BEN708', 'CMP_AS_OF_DT', $BEN708-CMP-AS-OF-DT,#CW)
  do Get_Field_Information ('BEN708', 'AGEASOF',      $BEN708-AGEASOF,     #CW)
  do Get_Field_Information ('BEN708', 'BASEASOF',     $BEN708-BASEASOF,    #CW)
  do Get_Field_Information ('BEN708', 'MJ_OPTIONS1',  $BEN708-MJ-OPTIONS1, #CW)
  do Get_Field_Information ('BEN708', 'MJ_OPTIONS2',  $BEN708-MJ-OPTIONS2, #CW)
  do Get_Field_Information ('BEN708', 'ANY_STATUS',   $BEN708-ANY-STATUS,  #CW)
  do Get_Field_Information ('BEN708', 'ACTV_STATUS',  $BEN708-ACTV-STATUS, #CW)
  do Get_Field_Information ('BEN708', 'DEDN_LIMIT',   $BEN708-DEDN-LIMIT,  #CW)
  do Get_Field_Information ('BEN708', 'PR_OF_GROSS',  $BEN708-PR-OF-GROSS, #CW)

  do Get_Field_Information ('BEN1',   'RULE',         $BEN1-RULE,          #CW)
  do Get_Field_Information ('BEN1',   'EFFECTIVE',    $BEN1-EFFECTIVE,     #CW)
  do Get_Field_Information ('BEN1',   'DATE',         $BEN1-DATE,          #CW)
  do Get_Field_Information ('BEN1',   'SERVICE2',     $BEN1-SERVICE2,      #CW)

End-Procedure

!***************************************
begin-HEADING 7
!***************************************
  #Include 'stdhdgtr.sqc'

!Column Heading line 1
  print $BEN708-CALC                    (+2,1,{col_calc_rule})
  print $BEN1-EFFECTIVE                 (0,+{col_sep},{col_eff_date})
  print $BEN708-SRC-FOR                 (0,+{col_sep},{col_biographics})
  print $BEN708-REFER-DATES             (0,+{col_sep},{col_ref_dates})
  print $BEN708-BENEFITS                (0,+{col_sep},{col_ben_base})
  print $BEN708-ABBR                    (0,+{col_sep},{col_abbr})
  print $BEN708-MJ-OPTIONS1             (0,+{col_sep},{col_mj_options})
  print $BEN708-DEDN-LIMIT              (0,+{col_sep},{col_ded_lim_of_gr})

!Column Heading line 2
  print $BEN1-RULE                      (+1,1,{col_calc_rule})
  print $BEN1-DATE                      (0,+{col_sep},{col_eff_date})
  print $BEN708-BIOGRAPHICS             (0,+{col_sep},{col_biographics})
  print $BEN708-CMP-AS-OF-DT            (0,+{col_sep},{col_ref_dates})
  print $BEN708-BASE                    (0,+{col_sep},{col_ben_base})
  print $BEN708-CODE                    (0,+{col_sep},{col_abbr})
  print $BEN708-MJ-OPTIONS2             (0,+{col_sep},{col_mj_options})
  print $BEN708-PR-OF-GROSS             (0,+{col_sep},{col_ded_lim_of_gr})

!Column Heading line 3
  print '='                    (+1,1,{col_calc_rule})              FILL
  print '='                    (0,+{col_sep},{col_eff_date})       FILL
  print '='                    (0,+{col_sep},{col_biographics})    FILL
  print '='                    (0,+{col_sep},{col_ref_dates})      FILL
  print '='                    (0,+{col_sep},{col_ben_base})       FILL
  print '='                    (0,+{col_sep},{col_abbr})           FILL
  print '='                    (0,+{col_sep},{col_mj_options})     FILL
  print '='                    (0,+{col_sep},{col_ded_lim_of_gr})  FILL

  end-HEADING

!***************************************
BEGIN-PROCEDURE Process-Main
!***************************************

BEGIN-SELECT

CALC_RULES_ID                         (+2,1,5)

EFFDT
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out                          (0,+2,10)

AGE_SOURCE
  move 'AGE_SOURCE' to $FieldName
  move &AGE_SOURCE  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName                (0,+2,11)

AGE_AS_OF_CD
  move 'AGE_AS_OF_CD' to $FieldName
  move &AGE_AS_OF_CD  to $FieldValue
  do Read-Translate-Table
  print $BEN708-AGEASOF               (0,{RefDateCol},9)
  print $XlatShortName                (0,+1,10)

AGE_AS_OF_MM
AGE_AS_OF_DD
  if (&AGE_AS_OF_MM > 0)
    print &AGE_AS_OF_MM               (0,+1,2)  EDIT '09'
    print '-'                         (0,0,1)
    print &AGE_AS_OF_DD               (0,0,2)   EDIT '09'
  else
     print ' '                        (0,+1,5)
  end-if

BENEFITS_BASE
  move 'BENEFITS_BASE' to $FieldName
  move &BENEFITS_BASE  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName                (0,+2,10)

BN_ANNL_BENBASE_CD
  if (&BENEFITS_BASE = '2')
     print &BN_ANNL_BENBASE_CD        (0,+2,4)
  else
     print ' '                        (0,+2,4)
  end-if

COMBINE_MULT_SAL
  move 'PSYESNO' to $FieldName
  move &COMBINE_MULT_SAL  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName                (0,+2,7)

GRP_MTHD
  if (&COMBINE_MULT_SAL = 'Y')
     move 'GRP_MTHD' to $FieldName
     move &GRP_MTHD  to $FieldValue
     do Read-Translate-Table
     print $XlatShortName             (0,+2,10)
  else
     print ' '                        (0,+2,10)
  end-if

EVAL_ACTV
  if (&COMBINE_MULT_SAL = 'Y')
     if (&EVAL_ACTV = 'Y')
        print $BEN708-ACTV-STATUS     (0,+2,11)
     else
        print $BEN708-ANY-STATUS      (0,+2,11)
     end-if
  else
     print ' '                        (0,+2,11)
  end-if

MAX_PAY_DED_LIM
  do Format-Number(&max_pay_ded_lim, $out, '999999.999')
  print $out                          (0,+2,10)

SERVICE_AS_OF_CD
  move 'SERVICE_AS_OF_CD' to $FieldName
  move &SERVICE_AS_OF_CD  to $FieldValue
  do Read-Translate-Table
  print $BEN1-SERVICE2                (+1,{RefDateCol},9)
  print $XlatShortName                (0,+1,10)

SERVICE_AS_OF_MM
SERVICE_AS_OF_DD
  if (&SERVICE_AS_OF_MM > 0)
    print &SERVICE_AS_OF_MM           (0,+1,2)  EDIT '09'
    print '-'                         (0,0,1)
    print &SERVICE_AS_OF_DD           (0,0,2)   EDIT '09'
  end-if

BENBASE_AS_OF_CD
  move 'BENBASE_AS_OF_CD' to $FieldName
  move &BENBASE_AS_OF_CD  to $FieldValue
  do Read-Translate-Table
  print $BEN708-BASEASOF              (+1,{RefDateCol},9)
  print $XlatShortName                (0,+1,10)

BENBASE_AS_OF_MM
BENBASE_AS_OF_DD
  if (&BENBASE_AS_OF_MM > 0)
    print &BENBASE_AS_OF_MM           (0,+1,2)  EDIT '09'
    print '-'                         (0,0,1)
    print &BENBASE_AS_OF_DD           (0,0,2)   EDIT '09'
  end-if

FROM PS_CALC_RULES_TBL
ORDER BY CALC_RULES_ID, EFFDT DESC
END-SELECT

END-PROCEDURE

!***************************************

#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!************************ End Of Report ********************************

