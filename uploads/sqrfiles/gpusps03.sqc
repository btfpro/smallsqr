!***********************************************************************
!  GPUSPS03: Global Payroll Payslips - Static USA ERN/HRS/Tax          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/08/22:18:08:09                                 *
!       $Release:  HR9                                                 *
!      $Revision:  102                                                 *
!                                                                      *
!***********************************************************************


#Define ET_DetailArray_Size 99
#Define Tax_DetailArray_Size 99
#Define EarnHoursTax_DemoDataArray_Size 10
#DEFINE Hours_Earnings_Taxes_Static_TITLE_SIZE 3

!***********************************************************************
begin-procedure Init-Arrays_Earn_Static
!***********************************************************************

Create-Array Name=ET_DetailArray Size = {ET_DetailArray_Size}
    Field=Name:char
    Field=CompRateCode:Number
    Field=CurrentHrs:Number
    Field=CurrentEarn:Number
    Field=YTDHrs:Number
    Field=YTDEarn:Number
    Field=Print_Blank:char


Create-Array Name=ET_SummaryArray Size = 1
    Field=Name:char
    Field=CompRateCode:Number
    Field=CurrentHrs:Number
    Field=CurrentEarn:Number
    Field=YTDHrs:Number
    Field=YTDEarn:Number

Create-Array Name=ET_TotalArray Size = 1
    Field=Name:char
    Field=CompRateCode:Number
    Field=CurrentHrs:Number
    Field=CurrentEarn:Number
    Field=YTDHrs:Number
    Field=YTDEarn:Number


Create-Array Name=Tax_DetailArray Size = {Tax_DetailArray_Size}
    Field=Name:char
    Field=CurrentTax:Number
    Field=YTDTax:Number

Create-Array Name=Tax_SummaryArray Size = 1
    Field=Name:char
    Field=CurrentTax:Number
    Field=YTDTax:Number


Create-Array Name=Tax_TotalArray Size = 1
    Field=Name:char
    Field=CurrentTax:Number
    Field=YTDTax:Number


let #i = 0

while (#i < {ET_DetailArray_Size} )


    Put ' ' into ET_DetailArray(#i) name
    Put 0 into ET_DetailArray(#i) CompRateCode
    Put 0 into ET_DetailArray(#i) CurrentHrs
    Put 0 into ET_DetailArray(#i) CurrentEarn
    Put 0 into ET_DetailArray(#i) YTDHrs
    Put 0 into ET_DetailArray(#i) YTDEarn
    Put ' ' into ET_DetailArray(#i) Print_Blank
    add 1 to #i

end-while

    Put ' ' into ET_SummaryArray(0) name
    Put 0 into ET_SummaryArray(0) CompRateCode
    Put 0 into ET_SummaryArray(0) CurrentHrs
    Put 0 into ET_SummaryArray(0) CurrentEarn
    Put 0 into ET_SummaryArray(0) YTDHrs
    Put 0 into ET_SummaryArray(0) YTDEarn

    Put ' ' into ET_TotalArray(0) name
    Put 0 into ET_TotalArray(0) CompRateCode
    Put 0 into ET_TotalArray(0) CurrentHrs
    Put 0 into ET_TotalArray(0) CurrentEarn
    Put 0 into ET_TotalArray(0) YTDHrs
    Put 0 into ET_TotalArray(0) YTDEarn

let #i = 0

while (#i < {Tax_DetailArray_Size} )
    
    Put ' ' into Tax_DetailArray(#i) name
    Put 0 into Tax_DetailArray(#i) CurrentTax
    Put 0 into Tax_DetailArray(#i) YTDTax
    Add 1 to #i

end-while

    Put ' ' into Tax_SummaryArray(0) name
    Put 0 into Tax_SummaryArray(0) CurrentTax
    Put 0 into Tax_SummaryArray(0) YTDTax

    Put ' ' into Tax_TotalArray(0) name
    Put 0 into Tax_TotalArray(0) CurrentTax
    Put 0 into Tax_TotalArray(0) YTDTax

end-procedure

!***********************************************************************


!***********************************************************************
begin-procedure Init-Variables_Earn_Static
!***********************************************************************

let $space_left   = 'y'
let #curr_detail_line = 0
let #curr_tax_detail_line = 0

let #ytd_detail_line = 0
let #ytd_tax_detail_line = 0
let $section_printed = 'n'
let #detail_line = 0
let #gbl_total_earn_hrs_cur = 0
let #gbl_total_earn_cur = 0
let #gbl_total_earn_hrs_ytd = 0
let #gbl_total_earn_ytd = 0
let #gbl_total_tax_cur = 0
let #gbl_total_tax_ytd = 0
let $print_summary_cur_line = 'n'
let $print_summary_ytd_line = 'n'
let $print_summary_cur_tax_line = 'n'
let $print_summary_ytd_tax_line = 'n'
if upper($gbl_new_lang_flag) = 'Y'
             
    do Get_Field_Information ($ReportID, 'HRS_EARN_LBL'       , $HRS_EARN_LBL,         #DW)
    do Get_Field_Information ($ReportID, 'DESCR_LBL'   , $DESCR_LBL,       #DW)
    do Get_Field_Information ($ReportID, 'CUR_LBL'       , $CUR_LBL,        #DW)
    do Get_Field_Information ($ReportID, 'YTD_LBL'    , $YTD_LBL,        #DW)
    do Get_Field_Information ($ReportID, 'RATE_LBL'     , $RATE_LBL,           #DW)
    do Get_Field_Information ($ReportID, 'HOURS_LBL'           , $HOURS_LBL,           #DW)
    do Get_Field_Information ($ReportID, 'EARN_LBL'     , $EARN_LBL,       #DW)
    do Get_Field_Information ($ReportID, 'TAX_LBL'     , $TAX_LBL,           #DW)
    do Get_Field_Information ($ReportID, 'OTHER_LBL'     , $OTHER_LBL,       #DW)
    do Get_Field_Information ($ReportID, 'TOTAL_LBL'     , $TOTAL_LBL,           #DW)
    do Get_Field_Information ($ReportID, 'WTHLD_LBL'     , $WTHLD_LBL,           #DW)
  end-if

end-procedure

!***********************************************************************



    
!**********************************************************************
!**********************************************************************
!      Hours_Earnings_Taxes_Static
!**********************************************************************
!**********************************************************************

begin-procedure Hours_Earnings_Taxes_Static

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section GPUSPS03.SQC ')
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure Hours_Earnings_Taxes_Static')

    do Init-Variables_Earn_Static
    do Init-Arrays_Earn_Static
    do Get-Current-DateTime
    while ($section_printed = 'n')
        do get_start_page_line_number(#current-line, #page-count, #start-section-line-number, #start-section-page-number)
        do evaluate_space(#start-section-line-number,$space_left)
        #debug1 show '** USA STATIC HRS EARN TAX section:        #start-section-line-number = ' #start-section-line-number ' $space_left = ' $space_left
        if $space_left = 'n'
            if $gbl_Paycheck_To_Do = 'y'
                Do print_check_usa
                #debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Back into Procedure Hours_Earnings_Taxes_Static')
                move 'n' to $check_collision 
                move 'n' to $gbl_Paycheck_To_Do
            else
                new-page
                Move 1 to #gbl_curr_Section_Start_Line
                Move 1 to #start-section-line-number
                do Hours_Earnings_Taxes
                move 'y' to $section_printed
                break
            end-if
        end-if
        if $space_left = 'y'
            if $gbl_Paycheck_To_Do = 'y'
                do evaluate_bankdraft_collision(#start-section-line-number,$check_collision)
                if $check_collision = 'y'
                
                    Do print_check_usa
                    move 'n' to $check_collision 
                    move 'n' to $gbl_Paycheck_To_Do
                else
                    do Hours_Earnings_Taxes
                    move 'y' to $section_printed
                    break
                end-if
            else
                do Hours_Earnings_Taxes
                move 'y' to $section_printed
                break
            end-if
        end-if
    end-while
   
end-procedure



!**********************************************************************
!**********************************************************************
!      Hours_Earnings_Taxes
!**********************************************************************
!**********************************************************************
begin-procedure Hours_Earnings_Taxes
    
    
    do get_earnings_elements
    Do get_Earn_Accum
    do Get_Taxes
    do Get_Taxes_Accum
    do total_line
    do total_tax_line
    do Print_Hours_Earnings_Taxes_Title
    do Print_Hours_Earnings_Tax_Detail_Lines
    do print_summary_line
    do Print_Total_Line
    !do Get_Payment_Details
    !do evaluate_space($gbl_curr_Section_Start_Line, $space_left)
    

end-procedure Hours_Earnings_Taxes_Static

!**********************************************************************
!**********************************************************************
!      get_earnings_elements
!**********************************************************************
!**********************************************************************
begin-procedure get_earnings_elements
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure get_earnings_elements')
!select from the extract table

BEGIN-SELECT ON-ERROR=SQL-ERROR

A.RATE_RSLT_VAL
SUM(A.UNIT_RSLT_VAL)    &A.UNIT_RSLT_VAL
SUM(A.CALC_RSLT_VAL)    &A.CALC_RSLT_VAL
A.PIN_NUM
    #debug1 show '** USA STATIC HRS EARN TAX section:        UNIT_RSLT_VAL = ' &A.UNIT_RSLT_VAL
    Do get_pin_name(&A.PIN_NUM, $pin_nm)
    #debug1 show '** USA STATIC HRS EARN TAX section:        $pin_nm = ' $pin_nm
A.GPUS_PSLP_PRT_HRS     &A.GPUS_PSLP_PRT_HRS_EARN_CUR_STAT
    let $print_cur_earn_static_hrs = &A.GPUS_PSLP_PRT_HRS_EARN_CUR_STAT
A.GPUS_PSLP_PRT_RATE    &A.GPUS_PSLP_PRT_RATE_EARN_CUR_STAT 
    let $print_cur_earn_static_rate = &A.GPUS_PSLP_PRT_RATE_EARN_CUR_STAT
    Do curr_detail_line
    #debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Back into rocedure get_earnings_elements')
A.GP_PSLP_ORDER              &A.STAT_EARN_GP_PSLP_ORDER
FROM PS_GPUS_PSLP_RSLT A
WHERE A.JOBINSTANCE = #gbl_job_instance AND
A.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
A.EMPLID = $gbl_tgt_Emplid AND
A.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
A.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
A.GP_ELN_SET        = $gbl_GP_ELN_SET AND
A.GPUS_PSLP_ELN_CAT = '10' AND
A.GPUS_PSLP_CURR_YTD = '10' AND A.SEQ_NUM = 20 GROUP BY A.PIN_NUM, A.RATE_RSLT_VAL,
A.GP_PSLP_ORDER,A.GPUS_PSLP_PRT_HRS, A.GPUS_PSLP_PRT_RATE ORDER BY A.GP_PSLP_ORDER

END-SELECT

end-procedure


!**********************************************************************
!**********************************************************************
!      get_Earn_Accum
!**********************************************************************
!**********************************************************************
begin-procedure get_Earn_Accum
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure get_Earn_Accum')
!select from the extract table

BEGIN-SELECT ON-ERROR=SQL-ERROR
B.RATE_RSLT_VAL
B.UNIT_RSLT_VAL    &B.UNIT_RSLT_VAL
B.CALC_RSLT_VAL    &B.CALC_RSLT_VAL
B.PIN_NUM
B.GPUS_PSLP_PRT_HRS     &B.GPUS_PSLP_PRT_HRS_EARN_YTD_STAT
    let $print_ytd_earn_static_hrs = &B.GPUS_PSLP_PRT_HRS_EARN_YTD_STAT
B.GPUS_PSLP_PRT_RATE    &B.GPUS_PSLP_PRT_RATE_EARN_YTD_STAT
    let $print_ytd_earn_static_rate = &B.GPUS_PSLP_PRT_RATE_EARN_YTD_STAT 
    Do get_pin_name(&B.PIN_NUM, $pin_nm)  
    Do ytd_detail_line
B.GP_PSLP_ORDER              &B.STAT_EARN_GP_PSLP_ORDER
FROM PS_GPUS_PSLP_RSLT B
WHERE B.JOBINSTANCE = #gbl_job_instance AND
B.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
B.EMPLID = $gbl_tgt_Emplid AND
B.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
B.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
B.GP_ELN_SET        = $gbl_GP_ELN_SET AND
B.GPUS_PSLP_ELN_CAT = '10' AND
B.GPUS_PSLP_CURR_YTD = '20' AND B.SEQ_NUM = 10 AND
B.SLICE_END_DT = (SELECT MAX(X.SLICE_END_DT)  
FROM PS_GPUS_PSLP_RSLT X
WHERE X.JOBINSTANCE = #gbl_job_instance AND
X.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
X.EMPLID = $gbl_tgt_Emplid AND
X.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
X.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
X.GP_ELN_SET        = $gbl_GP_ELN_SET AND
X.GPUS_PSLP_ELN_CAT = '10' AND
X.GPUS_PSLP_CURR_YTD = '20' AND X.SEQ_NUM = 10) 
ORDER BY B.GP_PSLP_ORDER
END-SELECT

!GROUP BY B.PIN_NUM, B.RATE_RSLT_VAL,
!B.UNIT_RSLT_VAL, B.CALC_RSLT_VAL, B.GP_PSLP_ORDER, B.GPUS_PSLP_PRT_HRS, B.GPUS_PSLP_PRT_RATE 

end-procedure


!**********************************************************************
!**********************************************************************
!      curr_detail_line
!**********************************************************************
!**********************************************************************


begin-procedure curr_detail_line
#debug1 do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure curr_detail_line')
#debug1 show '** USA STATIC HRS EARN TAX section:        #detail_line = ' #curr_detail_line

if (#curr_detail_line < (#gbl_curr_Section_Dtl_Lines - 2) )

    
    Move &A.RATE_RSLT_VAL TO #CompRateCode
    Move &A.UNIT_RSLT_VAL to #CurrentHrs
    Move &A.CALC_RSLT_VAL to #CurrentEarn
    Put $pin_nm into ET_DetailArray(#curr_detail_line) name
    if upper($print_cur_earn_static_rate) = 'Y'
      Put #CompRateCode into ET_DetailArray(#curr_detail_line) CompRateCode
    end-if
    if upper($print_cur_earn_static_hrs) = 'Y'
      Put #CurrentHrs into ET_DetailArray(#curr_detail_line) CurrentHrs
    end-if
    Put #CurrentEarn into ET_DetailArray(#curr_detail_line) CurrentEarn
    Add &A.UNIT_RSLT_VAL to #gbl_total_earn_hrs_cur
    Add &A.CALC_RSLT_VAL to #gbl_total_earn_cur
    Add 1 to #curr_detail_line

else
  
   if (#curr_detail_line < (#gbl_curr_Section_Dtl_Lines - 1) )
      let $print_summary_cur_line = 'y'
      do summary_curr_line
   else
      if (#gbl_curr_Section_Dtl_Lines = 1) 
         let $print_summary_cur_line = 'n'
         do summary_curr_line
      end-if
   end-if


end-if


end-procedure


!**********************************************************************
!**********************************************************************
!      ytd_detail_line
!**********************************************************************
!**********************************************************************


begin-procedure ytd_detail_line

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure ytd_detail_line')
    let $found = 'n'
    let $update_array = 'n'
    Move &B.RATE_RSLT_VAL TO #CompRateCode
    Move &B.UNIT_RSLT_VAL to #YtdHrs
    Move &B.CALC_RSLT_VAL to #YtdEarn
    do search_DetailArray($pin_nm, $found, #ind, $update_array)
    if $found = 'y' 
     if $update_array = 'y'
      if upper($print_ytd_earn_static_hrs) = 'Y'
        Array-Add #YtdHrs to ET_DetailArray(#ind) YtdHrs
      end-if
        Array-Add #YtdEarn to ET_DetailArray(#ind) YtdEarn
        Add #YtdHrs to #gbl_total_earn_hrs_ytd
       Add #YtdEarn to #gbl_total_earn_ytd
     end-if
    else

     if (#curr_detail_line < (#gbl_curr_Section_Dtl_Lines - 2) )

        Put $pin_nm into ET_DetailArray(#curr_detail_line) name
      if upper($print_ytd_earn_static_rate) = 'Y'
        Put #CompRateCode into ET_DetailArray(#curr_detail_line) CompRateCode
      end-if
      if upper($print_ytd_earn_static_hrs) = 'Y'
        Put #YtdHrs into ET_DetailArray(#curr_detail_line) YtdHrs
      end-if
        Put #YtdEarn into ET_DetailArray(#curr_detail_line) YtdEarn
       Add 1 to #curr_detail_line
       Add #YtdHrs to #gbl_total_earn_hrs_ytd
       Add #YtdEarn to #gbl_total_earn_ytd
     else
       if (#curr_detail_line < (#gbl_curr_Section_Dtl_Lines - 1) )
         let $print_summary_ytd_line = 'y'
         do summary_ytd_line
       else
          if (#gbl_curr_Section_Dtl_Lines = 1)
            let $print_summary_ytd_line = 'n'
             do summary_ytd_line
          end-if
       end-if
     end-if

    end-if

   

end-procedure

!**********************************************************************
!**********************************************************************
!      search_DetailArray
!**********************************************************************
!**********************************************************************
begin-procedure search_DetailArray($search_pin_nm, :$found, :#found_ind, :$update_earn_array)

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure search_DetailArray')

let #i = 0
let #found_ind = 0
let #num_pin_found = 0
while (#i < {ET_DetailArray_Size})
    get $Pin_nm from ET_DetailArray(#i) name 
    
    if $search_pin_nm = $Pin_nm
        let $found = 'y'
        let #found_ind = #i
        Add 1 to #num_pin_found  
    end-if

    Add 1 to #i

end-while
If #num_pin_found = 1
     move 'y' to $update_earn_array
else
     move 'n' to $update_earn_array
     do Append_detail_array(#num_pin_found, $search_pin_nm)
end-if
end-procedure

!**********************************************************************
!**********************************************************************
!      Append_detail_array
!**********************************************************************
!**********************************************************************

begin-procedure Append_detail_array(#num_pin_found, $search_pin_nm)
let #i = 0
let #j = 0
while (#i < {ET_DetailArray_Size})
    get $Pin_nm from ET_DetailArray(#i) name 
    if $search_pin_nm = $Pin_nm
      Add 1 to #j
      if (#j = #num_pin_found)
        if upper($_print_ytd_earn_static_hrs) = 'Y'
          Array-Add #_YtdHrs to ET_DetailArray(#i) YtdHrs
        end-if
        Array-Add #_YtdEarn to ET_DetailArray(#i) YtdEarn
        Add #_YtdHrs to #_gbl_total_earn_hrs_ytd
        Add #_YtdEarn to #_gbl_total_earn_ytd
      else     
        Put 'y' into ET_DetailArray(#i) Print_Blank 
      end-if 
     end-if

    Add 1 to #i

end-while

end-procedure




!**********************************************************************
!**********************************************************************
!      summary_curr_line
!**********************************************************************
!**********************************************************************

begin-procedure summary_curr_line

Put $OTHER_LBL into ET_summaryArray(0) name
Put 0 into ET_summaryArray(0) CompRateCode

Move &A.UNIT_RSLT_VAL to #CurrentHrs
Move &A.CALC_RSLT_VAL to #CurrentEarn

Array-Add #CurrentHrs to ET_summaryArray(0) CurrentHrs
Array-Add #CurrentEarn to ET_summaryArray(0) CurrentEarn

!Update Total variables
Add #CurrentHrs to #gbl_total_earn_hrs_cur
Add #CurrentEarn to #gbl_total_earn_cur

end-procedure



!**********************************************************************
!**********************************************************************
!      summary_ytd_line
!**********************************************************************
!**********************************************************************


begin-procedure summary_ytd_line


Put $OTHER_LBL into ET_summaryArray(0) name
Put 0 into ET_summaryArray(0) CompRateCode


Move &B.UNIT_RSLT_VAL to #YTDHrs
Move &B.CALC_RSLT_VAL to #YTDEarn


Array-Add #YTDHrs to ET_summaryArray(0) YTDHrs
Array-Add #YTDEarn to ET_summaryArray(0) YTDEarn

!Update Total variables
Add #YTDHrs to #gbl_total_earn_hrs_ytd
Add #YTDEarn to #gbl_total_earn_ytd

end-procedure

!**********************************************************************
!**********************************************************************
!      total_line
!**********************************************************************
!**********************************************************************


begin-procedure total_line

Put $TOTAL_LBL into ET_totalArray(0) name
Put 0 into ET_totalArray(0) CompRateCode
Put #gbl_total_earn_hrs_cur into ET_totalArray(0) CurrentHrs
Put #gbl_total_earn_cur into ET_totalArray(0) CurrentEarn
Put #gbl_total_earn_hrs_ytd into ET_totalArray(0) YTDHrs
Put #gbl_total_earn_ytd into ET_totalArray(0) YTDEarn

end-procedure

!**********************************************************************
!**********************************************************************
!      Read-DetailArray
!**********************************************************************
!**********************************************************************


begin-procedure Read-DetailArray

let #i = 0

while (#i < (#gbl_curr_Section_Dtl_Lines - 2) )
    
    get $Pin_nm #CompRateCode #CurrentHrs #CurrentEarn #YTDHrs #YTDEarn $print_blank from ET_DetailArray(#i) 
          name CompRateCode CurrentHrs CurrentEarn YTDHrs YTDEarn Print_Blank
    
    if $Pin_nm = ' '
        break
    end-if
    Add 1 to #i
end-while

end-procedure


!**********************************************************************
!**********************************************************************
!      Read-summaryArray
!**********************************************************************
!**********************************************************************


begin-procedure Read-SummaryArray

    get $Pin_nm #CompRateCode #CurrentHrs #CurrentEarn #YTDHrs #YTDEarn from ET_SummaryArray(0) 
       name CompRateCode CurrentHrs CurrentEarn YTDHrs YTDEarn
   
end-procedure

!**********************************************************************
!**********************************************************************
!      Read-totalArray
!**********************************************************************
!**********************************************************************


begin-procedure Read-TotalArray

    get $Pin_nm #CompRateCode #CurrentHrs #CurrentEarn #YTDHrs #YTDEarn from ET_TotalArray(0) 
      name CompRateCode CurrentHrs CurrentEarn YTDHrs YTDEarn
    
end-procedure


!**********************************************************************
!**********************************************************************
!      Print_Hours_Earnings_Taxes_Title
!**********************************************************************
!**********************************************************************
begin-procedure Print_Hours_Earnings_Taxes_Title

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure Print_Hours_Earnings_Taxes_Title')

let $title_printed = 'n'
let #title_line_count = {Hours_Earnings_Taxes_Static_TITLE_SIZE}
let #title_dtl_diff = #gbl_curr_Section_Nbr_Lines - #gbl_curr_Section_Dtl_Lines
#debug1 show '** USA STATIC HRS EARN TAX section:        #title_dtl_diff = ' #title_dtl_diff ' #title_line_count = ' #title_line_count
let #row1 = #start-section-line-number

if #title_dtl_diff > 0
  let #title_line_printed = 0
  if (#gbl_curr_Section_Dtl_Lines > 1)
      let #totalboxpos = #gbl_curr_Section_Dtl_Lines - 1
  else
      let #totalboxpos = 1
  end-if
  #debug1 show '** USA STATIC HRS EARN TAX section:        #totalboxpos = ' #totalboxpos ' #gbl_curr_Section_Dtl_Lines = '#gbl_curr_Section_Dtl_Lines

  columns 2 86 
  graphic (#row1,2,84) box  1 10 12
  graphic (#row1,,84)  box  1 10
     Add 2 to #title_line_printed
     if #title_line_printed < #title_dtl_diff
        graphic (+1,2,84)     box  2 10
     else
        Move 0 to #title_line_printed
        Add 1 to #title_line_printed
        if #title_line_printed < #title_dtl_diff
         graphic (+1,2,84)     box  1 10
        end-if
     end-if
       if #title_line_printed = 0
        let #title_line_printed = 1
       end-if
       
         graphic (+#title_line_printed,2,84)     box #totalboxpos 10
       
       if (#gbl_curr_Section_Dtl_Lines > 1)
         graphic (+#totalboxpos,2,84)    box  1 10
       end-if
  
    let #Row_for_total_line = #current-line
  next-column
  let #title_line_printed = 0
  graphic (#row1,2,42) box  1 10 12
  graphic (#row1,,42)  box  1 10
     Add 2 to #title_line_printed
     if #title_line_printed < #title_dtl_diff
        graphic (+1,,42)     box  2 10
     else
        Move 0 to #title_line_printed
        Add 1 to #title_line_printed
        if #title_line_printed < #title_dtl_diff
          graphic (+1,,42)     box  1 10
        end-if
     end-if
     if #title_line_printed = 0
        let #title_line_printed = 1
       end-if
     
      graphic (+#title_line_printed,,42)     box #totalboxpos 10
     
     if (#gbl_curr_Section_Dtl_Lines > 1)
      graphic (+#totalboxpos,,42)    box  1 10
     end-if

  Move 0 to #title_line_printed
  use-column 0
  print $HRS_EARN_LBL    (#row1,35) bold
  print $TAX_LBL                 (,105)      bold
      Add 1 to #title_line_printed
      if #title_line_printed < #title_dtl_diff

        print '-'                     (+1,29,12) fill
        print $CUR_LBL               (,41)
        print '-'                     (,48,12)   fill
        print '-'                     (,63,10)   fill
        print $YTD_LBL                   (,73)
        print '-'                     (,76,10)   fill
       Add 1 to #title_line_printed
      end-if
      
      if #title_line_printed < #title_dtl_diff
        print $DESCR_LBL           (+1,4)
        print $RATE_LBL                  (,29)
        print $HOURS_LBL                 (,41)
        print $EARN_LBL              (,52)
        print $HOURS_LBL                 (,63)
        print $EARN_LBL             (,78)
        print $DESCR_LBL           (,88)
        print $CUR_LBL              (,109)
        print $YTD_LBL                (,125)
        Add 1 to #title_line_printed
End-if
!Add 1 to #title_line_printed !1 FOR THE FIRST LINE
let #row1 = #row1 + #title_line_printed
  !    add 1 to #row1
    !   next-column

end-if
end-procedure
 

!**********************************************************************
!**********************************************************************
!      Print_Hours_Earnings_Tax_Detail_Lines
!**********************************************************************
!**********************************************************************
begin-procedure Print_Hours_Earnings_Tax_Detail_Lines

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure Print_Hours_Earnings_Tax_Detail_Lines')
let #i = 0
let #start_tax_row = #row1 
#debug1 show '** USA STATIC HRS EARN TAX section:        #start_tax_row = ' #start_tax_row
!columns 4 27 34 46 51 65 69  88 104 116 
!columns 4 20 25 39 48 59 72
while (#i < (#gbl_curr_Section_Dtl_Lines - 2) )
columns 4 20 25 39 48 59 72   
    if (#i >= #curr_detail_line)
      #debug2 show '** USA STATIC HRS EARN TAX section:        Printing blank lines' 
        add 1 to #row1
        add 1 to #i
    else 
      get $Pin_nm #CompRateCode #CurrentHrs #CurrentEarn #YTDHrs #YTDEarn $print_blank from ET_DetailArray(#i)
       name CompRateCode CurrentHrs CurrentEarn YTDHrs YTDEarn Print_Blank
      
      
      #debug1 show '** USA STATIC HRS EARN TAX section:        $TaxName = '$TaxName ' #CurrentTax = '#CurrentTax ' #YTDTax = '#YTDTax
      !if ($Pin_nm = ' ' and $TaxName = ' ')
    if (#CurrentEarn <> 0 or #YTDEarn <> 0)  
        print $Pin_nm             (#row1,1,20)
        next-column             !++
 
         next-column
     if #i < #curr_detail_line 
        if #CompRateCode <> 0
              print #CompRateCode          () edit b999.9999mi ! change to 12,6 if needed
        end-if
     end-if
     
        next-column
     if #i < #curr_detail_line 
        print #CurrentHrs            () edit b999.99mi
     end-if
    
        next-column

     if #i < #curr_detail_line 
        print #CurrentEarn          () edit 9,999,990.99mi
     end-if  
    
        next-column

     if #i < #curr_detail_line 
       if $print_blank <> 'y' 
        print #YTDHrs            () edit b9,999.99mi
       end-if
     end-if
    
        next-column

     if #i < #curr_detail_line 
       if $print_blank <> 'y'
          print #YTDEarn             () edit 999,999,990.99mi
       end-if
     end-if  
        
      add 1 to #row1
                    
      end-if
      add 1 to #i
    END-IF   
 !   if (#CurrentEarn = 0 and #YTDEarn = 0)
  !      let #print_blank_rows = #print_blank_rows + 1
  !  end-if

    
end-while
let #end_earn_row = #row1

LET #i = 0
LET #row1 = #start_tax_row
#debug1 show '** USA STATIC HRS EARN TAX section:       #start_tax_row = ' #start_tax_row
while (#i < (#gbl_curr_Section_Dtl_Lines - 2) )
columns 88 104 115   
    if (#i>= #curr_tax_detail_line)
       #debug2 show '** USA STATIC HRS EARN TAX section:       Printing blank lines' 
        add 1 to #row1
        add 1 to #i
    else 
            
      get $TaxName #CurrentTax #YTDTax from Tax_DetailArray(#i) name CurrentTax YTDTax
      #debug1 show '** USA STATIC HRS EARN TAX section:       $TaxName = '$TaxName ' #CurrentTax = '#CurrentTax ' #YTDTax = '#YTDTax
      !if ($Pin_nm = ' ' and $TaxName = ' ')
    if (#CurrentTax <> 0 or #YTDTax <> 0)
    
    
     if #i < #curr_tax_detail_line
        print $TaxName              (#row1, ,16)
     end-if
    
        next-column
    
     if #i < #curr_tax_detail_line 
           print #CurrentTax           () edit 9,999,990.99mi
     end-if
    
            next-column
    
     if #i < #curr_tax_detail_line
            print #YTDTax            () edit 99,999,990.99mi
     end-if
    
    
            
        add 1 to #row1
        
            
            
      end-if
      
 !   if (#CurrentEarn = 0 and #YTDEarn = 0)
  !      let #print_blank_rows = #print_blank_rows + 1
  !  end-if
add 1 to #i
END-IF
    
end-while
let #end_tax_row = #row1

if #end_tax_row > #end_earn_row
     LET #row1 = #end_tax_row
else
     LET #row1 = #end_earn_row

END-IF


end-procedure

!**********************************************************************
!**********************************************************************
!      Print_Summary_Line
!**********************************************************************
!**********************************************************************
begin-procedure Print_Summary_Line

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure Print_Summary_Line') 
  
!columns 2 25 32 44 49 63 67  86 102 114 
columns 4 20 25 39 48 59 72 88 104 115
if ($print_summary_cur_line = 'y' or $print_summary_ytd_line = 'y' or $print_summary_cur_tax_line = 'y' 
                             or $print_summary_ytd_tax_line = 'y') 

do Read-summaryArray
do Read_Tax_SummaryArray

if ($Pin_nm = ' ' and $TaxName = ' ')
    print ' '             (#row1,1)
    add 1 to #row1
    #debug1 show '** USA STATIC HRS EARN TAX section:        No Summary Line'
else
    
    print $Pin_nm             (#row1,1)
    next-column             !++
 
    next-column
    if #CompRateCode <> 0
      print #CompRateCode           () edit b999.999999mi ! change to 12,6 if needed
    end-if
    next-column
   if $print_summary_cur_line = 'y'
    print #CurrentHrs            () edit b999.99mi
   end-if
    next-column
   if ($print_summary_cur_line = 'y' or $print_summary_ytd_line = 'y')
      print #CurrentEarn           () edit 9,999,990.99mi
   end-if 
    next-column
   if $print_summary_ytd_line = 'y'
    print #YTDHrs             () edit b9,999.99mi
   end-if
    next-column
   if ($print_summary_cur_line = 'y' or $print_summary_ytd_line = 'y')
      print #YTDEarn             () edit 999,999,990.99mi
   end-if

    next-column
    print $TaxName              ()
    next-column
   if ($print_summary_cur_tax_line = 'y' or $print_summary_ytd_tax_line = 'y')
      print #CurrentTax            () edit 9,999,990.99mi
   end-if
          next-column
   if ($print_summary_cur_tax_line = 'y' or $print_summary_ytd_tax_line = 'y')
         print #YTDTax            () edit 99,999,990.99mi
   end-if 
add 1 to #row1
    next-column
end-if
else
  if (#gbl_curr_Section_Dtl_Lines > 1)
    print ' '             (#row1,1)
    add 1 to #row1
    #debug1 show '** USA STATIC HRS EARN TAX section:        No Summary Line'
  end-if
end-if 
end-procedure

!**********************************************************************
!**********************************************************************
!      Print_Total_Line
!**********************************************************************
!**********************************************************************
begin-procedure Print_Total_Line

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure Print_Total_Line')

#debug1 show '** USA STATIC HRS EARN TAX section:        #gbl_curr_Section_Dtl_Lines = ' #gbl_curr_Section_Dtl_Lines
columns 4 20 25 39 48 59 72 88 104 115
do Read-TotalArray
do Read_Tax_TotalArray
 !let #total_row = #row1 - #gbl_curr_Section_Dtl_Lines
!#Debugx Show 'row1  #total_row' #row1 #total_row
!Add 1 to #row1
!let #row1 = #totalboxpos
!#Debugx Show ' #totalboxpos'  #totalboxpos
let #row1 = #Row_for_total_line
    print $Pin_nm             (#row1,1)
    next-column             !++
    next-column
    if #CompRateCode <> 0
      print #CompRateCode           () edit b999.999999mi ! change to 12,6 if needed
    end-if
    next-column
    print #CurrentHrs            () edit b999.99mi
    next-column
   
      print #CurrentEarn           () edit 9,999,990.99mi
    
    next-column
    print #YTDHrs             () edit b9,999.99mi
    next-column
   
      print #YTDEarn             () edit 999,999,990.99mi
    

    next-column
    print $TaxName              ()
   next-column
   
          print #CurrentTax            () edit 9,999,990.99mi
          next-column
         print #YTDTax            () edit 99,999,990.99mi
    

    !print ' '    (+3, 1)

    next-column
   
end-procedure



!**********************************************************************
!**********************************************************************
!      Get_Taxes
!**********************************************************************
!**********************************************************************
begin-procedure Get_Taxes
!select from the extract table
!For Multiple Resolutions
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure Get_Taxes')
do mrs_select
end-procedure
!**********************************************************************
!**********************************************************************
!      get_appropriate_select
!**********************************************************************
!**********************************************************************
begin-procedure get_appropriate_select($RES_DTL)
if $RES_DTL = 'N'
   do no_mrs_select
else
   do mrs_select
end-if
end-procedure
!**********************************************************************
!**********************************************************************
!      no_mrs_select
!**********************************************************************
!**********************************************************************
begin-procedure no_mrs_select
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure no_mrs_select')
BEGIN-SELECT ON-ERROR=SQL-ERROR
SUM(C.CALC_RSLT_VAL)    &C.CALC_RSLT_VAL
C.PIN_NUM        &C.PIN_NUM
    #debug1 show '** USA STATIC HRS EARN TAX section:        CALC_RSLT_VAL = ' &C.CALC_RSLT_VAL
    Do get_pin_name(&C.PIN_NUM, $TaxName)
    Do curr_tax_detail_line
    #debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Back into Procedure no_mrs_select')
C.GP_PSLP_ORDER       &C.STAT_EARN_GP_PSLP_ORDER       
FROM PS_GPUS_PSLP_RSLT C
WHERE C.JOBINSTANCE = #gbl_job_instance AND
C.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
C.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
C.GP_ELN_SET        = $gbl_GP_ELN_SET AND
C.EMPLID = $gbl_tgt_Emplid AND
C.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
C.GPUS_PSLP_ELN_CAT = '20' AND
C.GPUS_PSLP_CURR_YTD = '10' GROUP BY C.PIN_NUM, C.GP_PSLP_ORDER ORDER BY C.GP_PSLP_ORDER
END-SELECT
end-procedure
!**********************************************************************
!**********************************************************************
!      mrs_select
!**********************************************************************
!**********************************************************************
begin-procedure mrs_select
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure mrs_select')
BEGIN-SELECT ON-ERROR=SQL-ERROR
SUM(G.CALC_RSLT_VAL)    &G.CALC_RSLT_VAL
G.PIN_NUM        &G.PIN_NUM
G.INSTANCE       &G.INSTANCE
G.PIN_CATEGORY   &G.PIN_CATEGORY
G.GPUS_PSLP_TITLEOPT   &G.GPUS_PSLP_TITLEOPT
G.GPUS_PSLP_TITLE       &G.GPUS_PSLP_TITLE
G.USER_KEY1             &G.USER_KEY1
G.USER_KEY2             &G.USER_KEY2
G.GP_PSLP_ORDER       &G.STAT_EARN_GP_PSLP_ORDER
   #debug1 show '** USA STATIC HRS EARN TAX section:       get_Taxes = ' &G.CALC_RSLT_VAL
   Do get_appropriate_description(&G.PIN_NUM, &G.GPUS_PSLP_TITLEOPT,&G.GPUS_PSLP_TITLE,&G.PIN_CATEGORY,
                                                     $WTHLD_LBL,&G.USER_KEY2,$TaxName)
   #debug1 show '** USA STATIC HRS EARN TAX section:       TAXNAME = ' $TaxName
   Do curr_tax_detail_line
   #debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Back into Procedure mrs_select')
FROM PS_GPUS_PSLP_RSLT G
WHERE G.JOBINSTANCE = #gbl_job_instance AND
G.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
G.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
G.GP_ELN_SET        = $gbl_GP_ELN_SET AND
G.EMPLID = $gbl_tgt_Emplid AND
G.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
G.GPUS_PSLP_ELN_CAT = '20' AND
G.GPUS_PSLP_CURR_YTD = '10' 
GROUP BY G.PIN_CATEGORY,G.PIN_NUM,G.GPUS_PSLP_TITLEOPT,G.GPUS_PSLP_TITLE,G.USER_KEY1,G.USER_KEY2,G.INSTANCE,
                                     G.RATE_RSLT_VAL,G.GP_PSLP_ORDER ORDER BY G.GP_PSLP_ORDER
END-SELECT
end-procedure

!**********************************************************************
!**********************************************************************
!      Get_Taxes_Accum
!**********************************************************************
!**********************************************************************
begin-procedure Get_Taxes_Accum
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure Get_Taxes_Accum')

!select from the extract table

!BEGIN-SELECT ON-ERROR=SQL-ERROR

!D.RATE_RSLT_VAL
!SUM(D.UNIT_RSLT_VAL)    &D.UNIT_RSLT_VAL
!SUM(D.CALC_RSLT_VAL)    &D.CALC_RSLT_VAL
!D.PIN_NUM
!    Do get_pin_name(&D.PIN_NUM, $TaxName)
!    Do ytd_tax_detail_line
!D.GP_PSLP_ORDER              &D.STAT_EARN_GP_PSLP_ORDER
!FROM PS_GPUS_PSLP_RSLT D
!WHERE D.JOBINSTANCE = #gbl_job_instance AND
!D.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
!D.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
!D.GP_ELN_SET        = $gbl_GP_ELN_SET AND
!D.EMPLID = $gbl_tgt_Emplid AND
!D.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
!D.GPUS_PSLP_ELN_CAT = '20' AND
!D.GPUS_PSLP_CURR_YTD = '20' GROUP BY D.PIN_NUM, D.RATE_RSLT_VAL, D.GP_PSLP_ORDER ORDER BY D.GP_PSLP_ORDER


!END-SELECT
BEGIN-SELECT ON-ERROR=SQL-ERROR
H.RATE_RSLT_VAL
H.UNIT_RSLT_VAL    &H.UNIT_RSLT_VAL
H.CALC_RSLT_VAL    &H.CALC_RSLT_VAL
H.PIN_NUM        &H.PIN_NUM
H.INSTANCE       &H.INSTANCE
H.PIN_CATEGORY   &H.PIN_CATEGORY
H.GPUS_PSLP_TITLEOPT   &H.GPUS_PSLP_TITLEOPT
H.GPUS_PSLP_TITLE       &H.GPUS_PSLP_TITLE
H.USER_KEY1             &H.USER_KEY1
H.USER_KEY2             &H.USER_KEY2
H.GP_PSLP_ORDER       &H.STAT_EARN_GP_PSLP_ORDER
#debug1 show '** USA STATIC HRS EARN TAX section:       get_Taxes = ' &H.CALC_RSLT_VAL
   Do get_appropriate_description(&H.PIN_NUM, &H.GPUS_PSLP_TITLEOPT,&H.GPUS_PSLP_TITLE,&H.PIN_CATEGORY,$WTHLD_LBL, 
                               &H.USER_KEY2,$TaxName)
   #debug1 show '** USA STATIC HRS EARN TAX section:       TAXNAME = ' $TaxName
   Do ytd_tax_detail_line
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Back into Procedure Get_Taxes_Accum')
FROM PS_GPUS_PSLP_RSLT H
WHERE H.JOBINSTANCE = #gbl_job_instance AND
H.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
H.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
H.GP_ELN_SET        = $gbl_GP_ELN_SET AND
H.EMPLID = $gbl_tgt_Emplid AND
H.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
H.GPUS_PSLP_ELN_CAT = '20' AND
H.GPUS_PSLP_CURR_YTD = '20' AND
H.SLICE_END_DT = (SELECT MAX(X.SLICE_END_DT) 
FROM PS_GPUS_PSLP_RSLT X
WHERE X.JOBINSTANCE = #gbl_job_instance AND
X.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
X.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
X.GP_ELN_SET        = $gbl_GP_ELN_SET AND
X.EMPLID = $gbl_tgt_Emplid AND
X.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
X.GPUS_PSLP_ELN_CAT = '20' AND
X.GPUS_PSLP_CURR_YTD = '20')
ORDER BY H.GP_PSLP_ORDER

END-SELECT

end-procedure


!**********************************************************************
!**********************************************************************
!      curr_tax_detail_line
!**********************************************************************
!**********************************************************************


begin-procedure curr_tax_detail_line

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure curr_tax_detail_line')

if (#curr_tax_detail_line < (#gbl_curr_Section_Dtl_Lines - 2) )

    
    Move &G.CALC_RSLT_VAL to #CurrentTax
    Put $TaxName into Tax_DetailArray(#curr_tax_detail_line) name
    Put #CurrentTax into Tax_DetailArray(#curr_tax_detail_line) CurrentTax
    Add #CurrentTax to #gbl_total_tax_cur
    
    Add 1 to #curr_tax_detail_line

else

  if (#curr_detail_line < (#gbl_curr_Section_Dtl_Lines - 1) )
    let $print_summary_cur_tax_line = 'y'
    do summary_tax_curr_line
  else
      if (#gbl_curr_Section_Dtl_Lines = 1) 
        let $print_summary_cur_tax_line = 'n'
        do summary_tax_curr_line
      end-if
  end-if
end-if

#debug1 show '** USA STATIC HRS EARN TAX section:       #gbl_total_tax_cur = ' #gbl_total_tax_cur

end-procedure


!**********************************************************************
!**********************************************************************
!      ytd_tax_detail_line
!**********************************************************************
!**********************************************************************

begin-procedure ytd_tax_detail_line

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure ytd_tax_detail_line')
   
    Move &H.CALC_RSLT_VAL to #YtdTax
    #debug1 show '** USA STATIC HRS EARN TAX section:       YTD tax = ' #YtdTax
    do search_TaxDetailArray($TaxName, $found, #ind)
    if $found = 'y'
        Array-Add #YtdTax to Tax_DetailArray(#ind) YtdTax
        Add #YtdTax to #gbl_total_tax_ytd
    else
      if (#curr_tax_detail_line < (#gbl_curr_Section_Dtl_Lines - 2) )
        Put $TaxName into Tax_DetailArray(#curr_tax_detail_line) name
        Put #YtdTax into Tax_DetailArray(#curr_tax_detail_line) YtdTax
        Add 1 to #curr_tax_detail_line
        Add #YtdTax to #gbl_total_tax_ytd
      else
       if (#curr_detail_line < (#gbl_curr_Section_Dtl_Lines - 1) )
         let $print_summary_ytd_tax_line = 'y'
         do summary_tax_ytd_line
       else
         if (#gbl_curr_Section_Dtl_Lines = 1) 
           let $print_summary_ytd_tax_line = 'n'
           do summary_tax_ytd_line
         end-if
      end-if
    end-if

  end-if

end-procedure


!**********************************************************************
!**********************************************************************
!      search_TaxDetailArray
!**********************************************************************
!**********************************************************************
begin-procedure search_TaxDetailArray($search_pin_nm, :$found, :#i)

let #i = 0

while (#i < {Tax_DetailArray_Size})
    get $Pin_nm from Tax_DetailArray(#i) name 
    if $search_pin_nm = $Pin_nm
        let $found = 'y'
        break
    else
        let $found = 'n'
    end-if

    Add 1 to #i

end-while

end-procedure

!**********************************************************************
!**********************************************************************
!      summary_tax_curr_line
!**********************************************************************
!**********************************************************************

begin-procedure summary_tax_curr_line
#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure summary_tax_curr_line')
 
Put $OTHER_LBL into Tax_SummaryArray(0) name
Move &G.CALC_RSLT_VAL to #CurrentTax
#debug1 show '** USA STATIC HRS EARN TAX section:       #CurrentTax = ' #CurrentTax
Array-Add #CurrentTax to Tax_summaryArray(0) CurrentTax

!Update Total variables
Add #CurrentTax to #gbl_total_tax_cur

end-procedure



!**********************************************************************
!**********************************************************************
!      summary_tax_ytd_line
!**********************************************************************
!**********************************************************************
begin-procedure summary_tax_ytd_line

#debug do Pslp-Proc-Debug-Msg('USA STATIC HRS EARN TAX section: Procedure summary_tax_ytd_line')

Put $OTHER_LBL into Tax_summaryArray(0) name
Move &H.CALC_RSLT_VAL to #YtdTax

Array-Add #YtdTax to Tax_summaryArray(0) YtdTax

!Update Total variables
Add #YtdTax to #gbl_total_tax_ytd
end-procedure

!**********************************************************************
!**********************************************************************
!      total_tax_line
!**********************************************************************
!**********************************************************************
begin-procedure total_tax_line

Put $TOTAL_LBL into Tax_totalArray(0) name
Put #gbl_total_tax_cur into Tax_totalArray(0) currentTax
Put #gbl_total_tax_ytd into Tax_totalArray(0) YtdTax

#debug1 show '** USA STATIC HRS EARN TAX section: procedure total_tax_line'
#debug1 show '** USA STATIC HRS EARN TAX section:        #gbl_total_tax_cur = '#gbl_total_tax_cur ' #gbl_total_tax_ytd = '#gbl_total_tax_ytd

end-procedure

!**********************************************************************
!**********************************************************************
!      Read_Tax_SummaryArray
!**********************************************************************
!**********************************************************************
begin-procedure Read_Tax_SummaryArray

    get $TaxName #CurrentTax #YTDTax from Tax_SummaryArray(0) name CurrentTax YTDTax
    #debug1 show '** USA STATIC HRS EARN TAX section: procedure Read_Tax_SummaryArray'
    #debug1 show '** USA STATIC HRS EARN TAX section:        Read_TaxTotalArray(0)  $TaxName  = '$TaxName ' #CurrentTax = '#CurrentTax ' #YTDTax = '  #YTDTax
end-procedure

!**********************************************************************
!**********************************************************************
!      Read_Tax_TotalArray
!**********************************************************************
!**********************************************************************
begin-procedure Read_Tax_TotalArray

    get $TaxName #CurrentTax #YTDTax from Tax_TotalArray(0) name CurrentTax YTDTax    
    #debug1 show '** USA STATIC HRS EARN TAX section: procedure Read_Tax_TotalArray'
    #debug1 show '** USA STATIC HRS EARN TAX section:        Read_TaxTotalArray(0)  $TaxName  = '$TaxName ' #CurrentTax = '#CurrentTax ' #YTDTax = '  #YTDTax

end-procedure

