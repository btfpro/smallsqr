!********************************************************
!  PAY130CN: Re-Open/Close Year-End Off-Cycle Calendar  *
!********************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2010, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2010/11/02:12:50:58                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  813289                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
 #ifdef PRCSSCHD
  do StdAPI-Term
 #endif
end-report

begin-procedure Init-Report
  move 'PAY130CN' to $ReportID
  move 'Open/Close Year-End Off-Cycle Calendar' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Get-Current-DateTime
  do Get-Calendar-Year-Id

 #ifdef PRCSSCHD
  do StdAPI-Init
 #endif

  if $Prcs_Process_Instance = ''
     do Prompts
  else
#ifdef PRCSSCHD
     do Select-Canadian-YrEnd-Parameters
     do Convert-Parameters
#endif
  end-if
end-procedure


begin-procedure Process-Main
  do Get-Current-Year-Calendar
  if $Current-Year-Found = 'Y'
    do Get-Year-End-Calendar
    if $Year-End-Found = 'Y'
      if $OpenClose = 'O'
        if &B.Pay_Off_Cycle_Cls = 'Y'
          move 'N' to $Pay_Off_Cycle_Cls
          do Update-Year-End-Calendar
          do Update-HR-Calendar-Xref
          if $Update-Error = 'N'
            display 'Year-End Calendar has been opened.'
  !         input $Junk 'Press enter to continue.'
          end-if
        else
          display 'Year-End Calendar is already open.'
  !       input $Junk 'Press enter to continue.'
        end-if
      else
        if &B.Pay_Off_Cycle_Cls = 'N'
          move 'Y' to $Pay_Off_Cycle_Cls
          do Update-Year-End-Calendar
          if $Update-Error = 'N'
            Display  'Year-End Calendar has been closed.'
  !         input $Junk 'Press enter to continue.'
          end-if
        else
          display 'Year-End Calendar is already closed.'
  !       input $Junk 'Press enter to continue.'
        end-if
      end-if
    else
      display 'Year-End Calendar not found.'
  !   input $Junk 'Press enter to end process.'
    end-if
  else
    display 'Current Year Calendar not found.'
  ! input $Junk 'Press enter to end process.'
  end-if
end-procedure


begin-procedure Get-Current-Year-Calendar
  let $Current-Year-Found = 'N'
begin-SELECT
MAX(D.BALANCE_YEAR)   &Current-Year
  let #Current-Year = &Current-Year
  let $Current-Year-Found = 'Y'
FROM PS_PAY_CALENDAR    A,
     PS_PAY_CAL_BAL_ID  D
WHERE A.COMPANY       = $Company
 AND  A.PAYGROUP      = $PayGroup
 AND  A.PAY_SHEETS_RUN = 'Y'
 AND  A.COMPANY       = D.COMPANY
 AND  A.PAYGROUP      = D.PAYGROUP
end-SELECT
end-procedure


begin-procedure Get-Year-End-Calendar
  let $Year-End-Found = 'N'
begin-SELECT
B.PAY_END_DT
B.PAY_OFF_CYCLE_CLS
  let $Year-End-Found = 'Y'
FROM PS_PAY_CALENDAR B
WHERE B.COMPANY       = $Company
 AND  B.PAYGROUP      = $PayGroup
 AND  B.PAY_END_DT    =
            (SELECT MAX(E.PAY_END_DT)
             FROM  PS_PAY_CAL_BAL_ID  E
             WHERE  E.COMPANY       = B.COMPANY
              AND   E.PAYGROUP      = B.PAYGROUP
              AND   E.BALANCE_YEAR  < #Current-Year
              AND   E.BALANCE_ID    = $Calendar_Year_Id)
end-SELECT
end-procedure

begin-procedure Update-Year-End-Calendar
  let $Update-Error = 'N'
begin-SQL  on-error=Bad-Update
UPDATE PS_PAY_CALENDAR
SET PAY_OFF_CYCLE_CLS = $PAY_OFF_CYCLE_CLS
WHERE COMPANY    = $COMPANY
 AND  PAYGROUP   = $PAYGROUP
 AND  PAY_END_DT = &B.PAY_END_DT
end-SQL
end-procedure

begin-procedure Bad-Update
  input $Junk 'Unsuccessful Update.  Press enter to end process.'
  let $Update-Error = 'Y'
end-procedure


! Update cross reference used by HR Wage Progression if matching entry found
begin-procedure Update-HR-Calendar-Xref
begin-SQL ON-ERROR=Ignore
UPDATE PS_HR_PAY_CAL_XREF
SET PAY_OFF_CYCLE_CLS = 'N'
WHERE COMPANY    = $COMPANY
 AND  PAYGROUP   = $PAYGROUP
 AND  PAY_END_DT = &B.PAY_END_DT
 AND  PAY_OFF_CYCLE_CLS = 'Y'
end-SQL
end-procedure


begin-procedure Prompts
  display ' '
  input $Company maxlen=10 'Enter Company' type=char
  uppercase $Company
  display ' '
  input $PayGroup maxlen=10 'Enter Pay Group' type=char
  uppercase $PayGroup
  display ' '
  input $OpenClose maxlen=1 'Enter "O" to Open or "C" to Close' type=char
  uppercase $OpenClose
  display ' '
end-procedure

begin-procedure Convert-Parameters

move &RC_CAN_YE.Company            to $Company
move &RC_CAN_YE.Paygroup           to $Paygroup
move &RC_CAN_YE.Open_Close_Flag    to $OpenClose

end-procedure


!**********************
begin-procedure Ignore
!**********************

! dummy procedure to avoid SQL error message
end-procedure



#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#ifdef PRCSSCHD
#Include 'ctxrctl1.sqc'  !Select-Year-End Parameters procedure
#Include 'stdapi.sqc'    !Update Process API
#end-if
#include 'getbalid.sqc'  !Get-Calendar-Year-Id
