!********************************************************
!  PER027B.SQR - VETS-100 Federal Contractors Report    !
!********************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                $Date:  2006/01/17:21:25:57                           !
!             $Release:  HR9                                           !
!            $Revision:  102                                           !
!                                                                      *
!***********************************************************************
! 05/30/00 - Rewritten for release 8 - this is the second of three     *
!      JDL   processes that need to be run to produce a compliant      *
!            digital file for submission to the VA                     *
!***********************************************************************
! Uncomment the following line to have debug values put into the sqr.log
! #DEFINE DEBUGPER027

#include 'setenv.sqc'   !Set environment
#include 'hrhl01.sqc'   !Landscape Confidential Header

!----------------------------------------------------------------------
!---------Program------------------------------------------------------
!----------------------------------------------------------------------
! This process will print reports based on the information found
! either in the Vets 100 temp table or in the Vets 100 history table.
!
! The field V100_HIRING_LOC is designed as a reference field - we use
! this to store the ESTABID of the establishment that the line
! holds stored calculations for.
!----------------------------------------------------------------------
Begin-Program

#IFDEF DEBUGPER027
  DISPLAY 'FLOW: Begin Program'
#ENDIF

  DO Use-Standard-Layout
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report

  ! Set the confidential flag for the header
  !------------------------------------------------------------
  LET $Confidential = 'YES'

  ! Set the No Submission flag for the header
  !------------------------------------------------------------
  LET $No_Submission = 'YES'

  ! If we're only running for one HQ Unit, populate the dynamic
  ! where clause with a conditional to limit to the appropriate
  ! establishments
  !------------------------------------------------------------
  
  !**********Begin Resolution 631970 *********
  !IF $ReportChoice = '3'
  !   LET $Where-Clause = 'WHERE CTL_ESTABID = ' || $Quote || $CtlEstabID || $Quote
  !ELSE
  !   LET $Where-Clause = ''
  !END-IF

  IF $ReportChoice = '1'
        LET $Where-Clause = 'WHERE V100_HIRING_LOC IN (SELECT B.ESTABID FROM PS_COMPANY_TBL A, PS_ESTAB_TBL B WHERE A.COMPANY=B.COMPANY AND A.COMPANY=' || $Quote || $COMPANY || $Quote || ')'
  ELSE
    IF $ReportChoice = '2'
      LET $Where-Clause = 'WHERE V100_HIRING_LOC = ' || $Quote || $Estab || $Quote
    ELSE
      IF $ReportChoice = '3'
        LET $Where-Clause = 'WHERE CTL_ESTABID = ' || $Quote || $CtlEstabID || $Quote
      ELSE
        LET $Where-Clause = ''
      END-IF
    END-IF
  END-IF
  !**********End Resolution 631970 *********
  
  IF IsBlank($Where-Clause)
        IF $Run_From_Current = 'N'
           LET $Where-Clause = 'WHERE V100_REPORTING_YR = ' || $Quote || $History_Year || $Quote
        END-IF
    ELSE
        IF $Run_From_Current = 'N'
           LET $Where-Clause = $Where-Clause || ' AND V100_REPORTING_YR = ' || $Quote || $History_Year || $Quote
        END-IF
    END-IF
  
  ! Use dynamic SQL to pick table data comes from
  !------------------------------------------------------------
  IF $Run_From_Current = 'N'
      LET $From-Clause = ' PS_V100_HISTORY'

  ELSE
      LET $From-Clause = ' PS_V100_TEMP_TBL'
  END-IF

  ! Begin the process - main SQL Select Loop
  !------------------------------------------------------------
  DO Process-Main


  ! Update the Process Scheduler
  !------------------------------------------------------------
  DO Stdapi-Term

End-Program

!----------------------------------------------------------------------
!---------Init-Report--------------------------------------------------
!----------------------------------------------------------------------
! We need to get the calendar year from the process instance
!----------------------------------------------------------------------

begin-procedure Init-Report

#IFDEF DEBUGPER027
  DISPLAY 'FLOW: Init-Report'
#ENDIF

  move 'PER027B' to $ReportID
  move 'Vets 100 Federal Contractors Report' to $ReportTitle

  display $ReportTitle

#IFDEF DEBUGPER027
  display 'Report ID: ' NOLINE
  display $ReportID
#ENDIF

  do Stdapi-Init

  if $prcs_process_instance = ''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
     STOP QUIET
  else
     do Select-Parameters
  end-if

end-procedure

!----------------------------------------------------------------------
!---------Get-Values---------------------------------------------------
!----------------------------------------------------------------------
! Fetch run control parameters
!----------------------------------------------------------------------
begin-procedure Get-Values
#IFDEF DEBUGPER027
  DISPLAY 'FLOW: Get-Values'
#ENDIF

   do Get-Run-Scope
   do Get-CTL_ESTABID
   do Get-Run-From-Current
   do Get-History-Year
!**********Begin Resolution 631970 *********
   do Get-Establishment
   do Get-Company
!**********End Resolution 631970 *********

   LET $Quote = ''''

#IFDEF DEBUGPER027
!**********Begin Resolution 631970 *********
  DISPLAY '   $Estab: ' NOLINE
  DISPLAY $Estab
  DISPLAY '   $COMPANY: ' NOLINE
  DISPLAY $COMPANY  
!**********End Resolution 631970 *********
  DISPLAY '   $CtlEstabID: ' NOLINE
  DISPLAY $CtlEstabID
  DISPLAY '   $ReportChoice: ' NOLINE
  DISPLAY $ReportChoice
  DISPLAY '   $Run_From_Current: ' NOLINE
  DISPLAY $Run_From_Current
  DISPLAY '   $History_Year: ' NOLINE
  DISPLAY $History_Year
#ENDIF

end-procedure Get-Values

!----------------------------------------------------------------------
!---------Process-Main-------------------------------------------------
!----------------------------------------------------------------------
! This routine will be run once - this SQL loop controls the flow of
! the program.
!----------------------------------------------------------------------
begin-procedure Process-Main

! Debugging stuff
!----------------------------------------------------
#IFDEF DEBUGPER027
  DISPLAY 'FLOW: Process-Main'
#ENDIF

BEGIN-SELECT
CTL_ESTABID
V100_ROW
V100_COMPANY_NO
V100_ENDING_PD
V100_ORG_TYPE
V100_FORM_TYPE
V100_MSCNUMBER
V100_PARENT_CO
V100_STREET
V100_CITY
V100_COUNTY
V100_STATE
V100_ZIPCODE
V100_HIRING_LOC    () ON-BREAK PRINT=NEVER PROCEDURE=Page-Break
V100_HL_NAME
V100_HL_STREET
V100_HL_CITY
V100_HL_COUNTY
V100_HL_STATE
V100_HL_ZIP
V100_NAICS
V100_DUNS_NUM
V100_EIN
V100_L1
V100_M1
V100_N1
V100_O1
V100_P1
V100_Q1
V100_R1
V100_S1
V100_L2
V100_M2
V100_N2
V100_O2
V100_P2
V100_Q2
V100_R2
V100_S2
V100_L3
V100_M3
V100_N3
V100_O3
V100_P3
V100_Q3
V100_R3
V100_S3
V100_L4
V100_M4
V100_N4
V100_O4
V100_P4
V100_Q4
V100_R4
V100_S4
V100_L5
V100_M5
V100_N5
V100_O5
V100_P5
V100_Q5
V100_R5
V100_S5
V100_L6
V100_M6
V100_N6
V100_O6
V100_P6
V100_Q6
V100_R6
V100_S6
V100_L7
V100_M7
V100_N7
V100_O7
V100_P7
V100_Q7
V100_R7
V100_S7
V100_L8
V100_M8
V100_N8
V100_O8
V100_P8
V100_Q8
V100_R8
V100_S8
V100_L9
V100_M9
V100_N9
V100_O9
V100_P9
V100_Q9
V100_R9
V100_S9
V100_L10
V100_M10
V100_N10
V100_O10
V100_P10
V100_Q10
V100_R10
V100_S10
V100_MAX_NUMBER
V100_MIN_NUMBER

     DO Get-Establishment-Info

     ALTER-PRINTER
      font=4
      point-size=10

     ! Horizontal Line above estab information
     !-------------------------------------------
     GRAPHIC (+1,2,103) HORZ-LINE

     ! Some of the submitted information about
     ! this estab
     !-------------------------------------------

     LET $Output = 'VETS-100 Establishment Number: ' || &V100_COMPANY_NO
     PRINT $Output                                             (+1, 7) BOLD

     PRINT 'MSC Number: '                                      (,82) BOLD
     PRINT &V100_MSCNUMBER                                     (,93,10) BOLD

     LET $Output = 'Report for Period Ending: '
     LET $Output = $Output || {ps-substr} (&V100_ENDING_PD,1,2) || '/' ! Month
     LET $Output = $Output || {ps-substr} (&V100_ENDING_PD,3,2) || '/' ! Day
     LET $Output = $Output || {ps-substr} (&V100_ENDING_PD,5,4)        ! Year
     PRINT $Output                                             (+1, 7)  BOLD

     PRINT 'EIN: '                                             (,82)    BOLD
     PRINT &V100_EIN                                           (,93,10) BOLD EDIT xx-xxxxxxx

     PRINT 'NAICS: '                                           (+1,82)  BOLD
     PRINT &V100_NAICS                                         (,93,10) BOLD

     IF NOT ISBLANK(&V100_HL_NAME)
        LET $Output = 'Parent Company: ' || &V100_PARENT_CO
        PRINT $Output                                          (+1,7)   BOLD
     ELSE
        LET $Compare = rtrim(&V100_FORM_TYPE,' ')
        IF $Compare != 'S'
            PRINT 'Headquarters Unit'                          (+1,7)  BOLD
        ELSE
            PRINT 'Single Establishment'                       (+1,7)  BOLD
        END-IF
     END-IF

     PRINT 'DUNS: '                                            (,82)  BOLD
     PRINT &V100_DUNS_NUM                                      (,93,11) BOLD EDIT xx-xxx-xxxx

     ! Horizontal Line below estab information
     !-------------------------------------------
     GRAPHIC (,2,103) HORZ-LINE

     ! Column Headers
     !-------------------------------------------
     ALTER-PRINTER
        point-size = 12


     PRINT 'Number of Employees'                               (+3,22) BOLD
     PRINT 'New Hires (Previous 12 Months)'                    (,62)   BOLD

     GRAPHIC (,15,32) HORZ-LINE
     GRAPHIC (,52,45) HORZ-LINE

     ALTER-PRINTER
        point-size = 7

     PRINT 'Special Disabled'                                  (+1,15) BOLD
     PRINT 'Vietnam Era'                                       (,28)   BOLD
     PRINT 'Other Protected'                                   (,39)   BOLD

     PRINT 'Special Disabled'                                  (,53)   BOLD
     PRINT 'Vietnam Era'                                       (,62)   BOLD
     PRINT 'Newly Separated'                                   (,69)   BOLD
     PRINT 'Other Protected'                                   (,78)   BOLD

     PRINT 'Total New Hires'                                   (,88)   BOLD

     ALTER-PRINTER
        point-size = 12

     PRINT 'Job Category'                                      (+1,2)  BOLD

     ALTER-PRINTER
        point-size = 7

     PRINT 'Veterans'                                          (,17)   BOLD
     PRINT 'Veterans'                                          (,29)   BOLD
     PRINT 'Veterans'                                          (,41)   BOLD

     PRINT 'Veterans'                                          (,55)   BOLD
     PRINT 'Veterans'                                          (,63)   BOLD
     PRINT 'Veterans'                                          (,71)   BOLD
     PRINT 'Veterans'                                          (,80)   BOLD

     PRINT 'Both Veterans'                                     (,88)   BOLD

     PRINT 'And Non-Veterans'                                  (+1,88) BOLD

     ! Horizontal Line below headers
     !-------------------------------------------
     GRAPHIC (,2,95) HORZ-LINE

     ALTER-PRINTER
        point-size = 8

     PRINT 'Officials & Managers'                              (+1,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L1                                            (,17) EDIT 999999
     PRINT &V100_M1                                            (,29) EDIT 999999
     PRINT &V100_N1                                            (,41) EDIT 999999
     PRINT &V100_O1                                            (,55) EDIT 999999
     PRINT &V100_P1                                            (,63) EDIT 999999
     PRINT &V100_Q1                                            (,71) EDIT 999999
     PRINT &V100_R1                                            (,79) EDIT 999999
     PRINT &V100_s1                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     GRAPHIC (+1,2,95) box 1 0 15
     PRINT 'Professionals'                                     (,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L2                                            (,17) EDIT 999999
     PRINT &V100_M2                                            (,29) EDIT 999999
     PRINT &V100_N2                                            (,41) EDIT 999999
     PRINT &V100_O2                                            (,55) EDIT 999999
     PRINT &V100_P2                                            (,63) EDIT 999999
     PRINT &V100_Q2                                            (,71) EDIT 999999
     PRINT &V100_R2                                            (,79) EDIT 999999
     PRINT &V100_s2                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8


     PRINT 'Technicians'                                       (+1,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L3                                            (,17) EDIT 999999
     PRINT &V100_M3                                            (,29) EDIT 999999
     PRINT &V100_N3                                            (,41) EDIT 999999
     PRINT &V100_O3                                            (,55) EDIT 999999
     PRINT &V100_P3                                            (,63) EDIT 999999
     PRINT &V100_Q3                                            (,71) EDIT 999999
     PRINT &V100_R3                                            (,79) EDIT 999999
     PRINT &V100_s3                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     GRAPHIC (+1,2,95) box 1 0 15
     PRINT 'Sales Workers'                                     (,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L4                                            (,17) EDIT 999999
     PRINT &V100_M4                                            (,29) EDIT 999999
     PRINT &V100_N4                                            (,41) EDIT 999999
     PRINT &V100_O4                                            (,55) EDIT 999999
     PRINT &V100_P4                                            (,63) EDIT 999999
     PRINT &V100_Q4                                            (,71) EDIT 999999
     PRINT &V100_R4                                            (,79) EDIT 999999
     PRINT &V100_s4                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     PRINT 'Office and Clerical'                               (+1,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L5                                            (,17) EDIT 999999
     PRINT &V100_M5                                            (,29) EDIT 999999
     PRINT &V100_N5                                            (,41) EDIT 999999
     PRINT &V100_O5                                            (,55) EDIT 999999
     PRINT &V100_P5                                            (,63) EDIT 999999
     PRINT &V100_Q5                                            (,71) EDIT 999999
     PRINT &V100_R5                                            (,79) EDIT 999999
     PRINT &V100_s5                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     GRAPHIC (+1,2,95) box 1 0 15
     PRINT 'Craft Workers (skilled)'                           (,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L6                                            (,17) EDIT 999999
     PRINT &V100_M6                                            (,29) EDIT 999999
     PRINT &V100_N6                                            (,41) EDIT 999999
     PRINT &V100_O6                                            (,55) EDIT 999999
     PRINT &V100_P6                                            (,63) EDIT 999999
     PRINT &V100_Q6                                            (,71) EDIT 999999
     PRINT &V100_R6                                            (,79) EDIT 999999
     PRINT &V100_s6                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     PRINT 'Operative (semi-skilled)'                          (+1,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L7                                            (,17) EDIT 999999
     PRINT &V100_M7                                            (,29) EDIT 999999
     PRINT &V100_N7                                            (,41) EDIT 999999
     PRINT &V100_O7                                            (,55) EDIT 999999
     PRINT &V100_P7                                            (,63) EDIT 999999
     PRINT &V100_Q7                                            (,71) EDIT 999999
     PRINT &V100_R7                                            (,79) EDIT 999999
     PRINT &V100_s7                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     GRAPHIC (+1,2,95) box 1 0 15
     PRINT 'Laborers (unskilled)'                              (,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L8                                            (,17) EDIT 999999
     PRINT &V100_M8                                            (,29) EDIT 999999
     PRINT &V100_N8                                            (,41) EDIT 999999
     PRINT &V100_O8                                            (,55) EDIT 999999
     PRINT &V100_P8                                            (,63) EDIT 999999
     PRINT &V100_Q8                                            (,71) EDIT 999999
     PRINT &V100_R8                                            (,79) EDIT 999999
     PRINT &V100_s8                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     PRINT 'Service Workers'                                   (+1,2)
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L9                                            (,17) EDIT 999999
     PRINT &V100_M9                                            (,29) EDIT 999999
     PRINT &V100_N9                                            (,41) EDIT 999999
     PRINT &V100_O9                                            (,55) EDIT 999999
     PRINT &V100_P9                                            (,63) EDIT 999999
     PRINT &V100_Q9                                            (,71) EDIT 999999
     PRINT &V100_R9                                            (,79) EDIT 999999
     PRINT &V100_s9                                            (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     ! Horizontal Line below calculations
     !-------------------------------------------
     GRAPHIC (,2,95) HORZ-LINE

     PRINT 'TOTAL'                                             (+2,2) BOLD
     !------------------------------------------------------------------------
     ALTER-PRINTER
        point-size = 7
     PRINT &V100_L10                                           (,17) EDIT 999999
     PRINT &V100_M10                                           (,29) EDIT 999999
     PRINT &V100_N10                                           (,41) EDIT 999999
     PRINT &V100_O10                                           (,55) EDIT 999999
     PRINT &V100_P10                                           (,63) EDIT 999999
     PRINT &V100_Q10                                           (,71) EDIT 999999
     PRINT &V100_R10                                           (,79) EDIT 999999
     PRINT &V100_s10                                           (,90) EDIT 999999
     ALTER-PRINTER
        point-size = 8

     LET #MaxOut = &V100_MAX_NUMBER
     LET $Output = 'Maximum Employees:  ' || TO_CHAR(#MaxOut)
     PRINT $Output                                             (+2,34) BOLD SHADE

     LET #MinOut = &V100_MIN_NUMBER
     LET $Output = 'Minimum Employees:  ' || TO_CHAR(#MinOut)
     PRINT $Output                                             (,58) BOLD SHADE







FROM [$From-Clause]
[$Where-Clause]
ORDER BY CTL_ESTABID, V100_ROW

END-SELECT
end-procedure

!----------------------------------------------------------------------
!---------Get-Establishment-Info---------------------------------------
!----------------------------------------------------------------------
! Retrieves name and address information for the specific location
!----------------------------------------------------------------------

Begin-Procedure Get-Establishment-Info

Do Get-From-Thru-Date

#IFDEF DEBUGPER025
  DISPLAY 'FLOW: Get-Establishment-Info'
#ENDIF

! For DB2/All we need to trim this before feeding
! the value back in the conditional
!-------------------------------------------------
LET $Hiring_Location = RTRIM(&V100_HIRING_LOC,' ')

BEGIN-SELECT
Q.DESCR
Q.ADDRESS1
Q.ADDRESS2
Q.ADDRESS3
Q.ADDRESS4
Q.NUM1
Q.NUM2
Q.CITY
Q.STATE
Q.POSTAL
Q.COUNTRY
Q.IN_CITY_LIMIT
Q.GEO_CODE
Q.HOUSE_TYPE

    LET $NAME = &Q.DESCR
    LET $ADDRESS1 = &Q.ADDRESS1
    LET $ADDRESS2 = &Q.ADDRESS2
    LET $ADDRESS3 = &Q.ADDRESS3
    LET $ADDRESS4 = &Q.ADDRESS4
    LET $NUM1 = &Q.NUM1
    LET $NUM2 = &Q.NUM2
    LET $HOUSE_TYE = &Q.HOUSE_TYPE

    LET $CITY = &Q.CITY
    LET $COUNTRY = &Q.COUNTRY

    LET $STATE = &Q.STATE

    LET $POSTAL = &Q.POSTAL

    LET $GEO_CODE = &Q.GEO_CODE
    LET $IN_CITY_LIMIT = &Q.IN_CITY_LIMIT

FROM PS_ESTAB_TBL Q
WHERE Q.ESTABID = $Hiring_Location
AND Q.EFFDT = (SELECT MAX(EFFDT) FROM PS_ESTAB_TBL
                WHERE ESTABID = Q.ESTABID
                AND  EFFDT <= $ThruDate
                AND  EFF_STATUS = 'A')


END-SELECT
End-Procedure Get-Establishment-Info

!----------------------------------------------------------------------
!---------Page-Break---------------------------------------------------
!----------------------------------------------------------------------
! Causes a page break between establishments
!----------------------------------------------------------------------
begin-procedure Page-Break
NEW-PAGE
end-procedure Page-Break
!----------------------------------------------------------------------
!---------SQCs---------------------------------------------------------
!----------------------------------------------------------------------
#include 'stdapi.sqc'      !Routine to update run status
#include 'rgrnctl1.sqc'    !Get run control parameter values
#include 'rggetval.sqc'    !Get values mask routines
#include 'askcalyr.sqc'    !Ask Calendar Year input
#Include 'curdttim.sqc'    !Get-Current-DateTime procedure
#Include 'datetime.sqc'    !Routines for date and time formatting
#Include 'datemath.sqc'    !Routines for date and time calculations
#Include 'number.sqc'      !Routines to format numbers
