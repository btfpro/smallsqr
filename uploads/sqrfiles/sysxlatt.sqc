! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
! SYSXLATT: PS Translate Table Edits report                            *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************

#define C_FieldName    3

!----------------------------------------------------------------------!
! Procedure: SYSXLATT-Main                              !
!----------------------------------------------------------------------!

begin-procedure SYSXLATT-Main
    let $ErrorFound = 'N'
    let $AuditTitle = 'XLATT Definition Audit'

    do SYSXLATT-Field-Exist
    do SYSXLATT-XlatFields-WitNoValueDef
    if $ErrorFound = 'N'
        do PrintSummaryRecord
    end-if

end-procedure

!----------------------------------------------------------------------!
! Procedure: SYSXLATT-Field-Exist                                      !
! Descr:     System table audit against PSDBFIELD table:               !
!            XLATT-1: Verify Translate table Field exists.             !
!----------------------------------------------------------------------!
begin-procedure SYSXLATT-Field-Exist
   move 0 to #rows
   let $AuditDescr = '(XLATT-1) Translate table Field does not exist '
      || 'in DBField table:'

#ifdef debugd
    show 'Starting: ' $AuditDescr
    date-time () {PTHMSM} &StrDateTime
    let $StrDateTime = strtodate(&StrDateTime,{PTHMSM})
    show 'StartTime   : ' &StrDateTime
#end-if

   let #rows            =  0
   let $DetailErrFound  = 'N'
   let #Start1          =  1

   let #numofhdr        =  1

   put 'Field Name'            #Start1 31 into printarray(0)

begin-SELECT DISTINCT on-Error=Rpt-SQL-Error
A.FIELDNAME        &XlatT1_FieldName

   if (((#current-line + 1) = #sqr-max-lines) and $DetailErrFound = 'Y') or ($DetailErrFound = 'N')
       move 'Y' to $DetailErrFound
       do PrintSectionHeadings
   end-if
   let #rows = #rows +1

   print &XlatT1_FieldName    (+1,#Start1)

FROM PSXLATDEFN A
WHERE A.FIELDNAME <> 'PSYESNO'
AND NOT EXISTS
  (SELECT 'X'
  FROM PSDBFIELD B
  WHERE B.FIELDNAME = A.FIELDNAME)
end-SELECT

   do End-Section

#ifdef debugd
    date-time () {PTHMSM} &EndDateTime
    show 'EndTime     : ' &EndDateTime
    let $EndDateTime = strtodate(&EndDateTime,{PTHMSM})
    let #ElapsedTime = datediff($EndDateTime, $StrDateTime,'second')
    show 'Elapsed Time: ' #ElapsedTime edit 9999,999.999 ' (seconds)'
    show 'Exiting : ' $AuditDescr
    show ' '
#End-if

end-procedure

#define C_LangCode     3
#define C_FieldName    10
#define C_Count        31

!----------------------------------------------------------------------!
! Procedure: SYSXLATT-XlatFields-WitNoValueDef                         !
! Descr:     System table audit against PSXLATDEFN:                    !
!            XLATT-3: Translate field(s) do not have associated        !
!                     translate values defiend.                        !
!----------------------------------------------------------------------!
begin-procedure SYSXLATT-XlatFields-WitNoValueDef
   move 0 to #rows
   let $AuditDescr = '(XLATT-3) Translate field(s) do not have associated translate values defined: '

#ifdef debugd
    show 'Starting: ' $AuditDescr
    date-time () {PTHMSM} &StrDateTime
    let $StrDateTime = strtodate(&StrDateTime,{PTHMSM})
    show 'StartTime   : ' &StrDateTime
#end-if

   let #rows            =  0
   let $DetailErrFound  = 'N'
   let #Start1          =  1
   let #Start2          =  33

   let #numofhdr        =  2

put 'Record Name'             #Start1 30 into printarray(0)
put 'Field Name'              #Start2 20 into printarray(1)

begin-SELECT on-Error=Rpt-SQL-Error
A.RECNAME           &Xlat3_Recname
A.FIELDNAME         &Xlat3_FieldName

    if (((#current-line + 1) = #sqr-max-lines) and $DetailErrFound = 'Y') or ($DetailErrFound = 'N')
       move 'Y' to $DetailErrFound
       do PrintSectionHeadings
   end-if
   let #rows = #rows +1

   print &Xlat3_Recname        (+1,  #Start1)
   print &Xlat3_FieldName      ( 0,  #Start2)

FROM PSRECFIELD A
WHERE

#ifdef DB2ALL
    (INTEGER(A.USEEDIT/512)) - (INTEGER(A.USEEDIT/1024)*2) <> 0
#else
    #ifdef ORACLE
        (TRUNC(A.USEEDIT/512, 0) - (TRUNC(A.USEEDIT/1024, 0)*2)) <> 0
    #else                                                                   ! MICROSOFT & SYBASE
        (A.USEEDIT/512) - (A.USEEDIT/1024)*2 <> 0
    #endif
#endif
  AND NOT EXISTS
      (SELECT 'X'
         FROM PSXLATDEFN B
        WHERE A.FIELDNAME = B.FIELDNAME)
ORDER BY A.RECNAME,  A.FIELDNAME
end-SELECT

 do End-Section

#ifdef debugd
    date-time () {PTHMSM} &EndDateTime
    show 'EndTime     : ' &EndDateTime
    let $EndDateTime = strtodate(&EndDateTime,{PTHMSM})
    let #ElapsedTime = datediff($EndDateTime, $StrDateTime,'second')
    show 'Elapsed Time: ' #ElapsedTime edit 9999,999.999 ' (seconds)'
    show 'Exiting : ' $AuditDescr
    show ' '
#End-if
end-procedure
