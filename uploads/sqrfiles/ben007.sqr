!*********************************
!  BEN007:   Leave Accruals      *
!*********************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/09/05:23:09:21                                 
!       $Release:  HR9                                                 
!      $Revision:  102                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
  #Include 'ptpsl177.sqc'
end-setup

#define col_sep                  1   !Column Separator
#define col_empl_id_rcd          9   !Employee ID/Rcd
#define col_empl_name           18   !Employee Name
#define col_plan_type            4   !Plan Type
#define col_ben_plan             7   !Benefit Plan
#define col_lst_dt_proc         10   !Lat Date Procesed
#define col_cur_hours_ac        14   !Curr Hours Accured
#define col_hours_carr_ov       14   !YTD Hours Carred Over
#define col_hours_ear_bogh      14   !YTD Hours Earned/Bought
#define col_hours_take_sold     11   !YTD Hours Taken/Sold
#define col_hours_adjust        10   !YTD Hours Adjusted
#define col_bal_hours           20   !Balance Hours
#define col_hourly_rt           16   !Hourly Rate
#define col_bal_amt             15   !Balance Amount


begin-program
  do Init-Report
  do Array-Initialization

  do Process-Main
  do Stdapi-Term
end-program

begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  move '**'             to  $DeptID
  move '**'             to  $BU
  move 'None Selected'  to  $DeptName
  do Stdapi-Init

  move 'BEN007' to $ReportID
  do Get-Report-Language
  let $ReportTitle  = $BEN007-REPORTTITLE
  display  'Report ID: ' noline
  display $ReportTitle

end-procedure

!**********************************
! This procedure is used to read language-dependent text from the database.
!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BEN007 program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN007', 'REPORT_TITLE', $BEN007-REPORTTITLE, #CW)
  do Get_Field_Information ('BEN007', 'GRND_TOT',     $BEN007-GRND-TOT,    #CW)
  do Get_Field_Information ('BEN007', 'COMP_TOT',     $BEN007-COMP-TOT,    #CW)
  do Get_Field_Information ('BEN007', 'SICK',         $BEN007-SICK,        #CW)
  do Get_Field_Information ('BEN007', 'VACATION',     $BEN007-VACATION,    #CW)
  do Get_Field_Information ('BEN007', 'PERSONAL',     $BEN007-PERSONAL,    #CW)
  do Get_Field_Information ('BEN007', 'YTD_HOURS',    $BEN007-YTD-HOURS,   #CW)
  do Get_Field_Information ('BEN007', 'BALANCE',      $BEN007-BALANCE,     #CW)
  do Get_Field_Information ('BEN007', 'HOUR_RATE',    $BEN007-HOUR-RATE,   #CW)
  do Get_Field_Information ('BEN007', 'BAL_AMT',      $BEN007-BAL-AMT,     #CW)
  do Get_Field_Information ('BEN007', 'LAST_DATE',    $BEN007-LAST-DATE,   #CW)
  do Get_Field_Information ('BEN007', 'CUR_HOURS',    $BEN007-CUR-HOURS,   #CW)
  do Get_Field_Information ('BEN007', 'PROCESSED',    $BEN007-PROCESSED,   #CW)
  do Get_Field_Information ('BEN007', 'ACCRUED',      $BEN007-ACCRUED,     #CW)
  do Get_Field_Information ('BEN007', 'CARR_OVER',    $BEN007-CARR-OVER,   #CW)
  do Get_Field_Information ('BEN007', 'EARN_BOGHT',   $BEN007-EARN-BOGHT,  #CW)
  do Get_Field_Information ('BEN007', 'TAKEN_SOLD',   $BEN007-TAKEN-SOLD,  #CW)
  do Get_Field_Information ('BEN007', 'ADJUSTED',     $BEN007-ADJUSTED,    #CW)
  do Get_Field_Information ('BEN007', 'COST_CENTR',   $BEN007-COST-CENTR,  #CW)
  do Get_Field_Information ('BEN007', 'ID_RCD',       $BEN007-ID-RCD,      #CW)
  do Get_Field_Information ('BEN007', 'COST_CTR_TOT', $BEN007-COST-CTR-TOT,#CW)

  do Get_Field_Information ('BEN1',   'EMPLOYEE',     $BEN1-EMPLOYEE,      #CW)
  do Get_Field_Information ('BEN1',   'NAME',         $BEN1-NAME,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,          #CW)
  do Get_Field_Information ('BEN1',   'BENEFIT',      $BEN1-BENEFIT,       #CW)
  do Get_Field_Information ('BEN1',   'HOURS',        $BEN1-HOURS,         #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-TYPE,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN_TYPE',    $BEN1-PLAN-TYPE,     #CW)
  do Get_Field_Information ('BEN1',   'AMT',          $BEN1-AMT,           #CW)

End-Procedure

!*---------------------------------------------------------------------*
begin-procedure Array-Initialization

  create-array name=Totals          -
               size=4               -
               field=TotalName:char -
               field=type:char:3    -
               field=value:number:3 -
               field=hours:number:3

  put $BEN007-GRND-TOT     into Totals(1) TotalName
  put $BEN007-COMP-TOT     into Totals(2) TotalName
  put $BEN007-COST-CTR-TOT into Totals(3) TotalName

  put $BEN007-SICK       into Totals(1) type(0)
  put $BEN007-VACATION   into Totals(1) type(1)
  put $BEN007-PERSONAL   into Totals(1) type(2)
  put $BEN007-SICK       into Totals(2) type(0)
  put $BEN007-VACATION   into Totals(2) type(1)
  put $BEN007-PERSONAL   into Totals(2) type(2)
  put $BEN007-SICK       into Totals(3) type(0)
  put $BEN007-VACATION   into Totals(3) type(1)
  put $BEN007-PERSONAL   into Totals(3) type(2)

end-procedure

!*---------------------------------------------------------------------*
begin-heading 7
#Include 'stdhtr02.sqc'

  print $BEN007-COST-CENTR  (+1,1)
  print $DeptID            (0,13)
  print $DeptName          (0,+2)

!Column Heading line 1
  print $BEN1-EMPLOYEE     (+2,1,{col_empl_id_rcd})
  print $BEN1-EMPLOYEE     (0,+{col_sep},{col_empl_name})
  print $BEN1-PLAN         (0,+{col_sep},{col_plan_type})
  print $BEN1-BENEFIT      (0,+{col_sep},{col_ben_plan})
  print $BEN007-LAST-DATE  (0,+{col_sep},{col_lst_dt_proc})
  print $BEN007-CUR-HOURS  (0,+{col_sep},{col_cur_hours_ac})
  print $BEN1-HOURS        (0,+{col_sep},{col_hours_carr_ov})
  print $BEN007-YTD-HOURS  (0,+{col_sep},{col_hours_ear_bogh})
  print $BEN007-YTD-HOURS  (0,+{col_sep},{col_hours_take_sold})
  print $BEN007-YTD-HOURS  (0,+{col_sep},{col_hours_adjust})
  print $BEN007-BALANCE    (0,+{col_sep},{col_bal_hours})
  print $BEN007-HOUR-RATE  (0,+{col_sep},{col_hourly_rt})
  print $BEN007-BAL-AMT    (0,+{col_sep},{col_bal_amt})

!Column Heading line 2
  print $BEN007-ID-RCD     (+1,1,{col_empl_id_rcd})
  print $BEN1-NAME         (0,+{col_sep},{col_empl_name})
  print $BEN1-TYPE         (0,+{col_sep},{col_plan_type})
  print $BEN1-PLAN         (0,+{col_sep},{col_ben_plan})
  print $BEN007-PROCESSED  (0,+{col_sep},{col_lst_dt_proc})
  print $BEN007-ACCRUED    (0,+{col_sep},{col_cur_hours_ac})
  print $BEN007-CARR-OVER  (0,+{col_sep},{col_hours_carr_ov})
  print $BEN007-EARN-BOGHT (0,+{col_sep},{col_hours_ear_bogh})
  print $BEN007-TAKEN-SOLD (0,+{col_sep},{col_hours_take_sold})
  print $BEN007-ADJUSTED   (0,+{col_sep},{col_hours_adjust})
  print $BEN1-HOURS        (0,+{col_sep},{col_bal_hours})

!Column Heading line 3
  print '='                (+1,1,{col_empl_id_rcd})             Fill
  print '='                (0,+{col_sep},{col_empl_name})       Fill
  print '='                (0,+{col_sep},{col_plan_type})       Fill
  print '='                (0,+{col_sep},{col_ben_plan})        Fill
  print '='                (0,+{col_sep},{col_lst_dt_proc})     Fill
  print '='                (0,+{col_sep},{col_cur_hours_ac})    Fill
  print '='                (0,+{col_sep},{col_hours_carr_ov})   Fill
  print '='                (0,+{col_sep},{col_hours_ear_bogh})  Fill
  print '='                (0,+{col_sep},{col_hours_take_sold}) Fill
  print '='                (0,+{col_sep},{col_hours_adjust})    Fill
  print '='                (0,+{col_sep},{col_bal_hours})       Fill
  print '='                (0,+{col_sep},{col_hourly_rt})       Fill
  print '='                (0,+{col_sep},{col_bal_amt})         Fill
end-heading

!*---------------------------------------------------------------------*
begin-procedure Process-Main

begin-SELECT
A.COMPANY      () on-break print=never level=1 procedure=Company-Change
C.DEPTID       () on-break print=never level=2 procedure=Dept-Change
A.EMPLID,
A.EMPL_RCD,
B.NAME,
A.PLAN_TYPE,
D.BENEFIT_PLAN,
C.HOURLY_RT,
C.BUSINESS_UNIT,
A.ACCRUAL_PROC_DT,
A.HRS_CARRYOVER,
A.HRS_EARNED_YTD,
A.HRS_TAKEN_YTD,
A.HRS_TAKEN_UNPROC,
A.HRS_ADJUST_UNPROC,
A.HRS_BOUGHT_UNPROC,
A.HRS_SOLD_UNPROC,
A.HRS_ADJUST_YTD,
A.HRS_BOUGHT_YTD,
A.HRS_SOLD_YTD

  do Get-Prior-Balance
  do Print-Accrual-Detail
  do Accumulate-Totals

FROM PS_LEAVE_ACCRUAL A,
     PS_PERSON_NAME   B,
     PS_PRIMARY_JOBS  PJ,
     PS_PER_ORG_ASGN  E,
     PS_JOB           C,
     PS_LEAVE_PLAN    D
WHERE A.EMPLID      = B.EMPLID
  AND C.EMPLID      = A.EMPLID
  AND A.ACCRUAL_PROC_DT =
      (SELECT MAX(AX.ACCRUAL_PROC_DT)
        FROM PS_LEAVE_ACCRUAL AX
        WHERE AX.EMPLID    = A.EMPLID
          AND AX.COMPANY   = A.COMPANY
          AND AX.EMPL_RCD = A.EMPL_RCD
          AND AX.PLAN_TYPE = A.PLAN_TYPE)
  AND E.EMPLID = A.EMPLID
  AND E.BENEFIT_RCD_NBR = A.EMPL_RCD
  AND PJ.EMPLID = A.EMPLID
  AND PJ.EMPL_RCD = C.EMPL_RCD
  AND PJ.EMPL_RCD = E.EMPL_RCD
  AND PJ.PRIMARY_JOB_APP = 'BN'
  AND PJ.PRIMARY_JOB_IND = 'Y'
  AND PJ.EFFDT = (SELECT MAX(PJ1.EFFDT)
                    FROM PS_PRIMARY_JOBS PJ1
                   WHERE PJ1.EMPLID = PJ.EMPLID
                     AND PJ1.EMPL_RCD = PJ.EMPL_RCD
                     AND PJ1.PRIMARY_JOB_APP = 'BN'
                     AND PJ1.EFFDT <= A.ACCRUAL_PROC_DT)
  AND C.EMPLID      = A.EMPLID
  AND C.EFFDT =
       (SELECT MAX(EFFDT)
        FROM   PS_JOB
        WHERE  EMPLID  = C.EMPLID
          AND  EMPL_RCD = C.EMPL_RCD
          AND  EFFDT <= A.ACCRUAL_PROC_DT)
  AND  C.EFFSEQ =
       (SELECT MAX(EFFSEQ)
        FROM   PS_JOB
        WHERE  EMPLID  = C.EMPLID
          AND  EMPL_RCD = C.EMPL_RCD
          AND  EFFDT  = C.EFFDT)
  AND D.EMPLID      = A.EMPLID
  AND D.EMPL_RCD   = A.EMPL_RCD
  AND D.PLAN_TYPE   = A.PLAN_TYPE
  AND D.EFFDT       =
       (SELECT MAX(DD.EFFDT)
        FROM   PS_LEAVE_PLAN DD
        WHERE  DD.EMPLID    = D.EMPLID
          AND  DD.EMPL_RCD = D.EMPL_RCD
          AND  DD.PLAN_TYPE = D.PLAN_TYPE
          AND  DD.BENEFIT_NBR = D.BENEFIT_NBR
          AND  DD.EFFDT    <= A.ACCRUAL_PROC_DT)
ORDER BY A.COMPANY,
         C.DEPTID,
         A.EMPLID,
         A.PLAN_TYPE
end-SELECT

  move 1 to #changeLevel
  move 'Y' to $End
  do Print-Totals

end-procedure

!*---------------------------------------------------------------------*
begin-procedure  Get-Prior-Balance

  move 0 to #Prior_Leave_Hours_Bal

begin-SELECT
AA.HRS_CARRYOVER,
AA.HRS_EARNED_YTD,
AA.HRS_TAKEN_YTD,
AA.HRS_TAKEN_UNPROC,
AA.HRS_ADJUST_UNPROC,
AA.HRS_BOUGHT_UNPROC,
AA.HRS_SOLD_UNPROC,
AA.HRS_ADJUST_YTD,
AA.HRS_BOUGHT_YTD,
AA.HRS_SOLD_YTD,
AA.ACCRUAL_PROC_DT

  if not IsNULL(&A.Accrual_Proc_Dt)
    do Format-DateTime(&A.Accrual_Proc_Dt, $Accrual_Proc_Dt, {DEFCMP}, '', '')
    do Get-DateComponents($Accrual_Proc_Dt, #CurrentYear,#CurrentMonth,#CurrentDate)
  end-if

  if not IsNULL(&AA.Accrual_Proc_Dt)
    do Format-DateTime(&AA.Accrual_Proc_Dt, $Prior_Accrual_Proc_Dt, {DEFCMP}, '', '')
    do Get-DateComponents($Prior_Accrual_Proc_Dt, #PriorYear,#PriorMonth,#PriorDate)
  end-if

  if #CurrentYear = #PriorYear
    let #Prior_Leave_Hours_Bal = &AA.Hrs_carryover + &AA.Hrs_earned_ytd
          - &AA.Hrs_taken_ytd - &AA.Hrs_taken_unproc
          + &AA.Hrs_adjust_unproc + &AA.Hrs_adjust_ytd
          - &AA.Hrs_sold_ytd - &AA.Hrs_sold_unproc
          + &AA.Hrs_bought_unproc + &AA.Hrs_bought_ytd
  end-if

FROM PS_LEAVE_ACCRUAL AA
WHERE AA.EMPLID     = &A.EmplID
  AND AA.EMPL_RCD  = &A.EMPL_RCD
  AND AA.PLAN_TYPE  = &A.Plan_Type
  AND AA.COMPANY    = &A.Company
  AND AA.ACCRUAL_PROC_DT =
      (SELECT MAX(ACCRUAL_PROC_DT)
        FROM PS_LEAVE_ACCRUAL
        WHERE EMPLID    = AA.EMPLID
          AND COMPANY   = AA.COMPANY
          AND PLAN_TYPE = AA.PLAN_TYPE
          AND ACCRUAL_PROC_DT <  &A.Accrual_Proc_Dt)
end-SELECT

end-procedure

!*---------------------------------------------------------------------*
begin-procedure Print-Accrual-Detail

  if &A.Company <> $Company
    move &A.Company to $Company
    do Get-Company-Data
  end-if

  if Rtrim(&C.DeptID, ' ')  <> $DeptID
      or &C.BUSINESS_UNIT <> $BU
    let $BU = &C.BUSINESS_UNIT
    let $DeptID = Rtrim(&C.DeptID, ' ')
    do Get-Department-Name
  end-if

  if &A.EmplID  <> $EmplID
    move &A.EmplID  to $EmplID
    move &A.EmplID  to $EmplID_Prt
    move &A.EMPL_RCD to $EmpRcdNo_Prt
    move &B.Name    to $Name_Prt
  else
    move '    '     to $EmplID_Prt
    move '    '     to $EmpRcdNo_Prt
    move '    '     to $Name_Prt
  end-if

  move &A.Plan_Type to $PlanType
  move &D.Benefit_Plan to $Benefit_Plan
  move 'PLAN_TYPE'  to $FieldName
  move $PlanType    to $FieldValue
  do Read-Translate-Table
  move  $XlatLongName  to $Plan_Description

  if not IsNULL(&A.Accrual_Proc_Dt)
    do Format-DateTime(&A.Accrual_Proc_Dt, $Accrual_Proc_Dt, {DEFDATE}, '', '')
  end-if

  if #CurrentYear > #PriorYear
    let #Leave_Hours_Bal = &A.Hrs_earned_ytd
          - &A.Hrs_taken_ytd - &A.Hrs_taken_unproc
          + &A.Hrs_adjust_unproc + &A.Hrs_adjust_ytd
          - &A.Hrs_sold_ytd - &A.Hrs_sold_unproc
          + &A.Hrs_bought_unproc + &A.Hrs_bought_ytd
    let #Cur_Hours_Accrued = #Leave_Hours_Bal
  else
    let #Leave_Hours_Bal = &A.Hrs_Carryover + &A.Hrs_earned_ytd
          - &A.Hrs_taken_ytd - &A.Hrs_taken_unproc
          + &A.Hrs_adjust_unproc + &A.Hrs_adjust_ytd
          - &A.Hrs_sold_ytd - &A.Hrs_sold_unproc
          + &A.Hrs_bought_unproc + &A.Hrs_bought_ytd
    let #Cur_Hours_Accrued = #Leave_Hours_Bal - #Prior_Leave_Hours_Bal
  end-if

  let #Leave_Hours_Value = #Leave_Hours_Bal * &C.Hourly_Rt

  do Format-Number(#Cur_Hours_Accrued, $Cur_Hours_Accrued, '99,999.999999mi')
  do Format-Number(&A.Hrs_Carryover, $Hrs_Carryover, '99,999.999999mi')
  do Format-Number(&A.Hrs_earned_ytd, $Hrs_earned_ytd, '9,999.999999mi')
  do Format-Number(&A.Hrs_taken_ytd, $Hrs_taken_ytd, '9,999.999mi')
  do Format-Number(&A.Hrs_adjust_ytd, $Hrs_adjust_ytd, '9,999.999mi')
  do Format-Number(&A.Hrs_bought_ytd, $Hrs_bought_ytd, '9,999.999mi')
  do Format-Number(&A.Hrs_sold_ytd, $Hrs_sold_ytd, '9,999.999mi')
  do Format-Number(#Leave_Hours_Bal, $Leave_Hours_Bal, '99,999.999999mi')
  do Format-Number(&C.Hourly_Rt, $Hourly_Rt, '999,999,999,999.999999')
  do Format-Number(#Leave_Hours_Value, $Leave_Hours_Value, '999,999,999,999.99mi')

  print $EmplID_Prt         (+2,1,9)
  print $Name_Prt           (0,11,20)
  print $PlanType           (0,31,2)
  print $Benefit_Plan       (0,35,7)
  print $Accrual_Proc_Dt    (0,43,10)

  print $Cur_Hours_Accrued  (0,53,14)
  print $Hrs_Carryover      (0,68,14)
  print $Hrs_earned_ytd     (0,83,13)
  print $Hrs_taken_ytd      (0,97,10)
  print $Hrs_adjust_ytd     (0,108,10)
  print $Leave_Hours_Bal    (0,119,14)
  print $Hourly_Rt          (0,134,22)
  print $Leave_Hours_Value  (0,157,19)
  print $EmpRcdNo_Prt        (+1,1)
  print $Hrs_bought_ytd     (0,83,10)
  print $Hrs_sold_ytd       (0,97,10)

end-procedure

!*---------------------------------------------------------------------*
begin-procedure Accumulate-Totals

  evaluate &A.Plan_Type
    when = '50'
      array-add #Leave_Hours_Value #Leave_Hours_Bal          -
            to Totals(3) value(0)  hours(0)
    when = '51'
      array-add #Leave_Hours_Value #Leave_Hours_Bal           -
            to Totals(3) value(1)  hours(1)
    when = '52'
      array-add #Leave_Hours_Value #Leave_Hours_Bal           -
            to Totals(3) value(2)  hours(2)
  end-evaluate

end-procedure

!*---------------------------------------------------------------------*
begin-procedure Company-Change
  move 2 to #changeLevel
  do Print-Totals
end-procedure

begin-procedure Dept-Change
  move 3 to #changeLevel
  do Print-Totals
end-procedure

!*---------------------------------------------------------------------*
begin-procedure Print-Totals

  move 3 to #totalRow

  while #totalRow >= #changeLevel
    next-listing need=5
    move #totalRow to #rollRow
    subtract 1 from #rollRow
    get $TotalName from Totals(#totalRow) TotalName
    print $TotalName                            (+1,90,20)
    print $BEN1-PLAN-TYPE                       (0,111,15)
    print $BEN1-HOURS                           (0,140,10)
    print $BEN1-AMT                             (0,168,10)
    move 34 to #typeCol
    move 48 to #totatCol
    move 0 to #i

    while #i <= 2
      get $type  from Totals(#totalRow) type(#i)
      print $type (+1, 111,15)

      get #hours from Totals(#totalRow) hours(#i)
      do Format-Number(#hours, $out, '99,999.999999mi')
      print $out (0, 133,14)

      get #value from Totals(#totalRow) value(#i)
      do Format-Number(#value, $out, '99999,999.99mi')
      print $out (0, 163,13)

      array-add #value #hours to Totals(#rollRow) value(#i) hours(#i)
      put 0 0 into Totals(#totalRow) value(#i) hours(#i)
      add 1 to #i
    end-while
    subtract 1 from #totalRow
  end-while

  new-page

  if $End = 'Y'
    do Reset
  end-if
end-procedure

!*---------------------------------------------------------------------*
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getdptnm.sqc'  !Get Department Name from the Department Table
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!*---------------------------------------------------------------------*
