!***********************************************
!  CMP010:  Salary Grade Change for Job Code   *
!***********************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:11:34:00                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
Begin-Setup
#Include 'ptpsl177.sqc'  !Printer and page-size initialization
End-Setup

#define col1  1
#define col1a 12
#define col2  16
#define col3  47
#define col3a 59
#define col4  64
#define col5  78
#define col6  90
#define col7  115
#define col7a 127
#define col8  132
#define col9  146
#define col10 158

#define BUS_UNIT_TBL_HR
#define GB_GROUP_TBL
#define JOBCODE_TBL
#include 'rellang.sqc'
!-----------------------------------------------------------------------
begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

!-----------------------------------------------------------------------
begin-procedure Init-Report
  move 'CMP010' to $ReportID
  do Stdapi-Init
  if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto last1
  end-if

  if $prcs_process_instance <> ''
    do select-parameters
  end-if
do Init_Printer
do Init_Report_translation ($ReportID, $language_cd)
do Append_Report_Translation ('HR')
last1:
end-procedure

!-----------------------------------------------------------------------
begin-heading 10

do Get_Field_Information ('CMP010', 'REPORT_TITLE', $REPORTTITLE, #DW)
do Get_Field_Information ('CMP010', 'EMPLID', $EMPL, #DW)
do Get_Field_Information ('CMP010', 'NAME', $EMPLNAME, #DW)
do Get_Field_Information ('CMP010', 'NEW', $NEW, #DW)
do Get_Field_Information ('CMP010', 'PREV', $PREV, #DW)
do Get_Field_Information ('CMP010', 'EFFDT', $EFFDT, #DW)
do Get_Field_Information ('CMP010', 'SAL_ADMIN_PLAN', $PLAN, #DW)
do Get_Field_Information ('CMP010', 'GRADE', $GRADE, #DW)
do Get_Field_Information ('CMP010', 'STEP', $STEP, #DW)
do Get_Field_Information ('HR', 'JOBCODE', $JOBCD, #DW)
do Get_Field_Information ('HR', 'START_DATE', $START_DATE, #DW)
do Get_Field_Information ('HR', 'END_DATE', $END_DATE, #DW)
do Get_Field_Information ('HR', 'BUS_UNIT', $BUS_UNIT, #DW)
do Get_Field_Information ('HR', 'GROUP_ID', $GROUP_ID, #DW)

#Include 'stdhdg01.sqc'

   STRING $START_DATE $OUT1 BY ': ' INTO $FROM_DT
   STRING $END_DATE $OUT2 BY ': ' INTO $TO_DT
   STRING $FROM_DT $TO_DT BY ' - ' INTO $FROM_TO_DT
   print  $FROM_TO_DT (,) CENTER

   LET $H1 = ' '
   LET $H2 = ' '
   IF $PROCESS_BY = 'G' 
      STRING $GROUP_ID ': ' $GROUP ' ' $GB_GROUP_TBL-DESCR BY '' INTO $H1 
      print $H1 (+1,1)
   ELSE
      IF $PROCESS_BY = 'J'
         LET $H1 = ' ' 
         LET $H2 = ' ' 
         STRING $BUS_UNIT ': ' BY '' INTO $H1
         STRING $BU $BUS_UNIT_TBL_HR-DESCR BY ' ' INTO $H2
         print $H1 (+1,1)
         print $H2 (,20)
         LET $H1 = ' ' 
         LET $H2 = ' ' 
         STRING $JOBCD ': ' BY '' INTO $H1
         STRING $JobCode $Setid $JOBCODE_TBL-DESCR BY ' ' INTO $H2
         print $H1 (+1,1)
         print $H2 (,20)
     END-IF
   END-IF

  print $PREV           (+2,{col3},67)
  print $NEW            (,{col7})
  print $EMPL           (+2,{col1},14)
  print $EMPLNAME       (,{col2},30)
  print $EFFDT          (,{col3},16)
  print $PLAN           (,{col4},13)
  print $GRADE          (,{col5},11)
  print $STEP           (,{col6},24)
  print $EFFDT          (,{col7},16)
  print $PLAN           (,{col8},13)
  print $GRADE          (,{col9},12)
  print $STEP           (,{col10})

end-heading

!-----------------------------------------------------------------------
begin-procedure Get-Values

  let $language_cd = $prcs_language_cd
  let $OPRID = &RUN_CNTL_HR.OPRID
  let $RUN_CNTL_ID = &RUN_CNTL_HR.RUN_CNTL_ID
  let $BU  = RTRIM(&RUN_CNTL_HR.BUSINESS_UNIT, ' ')
  let $JobCode  = RTRIM(&RUN_CNTL_HR.JobCode, ' ')
  let $EffDt    = RTRIM(&RUN_CNTL_HR.Job_Effdt, ' ')

  let $FROMDATE  = RTRIM(&RUN_CNTL_HR.FROMDATE, ' ')
  let $THRUDATE  = RTRIM(&RUN_CNTL_HR.THRUDATE, ' ')
  let $PROCESS_BY  = RTRIM(&RUN_CNTL_HR.PROCESS_BY, ' ')
  let $GROUP  = RTRIM(&RUN_CNTL_HR.GB_GROUP_ID, ' ')
  let $BU  = RTRIM(&RUN_CNTL_HR.BUSINESS_UNIT, ' ')
  let $JOBCD  = RTRIM(&RUN_CNTL_HR.JOBCODE, ' ')

 ! Start Date - End Date
   do Format-DateTime($FROMDATE, $OUT1, {DEFDATE}, '', '')
   do Format-DateTime($THRUDATE, $OUT2, {DEFDATE}, '', '')

 let $AsOfDate = $THRUDATE
 IF $PROCESS_BY = 'G' 
   ! Get Group definition
     let $GB_Group_ID = $GROUP
     let $AsOfDate = $THRUDATE
     do Get-GB-Group-Descr
     LET $GB_GROUP_TBL-DESCR = $Group_Descr
     DO Get_Related_GB_GROUP_TBL($AsOfDate, $GROUP)
 ELSE
     IF $PROCESS_BY = 'J' 
          do Get-Setid  ($BU, 'JOBCODE_TBL', $Setid)
          do Get-Business-Unit-Name
          LET $BUS_UNIT_TBL_HR-DESCR = $BU_DESCR
          DO Get_Related_BUS_UNIT_TBL_HR($BU)
          do Get-JobCode-Descr($Setid, $AsOfDate, $JobCode, $JOBCODE_TBL-DESCR)
          DO Get_Related_JOBCODE_TBL($AsOfDate, $JobCode, $Setid)
     END-IF
 END-IF

end-procedure

!-----------------------------------------------------------------------
begin-procedure Process-Main

begin-SELECT
B.NAME
T.EMPLID
T.EMPL_RCD
T.EFFDT
T.EFFSEQ
T.SAL_ADMIN_PLAN
T.GRADE
T.STEP
T.PRIOR_EFFDT
T.PRIOR_EFFSEQ
T.OLD_PLAN
T.OLD_GRADE
T.OLD_STEP

  do Write-Report

FROM  PS_CMP010_TMP T, PS_PERSON_NAME B
WHERE T.OPRID = $OPRID
AND   T.RUN_CNTL_ID = $RUN_CNTL_ID
AND   T.EMPLID = B.EMPLID
end-SELECT

end-procedure

!-----------------------------------------------------------------------
begin-procedure Get-JobCode-Descr($Setid, $AsOfDate, $JobCode, :$JobDescr)

begin-SELECT
JT.DESCR
  let $JobDescr = rtrim(&JT.Descr,' ')
FROM  PS_JOBCODE_TBL JT
WHERE JT.SETID   = $SetID
  AND JT.JOBCODE = $JobCode
  AND JT.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_JOBCODE_TBL
       WHERE  JOBCODE = $JobCode
         AND  SETID = $SetID
         AND  EFFDT  <= $AsOfDate)
end-SELECT

end-procedure

!-----------------------------------------------------------------------
begin-procedure Write-Report

  print &T.EMPLID                  (+1,{col1})
  print &T.EMPL_RCD               (0,+2) edit 9
  let $Name = substr(&B.NAME,1,30)
  print $Name                      (0,{col2},30)

!previous information
  do Format-DateTime(&T.PRIOR_EFFDT, $out, {DEFDATE}, '', '')
  print $out                       (0,{col3})
  print &T.PRIOR_EFFSEQ            (0,+2) edit 99
  print &T.OLD_PLAN                (0,{col4},13)
  print &T.OLD_GRADE               (0,{col5},11)
  print &T.OLD_STEP                (0,{col6},24)

!new information
  do Format-DateTime(&T.EFFDT, $out, {DEFDATE}, '', '')
  print $out                       (0,{col7})
  print &T.EFFSEQ                  (0,+2) edit 99
  print &T.SAL_ADMIN_PLAN          (0,{col8},13)
  print &T.GRADE                   (0,{col9},11)
  print &T.STEP                      (0,{col10})

end-procedure

!-----------------------------------------------------------------------
#Include 'getgbgrp.sqc'  !Get GB Group Description
#Include 'getsetid.sqc'  !Get Set Id from the Tableset Record Detail
#Include 'getbunam.sqc'  !Get Business Unit Name from the BUS_UNIT_TBL_HR
#Include 'hrrnctl1.sqc'  !Get run control parameter values
#Include 'hrgetval.sqc'  !Get values mask routines
#Include 'reset.sqc'     !Reset printer
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'curdttim.sqc'  !Routines to get current date/time
#Include 'stdapi.sqc'    !Routines to Update Run Status
#include 'hrsecty.sqc'   !Get SQR Security parameters
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
