!***********************************************************************
!  BEN705: Life AD/D Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/03/21:15:00:22                           
!             $Release:  HR9                                           
!            $Revision:  102                                           
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

#define col_sep                  1   !Column Separator
#define col_plan_type            5   !Plan Type
#define col_plan_name           11   !Plan Name
#define col_ben_plan_id          9   !Benefit Plan ID
#define col_ben_plan_name       31   !Benefit Plan Name
#define col_eff_date            11   !Effective Date
#define col_cover               20   !Coverage Type
#define col_formula             10   !Formula ID
#define col_fact_sal            20   !Enrollee Type
#define col_Sum_dep             10   !Sum of Dependent
#define col_grp_code             7   !Group Code

!***********
begin-program
!***********

  do Init-Report
  do Get-Report-Language
!-- 'LIFE AD/D TABLE'
  let $ReportTitle  = $BEN705-REPORTTITLE
  display ' '
  display $ReportTitle
  display ' '

  do Process-Main

  do Reset
  do StdAPI-Term
end-program

!**************************
begin-procedure Init-Report
!**************************

  do Init-DateTime
  do Init-Number
  let $ReportID  = 'BEN705'

  do Get-Current-DateTime
  do StdAPI-Init
end-procedure

!**********************************
Begin-Procedure Report-Translation
!**********************************

  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN705', 'REPORT_TITLE', $BEN705-REPORTTITLE,#CW)
  do Get_Field_Information ('BEN705', 'TYPE',         $BEN705-SALARY,     #CW)
  do Get_Field_Information ('BEN705', 'GROUP',        $BEN705-GROUP,      #CW)
  do Get_Field_Information ('BEN705', 'ENROLLEE',     $BEN705-FACTOR,     #CW)

  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,         #CW)
  do Get_Field_Information ('BEN1',   'BENEFIT',      $BEN1-BENEFIT,      #CW)
  do Get_Field_Information ('BEN1',   'GROUP',        $BEN1-GROUP,        #CW)
  do Get_Field_Information ('BEN1',   'EFFECTIVE',    $BEN1-EFFECTIVE,    #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-TYPE,         #CW)
  do Get_Field_Information ('BEN1',   'NAME',         $BEN1-NAME,         #CW)
  do Get_Field_Information ('BEN1',   'PLAN_ID',      $BEN1-PLAN-ID,      #CW)
  do Get_Field_Information ('BEN1',   'PLAN_NAME',    $BEN1-PLAN-NAME,    #CW)
  do Get_Field_Information ('BEN1',   'DATE',         $BEN1-DATE,         #CW)
  do Get_Field_Information ('BEN1',   'FORMULA',      $BEN1-FORMULA,      #CW)
  do Get_Field_Information ('BEN1',   'ID',           $BEN1-FORMULA-ID,   #CW)
  do Get_Field_Information ('BEN1',   'COVERAGE',     $BEN1-COVERAGE,     #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-COVERAGE-TYPE,  #CW)
  do Get_Field_Information ('BEN1',   'CODE',         $BEN1-CODE,         #CW)
  do Get_Field_Information ('BEN1',   'SUM',          $BEN1-SUM,          #CW)
  do Get_Field_Information ('BEN1',   'DEPENDENT',    $BEN1-DPND,         #CW)

End-Procedure

!**************
begin-HEADING 7
!**************

  #Include 'stdhdgtr.sqc'

!Column Heading line 1
  print $BEN1-PLAN               (+2,1,{col_plan_type})
  print $BEN1-PLAN               (0,+{col_sep},{col_plan_name})
  print $BEN1-BENEFIT            (0,+{col_sep},{col_ben_plan_id})
  print $BEN1-BENEFIT            (0,+{col_sep},{col_ben_plan_name})
  print $BEN1-EFFECTIVE          (0,+{col_sep},{col_eff_date})
  print $BEN1-COVERAGE           (0,+{col_sep},{col_cover})
  print $BEN1-FORMULA            (0,+{col_sep},{col_formula})
  print $BEN705-FACTOR           (0,+{col_sep},{col_fact_sal})
  print $BEN1-SUM                (0,+{col_sep},{col_Sum_dep})
  print $BEN705-GROUP            (0,+{col_sep},{col_grp_code})

!Column Heading line 2
  print $BEN1-TYPE               (+1,1,{col_plan_type})
  print $BEN1-NAME               (0,+{col_sep},{col_plan_name})
  print $BEN1-PLAN-ID            (0,+{col_sep},{col_ben_plan_id})
  print $BEN1-PLAN-NAME          (0,+{col_sep},{col_ben_plan_name})
  print $BEN1-DATE               (0,+{col_sep},{col_eff_date})
  print $BEN1-COVERAGE-TYPE      (0,+{col_sep},{col_cover})
  print $BEN1-FORMULA-ID         (0,+{col_sep},{col_formula})
  print $BEN705-SALARY           (0,+{col_sep},{col_fact_sal})
  print $BEN1-DPND               (0,+{col_sep},{col_Sum_dep})
  print $BEN1-CODE               (0,+{col_sep},{col_grp_code})

!Column Heading line 3
  print '='             (+1,1,{col_plan_type})             FILL
  print '='             (0,+{col_sep},{col_plan_name})     FILL
  print '='             (0,+{col_sep},{col_ben_plan_id})   FILL
  print '='             (0,+{col_sep},{col_ben_plan_name}) FILL
  print '='             (0,+{col_sep},{col_eff_date})      FILL
  print '='             (0,+{col_sep},{col_cover})         FILL
  print '='             (0,+{col_sep},{col_formula})       FILL
  print '='             (0,+{col_sep},{col_fact_sal})      FILL
  print '='             (0,+{col_sep},{col_Sum_dep})       FILL
  print '='             (0,+{col_sep},{col_grp_code})      FILL

end-HEADING

!***********************************************************************
!  PROC Process Main
!***********************************************************************

BEGIN-PROCEDURE Process-Main

BEGIN-SELECT

PLAN_TYPE                           (+1,1,4)
  move 'PLAN_TYPE' to $FieldName
  move &plan_type   to $FieldValue
  do Read-Translate-Table
  print $XlatShortName              (0,+2,10)
BENEFIT_PLAN                        (0,+2,8)
  move &benefit_plan to $benefit_plan
  move &plan_type    to $plan_type
  do Get-Benefit-Name
  print $BenefitName                (0,+2,30)
EFFDT  &EffDt
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out                        (0,+2,10)
COVERAGE_TYPE
   move 'COVERAGE_TYPE' to $FieldName
   move &COVERAGE_TYPE  to $FieldValue
   do Read-Translate-Table
   print $XlatLongName              (0,+2,19)
BN_FORMULA_ID
   print &BN_FORMULA_ID             (0,+2,10)
ENROLLE_TYPE
   move 'ENROLLE_TYPE' to $FieldName
   move &enrolle_type  to $FieldValue
   do Read-Translate-Table
   print $XlatLongName              (0,+1,20)
SUM_DEP_COVG
   print &sum_dep_covg              (0,+4,5)
COVG_GROUP_CD                       (0,+3,5)

FROM PS_LIFE_ADD_TBL
ORDER BY PLAN_TYPE, BENEFIT_PLAN, EFFDT DESC

END-SELECT

END-PROCEDURE

!*****************************************************************
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'getbennm.sqc'  !Get-Benefit-Name procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!*****************************************************************
