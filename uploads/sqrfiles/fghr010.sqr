!***********************************************************************
!  FGHR010: Vacant Budgeted Position Report                           *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/10/09:22:02:29                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************
! FGHR010 - Vacant budgeted Position Report                          *
!***********************************************************************
! 6/14/01
!  > Changed JOB_REQ_STATUS field to STATUS_CODE and created another 
!     procudure to look up descr from PS_ER_STS_REQ_VW
!  > Changed a few prints statements to make more room for longer status_code descrs
! 08/31/2006
! Picked the SQR from 88 and included the SQR in Clearcase as this was missing (1549959001)
!************************************************************************

#Include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

begin-Report
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Init-Report
   !do Process-Main
   do Reset
   do Stdapi-Term
end-Report

!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from FGHR010 program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation ('HR')

  do Get_Field_Information ('FGHR010', 'REPORT_TITLE',   $FGHR010-REPORTTITLE,    #CW)
  do Get_Field_Information ('FGHR010', 'FPC',            $FGHR010-FPC,            #CW)
  do Get_Field_Information ('HR',      'POSITION_STAT1', $POSITION_STAT1,         #CW)
  do Get_Field_Information ('FGHR010', 'RTUAF',          $FGHR010-RTUAF,          #CW)
  do Get_Field_Information ('FGHR010', 'REQN',           $FGHR010-REQN,           #CW)
  do Get_Field_Information ('FGHR010', 'DEPT',           $FGHR010-DEPT,           #CW)
  do Get_Field_Information ('HR',      'JOBCODE1',       $JOBCODE1,               #CW)
  do Get_Field_Information ('FGHR010', 'EMLRD',          $FGHR010-EMLRD,          #CW)
  do Get_Field_Information ('HR',      'STATUS',         $STATUS,                 #CW)
  do Get_Field_Information ('FGHR010', 'VACANCY',        $FGHR010-VACANCY,        #CW)
  do Get_Field_Information ('HR',      'RECRUIT',        $RECRUIT,                #CW)
  do Get_Field_Information ('FGHR010', 'ID',             $FGHR010-ID,             #CW)
  do Get_Field_Information ('HR',      'JOBCODE2',       $JOBCODE2,               #CW)
  do Get_Field_Information ('HR',      'POSITION_NBR2',  $POSITION_NBR2,          #CW)
  do Get_Field_Information ('HR',      'POSITION_TITLE', $POSITION_TITLE,         #CW)
  do Get_Field_Information ('FGHR010', 'GPLTL',          $FGHR010-GPLTL,          #CW)
  do Get_Field_Information ('HR',      'EFFDT2',         $EFFDT2,                 #CW)
  do Get_Field_Information ('FGHR010', 'ANN',            $FGHR010-ANN,            #CW)
  do Get_Field_Information ('HR',      'RECRUIT2',       $RECRUIT2,               #CW)
  do Get_Field_Information ('FGHR010', 'CLOSES',         $FGHR010-CLOSES,         #CW)

End-Procedure


begin-heading 8
#Include 'stdhdgtr.sqc'
   print $FGHR010-FPC      (+1,61)
   print $FGHR010-RTUAF    (+1,61)
   print $POSITION_STAT1   (0,81)
   print $FGHR010-REQN     (0,100)

   print $FGHR010-DEPT     (+1,1)
   print $JOBCODE1         (0,13)
   print $POSITION_STAT1   (0,21)
   print $FGHR010-EMLRD    (0,61)

   print $POSITION_STAT1   (0,72)
   print $POSITION_STAT2   (0,81)
   print $FGHR010_VACANCY  (0,92)
   print $POSITION_STAT2   (0,100)
   print $FGHR010-VACANCY  (0,111)
   print $RECRUIT1         (0,120)
   print $RECRUIT1         (0,135)

   print $FGHR010-ID       (+1,1)
   print $JOBCODE2         (0,13)
   print $POSITION_NBR2    (0,21)
   print $POSITION_TITLE   (0,30)
   print $FGHR010-GPLTL    (0,61)
   print $STATUS           (0,72)
   print $EFFDT2           (0,81)
   print $FGHR010-ANN      (0,93)
   print $EFFDT2           (0,100)
   print $STATUS           (0,112)
   print $RECRUIT2         (0,124)
   print $FGHR010-CLOSES   (0,135)

end-heading

!********************************************
begin-procedure Init-Report

  do Stdapi-Init

  let $ReportID = 'FGHR010'
  do Get-Report-Language
  let $ReportTitle = $FGHR010-REPORTTITLE
  display $ReportTitle

  if $prcs_process_instance = ''
     do Ask-Vacant-Pos-Request
  else
     do Select-Parameters
     do process-main
  end-if
end-procedure



!***************************************************
!   PROC Process-Main                              *
!               Main Procedure                     *
!***************************************************
begin-procedure Process-Main
begin-SELECT
A.DEPTID                 (+1,1)  on-break level=1 skiplines=1
A.JOBCODE                (0,13)
A.POSITION_NBR           (0,21)
A.DESCR                  (0,30)
A.REG_TEMP               (0,61)
A.FULL_PART_TIME         (0,65)
A.DESCRLONG
A.CONFIDENTIAL_POSN
   if &A.Confidential_Posn = 'Y'
     print 'Y'           (0,69)
   end-if
A.POSN_STATUS
  move 'POSN_STATUS' to $FieldName
  move &A.Posn_Status  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName   (0,72)
A.STATUS_DT
   if RTRIM(&A.Status_Dt, ' ') <> ''
     do Format-DateTime(&A.Status_Dt, $out, {DEFDATE}, '', '')
     print $out          (0,81)
   end-if

  do Get-Requisition

FROM  PS_POSN_VACANT A
ORDER BY A.DEPTID,
         A.JOBCODE,
         A.POSITION_NBR
end-SELECT
end-procedure

begin-procedure Get-Requisition
begin-SELECT
J.JOB_REQ_NBR     (0,92)
J.STATUS_CODE
   DO GET-STATUS-CODE-DESCR
   print $status-code-defn (0,112)
J.STATUS_DT
   do Format-DateTime(&J.Status_Dt, $out, {DEFDATE}, '', '')
   print $out          (0,100)
J2.GVT_DT_ANN_OPENED
J2.GVT_DT_LAST_SUBMIT


  if RTRIM(&J2.GVT_DT_ANN_OPENED, ' ') <> ''
      do Format-DateTime(&J2.GVT_DT_ANN_OPENED, $out, {DEFDATE}, '', '')
      print $out  (0,124)
  end-if
  if RTRIM(&J2.GVT_DT_LAST_SUBMIT, ' ') <> ''
      do Format-DateTime(&J2.GVT_DT_LAST_SUBMIT, $out, {DEFDATE}, '', '')
      print $out  (0,135)
  end-if

FROM PS_JOB_REQUIS_TAO J,
     PS_GVT_RQSN_POSN J1,
     PS_HRS_G_JOPST_HST J2
WHERE J1.POSITION_NBR = &A.Position_Nbr
  AND J1.JOB_REQ_NBR = J.JOB_REQ_NBR
  end-SELECT

end-procedure

BEGIN-PROCEDURE GET-STATUS-CODE-DESCR

LET $status-code-defn = ''

BEGIN-SELECT
REQ_VW.DESCRSHORT
   let $status-code-defn = &REQ_VW.DESCRSHORT
FROM  PS_HRS_STS_JO_VW REQ_VW
WHERE STATUS_AREA = '1'
AND STATUS_CODE = &J.STATUS_CODE
END-SELECT
END-PROCEDURE



!**********************************************************************

#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines

#Include 'askvacnt.sqc'  !Get Vacant Report Request
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Format date and time
#Include 'number.sqc'    !Routines to format numbers
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Routines to Update Run Status

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
