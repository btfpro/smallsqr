!***********************************************************************
!  TAX962PH:  Philadelphia Local W-2 Audit Report                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:07:34:36                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Get-Transmitter
  do Select-Reports
  do Stdapi-Term
end-report

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Initialization
end-procedure

begin-procedure Initialization
  display ''
  display 'Philadelphia Local W-2 Audit Report '
  display ' '
  move 'TAX962PH' to $ReportID
  do Get-Current-DateTime

  If $Prcs_Process_Instance = ''
    Display 'Report cannot be run outside of PeopleSoft, please use process scheduler.'
    Stop
  End-if

  do Select-US-Year-End-Parameters
  do Convert-Parameters


  move 'Y' to $Local-Process

  move '' to $ErName
end-procedure

begin-procedure Select-Reports


  let $FileIn = 'TAX960' || 'PH'
  let $ReportTitle = ' W-2 Audit Report - Philadelphia Local '
  display 'Processing Philadelphia' noline
  display ' '

  if $FileType = 'D'
    do Open-Diskette
  else
      do Open-File
  end-if
  if #filestat = 0
    if $FileType = 'D'
      do Read-Diskette-File
    else
      do Read-Tape-File
    end-if
  end-if
  if $TotalsOnly <> 'N'
    if $SRecordFound = 'Y'
      if $TRecordFound <> 'Y'
        do Print-Employer-Totals
      end-if
    end-if

    if $SRecordFound = 'Y'
      if $FRecordFound <> 'Y'
        do Print-File-Totals
      end-if
    end-if
  else
    do Print-Employer-Totals
    do Print-File-Totals
    new-page
  end-if
end-procedure


begin-procedure Open-Diskette
  move 1 to #FileExtension
   move #FileExtension to $FileExtension 099
#ifdef OS400
   let $FileExtension = '(D' || $FileExtension || ')'
   let $FileID = '{IMPORTPREFIX}' || $FileIn || $FileExtension
#else
   let $FileID = '{IMPORTPREFIX}' || $FileIn || '.' || $FileExtension
#endif
  open $FileID as 1 for-reading record=512:fixed
    status=#filestat
  if #filestat != 0
    display $FileID noline
    display ' not found '
    display ' '
  end-if
end-procedure

begin-procedure Open-File
  let $FileID = '{IMPORTPREFIX}' || $FileIn || '{IMPORTSUFFIX}'
  open $FileID as 1 for-reading record=512
    status=#filestat
  if #filestat != 0
    display $FileID noline
    display ' not found '
    display ' '
  end-if
end-procedure

begin-procedure Process-RA-Record
  !do Print-RA-Record
  let $SRecordFound = 'N'
  let $TRecordFound = 'N'
  let $FRecordFound = 'N'
end-procedure


begin-procedure Process-RW-Record
  do Print-RW-Record
end-procedure

begin-procedure Process-RE-Record
  if $ErName <> ''
    if $TotalsOnly = 'N'
      new-page
      move ' ' to $Col1Hdr1
      move ' ' to $Col2Hdr1
      move ' ' to $Col3Hdr1
      move ' ' to $Col4Hdr1
      move ' ' to $Col5Hdr1
      move ' ' to $Col6Hdr1
      move ' ' to $Col1Hdr2
      move ' ' to $Col2Hdr2
      move ' ' to $Col3Hdr2
      move ' ' to $Col4Hdr2
      move ' ' to $Col5Hdr2
      move ' ' to $Col6Hdr2
    end-if
  end-if

  next-listing  NEED = 24
  do Print-RE-Record

  if $TotalsOnly = 'N'
    move $ErName              to $Employer
    new-page
    move 'Last'             to $Col1Hdr1
    move 'Name'             to $Col1Hdr2
    move 'First'            to $Col2Hdr1
    move 'Name'             to $Col2Hdr2
    move 'Middle'           to $Col3Hdr1
    move 'Name'             to $Col3Hdr2
    move 'SSN'              to $Col4Hdr2
    move '              '   to $Col5Hdr1
    move '   Local Wages'   to $Col5Hdr2
    move '              '   to $Col6Hdr1
    move '   Local Taxes'   to $Col6Hdr2
  end-if
end-procedure

begin-procedure Process-RS-Record
  let $SRecordFound = 'Y'
  do Print-RS-Record
end-procedure

begin-procedure Process-RO-Record
  do Print-RO-Record
end-procedure

begin-procedure Process-Employer-Record
  do Print-RE-Record
end-procedure

begin-procedure Process-RF-Record
  let $FRecordFound = 'Y'
  do Print-RF-Record
end-procedure

begin-procedure Process-RU-Record
  do Print-RU-Record
end-procedure

begin-procedure Process-RT-Record
  let $TRecordFound = 'Y'
  do Print-RT-Record
end-procedure

begin-procedure Print-Employer-Totals

  move 'Y' to $TRecord
  print 'Employer Totals - Wages: ' (+2,1)
  print  #T_LclWages (0,26) edit 999,999,999.99   !Local Taxable Wages

  print 'Taxes: ' (+1,19)
  print  #T_LclTax   (0,26) edit 999,999,999.99   !Local Taxes

  add #T_LclWages to #F_LclWages
  add #T_LclTax   to #F_LclTax

  move 0 to #T_LclWages
  move 0 to #T_LclTax
end-procedure

begin-procedure Print-File-Totals
  print 'File Totals - Wages: ' (+2,1)
  print  #F_LclWages  (0,22) edit 999,999,999.99   !Local Taxable Wages
  print 'Taxes: ' (+1,15)
  print  #F_LclTax  (0,22) edit 999,999,999.99     !Local Taxes
  move 0 to #F_LclWages
  move 0 to #F_LclTax
  move '' to $ErName
end-procedure

begin-procedure Get-Transmitter
begin-SELECT
TX.COMPANY, TX.BALANCE_YEAR

FROM PS_TAXRPT_RUNCTL TX
end-SELECT

begin-SELECT
X.DESCR

FROM  PS_W2_COMPANY X
WHERE COMPANY = &TX.Company
  AND X.CALENDAR_YEAR = &TX.Balance_Year
end-SELECT
  move &X.Descr to $TrName
end-procedure


begin-procedure Convert-Parameters

  move $RC_US_YE.Totals_Only to $TotalsOnly

Begin-Select

RC960.REPORTING_MEDIUM
RC960.DISKETTE_TYPE
RC960.EMPLR_PIN
RC960.RESUB_INDICATOR
RC960.RESUB_TLCN
RC960.CONTACT_NAME_SSA
RC960.TELEPHONE_NBR
RC960.PHONE_EXTN
RC960.EMAIL_ADDR_MMREF
RC960.CONTACT_FAX
RC960.NOTIFY_METHOD

FROM PS_TAX960_PARM RC960
End-Select

  if rtrim(&RC960.CONTACT_NAME_SSA, ' ') = ''
    Show 'MMREF Parameters must be set up on SETUP Menu.'
    Show 'Re-execute after establishing MMREF Parameters.'
    Stop
  End-if

  move &RC960.EMAIL_ADDR_MMREF to $RC960.EMAIL_ADDR
  uppercase $RC960.EMAIL_ADDR
  move &RC960.Reporting_Medium to $FileType
end-procedure


#Include 'hdg962.sqc'    !Report and Column Headings
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'rdtap962.sqc'  !Read state mag tape file
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl2.sqc'  !US Year End Process Scheduler Run Controls
