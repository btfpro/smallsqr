!***********************************************************************
!  HRS034:  Position Status Report                                   *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2006/09/12:22:30:19                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#include 'setup32.sqc'   !Printer and page-size initialization

!begin-setup
! declare-variable
! date $asofdate
! end-declare
!end-setup

begin-report
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Init-Report
   do Process-Main
   do Reset
   do Stdapi-Term
end-report

begin-heading 8
#Include 'stdhtr03.sqc'

  do Get_Field_Information ('HRS034', 'RTC',            $HRS034-RTC,            #CW)
  do Get_Field_Information ('HR',      'POSITION_STAT1', $POSSTAT1,               #CW)
  do Get_Field_Information ('HR',      'EFFDT',          $EFFDT1,                 #CW)
  do Get_Field_Information ('HRS034', 'EMO',            $HRS034-EMO,            #CW)
  do Get_Field_Information ('HR',      'POSITION_NBR1',  $POSNBR1,                #CW)
  do Get_Field_Information ('HR',      'STATUS',         $STATUS,                 #CW)
  do Get_Field_Information ('HR',      'RECRUIT1',       $RECRUIT1,               #CW)
  do Get_Field_Information ('HR',      'INTERNAL',       $INT,                    #CW)
  do Get_Field_Information ('HR',      'EXTERNAL',       $EXT,                    #CW)
  do Get_Field_Information ('HR',      'DEPTID',         $DEPTID,                 #CW)
  do Get_Field_Information ('HR',      'JOBCODE',        $JOBCODE,                #CW)
  do Get_Field_Information ('HR',      'POSITION_NBR2',  $POSNBR2,                #CW)
  do Get_Field_Information ('HR',      'EFFDT2',         $EFFDT2,                 #CW)
  do Get_Field_Information ('HR',      'POSITION_TITLE', $POSTITLE,               #CW)
  do Get_Field_Information ('HRS034', 'GPN',            $HRS034-GPN,            #CW)
  do Get_Field_Information ('HR',      'POSITION_STAT2', $POSSTAT2,               #CW)
  do Get_Field_Information ('HR',      'RECRUIT2',       $RECRUIT2,               #CW)
  do Get_Field_Information ('HR',      'POSTING',        $POSTING,                #CW)
  do Get_Field_Information ('HRS034', 'EXPIRATN_DT',    $HRS034-EXPIRATN,       #CW)
  do Get_Field_Information ('HRS034', 'INCUMBENT',      $HRS034-INCUMBENT,      #CW)

   print $HRS034-RTC        (+1,71)
   print $POSSTAT1           (+1,20)
   print $EFFDT1             (0,30)
   print $HRS034-EMO        (0,71)
   print $POSNBR1            (0,79)
   print $STATUS             (0,90)
   print $RECRUIT1           (0,100)
   print $INT                (0,113)
   print $EXT                (0,136)
   print $DEPTID             (+1,1)
   print $JOBCODE            (0,12)
   print $POSNBR2            (0,23)
   print $EFFDT2             (0,32)
   print $POSTITLE           (0,47)
   print $HRS034-GPN        (0,71)
   print $POSSTAT2           (0,80)
   print $EFFDT2             (0,91)
   print $RECRUIT2           (0,101)
   print $POSTING            (0,111)
   print $EXPIRATN           (0,122)
   print $POSTING            (0,134)
   print $HRS034-EXPIRATN   (0,145)
   print $HRS034-INCUMBENT  (0,157)
end-heading

!********************************************
begin-procedure Init-Report

  move 'HRS034' to $ReportID
  do Stdapi-Init

  do Sqr-Param

 ! do Stdapi-Init

  if $prcs_process_instance = ''
    do Ask-As-Of-Date
  else
    do Select-Parameters
    do Get-As-Of-Date
  end-if

  let $language_cd = $PRCS_LANGUAGE_CD

  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

  do Get_Field_Information ('HRS034', 'REPORT_TITLE',   $HRS034-REPORTTITLE,    #CW)
  display $HRS034-REPORTTITLE
end-procedure


!********************************************
begin-procedure Process-Main
begin-SELECT
A.DEPTID                  (+1,1) on-break level=1 skiplines=1
A.JOBCODE                 (0,12)
A.POSITION_NBR            (0,20)
A.EFFDT
    do Format-DateTime(&A.EffDt,  $out,{DEFDATE}, '', '')
    print $out             (0,29)
B.DESCR
A.DESCR
   if RTRIM(&A.Descr, ' ') = ''
     print &B.Descr       (0,40)
   else
     print &A.Descr       (0,40)
   end-if
A.REG_TEMP                (0,72)
A.CONFIDENTIAL_POSN
   if &A.Confidential_Posn = 'Y'
     print 'Y'            (0,76)
   end-if
A.POSN_STATUS
   move 'POSN_STATUS' to $FieldName
   move &A.Posn_Status  to $FieldValue
   do Read-Translate-Table
   print $XlatShortName   (0,79)
A.STATUS_DT
   if RTRIM(&A.Status_Dt, ' ') <> ''
     do Format-DateTime(&A.Status_Dt, $out, {DEFDATE}, '', '')
     print $out                (0,88)
     !print &A.Status_Dt                (0,88)
   end-if
   do Get-Int-Requisition
   do Get-Ext-Requisition

   if &A.Posn_Status <> 'I'  !Inactive
     do Get-Incumbent
   end-if
FROM  PS_POSITION_DATA A,
      PS_JOBCODE_TBL B
WHERE B.JOBCODE = A.JOBCODE
  and A.EFF_STATUS = 'A'
  and A.EFFDT = (SELECT MAX(A1.EFFDT)
                from PS_POSITION_DATA A1
                where A1.POSITION_NBR = A.POSITION_NBR
                and A1.EFFDT <= $AsOfDate)
  and B.EFFDT = (SELECT MAX(B1.EFFDT)
                from PS_JOBCODE_TBL B1
                where B1.JOBCODE = B.JOBCODE
                and B1.EFFDT <= $AsOfDate)
ORDER BY A.DEPTID, A.JOBCODE, A.POSITION_NBR
end-SELECT
end-procedure

begin-procedure Get-Int-Requisition
begin-SELECT DISTINCT
J.OPEN_DT,
J3.HRS_JO_PST_OPN_DT
J3.HRS_JO_PST_CLS_DT
   if RTRIM(&J.Open_Dt, ' ') <> ' '
     do Format-DateTime(&J.Open_Dt, $out, {DEFDATE}, ' ', ' ')
     print $out   (0,99)
   end-if
   if RTRIM(&J3.HRS_JO_PST_OPN_DT, ' ') <> ' '
     do Format-DateTime(&J3.HRS_JO_PST_OPN_DT, $out, {DEFDATE}, ' ', ' ')
     print $out   (0,110)
   end-if
   if RTRIM(&J3.HRS_JO_PST_CLS_DT, ' ') <> ' '
     do Format-DateTime(&J3.HRS_JO_PST_CLS_DT, $out, {DEFDATE}, ' ', ' ')
     print $out   (0,121)
   end-if
FROM  PS_HRS_JO_I J,
      PS_HRS_JO_PSTDST_I J3,
      PS_HRS_JO_PST_I J4,
      PS_HRS_JO_POSN_I  J1,
      PS_HRS_JO_RQMT_I J2
WHERE J1.POSITION_NBR = &A.Position_Nbr
  AND J1.HRS_JOB_OPENING_ID = J.HRS_JOB_OPENING_ID
  AND J2.HRS_JOB_OPENING_ID = J.HRS_JOB_OPENING_ID
  AND J4.HRS_JOB_OPENING_ID = J.HRS_JOB_OPENING_ID
  AND J3.HRS_JOB_OPENING_ID = J4.HRS_JOB_OPENING_ID
  AND J3.HRS_JO_PST_SEQ = J4.HRS_JO_PST_SEQ
  AND J3.HRS_JO_PST_TYPE = 'I'
  AND J4.GVT_VA_NBR <> ' '
  AND J2.JOBCODE  = &A.JobCode
  AND J.OPEN_DT <= $AsOfDate
  AND (J.CLOSE_DT >= $AsOfDate OR J.CLOSE_DT IS NULL)
end-SELECT
end-procedure

begin-procedure Get-Ext-Requisition
begin-SELECT DISTINCT
K.OPEN_DT,
K3.HRS_JO_PST_OPN_DT
K3.HRS_JO_PST_CLS_DT
   if RTRIM(&K.Open_Dt, ' ') <> ' '
     do Format-DateTime(&K.Open_Dt, $out, {DEFDATE}, ' ', ' ')
     print $out   (0,99)
   end-if
   if RTRIM(&K3.HRS_JO_PST_OPN_DT, ' ') <> ' '
     do Format-DateTime(&K3.HRS_JO_PST_OPN_DT, $out, {DEFDATE}, ' ', ' ')
     print $out   (0,133)
   end-if
   if RTRIM(&K3.HRS_JO_PST_CLS_DT, ' ') <> ' '
     do Format-DateTime(&K3.HRS_JO_PST_CLS_DT, $out, {DEFDATE}, ' ', ' ')
     print $out   (0,144)
   end-if
FROM  PS_HRS_JO_I K,
      PS_HRS_JO_PSTDST_I K3,
      PS_HRS_JO_PST_I K4,
      PS_HRS_JO_POSN_I  K1,
      PS_HRS_JO_RQMT_I K2
WHERE K1.POSITION_NBR = &A.Position_Nbr
  AND K1.HRS_JOB_OPENING_ID = K.HRS_JOB_OPENING_ID
  AND K2.HRS_JOB_OPENING_ID = K.HRS_JOB_OPENING_ID
  AND K4.HRS_JOB_OPENING_ID = K.HRS_JOB_OPENING_ID
  AND K3.HRS_JOB_OPENING_ID = K4.HRS_JOB_OPENING_ID
  AND K3.HRS_JO_PST_SEQ = K4.HRS_JO_PST_SEQ
  AND K3.HRS_JO_PST_TYPE = 'E'
  AND K4.GVT_VA_NBR <> ' '
  AND K2.JOBCODE  = &A.JobCode
  AND K.OPEN_DT <= $AsOfDate
  AND (K.CLOSE_DT >= $AsOfDate OR K.CLOSE_DT IS NULL)
end-SELECT
end-procedure

begin-procedure Get-Incumbent
let #firstIncumbent = 0
begin-SELECT
  if #firstIncumbent <> 0
    print ' ' (+1,155)
  end-if
   let #firstIncumbent = 1
!*************************************Begin Name Change *******************
!L.NAME (0,155,20)
L.NAME_DISPLAY (0,155,20)
!*************************************End Name Change *******************
M.EMPLID
FROM  PS_PERSON_NAME L,
      PS_EMPLOYMENT N,
      PS_JOB M
WHERE M.POSITION_NBR = &A.Position_Nbr      !Uncomment if Posn# is used
  AND N.EMPLID = L.EMPLID
  AND M.EMPLID = N.EMPLID
  AND M.EMPL_RCD = N.EMPL_RCD
  AND M.EMPL_STATUS IN ('A','P','L','S')
  AND M.EFFDT = (SELECT MAX(C.EFFDT)
                   from PS_JOB C
                  where C.EMPLID = M.EMPLID
                    and C.EMPL_RCD = M.EMPL_RCD
                    and C.EFFDT <= $asOfDate)
  AND M.EFFSEQ =(SELECT MAX(D.EFFSEQ)
                   from PS_JOB D
                  where D.EMPLID = M.EMPLID
                    and D.EMPL_RCD = M.EMPL_RCD
                    and D.EFFDT = M.EFFDT)
end-SELECT
end-procedure

!****************************************************************


#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines
#include 'askaod.sqc'    !Ask As Of Date input

#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status

#Include 'hrsecty.sqc'   !SQR security
#Include 'sqrtrans.sqc'  !Routines to translate SQR strings
