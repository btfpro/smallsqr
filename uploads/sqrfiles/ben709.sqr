!***********************************************************************
!  BEN709: Benefit Plan Table                                          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:08:47:42                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

#define col_sep                  1   !Column Separator
#define col_plan_type            4   !Plan Type
#define col_plan_name           10   !Plan Name
#define col_ben_plan             8   !Benefit Plan
#define col_eff_date            11   !Eff Date
#define col_name                31   !Name
#define col_short_name          11   !Short Name
#define col_setid                9   !SetID
#define col_provdr_id           11   !Provider ID (Vendor ID)
#define col_provdr_name         25   !Provider Name
#define col_deflt_ded           10   !Default Deduction
#define col_ded_name            20   !Deduction Name
#define col_incl_in_ndt          7   !Include In NDT?
#define col_min_spousal         12   !Min Spousal
#define col_allocation          11   !Allocation

begin-program

  do Init-Report
  do Process-Main
  do Reset
  do StdAPI-Term

end-program

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number

  do Get-Current-DateTime
  do StdAPI-Init

  let $ReportID = 'BEN709'
  do Get-Report-Language
  let $ReportTitle  = $BEN709-REPORTTITLE
  display ' '
  display $ReportTitle
  display ' '

end-procedure


!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BEN709 program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN709', 'REPORT_TITLE', $BEN709-REPORTTITLE, #CW)
  do Get_Field_Information ('BEN709', 'IN_NDT',       $BEN709-IN-NDT,      #CW)
  do Get_Field_Information ('BEN709', 'SHORT',        $BEN709-SHORT,       #CW)
  do Get_Field_Information ('BEN709', 'INCLUDE',      $BEN709-INCLUDE,     #CW)
  do Get_Field_Information ('BEN709', 'MIN_SPOUSAL',  $BEN709-MIN-SPOUSAL, #CW)
  do Get_Field_Information ('BEN709', 'ALLOCATION',   $BEN709-ALLOCATION,  #CW)

  do Get_Field_Information ('BEN1',   'EFFECTIVE',    $BEN1-EFFECTIVE,     #CW)
  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN_TYPE',    $BEN1-PLAN-TYPE,     #CW)
  do Get_Field_Information ('BEN1',   'BENEFIT',      $BEN1-BENEFIT,       #CW)
  do Get_Field_Information ('BEN1',   'BEN_PLAN',     $BEN1-BEN-PLAN,      #CW)
  do Get_Field_Information ('BEN1',   'BEN_PLAN3',    $BEN1-BEN-PLAN3,     #CW)
  do Get_Field_Information ('BEN1',   'DATE',         $BEN1-DATE,          #CW)
  do Get_Field_Information ('BEN1',   'SETID',        $BEN1-SETID,         #CW)
  do Get_Field_Information ('BEN1',   'VENDOR',       $BEN1-VENDOR,        #CW)
  do Get_Field_Information ('BEN1',   'DEFAULT',      $BEN1-DEFAULT,       #CW)
  do Get_Field_Information ('BEN1',   'DEDCTON',      $BEN1-DEDCTON,       #CW)
  do Get_Field_Information ('BEN1',   'ID',           $BEN1-ID,            #CW)
  do Get_Field_Information ('BEN1',   'NAME',         $BEN1-NAME,          #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-TYPE,          #CW)

End-Procedure

!***************
begin-HEADING 8
!***************
  #Include 'stdhdgtr.sqc'

!Column Heading line 1
  print $BEN1-PLAN                      (+2,1,{col_plan_type})
  print $BEN1-PLAN-TYPE                 (0,+{col_sep},{col_plan_name})
  print $BEN1-BENEFIT                   (0,+{col_sep},{col_ben_plan})
  print $BEN1-EFFECTIVE                 (0,+{col_sep},{col_eff_date})
  print $BEN1-BEN-PLAN                  (0,+{col_sep},{col_name})
  print $BEN1-BEN-PLAN3                 (0,+{col_sep},{col_short_name})
  print $BEN1-VENDOR                    (0,+{col_sep},{col_setid})
  print ' '                             (0,+{col_sep},{col_provdr_id})
  print ' '                             (0,+{col_sep},{col_provdr_name})
  print $BEN1-DEFAULT                   (0,+{col_sep},{col_deflt_ded})
  print $BEN1-DEDCTON                   (0,+{col_sep},{col_ded_name})
  print $BEN709-INCLUDE                 (0,+{col_sep},{col_incl_in_ndt})
  print $BEN709-MIN-SPOUSAL             (0,+{col_sep},{col_min_spousal})

!Column Heading line 2
  print $BEN1-TYPE                      (+1,1,{col_plan_type})
  print $BEN1-NAME                      (0,+{col_sep},{col_plan_name})
  print $BEN1-PLAN                      (0,+{col_sep},{col_ben_plan})
  print $BEN1-DATE                      (0,+{col_sep},{col_eff_date})
  print $BEN1-NAME                      (0,+{col_sep},{col_name})
  print $BEN709-SHORT                   (0,+{col_sep},{col_short_name})
  print $BEN1-SETID                     (0,+{col_sep},{col_setid})
  print $BEN1-VENDOR                    (0,+{col_sep},{col_provdr_id})
  print $BEN1-NAME                      (0,+{col_sep},{col_provdr_name})
  print $BEN1-DEDCTON                   (0,+{col_sep},{col_deflt_ded})
  print $BEN1-NAME                      (0,+{col_sep},{col_ded_name})
  print $BEN709-IN-NDT                  (0,+{col_sep},{col_incl_in_ndt})
  print $BEN709-ALLOCATION              (0,+{col_sep},{col_allocation})

!Column Heading line 3
  print '='                          (+1,1,{col_plan_type})            FILL
  print '='                          (0,+{col_sep},{col_plan_name})    FILL
  print '='                          (0,+{col_sep},{col_ben_plan})     FILL
  print '='                          (0,+{col_sep},{col_eff_date})     FILL
  print '='                          (0,+{col_sep},{col_name})         FILL
  print '='                          (0,+{col_sep},{col_short_name})   FILL
  print '='                          (0,+{col_sep},{col_setid})        FILL
  print '='                          (0,+{col_sep},{col_provdr_id})    FILL
  print '='                          (0,+{col_sep},{col_provdr_name})  FILL
  print '='                          (0,+{col_sep},{col_deflt_ded})    FILL
  print '='                          (0,+{col_sep},{col_ded_name})     FILL
  print '='                          (0,+{col_sep},{col_incl_in_ndt})  FILL
  print '='                          (0,+{col_sep},{col_allocation})   FILL

end-HEADING

!***********************************************************************
!  PROC Process Main
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main

  let #SS_OffSet = {col_plan_type} + (2 * {col_sep})
  let #SS_Label = {col_plan_name} + {col_ben_plan} + {col_eff_date} + (3 * {col_sep})
  let #SS_Offset2 = #SS_OffSet + #SS_Label
  let #SS_Wrap = {col_name}+{col_short_name}+{col_setid}+{col_provdr_id}+{col_provdr_name}+ -
                 {col_deflt_ded}+{col_ded_name}+{col_incl_in_ndt}+{col_min_spousal}+(7*{col_sep})

BEGIN-SELECT

PLAN_TYPE                       (+1,1,3)
  move 'PLAN_TYPE' to $FieldName
  move &plan_type   to $FieldValue
  do Read-Translate-Table
  print $XlatShortName          (0,+2,9)
BENEFIT_PLAN                    (0,+2,7)
EFFDT  &EffDt
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out                    (0,+2,10)
DESCR
  move &DESCR to $DESCRLANG
DESCRSHORT
  move &DESCRSHORT to $SHORTLANG
  if $curr_language_cd <> $PSOptions_language_cd
     do Get-Benef-Plan-Lang
  end-if
  PRINT $DESCRLANG              (0,+2,30)
  PRINT $SHORTLANG              (0,+2,10)
SETID                           (0,+2,8)
  move &SETID to $SetID
VENDOR_ID                       (0,+2,10)
  move &VENDOR_ID to $provider
  do Get-Provider-Name
  print $ProviderName           (0,+2,24)
DEDCD                           (0,+2,9)
  move &PLAN_TYPE to $PlanType
  move &DEDCD     to $DedCd
  if $dedcd = ''
     let $dedcd = ' '
  end-if
  do Get-Deduction-Name
  print $DeductionName          (0,+2,20)
INCLUDE_IN_NDT                  (0,+2,6)
MIN_BENEFIC_ALLOC
  do Format-Number(&MIN_BENEFIC_ALLOC, $out, '999.99')
  print $out                    (0,+2,11)
DESCRLONG
  if NOT IsBlank(&DESCRLONG)
     print $BEN709-DESCRLONG    (+2,#SS_OffSet,#SS_Label)
     print &DESCRLONG           (0,#SS_OffSet2,#SS_Wrap) WRAP #SS_Wrap 6
     print ''                   (+1,1)
  end-if

FROM PS_BENEF_PLAN_TBL
ORDER BY PLAN_TYPE, BENEFIT_PLAN, EFFDT

END-SELECT
END-PROCEDURE

!***************************************
Begin-Procedure Get-Benef-Plan-Lang
!***************************************
Begin-Select

K.DESCR
  if Rtrim(&K.DESCR, ' ') <> ''
     let $DESCRLANG = Rtrim(&K.DESCR, ' ')
  end-if
K.DESCRSHORT
  if Rtrim(&K.DESCRSHORT, ' ') <> ''
     let $SHORTLANG =  Rtrim(&K.DESCRSHORT, ' ')
  end-if

FROM PS_BENEF_PLAN_LANG K
WHERE K.PLAN_TYPE    =  &PLAN_TYPE
AND   K.BENEFIT_PLAN =  &BENEFIT_PLAN
AND   K.LANGUAGE_CD  =  $Curr_Language_Cd
AND   K.EFFDT <= &EFFDT
AND   K.EFFDT = (SELECT MAX(EFFDT)
                 FROM  PS_BENEF_PLAN_LANG
                 WHERE PLAN_TYPE    = K.PLAN_TYPE
                  AND  BENEFIT_PLAN = K.BENEFIT_PLAN
                  AND  EFFDT       <= &EFFDT)
END-select
END-procedure

!***********************************************************************
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'getprvnm.sqc'  !Get-Provider-Name procedure
#Include 'getdednm.sqc'  !Get-Deduction-Name procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!************************ End Of Report ********************************
