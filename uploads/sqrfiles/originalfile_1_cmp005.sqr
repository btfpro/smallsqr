!************************************
!  CMP005:  Above-Maximum Analysis  *
!************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:11:32:18                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!Porcedures ending with "Scrty" and Record alias ending with "SC" are
!using SQR Seurity.
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
Begin-Setup
#Include 'ptpsl177.sqc'  !Printer and page-size initialization
End-Setup

#define JOBCODE_TBL

#include 'rellang.sqc'   ! Translations File

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
 if $prcs_oprid=''
  goto last2
 end-if

  do Process-Main

  do Reset
  do Stdapi-Term
last2:
end-report

begin-heading 7

  do Get_Field_Information ('CMP005',   'REPORT_TITLE', $REPORTTITLE,   #DW)
  #Include 'stdhtr03.sqc'
  do Get_Field_Information ('CMP005',   'MAXIMUM',      $MAX,           #DW)
  do Get_Field_Information ('CMP005',   'AMOUNT',       $AMOUNT,        #DW)
  do Get_Field_Information ('CMP005',   'PERCENT_TYPE1',$PERCENT1,      #DW)
  do Get_Field_Information ('CMP005',   'PERCENT_TYPE2',$PERCENT2,      #DW)
  do Get_Field_Information ('HR',       'ANNUAL_RT',    $ANNUAL,        #DW)
  do Get_Field_Information ('HR',       'GRADE1',       $GRADE1,        #DW)
  do Get_Field_Information ('HR',       'GRADE2',       $GRADE2,        #DW)
  do Get_Field_Information ('HR',       'JOBCODE1',     $JOB1,          #DW)
  do Get_Field_Information ('HR',       'JOBCODE2',     $JOB2,          #DW)
  do Get_Field_Information ('HR',       'JOBTITLE',     $JOBTITLE,      #DW)
  do Get_Field_Information ('HR',       'EMPL_NAME',    $NAME,          #DW)
  do Get_Field_Information ('HR',       'DEPTID',       $DEPTID,        #DW)
  do Get_Field_Information ('HR',       'CONV_TO_BASE', $CONVBASE,      #DW)

  do Get_Field_Information ('HR',       'BUS_UNIT1X',        $BUS_UNIT1X,         #DW)
  do Get_Field_Information ('HR',       'BUS_UNIT2',        $BUS_UNIT2,         #DW)

  if #MultiCurr
    print $CONVBASE   (3,72,42)
    print $ReportCurr (0,115)
    print '*'         (7,21)
    print '*'         (7,129)
    print '*'         (7,155)

  end-if
  PRINT   $BUS_UNIT1X             (6,1,6)
  print $GRADE1                                   (6,8,29)
  print $JOB1                                     (0,38,132)
  print $PERCENT1                                 (0,171)

  PRINT   $BUS_UNIT2              (7,1,6)
  print $GRADE2                                   (0,8,13)
  print $MAX                                      (0,22,15)
  print $JOB2                                     (0,38,7)
  print $JOBTITLE                                 (0,46,33)
  print $NAME                                     (0,80,31)
  print $DEPTID                                   (0,112,18)
  print $ANNUAL                                   (0,130,25)
  print $AMOUNT                                   (0,156,14)
  print $PERCENT2                                 (0,171)

end-heading

begin-procedure Init-Report

  move 'CMP005' to $ReportID
  move $BaseCurr to $ReportCurr
  move $BaseRtType to $ReportRtType
  move 'W' to $ReportMsgType  !Warning

  do Stdapi-Init
  if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto last1
  end-if

  do Security-Param
  if $prcs_process_instance = ''
    do Ask-As-Of-Date
  else
    do Select-Parameters
  end-if
do Init_Printer
do Init_Report_Translation ($ReportID, $language_cd)
do Append_Report_Translation ('HR')
last1:
end-procedure

begin-procedure Get-Values

   let $language_cd = $prcs_language_cd
   do Get-As-Of-Date

end-procedure

begin-procedure Process-Main

if $language_cd = 'JPN'
          let $qualifier = 'A.NAME_AC'
      else
           let $qualifier = 'A.NAME'
     end-if

begin-SELECT

C.BUSINESS_UNIT
C.SAL_ADMIN_PLAN
C.GRADE
C.EFFDT
C.JOBCODE
D.SETID
D.EFFDT
D.DESCR
A.NAME
A.NAME_AC
C.DEPTID
C.CURRENCY_CD &PersCur
C.ANNUAL_RT

  if #MultiCurr and &PersCur <> $ReportCurr
    do ConvertCurrency(&C.Annual_Rt,&PersCur,$ReportCurr,
                       $ReportRtType,&C.EFFDT,#AROut,$ReportMsgType)
  else
    move &C.Annual_Rt to #AROut
  end-if

  if &C.Sal_Admin_Plan <> $SalAdminPlan or &C.Grade <> $Grade
    move &C.Sal_Admin_Plan to $SalAdminPlan
    move &C.Grade          to $Grade
    do Get-Salary-Grade-Data
    if #MultiCurr and &Sal_Grade_Cur <> $ReportCurr And
       Rtrim(&Sal_Grade_Cur, ' ') <> ''
      do ConvertCurrency(&SG.Max_Rt_Annual,&Sal_Grade_Cur,$ReportCurr,
                         $ReportRtType,&C.EFFDT,#MROut,$ReportMsgType)
    else
      move &SG.Max_Rt_Annual to #MROut
    end-if
  end-if

  if #ARout > #MRout
    print &C.BUSINESS_UNIT             (+1,1,6)
    print &C.Sal_Admin_Plan            (,8)  edit xxx/
    print &C.Grade                     ()

    do Format-Number(#MRout, $out, '999,999,999,999,999')
    print $out                         (0,17)

    print &C.JobCode                   (0,38,7)
    LET $JOBCODE_TBL-DESCR = RTRIM(&D.DESCR,' ')
    do Get_Related_JOBCODE_TBL(&D.EFFDT,&C.JOBCODE,&D.SETID)
    print $JOBCODE_TBL-DESCR           (0,46,33)
    print &A.Name                      (0,80,31)
    print &C.DeptID                    (0,112,5)

    do Format-Number(#ARout, $out, '999,999,999,999,999.999')
    print $out                         (0,118)

    move #ARout to #field
    subtract #MRout from #field
    do Format-Number(#field, $out, '999,999,999,999,999.999')
    print $out                         (0,145)

    if #MRout <> 0
      divide #MRout into #field
    else
      move 9.9999 to #field
    end-if
    multiply 100 times #field
    do Format-Number(#field, $out, '999.99')
    print $out                         (0,171)
  end-if

FROM  PS_PERSON_NAME A,
      PS_PERSON E,
      PS_EMPLOYMENT B,
      PS_JOB C,
      PS_JOBCODE_TBL D,
      PS_FAST_SQR_SEC_VW SCRTY
WHERE C.PER_ORG = 'EMP'
  AND C.HR_STATUS = 'A'                            !HP99999
  AND A.EMPLID = B.EMPLID
  AND E.EMPLID = B.EMPLID
  AND C.EMPLID = B.EMPLID
  AND C.EMPL_RCD = B.EMPL_RCD
  AND C.EFFDT =
      (SELECT MAX(H.EFFDT)
       FROM PS_JOB H
       WHERE H.EMPLID = C.EMPLID
         AND H.EMPL_RCD = C.EMPL_RCD
         AND H.EFFDT <= $AsofDate)
  AND C.EFFSEQ =
      (SELECT MAX(I.EFFSEQ)
       FROM PS_JOB I
       WHERE I.EMPLID = C.EMPLID
         AND I.EMPL_RCD = C.EMPL_RCD
         AND I.EFFDT = C.EFFDT)
  AND D.JOBCODE = C.JOBCODE
  AND D.SETID = C.SETID_JOBCODE
  AND D.EFFDT =
      (SELECT MAX(J.EFFDT)
       FROM PS_JOBCODE_TBL J
       WHERE J.JOBCODE = D.JOBCODE
         AND J.SETID = D.SETID
         AND J.EFFDT <= C.EFFDT)

[$SecurityClause]
AND A.EMPLID = SCRTY.EMPLID
AND B.EMPL_RCD = SCRTY.EMPL_RCD
ORDER BY C.BUSINESS_UNIT, C.SAL_ADMIN_PLAN, C.GRADE DESC, C.JOBCODE ASC,[$qualifier] ASC

end-SELECT

end-procedure

#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askaod.sqc'    !Ask As Of Date input
#include 'hrsecty.sqc'   !Get SQR Security parameters

#Include 'getslgrd.sqc'  !Get-Salary-Grade-Data procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to update run status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
