!***********************************************************************
!  TAX002AL:  Qtrly State Employee Wage List - ALABAMA                *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/05/09:16:42:21                           !
!             $Release:  HR9                                           !
!            $Revision:  102                                           !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Get USA variables

begin-setup

#include 'prt002.sqc'  !Printer and page-size initialization

end-setup


begin-report
  do Init-Report

  do Process-Main

  if #Employees_On_Page > 0
     move 'Y' to $Co-End
     do Page-Complete
  end-if

  do Stdapi-Term
end-report

begin-procedure Init-Report
  display 'Quarterly Employee Wage List - Alabama'

  do Init-DateTime
  do Init-Number

  do Get-Tax-Reporting-Run-Controls

  move $PeriodEndDate to $AsOfDate  !for included procedures

  do Initialize

  do Stdapi-Init

  do Get-Calendar-Year-Id

end-procedure

begin-procedure Initialize
#ifdef TAXTEST
  move 'CA' to $State
#else
  move 'AL' to $State
#endif
  move 'N' to $Co-End
end-procedure

begin-heading 7
  print 'PAGE'                    (+2,34)
  do Format-Number(#PAGE_NUM, $out, '999')
  print $out                      (0,40)
  uppercase $RptQtrYr
  print $RptQtrYr                 (0,55)
  uppercase $CompanyName
  print $CompanyName              (+2,10)
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    print &Employer_ID_SUT        (0,40)
  else
    print &Employer_ID_SWT        (0,40)
  end-if
  print 'SSN'                     (+2,8)
  print 'NAME'                    (0,25)
  print 'TOTAL WAGES'             (0,56)
end-heading

begin-procedure Process-Main

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
C.COMPANY         () on-break print=never level=1 before=Company-Change
C.STATE
C.EMPLID          () on-break print=never level=2 after=EmplID-Change
C.TAX_CLASS
C.LOCALITY
C.NLGRS_QTD

D1.NATIONAL_ID    () on-break print=never level=2 save=$SSN
PN.NAME           () on-break print=never level=2 save=$Name

  move &C.Company to $Company
  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_NAMES PN, PS_PERS_NID D1, PS_COMPANY_TBL A
WHERE C.EMPLID             = D1.EMPLID
  AND D1.COUNTRY           = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE  = $PAYROLL_NID_TYPE
  AND PN.EMPLID            = C.EMPLID
#ifdef TAXTEST
  AND C.STATE              = 'CA'
#else
  AND C.STATE              = 'AL'
#endif
  AND C.LOCALITY           = ' '
  AND C.TAX_CLASS          = 'U'
  AND C.BALANCE_ID         = $Calendar_Year_Id
  AND C.BALANCE_YEAR       = &TX.Balance_Year
  AND C.BALANCE_QTR        = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD     =
      (SELECT MAX(C1.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C1
       WHERE  C1.EMPLID       = C.EMPLID
         AND  C1.COMPANY      = C.COMPANY
         AND C1.BALANCE_ID    = C.BALANCE_ID
         AND C1.BALANCE_YEAR  = C.BALANCE_YEAR
         AND C1.BALANCE_QTR   = C.BALANCE_QTR
         AND  C1.STATE        = C.STATE
         AND  C1.LOCALITY     = C.LOCALITY
         AND  C1.TAX_CLASS    = C.TAX_CLASS)
  AND C.NLGRS_QTD > 0
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
  AND PN.NAME_TYPE = 'PRI'
  AND PN.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PN.EMPLID
         AND  PX.NAME_TYPE = PN.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfDate)
ORDER BY C.COMPANY, D1.NATIONAL_ID, C.STATE, C.LOCALITY, C.TAX_CLASS
end-SELECT
end-procedure

begin-procedure Process-Tax-Balance
  add &C.NLGrs_QTD to #NLGrs_QTD
  move &C.EmplID to $EmplID
end-procedure

begin-procedure EmplID-Change
  add 1 to #Employees_On_Page

  if #Employees_On_Page > 20
     do Page-Complete
     move 1 to #Employees_On_Page
  end-if

  move $SSN to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM($SSN, ' ') = ''
     print 'Applied For' (+2,8)
  else
     print $SSN          (+2,8)  edit xxxbxxbxxxx
  end-if

  uppercase $Name
  print $Name           (0,25,20)

  do Format-Number (#NLGrs_QTD, $NLGrs_QTD, '99999999.99')
  print $NLGrs_QTD      (0,56)

  move ''        to $NLGrs_QTD
  add #NLGrs_QTD to #PageTotal
  move 0         to #NLGrs_QTD
end-procedure

begin-procedure Page-Complete
  print 'THIS PAGE TOTAL' (+2,38)
  do Format-Number (#PageTotal, $out, '99999999.99')
  print $out              (0,56)
  add #PageTotal to #CoTotal

  if $Co-End = 'Y'
     do End-Company
  end-if

  add 1 to #PAGE_NUM
  new-page
  move 0 to #PageTotal
end-procedure

begin-procedure Company-Change
  if #Employees_On_Page > 0
     move 'Y' to $Co-End
     do Page-Complete
     move 0 to #Employees_On_Page
  end-if

  move 0 to #PAGE_NUM
  move &C.Company to $Company
  do Get-Company-Data
  do Get-State-Tax-Data             !Employer ID
end-procedure

begin-procedure End-Company
  print 'ALL PAGES TOTAL' (+2,38)
  do Format-Number(#CoTotal, $out, '999999999.99')
  print $out              (0,55)
  move 0 to #CoTotal
  move 'N' to $Co-End
end-procedure


#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'stdapi.sqc'    !Update Process API
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
