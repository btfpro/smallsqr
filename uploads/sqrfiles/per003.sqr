!******************************************
!  PER003:  Years of Service              *
!******************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/08/06:00:28:47                                 
!       $Release:  HR9                                                 
!    $Resolution:  689848                                              
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
begin-setup
#Include 'ptset02.sqc'   !Printer and page-size initialization
end-setup

!***********************************************************************
begin-report
  do Init-DateTime
  do Init-Number
  !do Get-Current-DateTime
  Move 1 to $Year4
  do Init-Report
  if $prcs_oprid=''
     goto last2
  end-if

  do Process-Main

  do Reset
  do Stdapi-Term
last2:
end-report
!***********************************************************************


!***********************************************************************
begin-heading 7
do Get_Field_Information ('PER003',     'REPORT_TITLE',         $REPORTTITLE,   #DW)
  #Include 'stdhtr03.sqc'

do Get_Field_Information ('PER003',     'SERVICE_NAME',         $SERVICE,       #DW)
do Get_Field_Information ('PER003',     'YRS_SERVICE1',         $YRS_SERVICE1,  #DW)
do Get_Field_Information ('PER003',     'MONTHCD',              $MONTH,         #DW)
do Get_Field_Information ('HR',         'EMPL_NAME',            $NAME,          #DW)
do Get_Field_Information ('HR',         'HIRE_DT',              $HIRE,          #DW)
do Get_Field_Information ('HR',         'DEPARTMENT',           $DEPT,          #DW)
do Get_Field_Information ('HR',         'JOBTITLE',             $JOBTITLE,      #DW)
do Get_Field_Information ('HR',         'EMPL_RCD',             $EMPL,          #DW)        ! 09/00
do Get_Field_Information ('HR',         'EMPLID',               $EMPLID,        #DW)

  if $EMPL=''                     ! 09/00
    let $EMPL = 'Rcd#'            ! 09/00
  end-if                          ! 09/00

  let $title = $REPORTTITLE || ':'

  print $TITLE                                          (3,1)
  print #Service_Years                                  (3,+1) edit 999

  PRINT $SERVICE                                        (+2,58)
  PRINT $EMPLID                                         (+1,6,11)
  PRINT $NAME                                           (0,18,35)
  PRINT $EMPL                                           (0,55,4)
  PRINT $HIRE                                           (0,60,10)
  PRINT $YRS_SERVICE1                                   (0,72,4)
  PRINT $MONTH                                          (0,77,5)
  PRINT $DEPT                                           (0,84,30)
  PRINT $JOBTITLE                                       (0,115)
end-heading
!***********************************************************************


!***********************************************************************
begin-procedure Init-Report

  Let $_TableAlias = 'B'
  move 'PER003' to $ReportID
  do Delete-Worktable
  do Stdapi-Init
  if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto last1
  end-if

  do Security-Param

  if $prcs_process_instance = ''
    do Ask-As-Of-Date
    do Ask-Years-Of-Service
  else
    do Select-Parameters
  end-if
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')
last1:
end-procedure
!***********************************************************************

!***********************************************************************
begin-procedure Get-Values

   let $language_cd = $PRCS_LANGUAGE_CD
   do Get-As-Of-Date
   do Get-Years-Of-Service

end-procedure
!***********************************************************************

!***********************************************************************
begin-procedure Process-Main

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($AsOfDate, $AsOf_YMD, {DEFYMD}, '', '')
  do Delete-Worktable        ! Done before as well in case the pgm terminated abnormally ...
  do Data-Selection
  do Create-Report
  do Delete-Worktable

end-procedure
!***********************************************************************

!***********************************************************************
begin-procedure Data-Selection
begin-SELECT
A.NAME
A.EMPLID
B.HIRE_DT
B.EMPL_RCD              ! 09/00
B.TERMINATION_DT        ! 09/00
C.BUSINESS_UNIT
C.DEPTID
C.JOBCODE
C.SETID_JOBCODE

  if not IsNULL(&B.Hire_Dt)
    move '1' to $Year4
    move '-' to $DDelimiter
    do Format-DateTime(&B.Hire_Dt, $Hire_YMD, {DEFYMD}, '', '')
    do Dtu-Diff-Months($Hire_YMD, $AsOf_YMD, #Months)

    Let #Years = trunc((#Months / 12),0)
    if #Years >= #Service_Years
      let #Months = #Months - (#Years * 12)
      do Dtu-Add-Years($Hire_YMD, #Years, $Hire_Y)
      do Dtu-Add-Months($Hire_Y, #Months, $Hire_M)
      do Dtu-Diff-Days($Hire_M, $AsOf_YMD, #Days)
      if #Days < 0 
       if #Months = 0
           let #Months = 12
           let #Years = #Years - 1
        end-if
      end-if
      let #Months_Fraction = (#Days)/30
      let #Months = round((#Months + #Months_Fraction),1)
      if #Months = 12
         let #Months = 11.9
      end-if
      do Insert-Worktable
    end-if
  end-if

FROM  PS_PERSON_NAME A,
      PS_EMPLOYMENT B,
      PS_JOB C,
      PS_PERSON D
WHERE C.HR_STATUS = 'A'                            !HP99999
  AND A.EMPLID = B.EMPLID
  AND B.EMPLID = C.EMPLID
      AND B.EMPL_RCD = C.EMPL_RCD
  AND C.EFFDT =
      (SELECT MAX(H.EFFDT)
       FROM PS_JOB H
       WHERE H.EMPLID = C.EMPLID
         AND H.EMPL_RCD = C.EMPL_RCD
         AND H.EFFDT <= $AsofDate)
  AND C.EFFSEQ =
      (SELECT MAX(I.EFFSEQ)
       FROM PS_JOB I
       WHERE I.EMPLID = C.EMPLID
         AND I.EMPL_RCD = C.EMPL_RCD
         AND I.EFFDT = C.EFFDT)
  AND D.EMPLID = A.EMPLID
  AND C.PER_ORG = 'EMP'
[$SecurityClausewithERN]

end-SELECT
end-procedure
!***********************************************************************

!***********************************************************************
begin-procedure Insert-Worktable
let $sql-statement = 'PER003.SQR,Insert-Worktable,Insert,PS_R_PER003'
let $termination-dt = &B.TERMINATION_DT
begin-SQL On-Error=SQL-Error
INSERT INTO PS_R_PER003 (SERVICE_YEARS,SERVICE_MONTHS,NAME,EMPLID,HIRE_DT,BUSINESS_UNIT,DEPTID,JOBCODE, SETID_JOBCODE, EMPL_RCD, TERMINATION_DT)   ! 09/00
       VALUES (#years,#months,&A.Name,&A.EmplID,&B.Hire_Dt,&C.BUSINESS_UNIT,&C.DeptID,&C.JobCode, &C.SETID_JOBCODE, &B.EMPL_RCD, $termination-dt)  ! 09/00
end-SQL
end-procedure
!***********************************************************************

!***********************************************************************
begin-procedure Delete-Worktable
let $sql-statement = 'PER003.SQR,Delete-Worktable,Delete,PS_R_PER003'
begin-SQL On-Error=SQL-Error
DELETE FROM PS_R_PER003;
end-SQL
end-procedure
!***********************************************************************

!***********************************************************************
begin-procedure Create-Report
begin-SELECT
R.EMPLID                        (+1,6,11)
R.NAME                          (0,18,35)
R.EMPL_RCD                      (0,55,4)                          ! 09/00
R.HIRE_DT
R.SERVICE_YEARS
R.SERVICE_MONTHS
R.BUSINESS_UNIT
R.DEPTID
R.JOBCODE
R.SETID_JOBCODE
  Move 1 to $Year4
  do Format-DateTime(&R.Hire_Dt, $out, {DEFDATE}, '', '')
  print $out                    (0,60,10)

  do Format-Number(&R.Service_Years, $out, '99')
  print $out                    (0,72)

  do Format-Number(&R.Service_Months, $out, '99.9')
  print $out                    (0,77)

  LET $BU = RTRIM(&R.BUSINESS_UNIT,' ')
  move &R.DeptID to $DeptID
  do Get-Department-Name
  print $DeptName               (0,84,30)

  move &R.SETID_JOBCODE to $setid
  move &R.JobCode to $Jobcode
  do Get-Job-Title
  print $JobTitle               (0,115)

FROM PS_R_PER003 R
ORDER BY 5 DESC, 6 DESC, 1 ASC, 2 ASC                          ! 09/00
end-SELECT
end-procedure
!***********************************************************************


#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askaod.sqc'    !Ask As Of Date input
#include 'asksrvyr.sqc'  !Years Of Service input
#include 'hrsecty.sqc'   !Get SQR Security parameters
#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'getjobtl.sqc'  !Get-Job-Title procedure
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
