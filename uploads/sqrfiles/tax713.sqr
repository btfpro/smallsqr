!***********************************************************************
!  TAX713: Print Local Work-Work Tax Reciprocity Data                  *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:31:50                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'TAX713' to $ReportID
  move 'Local Work-Work Reciprocity Rules' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Get-Current-DateTime
  do Stdapi-Init
end-procedure

begin-HEADING 7
  #Include 'stdhdg01.sqc'

  print 'Work     '                     (+2,2,9)
  print 'Work     '                     (0,+3,10)
  print 'Locality'                      (0,+3,35)
  print 'Other'                         (0,+3,8)
  print 'Other'                         (0,+3,10)
  print 'Locality'                      (0,+3,35)
  print 'Eff'                           (0,+3)
  print 'Reciprocity'                   (0,137,30)
  print 'Reduce'                        (0,169,7)

  print 'State'                         (+1,2,9)
  print 'Locality'                      (0,+3,10)
  print 'Name'                          (0,+3,35)
  print 'State'                         (0,+3,8)
  print 'Locality'                      (0,+3,10)
  print 'Name'                          (0,+3,35)
  print 'Date'                          (0,+3)
  print 'Rule'                          (0,137,30)
  print 'Percent'                       (0,169,7)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

A.STATE_WORK                       (+1,2,9)
A.LOCALITY_WORK                    (0,+3,10)
  if RTRIM(&A.Locality_Work, ' ') <> ''
        and RTRIM(&A.Locality_Work, ' ') <> 'STATEREC'
    let $state = rtrim(&A.State_Work, ' ')
    move &A.Locality_Work to $locality
    do Get-Locality-Name
    print &C.Locality_Name              (0,+3,35)
  else
    print ' '                           (0,+3,35)
  end-if
A.OTHER_STATE                           (0,+3,8)
A.OTHER_LOCALITY                        (0,+3,10)
  if RTRIM(&A.Other_Locality, ' ') <> ''
        and RTRIM(&A.OTHER_LOCALITY, ' ') <> 'STATEREC'
    let $state = rtrim(&A.Other_State, ' ')
    move &A.Other_Locality to $locality
    do Get-Locality-Name
    print &C.Locality_Name              (0,+3,35)
  else
    print ' '                           (0,+3,35)
  end-if

A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                       (0,+2)

A.EFF_STATUS
  if &A.Eff_Status = 'I'
    print 'I'                           (0,0,1)
  else
    print ' '                           (0,0,1)
  end-if
A.RECIPROCITY_RULE
  move 'RECIPROCITY_RULE' to $FieldName
  move &A.Reciprocity_Rule  to $FieldValue
  do Read-Translate-Table
  print $XlatLongName                   (0,137,30)
A.REDUCE_PCT                            (0,169,7)   EDIT 'b9.999'

FROM PS_LCLWK_TXRCP_TBL A
ORDER BY A.STATE_WORK, A.LOCALITY_WORK,
         A.OTHER_STATE, A.OTHER_LOCALITY, A.EFFDT DESC

END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Get-Locality-Name
BEGIN-SELECT

C.LOCALITY_NAME

FROM PS_LOCAL_TAX_TBL C
WHERE C.STATE            = $state
  AND C.LOCALITY         = $locality
  AND C.EFFDT            =
      ( SELECT MAX(EFFDT)
          FROM PS_LOCAL_TAX_TBL
         WHERE STATE    = $state
           AND LOCALITY = $locality)

END-SELECT
END-PROCEDURE

#Include 'readxlat.sqc'  !get translates for fieldname, value
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'stdapi.sqc'    !Update Process API
