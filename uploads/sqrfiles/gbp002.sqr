!*********************************************
!  GBP002:  Group Membership List Report     *
!*********************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/03:22:15:22                           
!             $Release:  HR9                                           
!            $Revision:  101                                           
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

#define C01    1
#define C02   30
#define C03   60
#define C04   90
#define C05   100

#define JOBCODE_TBL

!***********************************************************************

begin-program
        do Init-DateTime
        do Init-Number
        do Get-Current-DateTime
        do Stdapi-Term
        do Init-Report
        do Process-Main
        do Reset
        do Stdapi-Term
end-program

!***********************************************************************
!  Heading
!***********************************************************************

begin-heading 7
#include 'stdhdg01.sqc'

print $GB_EFFDT_LBL (+1,{C01},28)
print $GB_GROUP_ID_LBL (0,{C02},29)
print $GB_REFINE_DATE_LBL (0,{C03})

print $GB_EFFDT (+1,{C01},28)
print $GB_GROUP_ID (,{C02},29)
print $GB_REFINE_DATE (,{C03})

print $EMPLID_LBL               (+2,{C01},28)
print $NAME_LBL                 (,{C02},29)
print $EMPL_RCD_LBL             (,{C03},29)
print $JOBCODE_LBL              (,{C04},9)
print $JOBDESCR_LBL             (,{C05})

end-heading

!***********************************************************************
!  Init Report
!***********************************************************************

begin-procedure Init-Report

  move 'GBP002' to $ReportID

  do Stdapi-Init
!***********************************************************************
! Was used for calling SQR Group Build API
! do GB_InitAPI
!***********************************************************************
  let $language_cd = $CURR_LANGUAGE_CD
  do Init_Report_Translation($ReportID, $language_cd)
  do Append_Report_Translation($ReportID)
  do Get_Field_Information($ReportID, 'REPORT_TITLE', $ReportTitle, #DW)

  if $prcs_process_instance = ''
        !No Prompt
  else
                do select-parameters
  end-if

  do Init_Printer

end-procedure

!***********************************************************************
!  Get Values
!***********************************************************************

begin-procedure Get-Values
   do Get-GBP002-Param
end-procedure

!***********************************************************************
!  Main: Creates the Group Member List Report
!***********************************************************************

begin-procedure Process-Main

        do Get-Translat-Info     ! Global

#ifdef debugx
           display 'Group As Of Date'
           display $GB_EFFDT
           display 'Group ID'
           display $GB_GROUP_ID
           display 'Refinement Date'
           display $GB_REFINE_DATE
#end-if

   do Retrieve-Group-Build
end-procedure

!***********************************************************************
!  Print Group Build Data
!***********************************************************************
begin-procedure Print-Group-Build-Data

        print $EMPLID               (+1,{C01},28)
        print $NAME                 (,{C02},29)
        print #EMPL_RCD             (,{C03}) EDIT 999999
        print $JOBCODE              (,{C04},9)
        print $JOBCODE_TBL-DESCR    (,{C05})

end-procedure Print-Group-Build-Data

!***********************************************************************
!  Retrieve Group Build Data
!***********************************************************************

begin-procedure Retrieve-Group-Build

begin-SELECT ON-ERROR=SQL-Error
A.EMPLID                          &A.EMPLID
A.NAME                            &A.NAME
B.NAME_PSFORMAT                   &B.NAME_PSFORMAT                     
A.EMPL_RCD                        &A.EMPL_RCD
A.JOBCODE                         &A.JOBCODE
A.SETID                           &A.SETID_JOBCODE
A.JOB_EFFDT                       &A.JOB_EFFDT

        let $EMPLID = &A.EMPLID
        let $NAME = &B.NAME_PSFORMAT                     
        let #EMPL_RCD = &A.EMPL_RCD
        let $JOBCODE = &A.JOBCODE
        do Get-Job-Descr
 ! HR 89 start
        let $JOBCODE_TBL-DESCR = $JOBDESCR
        do Get_Related_JOBCODE_TBL(&A.JOB_EFFDT,&A.JOBCODE,&A.SETID_JOBCODE)
 ! HR 89 end


        do Print-Group-Build-Data

from PS_GB_GRP_RES_VW A,PS_PERSON_NAME B
where A.GB_GROUP_ID = $GB_GROUP_ID
and A.VERSIONGBQDM = #VERSIONGBQDM
AND A.EMPLID = B.EMPLID
and A.JOB_EFFDT = (select max(B.JOB_EFFDT) from PS_GB_GRP_RES_VW B
    where B.GB_GROUP_ID = A.GB_GROUP_ID and B.VERSIONGBQDM = A.VERSIONGBQDM and
    B.EMPLID = A.EMPLID and B.EMPL_RCD = A.EMPL_RCD and B.SETID = A.SETID)
order by A.EMPLID, B.NAME_PSFORMAT, A.EMPL_RCD, A.JOBCODE
end-SELECT

end-procedure Retrieve-Group-Build

!***********************************************************************
!  Retrieve Jobcode Description
!***********************************************************************
begin-procedure Get-Job-Descr
begin-Select
B.DESCR             &B.DESCR
       let $JOBDESCR = &B.DESCR
from PS_JOBCODE_TBL B
WHERE B.SETID = &A.SETID_JOBCODE
and   B.JOBCODE = &A.JOBCODE
and   B.EFFDT = (Select MAX(EFFDT)
                 from PS_JOBCODE_TBL C
                 where C.SETID = &A.SETID_JOBCODE
                   and C.JOBCODE = &A.JOBCODE
                   and C.EFFDT <= &A.JOB_EFFDT )
End-Select
end-procedure
!***********************************************************************
!  Gets the strings printed in this report (Globalization)
!***********************************************************************

begin-procedure Get-Translat-Info
        do Get_Field_Information ($ReportID,     'EMPLID_LBL',                  $EMPLID_LBL,      #DW)
        do Get_Field_Information ($ReportID,     'NAME_LBL',                    $NAME_LBL,        #DW)
        do Get_Field_Information ($ReportID,     'EMPL_RCD_LBL',                $EMPL_RCD_LBL,    #DW)
        do Get_Field_Information ($ReportID,     'GB_EFFDT_LBL',                $GB_EFFDT_LBL,    #DW)
        do Get_Field_Information ($ReportID,     'GB_GROUP_ID_LBL',             $GB_GROUP_ID_LBL, #DW)
        do Get_Field_Information ($ReportID,     'GB_REFINE_DATE_LBL',  $GB_REFINE_DATE_LBL,  #DW)
        do Get_Field_Information ($ReportID,     'JOBCODE_LBL',                 $JOBCODE_LBL,  #DW)
        do Get_Field_Information ($ReportID,     'JOBDESCR_LBL',                $JOBDESCR_LBL,  #DW)
end-procedure Get-Translat-Info

!----------------------------------------------------------------------!
! Procedure:    Get-GBP002-Parm                                        !
! Description:  Get the effective date the group id the as of date and !
!               the version number parameters for the Group Membership !
!               Process of Group Build                                 !
!----------------------------------------------------------------------!
begin-procedure Get-GBP002-Param

  let $GB_EFFDT = &RUN_CNTL_HR.GB_EFFDT
  let $GB_GROUP_ID = RTRIM(&RUN_CNTL_HR.GB_GROUP_ID, ' ')
  let $GB_REFINE_DATE = &RUN_CNTL_HR.GB_REFINE_DATE
  let #VERSIONGBQDM = &RUN_CNTL_HR.VERSIONGBQDM

end-procedure

!***********************************************************************
!  Includes
!***********************************************************************

#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'stdapi.sqc'    !Routines to update run status
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'useprntr.sqc'
#Include 'sqrtrans.sqc'
#Include 'datemath.sqc'

#include 'rellang.sqc'
