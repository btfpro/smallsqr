! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!**********************************************************************
!  ReadXlat:  Read the Translate Table for the desired values.        *
!                                                                     *
!  Parameters passed:  $FieldName  (name of field to be translated)   *
!                      $FieldValue (value of field to be translated)  *
!                      $AsOfDate   (As-Of-Date -- optional)           *
!**********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************
begin-procedure Read-Translate-Table
#ifdef debugx
  display 'Entering READXLAT.SQC: Read-Translate-Table'
  display ' Input $FieldName: ' noline
  display $FieldName
  display ' Input $FieldValue: ' noline
  display $FieldValue
  display ' Input $AsOfDate: ' noline
  display $AsOfDate
  #ifdef debugy
    display '   $curr_language_cd: ' noline
    display $curr_language_cd
  #endif
#endif

  move '' to $XlatShortName
  move '' to $XlatLongName

  if $FieldName <> '' and $FieldValue <> ''
    if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
      move $AsOfToday to $AsOfDate
    end-if

      if $PSOptions_language_cd = ''
        do Get-PSOptions-Language ($PSOptions_language_cd)
      end-if

    if $PSOptions_language_cd = $curr_language_cd
      do Select_From_Xlat ($FieldName, $FieldValue,
                           $XlatShortName, $XlatLongName)
    else
      do Select_From_XlatLang ($FieldName, $FieldValue, $curr_language_cd,
                               $XlatShortName, $XlatLongName)

      if $XlatShortName = '' and $XlatLongName = ''
      do Select_From_Xlat ($FieldName, $FieldValue,
                           $XlatShortName, $XlatLongName)
      end-if
    end-if
  end-if

#ifdef debugx
  display 'Exiting READXLAT.SQC: Read-Translate-Table'
  display ' Output $XlatShortName: ' noline
  display $XlatShortName
  display ' Input $XlatLongName: ' noline
  display $XlatLongName
#endif
end-procedure


!***********************************************************************
! Select_From_Xlat                                                     *
!                                                                      *
!  Retrieves the XLATSHORTNAME and XLATLONGNAME from PSXLATITEM        *
!  for the input language code.                                        *
!***********************************************************************
begin-procedure Select_From_Xlat ($FldName, $FldVal,
                                  :$ShortName, :$LongName)
#ifdef debugx
  display 'Entering READXLAT.SQC: Select_From_Xlat'
  display ' Input $FldName: ' noline
  display $FldName
  display ' Input $FldVal: ' noline
  display $FldVal
#endif

  begin-SELECT
XLATSHORTNAME
XLATLONGNAME
  move &XlatShortName to $ShortName
  move &XlatLongName  to $LongName
    FROM  PSXLATITEM
    WHERE FIELDNAME   = $FldName
      AND FIELDVALUE  = $FldVal
      AND EFFDT =
          (SELECT MAX(EFFDT)
             FROM   PSXLATITEM
             WHERE  FIELDNAME  = $_FieldName
               AND  FIELDVALUE = $_FieldValue
               AND  EFFDT     <= $_AsOfDate)
  end-SELECT

#ifdef debugx
  display 'Exiting READXLAT.SQC: Select_From_Xlat'
  display ' Output $ShortName: ' noline
  display $ShortName
  display ' Output $LongName: ' noline
  display $LongName
#endif
end-procedure

!***********************************************************************
! Select_From_XlatLang                                                 *
!                                                                      *
!  Retrieves the XLATSHORTNAME and XLATLONGNAME from PSXLATITEMLANG    *
!  for the input language code.                                        *
!***********************************************************************
begin-procedure Select_From_XlatLang ($FldName, $FldVal, $Language_cd,
                                      :$ShortName, :$LongName)
#ifdef debugx
  display 'Entering READXLAT.SQC: Select_From_XlatLang'
  display ' Input $Language_cd: ' noline
  display $Language_cd
  display ' Input $FldName: ' noline
  display $FldName
  display ' Input $FldVal: ' noline
  display $FldVal
#endif

  let $Lcl_Language_Cd = rtrim($Language_cd, ' ')

  begin-SELECT
XLATSHORTNAME
XLATLONGNAME
  move &XlatShortName to $ShortName
  move &XlatLongName  to $LongName
    FROM  PSXLATITEMLANG
    WHERE FIELDNAME   = $FldName
      AND FIELDVALUE  = $FldVal
      AND LANGUAGE_CD = $Lcl_Language_Cd
      AND EFFDT =
          (SELECT MAX(EFFDT)
             FROM   PSXLATITEMLANG
             WHERE  FIELDNAME  = $_FieldName
               AND  FIELDVALUE = $_FieldValue
               AND LANGUAGE_CD = $Lcl_Language_Cd
               AND  EFFDT     <= $_AsOfDate)
  end-SELECT

#ifdef debugx
  display 'Exiting READXLAT.SQC: Select_From_XlatLang'
  display ' Output $ShortName: ' noline
  display $ShortName
  display ' Output $LongName: ' noline
  display $LongName
#endif
end-procedure
