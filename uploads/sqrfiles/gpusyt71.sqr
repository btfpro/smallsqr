!***********************************************************************
!  gpusyt71:  Create W-2c/W-3c Print Files - Puerto Rico               *
!***********************************************************************
!                                                                      !
!               Confidentiality Information:                           !
!                                                                      !
!                                                                      !
! This module contains confidential and proprietary information        !
! of Oracle; it is not to be copied, reproduced, or transmitted        !
! in any form, by any means, in whole or in part, nor is it to         !
! be used for any purpose other than that for which it is              !
! expressly provided under the applicable license agreement.           !
!                                                                      !
! Copyright (C) 2006 Oracle. All Rights Reserved.                      !
!                                                                      !
!**********************************************************************!
!                                                                      !
!          $Date:  2006/08/31:17:55:18                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      !
!**********************************************************************!


#include 'setenv.sqc'   !Set environment
#DEFINE WCOLUMN1 4      !Set up first print column of form
#DEFINE WCOLUMN2 42     !Set up mid-point column of form


begin-setup
    #Include 'setup03i.sqc'  !printer and page-size init


end-setup


begin-report

  do Init-Report
  do Stdapi-Term
end-report

begin-procedure Init-Report
  display ''
  display 'Printing W-2c PR'

  let $t1 = DateToStr(DateNow(),'hh:mi:ss')  
  display $t1

  move 'GPUSYT71' to $ReportID
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Initialization
  do Assign-Year-End-Variables
  if &TX.GPUS_YE_ALIGNCOUNT > 0
    DISPLAY 'PRINTING ALIGNMENT FORMS  '
    do Print-Alignment-Forms
    do Init-Values
  end-if

  move &TX.Telephone_Nbr to $Phone
  move &W2C_INSTALL.GPUS_CAL_YEAR  to $ER_Year
  if &TX.GPUS_YE_REPRINT  = '2'
    do Read-EEs
  else
    do Read-Employee-Data
  end-if

  if $EmployeeFound = 'N'
   if &TX.GPUS_YE_ALIGNCOUNT > 0
    new-page
   end-if
    display ' '
    display '****** No Employee Records Selected ******'
    display ' '
    print '********************************'  (+3,1)
    print '  No Employee Records Selected  '  (+1,1)
    print '********************************'  (+1,1)
  else
    if &TX.GPUS_YE_REPRINT  = '1'
      do Update-W2C-Parameters
    else
      do Update-Reg-Reprint-Status
    end-if
    display ''
    display 'Upon successful conclusion of this program, '
    display 'an output print file for the W-2c will be created '
    display 'GPUSYT71.LIS contains print images for paper W-2cs '
    display 'for Puerto Rico.                                  '
    display ''
    display 'Run the W-2cPR audit program(GPUSYT61) to get'
    display 'the totals for the W-3c report.'
    display ''
    display '** NOTE: If you wish to save these files, rename them before'
    display 'you run the next batch.'
    display ''
  end-if

  let $t2 = DateToStr(DateNow(),'hh:mi:ss')  
  display $t2

end-procedure

begin-procedure Initialization
  do Get-Current-DateTime
  move '1' to $MMLZero
  move '1' to $Year4
  do Format-DateTime($AsOfToday, $920Date, {DEFYMD},'','')

  move $920Date to $AsOfYear xxxx   !extract the current year
  move $AsOfYear  to #AsOfYear
  move #AsOfYear  to #saveAsOfYear
  subtract 1    from #AsOfYear

  do Init-Values
  do Get-W2C-Run-Controls
  do Get-W2C-Parameters

  if RTRIM(&TX.GPUS_PREPRTFORM ,' ') = ''
    display '**************************************************************'
    display 'Form ID not specified on Print Year End Correction Forms Page '
    display 'Program stopped'
    display '**************************************************************'
    stop
  end-if
  move &TX.CALENDAR_YEAR  to $CalYear 9999
  if &TX.CALENDAR_YEAR = 0
  display ' '
  display '****** No Employee Records Selected ******'
  display ' '
  print '********************************'  (+3,1)
  print '  No Employee Records Selected  '  (+1,1)
  print '********************************'  (+1,1)
  stop
   end-if
  let $AsOfDate = $CalYear || '1231'
    do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP}, '', 'native')

  do Load-W2C-PRT


  move  0 to #Form#
  let $First_time = 'Y'

  move &TX.GPUS_YE_EE_PERPAGE  to #EmployeesPerPage
  if $Layout_Styple = '40'
    COLUMNS {WCOLUMN2}
    move 1 to #FormsPerPage
  else
    if $Layout_Style = '10'
      COLUMNS {WCOLUMN1}
      move 1 to #FormsPerPage
    else
      display 'Forms per page set to 1  '
      move 1 to #FormsPerPage
    end-if
  end-if

  if #EmployeesPerPage  > #FormsPerPage
    display '*********************************************************'
    display 'Employees Per Page specifed on Print Year End Correction '
    display 'Forms Page  is greater than the number of forms per page '
    display 'for the Form ID selected.                                '
    display '*********************************************************'
    stop
  end-if


begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

end-procedure


begin-procedure Read-Employee-Data

  move 'N' to $EmployeeFound

begin-SELECT
E.GPUS_TAX_EIN          () ON-BREAK LEVEL=1 PRINT=NEVER BEFORE=BEFORE-COMPANY
E.EMPLID   () ON-BREAK LEVEL=2 PRINT=NEVER BEFORE=BEFORE-EMPLID
E.SSN
E.GPUS_YE_FNAME
E.GPUS_YE_MNAME
E.GPUS_YE_LNAME
E.GPUS_YE_SUFFIX
E.ADDRESS1
E.ADDRESS2
E.CITY
E.STATE
E.POSTAL
E.COUNTRY
E.GPUS_CONTROL_NBR
E.GPUS_MARITAL_STAT
E.GPUS_SPOUSE_SSN
E.GPUS_PSSN
E.GPUS_YE_PFNAME
E.GPUS_YE_PMNAME
E.GPUS_YE_PLNAME
E.GPUS_YE_PSUFFIX
E.GPUS_PADDRESS1
E.GPUS_PADDRESS2
E.GPUS_PCITY
E.GPUS_PSTATE
E.GPUS_PPOSTAL
E.GPUS_PCOUNTRY
E.GPUS_PMARITAL_STAT
E.GPUS_PSPOUSE_SSN
E.GPUS_PCONTROL_NBR
E.GPUS_YEC_BATCH_NBR
E.GPUS_CAL_YEAR
E.DESCRLONG

  move 'Y' to $EmployeeFound
  move &E.GPUS_CAL_YEAR  to $ER_Year  9999
  do Load-Amounts
  do Print-W2C
  if &TX.GPUS_YE_REPRINT  = '1'
    do Update-W2-History
  end-if

FROM  PS_GPUS_YEC_DAT_PR E
  WHERE E.GPUS_YEC_STATUS       <> '30'
  AND GPUS_TAXFORM_ID = $Taxform_ID
  [$E.Batch_Nbr]
  [$E.SelectedEEs]        !NULL string if SELECT EEs option not used
ORDER BY E.GPUS_TAX_EIN, E.SSN
end-SELECT
end-procedure

begin-procedure Before-Company

begin-SELECT
A.GPUS_YE_RPT_CO
A.DESCR
A.GPUS_TAX_EIN
A.ADDRESS1
A.ADDRESS2
A.CITY
A.STATE
A.POSTAL
A.COUNTRY
A.GPUS_BUS_TERM_DT

  move &A.Gpus_Tax_EIN      to $Federal_EIN 099999999
  move $Federal_EIN         to $A.Federal_EIN xx-xxxxxxx
  move &A.Descr             to $A.Descr
  move &A.Address1          to $A.Address1
  move &A.Address2          to $A.Address2
  move &A.Country           to $A.Country
  let  $A.City              =  RTRIM(&A.City,' ')
  let  $A.State             =  RTRIM(&A.State,' ')
  move &A.Postal            to $A.Postal
  move &A.Gpus_Ye_Rpt_Co    to $A.W2_Reporting_Co
  move &A.Gpus_Bus_Term_Dt  to $A.Closing_Dt
  do Format-DateTime($A.Closing_Dt, $A.Closing_Date, {DEFMDY},'','')

  UPPERCASE $A.Descr
  UPPERCASE $A.Address1
  UPPERCASE $A.Address2
  UPPERCASE $A.Country
  UPPERCASE $A.City

FROM  PS_GPUS_YE_CO_TBL A, PS_GPUS_YE_EE0_VW B
WHERE B.GPUS_TAX_EIN = &E.GPUS_TAX_EIN
AND B.GPUS_YE_RPT_CO = A.GPUS_YE_RPT_CO
AND B.EMPLID = &E.EMPLID
AND B.GPUS_CAL_YEAR = &E.GPUS_CAL_YEAR

end-SELECT
end-procedure

begin-procedure Before-EmplID

  move &E.EMPLID   to $E.EmplID

  move &E.SSN to $S123 xxx
  if $S123 = &Temp_SSN_Mask or RTRIM(&E.SSN, ' ') = ''
    move 'Applied For' to $E.SSN
  else
    move &E.SSN to $E.SSN xxx-xx-xxxx
  end-if

  move &E.GPUS_PSSN to $S123 xxx
  if $S123 = &Temp_SSN_Mask or RTRIM(&E.GPUS_PSSN, ' ') = ''
    move 'Applied For' to $E.GPUS_PSSN
  else
    move &E.GPUS_PSSN to $E.GPUS_PSSN xxx-xx-xxxx
  end-if

  move &E.GPUS_YE_FNAME to $E.Name
  move &E.GPUS_YE_FNAME to $E.Fname
  move &E.GPUS_YE_MNAME to $E.MiddleName
  move &E.GPUS_YE_LNAME to $E.Lname
  move &E.GPUS_YE_SUFFIX to $E.Suffix
  let $E.Name = rtrim($E.Name, ' ')  || ' '
  let $E.Fname = rtrim($E.Fname, ' ')  || ' '
  let $E.MiddleName = rtrim($E.MiddleName, ' ') || ' '
  let $E.Lname = rtrim($E.Lname, ' ')  || ' '
  let $E.Suffix =rtrim($E.Suffix, ' ')  || ' '

  if $E.MiddleName <> ' '
    let $E.YE_Name = $E.Name || $E.MiddleName || $E.Lname || $E.Suffix
  else
    let $E.YE_Name = $E.Name || $E.Lname || $E.Suffix
  end-if

  move $E.YE_Name to $YE_Name
  move $YE_Name to $E.YE_Name

  if $E.MiddleName <> ' '
    let $E.YE_Fname = $E.Fname || $E.MiddleName
  else
    let $E.YE_Fname = $E.Fname
  end-if

  if $E.Suffix <> ' '
    let $E.YE_Lname = $E.Lname ||  $E.Suffix
  else
    let $E.YE_Lname = $E.Lname
  end-if
  move $E.YE_Fname to $YE_Fname
  move $YE_Fname to $E.YE_Fname

  move $E.YE_LName to $YE_LName
  move $YE_LName to $E.YE_LName

  move &E.Gpus_Control_Nbr        to $E.Control_Number
  move &E.Gpus_Marital_Stat            to $E.Mar_Status
  move &E.Gpus_Spouse_SSN            to $E.Spouse_SSN

  move &E.Address1        to $E.Address1
  move &E.Address2        to $E.Address2
  move &E.Country         to $E.Country
  let  $E.City            =  RTRIM(&E.City,' ')
  let  $E.State           =  RTRIM(&E.State,' ')
  move &E.Postal          to $E.Postal
  move &E.DescrLong       to $E.DescrLong
  let $E.Comments_A   = substr ($E.DescrLong, 1, 60)
  let $E.Comments_B   = substr ($E.DescrLong, 61, 60)

  move &E.Gpus_Ye_PFName to $E.Prv_Name
  move &E.Gpus_Ye_PFName to $E.Prv_Fname
  move &E.Gpus_Ye_PMName to $E.Prv_MiddleName
  move &E.Gpus_Ye_PLName to $E.Prv_Lname
  move &E.Gpus_Ye_PSuffix to $E.Prv_Suffix
  let $E.Prv_Name = rtrim($E.Prv_Name, ' ')  || ' '
  let $E.Prv_Fname = rtrim($E.Prv_Fname, ' ')  || ' '
  let $E.Prv_MiddleName = rtrim($E.Prv_MiddleName, ' ') || ' '
  let $E.Prv_Lname = rtrim($E.Prv_Lname, ' ')  || ' '
  let $E.Prv_Suffix =rtrim($E.Prv_Suffix, ' ')  || ' '
  if $E.Prv_MiddleName <> ' '
    let $E.Prv_W2C_Name = $E.Prv_Name || $E.Prv_MiddleName || $E.Prv_Lname || $E.Prv_Suffix
  else
    let $E.Prv_W2C_Name = $E.Prv_Name || $E.Prv_Lname || $E.Prv_Suffix
  end-if

  move $E.Prv_W2C_Name to $Prv_Name
  move $Prv_Name to $E.Prv_W2C_Name

  if $E.Prv_MiddleName <> ' '
    let $E.Prv_W2C_Fname = $E.Prv_Fname || $E.Prv_MiddleName
  else
    let $E.Prv_W2C_Fname = $E.Prv_Fname
  end-if

  if $E.Prv_Suffix <> ' '
    let $E.Prv_W2C_Lname = $E.Prv_Lname ||  $E.Prv_Suffix
  else
    let $E.Prv_W2C_Lname = $E.Prv_Lname
  end-if
  move $E.Prv_W2C_Fname to $Prv_Fname
  move $Prv_Fname to $E.Prv_W2C_Fname

  move $E.Prv_W2C_LName to $Prv_LName
  move $Prv_LName to $E.Prv_W2C_LName

  move &E.Gpus_PAddress1        to $E.Prv_Address1
  move &E.Gpus_PAddress2        to $E.Prv_Address2
  move &E.Gpus_PCountry         to $E.Prv_Country
  let  $E.Prv_City            =  RTRIM(&E.Gpus_PCity,' ')
  let  $E.Prv_State           =  RTRIM(&E.Gpus_PState,' ')
  move &E.Gpus_PPostal          to $E.Prv_Postal

  move &E.Gpus_PControl_Nbr           to $E.Prv_Control_Number
  move &E.Gpus_PMarital_Stat            to $E.Prv_Mar_Status
  move &E.Gpus_PSpouse_SSN            to $E.Prv_Spouse_SSN

  uppercase $YE_Name
  uppercase $E.Address1
  uppercase $E.Address2
  uppercase $E.Country
  uppercase $E.City

  uppercase $E.Prv_Name
  uppercase $E.Prv_Address1
  uppercase $E.Prv_Address2
  uppercase $E.Prv_Country
  uppercase $E.Prv_City

end-procedure


begin-procedure Print-W2C
  move 0 to #NextStateLoc
  move 0 to #HoldNextStateLoc
  move 0 to #LocalPrintCount
  move 0 to #StateSWTCount
  move 1 to #j

  while #j <= #FormsPerPage/#EmployeesPerPage  !loop for individual ee's
    do Next-Form
    do Print-ER-Data
    do Print-EE-Data
    do Print-Page1-Data
    do Print-Fed-Amounts
    add 1 to #j
  end-while

  do Init-Values


end-procedure

begin-procedure Init-Values

  move 0 to #Cost_Pension_Annuity           ! Box 07
  move 0 to #Wages                          ! Box 08
  move 0 to #Reimbursed_Expense             ! Box 13
  move 0 to #Tax_Withheld                   ! Box 14
  move 0 to #Retire_Fund                    ! Box 15
  move 0 to #CODA_Plans                     ! Box 16
  move 0 to #SS_Wages                       ! Box 18
  move 0 to #SS_Tax                         ! Box 19
  move 0 to #Med_Wages                      ! Box 20
  move 0 to #Med_Tax                        ! Box 21
  move 0 to #SS_Tips                        ! Box 22
  move 0 to #Tips                           ! Box 11
  move 0 to #Commissions                    ! Box 09
  move 0 to #Allowance                      ! Box 10
  move 0 to #Total_Comm_Allow_Tips          ! Box 12
  move 0 to #TotalAbove                     ! Box 12 Calc'd
  move 0 to #SS_Tax_Tips                    ! Box 23
  move 0 to #Med_Tax_Tips                   ! Box 24

  move 0 to #Prv_Cost_Pension_Annuity
  move 0 to #Prv_Wages
  move 0 to #Prv_Reimbursed_Expense
  move 0 to #Prv_Tax_Withheld
  move 0 to #Prv_Retire_Fund
  move 0 to #Prv_CODA_Plans
  move 0 to #Prv_SS_Wages
  move 0 to #Prv_SS_Tax
  move 0 to #Prv_Med_Wages
  move 0 to #Prv_Med_Tax
  move 0 to #Prv_SS_Tips
  move 0 to #Prv_Tips
  move 0 to #Prv_Commissions
  move 0 to #Prv_Allowance
  move 0 to #Prv_Total_Comm_Allow_Tips
  move 0 to #Prv_TotalAbove
  move 0 to #Prv_SS_Tax_Tips
  move 0 to #Prv_Med_Tax_Tips
  let $Found_Wages       = 'N'
  let $Found_Commissions = 'N'
  let $Found_Allowance   = 'N'
  let $Found_Tips        = 'N'

end-procedure

begin-procedure Print-ER-Data
  do Print-Value($ER_Year,#L_Year_and_Form,#C_Year_and_Form,$YE_Year)
  do Print-Value($A.Federal_EIN,#L_ER_EIN,#C_ER_EIN,$YE_ER_EIN)

  do Print-Value($A.Descr,#L_ER_Name,#C_ER_Name,$YE_ER_Name)
  do Print-Value($A.Address1,#L_ER_Address1,#C_ER_Address1,$YE_ER_Address1)
  if RTRIM($A.Address2, ' ') <> ''
    do Print-Value($A.Address2,#L_ER_Address2,#C_ER_Address2,$YE_ER_Address2)
    move 'N' to $LineShift
  else
    move 'Y' to $LineShift
  end-if
  if #Align_Count = 0
    if $A.Country = 'USA'
      move $A.Postal           to $Zip
      move $Zip                to $ZipExtsn ~~~~~~xxxx
      if instr($Zip,'-',6) = 0
      and $ZipExtsn <> '    '
        let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
      end-if
      let $State_City_Zip = $A.City || ', ' || $A.State || ' ' || $Zip
    else
      let $State_City_Zip = $A.City || ', ' || $A.State || ' ' ||
              $A.Country || ' ' || $A.Postal
    end-if
  end-if
  let $State_City_Zip = SUBSTR($State_City_Zip,1,25)
  if $LineShift = 'Y'
    let #PrintLine = #L_ER_Address2
  else
    let #PrintLine = #L_State_City_Zip
  end-if
  do Print-Value($State_City_Zip,#PrintLine,#C_State_City_Zip,$YE_ER_State_City_Zip)
  do Print-Value($A.Closing_Date, #L_ER_Closing, #C_ER_Closing, $F1B)
  do Print-Value($Phone, #L_ER_Phone, #C_ER_Phone, $F5B)

end-procedure

begin-procedure Create-Total-Box        !Look for all components to totals box that sums up
                                        !Wages,Commissions,Allowances and Tips, even if the
if $Found_Wages       <> 'Y'            !amounts had not changed on this W-2CPR
   let $Search_Box = $YE_Wages
   do Find-Previous-W2CPR-Balance
end-if

if $Found_Commissions <> 'Y'
   let $Search_Box = $YE_Commissions
   do Find-Previous-W2CPR-Balance
end-if

if $Found_Allowance   <> 'Y'
   let $Search_Box = $YE_Allowance
   do Find-Previous-W2CPR-Balance
end-if

if $Found_Tips       <> 'Y'
   let $Search_Box = $YE_Tips
   do Find-Previous-W2CPR-Balance
end-if

end-procedure

begin-procedure Find-Previous-W2CPR-Balance

let $Found-W2CPR-Amount = 'N'

begin-SELECT

W2CP_AMTS.GPUS_YE_AMOUNT

   let $Found-W2CPR-Amount = 'Y'

FROM PS_GPUS_YEC_AMT_PR W2CP_AMTS, PS_GPUS_YEC_DAT_PR W2CP_DATA
WHERE W2CP_AMTS.GPUS_TAX_EIN            = &E.GPUS_TAX_EIN
AND W2CP_AMTS.EMPLID            = &E.EMPLID
AND W2CP_AMTS.GPUS_CAL_YEAR         = &E.GPUS_CAL_YEAR
AND W2CP_AMTS.GPUS_TAXFORM_ID       = $Taxform_ID
AND W2CP_AMTS.GPUS_TAXFORM_BOX          = $Search_Box
AND W2CP_AMTS.GPUS_YEC_BATCH_NBR        =
    (SELECT MAX(GPUS_YEC_BATCH_NBR)FROM PS_GPUS_YEC_AMT_PR
    WHERE W2CP_AMTS.GPUS_TAX_EIN    = GPUS_TAX_EIN
    AND W2CP_AMTS.EMPLID        = EMPLID
    AND W2CP_AMTS.GPUS_CAL_YEAR     = GPUS_CAL_YEAR
    AND W2CP_AMTS.GPUS_TAXFORM_ID   = GPUS_TAXFORM_ID
    AND W2CP_AMTS.GPUS_TAXFORM_BOX  = GPUS_TAXFORM_BOX)
AND W2CP_DATA.GPUS_YEC_STATUS       = '10'
AND W2CP_AMTS.GPUS_TAX_EIN      = W2CP_DATA.GPUS_TAX_EIN
AND W2CP_AMTS.EMPLID            = W2CP_DATA.EMPLID
AND W2CP_AMTS.GPUS_CAL_YEAR         = W2CP_DATA.GPUS_CAL_YEAR
AND W2CP_AMTS.GPUS_YEC_BATCH_NBR    = W2CP_DATA.GPUS_YEC_BATCH_NBR

end-SELECT

if $Found-W2CPR-Amount =  'Y'
   add &W2CP_AMTS.GPUS_YE_AMOUNT to #Prv_Total_Comm_Allow_Tips
   add &W2CP_AMTS.GPUS_YE_AMOUNT to #Total_Comm_Allow_Tips
else
   do Find-Previous-W2PR-Balance
end-if


end-procedure

begin-procedure Find-Previous-W2PR-Balance

let $Found-W2PR-Amount = 'N'

begin-SELECT
GPUS_YE_AMOUNT  &W2PR.SUM

  let $Found-W2PR-Amount = 'Y'

FROM PS_GPUS_YE_EE1_VW
WHERE EMPLID         = &E.EMPLID
AND GPUS_CAL_YEAR    = &E.GPUS_CAL_YEAR
AND GPUS_TAX_EIN     = &E.GPUS_TAX_EIN
AND GPUS_TAXFORM_ID  = $Taxform_ID
AND GPUS_TAXFORM_BOX = $Search_Box

end-SELECT

if $Found-W2PR-Amount =  'Y'
   add &W2PR.SUM to #Prv_Total_Comm_Allow_Tips
   add &W2PR.SUM to #Total_Comm_Allow_Tips
   end-if

end-procedure

begin-procedure Print-EE-Data

  let $YE_W2C_Name  = SUBSTR($YE_Name,1,30)
  let $E.Address1  = SUBSTR($E.Address1,1,30)
  let $E.Address2  = SUBSTR($E.Address2,1,30)
  do Print-Value($YE_W2C_Name,#L_EE_Full_Name,#C_EE_Full_Name,$YE_W2C_EE_FullName)
  let $E.Address1 = RTRIM($E.Address1, ' ')
  do Print-Value($E.Address1,#L_EE_Address1,#C_EE_Address1,$YE_W2C_EE_Address1)
  let $E.Address2 = RTRIM($E.Address2, ' ')
  if RTRIM($E.Address2, ' ') <> ''
    do Print-Value($E.Address2,#L_EE_Address2,#C_EE_Address2,$YE_W2C_EE_Address2)
    move 'N' to $LineShift
  else
    move 'Y' to $LineShift
  end-if
  if #Align_Count = 0
    if $E.Country = 'USA'
      move $E.Postal           to $Zip
      move $Zip                to $ZipExtsn ~~~~~~xxxx
      if instr($Zip,'-',6) = 0
      and $ZipExtsn <> '    '
        let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
      end-if
      let $B4 = $E.City || ', ' || $E.State || ' ' || $Zip
    else
      let $B4 = $E.City || ', ' || $E.State || ' ' ||
                $E.Country || ' ' || $E.Postal
    end-if
  end-if
  if $LineShift = 'Y'
    let #PrintLine = #L_EE_Address2
  else
    let #PrintLine = #L_EE_State_City_Zip
  end-if
  do Print-Value($B4,#PrintLine,#C_EE_State_City_Zip,$YE_W2C_EE_State_City_Zip)

  do Print-Value($E.SSN,#L_EE_SSN,#C_EE_SSN,$YE_EE_SSN)
  if ($YE_Name <> $Prv_Name)
    do Print-Value($E.Prv_Incorrect_Name,#L_EE_Incorrect_Name,#C_EE_Incorrect_Name,$F06B)
  end-if

  if ($E.SSN <> $E.GPUS_PSSN)
    do Print-Value($E.GPUS_PSSN,#L_EE_Incorrect_SSN,#C_EE_Incorrect_SSN,$F06A)
  end-if
  do Print-Value ($E.Prv_Control_Number, #L_Prv_Control_Number, #C_Prv_Control_Number, $F27B)
  do Print-Value ($E.Comments_A, #L_Comments_A, #C_Comments_A, $F26A)
  do Print-Value ($E.Comments_B, #L_Comments_B, #C_Comments_B, $F26B)

end-procedure

begin-procedure Load-Amounts
  move 0 to #StateLocalCount
begin-SELECT
D.STATE
D.GPUS_TAXFORM_BOX
D.GPUS_YE_AMOUNT
D.GPUS_YE_PAMOUNT

  move &D.GPUS_YE_AMOUNT to #Amount
  move &D.GPUS_YE_PAMOUNT to #Prv_Amount
  if #Amount <> #Prv_Amount

   let $D.Box = RTRIM(&D.GPUS_TAXFORM_BOX,' ')
   evaluate $D.Box
     when = $YE_Cost_Pension_Annuity
       let  #Cost_Pension_Annuity = #Amount
       let  #Prv_Cost_Pension_Annuity = #Prv_Amount
       break
     when = $YE_Wages                             !Wages
       let  #Wages = #Amount
       let  #Prv_Wages = #Prv_Amount
       let $Found_Wages = 'Y'
       break
     when = $YE_Commissions                       !Commissions
       let  #Commissions = #Amount
       let  #Prv_Commissions = #Prv_Amount
       let $Found_Commissions = 'Y'
       break
     when = $YE_Allowance                         !Allowance
       let  #Allowance = #Amount
       let  #Prv_Allowance = #Prv_Amount
       let $Found_Allowance = 'Y'
       break
     when = $YE_Tips                              !Tips
       let  #Tips = #Amount
       let  #Prv_Tips = #Prv_Amount
       let $Found_Tips = 'Y'
       break
     when = $YE_Reimbursed_Expense                !Reimbursed Expense
       let  #Reimbursed_Expense = #Amount
       let  #Prv_Reimbursed_Expense = #Prv_Amount
       break
     when = $YE_Tax_Withheld                      !Tax Withheld
       let  #Tax_Withheld = #Amount
       let  #Prv_Tax_Withheld = #Prv_Amount
       break
     when = $YE_Retire_Fund                       !Retire Fund
       let  #Retire_Fund = #Amount
       let  #Prv_Retire_Fund = #Prv_Amount
       break
     when = $YE_CODA_Plans                        !CODA Plans
       let  #CODA_Plans = #Amount
       let  #Prv_CODA_Plans = #Prv_Amount
       break
     when = $YE_Subtotals                         !Subtotals
       let  #Subtotals = #Amount
       let  #Prv_Subtotals = #Prv_Amount
       break
     when = $YE_SS_Wages                          !SS wages
       let  #SS_Wages = #Amount
       let  #Prv_SS_Wages = #Prv_Amount
       break
     when = $YE_SS_Tax                            !SS Tax
       let  #SS_Tax = #Amount
       let  #Prv_SS_Tax = #Prv_Amount
       break
    when = $YE_Med_Wages                          !Medicare wages
       let  #Med_Wages = #Amount
       let  #Prv_Med_Wages = #Prv_Amount
       break
     when = $YE_Med_Tax                           !Medicare tax
       let  #Med_Tax = #Amount
       let  #Prv_Med_Tax = #Prv_Amount
       break
     when = $YE_SS_Tips                       !SS tax on tips
       let  #SS_Tips = #Amount
       let  #Prv_SS_Tips = #Prv_Amount
       break
     when = $YE_SS_Tax_Tips                       !SS tax on tips
       let  #SS_Tax_Tips = #Amount
       let  #Prv_SS_Tax_Tips = #Prv_Amount
       break
     when = $YE_Med_Tax_Tips                      !Medi tax on tips
       let  #Med_Tax_Tips = #Amount
       let  #Prv_Med_Tax_Tips = #Prv_Amount
       break
   end-evaluate

  end-if


FROM  PS_GPUS_YEC_AMT_PR D
WHERE D.GPUS_YEC_BATCH_NBR   = &E.GPUS_YEC_BATCH_NBR
AND   D.GPUS_TAX_EIN         = &E.GPUS_TAX_EIN
AND   D.EMPLID  = &E.EMPLID
AND   D.GPUS_CAL_YEAR   = &E.GPUS_CAL_YEAR
ORDER BY D.STATE, D.GPUS_TAXFORM_BOX
end-SELECT
end-procedure

begin-procedure Print-Fed-Amounts

  if #Cost_Pension_Annuity <> #Prv_Cost_Pension_Annuity
    let #Cost_Pension_Annuity_Diff = #Cost_Pension_Annuity - #Prv_Cost_Pension_Annuity

    do Format-W2c-Number(#Prv_Cost_Pension_Annuity, $out, $FCost_Pension_Annuity)         !Federal wages
    do Print-Value($out,#L_Cost_Pension_Annuity,#C_Cost_Pension_Annuity,$YE_Cost_Pension_Annuity)

    do Format-W2c-Number(#Cost_Pension_Annuity, $out, $FCost_Pension_Annuity)
    print $out (,+5)

    do Format-W2c-Number(#Cost_Pension_Annuity_Diff, $out, $FCost_Pension_Annuity)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Wages <> #Prv_Wages
    let #Wages_Diff = #Wages - #Prv_Wages
    add #Wages to #TotalAbove
    add #Prv_Wages to #Prv_TotalAbove

    do Format-W2c-Number(#Prv_Wages, $out, $FWages)         !Federal wages
    do Print-Value($out,#L_Wages,#C_Wages,$YE_Wages)

    do Format-W2c-Number(#Wages, $out, $FWages)
    print $out (,+5)

    do Format-W2c-Number(#Wages_Diff, $out, $FWages)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Commissions <> #Prv_Commissions
    let #Commissions_Diff = #Commissions - #Prv_Commissions
    add #Commissions to #TotalAbove
    add #Prv_Commissions to #Prv_TotalAbove

    do Format-W2c-Number(#Prv_Commissions, $out, $FCommissions)         !Federal Commissions
    do Print-Value($out,#L_Commissions,#C_Commissions,$YE_Commissions)

    do Format-W2c-Number(#Commissions, $out, $FCommissions)
    print $out (,+5)

    do Format-W2c-Number(#Commissions_Diff, $out, $FCommissions)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Allowance <> #Prv_Allowance
    let #Allowance_Diff = #Allowance - #Prv_Allowance
    add #Allowance to #TotalAbove
    add #Prv_Allowance to #Prv_TotalAbove

    do Format-W2c-Number(#Prv_Allowance, $out, $FAllowance)         !Federal Allowance
    do Print-Value($out,#L_Allowance,#C_Allowance,$YE_Allowance)

    do Format-W2c-Number(#Allowance, $out, $FAllowance)
    print $out (,+5)

    do Format-W2c-Number(#Allowance_Diff, $out, $FAllowance)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Tips <> #Prv_Tips
    let #Tips_Diff = #Tips - #Prv_Tips
    add #Tips to #TotalAbove
    add #Prv_Tips to #Prv_TotalAbove

    do Format-W2c-Number(#Prv_Tips, $out, $FTips)
    do Print-Value($out,#L_Tips,#C_Tips,$YE_Tips)

    do Format-W2c-Number(#Tips, $out, $FTips)
    print $out (,+5)

    do Format-W2c-Number(#Tips_Diff, $out, $FTips)
    print $out (,+4) edit 99999999.99ps

  end-if

  if $Found_Wages       = 'Y'
  or $Found_Commissions = 'Y'
  or $Found_Allowance   = 'Y'
  or $Found_Tips        = 'Y'

    do Create-Total-Box
    let #TotalAbove = #Total_Comm_Allow_Tips  + #TotalAbove
    let #Prv_TotalAbove = #Prv_Total_Comm_Allow_Tips + #Prv_TotalAbove

    let #TotalAbove_Diff = #TotalAbove - #Prv_TotalAbove

    do Format-W2c-Number(#Prv_TotalAbove, $out, $FTotalAbove)
    do Print-Value($out,#L_TotalAbove,#C_TotalAbove,$YE_Comm_Allow_Tips_Total)

    do Format-W2c-Number(#TotalAbove, $out, $FTotalAbove)
    print $out (,+5)

    do Format-W2c-Number(#TotalAbove_Diff, $out, $FTotalAbove)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Reimbursed_Expense <> #Prv_Reimbursed_Expense
    let #Reimbursed_Expense_Diff = #Reimbursed_Expense - #Prv_Reimbursed_Expense

    do Format-W2c-Number(#Prv_Reimbursed_Expense, $out, $FReimbursed_Expense)
    do Print-Value($out,#L_Reimbursed_Expense,#C_Reimbursed_Expense,$YE_Reimbursed_Expense)

    do Format-W2c-Number(#Reimbursed_Expense, $out, $FReimbursed_Expense)
    print $out (,+5)

    do Format-W2c-Number(#Reimbursed_Expense_Diff, $out, $FReimbursed_Expense)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Tax_Withheld <> #Prv_Tax_Withheld
    let #Tax_Diff = #Tax_Withheld - #Prv_Tax_Withheld

    do Format-W2c-Number(#Prv_Tax_Withheld, $out, $FTax)
    do Print-Value($out,#L_Tax,#C_Tax,$YE_Tax_Withheld)

    do Format-W2c-Number(#Tax_Withheld, $out, $FTax)
    print $out (,+5)

    do Format-W2c-Number(#Tax_Diff, $out, $FTax)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Retire_Fund <> #Prv_Retire_Fund
    let #Retire_Fund_Diff = #Retire_Fund - #Prv_Retire_Fund

    do Format-W2c-Number(#Prv_Retire_Fund, $out, $FRetire_Fund)
    do Print-Value($out,#L_Retire_Fund,#C_Retire_Fund,$YE_Retire_Fund)

    do Format-W2c-Number(#Retire_Fund, $out, $FRetire_Fund)
    print $out (,+5)

    do Format-W2c-Number(#Retire_Fund_Diff, $out, $FRetire_Fund)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #CODA_Plans <> #Prv_CODA_Plans
    let #CODA_Plans_Diff = #CODA_Plans - #Prv_CODA_Plans

    do Format-W2c-Number(#Prv_CODA_Plans, $out, $FCODA_Plans)
    do Print-Value($out,#L_CODA_Plans,#C_CODA_Plans,$YE_CODA_Plans)

    do Format-W2c-Number(#CODA_Plans, $out, $FCODA_Plans)
    print $out (,+5)

    do Format-W2c-Number(#CODA_Plans_Diff, $out, $FCODA_Plans)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #SS_Wages <> #Prv_SS_Wages
    let #SS_Wages_Diff = #SS_Wages - #Prv_SS_Wages

    do Format-W2c-Number(#Prv_SS_Wages, $out, $FSS_Wages)
    do Print-Value($out,#L_SS_Wages,#C_SS_Wages,$YE_SS_Wages)

    do Format-W2c-Number(#SS_Wages, $out, $FSS_Wages)
    print $out (,+5)

    do Format-W2c-Number(#SS_Wages_Diff, $out, $FSS_Wages)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #SS_Tax <> #Prv_SS_Tax
    let #SS_Tax_Diff = #SS_Tax - #Prv_SS_Tax

    do Format-W2c-Number(#Prv_SS_Tax, $out, $FSS_Tax)
    do Print-Value($out,#L_SS_Tax,#C_SS_Tax,$YE_SS_Tax)

    do Format-W2c-Number(#SS_Tax, $out, $FSS_Tax)
    print $out (,+5)

    do Format-W2c-Number(#SS_Tax_Diff, $out, $FSS_Tax)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Med_Wages <> #Prv_Med_Wages
    let #Med_Wages_Diff = #Med_Wages - #Prv_Med_Wages

    do Format-W2c-Number(#Prv_Med_Wages, $out, $FMed_Wages)
    do Print-Value($out,#L_Med_Wages,#C_Med_Wages,$YE_Med_Wages)

    do Format-W2c-Number(#Med_Wages, $out, $FMed_Wages)
    print $out (,+5)

    do Format-W2c-Number(#Med_Wages_Diff, $out, $FMed_Wages)
    print $out (,+4) edit 99999999.99ps

  end-if


  if #Med_Tax <> #Prv_Med_Tax
    let #Med_Tax_Diff = #Med_Tax - #Prv_Med_Tax

    do Format-W2c-Number(#Prv_Med_Tax, $out, $FMed_Tax)
    do Print-Value($out,#L_Med_Tax,#C_Med_Tax,$YE_Med_Tax)

    do Format-W2c-Number(#Med_Tax, $out, $FMed_Tax)
    print $out (,+5)

    do Format-W2c-Number(#Med_Tax_Diff, $out, $FMed_Tax)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #SS_Tips <> #Prv_SS_Tips
    let #SS_Tips_Diff = #SS_Tips - #Prv_SS_Tips

    do Format-W2c-Number(#Prv_SS_Tips, $out, $FSS_Tips)
    do Print-Value($out,#L_SS_Tips,#C_SS_Tips,$YE_SS_Tips)


    do Format-W2c-Number(#SS_Tips, $out, $FSS_Tips)
    print $out (,+5)

    do Format-W2c-Number(#SS_Tips_Diff, $out, $FSS_Tips)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #SS_Tax_Tips <> #Prv_SS_Tax_Tips
    let #SS_Tax_Tips_Diff = #SS_Tax_Tips - #Prv_SS_Tax_Tips

    do Format-W2c-Number(#Prv_SS_Tax_Tips, $out, $FSS_Tax_Tips)
    do Print-Value($out,#L_SS_Tax_Tips,#C_SS_Tax_Tips,$YE_SS_Tax_Tips)


    do Format-W2c-Number(#SS_Tax_Tips, $out, $FSS_Tax_Tips)
    print $out (,+5)

    do Format-W2c-Number(#SS_Tax_Tips_Diff, $out, $FSS_Tax_Tips)
    print $out (,+4) edit 99999999.99ps

  end-if

  if #Med_Tax_Tips <> #Prv_Med_Tax_Tips
    let #Med_Tax_Tips_Diff = #Med_Tax_Tips - #Prv_Med_Tax_Tips


    do Format-W2c-Number(#Prv_Med_Tax_Tips, $out, $FMed_Tax_Tips)
    do Print-Value($out,#L_Med_Tax_Tips,#C_Med_Tax_Tips,$YE_Med_Tax_Tips)


    do Format-W2c-Number(#Med_Tax_Tips, $out, $FMed_Tax_Tips)
    print $out (,+5)

    do Format-W2c-Number(#Med_Tax_Tips_Diff, $out, $FMed_Tax_Tips)
    print $out (,+4) edit 99999999.99ps

  end-if


end-procedure



begin-procedure Print-Page1-Data

  UPPERCASE $Prv_Name
  if ($YE_Name <> $Prv_Name)
    do Print-Value($Prv_Name,#L_EE_Incorrect_Name,#C_EE_Incorrect_Name,$F06B)
  end-if


  if ($E.SSN <> $E.GPUS_PSSN)
    do Print-Value($E.GPUS_PSSN,#L_EE_Incorrect_SSN,#C_EE_Incorrect_SSN,$F06A)
  end-if

end-procedure


begin-procedure Next-Form
  do Next-Form-TwoVertical
end-procedure



begin-procedure Load-W2C-PRT

begin-SELECT
F.GPUS_LAYOUT_FORMAT
F.GPUS_TOP_MARGIN
F.GPUS_TOP_SPLIT
F.GPUS_CENTER_SPLIT
F.GPUS_BOTTOM_SPLIT
F.GPUS_LAST_PRT_LINE

  let $Layout_Style = RTRIM(&F.Gpus_Layout_Format,' ')
  move &F.Gpus_Top_Margin       to #Top_Margin
  move &F.Gpus_Top_Split        to #Top_Split
  move &F.Gpus_Center_Split     to #Center_Split
  move &F.Gpus_Bottom_Split     to #Bottom_Split
  move &F.Gpus_Last_Prt_Line    to #Last_Print_Line


! encode '<27>E<27>(0N<27>&l6D<27>&l0E<27>&l70F' into $SetupString
! print $SetupString (1,1)
!            |    |       |       |       |
!            |    |       |       |        --> 70 text lines
!            |    |       |        --> top margin = 0 lines
!            |    |        --> 6 lines per inch
!            |     --> ISO 8859-1 symbol set
!             --> Reset

FROM  PS_GPUS_YE_PRT_FRM F
WHERE F.GPUS_PREPRTFORM = &TX.GPUS_PREPRTFORM
  AND F.GPUS_TAXFORM_ID = $Taxform_ID
  AND F.EFFDT   =
      (SELECT MAX(EFFDT)
       FROM   PS_GPUS_YE_TXFRM
       WHERE  GPUS_TAXFORM_ID = F.GPUS_TAXFORM_ID
         AND  EFFDT  <= $AsOfDate)
end-SELECT

  do assign-year-end-variables

begin-SELECT
P.GPUS_TAXFORM_BOX
P.GPUS_PRINT_LINE
P.GPUS_PRINT_COLUMN
P.GPUS_PRINT_FORMAT
P.GPUS_PRINT_TEXT

  let $P.Box = RTRIM(&P.Gpus_Taxform_Box,' ')
  evaluate $P.Box
    when = $YE_Cost_Pension_annuity
      move &P.Gpus_Print_Line   to #L_Cost_Pension_annuity               ! previous
      move &P.Gpus_Print_Column    to #C_Cost_Pension_annuity
      let $FCost_Pension_annuity  = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Wages
      move &P.Gpus_Print_Line   to #L_Wages              ! previous
      move &P.Gpus_Print_Column    to #C_Wages
      let $FWages = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Commissions
      move &P.Gpus_Print_Line   to #L_Commissions              ! previous
      move &P.Gpus_Print_Column    to #C_Commissions
      let $FCommissions = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Allowance
      move &P.Gpus_Print_Line   to #L_Allowance             ! previous
      move &P.Gpus_Print_Column    to #C_Allowance
      let $FAllowance = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Tips
      move &P.Gpus_Print_Line   to #L_Tips              ! previous
      move &P.Gpus_Print_Column    to #C_Tips
      let $FTips = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $Comm_Allow_Tips_Total
      move &P.Gpus_Print_Line   to #L_TotalAbove             ! previous
      move &P.Gpus_Print_Column    to #C_TotalAbove
      let $FTotalAbove = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Reimbursed_Expense
      move &P.Gpus_Print_Line   to #L_Reimbursed_Expense              ! previous
      move &P.Gpus_Print_Column    to #C_Reimbursed_Expense
      let $FReimbursed_Expense = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Tax_Withheld
      move &P.Gpus_Print_Line   to #L_Tax              ! previous
      move &P.Gpus_Print_Column    to #C_Tax
      let $FTax = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Retire_Fund
      move &P.Gpus_Print_Line   to #L_Retire_Fund             ! previous
      move &P.Gpus_Print_Column    to #C_Retire_Fund
      let $FRetire_Fund = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_CODA_Plans
      move &P.Gpus_Print_Line   to #L_CODA_Plans              ! previous
      move &P.Gpus_Print_Column    to #C_CODA_Plans
      let $FCODA_Plans = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_SS_Wages                          !SS wages
      move &P.Gpus_Print_Line   to #L_SS_Wages              ! previous
      move &P.Gpus_Print_Column    to #C_SS_Wages
      let $FSS_Wages = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_SS_Tax                            !SS Tax
      move &P.Gpus_Print_Line   to #L_SS_Tax              ! previous
      move &P.Gpus_Print_Column    to #C_SS_Tax
      let $FSS_Tax = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Med_Wages                         !Medicare wages
      move &P.Gpus_Print_Line   to #L_Med_Wages              ! previous
      move &P.Gpus_Print_Column    to #C_Med_Wages
      let $FMed_Wages = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Med_Tax                           !Medicare tax
      move &P.Gpus_Print_Line   to #L_Med_Tax              ! previous
      move &P.Gpus_Print_Column    to #C_Med_Tax
      let $FMed_Tax = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_SS_Tips                           !SS tips
      move &P.Gpus_Print_Line   to #L_SS_Tips              ! previous
      move &P.Gpus_Print_Column    to #C_SS_Tips
      let $FSS_Tips = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_SS_Tax_Tips
      move &P.Gpus_Print_Line   to #L_SS_Tax_Tips              ! previous
      move &P.Gpus_Print_Column    to #C_SS_Tax_Tips
      let $FSS_Tax_Tips  = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Med_Tax_Tips
      move &P.Gpus_Print_Line   to #L_Med_Tax_Tips              ! previous
      move &P.Gpus_Print_Column    to #C_Med_Tax_Tips
      let $FMed_Tax_Tips  = RTRIM(&P.Gpus_Print_Format,' ')
      break

    when = $YE_Year
      move &P.Gpus_Print_Line   to #L_Year_and_Form
      move &P.Gpus_Print_Column    to #C_Year_and_Form
      let $FA = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_EE_FullName
      move &P.Gpus_Print_Line   to #L_EE_Full_Name
      move &P.Gpus_Print_Column    to #C_EE_Full_Name
      let $FB1 = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_EE_Address1
      move &P.Gpus_Print_Line   to #L_EE_Address1
      move &P.Gpus_Print_Column    to #C_EE_Address1
      let $FB2 = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_EE_Address2
      move &P.Gpus_Print_Line   to #L_EE_Address2
      move &P.Gpus_Print_Column    to #C_EE_Address2
      let $FB3 = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_State_City_Zip
      move &P.Gpus_Print_Line   to #L_EE_State_City_Zip
      move &P.Gpus_Print_Column    to #C_EE_State_City_Zip
      let $FB4 = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_EE_Incorrect_Name
      move &P.Gpus_Print_Line   to #L_EE_Incorrect_Name
      move &P.Gpus_Print_Column    to #C_EE_Incorrect_Name
      let $FB5 = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_ER_Name
      move &P.Gpus_Print_Line   to #L_ER_Name
      move &P.Gpus_Print_Column    to #C_ER_Name
      let $FER_Name = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_ER_Address1
      move &P.Gpus_Print_Line   to #L_ER_Address1
      move &P.Gpus_Print_Column    to #C_ER_Address1
      let $FER_Address1 = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_ER_Address2
      move &P.Gpus_Print_Line   to #L_ER_Address2
      move &P.Gpus_Print_Column    to #C_ER_Address2
      let $FER_Address2 = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_ER_State_City_Zip
      move &P.Gpus_Print_Line   to #L_State_City_Zip
      move &P.Gpus_Print_Column    to #C_State_City_Zip
      let $FERState_City_Zip = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_ER_Phone
      move &P.Gpus_Print_Line   to #L_ER_Phone
      move &P.Gpus_Print_Column    to #C_ER_Phone
      let $FERPhone = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_ER_Closing
      move &P.Gpus_Print_Line   to #L_ER_Closing
      move &P.Gpus_Print_Column    to #C_ER_Closing
      let $FERClosing = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_EE_SSN
      move &P.Gpus_Print_Line   to #L_EE_SSN
      move &P.Gpus_Print_Column    to #C_EE_SSN
      let $FD = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_ER_ID
      move &P.Gpus_Print_Line   to #L_ER_EIN
      move &P.Gpus_Print_Column    to #C_ER_EIN
      let $FF = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_EE_Incorrect_SSN                        !EE incorrect SSN
      move &P.Gpus_Print_Line   to #L_EE_Incorrect_SSN
      move &P.Gpus_Print_Column    to #C_EE_Incorrect_SSN
      break
    when = $YE_ER_Prv_Control_Number
      move &P.Gpus_Print_Line   to #L_Prv_Control_Number
      move &P.Gpus_Print_Column    to #C_Prv_Control_Number
      let $F27B = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Comments_A
      move &P.Gpus_Print_Line   to #L_Comments_A
      move &P.Gpus_Print_Column    to #C_Comments_A
      let $F26A = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when = $YE_Comments_B
      move &P.Gpus_Print_Line   to #L_Comments_B
      move &P.Gpus_Print_Column    to #C_Comments_B
      let $F26B = RTRIM(&P.Gpus_Print_Format,' ')
      break
    when-other
        move $P.Box to $Box xx
      break
  end-evaluate

FROM  PS_GPUS_YE_PRT_BOX P
WHERE P.GPUS_PREPRTFORM = &TX.GPUS_PREPRTFORM
  AND P.GPUS_TAXFORM_ID = $Taxform_ID
  AND P.EFFDT   =
      (SELECT MAX(EFFDT)
       FROM   PS_GPUS_YE_TXFRM
       WHERE  GPUS_TAXFORM_ID = $Taxform_ID
         AND  EFFDT  <= $AsOfDate)
ORDER BY P.GPUS_TAXFORM_BOX
end-SELECT
end-procedure


begin-procedure Load-Test-Data

  let $A.Federal_EIN =
         LPAD($x,TO_NUMBER(SUBSTR($FF,3,INSTR($FF,')',1)-2)),'X')

  let $A.Descr =
         LPAD($x,TO_NUMBER(SUBSTR($FER_Name,3,INSTR($FER_Name,')',1)-2)),'X')

  let $A.Address1 =
         LPAD($x,TO_NUMBER(SUBSTR($FER_Address1,3,INSTR($FER_Address1,')',1)-2)),'X')

  let $A.Address2 =
         LPAD($x,TO_NUMBER(SUBSTR($FER_Address2,3,INSTR($FER_Address2,')',1)-2)),'X')

  let $State_City_Zip =
         LPAD($x,TO_NUMBER(SUBSTR($FState_City_Zip,3,INSTR($FState_City_Zip,')',1)-2)),'Y')

  let $ER_Year = '9999'

  let $YE_Name =
         LPAD($x,TO_NUMBER(SUBSTR($FB1,3,INSTR($FB1,')',1)-2)),'Y')

  let $Prv_Name =
         LPAD($x,TO_NUMBER(SUBSTR($F06B,3,INSTR($F06B,')',1)-2)),'X')

  let $E.SSN =
         LPAD($x,TO_NUMBER(SUBSTR($FD,3,INSTR($FD,')',1)-2)),'X')

  let $E.GPUS_PSSN = 'YYYYYYYYYYY'

  let $A.Closing_Date = 'MM-DD-YYYY'

  let $Phone = '999-999-9999'

  let $E.Address1 =
         LPAD($x,TO_NUMBER(SUBSTR($FB2F1,3,INSTR($FB2,')',1)-2)),'X')

  let $E.Address2 =
         LPAD($x,TO_NUMBER(SUBSTR($FB3,3,INSTR($FB3,')',1)-2)),'X')

  let $B4 =
         LPAD($x,TO_NUMBER(SUBSTR($FB4,3,INSTR($FB4,')',1)-2)),'X')

  let $E.Comments_A =
         LPAD($x,TO_NUMBER(SUBSTR($F26A,3,INSTR($F26A,')',1)-2)),'X')

  let $E.Comments_B =
         LPAD($x,TO_NUMBER(SUBSTR($F26B,3,INSTR($F26B,')',1)-2)),'X')

  let #Cost_Pension_Annuity = 99999999.99
  let #Prv_Cost_Pension_Annuity = 0.00

  let #Wages = 0.00
  let #Prv_Wages = 99999999.99

  let #Commissions = 99999999.99
  let #Prv_Commissions = 0.00

  let #Allowance = 0.00
  let #Prv_Allowance = 99999999.99

  let #Tips = 99999999.99
  let #Prv_Tips = 0.00

  let #TotalAbove = 0.00
  let #Prv_TotalAbove = 99999999.99

  let #Reimbursed_Expense = 99999999.99
  let #Prv_Reimbursed_Expense = 0.00

  let #Tax_Withheld = 0.00
  let #Prv_Tax_Withheld = 99999999.99

  let #Retire_Fund = 99999999.99
  let #Prv_Retire_Fund = 0.00

  let #CODA_Plans = 0.00
  let #Prv_CODA_Plans = 99999999.99

  let #SS_Wages = 99999999.99
  let #Prv_SS_Wages = 0.00

  let #SS_Tax = 0
  let #Prv_SS_Tax = 99999999.99

  let #Med_Wages = 99999999.99
  let #Prv_Med_Wages = 0.00

  let #Med_Tax = 0
  let #Prv_Med_Tax = 99999999.99

  let #SS_Tips = 99999999.99
  let #Prv_SS_Tips = 0.00

  let #SS_Tax_Tips = 0
  let #Prv_SS_Tax_Tips = 99999999.99

  let #Med_Tax_Tips = 99999999.99
  let #Prv_Med_Tax_Tips = 0.00


end-procedure

begin-procedure Print-Alignment-Forms
  move &TX.GPUS_YE_ALIGNCOUNT  to #Align_Count
  let #Align_Count = #Align_Count * #FormsPerPage
  move #EmployeesPerPage to #temp
  move #FormsPerPage to #EmployeesPerPage
  move 1 to #k
  while #k <= #Align_Count
    do Load-Test-Data
    do Print-W2C
    add 1 to #k
  end-while
  move 0 to #Align_Count
  move #temp to #EmployeesPerPage
  move '' to $priorState
  move '' to $priorLocality
  move '' to $LocalityPrintName

end-procedure

begin-procedure Print-Value($String,#Line,#Col,$Identifier)

  #ifdef TAXTEST
    display $String
    display #Line
    display #Col
    display $Identifier
  #endif

  if #Line > 0
    print $String (#Line,#Col)
  else
    display ''
    display '***************************************'
    display 'Print Line not defined for Box ' noline
    display $Identifier
    display 'in Tax Form Print Parameters Table.'
    display 'Program Stopped.'
    display '***************************************'
    display ''
    stop
  end-if
end-procedure


begin-procedure Read-EEs
let $FirstEE = 'Y'

begin-SELECT
C.EMPLID
C.GPUS_YEC_BATCH_NBR

  move &C.EMPLID  to $SelectedEmplID
  move &C.GPUS_YEC_BATCH_NBR to #W2c_Batch_Nbr
  move &C.GPUS_YEC_BATCH_NBR to $W2c_Batch_Nbr
  move &C.EMPLID  to $ReprintCheck
  let $ReprintCheck = rtrim(&C.EMPLID ,' ')

  if isnull(rtrim($ReprintCheck,'')) and $FirstEE = 'Y'
    do Get-Reprint-Info
  else
    let $E.SelectedEEs = 'AND E.EMPLID = '''
    let $E.SelectedEEs = $E.SelectedEEs || RTRIM($SelectedEmplID  , ' ') || ''''

        let $E.Batch_Nbr = 'AND E.GPUS_YEC_BATCH_NBR = (SELECT MAX(GPUS_YEC_BATCH_NBR) FROM ' ||
       'PS_GPUS_YEC_DAT_PR WHERE E.GPUS_TAX_EIN = GPUS_TAX_EIN ' ||
       'AND E.EMPLID = EMPLID AND E.GPUS_YEC_STATUS = GPUS_YEC_STATUS)'

  end-if
  do Read-Employee-Data

  let $FirstEE = 'N'

FROM  PS_GPUS_RC_YE4 C
WHERE C.OPRID         = $Prcs_OprID
  AND C.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
end-SELECT

end-procedure

begin-procedure Get-Reprint-Info

begin-SELECT
CC.GPUS_YEC_BAT_OPEN

FROM PS_GPUS_YEC_BATCH CC
WHERE CC.GPUS_YEC_BATCH_NBR=&C.GPUS_YEC_BATCH_NBR
AND CC.GPUS_CAL_YEAR=$CalYear
AND CC.GPUS_TAXFORM_ID=$Taxform_ID
end-SELECT

  if &CC.GPUS_YEC_BAT_OPEN  = 'Y'
    do Get-Most-Recent-Batch
  end-if
  if $W2c_Batch_Nbr <> ''
  let $E.Batch_Nbr = 'AND E.GPUS_YEC_BATCH_NBR = ' || $W2c_Batch_Nbr
  end-if
end-procedure


begin-procedure Get-Most-Recent-Batch
begin-SELECT

COUNT(*)   &W2cCount

FROM PS_GPUS_YEC_DAT_PR CT
WHERE CT.GPUS_YEC_BATCH_NBR = &W2C_INSTALL.GPUS_YEC_BATCH_NBR

end-SELECT

if &W2cCount > 0
    let #W2c_Batch_Nbr = &W2C_INSTALL.GPUS_YEC_BATCH_NBR
else
    let #W2c_Batch_Nbr = &W2C_INSTALL.GPUS_YEC_BATCH_NBR  - 1
end-if

let $W2c_Batch_Nbr_X = #W2c_Batch_Nbr
unstring $W2c_Batch_Nbr_X by '.' into $W2c_Batch_Nbr $zeros
end-procedure

begin-procedure Get-W2C-Run-Controls
  do Get-Tax-Reporting-Run-Controls

  move &TX.Telephone_Nbr to $Phone
  let $Taxform_ID = &TX.GPUS_TAXFORM_ID

end-procedure

begin-procedure Get-W2C-Parameters

begin-select
W2C_INSTALL.GPUS_YEC_BATCH_NBR
W2C_INSTALL.GPUS_CAL_YEAR

FROM  PS_GPUS_YEC_BATCH W2C_INSTALL
WHERE GPUS_TAXFORM_ID = $Taxform_ID
end-SELECT


move &W2C_INSTALL.GPUS_YEC_BATCH_NBR to #W2c_Batch_Nbr
move &W2C_INSTALL.GPUS_YEC_BATCH_NBR to $W2c_Batch_Nbr

move &W2C_INSTALL.GPUS_CAL_YEAR   to $W2C_Batch_Yr 9999
move &W2C_INSTALL.GPUS_CAL_YEAR   to #W2C_Batch_Yr
if $W2c_Batch_Nbr <> ''
let $E.Batch_Nbr = 'AND E.GPUS_YEC_BATCH_NBR = ' || $W2c_Batch_Nbr
end-if
end-procedure


begin-procedure Update-W2-History

 let $W2c_Error = 'Update-W2-History '

 begin-SQL on-error=Error-W2c-Processing
  UPDATE PS_GPUS_YE_EE
      SET GPUS_YE_STATUS = '30'
         WHERE EMPLID = &E.EMPLID
          AND GPUS_CAL_YEAR = &E.GPUS_CAL_YEAR
          AND GPUS_TAXFORM_ID = $Taxform_ID
          AND GPUS_TAX_EIN IN (SELECT GPUS_TAX_EIN FROM PS_GPUS_YE_EE0_VW VW
                                  WHERE VW.GPUS_TAX_EIN = &E.GPUS_TAX_EIN
                                AND VW.GPUS_CAL_YEAR = &E.GPUS_CAL_YEAR);
 end-SQL
end-procedure



begin-procedure Update-W2C-Parameters

 let $W2c_Error = 'Update-W2C-Parameters  '

 begin-SQL on-error=Error-W2c-Processing
  UPDATE PS_GPUS_YEC_DAT_PR
   SET GPUS_YEC_STATUS = '20'
  WHERE GPUS_YEC_BATCH_NBR = &W2C_INSTALL.GPUS_YEC_BATCH_NBR
  AND GPUS_YEC_STATUS <> '30';
end-SQL

  do Update-Yec-Batch
end-procedure

begin-procedure Update-Reg-Reprint-Status

 let $W2c_Error = 'Update-Reg-Reprint-Status '

 begin-SQL on-error=Error-W2c-Processing
  UPDATE PS_GPUS_RC_YE2
   SET GPUS_YE_REPRINT  = '1';
 end-SQL

 let $W2c_Error = 'Delete-W2c-EE  '

 begin-SQL on-error=Error-W2c-Processing
  DELETE FROM  PS_GPUS_RC_YE4
  WHERE OPRID         = $Prcs_OprID
  AND RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
 end-SQL

end-procedure


begin-procedure Update-Yec-Batch
let $W2c_Error = 'Update-Yec-Batch'
begin-SQL on-error=Error-W2c-Processing
UPDATE PS_GPUS_YEC_BATCH
 SET GPUS_YEC_BAT_OPEN ='N'
  WHERE GPUS_CAL_YEAR = #W2c_Batch_Yr
  AND GPUS_TAXFORM_ID= $Taxform_ID
  AND GPUS_YEC_BATCH_NBR=#W2c_Batch_Nbr;
end-SQL
end-procedure


begin-procedure Error-W2c-Processing
  display 'W2c Processing Error ' noline
end-procedure


begin-procedure Next-Form-TwoVertical
 if #Form# <> 1
    add 1 to #Form#
  else
    move 1 to #Form#
  end-if
  evaluate #Form#                 !use to position for next forms
    when = 1
      if $First_time = 'N'
         new-page
      end-if
      next-column
      do Print-Value(' ',#Top_Margin,1,'Top Margin')
      do Print-Value(' ',#Last_Print_Line,1,'Last Print Line')

      break

  end-evaluate

  let $First_time = 'N'

end-procedure


begin-procedure Format-W2c-Number(#in, :$out, $mask)
  if #in <> 0
    move #in to $out :$mask
    let $out = translate($out,',.',$_Delimiters)
  else
      let $out = '           0'
  end-if
end-procedure

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'gpusw2c.sqc'   !Routine for W2 Year End W-2c Box variables
#Include 'gpusrntl.sqc'  !Retrieve data from Tax Reporting Run Control record