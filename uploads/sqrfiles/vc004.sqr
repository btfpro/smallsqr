!***********************************************************************
!  VC004:  Variable Compensation - Carryover Awards Report             *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/09/06:01:01:21                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  689848                                              !
!                                                                      *
!***********************************************************************
!
! Implements row-level security via PS_FAST_SQR_SEC_VW in hrsecty.sqc  *
!                                                                      *
! HP99999       Release 8                                              *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

#define GB_GROUP_TBL
#define VC_PLAN_TBL

#Include 'rellang.sqc'

Begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report

  if $start-error <> 'Y'
    do Report
  end-if
  do Stdapi-Term
  do Reset
end-report

!********************************************
begin-procedure Init-Report
  move 'VCREPS' to $ReportID
  move 'Carryover Awards Report' to $ReportTitle
  display $ReportTitle
  display ' '
  do Stdapi-init
  do Get-Report-Language   ! Procedure that decides the report language
                           ! and then calls 'Report-Translation' procedure
                           ! that reads the language dep. text.

  display $reportid
  display 'lang - '
  display $language_cd

  if $prcs_process_instance = ''
     display 'This process cannot be run outside the Process scheduler'
     let $start_error = 'Y'
  else
     let $start_error = 'N'
     do Security-Param
     do Select-Parameters
  end-if
end-procedure

!*********************************************************************
begin-procedure select-parameters

  !Setup for row-level security...
  let $_TableAlias = 'VA'
  do Security-Param

BEGIN-SELECT
A.OPRID
A.RUN_CNTL_ID
A.VC_PLAN_ID
A.VC_PAYOUT_PRD_ID
A.PRD_END_DT
A.TREE_NAME
A.VC_TREE_DT
A.VC_REPORT_TYPE
A.VC_PLAN_MEM_CRIT

  move &A.PRD_END_DT to $prd_end_dt
  do Format-DateTime($prd_end_dt, $prd_end_dt, {DEFYMD}, '', '')

  if rtrim(&A.VC_PLAN_MEM_CRIT,' ') = 'E' or rtrim(&A.VC_PLAN_MEM_CRIT,' ') = 'A'
     let $grp_flag = 'N'
     let $sec_from = ''
     let $sec_where = $SecurityClauseWithERN
     let $sec_list = '''NOGROUP'''
  else
     let $grp_flag = 'Y'
     let $sec_from = ',PS_GB_GROUP_TBL    GT, PS_GB_SECGRPOPR_TB GS'
     let $sec_where = 'AND VA.GB_GROUP_ID = GT.GB_GROUP_ID AND GT.EFFDT =
                        (SELECT MAX(G2.EFFDT) FROM PS_GB_GROUP_TBL G2
                        Where G2.GB_GROUP_ID = GT.GB_GROUP_ID AND
                              G2.EFFDT <= '''||&A.PRD_END_DT||''''||')
                       AND GT.GB_GROUP_ID = GS.GB_GROUP_ID AND GS.GB_OPRID = '''||$prcs_oprid||''''
     let $sec_list = 'GT.DESCR'
  end-if

from PS_RC_VC_REPORTS A
where A.OPRID       = $prcs_oprid and
      A.RUN_CNTL_ID = $prcs_run_cntl_id
end-select
if &A.VC_REPORT_TYPE = 'D'
    let $reportname = $reportname||' - '||$Detail
 else
    let $reportname = $reportname||' - '||$Summary
 end-if
end-procedure

!**************************************************************************
!    Report procedure
!**************************************************************************
begin-procedure Report
 Position (1,1)

 Move 0 To #Group_carryover
 Move 0 to #total_carryover

 let $init_group = ''

Begin-Select
PT.DESCR50
VA.VC_CARRYOVER_BAL
VA.GB_GROUP_ID
VA.VC_PLAN_ID
VA.VC_PAYOUT_PRD_ID
VA.EMPLID
VA.EMPL_RCD
VA.CURRENCY_CD
PD.NAME
[$sec_list] &GT_DESCR=char

  do getrellang

  if &VA.GB_GROUP_ID <> $init_group and $grp_flag = 'Y'
     if $init_group <> ''
        !print group totals
        if &A.VC_REPORT_TYPE = 'D'
          position (+1,)
          Print $total_group (,60)
        end-if
        Print #group_carryover (,80) edit 999999999999.999999
        position (+2,)
        do getchild
     end-if
     position (+1)
     Print $group (,1)
     Print &VA.GB_GROUP_ID (,9)
     Print $GT_DESCR (,25)
     if &A.VC_REPORT_TYPE = 'D'
       position (+1,)
     end-if
     move 0 to #group_carryover
     let $init_group = &VA.GB_GROUP_ID
  end-if
  let #co_bal = &VA.VC_CARRYOVER_BAL
  let #group_carryover = #group_carryover + #co_bal
  let #total_carryover = #total_carryover + #co_bal

  !print row
  if &A.VC_REPORT_TYPE = 'D'
    do printrow
  end-if

From    PS_VC_AWARD VA,
        PS_PERSON_NAME PD,
        PS_VC_PLAN_TBL PT
        [$sec_from]
Where   VA.VC_PLAN_ID = &A.VC_PLAN_ID AND
        VA.VC_PAYOUT_PRD_ID = &A.VC_PAYOUT_PRD_ID AND
        VA.VC_PLAN_ID = PT.VC_PLAN_ID AND
        VA.EMPLID = PD.EMPLID
        [$sec_where]
Order By VA.GB_GROUP_ID,VA.EMPLID,VA.EMPL_RCD
End-Select

if $grp_flag = 'Y'
  if &A.VC_REPORT_TYPE = 'D'
    position (+1,)
    Print $total_group (,60)
  end-if
  Print #group_carryover (,80) edit 999999999999.999999
  position (+2,)
  do getchild
end-if

position (+2,)
Print #total_carryover (,80) edit 999999999999.999999
Print $total_plan (,60)

end-procedure

!********************************************************
Begin-procedure getchild

move &A.PRD_END_DT to $prd_end_dt
do Format-DateTime($prd_end_dt, $prd_end_dt, {DEFYMD}, '', '')

begin-select
TG.GB_CHILD_GROUP_ID
GT.DESCR

  let $child_group = &TG.GB_CHILD_GROUP_ID
  do getsum
  if &A.VC_REPORT_TYPE = 'D'
    print $child_group (,8)
    print &GT.DESCR (,25)
    print #sum_carry (,80) edit 99999999999.999999
  position (+1,)
  end-if

  let #group_carryover = #group_carryover + #sum_carry
  let #total_carryover = #total_carryover + #sum_carry

from PS_VC_TREE_GRP_VW TG,
     PS_GB_GROUP_TBL    GT,
     PS_GB_SECGRPOPR_TB GS
where TG.TREE_NAME = &A.TREE_NAME AND
      TG.TREE_EFFDT = &A.VC_TREE_DT AND
      TG.GB_GROUP_ID = &VA.GB_GROUP_ID AND
      TG.GB_GROUP_ID = GT.GB_GROUP_ID AND
      GT.EFFDT =  (SELECT MAX(G2.EFFDT) FROM PS_GB_GROUP_TBL G2
                   Where G2.GB_GROUP_ID = GT.GB_GROUP_ID AND
                         G2.EFFDT <= &A.PRD_END_DT ) AND
      GT.GB_GROUP_ID = GS.GB_GROUP_ID  AND
      GS.GB_OPRID = '''||$prcs_oprid||'''
end-select
end-procedure

!********************************************************
Begin-procedure getsum

move 0 to #sum_carry
begin-select
VA2.VC_CARRYOVER_BAL
  let #sum_carry = #sum_carry + &VA2.VC_CARRYOVER_BAL
from PS_VC_AWARD VA2
where   VA2.VC_PLAN_ID = &A.VC_PLAN_ID AND
        VA2.VC_PAYOUT_PRD_ID = &A.VC_PAYOUT_PRD_ID AND
        VA2.GB_GROUP_ID = &TG.GB_CHILD_GROUP_ID
end-select
end-procedure

!********************************************************
Begin-procedure PrintRow
  print &VA.EMPLID (,8)
  print &VA.EMPL_RCD (,21)
  print &PD.NAME (,27)
  print &VA.VC_CARRYOVER_BAL (,80) edit 999999999999.999999
  position (+1,)
end-procedure

!********************************************************
Begin-Heading 10
 Page-Number (1,100)
 Print $current-date (2,90) edit 'MM/DD/YYYY'
 Print $reportname (2,30)

 Print $plan_id (4,2)
 let $plan_info =  &VA.VC_PLAN_ID || ' ' || $PT.DESCR50
 Print $plan_info (4,22)
 Print $payout_prd_id (5,2)
 Print &VA.VC_PAYOUT_PRD_ID (5,22)
 Print $curr_cd (6,2)
 Print &VA.Currency_CD (6,22)

 if &A.VC_REPORT_TYPE = 'D'
   Print $emplid (10,8)
   Print $empl_rcd (10,21) wrap 4 2
   Print $name (10,27)

 end-if
 let #pos = 80+(19 - (length($carryover_balance)))
 Print $carryover_balance (10,#pos)
End-Heading

!********************************************************
Begin-procedure getrellang

if rtrim(&A.VC_PLAN_MEM_CRIT,' ') = 'G' or rtrim(&A.VC_PLAN_MEM_CRIT,' ') = 'T'

begin-select
MAX(G2.EFFDT)  &G2_EFFDT=char
FROM PS_GB_GROUP_TBL G2
WHERE G2.GB_GROUP_ID = &VA.GB_GROUP_ID
AND   G2.EFFDT <= &A.PRD_END_DT
end-select

! Get Related Language info for PS_GB_GROUP_TBL
  move &GT_DESCR to $GB_GROUP_TBL-DESCR
  do Get_Related_GB_GROUP_TBL(&G2_EFFDT,&VA.GB_GROUP_ID)
  move $GB_GROUP_TBL-DESCR to $GT_DESCR

end-if

! Get Related Language info for PS_VC_PLAN_TBL
  move &PT.DESCR50 to $VC_PLAN_TBL-DESCR50
  do Get_Related_VC_PLAN_TBL(&A.VC_PLAN_ID)
  move  $VC_PLAN_TBL-DESCR50 to $PT_DESCR50

end-procedure

!**************************************************************************
begin-procedure report-translation
!**************************************************************************
do Init_Report_Translation($ReportID,$Language_Cd)

do Get_Field_Information ('VCREPS','ACTUAL_AWARD',$actual_award,#DW)
do Get_Field_Information ('VCREPS','ACTUAL_FUNDING',$actual_funding,#DW)
do Get_Field_Information ('VCREPS','ANNUAL_RATE',$annual_rate,#DW)
do Get_Field_Information ('VCREPS','ATTAINED',$attained,#DW)
do Get_Field_Information ('VCREPS','VC_CARRYOVER_BAL',$carryover_balance,#DW)
do Get_Field_Information ('VCREPS','EMPLID',$emplid,#dw)
do Get_Field_Information ('VCREPS','EMPL_RCD',$empl_rcd,#DW)
do Get_Field_Information ('VCREPS','GROUP',$group,#DW)
do Get_Field_Information ('VCREPS','GROUP_GOALS',$group_goals,#DW)
do Get_Field_Information ('VCREPS','GUARANTEE',$guarantee,#DW)
do Get_Field_Information ('VCREPS','HIRE_DATE',$hire_date,#DW)
do Get_Field_Information ('VCREPS','JOB_CODE',$job_code,#DW)
do Get_Field_Information ('VCREPS','MAX_AWARD',$max_award,#DW)
do Get_Field_Information ('VCREPS','MIN/MAX',$minmax,#DW)
do Get_Field_Information ('VCREPS','MIN_AWARD',$min_award,#DW)
do Get_Field_Information ('VCREPS','NAME',$name,#DW)
do Get_Field_Information ('VCREPS','OVERALL_PERF_FACT',$overall_perf_factor,#DW)
do Get_Field_Information ('VCREPS','VC_PAYOUT_PRD_ID',$payout_prd_id,#DW)
do Get_Field_Information ('VCREPS','PERFORMACE_FACTOR',$parf_factor,#DW)
do Get_Field_Information ('VCREPS','PLAN ID',$plan_id,#DW)
do Get_Field_Information ('VCREPS','PROJECTED_PAYOUT',$projected_payout,#DW)
do Get_Field_Information ('VCREPS','VC_TARGET_AWARD',$target_award,#DW)
do Get_Field_Information ('VCREPS','VC_TARGET_VALUE',$target_value,#DW)
do Get_Field_Information ('VCREPS','TOTAL_PLAN',$total_plan,#DW)
do Get_Field_Information ('VCREPS','TOTAL_GROUP',$total_group,#DW)
do Get_Field_Information ('VCREPS','UNALLOCATED_BAL',$unallocated_balance,#DW)
do Get_Field_Information ('VCREPS','VALUATION',$valuation,#DW)
do Get_Field_Information ('VCREPS','VARIANCE',$variance,#DW)
do Get_Field_Information ('VCREPS','WEIGHTING',$weighting,#DW)
do Get_Field_Information ('VCREPS','DETAIL',$detail,#DW)
do Get_Field_Information ('VCREPS','SUMMARY',$summary,#DW)
do Get_Field_Information ('VCREPS','CURR_CD',$curr_cd,#DW)
do Get_Field_Information ('VCREPS','VC_AWARD_VALUE',$award_value,#DW)
do Get_Field_Information ('VCREPS','VC004',$reportname,#DW)

let $group = $group||' :'

end-procedure

!*****************************************************************
#Include 'stdapi.sqc'    !Routine to update run Status
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'datemath.sqc'
#Include 'sqrtrans.sqc'
#Include 'getrplng.sqc'  !Get the report language
#include 'hrsecty.sqc'   !Get SQR Security parameters
