!************************************************************************
! FGMIXCHR.SQC:  Convert UpperCase to MixedCase                         *
!************************************************************************
!************************************************************************
!                                                                       *
!               Confidentiality Information:                            *
!                                                                       *
!   This module contains confidential and proprietary information       *
!   of Oracle; it is not to be copied, reproduced, or transmitted       *
!   in any form, by any means, in whole or in part, nor is it to        *
!   be used for any purpose other than that for which it is             *
!   expressly provided under the applicable license agreement.          *
!                                                                       *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                     *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2006/07/19:12:07:57                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                       *
!************************************************************************
!-----------------------------------------------------------------------!
! Procedure:    Mixed-Case-Conversion                                   !
!                                                                       !
! Description:  Converts Uppercase to Mixedcase and Strips Excess       !
!               Blanks.  This procedure functions like the ASCII        !
!               INITCAP function.  It changes the first letter of a     !
!               word or series of words into uppercase.  Also it        !
!               notes the presence of symbols, and will INITCAP any     !
!               letter that follows a space or a symbol, such as a      !
!               comma, period, colon, semicolon, !, @, and so on.       !
!               It DIFFERS from INITCAP when it encounters a number     !
!               followed by a letter(a-z) it WILL NOT capitalize the    !
!               letter, this feature accommodates street addresses      !
!               (17TH STREET, N.W. will convert to 17th Street, N.W.).  !
!               Additionally, it WILL capitalize the last letter of a   !
!               string if it follows a number (APT 701A converts to     !
!               Apt 701A). This procedure is a tool to ASSIST in        !
!               mixedcase conversion output must be reviewed and        !
!               adjusted for special circumstances.                     !
!                                                                       !
!-----------------------------------------------------------------------!
begin-procedure Init-Chrtbl-Array

create-array name=chrtbl size=1 field=ltr:char:100

end-procedure

begin-procedure Mixed-Case-Conversion

  while 1 = 1
     let $charfield = (lower(ltrim(rtrim($parmfield,' '),' ')))
     if isnull($charfield)
       move ' ' to $charfield
       break
     end-if

     !Load the array character by character with passed field

     let #j = 0
     let #len = length($charfield)
     while #j <= #len - 1
           let $chars = substr($charfield,#j+1,1)
           put $chars into chrtbl(0) ltr(#j)
           add 1 to #j
     end-while

    !Evaluate the Array character by character

     let #j = 0                 !Control for Stepping through Chrtbl Array
                                !Action Flags - 1=ON, 0=OFF
     let #cap = 1               !Set Capitalization Flag ON
     let #blank = 0             !Set Last Char Blank Flag OFF
     let #num = 0               !Set Last Char Numberic Flag OFF
     let #log-len = 0           !Blank Stripped Length of Field

     while #j <= #len - 1
        get $chars from chrtbl(0) ltr(#j)
        if ascii($chars) > 47 and ascii($chars) < 58    !If NUMERIC
            move 0 to #blank                            !Set Last Char Blank OFF
            move 1 to #num                              !Set Last Char Numeric ON
            goto Accept                                 !Accept Char As Is
        end-if

        if ascii($chars) > 96 and ascii($chars) < 123   !If Lowercase LETTER
            if (#cap = 1 and #num=0)                    !If Caps ON and last NOT Number OR
              or (#num=1 and #j = #len - 1)             !Last Char Numeric ON and End of String
                uppercase $chars                        !Cap It
                move 0 to #cap                          !Set Caps Flag OFF
            end-if
            move 0 to #blank                            !Set Last Char Blank OFF
            goto Accept                                 !Accept Character
        end-if

        if ascii($chars) = 32                           !If BLANK Character
          if #blank = 1                                 !If Last Char Blank
            goto Skip                                   !Skip this Blank
          else
            move 1 to #cap                              !Set Capitialize ON
            move 1 to #blank                            !Set Last Char Blank ON
            move 0 to #num                              !Set Last Char Numeric OFF
            goto Accept                                 !Accept Blank
          end-if
        end-if

        move 1 to #cap                                  !If Special Char - Set Capitialize ON
        move 0 to #num                                  !Set Last Char Numeric OFF
        move 0 to #blank                                !Set Last Char Blank OFF

Accept:
        put $chars into chrtbl(0) ltr(#log-len)         !Load Character into Array
        add 1 to #log-len                               !Keep Track of Characters in String

Skip:
        add 1 to #j                                     !Read Next Character in String
     end-while

!Unload array into return field

     move 0 to #j
     get $charfield from chrtbl(0) ltr(#j)
     add 1 to #j
     while #j <= #log-len - 1
         get $chars from chrtbl(0) ltr(#j)
         concat $chars with $charfield
         add 1 to #j
     end-while
     move $charfield to $parmfield
     break
  end-while

end-procedure

