!*************************************************
!  APP013:  Job Requisition for Works Council   *
!*************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:13:40:55                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'     !Set environment
begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

#define HRS_STS_TBL
#Include 'rellang.sqc'    ! Translations File

begin-Report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
last2:
end-report

begin-Heading 3
  #Include 'stdhdg01.sqc'
end-Heading

!********************************************
begin-procedure Init-Report

  move 'APP013' to $ReportID
  do Stdapi-Init
   if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
     goto last1
  end-if

  if $prcs_process_instance <> ''
    do Select-Parameters
  end-if
do Init_Printer
do Init_Report_translation ($ReportID, $language_cd)
do Append_Report_Translation ('HR')
last1:
end-procedure

!********************************************
begin-procedure Get-Values
   let $language_cd = $prcs_language_cd
   let $Ctl_Company = &HRS_RUN_JOB.COMPANY
   let $Ctl_Wrks_Cncl = &HRS_RUN_JOB.WC_COUNCIL_ID
end-procedure

!************************************************
begin-procedure Init-Layout

   #define col0 1               !Label
   #define col1 35              !Felder
   #define col2 50              !Description
   #define col3 90              !Erforderlich...
   #define col4 75              !
   #define col5 80              !

end-procedure

!********************************************
begin-procedure Process-Main

do Get-Labels

!-------A.GRADE_TYPE
BEGIN-SELECT
A.HRS_JOB_OPENING_ID          () on-break print=never procedure=Print-New-Page
A.BUSINESS_UNIT
A.STATUS_CODE
A.STATUS_DT
A.COMPANY
A.HRS_PRM_LOCATION
A.DEPTID
A.WC_COUNCIL_ID
A.HRS_PRM_JOBCODE
C.BEGIN_DT
A.OPENINGS_CNT
C.SHIFT
C.STD_HOURS
C.STD_HRS_FREQUENCY
C.SAL_ADMIN_PLAN
C.GRADE_FROM
C.STEP_FROM
C.GRADE_TO
C.STEP_TO
B.HIGHEST_EDUC_LVL
B.YEARS_OF_EXP


 let $BU = &A.BUSINESS_UNIT
 do Get-Location-Setid
 do Print-Requisition
 do Print-Skill
 do Print-Test

FROM PS_HRS_JOB_OPENING A, PS_HRS_JO_EDUC_EXP B, PS_HRS_JO_RQMT C
WHERE    A.COMPANY = $Ctl_Company
AND A.WC_COUNCIL_ID = $Ctl_Wrks_Cncl
AND A.HRS_JOB_OPENING_ID = B.HRS_JOB_OPENING_ID
AND A.HRS_JOB_OPENING_ID = C.HRS_JOB_OPENING_ID
AND B.HRS_JO_RQMT_SEQ = C.HRS_JO_RQMT_SEQ


END-SELECT

end-procedure

!********************************************
begin-procedure Print-Requisition

  print $WRK_CNL                  (4,{col0},33)
  print &A.WC_COUNCIL_ID          (0,{col1},14)
  let $Wks_Cnl_Company = rtrim (&A.COMPANY, ' ')
  let $Wks_Cnl_Wrks_Cncl_Id = rtrim (&A.WC_COUNCIL_ID, ' ')
  if $Wks_Cnl_Wrks_Cncl_Id <> ''
    do Get-Works-Cncl-Data
    print $Wks_Cnl_Descr                 (0,{col2})
  end-if

  print '_'                              (+2,{col0},123) FILL

  print $JOB_POSTING_ID        (+2,{col0},33)
  print &A.HRS_JOB_OPENING_ID              (0,{col1},14)
  print $STATUS                      (0,{col2})

  do Get-Status-Description
  print $StatusCodeDescr                  (0,+2)

  print $DATE                        (0,{col4})
  do Format-DateTime(&A.STATUS_DT, $out, {DEFDATE}, '', '')
  print $out                             (0,+1)

  print '_'                              (+2,{col0},123) FILL

  print $xCOMPANY                  (+2,{col0},33)
  print &A.COMPANY                       (0,{col1},14)
  let $Company = rtrim(&A.COMPANY, ' ')
  if $Company <> ''
    do Get-Company-Data
    print $CompanyName                   (0,{col2})
  end-if

  print $xLOCATION                     (+1,{col0},33)
  print &A.HRS_PRM_LOCATION             (0,{col1},14)
  let $Location = rtrim(&A.HRS_PRM_LOCATION, ' ')
  if $Location <> ''
     do Get-Location-Name
     print $LocationName                 (0,{col2})
  end-if

  print $xDEPTID                    (+1,{col0},33)
  print &A.DEPTID                        (0,{col1},14)
  let $DeptID = rtrim(&A.DEPTID, ' ')
  if $DeptID <> ''
    do Get-Department-Name
    print $DeptName                      (0,{col2})
  end-if


  print $JOB                     (+1,{col0},33)
  print &A.HRS_PRM_JOBCODE                      (0,{col1},14)
  let $JobCode = rtrim(&A.HRS_PRM_JOBCODE, ' ')
  if $JobCode <> ''
    move ' ' to $SETID
    do Get-Job-Setid
    move &SCG.SETID to $SETID
   do Get-Job-Title
    print $JobTitle                      (0,{col2})
  end-if

  print $START_DATE                       (+3,{col0},33)
  do Format-DateTime(&C.BEGIN_DT, $out, {DEFDATE}, '', '')
  print $out                             (0,{col1})

  print $OPEN_POS        (+1,{col0},33)
  print &A.OPENINGS_CNT                  (0,{col1})

  print $JOB_DESCR          (+2,{col0},33)
  do Get-Job-Description
  print $JobDescriptionDuties            (0,{col1}) WRAP 50 10

  print $SHIFT                      (+2,{col0},33)
  print &C.SHIFT                         (0,{col1})

  print $STD_HOURS     (+1,{col0},33)
  print &C.STD_HOURS                     (0,{col1})

  print $WORK_PERIOD     (+1,{col0},33)
  let $std_hrs_freq = rtrim(&C.STD_HRS_FREQUENCY, ' ')
  !print $std_hrs_freq                    (0,{col1},14)
  ! Determine descrshort of $std_hrs_freq
  do Det-Annual-Factor($std_hrs_freq,&A.STATUS_DT,$freq_descrshort,$dummy1,#dummy2,$dummy3)
  print $freq_descrshort                 (0,{col1})


  print $COMP_CATEG                (+1,{col0},33)
  print $FROM                           (0,{col1})
  print &C.SAL_ADMIN_PLAN                (0,+1)
  if rtrim (&C.GRADE_FROM, ' ') <> ''
    print '/'                            (0,+1)
    print &C.GRADE_FROM                  (0,+1)
  end-if
  if &C.STEP_FROM <> 0
    print '/'                            (0,+1)
    print &C.STEP_FROM                   (0,+1)
  end-if

  print $TO                           (0,{col4})
  print &C.SAL_ADMIN_PLAN                (0,+1)
  if rtrim (&C.GRADE_TO, ' ') <> ''
    print '/'                            (0,+1)
    print &C.GRADE_TO                    (0,+1)
  end-if
  if &C.STEP_TO <> 0
    print '/'                            (0,+1)
    print &C.STEP_TO                     (0,+1)
  end-if

 ! print $RECRUITER_NAME                  (+2,{col0},33)
 ! print &A.RECRUITER_NAME                (0,{col1})

  print '_'                              (+2,{col0},123) FILL

  print $JOB_OPENING                       (+2,{col0})

  print $PRI_WRK_EXP                     (+2,{col0},33)
  do Format-Number (&B.YEARS_OF_EXP, $YOE, '9.9')
  print $YOE                             (0,{col1})
  print $YEARS_OF_EDUCATN                (0,+1)

  print $SCH_EDUC                        (+2,{col0},33)
  if &B.HIGHEST_EDUC_LVL <> 'A'
    move 'HIGHEST_EDUC_LVL'   to $FieldName
    move &B.HIGHEST_EDUC_LVL  to $FieldValue
    do Read-Translate-Table
    print $XlatLongName                    (0,{col1},54)
  end-if

 ! if (&C.EDUC_EXP_MAND = 'Y')
 !   print $NEEDED                        (0,{col3})
 ! end-if

  print $PROF_EDUC                       (+2,{col0},88)
  !if (&B.REQUIRED = 'Y')
  !  print $NEEDED                        (0,{col3})
  !end-if
end-procedure


!********************************************
begin-procedure Print-New-Page
  new-page
end-procedure

!********************************************
begin-procedure Print-Skill

print $SKILLS                            (+2,{col0})

begin-SELECT

SKL.COMPETENCY                           (+1,{COL1},14)
SKT.DESCR                                (0,{COL2})
!SKL.REQUIRED

! if (&SKL.REQUIRED = 'Y')
!    print $NEEDED                        (0,{col3},39)
!  end-if

FROM PS_HRS_JO_CMP SKL, PS_COMPETENCY_TBL SKT
WHERE SKL.COMPETENCY = SKT.COMPETENCY
AND SKL.HRS_JOB_OPENING_ID = &A.HRS_JOB_OPENING_ID
AND SKT.CM_CATEGORY = 'S'

end-SELECT


end-procedure

!********************************************
begin-procedure Print-Test

print $TESTS                             (+2,{col0})

begin-SELECT

TST.ACCOMPLISHMENT                       (+1,{COL1},14)
TBL.DESCR                                (0,{COL2},39)
!TST.REQUIRED

! if (&SKL.REQUIRED = 'Y')
!    print $NEEDED                        (0,{col3})
! end-if

FROM PS_HRS_JO_ACMP TST, PS_ACCOMP_TBL TBL
WHERE TST.ACCOMPLISHMENT = TBL.ACCOMPLISHMENT
AND   TST.HRS_JOB_OPENING_ID = &A.HRS_JOB_OPENING_ID
AND   TBL.ACCOMP_CATEGORY = 'TST'

end-SELECT

end-procedure

!********************************************
begin-procedure Get-Job-Description
 let $JobDescriptionDuties = ' '
begin-SELECT
D.DESCRLONG
 let $JobDescriptionDuties = &D.DESCRLONG
FROM  PS_HRS_JO_PST_DSCR D
WHERE D.HRS_JOB_OPENING_ID = &A.HRS_JOB_OPENING_ID
AND   D.HRS_JO_DESCR_TYP  = 'D'
end-SELECT
end-procedure

!********************************************
begin-procedure Get-Location-Setid
begin-SELECT
SET_CNTRL_REC.SETID
 let $Setid = &SET_CNTRL_REC.SETID
FROM PS_SET_CNTRL_REC SET_CNTRL_REC
WHERE SET_CNTRL_REC.SETCNTRLVALUE = $BU
AND SET_CNTRL_REC.RECNAME = 'LOCATION_TBL'
end-SELECT
end-procedure

!***********************************************************************
!  PROC Get-Job-Setid
!***********************************************************************

BEGIN-PROCEDURE Get-Job-Setid
BEGIN-SELECT
SCG.SETID
FROM PS_SET_CNTRL_GROUP SCG
WHERE SETCNTRLVALUE = $BU
  AND REC_GROUP_ID = 'HR_02'
END-Select
END-PROCEDURE

!********************************************
begin-procedure Get-Status-Description
 let $StatusCodeDescr = ' '

begin-SELECT
ERS.STATUS_AREA
ERS.STATUS_CODE
ERS.DESCR

   let $ER_STATUS_TBL-DESCR = rtrim(&ERS.DESCR, ' ')
  ! do Get_Related_ER_STATUS_TBL(&ERS.STATUS_AREA, &ERS.STATUS_CODE)
   let $StatusCodeDescr = $ER_STATUS_TBL-DESCR

FROM  PS_HRS_STS_TBL ERS
WHERE ERS.STATUS_AREA = '1'
AND   ERS.STATUS_CODE = &A.STATUS_CODE
end-SELECT
end-procedure

!********************************************
begin-procedure Get-Labels
do Get_Field_Information ('APP013', 'REPORT_TITLE', $REPORTTITLE, #DW)
do Get_Field_Information ('APP013', 'COMPANY', $xCOMPANY, #DW)
do Get_Field_Information ('APP013', 'COMP_CATEG', $COMP_CATEG, #DW)
do Get_Field_Information ('APP013', 'DATE', $DATE, #DW)
do Get_Field_Information ('APP013', 'DEPTID', $xDEPTID, #DW)
do Get_Field_Information ('APP013', 'FROM', $FROM, #DW)
do Get_Field_Information ('APP013', 'HOURLY', $HOURLY, #DW)
do Get_Field_Information ('APP013', 'JOB', $JOB, #DW)
do Get_Field_Information ('APP013', 'JOB_DESCR', $JOB_DESCR, #DW)
do Get_Field_Information ('APP013', 'JOB_POSTING_ID', $JOB_POSTING_ID, #DW)
do Get_Field_Information ('APP013', 'JOB_REQ', $JOB_REQ, #DW)
do Get_Field_Information ('APP013', 'LOCATION', $xLOCATION, #DW)
do Get_Field_Information ('APP013', 'MONTHLY', $MONTHLY, #DW)
do Get_Field_Information ('APP013', 'NEEDED', $NEEDED, #DW)
do Get_Field_Information ('APP013', 'OPEN_POS', $OPEN_POS, #DW)
do Get_Field_Information ('APP013', 'PRI_WRK_EXP', $PRI_WRK_EXP, #DW)
do Get_Field_Information ('APP013', 'PROF_EDUC', $PROF_EDUC, #DW)
do Get_Field_Information ('APP013', 'RECRUITER_NAME', $RECRUITER_NAME, #DW)
do Get_Field_Information ('APP013', 'SCH_EDUC', $SCH_EDUC, #DW)
do Get_Field_Information ('APP013', 'SHIFT', $SHIFT, #DW)
do Get_Field_Information ('APP013', 'SKILLS', $SKILLS, #DW)
do Get_Field_Information ('APP013', 'START_DATE', $START_DATE, #DW)
do Get_Field_Information ('APP013', 'STATUS', $STATUS, #DW)
do Get_Field_Information ('APP013', 'STD_HOURS', $STD_HOURS, #DW)
do Get_Field_Information ('APP013', 'STD_HRS_FREQUENCY', $WORK_PERIOD, #DW)
do Get_Field_Information ('APP013', 'TESTS', $TESTS, #DW)
do Get_Field_Information ('APP013', 'TO', $TO, #DW)
do Get_Field_Information ('APP013', 'WRK_CNL', $WRK_CNL, #DW)
do Get_Field_Information ('APP013', 'YEARS_OF_EDUCATN', $YEARS_OF_EDUCATN, #DW)
end-procedure

!****************************************************************

#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'getcodta.sqc'  !Get-Company-Name procedure
#Include 'getjobtl.sqc'  !Get-Job-Title procedure
#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'getlocnm.sqc'  !Get-Location-Name procedure
#Include 'getwrkcn.sqc'  !Get-Works-Cncl-Data procedure
#Include 'frquency.sqc'  !Det-Annual-Factor procedure

#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
