!***********************************************************************
!  EXPACTLN:   Export HR_ACCTG_LINE for Public Sector                  *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:11:43:37                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
! Extract HR_ACCTG_LINE from the Database to the data file.            *
!                                                                      *
! Tables :  PS_HR_ACCTG_LINE                                           *
!                                                                      *
!***********************************************************************
! This program is specific to HRMS for Public Sector.                  *
!***********************************************************************


#include 'setenv.sqc'     !Set environment
#include 'setup31.sqc'    !Printer and page-size initialization


BEGIN-REPORT

  do Initialization
  do Open-File

  display 'Report began at: ' NoLine
  display $AsOfNow
  do Process-Main
  display 'Report ended at: ' NoLine
  display $AsOfNow

  close 1

  do Reset
  do StdAPI-Term

END-REPORT

BEGIN-PROCEDURE Initialization

  move 'EXPACTLN' to $ReportID
  move 'Extract HR_ACCTG_LINE' to $ReportTitle
  display $ReportID     NoLine
  display ':'           NoLine
  display $ReportTitle
  display ' '
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do StdAPI-Init

! Get Process Instance for SQR program which creates HR_ACCTG_LINE
! in order to retrieve HR_ACCTG_LINE for the current Job Definition run
  do Get-Job-Instance
  do Get-SQR-Report-Process-Instance

  do Get-FileName

  move 0 to #Total_Acctg_Line
  move ' ' to $Blank

END-PROCEDURE


BEGIN-PROCEDURE Process-Main

    Display 'Started Extracting HR_ACCTG_LINE .... '
    do Process-HR-ACCTG-LINE
    Display 'Finished Extracting HR_ACCTG_LINE'
    Display ' '
    do Format-Number(#Total_Acctg_Line, $out, '99,999,999')
    Display 'Number of HR_ACCT_LINE Record extracted : ' noline
    Display $out
    Display ' '

END-PROCEDURE


BEGIN-PROCEDURE Get-SQR-Report-Process-Instance

Begin-SELECT
A.PRCSINSTANCE
A.PRCSNAME

   let #SQR_Report_Prcs_Instance = &A.PRCSINSTANCE
   let $Prcsname                 = &A.PRCSNAME

FROM PSPRCSRQST A, PSPRCSRQST B
WHERE A.PRCSINSTANCE = B.PRCSINSTANCE
  AND A.JOBINSTANCE = #prcs_job_instance
  AND A.PRCSTYPE = 'SQR Report'
  AND A.PRCSJOBNAME = A.PRCSNAME
End-SELECT

END-PROCEDURE


BEGIN-PROCEDURE Process-HR-ACCTG-LINE

Begin-SELECT
A.RUN_DT
A.SEQNUM
A.LINE_NBR
A.KK_AMOUNT_TYPE
A.AUDIT_ACTN
A.IN_PROCESS_FLG
A.BUDGET_LINE_STATUS
A.BUDGET_DT
A.BUSINESS_UNIT_GL
A.JOURNAL_ID
A.JOURNAL_DATE
A.JOURNAL_LINE
A.ACCOUNT
A.DEPTID
A.PROJECT_ID
A.PRODUCT
A.FUND_CODE
A.PROGRAM_CODE
A.CLASS_FLD
A.AFFILIATE
A.OPERATING_UNIT
A.ALTACCT
A.STATISTICS_CODE
A.MONETARY_AMOUNT
A.FOREIGN_AMOUNT
A.FOREIGN_CURRENCY
A.RT_TYPE
A.RATE_MULT
A.RATE_DIV
A.STATISTIC_AMOUNT
A.JRNL_LN_REF
A.OPEN_ITEM_STATUS
A.LINE_DESCR
A.JRNL_LINE_STATUS
A.JOURNAL_LINE_DATE
A.BUSINESS_UNIT
A.APPL_JRNL_ID
A.ACCOUNTING_DT
A.GL_DISTRIB_STATUS
A.PROCESS_INSTANCE
A.CURRENCY_CD
A.ACCOUNTING_PERIOD
A.FISCAL_YEAR
A.LEDGER
A.LEDGER_GROUP
A.USE_DISTRIBUTION

    do Format-Number(&A.MONETARY_AMOUNT, $Monetary_Amount, '09999999999999.99')
    do Format-Number(&A.FOREIGN_AMOUNT, $Foreign_amount,   '09999999999999.99')
    do Format-Number(&A.RATE_MULT, $Rate_Mult, '09999999.99999999')
    do Format-Number(&A.RATE_DIV, $Rate_Div, '0999999.99999999')
    do Format-Number(&A.STATISTIC_AMOUNT, $Statistic_Amount, '09999999999999.99')
    do Format-DateTime (&A.RUN_DT, $run_dt, {DEFCMP},'','')
    do Format-DateTime (&A.BUDGET_DT, $budget_dt, {DEFCMP},'','')
    do Format-DateTime (&A.JOURNAL_DATE, $journal_date, {DEFCMP},'','')
    do Format-DateTime (&A.JOURNAL_LINE_DATE, $journal_line_date, {DEFCMP},'','')
    do Format-DateTime (&A.ACCOUNTING_DT, $accounting_dt, {DEFCMP},'','')

    let $File_Record_ID = ' '
    let $two_blanks = '  '

    do Write-HR-ACCTG-LINE

FROM PS_HR_ACCTG_LINE A
WHERE A.PROCESS_INSTANCE = #SQR_Report_Prcs_Instance

End-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Write-HR-ACCTG-LINE

   Write 1 from
         $File_Record_ID:1
         &A.AUDIT_ACTN:1
         $run_dt:8
         $two_blanks:2
         &A.SEQNUM:3
         &A.LINE_NBR:5
         &A.KK_AMOUNT_TYPE:1
         &A.IN_PROCESS_FLG:1
         &A.BUDGET_LINE_STATUS:1
         $budget_dt:8
         $two_blanks:2
         &A.BUSINESS_UNIT_GL:5
         &A.JOURNAL_ID:10
         $journal_date:8
         $two_blanks:2
         &A.JOURNAL_LINE:9
         &A.ACCOUNT:10
         &A.DEPTID:10
         &A.PROJECT_ID:15
         &A.PRODUCT:6
         &A.FUND_CODE:5
         &A.PROGRAM_CODE:5
         &A.CLASS_FLD:5
         &A.AFFILIATE:5
         &A.OPERATING_UNIT:8
         &A.ALTACCT:10
         &A.STATISTICS_CODE:3
         $Monetary_Amount:17
         $Foreign_amount:17
         &A.FOREIGN_CURRENCY:3
         &A.RT_TYPE:5
         $Rate_Mult:17
         $Rate_Div:16
         $Statistic_Amount:17
         &A.JRNL_LN_REF:10
         &A.OPEN_ITEM_STATUS:1
         &A.LINE_DESCR:30
         &A.JRNL_LINE_STATUS:1
         $journal_line_date:8
         $two_blanks:2
         &A.BUSINESS_UNIT:5
         &A.APPL_JRNL_ID:10
         $accounting_dt:8
         $two_blanks:2
         &A.GL_DISTRIB_STATUS:1
         &A.PROCESS_INSTANCE:10
         &A.CURRENCY_CD:3
         &A.ACCOUNTING_PERIOD:3
         &A.FISCAL_YEAR:4
         &A.LEDGER:10
         &A.LEDGER_GROUP:10
         &A.USE_DISTRIBUTION:1

    Add 1 to #Total_Acctg_Line

END-PROCEDURE

!----------------------------------------------------------------------!
! Procedure:   Get  FileName                                           !
! Description: Retrieves Output Journal FileName                       !
!----------------------------------------------------------------------!
begin-procedure Get-FileName

  let $prcs_process_instance = to_char(#prcs_process_instance)
  let $Prcsname = rtrim($Prcsname, ' ')


  #IFDEF MVS
    let $FileName = $Prcsname

  #ELSE
     let $FileName = $Prcsname||'_'||$prcs_process_instance
  #ENDIF


end-procedure

!***********************************************************************
!OPEN-FILE                                                             *
!***********************************************************************
begin-procedure OPEN-FILE

!************************************************************
!  CODE INSERTED TO ACCEPT INTERNAL EXPORT FILE NAME
!  {IMPORTPREFIX} is defined in setenv.sqc
!************************************************************

   let $OpenName = '{IMPORTPREFIX}' || $FileName || '.DAT'

!   open $OpenName as 1 for-writing record=359:fixed Status=#filestat
   open $OpenName as 1 for-writing record=363:fixed Status=#filestat

   Display 'Output File : ' noline
   Display $OpenName
   Display ' '

end-procedure OPEN-FILE

!*****     Include Files          **************************************

#Include 'reset.sqc'     !Reset printer procedure
#include 'stdapi.sqc'    !Update Process API
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stderror.sqc'  !Routine for error display.

