!********************************
!  PER030UK:  Job Group Roster  *
!********************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:36:30                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!Porcedures ending with "Scrty" and Record alias ending with "SC" are
!using SQR Seurity.
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************
#include 'setenv.sqc'   !Set environment
begin-setup
#Include 'ptpsl177.sqc'   !Printer and page-size initialization
end-setup


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  if $prcs_oprid=''
  goto last2
 end-if

  do Process-Main

  do Reset
  do Stdapi-Term
Last2:
end-report


begin-heading 7
  #Include 'stdhdg03.sqc'
  print 'Company :'   (+1,1,10)
  print $compdescr    (,+2)
  display $company
  print 'Job'                                                     (+2,1,69)
  print 'Ethnic        Job Entry  Salary'                        (0,71,33)
  if #MultiCurr
    print 'Cur'                                                   (0,106,13)
  end-if
  print 'Hourly             Monthly              Hire     Job Title'  (0,117)

  print 'Grp Job Title (current)            Employee Name'        (+1,1,69)
  print 'Group     Sex Date       Grade'                           (0,71,33)
  if #MultiCurr
    print 'Cd'                                                    (0,106,13)
  end-if
  print '  Rate                Rate Dept.  Name  Date     (at hire)'    (0,117)


end-heading

!-----------------------------------------------------------------------
begin-procedure Init-Report

  move 'PER030UK' to $ReportID
  move 'Job Group Roster' to $ReportTitle
  display $ReportTitle

  do Stdapi-Init

  do Security-Param

  if $prcs_process_instance = ''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto last1
 !    do Ask-As-Of-Date
 else
    do Select-Parameters
  end-if
    if $companyruncnt = ''
      let $wherecompany = ''
    else
      if $scrty_flag = 'Y'
      let $wherecompany = 'AND PSC.COMPANY = '||''''||$companyruncnt||''''
      else
      let $wherecompany = 'AND P.COMPANY = '||''''||$companyruncnt||''''
      end-if
    end-if
last1:
end-procedure Init-Report

!-----------------------------------------------------------------------
begin-procedure Get-Values

   do Get-As-Of-Date
   do Get-Per030-Parm

end-procedure Get-Values

!-----------------------------------------------------------------------
begin-procedure Process-Main

if $language_cd = 'JPN'
          let $qualifier = 'A2.NAME_AC'
      else
           let $qualifier = 'A2.NAME'
     end-if

begin-SELECT
P.COMPANY
    move &P.COMPANY TO $company
P.DESCR
  let $compdescr = rtrim(&P.DESCR,' ')
D.EEO_JOB_GROUP          (+1,1,4)
D.DESCR                  (0,5,30)
DVR.ETHNIC_GRP_CD_UK,
A.EMPLID,
  Let $Emplid = &A.EMPLID
B.EMPL_RCD
  move 'ETHNIC_GRP_CD_UK' to $FieldName
  move &DVR.Ethnic_Grp_CD_UK  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName   (0,71,11)

  Do Get-Personal_Data

C.JOB_ENTRY_DT
  do Format-DateTime(&C.Job_Entry_Dt, $out, {DEFDATE}, '', '')
  print $out             (0,86,10)
C.SAL_ADMIN_PLAN         (0,98,0) EDIT XXX/
C.GRADE                  ()
C.CURRENCY_CD
  if #MultiCurr
    print &C.Currency_cd (0,106,3)
  end-if
C.HOURLY_RT
  do Format-Number(&C.Hourly_Rt, $out, '999999999999.999999')
  print $out             (0,104,18)
C.MONTHLY_RT
  do Format-Number(&C.Monthly_Rt, $out, '999999999999.999999')
  print $out             (0,124,19)
C.BUSINESS_UNIT
  LET $BU = RTRIM(&C.BUSINESS_UNIT,' ')
C.DEPTID                 (0,146,5)
  move &C.DeptID to $DeptID
  do Get-Department-Name
  print $DeptNameAbbrv   (0,154,5)

B.HIRE_DT
  do Format-DateTime(&B.Hire_Dt, $out, {DEFDATE}, '', '')
  print $out             (0,160,8)

  do Get-Title-At-Hire
  print &JC.Descrshort   (0,171,10)

FROM  PS_PER_ORG_ASGN A,
      PS_EMPLOYMENT B,
      PS_JOB C,
      PS_JOBCODE_TBL D,
      PS_DIVERSITY DVR,
      PS_COMPANY_TBL P,
      PS_FAST_SQR_SEC_VW SCRTY,
      PS_PERSON_NAME A2

WHERE A.PER_ORG = 'EMP'
  AND A2.EMPLID = A.EMPLID
  AND C.HR_STATUS  = 'A'
  AND A.EMPLID = B.EMPLID
  AND DVR.EMPLID = A.EMPLID
  AND A.EMPL_RCD = B.EMPL_RCD
  AND B.EMPLID = C.EMPLID
  AND B.EMPL_RCD = C.EMPL_RCD
  AND C.EFFDT =
      (SELECT MAX(H.EFFDT)
       FROM PS_JOB H
       WHERE H.EMPLID = C.EMPLID
         AND H.EMPL_RCD = C.EMPL_RCD
         AND H.EFFDT <= $AsofDate)
  AND C.EFFSEQ =
      (SELECT MAX(I.EFFSEQ)
       FROM PS_JOB I
       WHERE I.EMPLID = C.EMPLID
         AND I.EMPL_RCD = C.EMPL_RCD
         AND I.EFFDT = C.EFFDT)
  AND D.JOBCODE = C.JOBCODE
  AND D.SETID = C.SETID_JOBCODE
  AND D.EFFDT =
      (SELECT MAX(J.EFFDT)
       FROM PS_JOBCODE_TBL J
       WHERE J.JOBCODE = D.JOBCODE
         AND J.SETID = D.SETID
         AND J.EFFDT <= C.EFFDT)
  AND DVR.ETHNIC_GRP_CD_UK <> '0'  !Not Applicable
  AND C.EEO_CLASS    <> 'E'  !Exclude from counts
  AND C.COMPANY = P.COMPANY
  [$wherecompany]
  AND A.EMPLID = SCRTY.EMPLID
  AND B.EMPL_RCD = SCRTY.EMPL_RCD
  [$SecurityClause]

ORDER BY P.COMPANY,D.EEO_JOB_GROUP,D.DESCR,[$Qualifier]
end-SELECT

end-procedure Process-Main

!-----------------------------------------------------------------------
begin-procedure Get-Title-At-Hire
begin-SELECT
JC.DESCR
JC.DESCRSHORT
FROM  PS_JOB JJ,PS_JOBCODE_TBL JC
WHERE JJ.EMPLID    = &A.EmplID
  AND JJ.EMPL_RCD = &B.EMPL_RCD
  AND JJ.ACTION IN ('HIR','REH')
  AND JJ.JOBCODE   = JC.JOBCODE
  AND JC.SETID = JJ.SETID_JOBCODE
  AND JC.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_JOBCODE_TBL
       WHERE  JOBCODE = JC.JOBCODE
         AND SETID = JC.SETID
         AND  EFFDT  <= JJ.EFFDT)
end-SELECT

end-procedure Get-Title-At-Hire

begin-procedure Get-Personal_Data

begin-select
A1.NAME                   (0,39,34)
A1.NAME_AC,
PDE.SEX                    (0,83,2)
From PS_PERSON_NAME A1, PS_PERS_DATA_EFFDT PDE
Where A1.EMPLID = $EmplID
and A1.EMPLID = PDE.EMPLID
  and PDE.EFFDT =
       (SELECT MAX(PDF.EFFDT)
        from PS_PERS_DATA_EFFDT PDF
        where PDF.EMPLID = PDE.EMPLID
        and   PDF.EFFDT <= $AsOfDate)
end-select

end-procedure !Get-Personal_Data

!-----------------------------------------------------------------------
#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'askaod.sqc'    !Ask As Of Date input
#include 'hrsecty.sqc'   !Get SQR Security parameters

#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
