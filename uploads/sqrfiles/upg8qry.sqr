! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!----------------------------------------------------------------------
! Report Name: UPG8QRY - Upgrade Query Fields to 8.0
!----------------------------------------------------------------------
!
!
!----------------------------------------------------------------------
! Program Descr: Creates unique Query FieldNames as required for 8.0 
!                PSQRYFIELD Unique Index. Program generates unique 
!                Query FieldNames, only when the existing Query Field
!                Names would result in duplicate keys.
!----------------------------------------------------------------------
!
!----------------------------------------------------------------------
! Environment Include Files:
#include 'setenv.sqc'    !Set environment
#include 'setup02.sqc'   !Printer and page-size initialization

!----------------------------------------------------------------------!
! Procedure: Begin-Report                                              !
!----------------------------------------------------------------------!

 
begin-report
 
  do HOUSEKEEPING
 
  do Get-QRYFLDNAME-Length
  do Report
 
  show ' '
  let $Count = edit(#Num_Updates, '9,999')
  show 'Number of QRYFIELD updates = ' $Count
 
  date-time () '' &TimeEnded
  show ' '
  show 'Process ended at ' &TimeEnded
 
  do RESET
 
end-report
 
begin-setup
 
end-setup
!******************************************************************************
! HOUSEKEEPING                                                                *
!******************************************************************************
begin-procedure HOUSEKEEPING
 
do INIT-DATETIME
 
do INIT-NUMBER
 
do GET-CURRENT-DATETIME
 
let #Num_Updates = 0
 
let $ReportID    = 'UPG8QRY.SQR'
let $ReportTitle = 'Assign Unique Values to PSQRYFIELD.QRYFLDNAME'
 
show ' '
 

display $ReportTitle
date-time () '' &TimeStarted
 
show 'Process started at ' &TimeStarted
show ' '
 
end-procedure
 
!----------------------------------------------------------------------!
begin-procedure Report
!----------------------------------------------------------------------!
begin-SELECT
COUNT(*),
Q.OPRID,
Q.QRYNAME,
Q.QRYFLDNAME
 
  Show 'Updating query: '
  Show '         OPRID      = ' &Q.OPRID
  Show '         Query Name = ' &Q.QRYNAME
  Show '         Field Name = ' &Q.QRYFLDNAME
  show ' '

  do Select-Single-Qryfield
 
FROM PSQRYFIELD Q
GROUP BY OPRID, QRYNAME, QRYFLDNAME
HAVING COUNT(*) > 1
end-select
end-procedure

!----------------------------------------------------------------------!
begin-procedure Get-QRYFLDNAME-Length
!----------------------------------------------------------------------!
let #FldLength = 30

begin-SELECT
DBF.LENGTH 

  let #FldLength = &DBF.LENGTH

FROM PSDBFIELD DBF
WHERE FIELDNAME = 'QRYFLDNAME'
end-select
end-procedure
 
!----------------------------------------------------------------------!
begin-procedure Select-Single-Qryfield
!----------------------------------------------------------------------!
LET #Seq_Num = 1
begin-SELECT
Q2.OPRID,
Q2.QRYNAME,
Q2.SELNUM,
Q2.FLDNUM,
Q2.QRYFLDNAME
 
  show 'OLD fldname = ' &q2.qryfldname
 
  let $Seq_Num      = to_char(#Seq_Num)
  let $QryFldName   = rtrim(&Q2.QRYFLDNAME,' ')
  let $Seq_Num      = rtrim($Seq_Num,' ')
  let $Seq_Num      = ltrim($Seq_Num,' ')
 
  let #Seq_Num_Len  = Length($Seq_Num)
  let #Name_Len     = Length($QryFldName)
  let #Tot_Len      = #Seq_Num_Len + #Name_Len
  let #Excess       = #Tot_Len - #FldLength

  if #Excess        > 0
    let $QryFldName = SUBSTR($QryFldName,1,(#Name_Len - #Excess))
  end-if
  
  let $NewFldName   = $QryFldName || '-' || $Seq_Num
  let $NewFldName   = LTRIM($NewFldName, ' ')

  show 'NEW fldname = ' $NewFldName
  do Update-Qryfield
  let #Num_Updates  = #Num_Updates + 1
  LET #Seq_Num      = #Seq_Num + 1
 
FROM PSQRYFIELD Q2
WHERE OPRID      = &Q.OPRID
  AND QRYNAME    = &Q.QRYNAME
  AND QRYFLDNAME = &Q.QRYFLDNAME
ORDER BY
  SELNUM, FLDNUM, QRYFLDNAME
End-select
end-procedure
 
!----------------------------------------------------------------------!
begin-procedure Update-Qryfield
!----------------------------------------------------------------------!
 
begin-SQL
 
update PSQRYFIELD
SET QRYFLDNAME   = $NewFldName
where OPRID      = &Q2.OPRID
  AND QRYNAME    = &Q2.QRYNAME
  AND SELNUM     = &Q2.SELNUM
  AND FLDNUM     = &Q2.FLDNUM
  AND QRYFLDNAME = &Q2.QRYFLDNAME
end-Sql
end-procedure
 
!----------------------------------------------------------------------!
begin-procedure SQL-Error
!----------------------------------------------------------------------!
Show 'SQL ERROR'
end-procedure

!****************************************************************************
! SQC's                                                                     *
!****************************************************************************
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

