!*****************************************************************
!  TAX002:  Quarterly State Tax Report - Employee Wage Listings  *
!           Negative Wage Listings                               *
!*****************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:58:04                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#Include 'setup31.sqc'  !printer and page-size initialization
#Include 'usarpt.sqc'   !Get USA variables


begin-report
  do Init-Report
  do Process-Main
  do Stdapi-Term
end-report

begin-procedure Init-Report
  display 'Quarterly Employee Wage (Negative) Listings - By State'

  do Init-DateTime
  do Init-Number

  do Get-Tax-Reporting-Run-Controls

  do Stdapi-Init
  do Get-Calendar-Year-Id

  move $PeriodEndDate to $AsOfDate  !for included procedures
end-procedure

begin-heading 16
  if $Company <> $priorCompany
    do Get-Company-Data
    move $Company to $priorCompany
    move ''       to $priorState  !force change in State below
  end-if
  if rtrim($State, ' ') <> $priorState
    do Get-State-Tax-Data  !Employer_ID_SWT, Employer_ID_SUT, State Name
    let $priorState = rtrim($State, ' ')
    move 0 to #PAGE_NUM
  end-if
  add 1 to #PAGE_NUM
  print $StateName         (5,2)
  do Format-DateTime($PeriodEndDate, $out, {DEFDATE}, '', '')
  print $out               (0,51)
  print 'Page'             (0,70)
  do Format-Number(#PAGE_NUM, $out, '999')
  print $out               (0,+2)
  print $CompanyName       (+3,12)
  print $Compnyadd1        (+1,12)
  let $City = RTRIM($Compnycity, ' ')
  print $City              (+1,12)
  print &CT.State          ()      edit ,bxx
  move  &CT.Postal         to $Zip
  move  &CT.Postal         to $ZipExtsn ~~~~~~xxxx
  if instr($Zip,'-',6) = 0
  and $ZipExtsn <> '    '
    let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if
  print $Zip               (0,+2)
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    print &Employer_ID_SUT (+2,12)
  else
    print &Employer_ID_SWT (+2,12)
  end-if
  print 'SSN'              (+3,2,11)
  print 'Name'             (+0,+2,20)
  print 'Wages'            (+0,39)
end-heading

begin-footing 6
  do Format-Number(#PgTotSWT, $out, '99999,999.99')
  print $out (+2,35)
  move 0 to #PgTotSWT
end-footing

begin-procedure Process-Main

  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
C.COMPANY () on-break print=never level=1 procedure=Company-Change
C.STATE   () on-break print=never level=2 procedure=State-Change
  if rtrim(&C.State, ' ') <> $prior_State1
    move 'Y' to $firstMonthCd1
  end-if
C.EMPLID
  if &C.EmplID <> $prior_EmplID1
    move 'Y' to $firstMonthCd1
  end-if
C.TAX_CLASS
  if &C.Tax_Class <> $prior_Tax_Class1
    move 'Y' to $firstMonthCd1
  end-if
C.BALANCE_YEAR
C.BALANCE_QTR
C.BALANCE_PERIOD
C.LOCALITY
C.NLGRS_QTD
C.TXGRS_QTD

D1.NATIONAL_ID
PN.NAME

  move &C.Company to $Company  !for Heading
  let $State = rtrim(&C.State, ' ')

  if $firstMonthCd1 = 'Y'
    if &C.NLGrs_QTD < 0 or &C.TxGrs_QTD < 0
      do Process-Tax-Balance
    end-if
    move 'N' to $firstMonthCd1
  end-if

  if #count = 10
    display '.' noline
    move 1 to #count
  end-if
  add 1 to #count

  move &C.EmplID    to $prior_EmplID1
  let $prior_State1 = rtrim(&C.State, ' ')
  move &C.Tax_Class to $prior_Tax_Class1

FROM  PS_TAX_BALANCE C, PS_NAMES PN,
      PS_PERS_NID D1, PS_COMPANY_TBL A
WHERE D1.EMPLID           = C.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND PN.EMPLID           = C.EMPLID
  AND C.STATE             <> '$U'
  AND C.LOCALITY          = ' '
  AND C.TAX_CLASS         = 'U'
  AND C.NLGRS_QTD         < 0
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = &TX.Balance_Year
  AND C.BALANCE_QTR       = &TX.Balance_Qtr
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
  AND PN.NAME_TYPE = 'PRI'
  AND PN.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PN.EMPLID
         AND  PX.NAME_TYPE = PN.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfDate)
ORDER BY C.COMPANY, C.STATE, C.LOCALITY, D1.NATIONAL_ID, C.TAX_CLASS,
         C.BALANCE_YEAR, C.BALANCE_QTR, C.BALANCE_PERIOD DESC
end-SELECT
  if #count = 0
    print '***   NO DATA SELECTED   ***'  (+3,) center
  end-if
end-procedure

begin-procedure Process-Tax-Balance
  if &C.EmplID <> $priorEmplID
    move &C.EmplID to $priorEmplID
    move $SSN   to $S123 xxx       !isolate first 3 digits

    if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
      print 'Applied For'          (+1,2)
    else
      print &D1.NATIONAL_ID        (+1,2)  edit xxxbxxbxxxx
    end-if
    print &PN.Name                  (0,15,20)
  end-if

  do Format-Number(&C.NLGrs_QTD, $out, '99999,999.99')
  print $out                       (0,35)
  add &C.NLGrs_QTD to #PgTotSWT
end-procedure

begin-procedure Company-Change
  new-page
end-procedure

begin-procedure State-Change
  new-page
end-procedure

#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
