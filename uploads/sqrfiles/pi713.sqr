!***********************************************************************
!  PI713.SQR:INTERFACE GROUP TABLE                                     *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:02:33:39                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup31.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


!**************************
begin-procedure Init-Report
!**************************

  move 'PI713' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi713_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
!**********************************
begin-procedure Report-Translation
!**********************************

! Read from PI713 program ids
do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PI713', 'REPORT_TITLE', $Pi713_ReportTitle, #dummy_width)
  do Get_Field_Information ('PI713', 'GROUPID', $Pi713_GroupId, #dummy_width)
!  DO GET_FIELD_INFORMATION ('PI713', 'SYSTEMID', $PI713_SYSTEMID, #DUMMY_WIDTH)
  do Get_Field_Information ('PI713', 'DESCRIPTION', $Pi713_Description, #dummy_width)
  do Get_Field_Information ('PI713', 'SHORT', $Pi713_Short, #dummy_width)

end-procedure


begin-heading 7
#Include 'stdhdgtr.sqc'

  print 'SId'              (+2,1,14)
  print $Pi713_GroupId     (,16,16)
  print $Pi713_Description (,34,30)
  print $Pi713_Short       (,66,10)

end-HEADING


!***************************
begin-procedure Process-Main
!***************************

begin-SELECT
A.PI_SYSTEM_ID               (+1,1,14)
A.PI_GROUP_ID                (,16,16)
A.DESCR
A.DESCRSHORT

   let $prnt_descr      = &A.DESCR
   let $prnt_descrshort = &A.DESCRSHORT

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!

  if $Curr_language_cd <> $Psoptions_language_cd
     do Get-Rel-Lang-Descr
  end-if

  PRINT $PRNT_DESCR          (,34,30)
  PRINT $PRNT_DESCRSHORT     (,66,10)

FROM PS_PI_GROUP_TBL A
ORDER BY A.PI_SYSTEM_ID, A.PI_GROUP_ID

end-SELECT

end-procedure


!*********************************
begin-procedure Get-Rel-Lang-Descr
!*********************************

begin-SELECT

AL.DESCR
AL.DESCRSHORT

  let $prnt_descr      = Cond(RTRIM(&AL.DESCR,' ')='', &A.DESCR, &AL.DESCR)
  let $prnt_descrshort = Cond(RTRIM(&AL.DESCRSHORT,' ')='', &A.DESCRSHORT, &AL.DESCRSHORT)

FROM PS_PI_GROUP_LANG AL
WHERE AL.PI_SYSTEM_ID = &A.PI_SYSTEM_ID
  AND AL.PI_GROUP_ID  = &A.PI_GROUP_ID
  AND AL.LANGUAGE_CD  = $Curr_language_cd

end-SELECT

end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

