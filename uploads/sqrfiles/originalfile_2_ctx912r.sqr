!***********************************************************************
!  CTX912R:    PRINT RL-1 Magnetic Media XML Audit Report              *
!***********************************************************************
!***********************************************************************
!                                                                      *
!Confidentiality Information:                                          *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/02/20:21:05:06                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  694792                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size init
#Include 'ctxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#define TAXFORM_BOX
#include 'rellang.sqc'

begin-heading 5
  #Include 'stdhdgtr.sqc'
  print $TaxYear   (3,,)      center
end-heading

begin-report

  do Init-Report

  do Process-Main

  close 10

#ifdef PRCSSCHD
  do StdAPI-Term
#endif

end-report

begin-procedure Init-Report

  do Init-DateTime
  #ifdef PRCSSCHD
    do StdAPI-Init
  #endif

 do Initialization

end-procedure

begin-procedure Initialization

  #define ColR 120

  move 'N' to $FileAtEnd

  do Get-Current-DateTime

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($AsOfToday, $AsOfDateYMD, {DEFYMD},'','')
  move $AsOfDateYMD to $AsOfYear xxxx

  move $AsOfYear  to #AsOfYear
  subtract 1 from #AsOfYear

  do Get-Can-Tax-Processing-Params

  move &TX.Balance_Year to #TaxYear
  move #TaxYear to $TaxYear 9999

  let $AsOfDate = $TaxYear || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP},'','native')

  if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''
    display ''
    display 'Current Year is not one greater than Tax Reporting Year.'
    input $Answer maxlen=1 'Current Year is not one greater than Tax Reporting Year. Do you want to continue? (Y/N)'
    uppercase $Answer

    if $Answer <> 'Y'
      stop
    end-if
  end-if

  do Open-Files

  do Array-Create
  do Load-Box-Arrays

  move 'CTX912R' to $ReportID
  do Get-Report-Language          ! Procedure that decides the report language
                                  ! and then calls 'Report-Translation' procedure
                                  ! that reads the language dep. text.

  move $Ctx912r_Title_rv1_audit to $ReportTitle
  display 'PRINTING RL-1 MAGNETIC MEDIA XML AUDIT REPORT'

  do Initialize-Transmitter
  do Initialize-Detail
  do Initialize-Totals

end-procedure

!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
!Read language-dependent text from the database.

begin-procedure Report-Translation        ! Read from CTX912R  program ids

   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('CTX912R', 'TITLE_RV1_AUDIT' , $Ctx912r_Title_rv1_audit , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_REC'       , $Ctx912r_Trans_rec       , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TAXATION_YEAR'   , $Ctx912r_Taxation_year   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_NAME'      , $Ctx912r_Trans_name      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_ADDR1'     , $Ctx912r_Trans_addr1     , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_ADDR2'     , $Ctx912r_Trans_addr2     , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_CITY'      , $Ctx912r_Trans_city      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_PROV'      , $Ctx912r_Trans_prov      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_NUMBER'    , $Ctx912r_Trans_number    , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_POST_CODE' , $Ctx912r_Trans_post_code , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TECH_RES_NAME'   , $Ctx912r_Tech_res_name   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TECH_RES_AREA'   , $Ctx912r_Tech_res_area   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TECH_RES_PHNE'   , $Ctx912r_Tech_res_phne   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TECH_RES_EXT'    , $Ctx912r_Tech_res_ext    , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TECH_RES_LANG'   , $Ctx912r_Tech_res_lang   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACCT_RES_NAME'   , $Ctx912r_Acct_res_name   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACCT_RES_AREA'   , $Ctx912r_Acct_res_area   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACCT_RES_PHNE'   , $Ctx912r_Acct_res_phne   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACCT_RES_EXT'    , $Ctx912r_Acct_res_ext    , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACCT_RES_LANG'   , $Ctx912r_Acct_res_lang   , #dummy_width)
   do Get_Field_Information ('CTX912R', 'PACK_TYPE'       , $Ctx912r_Pack_type       , #dummy_width)
   do Get_Field_Information ('CTX912R', 'PACK_TYPE1'      , $Ctx912r_Pack_type1      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'PACK_TYPE2'      , $Ctx912r_Pack_type2      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TRANS_TYPE'      , $Ctx912r_Trans_type      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'RELEVE_SOURCE1'  , $Ctx912r_Releve_source1  , #dummy_width)
   do Get_Field_Information ('CTX912R', 'RELEVE_SOURCE2'  , $Ctx912r_Releve_source2  , #dummy_width)
   do Get_Field_Information ('CTX912R', 'SIN'             , $Ctx912r_Sin             , #dummy_width)
   do Get_Field_Information ('CTX912R', 'EMPLID'          , $Ctx912r_Emplid          , #dummy_width)
   do Get_Field_Information ('CTX912R', 'RV1_BOX_DATA'    , $Ctx912r_Rv1_box_data    , #dummy_width)
   do Get_Field_Information ('CTX912R', 'SUMM_MSG'        , $Ctx912r_Summ_msg        , #dummy_width)
   do Get_Field_Information ('CTX912R', 'SUMM_REC'        , $Ctx912r_Summ_rec        , #dummy_width)
   do Get_Field_Information ('CTX912R', 'REPORTING_ID'    , $Ctx912r_Reporting_id    , #dummy_width)
   do Get_Field_Information ('CTX912R', 'EMPLER_NAME'     , $Ctx912r_Empler_name     , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ADDRESS'         , $Ctx912r_Address         , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ADDRESS2'        , $Ctx912r_Address2        , #dummy_width)
   do Get_Field_Information ('CTX912R', 'CITY'            , $Ctx912r_City            , #dummy_width)
   do Get_Field_Information ('CTX912R', 'PROVINCE'        , $Ctx912r_Province        , #dummy_width)
   do Get_Field_Information ('CTX912R', 'POSTAL_CODE'     , $Ctx912r_Postal_code     , #dummy_width)
   do Get_Field_Information ('CTX912R', 'NEQ'             , $Ctx912r_NEQ             , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TAX_YEAR'        , $Ctx912r_Tax_year        , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TOTAL_RV1_RECS'  , $Ctx912r_Total_rv1_recs  , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACC_PRINT_MSG1'  , $Ctx912r_Acc_print_msg1  , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACC_PRINT_MSG2'  , $Ctx912r_Acc_print_msg2  , #dummy_width)
   do Get_Field_Information ('CTX912R', 'ACCCUM_TOTALS'   , $Ctx912r_Accum_totals    , #dummy_width)
   do Get_Field_Information ('CTX912R', 'REPORT_ID'       , $Ctx912r_Report_id       , #dummy_width)
   do Get_Field_Information ('CTX912R', 'RV1_#SLIPS'      , $Ctx912r_RV1_#slips      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'SLIP_NO'         , $Ctx912r_Slip_No         , #dummy_width)
   do Get_Field_Information ('CTX912R', 'LAST_TRANS_SLIP#' , $Ctx912r_LastTrans_SlipNo , #dummy_width)
   do Get_Field_Information ('CTX912R', 'TYPE_OF_DATA'    , $Ctx912r_TypeOfData      , #dummy_width)
   do Get_Field_Information ('CTX912R', 'APPROVAL_NUM'    , $Ctx912r_Approval_Num    , #dummy_width)
   do Get_Field_Information ('CTX912R', 'DED_FORCE'       , $Ctx912r_Ded_Force       , #dummy_width)

end-procedure

begin-procedure Open-Files

#ifdef MPE
    open 'CTXRM.DAT' as 10 for-reading record=200
#else
    open '{IMPORTPREFIX}CTXRM{IMPORTSUFFIX}' as 10 for-reading record=200
#endif

end-procedure

begin-procedure Array-Create

  create-array name=PrR1_Area size=200 -
    field=PR1_Box:char                 -
    field=PR1_Descr:char

end-procedure

begin-procedure Load-Box-Arrays

    move 0 to #i

begin-SELECT
M.TAXFORM_ID
M.BOX
M.DESCR
M.EFFDT

   let $M.BOX = RTRIM(&M.BOX, ' ')
   let $TAXFORM_BOX-DESCR = &M.DESCR

   do Get_Related_TAXFORM_BOX(&M.BOX,&M.EFFDT,&M.TAXFORM_ID)

   put $M.Box $TAXFORM_BOX-DESCR into PrR1_Area(#i)
         PR1_Box PR1_Descr
       add 1 to #i
       move #i to #max_R1boxes


FROM PS_TAXFORM_BOX M
WHERE TAXFORM_ID = 'R' AND
      EFFDT = (SELECT MAX(EFFDT)
               FROM PS_TAXFORM_BOX
               WHERE TAXFORM_ID = 'R'
                 AND EFFDT <= $AsOfDate)
ORDER BY M.BOX ASC
end-SELECT

end-procedure


begin-procedure Process-Main

  let $Transmitter_rec = 'N'
  let $Detail_rec      = 'N'

  while 1 = 1

   if $FileAtEnd = 'Y'
       break
    else
      do Read-File
      if $FileAtEnd = 'Y'
        break
      else
        let $RV1Data_Full_Rec = ltrim($RV1Data, ' ')
        let $RV1Data_Full_Rec = rtrim($RV1Data, ' ')

        evaluate $RV1Data_Full_Rec
        when = '<P>'
          let $TypeOfData = 'P'
          let $Transmitter_rec = 'Y'
          break
        when = '<A>'
          let $Detail_rec = 'Y'
          let $TypeOfData = 'A'
          break
        when = '<R>'
          let $Detail_rec = 'Y'
          let $TypeOfData = 'R'
          break
        when = '<D>'
          let $Detail_rec = 'Y'
          let $TypeOfData = 'D'
          break
        when-other
          break
        end-evaluate

        do Process-RL1-XML-Records

      end-if
   end-if
  end-while

end-procedure


begin-procedure Read-File

  read 10 into $RV1Data:200

    if #end-file = 1
      move 'Y' to $FileAtEnd
    end-if

end-procedure


begin-procedure Process-RL1-XML-Records

  do Parse-XML
  if rtrim($Element, ' ') <> ''
    if $Transmitter_rec = 'Y'
      do Extract-Transmitter-Data
    else
      if $Detail_rec = 'Y'
        do Extract-EE-Detail-Data
      else
        do Extract-Totals-Data
      end-if
    end-if
  end-if

  evaluate $RV1Data_Full_Rec
  when = '</P>'
    do Print-Transmittal-Record
    do Initialize-Transmitter
    let $Transmitter_rec = 'N'
    let $Group_Tag =  ' '
    new-page
    break
  when = '</R>'
  when = '</A>'
    do Print-Detail
    do Initialize-Detail
    let $Detail_rec = 'N'
    let $Group_Tag  = ' '
    break
  when = '</D>'
    do Print-Detail-Cancelled
    do Initialize-Detail
    let $Detail_rec = 'N'
    let $Group_Tag  = ' '
    break
  when = '</T>'
    new-page
    do Print-Accumulated-Data
    new-page
    do Print-Summary
    new-page
    do Initialize-Totals
    break
  when-other
    break
  end-evaluate

end-procedure


begin-procedure Parse-XML

  let #RecLength           = 0
  let #BeginTagPosition    = 0
  let #EndTagPosition      = 0
  let #TagLength           = 0
  let #TagSlashPosition    = 0
  let $Tag                 = ' '
  let $Element_Tag         = ' '
  let $Element             = ' '

  let #RecLength = length($RV1Data_Full_Rec)

  while  instr ($RV1Data_Full_Rec , '<', 1) <> 0
    let #BeginTagPosition  = instr ($RV1Data_Full_Rec , '<', 1)

    while instr($RV1Data_Full_Rec , '>', 1)   <> 0
      let #EndTagPosition = instr($RV1Data_Full_Rec , '>', 1)

      if #RecLength =  #EndTagPosition        !No element found!
        let $Group_Tag = rtrim($RV1Data_Full_Rec , ' ')
        break
      else
        let #TagLength = (#EndTagPosition - #BeginTagPosition) + 1
        let $Tag = substr($RV1Data_Full_Rec , #BeginTagPosition, #TagLength)
        let #TagSlashPosition = instr($Tag, '/', 1)

        if #TagSlashPosition <> 2
          let $Element_Tag = $Tag
        end-if

        let $RV1Data_Full_Rec = ltrim (replace($RV1Data_Full_Rec , $Tag, ' '),' ')

        if instr($RV1Data_Full_Rec , '<',1) = 0
          let $Element = $RV1Data_Full_Rec    !Element left after tags removed!
        end-if
        break

      end-if

    end-while

    if #RecLength =  #EndTagPosition          !No element found!
      break
    end-if

  end-while

  let $Element = replace($Element, '&amp;'  , '&')
  let $Element = replace($Element, '&lt;'   , '<')
  let $Element = replace($Element, '&quot;' , '"')
  let $Element = replace($Element, '&gt; '  , '>')
  let $Element = replace($Element, '&apos;' , '''')

end-procedure


begin-procedure Extract-Transmitter-Data

  let $Element_Tag = replace($Element_Tag, '<', ' ')
  let $Element_Tag = replace($Element_Tag, '>', ' ')
  let $Element_Tag = ltrim($Element_Tag, ' ')
  let $Element_Tag = rtrim($Element_Tag, ' ')

  evaluate $Element_Tag
    when = 'Annee'
      let $TaxYear                     = $Element
      break
    when = 'TypeEnvoi'
      let $PackageType                 = $Element
      break
    when = 'Provenance'
      let $ReleveSource                = $Element
      break
    when = 'No'
      let $TransmitterNumber           = $Element
      break
    when = 'Type'
      let $TransmitterType             = $Element
      break
    when = 'Nom1'
      let $TransmitterNameLine1        = $Element
      break
    when = 'Ligne1'
      let $TransmitterAddrLine1        = $Element
      break
    when = 'Ligne2'
      let $TransmitterAddrLine2        = $Element
      break
    when = 'Ville'
      let $TransmitterCity             = $Element
      break
    when = 'Province'
      let $TransmitterProvince         = $Element
      break
    when = 'CodePostal'
      let $TransmitterPostalCode       = $Element
      break
    when = 'Nom'
      if $Group_Tag = '<Informatique>'
        let $TechResourceName          = $Element
      else
        if $Group_Tag = '<Comptabilite>'
          let $AcctingResourceName     = $Element
        end-if
      end-if
      break
    when = 'IndRegional'
      if $Group_Tag = '<Informatique>'
        let $TechResourceAreaCode      = $Element
      else
        if $Group_Tag = '<Comptabilite>'
          let $AcctingResourceAreaCode = $Element
        end-if
      end-if
      break
    when = 'Tel'
      if $Group_Tag = '<Informatique>'
        let $TechResourcePhone         = $Element
      else
        if $Group_Tag = '<Comptabilite>'
          let $AcctingResourcePhone    = $Element
        end-if
      end-if
      break
    when = 'PosteTel'
      if $Group_Tag = '<Informatique>'
        let $TechResourceExtNo         = $Element
      else
        if $Group_Tag = '<Comptabilite>'
          let $AcctingResourceExtNo    = $Element
        end-if
      end-if
      break
    when = 'Langue'
      if $Group_Tag = '<Informatique>'
        let $TechResourceLanguage      = $Element
      else
        if $Group_Tag = '<Comptabilite>'
          let $AcctingResourceLanguage = $Element
        end-if
      end-if
      break
    when = 'NoCertification'
      let $RL1ApprovalNum              = $Element
      break
    when-other
      break
  end-evaluate

end-procedure


begin-procedure Initialize-Transmitter

  let $TaxYear                     =  ' '
  let $PackageType                 =  ' '
  let $ReleveSource                =  ' '
  let $TransmitterNumber           =  ' '
  let $TransmitterType             =  ' '
  let $TransmitterNameLine1        =  ' '
  let $TransmitterAddrLine1        =  ' '
  let $TransmitterAddrLine2        =  ' '
  let $TransmitterCity             =  ' '
  let $TransmitterProvince         =  ' '
  let $TransmitterPostalCode       =  ' '
  let $TechResourceName            =  ' '
  let $AcctingResourceName         =  ' '
  let $TechResourceAreaCode        =  ' '
  let $AcctingResourceAreaCode     =  ' '
  let $TechResourcePhone           =  ' '
  let $AcctingResourcePhone        =  ' '
  let $TechResourceExtNo           =  ' '
  let $AcctingResourceExtNo        =  ' '
  let $TechResourceLanguage        =  ' '
  let $AcctingResourceLanguage     =  ' '
  let $RL1ApprovalNum              =  ' '

end-procedure


begin-procedure Extract-EE-Detail-Data

  let $Element_Tag = replace($Element_Tag, '<', ' ')
  let $Element_Tag = replace($Element_Tag, '>', ' ')
  let $Element_Tag = ltrim($Element_Tag, ' ')
  let $Element_Tag = rtrim($Element_Tag, ' ')

  evaluate $Element_Tag
    when = 'Annee'
      let $TaxYear                     = $Element
      break
    when = 'NoReleve'
      let $ReleveSlipNo                = $Element
      break
    when = 'NAS'
      let $E_SIN                       = $Element
      break
    when = 'No'
      let $EmplID                      = $Element
      break
    when = 'NomFamille'
      let $E_LastName                  = $Element
      break
    when = 'Prenom'
      let $E_FirstName                 = $Element
      break
    when = 'Initiale'
      let $E_Initial                   = $Element
      break
    when = 'Ligne1'
      let $E_AddrLine1                 = $Element
      break
    when = 'Ligne2'
      let $E_AddrLine2                 = $Element
      break
    when = 'Ville'
      let $E_City                      = $Element
      break
    when = 'Province'
      let $E_Province                  = $Element
      break
    when = 'CodePostal'
      let $E_PostalCode                = $Element
      break
    when = 'A_RevenuEmploi'
      let $BoxA                        = $Element
      break
    when = 'B_CotisationRRQ'
      let $BoxB                        = $Element
      break
    when = 'C_CotisationAssEmploi'
      let $BoxC                        = $Element
      break
    when = 'D_CotisationRPA'
      let $BoxD                        = $Element
      break
    when = 'E_ImpotQue'
      let $BoxE                        = $Element
      break
    when = 'F_CotisationSyndicale'
      let $BoxF                        = $Element
      break
    when = 'G_SalaireAdmisRRQ'
      let $BoxG                        = $Element
      break
    when = 'H_CotisationRQAP'
      let $BoxH                        = $Element
      break
    when = 'I_SalaireAdmisRQAP'
      let $BoxI                        = $Element
      break
    when = 'J_RegimeAssMaladie'
      let $BoxJ                        = $Element
      break
    when = 'K_Voyage'
      let $BoxK                        = $Element
      break
    when = 'L_AutreAvantage'
      let $BoxL                        = $Element
      break
    when = 'M_Commission'
      let $BoxM                        = $Element
      break
    when = 'N_DonBienfaisance'
      let $BoxN                        = $Element
      break
    when = 'MontantCaseO'
      let $BoxO                        = $Element
      break
    when = 'SourceCaseO'
      let $BoxCode                     = $Element
      break
    when = 'P_RegimeAssInterEntr'
      let $BoxP                        = $Element
      break
    when = 'Q_SalaireDiffere'
      let $BoxQ                        = $Element
      break
    when = 'R_RevenuIndien'
      let $BoxR                        = $Element
      break
    when = 'S_PourboireRecu'
      let $BoxS                        = $Element
      break
    when = 'T_PourboireAttribue'
      let $BoxT                        = $Element
      break
    when = 'U_RetraiteProgressive'
      let $BoxU                        = $Element
      break
    when = 'V_NourritureLogement'
      let $BoxV                        = $Element
      break
    when = 'W_Vehicule'
      let $BoxW                        = $Element
      break
    when = 'CotisationRPC'
      let $BoxB01                      = $Element
      break
    when = 'DeductionForce'
      let $DeductionForce              = $Element
      break
    when = 'NoReleveDerniereTrans'
      let $LastTransSlipNo             = $Element
      break
    when-other
      break
  end-evaluate

end-procedure


begin-procedure Initialize-Detail

  let $TaxYear        = ' '
  let $ReleveSlipNo   = ' '
  let $E_SIN          = ' '
  let $EmplID         = ' '
  let $E_LastName     = ' '
  let $E_FirstName    = ' '
  let $E_Initial      = ' '
  let $E_AddrLine1    = ' '
  let $E_AddrLine2    = ' '
  let $E_City         = ' '
  let $E_Province     = ' '
  let $E_PostalCode   = ' '
  let $BoxA           = ' '
  let $BoxB           = ' '
  let $BoxC           = ' '
  let $BoxD           = ' '
  let $BoxE           = ' '
  let $BoxF           = ' '
  let $BoxG           = ' '
  let $BoxH           = ' '
  let $BoxI           = ' '
  let $BoxJ           = ' '
  let $BoxK           = ' '
  let $BoxL           = ' '
  let $BoxM           = ' '
  let $BoxN           = ' '
  let $BoxO           = ' '
  let $BoxCode        = ' '
  let $BoxP           = ' '
  let $BoxQ           = ' '
  let $BoxR           = ' '
  let $BoxS           = ' '
  let $BoxT           = ' '
  let $BoxU           = ' '
  let $BoxV           = ' '
  let $BoxW           = ' '
  let $BoxB01         = ' '
  let $LastTransSlipNo = ' '
  let $DeductionForce = ' '

end-procedure


begin-procedure Extract-Totals-Data

  let $Element_Tag = replace($Element_Tag, '<', ' ')
  let $Element_Tag = replace($Element_Tag, '>', ' ')
  let $Element_Tag = ltrim($Element_Tag, ' ')
  let $Element_Tag = rtrim($Element_Tag, ' ')

  evaluate $Element_Tag
    when = 'Annee'
      let $TaxYear                     = $Element
      break
    when = 'NbReleves'
      let $RV1Count                    = $Element
      break
    when = 'NoId'
      let $ReportingId                 = $Element
      break
    when = 'TypeDossier'
      let $ReportingId                 = rtrim($ReportingId, ' ') || $Element
      break
    when = 'NoDossier'
      let $ReportingId                 = rtrim($ReportingId, ' ') || $Element
      break
    when = 'NEQ'
      let $NEQ                         = $Element
      break
    when = 'Nom1'
      let $CompanyName                 = $Element
      break
    when = 'Ligne1'
      let $C_Street1                   = $Element
      break
    when = 'Ligne2'
      let $C_Street2                   = $Element
      break
    when = 'Ville'
      let $C_City                      = $Element
      break
    when = 'Province'
      let $C_Province                  = $Element
      break
    when = 'CodePostal'
      let $C_PostalCode                = $Element
      break
    when = 'A_RevenuEmploi'
      let $SumEmplIncome               = $Element
      break
    when = 'B_CotisationRRQ'
      let $SumQPPContrib               = $Element
      break
    when = 'C_CotisationAssEmploi'
      let $SumUIPrem                   = $Element
      break
    when = 'D_CotisationRPA'
      let $SumRRPContrib               = $Element
      break
    when = 'E_ImpotQue'
      let $SumQITWheldSource           = $Element
      break
    when = 'F_CotisationSyndicale'
      let $SumUnionDues                = $Element
      break
    when = 'G_SalaireAdmisRRQ'
      let $SumQPPPEarns                = $Element
      break
    when = 'H_CotisationRQAP'
      let $SumQPIPPrem                 = $Element
      break
    when = 'I_SalaireAdmisRQAP'
      let $SumQPIPEarns                = $Element
      break
    when = 'J_RegimeAssMaladie'
      let $SumPrivHPlan                = $Element
      break
    when = 'K_Voyage'
      let $SumTripsRem                 = $Element
      break
    when = 'L_AutreAvantage'
      let $SumOtherB                   = $Element
      break
    when = 'M_Commission'
      let $SumComm                     = $Element
      break
    when = 'N_DonBienfaisance'
      let $SumCharDon                  = $Element
      break
    when = 'O_AutreRevenu'
      let $SumOthTaxInc                = $Element
      break
    when = 'P_RegimeAssInterEntr'
      let $SumContribMEIP              = $Element
      break
    when = 'Q_SalaireDiffere'
      let $SumDefWage                  = $Element
      break
    when = 'R_RevenuIndien'
      let $SumIndian                   = $Element
      break
    when = 'S_PourboireRecu'
      let $SumTip                      = $Element
      break
    when = 'T_PourboireAttribue'
      let $SumTipsAllocated            = $Element
      break
    when = 'U_RetraiteProgressive'
      let $SumPhasedRetire             = $Element
      break
    when = 'V_NourritureLogement'
      let $SumMealsAccom               = $Element
      break
    when = 'W_Vehicule'
      let $SumEmployAuto               = $Element
      break
    when = 'CotisationRPC'
      let $SumCPPContrib               = $Element
      break
    when = 'DeductionForce'
      let $SumDeductionForce           = $Element
      break
    when-other
      break
  end-evaluate

end-procedure


begin-procedure Initialize-Totals

  let $TaxYear           = ' '
  let $RV1Count          = ' '
  let $ReportingId       = ' '
  let $NEQ               = ' '
  let $CompanyName       = ' '
  let $C_Street1         = ' '
  let $C_Street2         = ' '
  let $C_City            = ' '
  let $C_Province        = ' '
  let $C_PostalCode      = ' '
  let $SumEmplIncome     = ' '
  let $SumQPPContrib     = ' '
  let $SumUIPrem         = ' '
  let $SumRRPContrib     = ' '
  let $SumQITWheldSource = ' '
  let $SumUnionDues      = ' '
  let $SumQPPPEarns      = ' '
  let $SumMealsAccom     = ' '
  let $SumEmployAuto     = ' '
  let $SumPrivHPlan      = ' '
  let $SumTripsRem       = ' '
  let $SumOtherB         = ' '
  let $SumComm           = ' '
  let $SumCharDon        = ' '
  let $SumOthTaxInc      = ' '
  let $SumContribMEIP    = ' '
  let $SumDefWage        = ' '
  let $SumIndian         = ' '
  let $SumTip            = ' '
  let $SumTipsAllocated  = ' '
  let $SumPhasedRetire   = ' '
  let $SumQPIPPrem       = ' '
  let $SumQPIPEarns      = ' '
  let $SumCPPContrib     = ' '
  let $SumDeductionForce = ' '

end-procedure


begin-procedure Print-Transmittal-Record

   print $Ctx912r_Trans_rec              (+2,3)
   print $Ctx912r_Trans_number           (+2,3)
   print $TransmitterNumber              (0,60)
   print $Ctx912r_Taxation_year          (+2,3)
   print $TaxYear                        (0,60)
   print $Ctx912r_Trans_name             (+2,3)
   print $TransmitterNameLine1           (0,60)
   print $Ctx912r_Trans_addr1            (+1,3)
   print $TransmitterAddrLine1           (0,60)
   print $Ctx912r_Trans_addr2            (+1,3)
   print $TransmitterAddrLine2           (0,60)
   print $Ctx912r_Trans_city             (+1,3)
   print $TransmitterCity                (0,60)
   print $Ctx912r_Trans_prov             (+1,3)
   print $TransmitterProvince            (0,60)
   print $Ctx912r_Trans_post_code        (+1,3)
   print $TransmitterPostalCode          (0,60)
   print $Ctx912r_Tech_res_name          (+2,3)
   print $TechResourceName               (0,60)
   print $Ctx912r_Tech_res_area          (+1,3)
   print $TechResourceAreaCode           (0,60)
   print $Ctx912r_Tech_res_phne          (+1,3)
   print $TechResourcePhone              (0,60)
   print $Ctx912r_Tech_res_ext           (+1,3)
   if rtrim($TechResourceExtNo, ' ') <> ''
     print $TechResourceExtNo            (0,60)
   end-if
   print $Ctx912r_Tech_res_lang          (+1,3)
   print $TechResourceLanguage           (0,60)
   print $Ctx912r_Acct_res_name          (+2,3)
   print $AcctingResourceName            (0,60)
   print $Ctx912r_Acct_res_area          (+1,3)
   print $AcctingResourceAreaCode        (0,60)
   print $Ctx912r_Acct_res_phne          (+1,3)
   print $AcctingResourcePhone           (0,60)
   print $Ctx912r_Acct_res_ext           (+1,3)
   if rtrim($AcctingResourceExtNo, ' ') <> ''
     print $AcctingResourceExtNo         (0,60)
   end-if
   print $Ctx912r_Acct_res_lang          (+1,3)
   print $AcctingResourceLanguage        (0,60)

   if rtrim($RL1ApprovalNum, ' ') <> ''
     print $Ctx912r_Approval_Num         (+2,3)
     print $RL1ApprovalNum               (0,60)
   end-if

   print $Ctx912r_Pack_type              (+2,3)
   print $Ctx912r_Pack_type1             (0,+1)
   print $Ctx912r_Pack_type2             (+1,18)
   print $PackageType                    (0,50)
   print $Ctx912r_Trans_type             (+1,3)
   print $TransmitterType                (0,80)
   print $Ctx912r_Releve_source1         (+1,3)
   print $Ctx912r_Releve_source2         ()
   print $ReleveSource                   (0,+2)



end-procedure

begin-procedure Print-Detail

  next-listing need=7

  let $Surname = RTRIM($E_LastName, ' ')
  print $Surname (+3,3)
  print ',' (0,0)
  let $FirstName = RTRIM($E_FirstName, ' ')
  print $FirstName (0,+1)
  print $E_Initial (0,+1)
  print $Ctx912r_Sin    (0,40)
  print $E_SIN (0,46) edit xxx-xxx-xxx
  print $Ctx912r_Emplid (0,61)
  print $EmplID (0,70)

  print $Ctx912r_Slip_No (0,85)
  print $ReleveSlipNo (0,101)

  print $Ctx912r_TypeOfData (0,122)
  print $TypeOfData (0,137)

  if $TypeOfData = 'A'
    print $Ctx912r_LastTrans_SlipNo (+1,122)
    print $LastTransSlipNo      (0,151)
  end-if

  add 1 to #RV1PrintCount

  move 'Y' to $PrintAmount

  do Get-Box-Descr

  print $Ctx912r_Rv1_box_data   (+2,1)
  print ' '                     (+1,1)
  let #BoxAmt = $BoxA
  add 1 to #BoxCount
  let $Message = $MsgA
  add #BoxAmt to #AccumEmplIncome
  do Print-Box

  let #BoxAmt = $BoxB
  add 1 to #BoxCount
  let $Message = $MsgB
  add #BoxAmt to #AccumQPPContrib
  do Print-Box

  let #BoxAmt = $BoxC
  add 1 to #BoxCount
  let $Message = $MsgC
  add #BoxAmt to #AccumUIPrem
  do Print-Box

  let #BoxAmt = $BoxD
  add 1 to #BoxCount
  let $Message = $MsgD
  add #BoxAmt to #AccumRRPContrib
  do Print-Box

  let #BoxAmt = $BoxE
  add 1 to #BoxCount
  let $Message = $MsgE
  add #BoxAmt to #AccumQITWheldSource
  do Print-Box

  let #BoxAmt = $BoxF
  add 1 to #BoxCount
  let $Message = $MsgF
  add #BoxAmt to #AccumUnionDues
  do Print-Box

  let #BoxAmt = $BoxG
  add 1 to #BoxCount
  let $Message = $MsgG
  add #BoxAmt to #AccumQPPPEarns
  do Print-Box

  let #BoxAmt = $BoxH
  add 1 to #BoxCount
  let $Message = $MsgH
  add #BoxAmt to #AccumQPIPPrem
  do Print-Box

  let #BoxAmt = $BoxI
  add 1 to #BoxCount
  let $Message = $MsgI
  add #BoxAmt to #AccumQPIPEarns
  do Print-Box

  let #BoxAmt = $BoxJ
  add 1 to #BoxCount
  let $Message = $MsgJ
  add #BoxAmt to #AccumPrivHPlan
  do Print-Box

  let #BoxAmt = $BoxK
  add 1 to #BoxCount
  let $Message = $MsgK
  add #BoxAmt to #AccumTripsRem
  do Print-Box

  let #BoxAmt = $BoxL
  add 1 to #BoxCount
  let $Message = $MsgL
  add #BoxAmt to #AccumOtherB
  do Print-Box

  let #BoxAmt = $BoxM
  add 1 to #BoxCount
  let $Message = $MsgM
  add #BoxAmt to #AccumComm
  do Print-Box

  let #BoxAmt = $BoxN
  add 1 to #BoxCount
  let $Message = $MsgN
  add #BoxAmt to #AccumCharDon
  do Print-Box

  let #BoxAmt = $BoxO
  add 1 to #BoxCount
  let $Message = $MsgO
  add #BoxAmt to #AccumOthTaxInc
  do Print-Box

  let #BoxAmt = $BoxP
  add 1 to #BoxCount
  let $Message = $MsgP
  add #BoxAmt to #AccumContribMEIP
  do Print-Box

  let #BoxAmt = $BoxQ
  add 1 to #BoxCount
  let $Message = $MsgQ
  add #BoxAmt to #AccumDefWage
  do Print-Box

  let #BoxAmt = $BoxR
  add 1 to #BoxCount
  let $Message = $MsgR
  add #BoxAmt to #AccumIndian
  do Print-Box

  let #BoxAmt = $BoxS
  add 1 to #BoxCount
  let $Message = $MsgS
  add #BoxAmt to #AccumTip
  do Print-Box

  let #BoxAmt = $BoxT
  add 1 to #BoxCount
  let $Message = $MsgT
  add #BoxAmt to #AccumTipsAllocated
  do Print-Box

  let #BoxAmt = $BoxU
  add 1 to #BoxCount
  let $Message = $MsgU
  add #BoxAmt to #AccumPhasedRetire
  do Print-Box

  let #BoxAmt = $BoxV
  add 1 to #BoxCount
  let $Message = $MsgV
  add #BoxAmt to #AccumMealsAccom
  do Print-Box

  let #BoxAmt = $BoxW
  add 1 to #BoxCount
  let $Message = $MsgW
  add #BoxAmt to #AccumEmployAuto
  do Print-Box

  let $Message = $MsgCDO
  print $Message (+1,5)
  print $BoxCode (0,74)

  let #BoxAmt = $BoxB01
  add 1 to #BoxCount
  let $Message = $MsgB01
  add #BoxAmt to #AccumCPPContrib
  do Print-Box

  let #BoxAmt = $DeductionForce
  add 1 to #BoxCount
  let $Message = $MsgDedForce
  add #BoxAmt to #AccumDedForce
  do Print-Box

end-procedure


begin-procedure Print-Detail-Cancelled

  do Get-Box-Descr

  next-listing need=7

  let $Surname = RTRIM($E_LastName, ' ')
  print $Surname (+2,3)
  print ',' (0,0)
  let $FirstName = RTRIM($E_FirstName, ' ')
  print $FirstName (0,+1)
  print $E_Initial (0,+1)
  print $Ctx912r_Sin    (0,40)
  print $E_SIN (0,46) edit xxx-xxx-xxx

  print $Ctx912r_Slip_No (+1,3)
  print $ReleveSlipNo (0,+1)

  print $Ctx912r_TypeOfData (0,40)
  print $TypeOfData (0,+1)

  add 1 to #RV1PrintCount

end-procedure


begin-procedure Print-Box

   print $Message (+1,5)
   print #BoxAmt (0,65) edit 99999999.99

end-procedure


begin-procedure Print-Summary

     print $Ctx912r_Summ_msg               (+2,1)


     print    $Ctx912r_Summ_rec            (+2,3)
     print    $Ctx912r_Reporting_id        (+2,3)
     print    $ReportingId                 (0,60)
     print    $Ctx912r_Empler_name         (+1,3)
     print    $CompanyName                 (0,60)
     print    $Ctx912r_Address             (+1,3)
     print    $C_Street1                   (0,60)
     print    $Ctx912r_Address2            (+1,3)
     print    $C_Street2                   (0,60)
     print    $Ctx912r_City                (+1,3)
     print    $C_City                      (0,60)
     print    $Ctx912r_Province            (+1,3)
     print    $C_Province                  (0,60)
     print    $Ctx912r_Postal_code         (+1,3)
     print    $C_PostalCode                (0,60)
     print    $Ctx912r_NEQ                 (+2,3)
     print    $NEQ                         (0,60)
     print    $Ctx912r_Tax_year            (+2,3)
     print    $TaxYear                     (0,60)
     print    $Ctx912r_Total_rv1_recs      (+2,3)
     let #RV1Count = $RV1Count
     print    #RV1Count   (0,60)  edit 9999999
     print    $SumA                        (+2,3)
     let #SumEmplIncome = $SumEmplIncome
     print    #SumEmplIncome    (0,60) edit 9,999,999,999.99
     print    $SumB                        (+2,3)
     let #SumQPPContrib = $SumQPPContrib
     print    #SumQPPContrib    (0,60)  edit 9,999,999,999.99
     print    $SumC                        (+2,3)
     let #SumUIPrem = $SumUIPrem
     print    #SumUIPrem   (0,60)  edit 9,999,999,999.99
     print    $SumD                        (+2,3)
     let #SumRRPContrib  = $SumRRPContrib
     print    #SumRRPContrib    (0,60)  edit 9,999,999,999.99
     print    $SumE                        (+2,3)
     let #SumQITWheldSource = $SumQITWheldSource
     print    #SumQITWheldSource   (0,60)  edit 9,999,999,999.99
     print    $SumF                        (+2,3)
     let #SumUnionDues = $SumUnionDues
     print    #SumUnionDues     (0,60)  edit 9,999,999,999.99
     print    $SumG                        (+2,3)
     let #SumQPPPEarns         = $SumQPPPEarns
     print    #SumQPPPEarns    (0,60)  edit 9,999,999,999.99
     print    $SumH                        (+2,3)
     let #SumQPIPPrem          = $SumQPIPPrem
     print    #SumQPIPPrem     (0,60)  edit 9,999,999,999.99
     print    $SumI                        (+2,3)
     let #SumQPIPEarns         = $SumQPIPEarns
     print    #SumQPIPEarns    (0,60)  edit 9,999,999,999.99
     print    $SumJ                        (+2,3)
     let #SumPrivHPlan = $SumPrivHPlan
     print    #SumPrivHPlan  (0,60)  edit 9,999,999,999.99
     print    $SumK                        (+2,3)
     let #SumTripsRem = $SumTripsRem
     print    #SumTripsRem      (0,60)  edit 9,999,999,999.99
     print    $SumL                        (+2,3)
     let #SumOtherB    = $SumOtherB
     print    #SumOtherB      (0,60)  edit 9,999,999,999.99
     print    $SumM                        (+2,3)
     let #SumComm = $SumComm
     print #SumComm     (0,60)  edit 9,999,999,999.99
     print    $SumN                        (+2,3)
     let #SumCharDon = $SumCharDon
     print #SumCharDon (0,60) edit 9,999,999,999.99
     print    $SumO                        (+2,3)
     let #SumOthTaxInc = $SumOthTaxInc
     print #SumOthTaxInc (0,60) edit 9,999,999,999.99
     print    $SumP                        (+2,3)
     let #SumContribMEIP = $SumContribMEIP
     print #SumContribMEIP (0,60) edit 9,999,999,999.99
     print    $SumQ                        (+2,3)
     let #SumDefWage = $SumDefWage
     print #SumDefWage (0,60) edit 9,999,999,999.99
     print    $SumR                        (+2,3)
     let #SumIndian = $SumIndian
     print #SumIndian (0,60) edit 9,999,999,999.99
     print    $SumS                        (+2,3)
     let #SumTip = $SumTip
     print #SumTip (0,60) edit 9,999,999,999.99
     print    $SumT                        (+2,3)
     let #SumTipsAllocated  = $SumTipsAllocated
     print #SumTipsAllocated (0,60) edit 9,999,999,999.99
     print    $SumU                        (+2,3)
     let #SumPhasedRetire   = $SumPhasedRetire
     print #SumPhasedRetire  (0,60) edit 9,999,999,999.99
     print    $SumV                        (+2,3)
     let #SumMealsAccom     = $SumMealsAccom
     print    #SumMealsAccom       (0,60)  edit 9,999,999,999.99
     print    $SumW                        (+2,3)
     let #SumEmployAuto     = $SumEmployAuto
     print    #SumEmployAuto (0,60)  edit 9,999,999,999.99
     print    $MsgB01                      (+2,3)
     let #SumCPPContrib = $SumCPPContrib
     print #SumCPPContrib (0,60) edit 9,999,999,999.99
     print    $MsgDedForce                 (+2,3)
     let #SumDeductionForce = $SumDeductionForce
     print #SumDeductionForce  (0,60) edit 9,999,999,999.99

end-procedure


begin-procedure Print-Accumulated-Data

  print $Ctx912r_Acc_print_msg1     (+1,1)
  print $Ctx912r_Acc_print_msg2     (+1,1)


  print $Ctx912r_Accum_totals          (+2,1)
  print $Ctx912r_Report_id             (+2,1)
  print $ReportingId                   (0,60)
  print $Ctx912r_RV1_#slips            (+2,1)
  print #RV1PrintCount                 (0,60)  edit 9999999
  print $SumA                          (+2,1)
  print #AccumEmplIncome               (0,60)  edit 9,999,999,999.99
  print $SumB                          (+2,1)
  print #AccumQPPContrib               (0,60)  edit 9,999,999,999.99
  print $SumC                          (+2,1)
  print #AccumUIPrem                   (0,60)  edit 9,999,999,999.99
  print $SumD                          (+2,1)
  print #AccumRRPContrib               (0,60)  edit 9,999,999,999.99
  print $SumE                          (+2,1)
  print #AccumQITWheldSource           (0,60)  edit 9,999,999,999.99
  print $SumF                          (+2,1)
  print #AccumUnionDues                (0,60)  edit 9,999,999,999.99
  print $SumG                          (+2,1)
  print #AccumQPPPEarns                (0,60)  edit 9,999,999,999.99
  print $SumH                          (+2,1)
  print #AccumQPIPPrem                 (0,60)  edit 9,999,999,999.99
  print $SumI                          (+2,1)
  print #AccumQPIPEarns                (0,60)  edit 9,999,999,999.99
  print $SumJ                          (+2,1)
  print #AccumPrivHPlan                (0,60) edit 9,999,999,999.99
  print $SumK                          (+2,1)
  print #AccumTripsRem                 (0,60) edit 9,999,999,999.99
  print $SumL                          (+2,1)
  print #AccumOtherB                   (0,60) edit 9,999,999,999.99
  print $SumM                          (+2,1)
  print #AccumComm                     (0,60) edit 9,999,999,999.99
  print $SumN                          (+2,1)
  print #AccumCharDon                  (0,60) edit 9,999,999,999.99
  print $SumO                          (+2,1)
  print #AccumOthTaxInc                (0,60) edit 9,999,999,999.99
  print $SumP                          (+2,1)
  print #AccumContribMEIP              (0,60) edit 9,999,999,999.99
  print $SumQ                          (+2,1)
  print #AccumDefWage                  (0,60) edit 9,999,999,999.99
  print $SumR                          (+2,1)
  print #AccumIndian                   (0,60) edit 9,999,999,999.99
  print $SumS                          (+2,1)
  print #AccumTip                      (0,60) edit 9,999,999,999.99
  print $SumT                          (+2,1)
  print #AccumTipsAllocated            (0,60) edit 9,999,999,999.99
  print $SumU                          (+2,1)
  print #AccumPhasedRetire             (0,60) edit 9,999,999,999.99
  print $SumV                          (+2,1)
  print #AccumMealsAccom               (0,60)  edit 9,999,999,999.99
  print $SumW                          (+2,1)
  print #AccumEmployAuto               (0,60) edit 9,999,999,999.99
  print $MsgB01                        (+2,1)
  print #AccumCPPContrib               (0,60) edit 9,999,999,999.99
  print $MsgDedForce                   (+2,1)
  print #AccumDedForce                 (0,60) edit 9,999,999,999.99

  move 0 to #AccumEmplIncome
  move 0 to #AccumQPPContrib
  move 0 to #AccumUIPrem
  move 0 to #AccumRRPContrib
  move 0 to #AccumQITWheldSource
  move 0 to #AccumUnionDues
  move 0 to #AccumQPPPEarns
  move 0 to #AccumMealsAccom
  move 0 to #AccumEmployAuto
  move 0 to #AccumPrivHPlan
  move 0 to #AccumTripsRem
  move 0 to #AccumOtherB
  move 0 to #AccumComm
  move 0 to #AccumCharDon
  move 0 to #AccumOthTaxInc
  move 0 to #AccumContribMEIP
  move 0 to #AccumDefWage
  move 0 to #AccumIndian
  move 0 to #AccumTip
  move 0 to #AccumTipsAllocated
  move 0 to #AccumPhasedRetire
  move 0 to #AccumQPIPPrem
  move 0 to #AccumQPIPEarns
  move 0 to #AccumCPPContrib
  move 0 to #AccumDedForce
  move 0 to #RV1PrintCount

end-procedure


begin-procedure Get-Box-Descr

  move 0 to #i

  while #i <= #max_R1boxes

        get $PrBox $PrDescr from PrR1_Area(#i)
                   PR1_Box PR1_Descr
        let $MsgBox = RTRIM($PrBox, ' ')
        let $MsgDescr = RTRIM($PrDescr, ' ') || ' (Box ' || RTRIM($PrBox, ' ') || ')'
        let $MsgSum   = RTRIM($PrDescr, ' ')

        evaluate $MsgBox
        when = 'A'
           let $MsgA = $MsgDescr
           let $SumA = $MsgSum
           break
        when = 'B'
           let $MsgB = $MsgDescr
           let $SumB = $MsgSum
           break
        when = 'C'
           let $MsgC = $MsgDescr
           let $SumC = $MsgSum
           break
        when = 'D'
           let $MsgD = $MsgDescr
           let $SumD = $MsgSum
           break
        when = 'E'
           let $MsgE = $MsgDescr
           let $SumE = $MsgSum
           break
        when = 'F'
           let $MsgF = $MsgDescr
           let $SumF = $MsgSum
           break
        when = 'G'
           let $MsgG = $MsgDescr
           let $SumG = $MsgSum
           break
        when = 'H'
           let $MsgH = $MsgDescr
           let $SumH = $MsgSum
           break
        when = 'I'
           let $MsgI = $MsgDescr
           let $SumI = $MsgSum
           break
        when = 'J'
           let $MsgJ = $MsgDescr
           let $SumJ = $MsgSum
           break
        when = 'K'
           let $MsgK = $MsgDescr
           let $SumK = $MsgSum
           break
        when = 'L'
           let $MsgL = $MsgDescr
           let $SumL = $MsgSum
           break
        when = 'M'
           let $MsgM = $MsgDescr
           let $SumM = $MsgSum
           break
        when = 'N'
           let $MsgN = $MsgDescr
           let $SumN = $MsgSum
           break
        when = 'O'
           let $MsgO = $MsgDescr
           let $SumO = $MsgSum
           break
        when = 'P'
           let $MsgP = $MsgDescr
           let $SumP = $MsgSum
           break
        when = 'Q'
           let $MsgQ = $MsgDescr
           let $SumQ = $MsgSum
           break
        when = 'R'
           let $MsgR = $MsgDescr
           let $SumR = $MsgSum
           break
        when = 'S'
           let $MsgS = $MsgDescr
           let $SumS = $MsgSum
           break
        when = 'T'
           let $MsgT = $MsgDescr
           let $SumT = $MsgSum
           break
        when = 'U'
           let $MsgU = $MsgDescr
           let $SumU = $MsgSum
           break
        when = 'V'
           let $MsgV = $MsgDescr
           let $SumV = $MsgSum
           break
        when = 'W'
           let $MsgW = $MsgDescr
           let $SumW = $MsgSum
           break
        when = 'CDO'
           let $MsgCDO = RTRIM($PrDescr, ' ')
           break
        when = 'B01'
           let $MsgB01 = RTRIM($PrDescr, ' ')
           break
        when = 'A43'
        when = 'A44'
           let $MsgDedForce = $Ctx912r_Ded_Force
           break
        when-other
           break

        end-evaluate

        add 1 to #i

  end-while

end-procedure

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
!#Include 'getcodta.sqc' !Get-Company-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getdptnm.sqc'  !Get-Department-Name
#ifdef PRCSSCHD
#Include 'ctxrctl1.sqc'  !Select-Canadian-YrEnd-Parameters
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls
#endif
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
