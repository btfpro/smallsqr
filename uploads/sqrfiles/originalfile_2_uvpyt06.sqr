!***********************************************************************
! UVPYT06: Selects and prints data from the ADDL_PAY_DATA table for    *
!  1099R Retirees without an End Date where the maximum EFFDT is       *
!  before the retirement date.  Since there is no End Date the data    *
!  belong to both the employment and to the retirement history.        *
!***********************************************************************
! Report Name:  UVPYT06.SQR - 1099R Upgrade Conversion Report          *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of PeopleSoft, Inc.; it is not to be copied, reproduced, or          *
! transmitted in any form, by any means, in whole or in part,          *
! nor is it to be used for any purpose other than that for             *
! which it is expressly provided under the applicable license          *
! agreement.                                                           *
!                                                                      *
! Copyright (c) 1988-2003 PeopleSoft, Inc. All Rights Reserved.        *
!                                                                      *
!----------------------------------------------------------------------
!
!                $Date:  2006/12/29:16:36:35                             !
!             $Release:  hcm9                                       !
!            $Revision:  N/A                                           !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and page-size initialization

!***********************************************************************

begin-program
        do Init-DateTime
        do Get-Current-DateTime
        do Init-Report
        display 'Report began at: ' NoLine
        do Display-Time
        do Process-Main
        do Reset
        do Stdapi-Term
        display 'Report ended at: ' NoLine
        do Display-Time
end-program

begin-heading 7
#include 'stdhdg01.sqc'

 PRINT 'EMPLID'                  (+1,1,11)
 PRINT 'EMPL_RCD'                (0,+2,+10)
 PRINT 'ERNCD'                   (0,+2,+8)
 PRINT 'EFFDT'                   (0,+2,+10)
 PRINT 'EARNINGS_END_DT'         (0,+4,+15)
 PRINT 'Retirement Dt'           (0,+5,+13)

end-heading

!***********************************************************************
!  Function:     Init Report
!  Description:  Prompt for run-time parameters
!***********************************************************************

begin-procedure Init-Report

  move 'UVPYT06' to $ReportID
  move '1099R ADDL_PAY_DATA before Retirement without End Date' to $ReportTitle
  display $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
    !No Prompt
  end-if

end-procedure Init-Report

begin-procedure Get-Values
!No Prompts
end-procedure Get-Values

!***********************************************************************
!  Function:     Process-Main
!  Description:  This is the main report procedure.  Calls select routines
!                for data in the ADDL_PAY_DATA table.
!***********************************************************************

begin-procedure Process-Main

  do GET-RET_ADDL_PAY_ERNCD

end-procedure Process-Main



!***********************************************************************
!  Procedure:    GET-RET_ADDL_PAY_ERNCD
!  Description:  This is the main report procedure.  Selects and prints
!                data from the ADDL_PAY_DATA table for 1099R retirees 
!                without an EARNINGS_END_DT and a maximum EFFDT before 
!                the retirement date.  
!***********************************************************************

begin-procedure GET-RET_ADDL_PAY_ERNCD
 let #COUNTER = 0

begin-SELECT ON-ERROR=SQL-Error
A.EMPLID                                (+1,1,11)
A.EMPL_RCD                              (0,+2,+10)
A.ERNCD                                 (0,+2,+8)
A.EFFDT                                 (0,+2,+10)
A.EARNINGS_END_DT                       (0,+4,+10)
B.EFFDT                                 (0,+10,+10)

 let #COUNTER = #COUNTER + 1

FROM PS_ADDL_PAY_DATA A
, PS_JOB B
, PS_COMPANY_TBL C
WHERE A.EMPLID = B.EMPLID
AND A.EMPL_RCD = B.EMPL_RCD
AND B.COMPANY = C.COMPANY
AND A.EFFDT = (SELECT MAX(A1.EFFDT)
  FROM PS_ADDL_PAY_DATA A1
  WHERE A1.EMPLID = A.EMPLID
  AND A1.EMPL_RCD = A.EMPL_RCD
  AND A1.ERNCD = A.ERNCD)
AND C.EFFDT = (SELECT MAX(C1.EFFDT)
  FROM PS_COMPANY_TBL C1
  WHERE C1.COMPANY = C.COMPANY
  AND C1.EFFDT <= $AsOfToday)
AND C.EFF_STATUS = 'A'
AND C.TAX_REPORT_TYPE = 'R'
AND B.EFFDT = (SELECT MIN(B1.EFFDT)
  FROM PS_JOB B1
  WHERE B1.EMPLID = B.EMPLID
  AND B1.COMPANY = B.COMPANY)
AND A.EARNINGS_END_DT IS NULL
AND A.EFFDT < B.EFFDT

END-SELECT

 let $COUNTER = EDIT(#COUNTER, '999')

 print 'Number of rows selected:  '                      (+2,1,26)
 print $COUNTER                                          (,+1,)

end-procedure GET-RET_ADDL_PAY_ERNCD


!*************************************
Begin-Procedure Display-Time
!*************************************

  do Get-Current-DateTime
  display $AsOfToday NoLine
  display ', '       NoLine
  display $AsOfNow
  display ''

End-Procedure

!*************************************


!***********************************************************************
! SQC Files for called procedures
!***********************************************************************
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Routines to update run status
#include 'prcsapi.sqc'   !Update Process API
#Include 'reset.sqc'     !Reset printer procedure


