!***********************************************************************
!  Pay718: Print contents of Tax Location Table                                *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:04:34                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY718' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language
  move $Pay718_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY718 program ids
  do Init_Report_Translation($ReportID,$language_cd)

  do Get_Field_Information ('PAY718', 'REPORT_TITLE', $Pay718_ReportTitle, #dummy_width)
  do Get_Field_Information ('PAY718', 'TAX_LOC_CD',   $Pay718_Tax_Loc_Cd,  #dummy_width)
  do Get_Field_Information ('PAY718', 'EFF_STATUS',   $Pay718_Eff_Status,  #dummy_width)
  do Get_Field_Information ('PAY718', 'DESCR',        $Pay718_Descr,       #dummy_width)
  do Get_Field_Information ('PAY718', 'DESCRSHORT',   $Pay718_DescrShort,  #dummy_width)
  do Get_Field_Information ('PAY718', 'ADDRESS',      $Pay718_Address,     #dummy_width)
  do Get_Field_Information ('PAY718', 'COUNTY',       $Pay718_County,      #dummy_width)
  do Get_Field_Information ('PAY718', 'AK_AREA_CD',   $Pay718_Ak_Area_Cd,  #dummy_width)
  do Get_Field_Information ('PAY718', 'STATE',        $Pay718_State,       #dummy_width)
  do Get_Field_Information ('PAY718', 'LOCALITY',     $Pay718_Locality,    #dummy_width)
  do Get_Field_Information ('PAY718', 'LOC_LINK',     $Pay718_Loc_Link,    #dummy_width)

end-procedure


begin-HEADING 7

  #Include 'stdhdgtr.sqc'

  print $Pay718_Tax_Loc_Cd               (+3,1,12)
  print $Pay718_Eff_Status               (0,14)
  print $Pay718_Descr                    (0,21)
  print $Pay718_DescrShort               (0,47)
  print $Pay718_Address                  (0,59)
  print $Pay718_County                   (0,90)
  print $Pay718_Ak_Area_Cd               (0,101)
  print $Pay718_State                    (0,126)
  print $Pay718_Locality                 (0,135)
  print $Pay718_Loc_Link                 (0,158)


end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

A.TAX_LOCATION_CD       (+2,1,12)
A.EFF_STATUS            (0,+1,6)
A.DESCR                 !(0,+1,25)
A.DESCRSHORT            !(0,+1,10)
A.ADDRESS1              !(0,+2,30)
A.COUNTY                !(0,+1,10)

          move &A.DESCR      to $PRNT_DESCR
          move &A.DESCRSHORT to $PRNT_DESCRSHORT
          move &A.ADDRESS1   to $PRNT_ADDRESS1
          move &A.COUNTY     to $PRNT_COUNTY
          move &A.ADDRESS2   to $PRNT_ADDRESS2
          move &A.CITY       to $PRNT_CITY
        if $Curr_language_cd <> $Psoptions_Language_Cd
          do Lang-Record-Details
        end-if
          print $PRNT_DESCR              (0,+1,25)
          print $PRNT_DESCRSHORT         (0,+1,10)
          print $PRNT_ADDRESS1           (0,+2,30)
          print $PRNT_COUNTY             (0,+1,10)
A.AK_AREA_CD            (0,+1,2)
          print $PRNT_ADDRESS2           (+1,59,20)
          print $PRNT_CITY               (+1,59,15)

A.ADDRESS2              !(+1,59,20)
A.CITY                  !(+1,59,15)
A.STATE                 (0,+1,6)
A.POSTAL                (0,+2,10)
A.COUNTRY               (+1,59,5)

     if &A.Country='CAN'
        print &A.State (+1,126,6)
     else
        do Print-States-Localities
     end-if

FROM PS_TAX_LOCATION1 A
ORDER BY A.TAX_LOCATION_CD

END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Lang-Record-Details
BEGIN-SELECT
LA.DESCR             &LA.DESCR
        let $PRNT_DESCR = Cond(Rtrim(&LA.DESCR,' ')='', &A.DESCR, &LA.DESCR)
LA.DESCRSHORT        &LA.DESCRSHORT
        let $PRNT_DESCRSHORT = Cond(Rtrim(&LA.DESCRSHORT,' ')='', &A.DESCRSHORT, &LA.DESCRSHORT)

FROM PS_TAX_LOCAT1_LANG LA

WHERE LA.TAX_LOCATION_CD = &A.TAX_LOCATION_CD
  AND LA.LANGUAGE_CD     = $Curr_language_cd

END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Print-States-Localities
BEGIN-SELECT

G.STATE                 (+1,126,6)
G.LOCALITY              (0,+3,10)
  if RTRIM(&G.Locality, ' ') <> ''
    move &G.State to $State
    move &G.Locality to $Locality
    do Get-Locality-Name
    print $Prnt_Locality_abbrv      (0,+1,10)
    let $Prnt_Locality_abbrv = ' '
  else
    print ' '                           (0,+1,10)
  end-if

G.LOCALITY_LINK         (0,+3,10)
  if RTRIM(&G.Locality_Link, ' ') <> ''
    move &G.State to $State
    move &G.Locality_Link to $Locality
    do Get-Locality-Name
    print $Prnt_Locality_abbrv      (0,+1,10)
    let $Prnt_Locality_abbrv = ' '
  else
    print ' '                           (0,+1,10)
  end-if

FROM PS_TAX_LOCATION2 G
WHERE G.TAX_LOCATION_CD = &A.TAX_LOCATION_CD

END-SELECT
END-PROCEDURE



BEGIN-PROCEDURE Get-Locality-Name
BEGIN-SELECT

L.LOCALITY_ABBRV

     Let $Prnt_Locality_abbrv = &L.LOCALITY_ABBRV
!     if $Curr_language_cd <> $Psoptions_Language_Cd
!       do Lang-Record-Loc-abbv1
!     end-if

FROM PS_LOCAL_TAX_TBL2 L
WHERE L.STATE            = $State
  AND L.LOCALITY         = $Locality
  AND L.EFFDT            =
      ( SELECT MAX(EFFDT)
          FROM PS_LOCAL_TAX_TBL2
         WHERE STATE    = $State
           AND LOCALITY = $Locality
           AND EFFDT <= $AsOfToday)

END-SELECT
END-PROCEDURE

! begin-procedure Lang-Record-Loc-abbv1
! begin-select

! LL.LOCALITY_ABBRV

!  let $Prnt_Locality_abbrv = Cond(Rtrim(&LL.LOCALITY_ABBRV,' ')='', $Prnt_Locality_abbrv, &LL.LOCALITY_ABBRV)

! FROM PS_LOCAL_TXTB2_LNG LL
! WHERE LL.STATE            = $State
!   AND LL.LOCALITY         = $Locality
!   AND LL.LANGUAGE_CD      = $Curr_language_cd
!   AND LL.EFFDT            =
!       ( SELECT MAX(EFFDT)
!           FROM PS_LOCAL_TAX_TBL
!          WHERE STATE          = $State
!            AND LOCALITY       = $Locality
!              AND LL.LANGUAGE_CD = $Curr_language_cd)

! end-select
! end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
