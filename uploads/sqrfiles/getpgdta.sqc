!*******************************************************
!  GetPGdta:  Get selected fields from PayGroup Table  *
!*******************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:10:54                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!********************************
begin-procedure Get-PayGroup-Data
!********************************

begin-SELECT
FQT.FREQUENCY_TYPE,PGT.DESCR,PGT.DESCRSHORT
ERNCD_REG_HRS,ERNCD_OT_HRS,ERNCD_REG_EARNS
ERNCD_HOLIDAY,ERNCD_HOL_NONSTAT
TIPS_DELAY_WTHLD,TIPS_ADJUST,ERNCD_TIPS_ADJUST,ERNCD_TIPS_CRED ! New Tips fields
PGT.COUNTRY,PGT.EFFDT

FROM  PS_PAYGROUP_TBL PGT , PS_FREQUENCY_TBL FQT
WHERE PGT.COMPANY  = $Company
  AND PGT.PAYGROUP = $PayGroup
  AND PGT.EFFDT =
      (SELECT MAX(PGT1.EFFDT)
       FROM   PS_PAYGROUP_TBL PGT1
       WHERE  PGT1.COMPANY  = $Company
         AND  PGT1.PAYGROUP = $PayGroup
         AND  PGT1.EFFDT   <= $AsOfDate)
  AND PGT.PAY_FREQUENCY    = FQT.FREQUENCY_ID
  AND FQT.EFF_STATUS   = 'A'
  AND FQT.EFFDT        =
      (SELECT MAX(FQT1.EFFDT)
       FROM   PS_FREQUENCY_TBL FQT1
       WHERE  FQT1.FREQUENCY_ID  = FQT.FREQUENCY_ID
         AND  FQT1.EFF_STATUS    = FQT.EFF_STATUS
         AND  FQT1.EFFDT        <= PGT.EFFDT)
end-SELECT
  move &PGT.Descr      to $PayGroupName
  move &PGT.DescrShort to $PayGroupShortName
  move &PGT.Country    to $PayGroupCountry
  move &PGT.Effdt      to $Basedate

!-------------------------------------------------------------------------!
! Only retrieve data from the related language table if                   !
! the operator language is not the same to the installation               !
! language or $curr_language_cd  is different from $PsOptions_Language_Cd !
!-------------------------------------------------------------------------!

  if $curr_language_cd <> $PsOptions_Language_Cd
     do Get-Rel-Lang-Pay
  end-if

end-procedure


!*******************************!
begin-procedure Get-Rel-Lang-Pay
!*******************************!

begin-SELECT

PGL.DESCR,PGL.DESCRSHORT

     if rtrim(&PGL.Descr,' ') <> ''
        let $PayGroupName      = rtrim(&PGL.Descr,' ')
     end-if

     if rtrim(&PGL.DescrShort,' ') <> ''
        let $PayGroupShortName = rtrim(&PGL.DescrShort,' ')
     end-if

FROM  PS_PAYGROUP_LANG PGL
WHERE  PGL.COMPANY       = $Company
   AND PGL.PAYGROUP      = $PayGroup
   AND PGL.LANGUAGE_CD   = $Curr_language_cd
   AND PGL.EFFDT         =
      (SELECT MAX(EFFDT)
       FROM   PS_PAYGROUP_LANG
       WHERE  COMPANY     = $Company
         AND  PAYGROUP    = $PayGroup
         AND  LANGUAGE_CD = PGL.LANGUAGE_CD
         AND  EFFDT      <= $Basedate)

end-SELECT


end-procedure
