!***********************************************************************
! FGTSPUPD.SQC:  Logic to keep the TSP Elections Record and the TSP    *
!               Update Indicator field on the GVT_JOB record in synch. *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:03:57:54                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

!***********************************************************************
!                                                                      *
!  Function:     Update-TSP-Investment-Elections                       *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Update-TSP-Investment-Elections

Begin-Select

PLN.EMPLID
PLN.EMPL_RCD
PLN.PLAN_TYPE
PLN.BENEFIT_NBR
PLN.EFFDT
PLN.COVERAGE_ELECT
PLN.GVT_INV_CHG_FLAG

   if  &PLN.COVERAGE_ELECT  =  'E'
       do  Process-Savings-Plan
   end-if
   do  Update-Investment-Change-Flag

FROM   PS_SAVINGS_PLAN   PLN,
       PS_DEDUCTION_TBL  DED

WHERE  PLN.GVT_INV_CHG_FLAG  IN ('A','B')
AND    PLN.EFFDT             <=  &B1.PAY_END_DT
AND    PLN.PLAN_TYPE         =   DED.PLAN_TYPE
AND    DED.EFFDT             =
       (SELECT  MAX (DED1.EFFDT)
        FROM    PS_DEDUCTION_TBL  DED1
        WHERE   DED1.PLAN_TYPE  =   DED.PLAN_TYPE
        AND     DED1.DEDCD      =   DED.DEDCD
        AND     DED1.EFFDT      <=  &B1.PAY_END_DT)
AND    DED.GVT_REPORTING_CD     IN ('TS','TB')

End-Select

do Commit-Transaction

End-Procedure


!***********************************************************************
!                                                                      *
!  Function:     Process-Savings-Plan                                  *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Process-Savings-Plan

Begin-SQL

DELETE  FROM  PS_GVT_TSP_INV_EFF
WHERE   EMPLID     =  &PLN.EMPLID
AND     EMPL_RCD  =  &PLN.EMPL_RCD
AND     EFFDT      =  &PLN.EFFDT

End-SQL

Begin-SQL

DELETE  FROM  PS_GVT_TSP_INV_DTA
WHERE   EMPLID     =  &PLN.EMPLID
AND     EMPL_RCD  =  &PLN.EMPL_RCD
AND     EFFDT      =  &PLN.EFFDT

End-SQL

Begin-SQL

INSERT  INTO  PS_GVT_TSP_INV_EFF  (EMPLID,
                                   EMPL_RCD,
                                   EFFDT,
                                   GVT_INPUT_SOURCE)

                          VALUES  (&PLN.EMPLID,
                                   &PLN.EMPL_RCD,
                                   &PLN.EFFDT,
                                   &PLN.GVT_INV_CHG_FLAG)

End-SQL

Begin-Select

INV.INVESTMENT_OPT
INV.INVESTMENT_PCT

    do  Process-Investment-Election

FROM   PS_SAVINGS_INVEST   INV

WHERE  INV.EMPLID     =  &PLN.EMPLID
AND    INV.EMPL_RCD  =  &PLN.EMPL_RCD
AND    INV.PLAN_TYPE  =  &PLN.PLAN_TYPE
AND    INV.BENEFIT_NBR   =  &PLN.BENEFIT_NBR
AND    INV.EFFDT      =  &PLN.EFFDT

End-Select

End-Procedure

!***********************************************************************
!                                                                      *
!  Function:     Process-Investment-Election                           *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Process-Investment-Election

Begin-SQL

INSERT  INTO  PS_GVT_TSP_INV_DTA  (EMPLID,
                                   EMPL_RCD,
                                   EFFDT,
                                   INVESTMENT_OPT,
                                   INVESTMENT_PCT)

                          VALUES  (&PLN.EMPLID,
                                   &PLN.EMPL_RCD,
                                   &PLN.EFFDT,
                                   &INV.INVESTMENT_OPT,
                                   &INV.INVESTMENT_PCT)

End-SQL

End-Procedure

!***********************************************************************
!                                                                      *
!  Function:     Update-Investment-Change-Flag                         *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Update-Investment-Change-Flag

Begin-SQL

UPDATE  PS_SAVINGS_PLAN

SET     GVT_INV_CHG_FLAG  =  'P'

WHERE   EMPLID     =  &PLN.EMPLID
AND     EMPL_RCD  =  &PLN.EMPL_RCD
AND     PLAN_TYPE  =  &PLN.PLAN_TYPE
AND     BENEFIT_NBR   =  &PLN.BENEFIT_NBR
AND     EFFDT      =  &PLN.EFFDT

End-SQL

End-Procedure

!***********************************************************************
!                                                                      *
!  Function:     Update-TSP-Update-Indicator                           *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Update-TSP-Update-Indicator

Begin-Select   DISTINCT

SAV.EMPLID
SAV.EMPL_RCD
SAV.PLAN_TYPE
SAV.BENEFIT_NBR

    move  &SAV.EMPLID     to  $update_emplid
    move  &SAV.EMPL_RCD  to  #update_empl_rcd
    do  Update-Federal-Job-Record
    do  Reset-Savings-Plan-Status-Change-Flag

FROM   PS_SAVINGS_PLAN   SAV,
       PS_DEDUCTION_TBL  DSV

WHERE  SAV.GVT_STAT_CHG_FLAG  IN ('A','B')
AND    SAV.EFFDT              <=  &B1.PAY_END_DT
AND    SAV.PLAN_TYPE          =   DSV.PLAN_TYPE
AND    DSV.EFFDT              =
       (SELECT  MAX (DSV1.EFFDT)
        FROM    PS_DEDUCTION_TBL  DSV1
        WHERE   DSV1.PLAN_TYPE  =   DSV.PLAN_TYPE
        AND     DSV1.DEDCD      =   DSV.DEDCD
        AND     DSV1.EFFDT      <=  &B1.PAY_END_DT)
AND    DSV.GVT_REPORTING_CD     IN ('TS','TB')

End-Select

do Commit-Transaction

Begin-Select   DISTINCT

RTR.EMPLID
RTR.EMPL_RCD
RTR.PLAN_TYPE
RTR.BENEFIT_NBR

    move  &RTR.EMPLID     to  $update_emplid
    move  &RTR.EMPL_RCD  to  #update_empl_rcd
    do  Update-Federal-Job-Record
    do  Reset-Retirement-Status-Change-Flag

FROM   PS_RTRMNT_PLAN    RTR,
       PS_DEDUCTION_TBL  DRT

WHERE  RTR.GVT_STAT_CHG_FLAG  <>  'P'
AND    RTR.EFFDT              <=  &B1.PAY_END_DT
AND    RTR.PLAN_TYPE          =   DRT.PLAN_TYPE
AND    DRT.EFFDT              =
       (SELECT  MAX (DRT1.EFFDT)
        FROM    PS_DEDUCTION_TBL  DRT1
        WHERE   DRT1.PLAN_TYPE  =   DRT.PLAN_TYPE
        AND     DRT1.DEDCD      =   DRT.DEDCD
        AND     DRT1.EFFDT      <=  &B1.PAY_END_DT)
AND    DRT.GVT_REPORTING_CD     IN ('TS','TB')

End-Select

do Commit-Transaction

End-Procedure

!***********************************************************************
!                                                                      *
!  Function:     Update-Federal-Job-Record                             *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Update-Federal-Job-Record

Begin-Select

JOB.EMPLID
JOB.EMPL_RCD
JOB.EFFDT
JOB.EFFSEQ

FROM   PS_GVT_JOB   JOB

WHERE  JOB.EMPLID     =  $update_emplid
AND    JOB.EMPL_RCD  =  #update_empl_rcd
AND    JOB.EFFDT      =
       (SELECT  MAX (JOB1.EFFDT)
        FROM    PS_GVT_JOB     JOB1
        WHERE   JOB1.EMPLID         =   JOB.EMPLID
        AND     JOB1.EMPL_RCD      =   JOB.EMPL_RCD
        AND     JOB1.EFFDT          <=  &B1.PAY_END_DT
        AND     JOB1.GVT_STATUS_TYPE  IN ('COM','COR'))
AND    JOB.EFFSEQ     =
       (SELECT  MAX (JOB2.EFFSEQ)
        FROM    PS_GVT_JOB     JOB2
        WHERE   JOB2.EMPLID         =   JOB.EMPLID
        AND     JOB2.EMPL_RCD      =   JOB.EMPL_RCD
        AND     JOB2.EFFDT          =   JOB.EFFDT
        AND     JOB2.GVT_STATUS_TYPE  IN ('COM','COR'))

End-Select

Begin-SQL

UPDATE   PS_GVT_JOB

SET      GVT_TSP_UPD_IND  =  'R'

WHERE    EMPLID     =  &JOB.EMPLID
AND      EMPL_RCD  =  &JOB.EMPL_RCD
AND      EFFDT      =  &JOB.EFFDT
AND      EFFSEQ     =  &JOB.EFFSEQ

End-SQL

End-Procedure

!***********************************************************************
!                                                                      *
!  Function:     Reset-Savings-Plan-Status-Change-Flag                 *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Reset-Savings-Plan-Status-Change-Flag

Begin-SQL

UPDATE  PS_SAVINGS_PLAN

SET     GVT_STAT_CHG_FLAG  =  'P'

WHERE   EMPLID     =   &SAV.EMPLID
AND     EMPL_RCD  =   &SAV.EMPL_RCD
AND     PLAN_TYPE  =   &SAV.PLAN_TYPE
AND     BENEFIT_NBR   =   &SAV.BENEFIT_NBR
AND     EFFDT      <=  &B1.PAY_END_DT

End-SQL

End-Procedure

!***********************************************************************
!                                                                      *
!  Function:     Reset-Retirement-Status-Change-Flag                   *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Reset-Retirement-Status-Change-Flag

Begin-SQL

UPDATE  PS_RTRMNT_PLAN

SET     GVT_STAT_CHG_FLAG  =  'P'

WHERE   EMPLID     =   &RTR.EMPLID
AND     EMPL_RCD  =   &RTR.EMPL_RCD
AND     PLAN_TYPE  =   &RTR.PLAN_TYPE
AND     BENEFIT_NBR   =   &RTR.BENEFIT_NBR
AND     EFFDT      <=  &B1.PAY_END_DT

End-SQL

End-Procedure
