!************************************************************
!  FSA004CN:  Flexible Spending Account Cheque Register     *
!             Cheque Register program for checks created    *
!             by FSA003CN for Canadian Customers            *
!************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/11:18:00:10                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'  !Set Database Environment

Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

#define MAX_LINES    55
#define TOTALS_COL   74
#define MAX_TOTALS    5

!*************************************************
begin-heading 7
!*************************************************

  #Include 'stdhdgtr.sqc'

   print $SubTitle             ( 0, 1)
   print $Fsa004cn_Ben          (+2, 1, 7)
   print $Fsa004cn_Ben          ( 0,+2, 7)
   print $Fsa004cn_Ben          ( 0,+17,7)
   print $Fsa004cn_Year         (+1, 1, 7)
   print $Fsa004cn_Program      ( 0,+2, 7)
   print $Fsa004cn_Plan_Typ     ( 0,+2,13)
   print $Fsa004cn_Plan         ( 0,+2, 7)
   print $Fsa004cn_Check_No     ( 0,+2,17)
   print $Fsa004cn_Check_Dt     ( 0,+2,10)
   print $Fsa004cn_Check_Amt    ( 0,+2,18)
   print $Fsa004cn_Name_Ee      ( 0,+2,30)
   print $Fsa004cn_Row_Empl_ID  ( 0,+2,11)
end-heading


!*************************************************
begin-program
!*************************************************

  do Init-Report

  if ($FormID <> '')
    do Array-Initialization
    do Construct-Where-Clauses
    do Process-Main
    do StdAPI-Term
  end-if

end-program


!*************************************************
begin-procedure Init-Report
!*************************************************

  do Init_Printer
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  move 'FSA004CN' to $ReportID

  do StdAPI-Init
  do Get-Report-Language   ! Procedure that decides the report language
                          ! and then calls 'Report-Translation' procedure
                          ! that reads the language dep. text.
  move $Fsa004cn_Title_FSA_Chq_Reg to $ReportTitle
  display $ReportTitle

  if $prcs_process_instance = ''
    do Ask-Values
  else
    do Get-Values
  end-if

end-procedure

 !>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
!Read language-dependent text from the database.

begin-procedure Report-Translation        ! Read from FSA004CN  program ids
   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('FSA004CN', 'BEN'  ,          $Fsa004cn_Ben, #dummy_width)
   do Get_Field_Information ('FSA004CN', 'YEAR',           $Fsa004cn_Year,        #dummy_width)
   do Get_Field_Information ('FSA004CN', 'PROGRAM',        $Fsa004cn_Program,        #dummy_width)
   do Get_Field_Information ('FSA004CN', 'PLAN_TYP',       $Fsa004cn_Plan_Typ,      #dummy_width)
   do Get_Field_Information ('FSA004CN', 'PLAN',           $Fsa004cn_Plan,    #dummy_width)
   do Get_Field_Information ('FSA004CN', 'CHECK_NO' ,      $Fsa004cn_Check_No ,    #dummy_width)
   do Get_Field_Information ('FSA004CN', 'CHECK_DT',       $Fsa004cn_Check_Dt,    #dummy_width)
   do Get_Field_Information ('FSA004CN', 'CHECK_AMT',      $Fsa004cn_Check_Amt,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'NAME_EE',         $Fsa004cn_Name_Ee ,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'EMPL_ID',        $Fsa004cn_Row_Empl_ID,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'TITLE_FSA_CHQ_REG', $Fsa004cn_Title_FSA_Chq_Reg,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'FORMID',         $Fsa004cn_FormID,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'CAL_YR',         $Fsa004cn_Cal_Yr,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'CHECKS_DT',      $Fsa004cn_Checks_Dt,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'BEN_PR',         $Fsa004cn_Ben_Pr,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'VOID',           $Fsa004cn_Void,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'REVRS',          $Fsa004cn_Revrs,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'GRND_TOT',       $Fsa004cn_Grnd_Tot,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'YR_TOT',         $Fsa004cn_Yr_Tot,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'BEN_PR_TOT',     $Fsa004cn_Ben_Pr_Tot,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'PL_TYP_TOT',     $Fsa004cn_Pl_Typ_Tot,   #dummy_width)
   do Get_Field_Information ('FSA004CN', 'BEN_PL_TOT',     $Fsa004cn_Ben_Pl_Tot,   #dummy_width)


end-procedure

!*************************************************
begin-procedure Ask-Values
!*************************************************

  do Ask-FormID
  if ($FormID <> '')
    do Ask-Calendar-Year
    do Ask-Benefit-Program
    do Ask-Check-Date
  end-if

end-procedure


!*************************************************
begin-procedure Get-Values
!*************************************************

  do Select-Parameters
  do Get-FormID
  do Get-Calendar-Year
  do Get-Benefit-Program
  do Get-Check-Date

end-procedure


!*************************************************
begin-procedure Ask-FormID
!*************************************************

  while (1 = 1)

    if ($FormID = '')
      input $FormID MaxLen=6 'Enter Form Identifier'
    else
      input $FormID MaxLen=6 '(Invalid Entry) Please Re-enter Form Identifier'
    end-if
    let $FormID = UPPER(RTRIM($FormID, ' '))

    if $FormID = ''
      break
    end-if

    do Edit-FormID

    if Rtrim(&Z.Form_ID, ' ') = $FormID
      break
    end-if

  end-while

end-procedure


!*************************************************
begin-procedure Edit-FormID
!*************************************************

begin-SELECT
Z.FORM_ID
FROM  PS_PAY_FORM_TBL Z
WHERE FORM_ID = $FormID
end-SELECT
end-procedure


!***********************************
begin-procedure Ask-Calendar-Year
!***********************************

  input $Calendar_Year MaxLen=4
     'Enter Balance Year (Leave blank for ALL open Years)'

  let #Calendar_Year = $Calendar_Year

end-procedure


!***********************************
begin-procedure Ask-Benefit-Program
!***********************************

  input $Benefit_Program MaxLen=3
     'Enter Benefit Program (Leave blank for ALL Programs)'

  let $Benefit_Program = UPPER(RTRIM($Benefit_Program, ' '))

end-procedure


!**********************************
begin-procedure Ask-Check-Date
!**********************************

  move 'N' to $DataOK

  while ($DataOK <> 'Y')

    input $CheckDate
      'Enter ''On or After'' Cheque Date ({Prompt-Date}) (Leave blank for ALL Cheques)'
    if Rtrim($CheckDate, ' ') = ''
      Break
    end-if

    move {PDelimiter} to $DDelimiter
    do Format-DateTime($CheckDate,$CheckDate,{Prompt-Mask},'','Native')
    do Validate-Native-Date($CheckDate,$DataOK)

  end-while

end-procedure


!*************************************************
begin-procedure Array-Initialization
!*************************************************

  create-array name=Totals              -
               size=6                   -
               field=TotalName:char     -
               field=total:number:12
  put $Fsa004cn_Grnd_Tot        into Totals(1) TotalName
  put $Fsa004cn_Yr_Tot          into Totals(2) TotalName
  put $Fsa004cn_Ben_Pr_Tot      into Totals(3) TotalName
  put $Fsa004cn_Pl_Typ_Tot      into Totals(4) TotalName
  put $Fsa004cn_Ben_Pl_Tot      into Totals(5) TotalName

end-procedure


!*************************************************
begin-procedure Construct-Where-Clauses
!*************************************************

  let $SubTitle = $Fsa004cn_FormID || ' ' || $FormID

  if (#Calendar_Year = 0)
    let $Calendar_Where = ''
  else
    let $Calendar_Year = edit(#Calendar_Year, '9999')
    let $Calendar_Where = 'AND B.CALENDAR_YEAR ='  || $Calendar_Year
    let $SubTitle = $SubTitle || $Fsa004cn_Cal_Yr || ' ' || $Calendar_Year
  end-if

  if ($Benefit_Program = '%') Or ($Benefit_Program = '')
    let $Program_Where = ''
  else
    let $Program_Where = 'AND G.BENEFIT_PROGRAM = ''' || $Benefit_Program || ''''
    let $SubTitle = $SubTitle || $Fsa004cn_Ben_Pr || ' '|| $Benefit_Program
  end-if

  if ($CheckDate = '')
    let $CheckDt_Where = ''
  else
    let $CheckDt_Where = 'AND G.CHECK_DT >= ''' || $CheckDate || ''''
    do Format-DateTime($CheckDate, $Check_Dt, {DEFDATE}, '', '')
    let $SubTitle = $SubTitle || $Fsa004cn_Checks_Dt || ' '  || $Check_Dt
  end-if

end-procedure


!*************************************************
begin-procedure Process-Main
!*************************************************
 let $Page_Break = 'Y'

begin-SELECT
G.CALENDAR_YEAR   () on-break print=never  level=1 procedure=Year-Change
G.BENEFIT_PROGRAM () on-break print=never  level=2 procedure=Program-Change
G.PLAN_TYPE       () on-break print=never  level=3 procedure=Plan-Change
G.BENEFIT_PLAN    () on-break print=never  level=4 procedure=Benefit-Change
G.PAYCHECK_NBR
G.CHECK_DT
G.FSA_CHECK_AMT
G.EMPLID
G.FSA_PAYMENT_STATUS
L.NAME

  if &G.Plan_Type <> $Plan_Type
    move &G.Plan_Type to $Plan_Type
    move &G.Plan_Type to $FieldValue
    move 'PLAN_TYPE'  to $FieldName
    do Read-Translate-Table
    move  $XlatShortName to $Plan_Name
  end-if

  if (&G.FSA_Payment_Status = 'P')
    ARRAY-ADD &G.FSA_Check_Amt to Totals({MAX_TOTALS}) Total(0)
  end-if

  do Print-Check-Register

FROM  PS_FSA_RUNCTL    B,
      PS_FSA_PAYMENT   G,
      PS_PERSON_NAME   L
WHERE B.FORM_ID            = $FormID
  AND B.FSA_RUN_STATUS     = 'F'
  [$Calendar_Where]
  AND G.FORM_ID            = B.FORM_ID
  AND G.CALENDAR_YEAR      = B.CALENDAR_YEAR
  AND G.FSA_PAYMENT_STATUS IN ('P','V','X')       !Paid, Void, Reversed
  [$Program_Where]
  AND G.BENEFIT_PROGRAM IN
     (SELECT P.BENEFIT_PROGRAM
      FROM   PS_BEN_DEFN_PGM P
      WHERE  P.FSA_RUN_ID = B.FSA_RUN_ID)
  [$CheckDt_Where]
  AND L.EMPLID             = G.EMPLID
ORDER BY G.CALENDAR_YEAR,
         G.BENEFIT_PROGRAM,
         G.PLAN_TYPE,
         G.BENEFIT_PLAN,
         G.PAYCHECK_NBR
end-SELECT

  move 1 to #changeLevel
  do Print-Totals

end-procedure


!*************************************************
begin-procedure Print-Check-Register
!*************************************************

  if ($Page_Break = 'Y')
    print &G.Calendar_Year      (+1, 1, 7)
    print &G.Benefit_Program    ( 0,+2, 7)
    print &G.Plan_Type          ( 0,+2, 2)
    print $Plan_Name            ( 0,+1,10)
    print &G.Benefit_Plan       ( 0,+2, 7)
    let $Page_Break = 'N'
  else
    print ''                    (+1, 1,40)
  end-if

  do Format-Number(&G.PAYCHECK_NBR, $PAYCHECK_NBR, '099999999999999')
  print $PAYCHECK_NBR                 ( 0,+2,17)

  do Format-DateTime(&G.Check_Dt, $Check_Dt, {DEFDATE}, '', '')
  print $Check_Dt               ( 0,+2,10)

  evaluate &G.FSA_Payment_Status
  when = 'P'
    do Format-Number(&G.FSA_Check_Amt, $NetPay, '$$$,$$$,$$$,$$9.99')
  when = 'V'
    let $NetPay = $Fsa004cn_Void
  when = 'X'
    let $NetPay =  $Fsa004cn_Revrs
  end-evaluate
  print $NetPay                 ( 0,+2,18)

  if (&G.FSA_Payment_Status = 'P')
    let $Name = RTrim(&L.Name,' ')
    print $Name                   ( 0,+2,30)
    print &G.EmplID               ( 0,+2,11)
  end-if

end-procedure


!*************************************************
begin-procedure Year-Change
!*************************************************

  move 2 to #changeLevel
  do Print-Totals

end-procedure


!*************************************************
begin-procedure Program-Change
!*************************************************

  move 3 to #changeLevel
  do Print-Totals

end-procedure


!*************************************************
begin-procedure Plan-Change
!*************************************************

  move 4 to #changeLevel
  do Print-Totals

end-procedure


!*************************************************
begin-procedure Benefit-Change
!*************************************************

  move 5 to #changeLevel
  do Print-Totals

end-procedure


!*************************************************
begin-procedure Print-Totals
!*************************************************

  move {MAX_TOTALS} to #totalRow

  !Don't split a total series across pages
  if (#current-line > ({MAX_LINES} - {MAX_TOTALS}))
    new-page
  end-if

  print '=======================' (+2,1)
  print '=================='      ( 0,{TOTALS_COL})

  while #totalRow >= #changeLevel

    let #rollRow = #totalRow - 1
    get $TotalName from Totals(#totalRow) TotalName
    get #total     from Totals(#totalRow) Total(0)
    do Format-Number(#total, $out, '$$$,$$$,$$$,$$9.99')

    print $TotalName     (+1,1)
    print $out           ( 0,{TOTALS_COL})

    array-add #total to Totals(#rollRow) Total(0)
    put 0 into Totals(#totalRow) Total(0)
    subtract 1 from #totalRow

  end-while

  if (#changeLevel = 1)
    do Reset
  end-if

  new-page
  let $Page_Break = 'Y'

end-procedure


!*************************************************

#Include 'readxlat.sqc'
#Include 'curdttim.sqc'
#Include 'reset1.sqc'
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'validdt.sqc'   !Routines for validating dates
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'bnrunctl.sqc'  !Get application run control parameters
#Include 'bngetval.sqc'  !Get BEN values mask routines
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
#Include 'useprntr.sqc'  !Select printer type

!**************************** End of Program ***************************
