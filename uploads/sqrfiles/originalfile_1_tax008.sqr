!**********************************************************
!  TAX008:  Quarterly State Tax Report - Employee Detail *
!**********************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/26:21:24:05                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization
#Include 'usarpt.sqc' !USA-specific variables

begin-report
  do Init-Report
  do Process-Main
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'TAX008' to $ReportID
  move 'Quarterly State Tax Report - Employee Detail' to $ReportTitle
  display $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Get-Tax-Reporting-Run-Controls
  do Get-Calendar-Year-Id
  do Stdapi-Init
  do Initialize

end-procedure

begin-procedure Initialize
  move $PeriodEndDate to $AsOfDate  !for included procedures
end-procedure


begin-heading 17
  let $tempState =  rtrim($State, ' ')
  if $tempState = $priorState
   if $Company <> $priorCompany
     let $Change = 'Y'
   end-if
  end-if
  #Include 'stdhdg02.sqc'  !standard heading 1
  if $Company <> $priorCompany
    do Get-Company-Data
    move $Company to $priorCompany
    move ''       to $priorState  !force change in State below
  end-if

  if rtrim($State, ' ') <> $priorState  or $Change = 'Y'
    do Get-State-Tax-Data  !Employer_ID_SWT, Employer_ID_SUT, State Name
    let $priorState = rtrim($State, ' ')

    move 0 to #PAGE_NUM
    let $Change = 'N'
  end-if

  add 1 to #PAGE_NUM
  print $StateName         (5,2)
  do Format-DateTime($PeriodEndDate, $out, {DEFDATE}, '', '')
  print $out       (0,53)
  do Format-Number(#PAGE_NUM, $out, '999')
  print $out               (0,70)
  print $CompanyName       (+3,12)
  print $CompnyAdd1       (+1,12)
  let $field = RTRIM($CompnyCity, ' ')
  print $field             (+1,12)                    !City
  print &CT.State          ()      edit ,bxx

  move &CT.Postal      to $Zip
  move $Zip            to $ZipExtsn ~~~~~~xxxx

  if instr($Zip,'-',6) = 0 and $ZipExtsn <> '    '
    let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if

  move $Zip         to $ZipExtsn ~~~~~xxxxx

  print $Zip               (0,+2)
  print 'Employer ID'      (+2,12)
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    print &Employer_ID_SUT (+0,+2)
  else
    print &Employer_ID_SWT (+0,+2)
  end-if
  print 'SUT Rate'         (+0,50)
  print &SUT_Experience_Rt (+0,+2) edit b9.999
  print ' '                                    (+3,2)
  print ' '                                    (0,15)
  print '------- Withholding -------'          (0,40)
  print '----- Unemployment -------------------------'      (0,69)
  print '--------  Disability  --------'       (0,114)
  print '--------  VPDI / EIC  --------'       (0,145)
  print 'SSN'                                  (+1,2)
  print 'Name'                                 (0,15)
  print '    TxGrs QTD       Tax QTD'          (0,40)
  print '    TxGrs QTD NL  Gross QTD       Tax QTD'         (0,69)
  print '    TxGrs QTD       Tax QTD'          (0,114)
  print '    TxGrs QTD       Tax QTD'          (0,145)
end-heading


begin-footing 11
  let #PgLinePos = 1
  let #DetLinePos = 1
  print 'Page Total'       (#PgLinePos,22)
  do Format-Number(#PgTotTxGrsSWT, $out, '999,999,999.99mi')
  print $out               (#PgLinePos,39)
  do Format-Number(#PgTotTaxSWT, $out, '999,999,999.99mi')
  print $out               (#PgLinePos,53)
  do Format-Number(#PgTotTxGrsSUT, $out, '999,999,999.99mi')
  print $out               (#PgLinePos,68)
  do Format-Number(#PgTotNlGrsSUT, $out, '999,999,999.99mi')
  print $out               (#PgLinePos,82)
  do Format-Number(#PgTotTaxSUT, $out, '999,999,999.99mi')
  print $out               (#PgLinePos,96)
  print 'ER'               (#PgLinePos,+0)
  add 1 to #PgLinePos
  if #PgTotTxGrsSUTEE <> 0 or #PgTotTaxSUTEE <> 0
    do Format-Number(#PgTotTxGrsSUTEE, $out, '999,999,999.99mi')
    print $out             (#PgLinePos,68)
    do Format-Number(#PgTotNlGrsSUTEE, $out, '999,999,999.99mi')
    print $out             (#PgLinePos,82)
    do Format-Number(#PgTotTaxSUTEE, $out, '999,999,999.99mi')
    print $out             (#PgLinePos,96)
    print 'EE'             (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if
  if #PgTotTxGrsSWAF  <> 0 or #PgTotTaxSWAF  <> 0
    do Format-Number(#PgTotTxGrsSWAF , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,68)
    do Format-Number(#PgTotNlGrsSWAF , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,82)
    do Format-Number(#PgTotTaxSWAF , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,96)
    print 'SW'             (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if
  if #PgTotTxGrsWFDP  <> 0 or #PgTotTaxWFDP  <> 0
    do Format-Number(#PgTotTxGrsWFDP , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,68)
    do Format-Number(#PgTotNlGrsWFDP , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,82)
    do Format-Number(#PgTotTaxWFDP , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,96)
    print 'WF'             (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if
  if #PgTotTxGrsHCSF  <> 0 or #PgTotTaxHCSF  <> 0
    do Format-Number(#PgTotTxGrsHCSF , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,68)
    do Format-Number(#PgTotNlGrsHCSF , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,82)
    do Format-Number(#PgTotTaxHCSF , $out, '999,999,999.99mi')
    print $out             (#PgLinePos,96)
    print 'HC'             (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if
  if #PgTotTxGrsSUTSp <> 0 or #PgTotTaxSUTSp <> 0
    do Format-Number(#PgTotTxGrsSUTSp, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,68)
    do Format-Number(#PgTotNlGrsSUTSp, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,82)
    do Format-Number(#PgTotTaxSUTSp, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,96)
    print 'SP'               (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if
  do Format-Number(#PgTotTxGrsSDI, $out, '999,999,999.99mi')
  print $out               (1,113)
  do Format-Number(#PgTotTaxSDI, $out, '999,999,999.99mi')
  print $out               (1,127)
  do Format-Number(#PgTotTxGrsSDIE, $out, '999,999,999.99mi')
  print $out               (2,113)
  do Format-Number(#PgTotTaxSDIE, $out, '999,999,999.99mi')
  print $out               (2,127)
  print 'ER'               (2,+0)
  move #PgLinePos to #PgLineMax
  let #PgLinePos = 1
  if #PgTotTxGrsVPDIEE <> 0 or #PgTotTaxVPDIEE <> 0
    do Format-Number(#PgTotTxGrsVPDIEE, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,144)
    do Format-Number(#PgTotTaxVPDIEE, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,158)
    print 'EE V'             (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if
  if #PgTotTxGrsVPDI <> 0 or #PgTotTaxVPDI <> 0
    do Format-Number(#PgTotTxGrsVPDI, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,144)
    do Format-Number(#PgTotTaxVPDI, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,158)
    print 'ER V'             (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if
  if #PgTotTxGrsEIC <> 0 or #PgTotTaxEIC <> 0
    do Format-Number(#PgTotTxGrsEIC, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,144)
    do Format-Number(#PgTotTaxEIC, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,158)
    print 'EIC'              (#PgLinePos,+0)
    add 1 to #PgLinePos
  end-if

  if #PgLinePos > #PgLineMax
    move #PgLinePos to #PgLineMax
  end-if

  move 0 to #PgTotTxGrsSWT
  move 0 to #PgTotTaxSWT
  move 0 to #PgTotTxGrsSUT
  move 0 to #PgTotNlGrsSUT
  move 0 to #PgTotTaxSUT
  move 0 to #PgTotTxGrsSDI
  move 0 to #PgTotTaxSDI
  move 0 to #PgTotTxGrsSDIE
  move 0 to #PgTotTaxSDIE
  move 0 to #PgTotTxGrsSWAF
  move 0 to #PgTotNlGrsSWAF
  move 0 to #PgTotTaxSWAF
  move 0 to #PgTotTxGrsWFDP
  move 0 to #PgTotNlGrsWFDP
  move 0 to #PgTotTaxWFDP
  move 0 to #PgTotTxGrsHCSF
  move 0 to #PgTotNlGrsHCSF
  move 0 to #PgTotTaxHCSF
  move 0 to #PgTotTxGrsSUTEE
  move 0 to #PgTotNlGrsSUTEE
  move 0 to #PgTotTaxSUTEE
  move 0 to #PgTotTxGrsSUTSp
  move 0 to #PgTotNlGrsSUTSp
  move 0 to #PgTotTaxSUTSp
  move 0 to #PgTotTxGrsVPDIEE
  move 0 to #PgTotTaxVPDIEE
  move 0 to #PgTotTxGrsVPDI
  move 0 to #PgTotTaxVPDI
  move 0 to #PgTotTxGrsEIC
  move 0 to #PgTotTaxEIC

  if $Print_State_Totals = 'Y'
    move #PgLineMax to #PgLinePos
    add 1 to #PgLinePos
    print 'State Total'      (#PgLinePos,21)
    do Format-Number(#StTotTxGrsSWT, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,39)
    do Format-Number(#StTotTaxSWT, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,53)
    do Format-Number(#StTotTxGrsSUT, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,68)
    do Format-Number(#StTotNlGrsSUT, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,82)
    do Format-Number(#StTotTaxSUT, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,96)
    print 'ER'               (#PgLinePos,+0)
    do Format-Number(#StTotTxGrsSDI, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,113)
    do Format-Number(#StTotTaxSDI, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,127)
    add 1 to #PgLinePos
    do Format-Number(#StTotTxGrsSDIE, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,113)
    do Format-Number(#StTotTaxSDIE, $out, '999,999,999.99mi')
    print $out               (#PgLinePos,127)
    print 'ER'               (#PgLinePos,+0)
    if #StTotTxGrsSUTEE <> 0 or #StTotTaxSUTEE <> 0
      do Format-Number(#StTotTxGrsSUTEE, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,68)
      do Format-Number(#StTotNlGrsSUTEE, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,82)
      do Format-Number(#StTotTaxSUTEE, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,96)
      print 'EE'             (#PgLinePos,+0)
      add 1 to #PgLinePos
    end-if
    if #StTotTxGrsSWAF  <> 0 or #StTotTaxSWAF  <> 0
      do Format-Number(#StTotTxGrsSWAF , $out, '999,999,999.99mi')
      print $out             (#PgLinePos,68)
      do Format-Number(#StTotNlGrsSWAF, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,82)
      do Format-Number(#StTotTaxSWAF , $out, '999,999,999.99mi')
      print $out             (#PgLinePos,96)
      print 'SW'             (#PgLinePos,+0)
      add 1 to #PgLinePos
    end-if
    if #StTotTxGrsWFDP  <> 0 or #StTotTaxWFDP  <> 0
      do Format-Number(#StTotTxGrsWFDP , $out, '999,999,999.99mi')
      print $out             (#PgLinePos,68)
      do Format-Number(#StTotNlGrsWFDP, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,82)
      do Format-Number(#StTotTaxWFDP , $out, '999,999,999.99mi')
      print $out             (#PgLinePos,96)
      print 'WF'             (#PgLinePos,+0)
      add 1 to #PgLinePos
    end-if
    if #StTotTxGrsHCSF  <> 0 or #StTotTaxHCSF  <> 0
      do Format-Number(#StTotTxGrsHCSF , $out, '999,999,999.99mi')
      print $out             (#PgLinePos,68)
      do Format-Number(#StTotNlGrsHCSF, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,82)
      do Format-Number(#StTotTaxHCSF , $out, '999,999,999.99mi')
      print $out             (#PgLinePos,96)
      print 'HC'             (#PgLinePos,+0)
      add 1 to #PgLinePos
    end-if
    if #StTotTxGrsSUTSp <> 0 or #StTotTaxSUTSp <> 0
      do Format-Number(#StTotTxGrsSUTSp, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,68)
      do Format-Number(#StTotNlGrsSUTSp, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,82)
      do Format-Number(#StTotTaxSUTSp, $out, '999,999,999.99mi')
      print $out             (#PgLinePos,96)
      print 'SP'             (#PgLinePos,+0)
    end-if
    move #PgLineMax to #PgLinePos
    add 1 to #PgLinePos
    if #StTotTxGrsVPDIEE <> 0 or #StTotTaxVPDIEE <> 0
      do Format-Number(#StTotTxGrsVPDIEE, $out, '999,999,999.99mi')
      print $out               (#PgLinePos,144)
      do Format-Number(#StTotTaxVPDIEE, $out, '999,999,999.99mi')
      print $out               (#PgLinePos,158)
      print 'EE V'             (#PgLinePos,+0)
      add 1 to #PgLinePos
    end-if
    if #StTotTxGrsVPDI <> 0 or #StTotTaxVPDI <> 0
      do Format-Number(#StTotTxGrsVPDI, $out, '999,999,999.99mi')
      print $out               (#PgLinePos,144)
      do Format-Number(#StTotTaxVPDI, $out, '999,999,999.99mi')
      print $out               (#PgLinePos,158)
      print 'ER V'             (#PgLinePos,+0)
      add 1 to #PgLinePos
    end-if
    if #StTotTxGrsEIC <> 0 or #StTotTaxEIC <> 0
      do Format-Number(#StTotTxGrsEIC, $out, '999,999,999.99mi')
      print $out               (#PgLinePos,144)
      do Format-Number(#StTotTaxEIC, $out, '999,999,999.99mi')
      print $out               (#PgLinePos,158)
      print 'EIC'              (#PgLinePos,+0)
      add 1 to #PgLinePos
    end-if

    move 0 to #StTotTxGrsSWT
    move 0 to #StTotTaxSWT
    move 0 to #StTotTxGrsSUT
    move 0 to #StTotNlGrsSUT
    move 0 to #StTotTaxSUT
    move 0 to #StTotTxGrsSDI
    move 0 to #StTotTaxSDI
    move 0 to #StTotTxGrsSDIE
    move 0 to #StTotTaxSDIE
    move 0 to #StTotTxGrsSWAF
    move 0 to #StTotNlGrsSWAF
    move 0 to #StTotTaxSWAF
    move 0 to #StTotTxGrsWFDP
    move 0 to #StTotNlGrsWFDP
    move 0 to #StTotTaxWFDP
    move 0 to #StTotTxGrsHCSF
    move 0 to #StTotNlGrsHCSF
    move 0 to #StTotTaxHCSF
    move 0 to #StTotTxGrsSUTEE
    move 0 to #StTotNlGrsSUTEE
    move 0 to #StTotTaxSUTEE
    move 0 to #StTotTxGrsSUTSp
    move 0 to #StTotNlGrsSUTSp
    move 0 to #StTotTaxSUTSp
    move 0 to #StTotTxGrsVPDIEE
    move 0 to #StTotTaxVPDIEE
    move 0 to #StTotTxGrsVPDI
    move 0 to #StTotTaxVPDI
    move 0 to #StTotTxGrsEIC
    move 0 to #StTotTaxEIC
  end-if
end-footing

begin-procedure Process-Main
begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
C.COMPANY      () on-break print=never level=1
C.STATE        () on-break print=never level=2 after=After-State
D1.NATIONAL_ID () on-break print=never level=3 before=New-Employee
D.NAME         () on-break print=never level=3 before=New-Employee
C.EMPLID       
C.TAX_CLASS
SUM(C.TXGRS_QTD) &C.TxGrs_QTD
SUM(C.TAX_QTD)   &C.Tax_QTD
SUM(C.NLGRS_QTD) &C.NLGrs_QTD

  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_PERSON_NAME D, PS_PERS_NID D1
WHERE D.EMPLID        = C.EMPLID  AND D1.EMPLID = D.EMPLID AND D1.COUNTRY = {NID_Country}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND C.STATE NOT IN ('$U','$UPR','$UAS','$UGU','$UVI') !Not a Federal entry
  AND C.LOCALITY      = ' '
  AND C.BALANCE_ID    = $Calendar_Year_Id
  AND C.BALANCE_YEAR  = &TX.Balance_Year
  AND C.BALANCE_QTR   = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD  =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE
       WHERE  EMPLID        = C.EMPLID
         AND  COMPANY       = C.COMPANY
         AND  BALANCE_ID    = C.BALANCE_ID
         AND  BALANCE_YEAR  = C.BALANCE_YEAR
         AND  BALANCE_QTR   = C.BALANCE_QTR
         AND  STATE         = C.STATE
         AND  LOCALITY      = C.LOCALITY
         AND  TAX_CLASS     = C.TAX_CLASS)
GROUP BY C.COMPANY, C.STATE, D1.NATIONAL_ID, D.NAME, C.EMPLID, C.TAX_CLASS
ORDER BY C.COMPANY, C.STATE, D1.NATIONAL_ID, D.NAME, C.EMPLID, C.TAX_CLASS
end-SELECT

  move 'Y' to $Print_State_Totals

end-procedure

begin-procedure New-Employee
  if &D.Name <> $priorName
    next-listing need=2
    move &D1.NATIONAL_ID  to $S123 xxx       !isolate first 3 digits
    if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
      print 'Applied For' (1,2)
    else
      print &D1.NATIONAL_ID       (1,2)  edit xxxbxxbxxxx
    end-if

    print &D.Name         (1,15,20)
    move &D.Name to $priorName

    move &C.EmplID  to $EmplID
    move &C.Company to $Company
    let $State = rtrim(&C.State, ' ')
    let #DetLinePos = 1
    let #DetLinePos2 = 1
  end-if
end-procedure

begin-procedure Process-Tax-Balance

  evaluate &C.Tax_Class
    when = 'D'                                         !Disability
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (1,114)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (1,128)

      add &C.TxGrs_QTD to #PgTotTxGrsSDI
      add &C.TxGrs_QTD to #StTotTxGrsSDI
      add &C.Tax_QTD   to #PgTotTaxSDI
      add &C.Tax_QTD   to #StTotTaxSDI
    break

    when = 'E'                                         !Disability Employer
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (2,114)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (2,128)
      print 'ER'             (2,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsSDIE
      add &C.TxGrs_QTD to #StTotTxGrsSDIE
      add &C.Tax_QTD   to #PgTotTaxSDIE
      add &C.Tax_QTD   to #StTotTaxSDIE
    break

    when = 'H'                                       !Withholding
    when = 'A'                                       !Withholding       !HP99999
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (1,40)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (1,54)

      add &C.TxGrs_QTD to #PgTotTxGrsSWT
      add &C.TxGrs_QTD to #StTotTxGrsSWT
      add &C.Tax_QTD   to #PgTotTaxSWT
      add &C.Tax_QTD   to #StTotTaxSWT
    break

    when = 'U'                                        !Unemployment - ER
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,69)
      do Format-Number(&C.NLGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,83)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,97)
      print 'ER'             (#DetLinePos,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsSUT
      add &C.TxGrs_QTD to #StTotTxGrsSUT
      add &C.NLGrs_QTD to #PgTotNlGrsSUT
      add &C.NLGrs_QTD to #StTotNlGrsSUT
      add &C.Tax_QTD   to #PgTotTaxSUT
      add &C.Tax_QTD   to #StTotTaxSUT
      add 1            to #DetLinePos
    break

    when = 'L'                                        !New Jersey SWAF
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,69)
      do Format-Number(&C.NLGrs_QTD, $out, '99,999,999.99mi')
      print $out             (2,83)
      print $out             (#DetLinePos,83)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,97)
      print 'SW'             (#DetLinePos,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsSWAF
      add &C.TxGrs_QTD to #StTotTxGrsSWAF
      add &C.NLGrs_QTD to #PgTotNlGrsSWAF
      add &C.NLGrs_QTD to #StTotNlGrsSWAF
      add &C.Tax_QTD   to #PgTotTaxSWAF
      add &C.Tax_QTD   to #StTotTaxSWAF
      add 1            to #DetLinePos
    break

    when = 'M'                                        !New Jersey WFDP
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,69)
      do Format-Number(&C.NLGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,83)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,97)
      print 'WF'             (#DetLinePos,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsWFDP
      add &C.TxGrs_QTD to #StTotTxGrsWFDP
      add &C.NLGrs_QTD to #PgTotNlGrsWFDP
      add &C.NLGrs_QTD to #StTotNlGrsWFDP
      add &C.Tax_QTD   to #PgTotTaxWFDP
      add &C.Tax_QTD   to #StTotTaxWFDP
      add 1            to #DetLinePos
    break

    when = 'N'                                        !New Jersey HCSF
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,69)
      do Format-Number(&C.NLGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,83)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,97)
      print 'HC'             (#DetLinePos,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsHCSF
      add &C.TxGrs_QTD to #StTotTxGrsHCSF
      add &C.NLGrs_QTD to #PgTotNlGrsHCSF
      add &C.NLGrs_QTD to #StTotNlGrsHCSF
      add &C.Tax_QTD   to #PgTotTaxHCSF
      add &C.Tax_QTD   to #StTotTaxHCSF
      add 1            to #DetLinePos
    break

    when = 'V'                                        !Unemployment - EE
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,69)
      do Format-Number(&C.NLGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,83)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,97)
      print 'EE'             (#DetLinePos,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsSUTEE
      add &C.TxGrs_QTD to #StTotTxGrsSUTEE
      add &C.NLGrs_QTD to #PgTotNlGrsSUTEE
      add &C.NLGrs_QTD to #StTotNlGrsSUTEE
      add &C.Tax_QTD   to #PgTotTaxSUTEE
      add &C.Tax_QTD   to #StTotTaxSUTEE
      add 1            to #DetLinePos
    break

    when = 'S'                                     !Special Unemployment
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,69)
      do Format-Number(&C.NLGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,83)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos,97)
      print 'SP'             (#DetLinePos,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsSUTSp
      add &C.TxGrs_QTD to #StTotTxGrsSUTSp
      add &C.NLGrs_QTD to #PgTotNlGrsSUTSp
      add &C.NLGrs_QTD to #StTotNlGrsSUTSp
      add &C.Tax_QTD   to #PgTotTaxSUTSp
      add &C.Tax_QTD   to #StTotTaxSUTSp
      add 1            to #DetLinePos
    break

    when = 'X'                                         !VPDI Employer
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos2,145)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos2,159)
      print 'ER V'           (#DetLinePos2,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsVPDI
      add &C.TxGrs_QTD to #StTotTxGrsVPDI
      add &C.Tax_QTD   to #PgTotTaxVPDI
      add &C.Tax_QTD   to #StTotTaxVPDI
      add 1            to #DetLinePos2
    break

    when = 'W'                                         !VPDI Employee
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos2,145)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos2,159)
      print 'EE V'           (#DetLinePos2,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsVPDIEE
      add &C.TxGrs_QTD to #StTotTxGrsVPDIEE
      add &C.Tax_QTD   to #PgTotTaxVPDIEE
      add &C.Tax_QTD   to #StTotTaxVPDIEE
      add 1            to #DetLinePos2
    break

    when = 'C'                                         !EIC IN
      do Format-Number(&C.TxGrs_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos2,145)
      do Format-Number(&C.Tax_QTD, $out, '99,999,999.99mi')
      print $out             (#DetLinePos2,159)
      print 'EIC'            (#DetLinePos2,+0)

      add &C.TxGrs_QTD to #PgTotTxGrsEIC
      add &C.TxGrs_QTD to #StTotTxGrsEIC
      add &C.Tax_QTD   to #PgTotTaxEIC
      add &C.Tax_QTD   to #StTotTaxEIC
      add 1            to #DetLinePos2

  end-evaluate
end-procedure

begin-procedure After-State
  move 'Y' to $Print_State_Totals
  new-page
  move 'N' to $Print_State_Totals
  move '' to $priorName
  move '' to $priorState
end-procedure

begin-procedure Ignore
end-procedure

#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
