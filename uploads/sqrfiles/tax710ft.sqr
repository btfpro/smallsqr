!***********************************************************************
!  TAX710FT: Print footnote entries from Tax Form Definition Table     *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2010, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2010/11/02:12:50:38                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  813289                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization
#define TAXFORM_NOTES
#include 'rellang.sqc'

begin-report
  do Init-Report
  do Process-Main
  do Reset
 #ifdef PRCSSCHD
  do StdAPI-Term
 #endif
end-report

begin-procedure Init-Report
  move 'TAX710FT' to $ReportID

  do Init-DateTime
  do Get-Current-DateTime

 #ifdef PRCSSCHD
  do StdAPI-Init
 #endif

  do Get-Report-Language
  move $Tax710ft_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

  if $Prcs_Process_Instance = ''
     do Prompts
  else
#ifdef PRCSSCHD
     do Select-Parameters
     do Convert-Parameters
#endif
  end-if
end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from TAX710FT program ids
  do Init_Report_Translation($ReportID,$language_cd)
  do Get_Field_Information ('TAX710FT', 'REPORTTITLE',  $Tax710ft_ReportTitle, #dummy_width)
  do Get_Field_Information ('TAX710FT', 'FORMID',       $Tax710ft_FormID,     #dummy_width)
  do Get_Field_Information ('TAX710FT', 'EFFDT',        $Tax710ft_EffDt,      #dummy_width)
  do Get_Field_Information ('TAX710FT', 'T4A_CODE1',    $Tax710ft_T4A_Code1,   #dummy_width)
  do Get_Field_Information ('TAX710FT', 'PRT_ORDER1',   $Tax710ft_Prt_Order1,  #dummy_width)
  do Get_Field_Information ('TAX710FT', 'BOX2',         $Tax710ft_Box2,        #dummy_width)
  do Get_Field_Information ('TAX710FT', 'FTNOTE_TXT2',  $Tax710ft_Ftnote_Txt2, #dummy_width)
  do Get_Field_Information ('TAX710FT', 'T4A_CODE2',    $Tax710ft_T4A_Code2,   #dummy_width)
  do Get_Field_Information ('TAX710FT', 'PRT_ORDER2',   $Tax710ft_Prt_Order2,  #dummy_width)
  do Get_Field_Information ('TAX710FT', 'DESCR2',       $Tax710ft_Descr2,      #dummy_width)

end-procedure



begin-HEADING 8
  #Include 'stdhdgtr.sqc'

  print $Tax710ft_FormID                                      (3,1)
  print $TaxForm_ID                                           (,+3)
  print $Tax710ft_EffDt                                       (4,1)
  print $Effdt                                                (,+2)

  position (+2)
  if $FieldValue = 'A'
    print $Tax710ft_T4A_Code1                                 (0,50)
  end-if
  print $Tax710ft_Prt_Order1                                  (0,56)

  print $Tax710ft_Box2                                        (+1,1)
  print $Tax710ft_Ftnote_Txt2                                 (0,6)
  if $FieldValue = 'A'
    print $Tax710ft_T4A_Code2                                 (0,50)
  end-if
  print $Tax710ft_Prt_Order2                                  (0,56)
  print $Tax710ft_Descr2                                      (0,65)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
  if $CurrentOnly = 'Y'
    do Process-Current
  else
    do Process-All
  end-if

END-PROCEDURE

BEGIN-PROCEDURE Process-Current
BEGIN-SELECT
A.TAXFORM_ID
A.EFFDT

  if instr($FormType,&A.TaxForm_ID,1) <> 0
    move 'TAXFORM_ID'  to $FieldName
    move &A.TaxForm_ID to $FieldValue
    do Read-Translate-Table
    move $XlatShortName to $TaxForm_ID

    do Format-DateTime(&A.EffDt, $Effdt, {DEFDATE}, '', '')
    move &A.Effdt to $ProcDate
    let $Footnotes_printed = 'N'
    do Process-Footnotes
    if $Footnotes_printed = 'Y'
      NEW-PAGE
    end-if
  end-if

 FROM PS_TAXFORM_TBL A
WHERE A.EFFDT = (SELECT MAX(EFFDT)
                FROM PS_TAXFORM_TBL
                WHERE TAXFORM_ID = A.TAXFORM_ID)

ORDER BY A.TAXFORM_ID ASC

END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Process-All
BEGIN-SELECT

C.TAXFORM_ID
C.EFFDT

  if instr($FormType,&C.TaxForm_ID,1) <> 0
    move 'TAXFORM_ID'  to $FieldName
    move &C.TaxForm_ID to $FieldValue
    do Read-Translate-Table
    move $XlatShortName to $TaxForm_ID

    do Format-DateTime(&C.EffDt, $Effdt, {DEFDATE}, '', '')
    move &C.Effdt to $ProcDate
    let $Footnotes_printed = 'N'
    do Process-Footnotes
    if $Footnotes_printed = 'Y'
      NEW-PAGE
    end-if
  end-if

 FROM PS_TAXFORM_TBL C
ORDER BY C.TAXFORM_ID ASC, C.EFFDT DESC

END-SELECT
END-PROCEDURE

Begin-Procedure Process-Footnotes
BEGIN-SELECT
B.BOX
B.BOX_NOTE_TEXT
B.BOX_NOTE_CD
B.BOX_NOTE_SEQ
B.BOX_NOTE_COMMENT
B.EFFDT


  let  $Box                            = &B.BOX
  let  $Box_Note_Cd                    = &B.BOX_NOTE_CD
  move &B.BOX_NOTE_SEQ                to #Box_Note_Seq
  let  $TAXFORM_NOTES-BOX_NOTE_COMMENT = &B.BOX_NOTE_COMMENT
  let  $TAXFORM_NOTES-BOX_NOTE_TEXT    = &B.BOX_NOTE_TEXT

  do Get_Related_TAXFORM_NOTES(&B.BOX,&B.EFFDT,$FieldValue)

  print $Box                            (0,1)
  print $TAXFORM_NOTES-BOX_NOTE_TEXT    (0,6)
  print $Box_Note_Cd                    (0,51)
  print #Box_Note_Seq                   (0,57) edit 999
  print $TAXFORM_NOTES-BOX_NOTE_COMMENT (0,65,0)  WRAP 65 20 STRIP=<13> ON=<10>

  NEXT-LISTING SKIPLINES=1 NEED=4
  let $Footnotes_printed = 'Y'

 FROM PS_TAXFORM_NOTES B
WHERE B.TAXFORM_ID = $FieldValue
  AND B.EFFDT      = $ProcDate

ORDER BY B.BOX ASC

end-select
end-procedure

begin-procedure Prompts

  while $CurrentOnly = ''
    input $CurrentOnly 'Current Table entries Only? (Y/N)'
    uppercase $CurrentOnly
    if INSTR('YN',$CurrentOnly,1) = 0
      move '' to $CurrentOnly
    end-if
  end-while

  input $TypeCode 'Form Type (A,R,T,V) or blank for all'
  uppercase $TypeCode
  let $FormType = substr($TypeCode,1,1)
  evaluate $FormType
    when =  'A'
      display ' '
      display 'T4A Footnotes Only'
      display ' '

    when =  'R'
      display ' '
      display 'RL-1 Footnotes Only'
      display ' '

    when =  'T'
      display ' '
      display 'T4 Footnotes Only'
      display ' '

    when =  'V'
      display ' '
      display 'RL-2 Footnotes Only'
      display ' '

    when-other
      move 'ARTV' to $FormType

  end-evaluate
end-procedure

begin-procedure Convert-Parameters

  move $RC_QTR_UI.Current_Only       to $CurrentOnly
  move $RC_QTR_UI.Canada_Form_Type   to $FormType
end-procedure



#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls

#ifdef PRCSSCHD
  #Include 'stdapi.sqc'    !Update Process API
#end-if

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language

