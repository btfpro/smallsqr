!************************************************************
!  PCMP002:  Professional Compliance                        *
!            Company Compliance Plan                        *
!************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/07/19:14:20:55                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
!                                                                      *
!***********************************************************************


#include 'setenv.sqc' !Set environment
begin-setup
#Include 'ptpsl177.sqc'   !Printer and page-size initialization
end-setup

! Related language stuff
#define PCMP_COMP_VW
#define PCMP_CMP_PLAN
#define PCMP_CMP_PLANA
#include 'rellang.sqc'
#define RELLANG_PROG

begin-REPORT
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Layout
  do Init-Report
  do Process-Main
  do Stdapi-Term
end-REPORT

!***********************************************************************
! Init Report
!***********************************************************************

begin-procedure Init-Report

  move ' ' to $CompanyDescr
  move ' ' to $PlanYrDescr
  move 'PCMP002' to $ReportID
  move 'COMPANY COMPLIANCE PLAN' to $ReportTitle

  do Stdapi-Init

  let $language_cd = $CURR_LANGUAGE_CD

  if $prcs_process_instance = ''
      INPUT $Company 'Enter Company : '
      INPUT $PlanYr 'Enter Compliance Plan Year : '
  else
    do Get-Values
  end-if

  do Init_Printer
  do Init_Report_Translation('PROFCOMP', $language_cd)
  do Append_Report_Translation('PROFCOMP')

end-procedure

!***********************************************************************
! Get Values
!***********************************************************************

begin-procedure Get-Values

  let $language_cd = $PRCS_LANGUAGE_CD

BEGIN-SELECT

RC.COMPANY
RC.PCMP_CMP_PLANYR

  let $Company = &RC.COMPANY
  let #PlanYr = &RC.PCMP_CMP_PLANYR

  do Get-Company-Descr
  do Get-Plan-Descr

from PS_RUN_CNTL_PCMP2 RC

where RC.OPRID = $prcs_oprid
  and RC.RUN_CNTL_ID = $prcs_run_cntl_id

end-select

end-procedure Get-Values

!***********************************************************************
! Layout
!***********************************************************************

begin-procedure Init-Layout

#define col0  1
#define col1  14
#define col2  24

end-procedure

!***********************************************************************
! Heading
!***********************************************************************

begin-heading  9

#include 'stdhdgtr.sqc'

print $LBL_COMPANY                (0,{col0})
print $Company                    (0,{col1})
print $CompanyDescr               (0,+1)
print $LBL_PLAN_YR                (+1,{col0})
print #PlanYr                     (0,{col2}) edit 9999
print $PlanYrDescr                (0,+1)

end-heading


!***********************************************************************
!  Gets the Company description
!***********************************************************************

begin-procedure Get-Company-Descr

begin-SELECT

C.COMPANY
C.DESCR

  let $PCMP_COMP_VW-DESCR = &C.DESCR
  do Get_Related_PCMP_COMP_VW(&C.COMPANY)
  let $CompanyDescr = $PCMP_COMP_VW-DESCR

FROM   PS_PCMP_COMP_VW C

WHERE  C.COMPANY = $Company

end-SELECT

end-procedure

!***********************************************************************
!  Gets the Company Compliance Plan description
!***********************************************************************

begin-procedure Get-Plan-Descr

begin-SELECT

CP.COMPANY
CP.PCMP_CMP_PLANYR
CP.PCMP_CMP_PLANDESC

  let $PCMP_CMP_PLAN-PCMP_CMP_PLANDESC = &CP.PCMP_CMP_PLANDESC
  do Get_Related_PCMP_CMP_PLAN(&CP.COMPANY,&CP.PCMP_CMP_PLANYR)
  let $PlanYrDescr = $PCMP_CMP_PLAN-PCMP_CMP_PLANDESC

FROM   PS_PCMP_CMP_PLAN CP

WHERE  CP.COMPANY = $Company
AND    CP.PCMP_CMP_PLANYR = #PlanYr

end-SELECT

end-procedure


!***********************************************************************
! Main
!***********************************************************************

begin-procedure Process-Main

   do Get-Translat-Info   ! Global

   do Get-Compliance-Plan

end-procedure Process-Main

!***********************************************************************
!  Gets the Company Compliance Plan details.
!***********************************************************************

begin-procedure Get-Compliance-Plan

begin-SELECT

CPL.COMPANY
CPL.PCMP_CMP_PLANYR
CPL.PCMP_CMP_PLANSEQ
CPL.PCMP_CMP_PLANAREA
CPL.PCMP_CMP_PLANNOTES

  let $PCMP_CMP_PLANA-PCMP_CMP_PLANAREA = &CPL.PCMP_CMP_PLANAREA
  let $PCMP_CMP_PLANA-PCMP_CMP_PLANNOTES = &CPL.PCMP_CMP_PLANNOTES

  do Get_Related_PCMP_CMP_PLANA(&CPL.COMPANY,&CPL.PCMP_CMP_PLANSEQ,&CPL.PCMP_CMP_PLANYR)

  move $PCMP_CMP_PLANA-PCMP_CMP_PLANAREA to $PlanArea
  move $PCMP_CMP_PLANA-PCMP_CMP_PLANNOTES to $PlanNotes

  print $LBL_PLAN_AREA               (+3,{col0})
  print $PlanArea                    (0,+1)
  print $LBL_NOTES                   (+2,{col0})
  print $PlanNotes                   (+1,{col0}) WRAP 80 20

FROM   PS_PCMP_CMP_PLANA CPL

WHERE  CPL.COMPANY = $Company
AND    CPL.PCMP_CMP_PLANYR = #PlanYr

ORDER BY CPL.PCMP_CMP_PLANSEQ

end-SELECT

end-procedure

!***********************************************************************
!  Gets the strings printed in this report (Globalization)
!***********************************************************************

begin-procedure Get-Translat-Info

   do Get_Field_Information('PROFCOMP', 'LBL_COMP_PLAN'      ,$LBL_COMP_PLAN       ,#DW)
   do Get_Field_Information('PROFCOMP', 'LBL_COMPANY'        ,$LBL_COMPANY         ,#DW)
   do Get_Field_Information('PROFCOMP', 'LBL_PLAN_YR'        ,$LBL_PLAN_YR         ,#DW)
   do Get_Field_Information('PROFCOMP', 'LBL_PLAN_AREA'      ,$LBL_PLAN_AREA       ,#DW)
   do Get_Field_Information('PROFCOMP', 'LBL_NOTES'          ,$LBL_NOTES           ,#DW)

end-procedure

!***********************************************************************


#include 'stdapi.sqc'    !Routines to update run status
#Include 'reset.sqc'     !Reset printer procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'useprntr.sqc'
#Include 'sqrtrans.sqc'

