!******************************************************
!  CMP020JP: Salary Increase Amount(Simulation) Report*
!                                                     *
!******************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!          $Date:  2006/07/19:11:37:56                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
Begin-Setup
#Include 'ptset02.sqc'  !Printer and page-size initialization
End-Setup

#define COMP_RATECD_TBL

#include 'rellang.sqc'   !Get_Related_COMP_RATECD_TBL

!-----------------------------------------------------------------------
begin-report
!-----------------------------------------------------------------------

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  if $prcs_oprid=''
    goto last2
  end-if
  do Process-Main
  do Reset
  do Stdapi-Term
LAST2:
end-report

!-----------------------------------------------------------------------
begin-heading 12
!-----------------------------------------------------------------------

do Get_Field_Information ('CMP020JP', 'SALRPT_TITLE', $REPORTTITLE, #DW)
  #Include 'stdhdgtr.sqc'
do Get_Field_Information ('CMP020JP', 'SAL_ID', $SAL_ID, #DW)
do Get_Field_Information ('CMP020JP', 'CAL_BASE_DT', $CAL_BASE_DT, #DW)
do Get_Field_Information ('CMP020JP', 'GB_GRP_ID', $GB_GRP_ID, #DW)
do Get_Field_Information ('CMP020JP', 'CALC_NUM', $CALC_NUM, #DW)
do Get_Field_Information ('CMP020JP', 'EMPLID', $EMPLID, #DW)
do Get_Field_Information ('CMP020JP', 'EMPL_RCD', $EMPL_RCD, #DW)
do Get_Field_Information ('CMP020JP', 'NAME', $NAME, #DW)
do Get_Field_Information ('CMP020JP', 'SETID_JOBCODE',  $SETID_JOBCODE, #DW)
do Get_Field_Information ('CMP020JP', 'JOBCODE',  $JOBCODE, #DW)
do Get_Field_Information ('CMP020JP', 'DESCRSHORT',  $DESCRSHORT, #DW)
do Get_Field_Information ('CMP020JP', 'SETID_SALARY',  $SETID_SALARY, #DW)
do Get_Field_Information ('CMP020JP', 'SAL_ADMIN_PLAN', $SAL_ADMIN_PLAN, #DW)
do Get_Field_Information ('CMP020JP', 'GRADE', $GRADE, #DW)
do Get_Field_Information ('CMP020JP', 'COMP_EFFSEQ', $COMP_EFFSEQ, #DW)
do Get_Field_Information ('CMP020JP', 'COMP_RATECD', $COMP_RATECD, #DW)
do Get_Field_Information ('CMP020JP', 'BEF_SALINC_AMOUNT', $BEF_SALINC_AMOUNT, #DW)
do Get_Field_Information ('CMP020JP', 'CURRENCY_CD', $CURRENCY_CD, #DW)
do Get_Field_Information ('CMP020JP', 'COMP_FREQUENCY', $COMP_FREQUENCY, #DW)
do Get_Field_Information ('CMP020JP', 'RATING_MODEL', $RATING_MODEL, #DW)
do Get_Field_Information ('CMP020JP', 'REVIEW_RATING', $REVIEW_RATING, #DW)
do Get_Field_Information ('CMP020JP', 'SALINC_PCT', $SALINC_PCT, #DW)
do Get_Field_Information ('CMP020JP', 'SALINC_AMOUNT', $SALINC_AMOUNT, #DW)
do Get_Field_Information ('CMP020JP', 'NEW_SAL_AMOUNT', $NEW_SAL_AMOUNT, #DW)
do Get_Field_Information ('CMP020JP', 'SALINC_ADJUST', $SALINC_ADJUST, #DW)
do Get_Field_Information ('CMP020JP', 'AJSTD_SAL_AMT', $AJSTD_SAL_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'AJSTD_SALINC_PCT', $AJSTD_SALINC_PCT, #DW)

do Get-Descr-Header
  print $SAL_ID      (+2,1,0)
  print $SALID           (,12,0)
  print $SalID_Descr      (,25,0)

  print $CAL_BASE_DT      (,56,0)
  PRINT $A_cal_base_dt_YMD (0,80)

  print $CALC_NUM        (,92,0)
  print #CAL             (,99,0) edit 999
  print $GB_GRP_ID     (+1,1,0)
  print $R2_GROUP        (,12,0)
  print $Origin     (,25,0)

  print $EMPLID          (+2,1,0)
  print $EMPL_RCD        (,9,0)
  print $NAME            (,19,0)
  print $SETID_JOBCODE       (,38,0)
  print $JOBCODE         (,52,0)
  print $DESCRSHORT      (,61,0)
  print $SETID_SALARY       (,72,0)
  print '/'              (0,84,0)
  print $SAL_ADMIN_PLAN  (,85,0)
  print '/'              (,91,0)
  print $GRADE           (,92,0)
  print $COMP_EFFSEQ     (,104,0)
  print $COMP_RATECD     (,114,0)
  print $DESCRSHORT      (,126,0)
  print $BEF_SALINC_AMOUNT  (,144,0)
  print '('              (,158,0)
  print $CURRENCY_CD     (,159,0)
  print '/'              (,167,0)
  print $COMP_FREQUENCY  (,168,0)
  print ')'              (,177,0)

  print $RATING_MODEL    (+1,30,0)
  print $REVIEW_RATING   (,45,0)
  print $SALINC_PCT      (,59,0)
  print $SALINC_AMOUNT   (,75,0)
  print $NEW_SAL_AMOUNT  (,96,0)
  print $SALINC_ADJUST   (,117,0)
  print $AJSTD_SAL_AMT   (,144,0)
  print $AJSTD_SALINC_PCT (,160,0)
  PRINT '- '              (+1,1,400) FILL


 end-heading


 !-----------------------------------------------------------------------
begin-procedure Init-Report
!-----------------------------------------------------------------------

  move 'CMP020JP' to $ReportID

!  do Init-Array        ! Define Total Area(Line)

  do Stdapi-Init
  if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto LAST1
  end-if

  if $prcs_process_instance <> ''
    do Select-Parameters
  end-if
do Init_Printer
do Init_Report_translation ($ReportID, $language_cd)
do Append_Report_Translation ('HR')
do Append_Report_Translation ('CMP020JP')

! Security (deleted)
!  let $PNLGRPNAME = &RUN_CMPRPT_JPN.PNLGRPNAME
!  DO Security-Param
!  DO Security-Param-JPN

LAST1:
end-procedure

!-----------------------------------------------------------------------
begin-procedure Get-Values
!-----------------------------------------------------------------------

  let $language_cd = $prcs_language_cd
  let $_CURR_language_cd = $prcs_language_cd   ! FOR COMP_RATECD DESCR
  let $CURR_language_cd = $prcs_language_cd    ! FOR GB_GROUP_ID DESCR


  let $OPRID = &RUN_CMPRPT_JPN.OPRID
  let $RUN_CNTL_ID = &RUN_CMPRPT_JPN.RUN_CNTL_ID
  let $SALID = &RUN_CMPRPT_JPN.SAL_INC_ID
  let #CAL = &RUN_CMPRPT_JPN.CALC_NUM
  let $GROUP = &RUN_CMPRPT2_JPN.GB_GROUP_ID

end-procedure


!-----------------------------------------------------------------------
begin-procedure Get-Descr-Header
!-----------------------------------------------------------------------

! get SalaryIncreaseID Descr ($SalID_Descr)

  let $SAL_INC_ID = $SALID
  do  Get-SalaryIncreaseID-Descr

! get Calculation Base Date (from SAL_ID_TBL_JPN )

begin-SELECT on-error=SQL-Error
A.CAL_BASE_DT
  let $A_cal_base_dt = &A.CAL_BASE_DT
FROM PS_SAL_ID_TBL_JPN A WHERE A.SAL_INC_ID = $SALID
end-SELECT

  do Convert-To-DTU-Date ($A_cal_base_dt, $A_cal_base_dt_YMD)

! get GroupID Descr ($Group_Descr)

  let $GB_Group_ID = $R2_GROUP
  let $AsOfDate = $S_sal_inc_dt
  do  Get-GB-Group-Descr
  let $Origin = $Group_Descr

end-procedure

!-----------------------------------------------------------------------
begin-procedure Process-Main
!-----------------------------------------------------------------------

LET $sql-statement = 'An error occurred in selecting applicable data from Salary Simulation Table.'

begin-SELECT on-error=SQL-Error
R.SAL_INC_ID
R.CALC_NUM
S.SAL_INC_DT           ()  on-break print=never
   move &S.SAL_INC_DT to $S_sal_inc_dt

R2.GB_GROUP_ID         ()  on-break print=never level=1 procedure=Group-Change
!                           before=Print-Totals
   move &R2.GB_GROUP_ID to $R2_GROUP    ! not printed as a ListItem


  if #CURRENT-LINE  >  45
    new-page
  end-if

A.EMPLID                       (+1,1,9) ON-BREAK PRINT=CHANGE/TOP-PAGE SKIPLINES=1 LEVEL=2
  let $S_Emplid = &A.EMPLID

A.EMPL_RCD                     (0,14,0)  edit 999 ON-BREAK PRINT=CHANGE/TOP-PAGE LEVEL=4
  let #S_Empl_rcd = &A.EMPL_RCD

A.NAME                         (0,19,0) ON-BREAK PRINT=CHANGE/TOP-PAGE LEVEL=3
  let $S_Name = &A.NAME

A.SETID_JOBCODE                (0,38,0)
  let $S_Setid_Job = &A.SETID_JOBCODE

A.JOBCODE                      (0,52,0)
  let $S_Jobcode = &A.JOBCODE

! Get JOBCODE DESCR ($JobTitleAbbrv)
  move $S_Setid_Job  to  $SetID
  move $S_Jobcode    to  $JobCode
  move $S_sal_inc_dt to  $AsOfDate
  do Get-Job-Title
  print $JobTitleAbbrv         (0,61,0)

A.SETID_SALARY                 (0,72,0)
  let $S_Setid_Sal = &A.SETID_SALARY
  print '/'                    (0,84,0)

A.SAL_ADMIN_PLAN               (0,85,0)
  let $S_Plan = &A.SAL_ADMIN_PLAN
  print '/'                    (0,91,0)

A.GRADE                        (0,92,0)
  let $S_Grade = &A.GRADE

A.COMP_EFFSEQ                  (0,107,0) edit 999
  let #S_Compseq = &A.COMP_EFFSEQ

A.COMP_RATECD                  (0,114,0)
  let $S_Crc = &A.COMP_RATECD

! Get COMP_RATECD  DESCR ($COMP_RATECD_TBL-DESCRSHORT)
  !Keys passed in:  $COMP_RATECD, $AsOfDate
  !Values returned: $COMP_RATECD_Descr, $COMP_RATECD_Descrshort
  let $COMP_RATECD = &A.COMP_RATECD
  let $AsOfDate    = $S_sal_inc_dt
  do  Get_COMP_RATECD_descr
  print $COMP_RATECD_Descrshort         (0,126,0)

A.BEF_SALINC_AMOUNT            (0,136,22) edit 999,999,999,999.999999
  let #S_CurAmt = &A.BEF_SALINC_AMOUNT
  print '('                    (0,158,0)

A.CURRENCY_CD            (0,159,3)
  let $S_CurCD = &A.CURRENCY_CD
  print '/'                    (0,162,0)

A.COMP_FREQUENCY            (0,163,1)
  let $S_Freq = &A.COMP_FREQUENCY
  print ')'                    (0,164,0)

A.RATING_MODEL                 (+1,30,0)

A.REVIEW_RATING                (0,45,0)
  let $S_Revbnd = &A.REVIEW_RATING

A.SALINC_PCT               (0,58,0) edit 999.999
  let #S_INCRATE = &A.SALINC_PCT

A.SALINC_AMOUNT               (0,67,22) edit 999,999,999,999.999999
  let #S_INCAMT = &A.SALINC_AMOUNT

A.NEW_SAL_AMOUNT               (0,89,22) edit 999,999,999,999.999999
  let #S_NEWAMT = &A.NEW_SAL_AMOUNT

A.SALINC_ADJUST               (0,111,22) edit 999,999,999,999.999999
  let #S_ADJUST = &A.SALINC_ADJUST

A.AJSTD_SAL_AMT               (0,136,22) edit 999,999,999,999.999999
  let #S_ACTNEWAMT = &A.AJSTD_SAL_AMT

A.AJSTD_SALINC_PCT               (0,160,0) edit 999.999
  let #S_ACTINCRATE = &A.AJSTD_SALINC_PCT

! Total/Grand Current Amount
   ADD #S_CurAmt to #T_CurAmt
   ADD #S_CurAmt to #G_CurAmt

! Total/Grand Calculated Increase Amount
   ADD #S_INCAMT to #T_INCAMT
   ADD #S_INCAMT to #G_INCAMT

! Total/Grand calc New Amount
   ADD #S_NEWAMT to #T_NEWAMT
   ADD #S_NEWAMT to #G_NEWAMT

! Total/Grand Adjustment Amount
   ADD #S_ADJUST to #T_ADJUST
   ADD #S_ADJUST to #G_ADJUST

! Total/Grand Actual New Amount
   ADD #S_ACTNEWAMT to #T_ACTNEWAMT
   ADD #S_ACTNEWAMT to #G_ACTNEWAMT

!Total/Grand Actual Increase Amount
   ADD #S_INCAMT to #T_ACTINCAMT
   ADD #S_ADJUST to #T_ACTINCAMT
   ADD #S_INCAMT to #G_ACTINCAMT
   ADD #S_ADJUST to #G_ACTINCAMT

! Average Increase Amount (Group Total)
   LET #T_INCRATE = ( (#T_NEWAMT / #T_CurAmt) - 1) * 100

!Actual Average Increase Rate (Group Total)
   LET #T_ACTINCRATE = ( (#T_ACTNEWAMT / #T_CurAmt) - 1) * 100

! Average Increase Amount (Grand Total)
   LET #G_INCRATE = ( (#G_NEWAMT / #G_CurAmt) - 1) * 100

!Actual Average Increase Rate (Grand Total)
   LET #G_ACTINCRATE = ( (#G_ACTNEWAMT / #G_CurAmt) - 1) * 100

!   do Write-Report

FROM  PS_SALINC_SIML_JPN A, PS_RUN_CMPRPT_JPN R, PS_RUN_CMPRPT2_JPN R2, PS_SAL_ID_TBL_JPN S

WHERE R.OPRID       = $OPRID
  AND R.RUN_CNTL_ID = $RUN_CNTL_ID
 AND R.OPRID        = R2.OPRID
 AND R.RUN_CNTL_ID  = R2.RUN_CNTL_ID
  AND A.SAL_INC_ID =  $SALID
  AND A.CALC_NUM   =  #CAL
  AND A.GB_GROUP_ID = R2.GB_GROUP_ID
  AND S.SAL_INC_ID =  $SALID

ORDER BY R.SAL_INC_ID,R.CALC_NUM,R2.GB_GROUP_ID,
A.EMPLID,A.EMPL_RCD,A.COMP_EFFSEQ,A.COMP_RATECD
end-SELECT
  do Print-Totals
  do Grand-Totals
end-procedure

!-----------------------------------------------
begin-procedure Get_COMP_RATECD_descr
!-----------------------------------------------

  !Keys passed in:  $COMP_RATECD, $AsOfDate
  !Values returned: $COMP_RATECD_Descr, $COMP_RATECD_Descrshort

  move '' to $COMP_RATECD_Descr
  move '' to $COMP_RATECD_Descrshort

  if Rtrim($COMP_RATECD,' ') = ''
    goto GetCRCExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

LET $sql-statement = 'An error occurred in reading Comp Rate Code Table(COMP_RATECD_TBL).'

begin-SELECT on-error=SQL-Error
CR.EFFDT
CR.DESCR
CR.DESCRSHORT

  let $COMP_RATECD_Effdt = &CR.EFFDT
  let $COMP_RATECD_Descr = Rtrim(&CR.DESCR,' ')
  let $COMP_RATECD_Descrshort = Rtrim(&CR.DESCRSHORT,' ')

FROM  PS_COMP_RATECD_TBL CR
WHERE CR.COMP_RATECD = $COMP_RATECD
  AND CR.EFFDT       =
       (SELECT MAX(EFFDT)
        FROM   PS_COMP_RATECD_TBL
        WHERE  COMP_RATECD = CR.COMP_RATECD
          AND  EFFDT <= $AsOfDate)
  AND CR.EFF_STATUS  = 'A'
end-SELECT

  do Get-COMP-RATECD-Lang

GetCRCExit:



end-procedure


!-----------------------------------------------
begin-procedure Group-Change
!-----------------------------------------------
  do Print-Totals
  new-page
end-procedure

!-----------------------------------------------
begin-procedure select-parameters
!-----------------------------------------------

LET $sql-statement = 'An error occurred in reading Run Control Record.'

BEGIN-SELECT on-error=SQL-Error

RUN_CMPRPT_JPN.OPRID
RUN_CMPRPT_JPN.RUN_CNTL_ID
RUN_CMPRPT_JPN.SAL_INC_ID
RUN_CMPRPT_JPN.CALC_NUM
RUN_CMPRPT_JPN.PNLGRPNAME
RUN_CMPRPT2_JPN.GB_GROUP_ID

  do Get-Values

from PS_RUN_CMPRPT_JPN RUN_CMPRPT_JPN, PS_RUN_CMPRPT2_JPN RUN_CMPRPT2_JPN

where RUN_CMPRPT_JPN.OPRID = $prcs_oprid
  and RUN_CMPRPT_JPN.RUN_CNTL_ID = $prcs_run_cntl_id
  and RUN_CMPRPT2_JPN.RUN_CNTL_ID = RUN_CMPRPT_JPN.RUN_CNTL_ID
  and RUN_CMPRPT2_JPN.OPRID = RUN_CMPRPT_JPN.OPRID
end-select

end-procedure
!-----------------------------------------------------------------------
begin-procedure Print-Totals
!-----------------------------------------------------------------------

!  do Group-Change     ! New Page

! Get information for total line
do Get_Field_Information ('CMP020JP', 'TTL_CUR_AMT', $TTL_CUR_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'AVERAGE_PCTJ', $AVERAGE_PCTJ, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_CLC_INC_AMT', $TTL_CLC_INC_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_CLC_NEW_AMT', $TTL_CLC_NEW_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_AJSTD_AMT', $TTL_AJSTD_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_ACTL_NEW_AMT', $TTL_ACTL_NEW_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_ACTL_INC_AMT', $TTL_ACTL_INC_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'AJSTD_AVE_INCPCT', $AJSTD_AVE_INCPCT, #DW)
  if #CURRENT-LINE  >  45
    new-page
  end-if
! print totals header
   PRINT  '- '                (+2,1,400) FILL
   print  $TTL_CUR_AMT       (,8,0)
   print  $AVERAGE_PCTJ       (,29,0)
   print  $TTL_CLC_INC_AMT   (,48,0)
   print  $TTL_CLC_NEW_AMT   (,72,0)
   print  $TTL_AJSTD_AMT     (,96,0)
   print  $AJSTD_AVE_INCPCT  (,118,0)
   print  $TTL_ACTL_INC_AMT  (,138,0)
   print  $TTL_ACTL_NEW_AMT  (,164,0)


! print list items
   PRINT  #T_CurAmt          (+2,1,0)  EDIT 99,999,999,999,999.999999
   PRINT  #T_INCRATE         (,30,0)   EDIT 999.999
   PRINT  #T_INCAMT          (,40,0)   EDIT 99,999,999,999,999.999999
   PRINT  #T_NEWAMT          (,65,0)   EDIT 99,999,999,999,999.999999
   PRINT  #T_ADJUST          (,90,0)   EDIT 99,999,999,999,999.999999
   PRINT  #T_ACTINCRATE      (,123,0)  EDIT 999.999
   PRINT  #T_ACTINCAMT       (,133,0)  EDIT 99,999,999,999,999.999999
   PRINT  #T_ACTNEWAMT       (,157,0)  EDIT 99,999,999,999,999.999999

!  Clear Total Area
   let    #T_CurAmt            = 0
   let    #T_INCRATE           = 0
   let    #T_INCAMT            = 0
   let    #T_NEWAMT            = 0
   let    #T_ADJUST            = 0
   let    #T_ACTINCRATE        = 0
   let    #T_ACTINCAMT         = 0
   let    #T_ACTNEWAMT         = 0


 end-procedure
!-----------------------------------------------------------------------
begin-procedure Grand-Totals
!-----------------------------------------------------------------------

!  do Group-Change     ! New Page

! Get information for total line
do Get_Field_Information ('CMP020JP', 'GRAND_TOTAL', $GRAND_TOTAL, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_CUR_AMT', $TTL_CUR_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'AVERAGE_PCTJ', $AVERAGE_PCTJ, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_CLC_INC_AMT', $TTL_CLC_INC_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_CLC_NEW_AMT', $TTL_CLC_NEW_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_AJSTD_AMT', $TTL_AJSTD_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_ACTL_NEW_AMT', $TTL_ACTL_NEW_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'TTL_ACTL_INC_AMT', $TTL_ACTL_INC_AMT, #DW)
do Get_Field_Information ('CMP020JP', 'AJSTD_AVE_INCPCT', $AJSTD_AVE_INCPCT, #DW)
  if #CURRENT-LINE  >  45
    new-page
  end-if
! print totals header
   print  $GRAND_TOTAL       (+3,1,0)
   print  $TTL_CUR_AMT       (+1,8,0)
   print  $AVERAGE_PCTJ       (,29,0)
   print  $TTL_CLC_INC_AMT   (,48,0)
   print  $TTL_CLC_NEW_AMT   (,72,0)
   print  $TTL_AJSTD_AMT     (,96,0)
   print  $AJSTD_AVE_INCPCT  (,118,0)
   print  $TTL_ACTL_INC_AMT  (,138,0)
   print  $TTL_ACTL_NEW_AMT  (,164,0)



! print list items
   PRINT  #G_CurAmt          (+2,1,0)  EDIT 99,999,999,999,999.999999
   PRINT  #G_INCRATE         (,30,0)   EDIT 999.999
   PRINT  #G_INCAMT          (,40,0)   EDIT 99,999,999,999,999.999999
   PRINT  #G_NEWAMT          (,65,0)   EDIT 99,999,999,999,999.999999
   PRINT  #G_ADJUST          (,90,0)   EDIT 99,999,999,999,999.999999
   PRINT  #G_ACTINCRATE      (,123,0)  EDIT 999.999
   PRINT  #G_ACTINCAMT       (,133,0)  EDIT 99,999,999,999,999.999999
   PRINT  #G_ACTNEWAMT       (,157,0)  EDIT 99,999,999,999,999.999999


 end-procedure


!-----------------------------------------------------------------------
!-----------------------------------------------------------------------
#Include 'reset1.sqc'     !Reset printer
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'curdttim.sqc'  !Routines to get current date/time
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
#include 'askaod.sqc'    !Ask As Of Date input
#Include 'datemath.sqc'  !Routines for date math
#include 'getcrc.sqc'    !Get-COMP-RATECD-Lang
#include 'getjobtl.sqc'   !Get-Job-Title
#include 'getgbjpn.sqc'   !Get-GB-Group-Descr
#include 'getsalid.sqc'   !Get-Salary-Increase-ID-Descr
#include 'lngutil.sqc'

