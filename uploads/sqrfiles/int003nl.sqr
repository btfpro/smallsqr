!***********************************************************************
!  INT003NL: Print contents of Paygroup / Paygrp Empltype Tables       *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/01/10:07:24:00                           
!             $Release:  HR9                                           
!            $Revision:  101                                           
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

begin-setup
  #Include 'ptset02.sqc'   !Printer and page-size initialization
end-setup

#define PAYGROUP_TBL
#include 'rellang.sqc'   ! Translations File

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'INT003NL' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language
  move $INT003NL_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
 do Init_Report_Translation($ReportID,$language_cd)

 do Get_Field_Information ('INT003NL', 'REPORT_TITLE',  $INT003NL_ReportTitle,   #dummy_width)
 do Get_Field_Information ('INT003NL', 'PAYGROUP1',     $INT003NL_Paygroup1,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'EFFDT1',        $INT003NL_EffDt1,        #dummy_width)
 do Get_Field_Information ('INT003NL', 'COUNTRY1',      $INT003NL_Country1,      #dummy_width)
 do Get_Field_Information ('INT003NL', 'DESCRSHORT1',   $INT003NL_Descrshort1,   #dummy_width)
 do Get_Field_Information ('INT003NL', 'PAY_FREQ1',     $INT003NL_Pay_Freq1,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'RET_PAYGRP1',   $INT003NL_Ret_Paygrp1,   #dummy_width)
 do Get_Field_Information ('INT003NL', 'TRANSIT1',      $INT003NL_Transit1,      #dummy_width)
 do Get_Field_Information ('INT003NL', 'EMPL_TYPE1',    $INT003NL_Empl_Type1,    #dummy_width)
 do Get_Field_Information ('INT003NL', 'DFLT_BEN_PGM1', $INT003NL_Dflt_Ben_Pgm1, #dummy_width)
 do Get_Field_Information ('INT003NL', 'ERN_PRGM1',     $INT003NL_Ern_Prgm1,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'HOL_SCHD1',     $INT003NL_Hol_Schd1,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'EMP_TYPE1',     $INT003NL_Emp_Type1,     #dummy_width)

 do Get_Field_Information ('INT003NL', 'COMPANY2',      $INT003NL_Company2,      #dummy_width)
 do Get_Field_Information ('INT003NL', 'PAYGROUP2',     $INT003NL_Paygroup2,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'EFFDT2',        $INT003NL_EffDt2,        #dummy_width)
 do Get_Field_Information ('INT003NL', 'COUNTRY2',      $INT003NL_Country2,      #dummy_width)
 do Get_Field_Information ('INT003NL', 'DESCRLONG2',    $INT003NL_Descrlong2,    #dummy_width)
 do Get_Field_Information ('INT003NL', 'DESCRSHORT2',   $INT003NL_Descrshort2,   #dummy_width)
 do Get_Field_Information ('INT003NL', 'PAY_FREQ2',     $INT003NL_Pay_Freq2,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'RET_PAYGRP2',   $INT003NL_Ret_Paygrp2,   #dummy_width)
 do Get_Field_Information ('INT003NL', 'TRANSIT2',      $INT003NL_Transit2,      #dummy_width)
 do Get_Field_Information ('INT003NL', 'EMPL_TYPE2',    $INT003NL_Empl_Type2,    #dummy_width)
 do Get_Field_Information ('INT003NL', 'DFLT_BEN_PGM2', $INT003NL_Dflt_Ben_Pgm2, #dummy_width)
 do Get_Field_Information ('INT003NL', 'ERN_PRGM2',     $INT003NL_Ern_Prgm2,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'HOL_SCHD2',     $INT003NL_Hol_Schd2,     #dummy_width)
 do Get_Field_Information ('INT003NL', 'EMP_TYPE2',     $INT003NL_Emp_Type2,     #dummy_width)

 do Get_Field_Information ('INT003NL', 'ACCOUNT1',      $INT003NL_Account1,      #dummy_width)
 do Get_Field_Information ('INT003NL', 'ACCOUNT2',      $INT003NL_Account2,      #dummy_width)

 do Get_Field_Information ('INT003NL', 'REGULAR',       $INT003NL_Regular,       #dummy_width)
 do Get_Field_Information ('INT003NL', 'NET_PAY',       $INT003NL_Net_Pay,       #dummy_width)
 do Get_Field_Information ('INT003NL', 'MAX',           $INT003NL_Max,           #dummy_width)
 do Get_Field_Information ('INT003NL', 'MIN',           $INT003NL_Min,           #dummy_width)
 do Get_Field_Information ('INT003NL', 'OVERTIME',      $INT003NL_Overtime,      #dummy_width)
 do Get_Field_Information ('INT003NL', 'HOURS',         $INT003NL_Hours,         #dummy_width)
 do Get_Field_Information ('INT003NL', 'EARN',          $INT003NL_Earn,          #dummy_width)
 do Get_Field_Information ('INT003NL', 'HOLIDAY',       $INT003NL_Holiday,       #dummy_width)

end-procedure


begin-HEADING 7

  #Include 'stdhdgtr.sqc'

  print $INT003NL_Paygroup1               (+2,5,3)
  print $INT003NL_EffDt1                  (0,9,11)
  print $INT003NL_Country1                (0,21,33)
  print $INT003NL_Descrshort1             (0,58,11)
  print $INT003NL_Dflt_Ben_Pgm1           (0,70,4)
  print $INT003NL_Empl_Type1              (0,75,4)
  print $INT003NL_Pay_Freq1               (0,80,6)
  print $INT003NL_Ret_Paygrp1             (0,87,4)
  print $INT003NL_Hol_Schd1               (0,92,4)
  print $INT003NL_Ern_Prgm1               (0,97,3)
  print $INT003NL_Transit1                (0,101,10)
  print $INT003NL_Account1                (0,112,27)
  print $INT003NL_Net_Pay                 (0,131,12) 
  print $INT003NL_Regular                 (0,146,3)
  print $INT003NL_Overtime                (0,150,3)
  print $INT003NL_Regular                 (0,154,3) 
  print $INT003NL_Emp_Type1               (0,163)

  print $INT003NL_Company2                (+1,1,3)
  print $INT003NL_Paygroup2               (0,5,3)
  print $INT003NL_EffDt2                  (0,9,11)
  print $INT003NL_Country2                (0,21,3)
  print $INT003NL_Descrlong2              (0,28,29)
  print $INT003NL_Descrshort2             (0,58,12)
  print $INT003NL_Dflt_Ben_Pgm2           (0,70,4)
  print $INT003NL_Empl_Type2              (0,75,4)
  print $INT003NL_Pay_Freq2               (0,82,6)
  print $INT003NL_Ret_Paygrp2             (0,85,4)
  print $INT003NL_Hol_Schd2               (0,92,4)
  print $INT003NL_Ern_Prgm2               (0,97,3)
  print $INT003NL_Transit2                (0,101,10)
  print $INT003NL_Account2                (0,112,18)
  print $INT003NL_Min                     (0,131,4)
  print $INT003NL_Max                     (0,136,8)
  print $INT003NL_Hours                   (0,146,3)
  print $INT003NL_Hours                   (0,150,3)
  print $INT003NL_Earn                    (0,154,3)
  print $INT003NL_Holiday                 (0,158,3)
  print $INT003NL_Emp_Type2               (0,163)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
  do Process-Main-table
END-PROCEDURE


BEGIN-PROCEDURE Process-Main-table
BEGIN-SELECT

A.COMPANY        &A.COMPANY    (+1,1,3)
A.PAYGROUP       &A.PAYGROUP   (0,+1,3)
A.EFFDT  &A.EffDt
  let $COMPANY = &A.COMPANY
  let $PAYGROUP = &A.PAYGROUP
  let $EFFDT = &A.EffDt

  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                   (0,+1,10)

A.EFF_STATUS
  if &A.eff_status = 'I'
    print 'I'                  (0,0,1)
  else
    print ' '                  (0,0,1)
  end-if
A.COUNTRY                      (0,+1,3)
A.DESCR                          
A.DESCRSHORT                     
  let $PAYGROUP_TBL-DESCR =  ''
  let $PAYGROUP_TBL-DESCRSHORT =  ''
  do Get_Related_PAYGROUP_TBL($COMPANY,$EFFDT,$PAYGROUP)
  let $Descr = rtrim($PAYGROUP_TBL-DESCR, ' ')
  let $DescrShort = rtrim($PAYGROUP_TBL-DESCRSHORT, ' ')
  if $DescrShort = ''
     let $Descr = &A.DESCR
  end-if
  if $DescrShort = ''
     let $DescrShort = &A.DESCRSHORT
  end-if
  print $Descr                 (0,+1,32)
  print $DescrShort            (0,+1,10)

A.DFLT_BEN_PROGRAM             (0,+2,3)
A.EMPL_TYPE                    (0,+2,3)
A.PAY_FREQUENCY                (0,+2,5)
A.RETIREE_PAYGROUP             (0,+2,4)
A.HOLIDAY_SCHEDULE             (0,+1,4)
A.ERN_PROGRAM                  (0,+1,3)
SB.BANK_CD                     (0,+1,9)
SB.ACCOUNT_EC_ID               (0,+2,17)
A.NET_PAY_MINIMUM              (0,+1,4)      EDIT B999
A.NET_PAY_MAXIMUM              (0,+3,8)      EDIT B9999999
A.ERNCD_REG_HRS                (0,+1,3)
A.ERNCD_OT_HRS                 (0,+1,3)
A.ERNCD_REG_EARNS              (0,+1,3)
A.ERNCD_HOLIDAY                (0,+1,3)

  do Get-EmplTypes

FROM PS_PAYGROUP_TBL A, PS_SRC_BANK SB
WHERE A.SRC_BANK_ID = SB.SRC_BANK_ID

ORDER BY A.COMPANY, A.PAYGROUP, A.EFFDT DESC

END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC GET-EMPLTYPES
!         Get data for each employee type within paygroup
!***********************************************************************

BEGIN-PROCEDURE Get-EmplTypes

  move 0 to #count

BEGIN-SELECT

B.COMPANY
B.PAYGROUP
B.EFFDT
B.EMPL_TYPE

  move 'EMPL_TYPE' to $FieldName
  move &B.empl_type to $FieldValue
  do Read-Translate-Table
  if #count = 0
    print $XlatShortName (0,+2,10)
    move 999 to #count
  else
    print $XlatShortName (+1,-10,10)
  end-if

FROM PS_PAYGRP_EMPLTYPE B

WHERE B.COMPANY          = $company
  AND B.PAYGROUP         = $paygroup
  AND B.EFFDT            =
      ( SELECT MAX(EFFDT)
        FROM   PS_PAYGRP_EMPLTYPE
        WHERE  EFFDT = $effdt )

END-SELECT
END-PROCEDURE

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
