!**************************************************************************
! PER072JP : Future Employee Assignment List                              *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
!                                                                         *
! This module contains confidential and proprietary information           *
! of Oracle; it is not to be copied, reproduced, or transmitted           *
! in any form, by any means, in whole or in part, nor is it to            *
! be used for any purpose other than that for which it is                 *
! expressly provided under the applicable license agreement.              *
!                                                                         *
! Copyright (C) 2006 Oracle. All Rights Reserved.                         *
!                                                                         *
!**************************************************************************
!                                                                         *
!          $Date:  2006/07/19:14:58:09                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                         *
!**************************************************************************
!                                                                         *
!**************************************************************************
!This report displays the Assignment List in New Organization             *
!                                                                         *
!**************************************************************************
Begin-setup

#define  REPORTID  'PER072JP'
#include 'setenv.sqc'      !Set environment
#include 'ptset02.sqc'     !Init printer and page sizeLandscape

end-setup

begin-program
    do Init-DateTime
    do Init-Number
    do Get-Current-DateTime
    do Define-Prcs-Vars
    do Init-Report
    do Process-Main
    do Delete-From-AE-Tables
    do Update-Prcs-Run-Status
end-program

!*************************************************************************!
! Init-Report
!*************************************************************************!
!***********************************************************************
! Translations procedures & Strings Table retrievals.                  *
!***********************************************************************

begin-procedure Init-Report
  move {REPORTID} to $ReportID
  let #Error_Total = 0
  do Get-Run-Control-Parms
  do Get-Run-Cntl-Acq
  do Get-Tree-Node
  do get-org-plan-date

  do Init_Report_Translation ('PER072JP', $curr_language_cd)
  do Append_Report_Translation ('HR')

  do Get_Field_Information ('PER072JP' ,'PER072JP_TITLE'    ,$PER072JP_Title          ,#DW)
  do Get_Field_Information ('PER072JP' ,'ORG_PLAN'          ,$org_plan_lbl            ,#DW)
  do Get_Field_Information ('PER072JP' ,'EFF_DATE'          ,$org_date_lbl            ,#DW)
  do Get_Field_Information ('PER072JP' ,'BUNIT'             ,$BUnit_lbl               ,#DW)
  do Get_Field_Information ('PER072JP' ,'TREE_NAME'         ,$TREE_NAME_lbl           ,#DW)
  do Get_Field_Information ('PER072JP' ,'NODE'              ,$NODE_lbl                ,#DW)
  do Get_Field_Information ('PER072JP' ,'LVL'               ,$Lvl_lbl                 ,#DW)
  do Get_Field_Information ('PER072JP' ,'DEPT'              ,$dept_lbl                ,#DW)
  do Get_Field_Information ('PER072JP' ,'SUPV_LVL'          ,$SUPV_LVL_lbl            ,#DW)
  do Get_Field_Information ('PER072JP' ,'APPT_TYPE'         ,$APPT_TYPE_lbl           ,#DW)
  do Get_Field_Information ('PER072JP' ,'EMPLID'            ,$EMPLID_lbl              ,#DW)
  do Get_Field_Information ('PER072JP' ,'NAME'              ,$NAME_lbl                ,#DW)
  do Get_Field_Information ('PER072JP' ,'JOB_CODE'          ,$JOB_CODE_lbl            ,#DW)
  do Get_Field_Information ('PER072JP' ,'SAL_PLAN'          ,$SAL_PLAN_lbl            ,#DW)
  do Get_Field_Information ('PER072JP' ,'GRADE'             ,$GRADE_lbl               ,#DW)
  do Get_Field_Information ('PER072JP' ,'STEP'              ,$STEP_lbl                ,#DW)
  do Get_Field_Information ('HR'       ,'EMPL_RCD'          ,$EMPL_RCD_LBL            ,#DW)

  move $PER072JP_Title to $ReportTitle
  let $ReportDate            = $AsOfToday          ! Printed in header
  let $ReportTime            = $AsOfNow            ! Printed in header
  let $PNLGRPNAME = 'RC_IDO_FUT_ASN_JPN'
end-procedure Init-Report

!*************************************************************************!
! Get-Run-Cntl-Acq
!*************************************************************************!
begin-procedure Get-Run-Cntl-Acq
  let $org_plan_JPN  = ''
  let $org_date_JPN  = ''
  let $hc_type_JPN  = ''
  let $Business_Unit  = ''
  let $TREE_NAME  = ''
  let $TREE_NODE  = ''
  let $EFFDT_JPN  = ''
  let $setid  = ''

begin-SELECT

RC.BUSINESS_UNIT
RC.ORG_PLAN_JPN
RC.TREE_NAME
RC.TREE_NODE
RC.EFFDT_JPN
RC.SETID

  let $org_plan_JPN  = &RC.ORG_PLAN_JPN
  let $Business_Unit = &RC.BUSINESS_UNIT
  let $TREE_NAME     = &RC.TREE_NAME
  let $TREE_NODE     = &RC.TREE_NODE
  let $EFFDT_JPN     = &RC.EFFDT_JPN
  let $setid         = &RC.SETID

FROM
PS_RC_IDO_JPN RC

WHERE
RC.OPRID = $prcs_oprid
AND RC.RUN_CNTL_ID = $prcs_run_cntl_id

end-SELECT
end-procedure !Get-Run-Cntl-Acq

!*************************************************************************!
! Get-Tree-Node
!*************************************************************************!
begin-procedure Get-Tree-Node
  let #tree_node_num = ''
  let #tree_node_num_end = ''
 if rtrim($TREE_NODE,' ') = ''
  let #tree_node_num = 0
  let #tree_node_num_end = 9999999999
 else
  do get-tree-node-num
 end-if
end-procedure !Get-Tree-Node

begin-procedure get-tree-node-num
begin-SELECT
A.TREE_NODE_NUM
A.TREE_NODE_NUM_END
  let #tree_node_num = &A.TREE_NODE_NUM
  let #tree_node_num_end = &A.TREE_NODE_NUM_END
FROM PSTREENODE A
WHERE A.SETID = $setid
AND A.TREE_NAME = $TREE_NAME
AND A.TREE_NODE = $TREE_NODE
AND A.EFFDT = $EFFDT_JPN
end-SELECT
end-procedure !get-tree-node-num

!*************************************************************************!
! get-org-plan-date
!*************************************************************************!

begin-procedure get-org-plan-date
begin-select
A.HC_TYPE_JPN
A.ORG_DATE_JPN
 let $org_date_JPN  = &A.ORG_DATE_JPN
 let $AsOfDate      = &A.ORG_DATE_JPN
 let $hc_type_JPN   = &A.HC_TYPE_JPN
 
FROM
PS_IDO_ORGPLAN_JPN A

WHERE
A.ORG_PLAN_JPN = $org_plan_JPN
end-select
end-procedure !get-org-plan-date

begin-procedure Process-Main
  let $OrderBy-Clause = ''
  if $hc_type_JPN = 'S'
   let $OrderBy-Clause = 'ORDER BY A.DEPTID, A.SUPV_LVL_ID, A.EMPLID'
  end-if

  if $hc_type_JPN = 'J'
   let $OrderBy-Clause = 'ORDER BY A.DEPTID, A.JOBCODE, A.EMPLID'
  end-if

  if $hc_type_JPN = 'G'
   let $OrderBy-Clause = 'ORDER BY A.DEPTID, A.SAL_ADMIN_PLAN, A.EMPLID'
  end-if

begin-select
A.DEPTID (+1,5,5) on-break  level = 1
B.TREE_LEVEL_NUM 
A.SUPV_LVL_ID 
A.APPT_TYPE 
A.EMPLID 
A.EMPL_RCD 
A.JOBCODE 
A.SAL_ADMIN_PLAN 
A.GRADE 
A.STEP 

 let $DeptID        = &A.DEPTID
 let $JobCode       = &A.JOBCODE
 let $BU = $Business_Unit
 let $SETID_DEPT = $setid
 let #EMPL_RCD       = &A.EMPL_RCD
 let $EmplID        = &A.EMPLID
 let $SUPV_LVL_ID   = &A.SUPV_LVL_ID
 
 ! Keys to be passed for Procedure Read-Translate-Table for Field APPT_TYPE
 let $FieldName   = 'APPT_TYPE'
 let $FieldValue  = &A.APPT_TYPE
 do Read-Translate-Table
 let $APPT_TYPE_value = $XlatShortName
 
 do Get-Department-Name
 do Get-Job-Title
 do Get-Supvlvl-Name
 do Get-Employee-Name
 do Print_Line
 !position (+1)
 
FROM  PS_IDO_NEW_ORG_JPN A, PSTREENODE B

WHERE B.TREE_NODE = A.DEPTID
AND B.SETID = $setid
AND B.TREE_NAME = $TREE_NAME 
AND B.TREE_NODE_NUM BETWEEN #tree_node_num AND #tree_node_num_end
AND B.EFFDT = $EFFDT_JPN
AND A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
[$OrderBy-Clause]

end-select
end-procedure !Process-Main


!*************************************************************************!
! get-setid-dept
!*************************************************************************!
begin-procedure get-setid-dept
begin-select
A5.SETID
 let $SETID_DEPT = &A5.SETID
FROM
PS_SET_CNTRL_REC A5
WHERE A5.RECNAME = 'DEPT_TBL'
AND A5.SETCNTRLVALUE = $Business_Unit
end-select
end-procedure !get-setid-dept

begin-heading 10
#include 'stdhdgtr.sqc'

   print $org_plan_lbl (+1,1,22)
   print $org_plan_JPN (0,25,4)
   print $org_date_lbl (+1,1,12)
   print $org_date_JPN (0,16,10)
   print $BUnit_lbl (+1,1,15)
   print $Business_Unit (0,16,6)
   print $TREE_NAME_lbl (+1,1,15)
   print $TREE_NAME (0,16,15)
   print $NODE_lbl (+1,1,15)
   print $TREE_NODE (0,16,15)
     
   print $Lvl_lbl (+1,1,4)
   print $dept_lbl (0,5,40)
   print $SUPV_LVL_lbl (0,50,20)
   print $APPT_TYPE_lbl (0,73,10)
   print $EMPLID_lbl (0,85,6)
   print $EMPL_RCD_LBL (0,93,4)
   print $NAME_lbl (0,99,20)
   print $JOB_CODE_lbl (0,124,10)
   print $SAL_PLAN_lbl (0,136,11)
   print $GRADE_lbl (0,149,10)
   print $STEP_lbl (0,161,10)
   print '-' (+1,1,45) FILL
   print '-' (0,50,20) FILL
   print '-' (0,73,10) FILL
   print '-' (0,85,6) FILL
   print '-' (0,93,4)  FILL
   print '-' (0,99,20) FILL
   print '-' (0,124,10) FILL
   print '-' (0,136,11) FILL
   print '-' (0,149,10) FILL
   print '-' (0,161,10) FILL
   print ''  (+1,1,1)

end-heading
!*************************************************************************!
! Print_Line
!*************************************************************************!
begin-procedure Print_Line

  print &B.TREE_LEVEL_NUM (0,1,4) on-break  level = 1
  print $DeptName (0,15,30) on-break  level = 1
  print $SupvlvlName (0,50,20)
  print $APPT_TYPE_value (0,73,10)
  print &A.EMPLID (0,85,6)
  print #EMPL_RCD (0,91) edit 999 
  !print &A.EMPL_RCD (0,93,3)
  print $Name (0,99,20)
  print &A.JOBCODE (0,124,10)
  print &A.SAL_ADMIN_PLAN (0,136,10)
  print &A.GRADE (0,149,10)
  print &A.STEP (0,161,10)
end-procedure !Print_Line

!*************************************************************************!
! Delete-From-AE-Tables
!*************************************************************************!

begin-procedure Delete-From-AE-Tables

begin-sql
Delete from PS_IDO_NEW_ORG_JPN
Where JOBINSTANCE = #prcs_job_instance
end-sql

end-procedure !Delete-From-AE-Tables

!*************************************************************************!
! SQC Files for called procedures
!*************************************************************************!
#include 'prcsdef.sqc'       !Update Process Request variable declare
#include 'prcsapi.sqc'       !Update Process Request API
#include 'hrsecty.sqc'       !Get SQR Security parameters
#include 'hrsectjp.sqc'      !Get JPN SQR Security parameters
#include 'curdttim.sqc'      !Get-Current-DateTime procedure
#include 'datetime.sqc'      !Routines for date and time formatting
#include 'number.sqc'        !Routines to format numbers
#include 'sqrtrans.sqc'      !Translate SQR strings to given language
#include 'datemath.sqc'      !Date conversion procedures
#include 'getsupjp.sqc'     ! Get Supervisor Level Name
#Include 'getempnm.sqc'     ! Get Employee Name
#include 'getdptnm.sqc'     ! Get Department Name from the Department Table
#include 'getjobtl.sqc'     ! Get Job Title
#include 'getgrdjp.sqc'     ! Get Grade Name
#include 'readxlat.sqc'     ! XLAT Table Value
