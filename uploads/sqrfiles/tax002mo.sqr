!***********************************************************************
!  TAX002MO:  Qtrly State Employee Wage List - MISSOURI                *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/19:16:23:13                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Get USA variables

begin-setup

#include 'prt002.sqc'  !Printer and page-size initialization

end-setup

#define GETVAL Y

begin-report
  do Init-Report
  do Report

  if #Employees_On_Page > 0 and #Employees_On_Page < 21
    do Page-Complete
  end-if

  do Stdapi-Term


end-report

begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
  display 'Quarterly Employee Wage List - Missouri'
  do Get-Tax-Reporting-Run-Controls
  move $PeriodEndDate to $AsOfDate  !for included procedures

  move &TX.Balance_Year to $QtrBeginDate xxxx
  let #QtrBeginMonth = #QtrEndMonth - 2
  move #QtrBeginMonth to $QtrBeginMonth 09
  let $QtrBeginDate = $QtrBeginDate || '-' || $QtrBeginMonth || '-01'

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($QtrBeginDate,$QtrBeginDate,{DEFYMD},'','native')
  do Get-Calendar-Year-Id

  do Stdapi-Init

  if $Prcs_Process_Instance = ''
    do Ask-Values
  else
    do Select-Parameters
    move $RC_QTR_UI.Report_Probatnary to $ReportProbationary
  end-if

end-procedure

begin-procedure Ask-Values
  while INSTR('YNQ',$ReportProbationary,1) = 0
    input $ReportProbationary 'Report Probationary Employees? Enter Y/N or Q (to quit)'
    uppercase $ReportProbationary
  end-while

  if $ReportProbationary = 'Q'
    stop quiet
  end-if
end-procedure

begin-heading 12
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    print &Employer_ID_SUT        (8,6)
  else
    print &Employer_ID_SWT        (8,6)
  end-if
  print $CompanyName              (10,6)
  print $Compnyadd1               (+1,6)
  let $field = RTRIM($Compnycity, ' ')
  print $field                    (+1,6)
  print &CT.State                 ()      edit ,bxx
  move  &CT.Postal                to $Zip
  move  &CT.Postal                to $ZipExtsn ~~~~~~xxxx
  if instr($Zip,'-',6) = 0
  and $ZipExtsn <> '    '
    let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if
  print $Zip                      (0,+2)
  move '0' to $year4
  do Format-DateTime($PeriodEndDate, $out, {DEFMDY}, '', '')
  print $out                      (8,42)
  do Format-Number(#PageTotal, $out, '99999999.99')
  print $out                      (57,54)
  do Format-Number(#PAGE_NUM, $out, 'b99')
  print $out                      (57,8)
  do Format-Number(#TotalPages, $out, 'b99')
  print $out                      (0,+6)
end-heading

begin-procedure Report

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
C.COMPANY () on-break print=never before=Company-Change
C.STATE
C.EMPLID
C.TAX_CLASS
C.LOCALITY
C.TXGRS_QTD
C.NLGRS_QTD

D1.NATIONAL_ID
PN.LAST_NAME
PN.FIRST_NAME
PN.MIDDLE_NAME

  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_PERSON_NAME PN,
      PS_PERS_NID D1, PS_COMPANY_TBL A
WHERE D1.EMPLID           = C.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND PN.EMPLID           = C.EMPLID
#ifdef TAXTEST
  AND C.STATE             = 'CA'
#else
  AND C.STATE             = 'MO'
#endif
  AND C.LOCALITY          = ' '
  AND C.TAX_CLASS         = 'U'                ! Unemployment
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = &TX.Balance_Year
  AND C.BALANCE_QTR       = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD    =
      (SELECT MAX(C1.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C1
       WHERE  C1.EMPLID        = C.EMPLID
         AND  C1.COMPANY       = C.COMPANY
         AND C1.BALANCE_ID     = C.BALANCE_ID
         AND C1.BALANCE_YEAR   = C.BALANCE_YEAR
         AND C1.BALANCE_QTR    = C.BALANCE_QTR
         AND  C1.STATE         = C.STATE
         AND  C1.LOCALITY      = C.LOCALITY
         AND  C1.TAX_CLASS     = C.TAX_CLASS)
  AND C.NLGRS_QTD     > 0
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
ORDER BY C.COMPANY, D1.NATIONAL_ID, C.STATE, C.LOCALITY, C.TAX_CLASS
end-SELECT
end-procedure

begin-procedure Process-Tax-Balance
  move &D1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
    print 'Applied For'          (+2,4)
  else
    print &D1.NATIONAL_ID        (+2,4)  edit xxx-xx-xxxx
  end-if

  let $LastName   = rtrim(&PN.LAST_NAME, ' ')
  let $FirstName  = rtrim(&PN.FIRST_NAME, ' ')
  let $MidInitial = {ps-substr}(&PN.MIDDLE_NAME,1,1)

  print $FirstName               (0,22) edit x
  print $Midinitial              (0,25)
  print $LastName                (0,29,20)

  move &C.NLGrs_QTD to #NLGrs_QTD
  do Format-Number(#NLGrs_QTD, $out, '99999999.99')
  print $out                     (0,54)

  if $ReportProbationary = 'Y'
    do Check-Probationary
    print $Probationary          (0,67)
  end-if

  add #NLGrs_QTD to #PageTotal
  add 1 to #Employees_On_Page

  if #Employees_On_Page = 21
    do Page-Complete
  end-if
end-procedure

begin-procedure Get-Total
begin-SELECT

COUNT(*)          &EmplCount
#ifdef DB2ALL
DECIMAL(SUM(TB.NLGRS_QTD),15,3) &CompanyTotal
#else
SUM(TB.NLGRS_QTD) &CompanyTotal
#endif


FROM  PS_TAX_BALANCE TB, PS_COMPANY_TBL A2
WHERE TB.COMPANY       = &C.Company
#ifdef TAXTEST
  AND TB.STATE         = 'CA'
#else
  AND TB.STATE         = 'MO'
#endif
  AND TB.LOCALITY      = ' '
  AND TB.TAX_CLASS     = 'U'                 ! Unemployment
  AND TB.BALANCE_ID    = $Calendar_Year_Id
  AND TB.BALANCE_YEAR  = &TX.Balance_Year
  AND TB.BALANCE_QTR   = &TX.Balance_Qtr
  AND TB.NLGRS_QTD     > 0
  AND TB.BALANCE_PERIOD       =
      (SELECT MAX(C2.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C2
       WHERE  C2.EMPLID        = TB.EMPLID
         AND  C2.COMPANY       = TB.COMPANY
         AND C2.BALANCE_ID    = TB.BALANCE_ID
         AND C2.BALANCE_YEAR  = TB.BALANCE_YEAR
         AND C2.BALANCE_QTR   = TB.BALANCE_QTR
         AND  C2.STATE         = TB.STATE
         AND  C2.LOCALITY      = TB.LOCALITY
         AND  C2.TAX_CLASS     = TB.TAX_CLASS)
  AND A2.COMPANY = TB.COMPANY
  AND A2.EFF_STATUS = 'A'
  AND A2.TAX_REPORT_TYPE = '2'
  AND A2.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A2.COMPANY
         AND  EFFDT  <= $AsOfDate)
GROUP BY TB.COMPANY, TB.STATE
end-SELECT
  let #TotalPages = CEIL(&EmplCount/21)
end-procedure

begin-procedure Check-Probationary
  move '' to $Probationary
begin-SELECT
B.EFFDT
E.HIRE_DT

  do Convert-To-DTU-Date(&B.EffDt,   $EffDtTmp)
  do Convert-To-DTU-Date(&E.Hire_Dt, $HireDtTmp)
  do DTU-Diff-Days($HireDtTmp, $EffDtTmp, #Service)

  if #Service <= 28
    move '0' to $year4
    do Format-DateTime(&E.Hire_Dt, $Hire_Dt, {DEFMDY}, '', '')

    move '0' to $year4
    do Format-DateTime(&B.EffDt, $Term_Dt, {DEFMDY}, '', '')

    let $Probationary = SUBSTR($Hire_Dt,1,5) || '-' ||
                        SUBSTR($TERM_DT,1,5) || ' P'
  end-if
FROM  PS_JOB B, PS_EMPLOYMENT E
WHERE B.COMPANY    = &C.Company
  AND B.EMPLID     = &C.EmplID
  AND B.EMPLID     = E.EMPLID
  AND B.EMPL_RCD  = E.EMPL_RCD
  AND B.ACTION  = 'TER'
  AND B.EFFDT >= $QtrBeginDate
  AND B.EFFDT <= $PeriodEndDate
end-SELECT
end-procedure

begin-procedure Page-Complete
  add 1 to #PAGE_NUM
  new-page
  move 0 to #PageTotal
  move 0 to #Employees_On_Page
end-procedure

begin-procedure Company-Change
  if #Employees_On_Page < 21 and #Employees_On_Page > 0
    do Page-Complete
  end-if
  move 0 to #PAGE_NUM
  move &C.Company to $Company
  do Get-Company-Data
#ifdef TAXTEST
  move 'CA' to $State
#else
  move 'MO' to $State
#endif
  do Get-State-Tax-Data             !Employer ID
  do Get-Total
end-procedure

#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date math
#Include 'number.sqc'    !Routines to format numbers

#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Get Run Controls for Process Scheduler

