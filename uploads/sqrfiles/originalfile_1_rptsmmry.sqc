!***********************************************************************
!  RptSmmry:  Print summary information                                *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/21:12:40:09                           !
!             $Release:  HR9                                           !
!            $Revision:  102                                           !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************

begin-heading 6
  #Include 'stdhdg01.sqc'
  print 'Company'          (3,1,11)
  print $LastCompany       (0,13,3)
  print $LastCompanyName   (0,17)
  print $RptQtrYr          (3,,)      center
end-heading

begin-procedure Print-Summary-Data($Type)
  if $_Company <> $_LastCompany
    move $_Company     to $_LastCompany
    move $_CompanyName to $_LastCompanyName
    move '' to $PrintHeader
  end-if

  evaluate $Type
    when = 'NegWage'
      if $PrintHeader = ''
        print '**** Negative Wages ******'  (+1,1)
        print ' '                         (+2,1,11)
        print '     QTD     '             (0,+4,13)
        print '     QTD      '            (0,+4)
        print 'EmplID'                    (+1,1,11)
        print 'Taxable Gross'             (0,+4,13)
        print 'No Limit Gross'            (0,+4)
        position (+2)
        move 'N' to $PrintHeader
      end-if
      do Print-Negative-Wages
    when = 'NegAmt'
      if $PrintHeader = ''
        print '**** Negative Amounts ****'  (+1,1)
        print ' '                         (+2,1,11)
        print '     QTD     '             (0,+4,13)
        print '     QTD      '            (0,+4)
        print 'EmplID'                    (+1,1,11)
        print 'Tax Withheld'              (0,+4,13)
        print 'No Limit Gross'            (0,+4)
        position (+2)
        move 'N' to $PrintHeader
      end-if
      do Print-Negative-Amounts
    when = 'AcctTotal'                                                  !HP99999
      do Print-Neg-Wages-Acct-Totals                                    !HP99999
    when = 'CoTotal'
      do Print-Negative-Wages-Totals
    when = 'CoAmt'
      do Print-Negative-Amounts-Totals
    when = 'FileTotal'
      move '' to $_LastCompany
      move '' to $_LastCompanyName
      do Print-File-Totals
    when = 'NoData'
      move '' to $_LastCompany
      move '' to $_LastCompanyName
      print '******  No Data Selected  ******'  (+2,) center
  end-evaluate
end-procedure

begin-procedure Print-Negative-Wages

  print &C.EmplID        (+1,1,14)
  print &C.TxGrs_QTD     (0,16) edit 99,999,999.99mi
  print &C.NlGrs_QTD     (0,34) edit 99,999,999.99mi

  add &C.TxGrs_QTD to #Neg_TxGrs_QTD
  add &C.NlGrs_QTD to #Neg_NlGrs_QTD
end-procedure


begin-procedure Print-Negative-Amounts

  print &C.EmplID        (+1,1,14)
  print #Tax_QTD         (0,16) edit 99,999,999.99mi
  print &C.NlGrs_QTD     (0,34) edit 99,999,999.99mi
  print $ErMsg           (0,52)

  add #Tax_QTD     to #Neg_Amts_Tax_QTD
  add &C.NlGrs_QTD to #Neg_Amts_NlGrs_QTD
end-procedure

begin-procedure Print-Neg-Wages-Acct-Totals                             !HP99999
  if #Neg_TxGrs_QTD <> 0 or #Neg_NlGrs_QTD <> 0                         !HP99999
    print '** Total **'      (+2,1,14)                                     !HP99999
    print #Neg_TxGrs_QTD     (0,16) edit 99,999,999.99mi                !HP99999
    print #Neg_NlGrs_QTD     (0,34) edit 99,999,999.99mi                !HP99999
                                                                        !HP99999
    move 0 to #Neg_TxGrs_QTD                                            !HP99999
    move 0 to #Neg_NlGrs_QTD                                            !HP99999
  end-if                                                                !HP99999
                                                                        !HP99999
  do Print-Acct-Totals                                                  !HP99999
                                                                        !HP99999
end-procedure                                                           !HP99999
                                                                        !HP99999
begin-procedure Print-Negative-Wages-Totals
  if #Neg_TxGrs_QTD <> 0 or #Neg_NlGrs_QTD <> 0
    print '** Total **'      (+2,1,14)
    print #Neg_TxGrs_QTD     (0,16) edit 99,999,999.99mi
    print #Neg_NlGrs_QTD     (0,34) edit 99,999,999.99mi

    move 0 to #Neg_TxGrs_QTD
    move 0 to #Neg_NlGrs_QTD
  end-if

  do Print-Company-Totals

end-procedure

begin-procedure Print-Negative-Amounts-Totals
  if #Neg_Amts_Tax_QTD <> 0 or #Neg_Amts_NlGrs_QTD <> 0
    print '** Total **'      (+2,1,14)
    print #Neg_Amts_Tax_QTD       (0,16) edit 99,999,999.99mi
    print #Neg_Amts_NlGrs_QTD     (0,34) edit 99,999,999.99mi

    move 0 to #Neg_Amts_Tax_QTD
    move 0 to #Neg_Amts_NlGrs_QTD
  end-if

  do Print-Company-Totals

end-procedure

begin-procedure Print-Acct-Totals                                       !HP99999
                                                                        !HP99999
  print '**** Account Number ('          (+3,1,21)                         !HP99999
  print $Employer_ID                     (  ,22,19)                        !HP99999
  print ') Total ****'                   (  ,42)                        !HP99999
  print 'No Limit Gross QTD '            (+2,1,26)                         !HP99999
  print #Acct_NlGrs_QTD                   (,28)   edit 999,999,999.99mi !HP99999
                                                                        !HP99999
  if #Co_TxGrs_QTD <> 0                                                 !HP99999
    print 'Taxable Gross QTD  '          (+2,1,26)                         !HP99999
    print #Acct_TxGrs_QTD                 (,28)   edit 999,999,999.99mi !HP99999
  end-if                                                                !HP99999
                                                                        !HP99999
  print 'Employees Reported'             (+2,1,29)                         !HP99999
  print #Count_ID                         (,31)   edit 999,999,999      !HP99999
                                                                        !HP99999
end-procedure                                                           !HP99999

begin-procedure Print-Company-Totals

  print '**** Company Total ****'        (+3,1)
  print 'No Limit Gross QTD '            (+2,1,26)
  print #Co_NlGrs_QTD                     (,28)   edit 999,999,999.99mi

  If $Prnt_CoTxGrs <> 'N'
    print 'Taxable Gross QTD  '            (+2,1,26)
    print #Co_TxGrs_QTD                     (,28)   edit 999,999,999.99mi
  End-If

  If $Prnt_CoExGrs <> 'N'
    print 'Excess Gross QTD   '            (+2,1,26)
    print #Co_ExGrs_QTD                     (,28)   edit 999,999,999.99mi
  End-If

  If $Prnt_CoTips = 'Y'
    print 'Declared Tips QTD  '            (+2,1)
    print #Co_Tips_QTD                      (,28)   edit 999,999,999.99mi
  End-If

  print 'Employees Reported'             (+2,1,29)
  print #Count_S                          (,31)   edit 999,999,999

  if #Count_R > 0
    print 'Records Reported'             (+2,1,29)
    print #Count_R                        (,31)   edit 999,999,999
  end-if

  if #TaxesDue <> 0
    print 'Taxes Due '                   (+2,1,26)
    print #TaxesDue                       (,28)   edit 999,999,999.99mi
  end-if

  if #PrevQtrUnderpay <> 0
    print 'Previous Qtr Underpayment '   (+2,1,26)
    print #PrevQtrUnderpay                (,28)   edit 999,999,999.99mi
  end-if

  if #Interest <> 0
    print 'Interest  '                   (+2,1,26)
    print #Interest                       (,28)   edit 999,999,999.99mi
  end-if

  if #Penalty <> 0
    print 'Penalty   '                   (+2,1,26)
    print #Penalty                        (,28)   edit 999,999,999.99mi
  end-if

  if #Credit <> 0
    print 'Credit/Overpayment'           (+2,1,26)
    print #Credit                         (,28)   edit 999,999,999.99mi
  end-if

  if #TotalPayment <> 0
    print 'Total Payment '               (+2,1,26)
    print #TotalPayment                   (,28)   edit 999,999,999.99mi
  end-if

  if #Co_STW_QTD <> 0
    print 'State Tax Withheld QTD'       (+2,1,26)
    print #Co_STW_QTD                     (,28)   edit 999,999,999.99mi
  end-if

  if #Month1Count <> 0
    print 'Month 1 Count '               (+2,1,33)
    print #Month1Count                    (,32)   edit 99,999,999
  end-if

  if #Month2Count <> 0
    print 'Month 2 Count '               (+2,1,33)
    print #Month2Count                    (,32)   edit 99,999,999
  end-if

  if #Month3Count <> 0
    print 'Month 3 Count '               (+2,1,33)
    print #Month3Count                    (,32)   edit 99,999,999
  end-if

  if RTRIM($DCN, ' ') <> ''
    print 'Document Control Number'      (+2,1,31)
    print $DCN                            (,33)
  end-if

  if RTRIM($TaxTypeCode, ' ') <> ''
    print 'Tax Type Code'                (+2,1,31)
    print $TaxTypeCode                    (,33)
    if $TaxTypeCode = 'T'
      print '(Taxable)'                   (,+1)
    else
      print '(Reimbursable)'              (,+1)
    end-if
  end-if

  IF $STATE='OR'
    PRINT 'Hours Reported'                  (+2,1,14)
    PRINT #SUB_HOURS                        ( ,31)  edit 999,999,999
  END-IF

  IF $STATE='NM'

    if #Co-QTD-WorkersComp <> 0
      print 'Workers Comp Withheld QTD'    (+2,1,26)
      print #Co-QTD-WorkersComp            (,28)   edit 999,999,999.99mi
    end-if

    if #XML_Calced_Tax_due <> 0
      print 'Calculated Tax Due'           (+2,1,26)
      print #XML_Calced_Tax_due            (,28)   edit 999,999,999.99mi
    end-if

  END-IF

end-procedure

begin-procedure Print-File-Totals
  print '**** File Total ****'     (+3,1)
  print 'No Limit Gross QTD '      (+2,1,26)
  print #Tot_NlGrs_QTD              (,28)   edit 999,999,999.99mi

  if #Tot_TxGrs_QTD <> 0
    print 'Taxable Gross QTD  '    (+2,1,26)
    print #Tot_TxGrs_QTD            (,28)   edit 999,999,999.99mi
  end-if

  if #Tot_ExGrs_QTD <> 0
    print 'Excess Gross QTD   '    (+2,1,26)
    print #Tot_ExGrs_QTD            (,28)   edit 999,999,999.99mi
  end-if

  if #Tot_Tips_QTD <> 0
    print 'Declared Tips QTD  '    (+2,1)
    print #Tot_Tips_QTD             (,28)   edit 999,999,999.99mi
  end-if

  print 'Employees Reported'       (+2,1,29)
  print #Count_F                    (,31)   edit 999,999,999

  if #Count_R_Total > 0
    print 'Records Reported'         (+2,1,29)
    print #Count_R_Total              (,31)   edit 999,999,999
  end-if

  if #Count_E <> 0
    print 'Employers Reported'     (+2,1,29)
    print #Count_E                  (,31)   edit 999,999,999
  end-if

  print 'Output File(s):  '        (+2,1)
  move 1 to #i
  if #FileExtension > 0
    while #i <= #FileExtension
      move #i to $i 099
      let $FileID = $ReportID || '.' || $i
      let $FileID = '{IMPORTPREFIX}' || $FileID || '{IMPORTSUFFIX}'
      print $FileID                  (+1,10)
      add 1 to #i
    end-while
  else
      let $FileID = $ReportID
      let $FileID = '{IMPORTPREFIX}' || $FileID || '{IMPORTSUFFIX}'
      print $FileID                  (+1,10)
  end-if

  IF $STATE='OR'
    PRINT 'Total Hours Reported'              (+2,1,20)
    PRINT #TOT_HOURS                          ( ,31) edit 999,999,999
  END-IF
end-procedure
