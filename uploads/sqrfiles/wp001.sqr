!***********************************************************************
! WP001: Wage progression - Employees Due Advancement
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:15:59:14                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#include 'setup32.sqc'  !page layout and printer definition

#define col_Sep                    1     !Column separator
#define col_HR_WP_ID               10    !HR WP run id
#define col_Emplid                 11    !Emplid
#define col_EmplRcd                8     !EmplRcd
#define col_Name                   26    !Name
#define col_SalPlanSETID           6     !Sal Plan setid
#define col_SalPlan                8     !Sal Plan
#define col_Grade                  5     !Grade
#define col_Step                   4     !Step
#define col_JobCode                8     !Jobcode
#define col_WPType                 20    !HR WP Type Category
#define col_BARGUnt                15    !Bargaining Unit
#define col_ADVDate                15    !Advancement Date
#define col_APPby                  12    !Approved By
#define col_APPDate                12    !Approval Date


Begin-Heading 9
#Include 'stdhdg01.sqc' !Standard Report header

! Print Title and WP run id on the header
  print $WP001-REPORTTITLE (+1) CENTER
  print 'Wage Progression Run ID:'  (+1,5,25)
  print $RC_HR_WP_ID (0,+{col_sep},{col_HR_WP_ID})

!Column Heading line 1
  print $WP001-EMPLID             (+2,5,{col_Emplid})
  print $WP001-EMPLRCD            (0,+{col_sep},{col_EmplRcd})
  print $WP001-NAME               (0,+{col_sep},{col_Name})
  print $WP001-SPSETID            (0,+{col_sep},{col_SalPlanSetid})
  print $WP001-SALPLAN            (0,+{col_sep},{col_SalPlan})
  print $WP001-SALGRADE           (0,+{col_sep},{col_Grade})
  print $WP001-STEP               (0,+{col_sep},{col_Step})
  print $WP001-JOBCODE            (0,+{col_sep},{col_Jobcode})
  print $WP001-WPTYPE             (0,+{col_sep},{col_WPType})
  print $WP001-ADVDATE            (0,+{col_sep},{col_ADVDate})
  print $WP001-APPBy              (0,+{col_sep},{col_APPby})
  print $WP001-APPDATE            (0,+{col_sep},{col_APPDate})
  print $WP001-BARGUNT            (0,+{col_sep},{col_BARGUnt})

!Column Heading line 2
  print '='            (+1,5,{col_Emplid})                    FILL
  print '='            (0,+{col_sep},{col_EmplRcd})           FILL
  print '='            (0,+{col_sep},{col_Name})              FILL
  print '='            (0,+{col_sep},{col_SalPlanSetid})      FILL
  print '='            (0,+{col_sep},{col_SalPlan})           FILL
  print '='            (0,+{col_sep},{col_Grade})             FILL
  print '='            (0,+{col_sep},{col_Step})              FILL
  print '='            (0,+{col_sep},{col_Jobcode})           FILL
  print '='            (0,+{col_sep},{col_WPType})            FILL
  print '='            (0,+{col_sep},{col_ADVDate})           FILL
  print '='            (0,+{col_sep},{col_APPby})             FILL
  print '='            (0,+{col_sep},{col_APPDate})           FILL
  print '='            (0,+{col_sep},{col_BARGUnt})           FILL

End-Heading

Begin-Report
  do Init-Report
  do Process-Main
  do Reset
  do StdAPI-Term

End-Report

!***********************************************************************
! Init-Report                                                          *
! Called By:    begin-report                                           *
! Calls:        Init-Datetime           - datetime.sqc                 *
!               Get-Current-Datetime    - curdttim.sqc                 *
!               Init-Number             - number.sqc                   *
!               StdAPI-Init             - stdapi.sqc                   *
!               Get-Language-Codes - prcslng.sqc              *
!               Report-Translation      -                              *
! Description:  Standard PeopleSoft initialization processing.         *
!***********************************************************************

begin-procedure Init-Report
        let $ReportID    = 'WP001'
        do Init-Datetime
        do Get-Current-Datetime
        do Init-Number
     do StdAPI-Init
        if $prcs_process_instance = ''
               display ''
               display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
               display ''
               STOP QUIET
     else
               do Select-Parameters
     end-if
        do Get-Language-Codes
        do Report-Translation
end-procedure ! Init-Report

!***********************************************************************
! Report-Translation                                                   *
! Called By:    Init-Report                                            *
! Calls:        Init_Report_Translation    - sqrtrans.sqc              *
!               Append_Report_Translation  - sqrtrans.sqc              *
!               Get_Field_Information                                  *
! Description:  Performs Strings Table translation.                    *
!***********************************************************************

Begin-Procedure Report-Translation

  do Init_Report_Translation($ReportID, $curr_language_cd)
  do Append_Report_Translation('WP001')

  do Get_Field_Information ('WP001', 'REPORT_TITLE', $WP001-REPORTTITLE, #CW)
  do Get_Field_Information ('WP001', 'HR_WP_ID',     $WP001-HR_WP_ID,    #CW)
  do Get_Field_Information ('WP001', 'EMPLID',       $WP001-EMPLID,      #CW)
  do Get_Field_Information ('WP001', 'EMPL_RCD#',    $WP001-EMPLRCD,     #CW)
  do Get_Field_Information ('WP001', 'NAME',         $WP001-NAME,        #CW)
  do Get_Field_Information ('WP001', 'SETID_SALARY', $WP001-SPSETID,     #CW)
  do Get_Field_Information ('WP001', 'SAL_PLAN',     $WP001-SALPLAN,     #CW)
  do Get_Field_Information ('WP001', 'GRADE',        $WP001-SALGRADE,    #CW)
  do Get_Field_Information ('WP001', 'STEP',         $WP001-STEP,        #CW)
  do Get_Field_Information ('WP001', 'JOBCODE',      $WP001-JOBCODE,     #CW)
  do Get_Field_Information ('WP001', 'WPTYPE',       $WP001-WPTYPE,      #CW)
  do Get_Field_Information ('WP001', 'ADVDATE',      $WP001-ADVDATE,     #CW)
  do Get_Field_Information ('WP001', 'APPRVBY',      $WP001-APPBY,       #CW)
  do Get_Field_Information ('WP001', 'APPDATE',      $WP001-APPDATE,     #CW)
  do Get_Field_Information ('WP001', 'BARGUNT',      $WP001-BARGUNT,     #CW)
  do Get_Field_Information ('WP001', 'APPMAN',       $WP001-APPMan,     #CW)
  do Get_Field_Information ('WP001', 'APPSYS',       $WP001-APPSys,     #CW)
End-Procedure

!***********************************************************************
! Report-Translation                                                   *
! Called By:    Begin-Report section                                   *
! Calls:                                                               *
!                                                                      *
! Description:  Main select logic for report.                          *
!***********************************************************************

Begin-Procedure Process-Main

Begin-Select
A.EMPLID            (+1,5,{col_Emplid})
A.EMPL_RCD          (0,+1,{col_EmplRcd})
B.NAME              (0,+1,{col_Name})
A.SETID_SALARY          (0,+1,{col_SalPlanSETID})
A.SAL_ADMIN_PLAN        (0,+1,{col_SalPlan})
A.GRADE                 (0,+1,{col_Grade})
A.STEP                  (0,+1,{col_Step})
A.JOBCODE                (0,+1,{col_JobCode})
A.HR_WP_TYPE_CATGRY

        Let $Fieldname = 'HR_WP_TYPE_CATGRY'
        Let $FieldValue = &A.HR_WP_TYPE_CATGRY
        do Read-Translate-Table
        print $XlatLongName (0,+1,{col_WPType})

A.HR_WP_EST_ADV_DT
     do Format-DateTime(&A.HR_WP_EST_ADV_DT, $ADVOut, {DEFDATE}, '', '')
        print $ADVOut   (0,+1,{col_ADVDate})

A.HR_WP_PROG_STATUS

A.HR_WP_PROCESS_APPR
     if (&A.HR_WP_PROCESS_APPR = 'Y' and (&A.HR_WP_PROG_STATUS = '2' or &A.HR_WP_PROG_STATUS = '3'))
               print $WP001-APPMan (0,+1,{col_APPby})
        else
               print $WP001-APPSys (0,+1,{col_APPby})

        end-if
A.APPRVL_DT
     do Format-DateTime(&A.APPRVL_DT, $APPOut, {DEFDATE}, '', '')
        print $APPOut   (0,+1,{col_APPDate})

A.BARG_UNIT             (0,+1,{col_BARGUnt})

FROM  PS_HR_WP_ACTIVITY A,
      PS_PERSON_NAME B

WHERE A.EMPLID = B.EMPLID
    AND A.HR_WP_ID = $RC_HR_WP_ID
    AND (A.HR_WP_EXCPTN_FLAG = 'N' OR A.HR_WP_EXCPTN_APPR='Y')
    AND (A.HR_WP_PROG_STATUS = '4'
    OR (A.HR_WP_PROG_STATUS IN ('2','3') AND A.HR_WP_PROCESS_APPR = 'Y'))
    AND A.ACTIVITY_DT = (SELECT MIN(ACTIVITY_DT)
                            FROM PS_HR_WP_ACTIVITY
                            WHERE A.EMPLID = EMPLID 
                                AND A.HR_WP_ID = HR_WP_ID
                                AND (HR_WP_PROG_STATUS = '4'
                                     OR (HR_WP_PROG_STATUS IN ('2','3') AND HR_WP_PROCESS_APPR = 'Y')))

ORDER BY B.NAME, A.SAL_ADMIN_PLAN, A.GRADE

End-Select
End-Procedure

!*************************************

#Include 'stdapi.sqc'     !Update Process API
#Include 'wprnctl1.sqc'   !Get application run control parameters
#Include 'curdttim.sqc'   !Get-Current-DateTime procedure
#include 'prcslng.sqc'    !Replaces getrplng.sqc
#include 'rellang.sqc'    !Related Language File
#include 'number.sqc'     !Routines to format numbers
#include 'datetime.sqc'   !Routines for date and time formatting
#Include 'reset1.sqc'     !Reset printer procedure
#Include 'readxlat.sqc'   !get translates for fieldname, value
#Include 'sqrtrans.sqc'   !Translate SQR strings to given language

!******************************* End Of Report *******************************
