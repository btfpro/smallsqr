!*******************************************
!  PER020:  Employee Home Address Listing  *
!*******************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/08/06:00:32:00                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  689848                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
begin-setup
#Include 'ptpsp125.sqc'  !Printer and page-size initialization
end-setup

#define page-len 79      !Used to deted need for a new page
                         !Keep the same as 'page-size' in printer definition

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-heading 6
do Get_Field_Information ('PER020',     'REPORT_TITLE',         $REPORTTITLE,   #DW)
  #Include 'stdhdgtr.sqc'
do Get_Field_Information ('HR',         'PHONE',                $PHONE,         #DW)
do Get_Field_Information ('HR',         'EMPLID',               $EMPLID_TTL,    #DW)
do Get_Field_Information ('HR',         'ADDRESS1',             $ADDRESS,       #DW)

do Get_Field_Information ('HR',         'EMPL_NAME',            $NAME1,          #DW)

PRINT   $NAME1                   (+2,1)
PRINT   $EMPLID_TTL              (0,52)
PRINT   $ADDRESS                 (0,65)
PRINT   $PHONE                   (0,115)
end-heading

begin-procedure Init-Report
Let $_TableAlias = 'A'
  #Define Year4 '1'
  move 'PER020' to $ReportID
  do Stdapi-Init
  do Security-Param

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

end-procedure

begin-procedure Get-Values
let $language_cd = $PRCS_LANGUAGE_CD
end-procedure

begin-procedure Process-Main
    if $language_cd = 'JPN'
          let $qualifier = 'A.NAME_AC'
      else
           let $qualifier = 'A.NAME'
     end-if
DISPLAY $qualifier

begin-SELECT distinct

A.NAME
A.EMPLID

   move &A.EMPLID to $Emplid

   Do Get-Empl-Address ($Emplid,'HOME', $AddressFound)

   let $COUNTRY  = $GETADDR_Country
   let $ADDRESS1 = $GETADDR_Address1
   let $ADDRESS2 = $GETADDR_Address2
   let $ADDRESS3 = $GETADDR_Address3
   let $ADDRESS4 = $GETADDR_Address4
   let $HOUSE_TYPE = $GETADDR_HOUSE_TYPE
   let $ADDR_FIELD1 = $GETADDR_ADDR_FIELD1
   let $ADDR_FIELD2 = $GETADDR_ADDR_FIELD2
   let $ADDR_FIELD3 = $GETADDR_ADDR_FIELD3
   let $NUM1 = $GETADDR_NUM1
   let $NUM2 =$GETADDR_NUM2
   let $CITY = $GETADDR_CITY
   let $STATE =$GETADDR_STATE
   let $POSTAL = $GETADDR_POSTAL
   let $COUNTY = $GETADDR_COUNTY
   let $GEO_CODE =$GETADDR_GEO_CODE
   let $IN_CITY_LIMIT =$GETADDR_IN_CITY_LIMIT

   do ADFORMAT                ! Formats the address based on Country

   do Check-Lines             ! Check if a page-break is required

   print &A.NAME         (+1,1)
   print &A.EMPLID       (0,52)

   do Get-Phone-Data
   print $Phone (0,115)

   let $First_Line = 'Y'
   if $ADDLINE1 <> ''
      let $First_Line = 'N'
      print $ADDLINE1  (0,65)
   end-if
   if $ADDLINE2 <> ''
      if $First_Line = 'Y'
         let $First_Line = 'N'
         print $ADDLINE2  (0,65)
      else
         print $ADDLINE2  (+1,65)
      end-if
   end-if
   if $ADDLINE3 <> ''
      if $First_Line = 'Y'
         let $First_Line = 'N'
         print $ADDLINE3  (0,65)
      else
         print $ADDLINE3  (+1,65)
      end-if
   end-if
   if $ADDLINE31 <> ''
      if $First_Line = 'Y'
         let $First_Line = 'N'
         print $ADDLINE31  (0,65)
      else
         print $ADDLINE31  (+1,65)
      end-if
   end-if
   if $ADDLINE4 <> ''
      if $First_Line = 'Y'
         let $First_Line = 'N'
         print $ADDLINE4  (0,65)
      else
         print $ADDLINE4  (+1,65)
      end-if
   end-if
   if $ADDLINE5 <> ''
      if $First_Line = 'Y'
         let $First_Line = 'N'
         print $ADDLINE5  (0,65)
      else
         print $ADDLINE5  (+1,65)
      end-if
   end-if
   if $ADDLINE6 <> ''
      if $First_Line = 'Y'
         let $First_Line = 'N'
         print $ADDLINE6  (0,65)
      else
         print $ADDLINE6  (+1,65)
      end-if
   end-if

FROM  PS_PERSON_NAME A, PS_PERSON C, PS_JOB O
WHERE O.PER_ORG = 'EMP'
!*************************Begin Resolution 612876************************
AND O.EMPL_STATUS  IN ('A','L','P','S','W')
AND O.EFFDT = (SELECT MAX(O1.EFFDT) 
               FROM PS_JOB O1
               WHERE O1.EMPLID = O.EMPLID
               AND O1.EMPL_RCD = O.EMPL_RCD)
              
AND O.EFFSEQ = (SELECT MAX(O2.EFFSEQ)
                FROM PS_JOB O2
                WHERE O2.EMPLID = O.EMPLID
                AND O2.EMPL_RCD = O.EMPL_RCD
                AND O2.EFFDT = O.EFFDT)
!*************************End Resolution 612876************************   
and A.EMPLID = C.EMPLID
and A.EMPLID = O.EMPLID
[$SecurityClauseWithoutERN]
ORDER BY [$qualifier]
end-SELECT
end-procedure


!***************************************!
begin-procedure Get-Phone-Data
!***************************************!

  if rtrim($Emplid,' ') = ''
    move '' to $Phone
    goto GetPhoneExit
  end-if

begin-SELECT
PP.PHONE
FROM  PS_PERSONAL_PHONE PP
WHERE PP.EMPLID = $Emplid
  AND PP.PHONE_TYPE = 'HOME'
end-SELECT

     let $Phone      = rtrim(&PP.Phone,' ')


GetPhoneExit:

end-procedure


!***************************************!
begin-procedure Check-Lines
!***************************************!

   ! Count how many additional lines are required
   ! for printing the address

   let #Address_Lines = 1
   if $ADDLINE2 <> ''
      add 1 to #Address_Lines
   end-if
   if $ADDLINE3 <> ''
      add 1 to #Address_Lines
   end-if
   if $ADDLINE31 <> ''
      add 1 to #Address_Lines
   end-if
   if $ADDLINE4 <> ''
      add 1 to #Address_Lines
   end-if
   if $ADDLINE5 <> ''
      add 1 to #Address_Lines
   end-if
   if $ADDLINE6 <> ''
      add 1 to #Address_Lines
   end-if

   ! If not enough room for address, then start a new page

   if {page-len} - #current-line < #Address_Lines
      new-page
   end-if

end-procedure


#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'adformat.sqc'  !Get global addressing format
#Include 'getaddr.sqc'   !Get current Employee Address

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
#include 'hrsecty.sqc'   !Get SQR Security parameters
