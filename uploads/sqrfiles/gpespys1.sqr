!***********************************************************************
!  GPESPYS1:   Payslip                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!----------------------------------------------------------------------
!
!          $Date:  2006/09/26:03:50:10                                 !
!       $Release:  HR9                                                 !
!      $Revision:  103                                                 !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment

begin-setup
#define PAGE_PAPER_SIZE (A4)
#include 'ptset01.sqc'

DECLARE-IMAGE Logo
  type         = BMP-FILE
  image-size   = (14,5)
  source       = 'logo.bmp'
end-declare

end-setup


#define EMPL_CTG_L1
#define RGM_BSE_ESP_TBL
#define GP_RUN_TYPE
#define COMPANY_TBL
#define GPES_LBR_XTR_PD
#Include 'rellang.sqc'   ! Translations File 

!************************************************************************
! Report Section
!************************************************************************
begin-report

  do Init-Report
  do Process-Main
  do GP-ePay-Control
  do Stdapi-Term

end-report


!************************************************************************
! Procedure Init-Report
!************************************************************************
begin-procedure Init-Report
#debug do Fin-Debug-Msg('Init-Report')

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  ALTER-PRINTER point-size=7.2

  let $Curr_Language_Cd = 'ESP'

  #define HEADER-DATA-SIZE     24      !Two years retro.
  #define BODY-DATA-SIZE     1000      !About 20 earnigs/deductions by payslip/retro
  #define FOOTER-DATA-SIZE    240      !10 Accumulators by payslip/retro

  CREATE-ARRAY NAME = Header-Data SIZE = {HEADER-DATA-SIZE}
    FIELD=Cmpny-Descr:Char
    FIELD=Cmpny-DescrShort:Char
    FIELD=Empl-Name:Char
    FIELD=Cmpny-Address:Char
    FIELD=Empl-DNI-NIE:Char
    FIELD=Matricula-Nbr:Char
    FIELD=Cmpny-FiscalID:Char
    FIELD=Empl-SSN:Char
    FIELD=Empl-Ctg-Descr:Char
    FIELD=Cmpny-SSN:Char
    FIELD=Empl-SS-Grp:Char
    FIELD=RunFinalized:Char
    FIELD=Retro-SW:Char
    FIELD=Extra-Period:Char

  CREATE-ARRAY NAME = Body-Data SIZE = {BODY-DATA-SIZE}
    FIELD=Pin-Type:Char
    FIELD=Pin-Num:Char
    FIELD=Description:Char
    FIELD=Units:Char
    FIELD=Rate:Char
    FIELD=Base:Char
    FIELD=Percentage:Char
    FIELD=Amount:Number
    FIELD=Lines-Before:Number
    FIELD=Lines-After:Number
    FIELD=Retro-SW:Char
    FIELD=Sep-Extra-Pd:Char

  CREATE-ARRAY NAME = Footer-Data SIZE = {FOOTER-DATA-SIZE}
    FIELD=Pin-Num:Char
    FIELD=Description:Char
    FIELD=Amount:Number
    FIELD=Lines-Before:Number
    FIELD=Lines-After:Number
    FIELD=Retro-SW:Char
  
  Let #eBeginning-Page = 1
  Let #eEnding-Page = 1
  do GP-ePay-Init !Initialize ePay Variables
  
end-procedure


!************************************************************************
! Procedure Main
!************************************************************************
begin-procedure Process-Main
#debug do Fin-Debug-Msg('Process-Main')

  Do Select-cal-run-id

  do Payslip-order

END-PROCEDURE


!************************************************************************
! Procedure Select-cal-run-id
!************************************************************************
begin-procedure Select-cal-run-id
#debug do Fin-Debug-Msg('Select-cal-run-id')

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_RC_PAYSLIP'
begin-SELECT on-error=SQL-Error
RUNCTL.CAL_RUN_ID
RUNCTL.GPES_PRINT_OPTN
  LET $Calendar-Selected  =  rtrim(&RUNCTL.CAL_RUN_ID, ' ')
  LET $PrintOptn-Selected =  &RUNCTL.GPES_PRINT_OPTN

  evaluate $PrintOptn-Selected
    when = '1'
      let $whereclause = ' AND (PRE.RSLT_VER_NUM = 1 AND PRE.RSLT_REV_NUM = 1)'
    when = '2'
      let $whereclause = ' AND (PRE.RSLT_VER_NUM <> 1 OR PRE.RSLT_REV_NUM <> 1)'
    when = '3'
      let $whereclause = ' '
  end-evaluate

FROM PS_GPES_RC_PAYSLIP   RUNCTL

WHERE RUNCTL.OPRID = $prcs_oprid
 AND  RUNCTL.RUN_CNTL_ID = $prcs_run_cntl_id
End-SELECT

Do Get-CAL-RUN-DESCR

#debugd show 'Calendar ID:  ' $Calendar-Selected
#debugd show 'Print Option: ' $PrintOptn-Selected

end-procedure


!************************************************************************
! Procedure Payslip-order
!************************************************************************
begin-procedure Payslip-order
#debug do Fin-Debug-Msg('Payslip-order')

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_PSLIP_TMP'
begin-SELECT DISTINCT on-error=SQL-Error
SEP_A.GPES_PAYSLIP_ID
{DATEOUT-PREFIX}SEP_A.EFFDT{DATEOUT-SUFFIX}   &SEP_A.EFFDT
TMP.GPES_FIELDNAME1
TMP.GPES_PYSL_SK_ORD1
TMP.GPES_FIELDNAME2
TMP.GPES_PYSL_SK_ORD2
TMP.GPES_FIELDNAME3
TMP.GPES_PYSL_SK_ORD3
TMP.GPES_FIELDNAME4
TMP.GPES_PYSL_SK_ORD4
TMP.GPES_FIELDNAME5
TMP.GPES_PYSL_SK_ORD5
  LET $Field1 =  rtrim(&TMP.GPES_FIELDNAME1 , ' ')
  LET $Field2 =  rtrim(&TMP.GPES_FIELDNAME2 , ' ')
  LET $Field3 =  rtrim(&TMP.GPES_FIELDNAME3 , ' ')
  LET $Field4 =  rtrim(&TMP.GPES_FIELDNAME4 , ' ')
  LET $Field5 =  rtrim(&TMP.GPES_FIELDNAME5 , ' ')

  LET $OrderByclause  = 'ORDER BY '
  if not(isblank($Field1))
     Let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field1 || ' ' || rtrim(&TMP.GPES_PYSL_SK_ORD1 , ' ') || ','
     Let $Selectclause1  = 'JOB.' || $Field1
  end-if
  if not(isblank($Field2))
     Let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field2 || ' ' || rtrim(&TMP.GPES_PYSL_SK_ORD2 , ' ') || ','
     Let $Selectclause2  = 'JOB.' || $Field2
  end-if
  if not(isblank($Field3))
     Let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field3 || ' ' || rtrim(&TMP.GPES_PYSL_SK_ORD3 , ' ') || ','
     Let $Selectclause3  = 'JOB.' || $Field3
  end-if
  if not(isblank($Field4))
     Let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field4 || ' ' || rtrim(&TMP.GPES_PYSL_SK_ORD4 , ' ') || ','
     Let $Selectclause4  = 'JOB.' || $Field4
  end-if
  if not(isblank($Field5))
     Let $OrderByclause =  $OrderByclause  || 'JOB.' || $Field5 || ' ' || rtrim(&TMP.GPES_PYSL_SK_ORD5 , ' ') || ','
     Let $Selectclause5  = 'JOB.' || $Field5
  end-if
  
  let $OrderByclause = $OrderByclause || 'PRE.PYMT_DT ASC'

  #Debugd show 'Payslip ID:      ' &SEP_A.GPES_PAYSLIP_ID
  #Debugd show 'Payslip EFFDT:   ' &SEP_A.EFFDT
  #Debugd show 'Sort Criteria:   ' $Selectclause1 ' ' $Ord1
  #Debugd show '                 ' $Selectclause2 ' ' $Ord2
  #Debugd show '                 ' $Selectclause3 ' ' $Ord3
  #Debugd show '                 ' $Selectclause4 ' ' $Ord4
  #Debugd show '                 ' $Selectclause5 ' ' $Ord5
  #Debugd show 'Order By clause: ' $OrderByclause


 ! VW.RUN_FINALIZED_IND
SEP_A.GPES_RETRO_TYPE
SEP_A.GPES_EXTRA_PERIOD
TMP.PYE_CALC_STAT
  Let $Retro-Type        =  rtrim(&SEP_A.GPES_RETRO_TYPE, ' ')
  Let $Separate-Extra-Pd =  rtrim(&SEP_A.GPES_EXTRA_PERIOD, ' ')
  Let $Pye-Calc-Stat     =  rtrim(&TMP.PYE_CALC_STAT, ' ')
  #Debugd show 'Retro Type:                  ' $Retro-Type
  #Debugd show 'Separate Extra Period Ind:   ' $Separate-Extra-Pd
 !  LET $RunFinalized =  rtrim(&VW.RUN_FINALIZED_IND , ' ')

  do Employee-PRESelection

FROM  PS_GPES_PSLIP_TMP   TMP
     ,PS_GPES_PSLP_S_PYE   P
 !     ,PS_GPES_E_PSLIP_VW   VW
     ,PS_GPES_PAYSLIP_HD   SEP_A

WHERE P.OPRID = $prcs_oprid
 AND  P.RUN_CNTL_ID = $prcs_run_cntl_id
 AND  P.CAL_RUN_ID = $Calendar-Selected
 AND  TMP.EMPLID = P.EMPLID
 AND  TMP.CAL_RUN_ID = P.CAL_RUN_ID
 AND  TMP.EMPL_RCD = P.EMPL_RCD
 AND  TMP.GP_PAYGROUP = P.GP_PAYGROUP
 AND  TMP.CAL_ID = P.CAL_ID
 AND  TMP.ORIG_CAL_RUN_ID = P.ORIG_CAL_RUN_ID
 AND  TMP.RSLT_SEG_NUM = P.RSLT_SEG_NUM
 AND  TMP.ORIG_CAL_RUN_ID = TMP.CAL_RUN_ID
 ! AND  TMP.CAL_RUN_ID = VW.CAL_RUN_ID
 ! AND  TMP.GP_PAYGROUP = VW.GP_PAYGROUP
 ! AND  TMP.CAL_ID = VW.CAL_ID
 AND  TMP.GPES_PAYSLIP_ID = SEP_A.GPES_PAYSLIP_ID
 AND  SEP_A.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPES_PAYSLIP_HD
        WHERE GPES_PAYSLIP_ID = SEP_A.GPES_PAYSLIP_ID
          AND EFFDT <= TMP.SEG_END_DT)
ORDER BY SEP_A.GPES_PAYSLIP_ID
End-Select

End-Procedure


!************************************************************************
! Procedure Employee-Preselection
!************************************************************************
begin-procedure Employee-Preselection
#debug do Fin-Debug-Msg('Employee-Preselection')

LET $Prev-Emplid     =  ''
LET #Prev-Empl-Rcd   =  9999
Let #Header-Data-Row =  0
Let #Body-Data-Row   =  0
Let #Footer-Data-Row =  0

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_PSLIP_TMP'
begin-SELECT on-error=SQL-Error
[$Selectclause1]    &D1=Char
[$Selectclause2]    &D2=Char
[$Selectclause3]    &D3=Char
[$Selectclause4]    &D4=Char
[$Selectclause5]    &D5=Char

PRE.PYMT_DT
PRE.EMPLID
PRE.CAL_RUN_ID
PRE.EMPL_RCD
PRE.GP_PAYGROUP
PRE.CAL_ID
PRE.ORIG_CAL_RUN_ID
PRE.RSLT_SEG_NUM
{DATEOUT-PREFIX}PRE.SEG_BGN_DT{DATEOUT-SUFFIX}   &PRE.SEG_BGN_DT
{DATEOUT-PREFIX}PRE.SEG_END_DT{DATEOUT-SUFFIX}   &PRE.SEG_END_DT
PRE.GPES_EXTRA_PERIOD
PRE.RSLT_REV_NUM
PRE.RSLT_VER_NUM

JOB.EFFDT
JOB.SETID_LOCATION
JOB.LOCATION
JOB.LABOR_AGREEMENT
JOB.SETID_LBR_AGRMNT
JOB.EMPL_CTG
JOB.MATRICULA_NBR
JOB.CURRENCY_CD
JOB.COMPANY
JOB.CONTRACT_NUM

  LET $Extra-Period =  rtrim(&PRE.GPES_EXTRA_PERIOD,  ' ')
  If $Extra-Period = 'Y' and $Separate-Extra-Pd = 'N'
     !Nothing to do. Already done in previous loop procedure Process-Extra-Pd.
  Else
     LET $Emplid          =  rtrim(&PRE.EMPLID,  ' ')
     LET #Empl-Rcd        =  &PRE.EMPL_RCD
     LET $Cal-Run-ID      =  rtrim(&PRE.CAL_RUN_ID, ' ')
     LET $GP-Paygroup     =  rtrim(&PRE.GP_PAYGROUP, ' ')
     LET $Cal-ID          =  rtrim(&PRE.CAL_ID, ' ')
     LET $Orig-Cal-Run-ID =  rtrim(&PRE.ORIG_CAL_RUN_ID, ' ')
     LET #Rslt-Seg-Num    =  &PRE.RSLT_SEG_NUM

     LET $Seg-Bgn-Dt      =  &PRE.SEG_BGN_DT
     LET $Seg-End-Dt      =  &PRE.SEG_END_DT
     do Format-DateTime($Seg-Bgn-Dt,$Seg-Bgn-Dt-DMY,{DEFDMY},'','')
     do Format-DateTime($Seg-End-Dt,$Seg-End-Dt-DMY,{DEFDMY},'','')
     LET $Begin-DAY       =  {PS-substr}($Seg-Bgn-Dt-DMY,1,2)
     LET $End-DAY         =  {PS-substr}($Seg-End-Dt-DMY,1,2)
     LET $MONTH           =  {PS-substr}($Seg-End-Dt-DMY,4,2)
     LET $YEAR            =  {PS-substr}($Seg-End-Dt-DMY,7,4)
     LET $LABOR_AGREEMENT =  rtrim(&JOB.LABOR_AGREEMENT,  ' ')
     LET $EMPL_CTG        =  rtrim(&JOB.EMPL_CTG,  ' ')
     LET $Matricula-Nbr   =  rtrim(&JOB.MATRICULA_NBR,  ' ')

     let #retro1          =  &PRE.RSLT_REV_NUM
     let #retro2          =  &PRE.RSLT_VER_NUM
     let $retro-sw = 'N'
     if #retro1 > 1 or #retro2 > 1
        let $Retro-SW = 'Y'
     end-if

     If $Prev-Emplid <> $Emplid
           Or #Prev-Empl-Rcd <> #Empl-Rcd
           Or $PrintOptn-Selected = '1' or $Retro-Type = '02'
           Or $retro-sw = 'N'
        DO PRINT-PAYSLIP
        Let #Header-Data-Row =  0
        Let #Body-Data-Row   =  0
        Let #Footer-Data-Row =  0
        Let $Prev-Emplid     =  $Emplid
        Let #Prev-Empl-Rcd   =  #Empl-Rcd
        DO GET-MONTH-NAME                 !Just to assign a value to $Seg-Bgn-Dt-Prt
        Do Get-Run-Type
        Let $Period-Bgn-Dt-Prt  =  $Period-Bgn-Dt
        Let $ePeriod-Bgn-Dt =  $Seg-Bgn-Dt
        Let #eBeginning-Page =  #eEnding-Page
        Let $eCal-ID = $Cal-ID
        Let $eGP-Paygroup = $GP-Paygroup
        Let $ePymt-Dt = &PRE.PYMT_DT
     End-If

  ! Force to print regular and extra in two separate blocks in the same page,
  ! in case of Separate Extra Pd = 'N'
     LET $Sep-Extra-Pd    =  'Y'

     do employee-HEADER-selection

     If $Extra-Period = 'Y'
        do Get-Extra-Period-Descr         !Fill variable $Extra-Pd-Descr-Prt
     Else
        DO GET-MONTH-NAME                 !Just to assing a value to $Seg-End-Dt
        Let $Period-End-Dt-Prt = $Period-End-Dt
     End-If
     Let $ePeriod-End-Dt = $Seg-End-Dt

     Evaluate $PrintOptn-Selected
       When = '1'                         !Current Period Only
         do Get-Line-Earning-Data
         do Get-Line-APORTACION-cantidad
         do Get-Line-irpf-Data
 !        do Get-Line-irpf-retro
         do Get-Line-deduction-Data
         When = '2'                         !Retro Periods Only
         do Get-Line-Earning-Retro
         do Get-Line-APORTACION-retro
         do Get-Line-irpf-retro
       When = '3'                         !Retro & Current Periods
         if $retro-sw = 'N'
           do Get-Line-Earning-Data
           do Get-Line-APORTACION-cantidad
           do Get-Line-irpf-Data
  !         do Get-Line-irpf-retro
           do Get-Line-deduction-Data
         else
           do Get-Line-Earning-Retro
           do Get-Line-APORTACION-retro
           do Get-Line-irpf-retro
         end-if
     End-Evaluate

     do Get-BASES
     If $Separate-Extra-Pd = 'N'
       LET $Sep-Extra-Pd = 'N'
       Do Process-Extra-Pd
     End-If

  End-If
  
FROM  PS_GPES_PSLIP_TMP PRE
     ,PS_GPES_PSLP_S_PYE P
     ,PS_JOB JOB

WHERE P.OPRID = $prcs_oprid
 AND  P.RUN_CNTL_ID = $prcs_run_cntl_id
 AND  P.CAL_RUN_ID = $Calendar-Selected
 AND  PRE.EMPLID = P.EMPLID
 AND  PRE.CAL_RUN_ID = P.CAL_RUN_ID
 AND  PRE.EMPL_RCD = P.EMPL_RCD
 AND  PRE.GP_PAYGROUP = P.GP_PAYGROUP
 AND  PRE.CAL_ID = P.CAL_ID
 AND  PRE.ORIG_CAL_RUN_ID = P.ORIG_CAL_RUN_ID
 AND  PRE.RSLT_SEG_NUM = P.RSLT_SEG_NUM
 AND  PRE.GPES_PAYSLIP_ID = &SEP_A.GPES_PAYSLIP_ID
 AND  JOB.EMPLID = PRE.EMPLID
 AND  JOB.EMPL_RCD = PRE.EMPL_RCD
 AND  JOB.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOB
        WHERE EMPLID = PRE.EMPLID
          AND EMPL_RCD = PRE.EMPL_RCD
          AND EFFDT <= PRE.SEG_END_DT)
 AND  JOB.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
        WHERE EMPLID = PRE.EMPLID
          AND EMPL_RCD = PRE.EMPL_RCD
          AND EFFDT = JOB.EFFDT)
[$whereclause]
[$OrderByclause]
end-select

DO PRINT-PAYSLIP

end-procedure


!************************************************************************
! Procedure Employee-HEADER-Selection
!************************************************************************
begin-procedure Employee-HEADER-Selection
#debug do Fin-Debug-Msg('Employee-HEADER-Selection')

let $sql-statement = 'GPESPYS1.SQR,Select,PS_PERSON'
begin-SELECT on-error=SQL-Error
ADDRESS.COUNTRY
PNM.NAME         &PDTA.NAME
  let $Empl-name = rtrim(&PDTA.NAME,  ' ')


FROM PS_PERSON_NAME PNM,  PS_PERSON_ADDRESS ADDRESS
WHERE PNM.EMPLID = $Emplid
 AND  ADDRESS.EMPLID = PNM.EMPLID 
End-SELECT

Do Get-Empl-Ctg-Descr
Do COMPANY-TBL
Do Get-Empl-Nid

Let #Header-Data-Row = #Header-Data-Row + 1
PUT $Cmpny-Descr    $Cmpny-DescrShort  $Empl-Name      $Cmpny-Address   $Empl-DNI-NIE
    $Matricula-Nbr  $Cmpny-FiscalID    $Empl-SSN       $Empl-Ctg-Descr
    $Cmpny-SSN      $Empl-SS-Grp       $RunFinalized   $Retro-SW        $Extra-Period
  INTO Header-Data(#Header-Data-Row)

#debugd show 'Company Name:       '  $Cmpny-Descr
#debugd show 'Employee Name:      '  $Empl-Name
#debugd show 'Company Address:    '  $Cmpny-Address
#debugd show 'Employee DNI/NIE:   '  $Empl-DNI-NIE
#debugd show 'Matricula Nbr:      '  $Matricula-Nbr
#debugd show 'Company Fiscal ID:  '  $Cmpny-FiscalID
#debugd show 'Employee SSN:       '  $Empl-SSN
#debugd show 'Employee Ctg. Desc: '  $Empl-Ctg-Descr
#debugd show 'Company SSN:        '  $Cmpny-SSN
#debugd show 'Employee Wrk.Grp.:  '  $Empl-SS-Grp
#debugd show 'Finalized ind.:     '  $RunFinalized
#debugd show 'Retro Ind.:         '  $Retro-SW
#debugd show 'Extra Period:       '  $Extra-Period

If #Header-Data-Row = {HEADER-DATA-SIZE}
    !ERROR
End-If

end-procedure


!************************************************************************
! Procedure Get-Line-Earning-Data
!************************************************************************
begin-procedure Get-Line-Earning-Data
#debug do Fin-Debug-Msg('Get-Line-Earning-Data')

#Debugd show 'Extra Period Ind.: ' $Extra-Period
#Debugd show 'Separate Payslip f/XtrPd: ' $Separate-Extra-Pd

Do Rst-Body-Vars

let $sql-statement = 'GPESPYS1.SQR,PSLIP_TMP,Select,PS_GPES_PSLIP_TMP2'
begin-SELECT on-error=SQL-Error
TMP2.EMPLID
TMP2.GPES_ELEMENT_ORD
TMP2.PIN_NUM
TMP2.DESCR
TMP2.CALC_RSLT_VAL
TMP2.RATE_RSLT_VAL
TMP2.UNIT_RSLT_VAL
TMP2.PIN_TYPE
TMP2.GPES_LINES_BEFORE
TMP2.GPES_LINES_AFTER
  Let $Pin-Type       =  rtrim(&TMP2.PIN_TYPE, '')
  Let $Pin-Num        =  &TMP2.PIN_NUM
  Let $Description    =  rtrim(&TMP2.DESCR, '')
  let $Units          =  &TMP2.UNIT_RSLT_VAL
  let $Rate           =  &TMP2.RATE_RSLT_VAL
  let #Amount         =  &TMP2.CALC_RSLT_VAL
  let #Lines-Before   =  &TMP2.GPES_LINES_BEFORE
  let #Lines-After    =  &TMP2.GPES_LINES_AFTER
  
  do store-body-data

FROM PS_GPES_PSLIP_TMP2 TMP2

WHERE TMP2.EMPLID = $Emplid
 AND TMP2.CAL_RUN_ID = $Cal-Run-ID
 AND TMP2.EMPL_RCD = #Empl-Rcd
 AND TMP2.GP_PAYGROUP = $GP-Paygroup
 AND TMP2.CAL_ID = $Cal-ID
 AND TMP2.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND TMP2.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND TMP2.PIN_TYPE = 'ER'
 AND TMP2.CALC_RSLT_VAL <> 0
ORDER BY EMPLID, GPES_ELEMENT_ORD
End-SELECT

end-procedure


!************************************************************************
! Procedure Get-Line-Earning-Retro
!************************************************************************
begin-procedure Get-Line-Earning-Retro
#debug do Fin-Debug-Msg('Get-Line-Earning-Retro')

Do Rst-Body-Vars

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GP_RSLT_DELTA'
begin-SELECT DISTINCT on-error=SQL-Error
RETRO_D.EMPLID
RETRO_D.GPES_ELEMENT_ORD
RETRO_D.PIN_NUM
RETRO_B.CALC_DELTA_VAL
RETRO_D.DESCR
RETRO_D.PIN_TYPE
RETRO_D.GPES_LINES_BEFORE
RETRO_D.GPES_LINES_AFTER
  Let $Pin-Type       =  rtrim(&RETRO_D.PIN_TYPE, '')
  Let $Pin-Num        =  &RETRO_D.PIN_NUM
  Let $Description    =  rtrim(&RETRO_D.DESCR, '')
  let #Amount         =  &RETRO_B.CALC_DELTA_VAL
  let #Lines-Before   =  &RETRO_D.GPES_LINES_BEFORE
  let #Lines-After    =  &RETRO_D.GPES_LINES_AFTER
  
  do store-body-data

FROM PS_GP_RSLT_DELTA RETRO_B
   , PS_GPES_PSLIP_TMP2 RETRO_D

WHERE RETRO_D.EMPLID = $Emplid
 AND  RETRO_D.CAL_RUN_ID = $Cal-Run-ID
 AND  RETRO_D.EMPL_RCD = #Empl-Rcd
 AND  RETRO_D.GP_PAYGROUP = $GP-Paygroup
 AND  RETRO_D.CAL_ID = $Cal-ID
 AND  RETRO_D.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  RETRO_D.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  RETRO_B.EMPLID = RETRO_D.EMPLID
 AND  RETRO_B.CAL_RUN_ID = RETRO_D.CAL_RUN_ID
 AND  RETRO_B.EMPL_RCD = RETRO_D.EMPL_RCD
 AND  RETRO_B.GP_PAYGROUP = RETRO_D.GP_PAYGROUP
 AND  RETRO_B.CAL_ID = RETRO_D.CAL_ID
 AND  RETRO_B.ORIG_CAL_RUN_ID = RETRO_D.ORIG_CAL_RUN_ID
 AND  RETRO_B.RSLT_SEG_NUM = RETRO_D.RSLT_SEG_NUM
 AND  RETRO_B.PIN_NUM = RETRO_D.PIN_NUM
 AND  RETRO_D.PIN_TYPE = 'ER'
ORDER BY RETRO_D.EMPLID, GPES_ELEMENT_ORD
End-SELECT

end-procedure


!************************************************************************
! Procedure Get-Line-Aportacion-Cantidad
!************************************************************************
begin-procedure Get-Line-Aportacion-Cantidad
#debug do Fin-Debug-Msg('Get-Line-Aportacion-Cantidad')

Do Rst-Body-Vars

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_PSLIP_TMP4'
begin-SELECT on-error=SQL-Error
AP.EMPLID
AP.GPES_ELEMENT_ORD
AP.PIN_NUM
AP.PIN_TYPE
AP.DESCR
AP.BASE_RSLT_VAL
AP.PCT_RSLT_VAL
AP.CALC_RSLT_VAL
AP.GPES_LINES_BEFORE
AP.GPES_LINES_AFTER
  Let $Pin-Type     =  rtrim(&AP.PIN_TYPE, '')
  Let $Pin-Num      =  &AP.PIN_NUM
  Let $Description  =  rtrim(&AP.DESCR, '')
  If not($Retro-SW = 'Y' and $Retro-Type = '01')
     Let $Base         =  &AP.BASE_RSLT_VAL
     Let $Percentage   =  &AP.PCT_RSLT_VAL
     Let $Percentage   =  $Percentage || '%'
  End-If
  let #Amount       =  &AP.CALC_RSLT_VAL
  let #Lines-Before =  &AP.GPES_LINES_BEFORE
  let #Lines-After  =  &AP.GPES_LINES_AFTER

  do store-body-data

FROM PS_GPES_PSLIP_TMP4 AP

WHERE AP.EMPLID = $Emplid
 AND  AP.CAL_RUN_ID = $Cal-Run-ID
 AND  AP.EMPL_RCD = #Empl-Rcd
 AND  AP.GP_PAYGROUP = $GP-Paygroup
 AND  AP.CAL_ID = $Cal-ID
 AND  AP.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  AP.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  AP.PIN_TYPE = 'AP'
 AND  AP.CALC_RSLT_VAL <> 0
ORDER BY AP.EMPLID, AP.GPES_ELEMENT_ORD
End-SELECT

End-procedure


!************************************************************************
! Procedure Get-Line-Aportacion-Retro
!************************************************************************
begin-procedure Get-Line-APORTACION-retro
#debug do Fin-Debug-Msg('Get-Line-APORTACION-retro')

Do Rst-Body-Vars

#debugd show 'Employee ID:         ' $Emplid
#debugd show 'Calendar Run ID:     ' $Cal-Run-ID
#debugd show 'Employee Record#:    ' #Empl-Rcd
#debugd show 'Paygroup:            ' $GP-Paygroup
#debugd show 'Calendar ID:         ' $Cal-ID
#debugd show 'Original Cal Run ID: ' $Orig-Cal-Run-ID
#debugd show 'Result Seg Num:      ' #Rslt-Seg-Num
let $sql-statement = 'GPESPYS1.SQR,GP_RSLT_RETRO,Select,PS_GP_RSLT_RETRO'
begin-SELECT on-error=SQL-Error
TMP4AP.EMPLID
TMP4AP.GPES_ELEMENT_ORD
TMP4AP.PIN_NUM
TMP4AP.PIN_TYPE
TMP4AP.DESCR
TMP4AP.GPES_LINES_BEFORE
TMP4AP.GPES_LINES_AFTER
RTO2A.CALC_DELTA_VAL
RTO2A.BASE_DELTA_VAL
RSLTEDAP.PCT_RSLT_VAL
  Let $Pin-Type     =  rtrim(&TMP4AP.PIN_TYPE, '')
  Let $Pin-Num      =  &TMP4AP.PIN_NUM
  Let $Description  =  rtrim(&TMP4AP.DESCR, '')
  If not($Retro-SW = 'Y' and $Retro-Type = '01')
     Let $Base         =  &RTO2A.BASE_DELTA_VAL
     Let $Percentage   =  &RSLTEDAP.PCT_RSLT_VAL
     Let $Percentage   =  $Percentage || '%'
  End-If
  let #Amount       =  &RTO2A.CALC_DELTA_VAL
  let #Lines-Before =  &TMP4AP.GPES_LINES_BEFORE
  let #Lines-After  =  &TMP4AP.GPES_LINES_AFTER

  do store-body-data

FROM PS_GP_RSLT_ERN_DED RSLTEDAP
   , PS_GP_RSLT_DELTA RTO2A
   , PS_GPES_PSLIP_TMP4 TMP4AP

WHERE TMP4AP.EMPLID = $Emplid
 AND  TMP4AP.CAL_RUN_ID = $Cal-Run-ID
 AND  TMP4AP.EMPL_RCD = #Empl-Rcd
 AND  TMP4AP.GP_PAYGROUP = $GP-Paygroup
 AND  TMP4AP.CAL_ID = $Cal-ID
 AND  TMP4AP.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  TMP4AP.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  RTO2A.EMPLID = TMP4AP.EMPLID
 AND  RTO2A.CAL_RUN_ID = TMP4AP.CAL_RUN_ID
 AND  RTO2A.EMPL_RCD = TMP4AP.EMPL_RCD
 AND  RTO2A.GP_PAYGROUP = TMP4AP.GP_PAYGROUP
 AND  RTO2A.CAL_ID = TMP4AP.CAL_ID
 AND  RTO2A.ORIG_CAL_RUN_ID = TMP4AP.ORIG_CAL_RUN_ID
 AND  RTO2A.RSLT_SEG_NUM = TMP4AP.RSLT_SEG_NUM
 AND  RTO2A.PIN_NUM = TMP4AP.PIN_NUM
 AND  TMP4AP.PIN_TYPE = 'AP'
 AND  RSLTEDAP.EMPLID = TMP4AP.EMPLID
 AND  RSLTEDAP.CAL_RUN_ID = TMP4AP.CAL_RUN_ID
 AND  RSLTEDAP.EMPL_RCD = TMP4AP.EMPL_RCD
 AND  RSLTEDAP.GP_PAYGROUP = TMP4AP.GP_PAYGROUP
 AND  RSLTEDAP.CAL_ID = TMP4AP.CAL_ID
 AND  RSLTEDAP.ORIG_CAL_RUN_ID = TMP4AP.ORIG_CAL_RUN_ID
 AND  RSLTEDAP.RSLT_SEG_NUM = TMP4AP.RSLT_SEG_NUM
 AND  RSLTEDAP.PIN_NUM = TMP4AP.PIN_NUM
ORDER BY TMP4AP.EMPLID, TMP4AP.GPES_ELEMENT_ORD
End-SELECT

end-procedure


!************************************************************************
! Procedure Get-Line-irpf-Data
!************************************************************************
begin-procedure Get-Line-irpf-Data
#debug do Fin-Debug-Msg('Get-Line-irpf-Data')

Do Rst-Body-Vars

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_PSLIP_TMP4'
begin-SELECT on-error=SQL-Error
IRPF.EMPLID
IRPF.GPES_ELEMENT_ORD
IRPF.PIN_NUM
IRPF.PIN_TYPE
IRPF.DESCR
IRPF.BASE_RSLT_VAL
IRPF.PCT_RSLT_VAL
IRPF.CALC_RSLT_VAL
IRPF.GPES_LINES_BEFORE
IRPF.GPES_LINES_AFTER
  Let $Pin-Type     =  rtrim(&IRPF.PIN_TYPE, '')
  Let $Pin-Num      =  &IRPF.PIN_NUM
  Let $Description  =  rtrim(&IRPF.DESCR, '')
  If not($Retro-SW = 'Y' and $Retro-Type = '01')
     Let $Base         =  &IRPF.BASE_RSLT_VAL
     Let $Percentage   =  &IRPF.PCT_RSLT_VAL
     Let $Percentage   =  $Percentage || '%'
  End-If
  let #Amount       =  &IRPF.CALC_RSLT_VAL
  let #Lines-Before =  &IRPF.GPES_LINES_BEFORE
  let #Lines-After  =  &IRPF.GPES_LINES_AFTER

  do store-body-data

FROM PS_GPES_PSLIP_TMP4 IRPF

WHERE IRPF.EMPLID = $Emplid
 AND  IRPF.CAL_RUN_ID = $Cal-Run-ID
 AND  IRPF.EMPL_RCD = #Empl-Rcd
 AND  IRPF.GP_PAYGROUP = $GP-Paygroup
 AND  IRPF.CAL_ID = $Cal-ID
 AND  IRPF.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  IRPF.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  IRPF.PIN_TYPE = 'IR'
 AND  IRPF.CALC_RSLT_VAL <> 0
ORDER BY IRPF.EMPLID, IRPF.GPES_ELEMENT_ORD
End-SELECT

end-procedure


!************************************************************************
! Procedure Get-Line-irpf-RETRO
!************************************************************************
begin-procedure Get-Line-irpf-RETRO
#debug do Fin-Debug-Msg('Get-Line-irpf-RETRO')

Do Rst-Body-Vars

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_PSLIP_TMP4'
begin-SELECT on-error=SQL-Error
TMP4IR.EMPLID
TMP4IR.GPES_ELEMENT_ORD
TMP4IR.PIN_NUM
TMP4IR.PIN_TYPE
TMP4IR.DESCR
TMP4IR.GPES_LINES_BEFORE
TMP4IR.GPES_LINES_AFTER
RTO3A.CALC_DELTA_VAL
  !#debugd show '*************************************************'
  ! #debugd show 'Delta 1: '  &RTO3A.CALC_DELTA_VAL
  ! RSLTEDIR.CALC_RSLT_VAL + RSLTEDIR.CALC_ADJ_VAL  &RSLTEDIR.CALC_RSLT_VAL
RTO3A.BASE_DELTA_VAL
  ! #debugd show 'Delta 2: '  &RTO3A.BASE_DELTA_VAL
  ! #debugd show '*************************************************'
  ! RSLTEDIR.BASE_RSLT_VAL + RSLTEDIR.BASE_ADJ_VAL  &RSLTEDIR.BASE_RSLT_VAL
  ! RSLTEDIR.PCT_RSLT_VAL
  Let $Pin-Type     =  rtrim(&TMP4IR.PIN_TYPE, '')
  Let $Pin-Num      =  &TMP4IR.PIN_NUM
  Let $Description  =  rtrim(&TMP4IR.DESCR, '')
  If not($Retro-SW = 'Y' and $Retro-Type = '01')
     Let $Base         =  &RTO3A.BASE_DELTA_VAL
  !   Let $Base         =  &RSLTEDIR.BASE_RSLT_VAL
  !   Let $Percentage   =  &RSLTEDIR.PCT_RSLT_VAL
  !   Let $Percentage   =  $Percentage || '%'
  End-If
  let #Amount       =  &RTO3A.CALC_DELTA_VAL
  ! let #Amount       =  &RSLTEDIR.CALC_RSLT_VAL
  let #Lines-Before =  &TMP4IR.GPES_LINES_BEFORE
  let #Lines-After  =  &TMP4IR.GPES_LINES_AFTER

  do store-body-data

FROM 
  ! PS_GP_RSLT_ERN_DED RSLTEDIR
     PS_GP_RSLT_DELTA  RTO3A
    , PS_GPES_PSLIP_TMP4  TMP4IR

WHERE TMP4IR.EMPLID = $Emplid
 AND  TMP4IR.CAL_RUN_ID = $Cal-Run-ID
 AND  TMP4IR.EMPL_RCD = #Empl-Rcd
 AND  TMP4IR.GP_PAYGROUP = $GP-Paygroup
 AND  TMP4IR.CAL_ID = $Cal-ID
 AND  TMP4IR.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  TMP4IR.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  TMP4IR.PIN_TYPE = 'IR'
 !  AND  TMP4IR.CALC_RSLT_VAL <> 0
 !  AND  RSLTEDIR.CALC_RSLT_VAL + RSLTEDIR.CALC_ADJ_VAL <> 0
 AND  RTO3A.EMPLID = TMP4IR.EMPLID
 AND  RTO3A.CAL_RUN_ID = TMP4IR.CAL_RUN_ID
 AND  RTO3A.EMPL_RCD = TMP4IR.EMPL_RCD
 AND  RTO3A.GP_PAYGROUP = TMP4IR.GP_PAYGROUP
 AND  RTO3A.CAL_ID = TMP4IR.CAL_ID
 AND  RTO3A.ORIG_CAL_RUN_ID = TMP4IR.ORIG_CAL_RUN_ID
 AND  RTO3A.RSLT_SEG_NUM = TMP4IR.RSLT_SEG_NUM
 AND  RTO3A.PIN_NUM = TMP4IR.PIN_NUM
 !  AND  RSLTEDIR.EMPLID = TMP4IR.EMPLID
 !  AND  RSLTEDIR.CAL_RUN_ID = TMP4IR.CAL_RUN_ID
 !  AND  RSLTEDIR.EMPL_RCD = TMP4IR.EMPL_RCD
 !  AND  RSLTEDIR.GP_PAYGROUP = TMP4IR.GP_PAYGROUP
 !  AND  RSLTEDIR.CAL_ID = TMP4IR.CAL_ID
 !  AND  RSLTEDIR.ORIG_CAL_RUN_ID = TMP4IR.ORIG_CAL_RUN_ID
 !  AND  RSLTEDIR.RSLT_SEG_NUM = TMP4IR.RSLT_SEG_NUM
 !  AND  RSLTEDIR.PIN_NUM = TMP4IR.PIN_NUM
END-SELECT

END-PROCEDURE


!************************************************************************
! Procedure Get-Line-Deduction-Data
!************************************************************************
begin-procedure Get-Line-Deduction-Data
#debug do Fin-Debug-Msg('Get-Line-Deduction-Data')

Do Rst-Body-Vars

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_PSLIP_TMP2'
begin-SELECT on-error=SQL-Error
TMP2B.EMPLID
TMP2B.GPES_ELEMENT_ORD
TMP2B.PIN_NUM
TMP2B.PIN_TYPE
TMP2B.DESCR
TMP2B.UNIT_RSLT_VAL
TMP2B.RATE_RSLT_VAL
TMP2B.PCT_RSLT_VAL
TMP2B.CALC_RSLT_VAL
TMP2B.GPES_LINES_BEFORE
TMP2B.GPES_LINES_AFTER
  Let $Pin-Type     =  rtrim(&TMP2B.PIN_TYPE, ' ')
  Let $Pin-Num      =  &TMP2B.PIN_NUM
  Let $Description  =  rtrim(&TMP2B.DESCR, ' ')
  Let $Units        =  &TMP2B.UNIT_RSLT_VAL
  Let $Rate         =  &TMP2B.RATE_RSLT_VAL
  Let $Percentage   =  &TMP2B.PCT_RSLT_VAL
  let #Amount       =  &TMP2B.CALC_RSLT_VAL
  let #Lines-Before =  &TMP2B.GPES_LINES_BEFORE
  let #Lines-After  =  &TMP2B.GPES_LINES_AFTER

  do store-body-data

FROM PS_GPES_PSLIP_TMP2 TMP2B

WHERE TMP2B.EMPLID = $Emplid
 AND  TMP2B.CAL_RUN_ID = $Cal-Run-ID
 AND  TMP2B.EMPL_RCD = #Empl-Rcd
 AND  TMP2B.GP_PAYGROUP = $GP-Paygroup
 AND  TMP2B.CAL_ID = $Cal-ID
 AND  TMP2B.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  TMP2B.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  TMP2B.PIN_TYPE = 'DD'
 AND  TMP2B.CALC_RSLT_VAL <> 0
ORDER BY TMP2B.EMPLID,  TMP2B.GPES_ELEMENT_ORD
End-SELECT

end-procedure


!************************************************************************
! Procedure Get-Bases
!************************************************************************
begin-procedure Get-BASES
#debug do Fin-Debug-Msg('Get-BASES')

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GPES_PSLIP_TMP2'
begin-SELECT on-error=SQL-Error
BASES.EMPLID
BASES.GPES_ELEMENT_ORD
BASES.PIN_NUM
BASES.DESCR
BASES.CAL_RUN_ID
BASES.ORIG_CAL_RUN_ID
BASES.CALC_RSLT_VAL
BASES.GPES_LINES_BEFORE
BASES.GPES_LINES_AFTER
  Let $Description  =  rtrim(&BASES.DESCR, ' ')
  Let $Pin-Num      =  &BASES.PIN_NUM
  let #Amount       =  &BASES.CALC_RSLT_VAL
  let #Lines-Before =  &BASES.GPES_LINES_BEFORE
  let #Lines-After  =  &BASES.GPES_LINES_AFTER

  If $Retro-SW = 'Y' and &BASES.CAL_RUN_ID <> &BASES.ORIG_CAL_RUN_ID
     #debugd show 'Employee:    ' &BASES.EMPLID
     #debugd show 'Base Amount: ' #Amount
     Do Get-BASES-Retro
     Let #Amount = #Amount - #Amount-Rto
  End-if

  Do Store-Footer-Data

FROM PS_GPES_PSLIP_TMP2 BASES

WHERE BASES.EMPLID = $Emplid
 AND  BASES.CAL_RUN_ID = $Cal-Run-ID
 AND  BASES.EMPL_RCD = #Empl-Rcd
 AND  BASES.GP_PAYGROUP = $GP-Paygroup
 AND  BASES.CAL_ID = $Cal-ID
 AND  BASES.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  BASES.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  BASES.PIN_TYPE = 'AC'
 AND  BASES.CALC_RSLT_VAL <> 0
ORDER BY BASES.EMPLID,  BASES.GPES_ELEMENT_ORD
End-SELECT

END-PROCEDURE


!************************************************************************
! Procedure Get-BASES-Retro
!************************************************************************
begin-procedure Get-BASES-Retro
#debug do Fin-Debug-Msg('Get-BASES-Retro')

Let #Amount-Rto = 0

let $sql-statement = 'GPESPYS1.SQR,Get-Bases-Retro,Select,PS_GP_RSLT_ACUM'
begin-SELECT on-error=SQL-Error
BASES_RETRO.CAL_RUN_ID
BASES_RETRO.CAL_ID
BASES_RETRO.CALC_RSLT_VAL
  let #Amount-Rto   =  #Amount-Rto + &BASES_RETRO.CALC_RSLT_VAL
  
  #debugd show 'Calendar Group: ' &BASES_RETRO.CAL_RUN_ID
  #debugd show 'Calendar ID: '    &BASES_RETRO.CAL_ID
  #debugd show 'Base Delta: '    &BASES_RETRO.CALC_RSLT_VAL

FROM PS_GP_RSLT_ACUM BASES_RETRO

WHERE BASES_RETRO.EMPLID = $Emplid
 AND  BASES_RETRO.CAL_RUN_ID = &BASES.ORIG_CAL_RUN_ID
 AND  BASES_RETRO.EMPL_RCD = #Empl-Rcd
 AND  BASES_RETRO.GP_PAYGROUP = $GP-Paygroup
 AND  BASES_RETRO.CAL_ID = $Cal-ID
 AND  BASES_RETRO.ORIG_CAL_RUN_ID = &BASES.ORIG_CAL_RUN_ID
 AND  BASES_RETRO.PIN_NUM = &BASES.PIN_NUM
End-SELECT

END-PROCEDURE


!************************************************************************
! Procedure Process-Extra-Pd
!************************************************************************
begin-procedure Process-Extra-Pd
#debug do Fin-Debug-Msg('Process-Extra-Pd')

let $sql-statement = 'GPESPYS1.SQR,Process-Extra-Pd,Select,PS_GPES_PSLIP_TMP'
begin-SELECT on-error=SQL-Error
XTRPD.EMPLID
XTRPD.EMPL_RCD
XTRPD.CAL_RUN_ID
XTRPD.GP_PAYGROUP
XTRPD.CAL_ID
XTRPD.ORIG_CAL_RUN_ID
XTRPD.RSLT_SEG_NUM
  LET $Emplid          =  rtrim(&XTRPD.EMPLID,  ' ')
  LET #Empl-Rcd        =  &XTRPD.EMPL_RCD
  LET $Cal-Run-ID      =  rtrim(&XTRPD.CAL_RUN_ID, ' ')
  LET $GP-Paygroup     =  rtrim(&XTRPD.GP_PAYGROUP, ' ')
  LET $Cal-ID          =  rtrim(&XTRPD.CAL_ID, ' ')
  LET $Orig-Cal-Run-ID =  rtrim(&XTRPD.ORIG_CAL_RUN_ID, ' ')
  LET #Rslt-Seg-Num    =  &XTRPD.RSLT_SEG_NUM

  Let $Extra-Period = 'Y'
  do Get-Extra-Period-Descr            !Fill variable $Extra-Pd-Descr-Prt
  
  Evaluate $PrintOptn-Selected
    When = '1'                         !Current Period Only
      do Get-Line-Earning-Data
 !      do Get-Line-irpf-Data
      do Get-Line-Irpf-Retro
      do Get-Line-deduction-Data
    When = '2'                         !Retro Periods Only
      do Get-Line-Earning-Retro
 !      do Get-Line-irpf-retro
    When = '3'                         !Retro & Current Periods
      if $retro-sw = 'N'
        do Get-Line-Earning-Data
 !        do Get-Line-irpf-Data
        do Get-Line-Irpf-Retro
        do Get-Line-deduction-Data
      else
        do Get-Line-Earning-Retro
 !        do Get-Line-irpf-retro
      end-if
  End-Evaluate

FROM PS_GPES_PSLIP_TMP XTRPD

WHERE XTRPD.EMPLID = $Emplid
 AND  XTRPD.CAL_RUN_ID = $Cal-Run-ID
 AND  XTRPD.EMPL_RCD = #Empl-Rcd
 AND  XTRPD.GP_PAYGROUP = $GP-Paygroup
 AND  XTRPD.ORIG_CAL_RUN_ID = $Orig-Cal-Run-ID
 AND  XTRPD.RSLT_SEG_NUM = #Rslt-Seg-Num
 AND  XTRPD.GPES_EXTRA_PERIOD = 'Y'
End-SELECT

end-procedure


!************************************************************************
! Procedure Store-Body-Data
!************************************************************************
begin-procedure Store-Body-Data
#debug do Fin-Debug-Msg('Store-Body-Data')

  If $Retro-SW = 'Y' and $Retro-Type = '01'
     Let $Found = 'N'
     Let #I = 1
     While #I <= #Body-Data-Row and $Found = 'N'
       GET $Pin-Type-Tmp     $Pin-Num-Tmp   $Description-Tmp
           $Units-Tmp        $Rate-Tmp      $Base-Tmp
           $Percentage-Tmp   #Amount-Tmp    #Lines-Before-Tmp
           #Lines-After-Tmp  $Retro-SW-Tmp  $Sep-Extra-Pd-Tmp
         FROM Body-Data(#I)
 
       If $Pin-Num-Tmp = $Pin-Num and $Sep-Extra-Pd = $Sep-Extra-Pd-Tmp
          Let #Units      =  #Units + #Units-Tmp
          Let #Rate       =  #Rate + #Rate-Tmp
          Let #Base       =  #Base + #Base-Tmp
          Let #Percentage =  #Percentage + #Percentage-Tmp
          Let #Amount     =  #Amount + #Amount-Tmp
          Let $Found = 'Y'
       Else
          Let #I = #I + 1
       End-If
     End-While
     If $Found <> 'Y'
       Let #Body-Data-Row = #I
     End-If
  Else
     Let #Body-Data-Row = #Body-Data-Row + 1
     Let #I = #Body-Data-Row
  End-If

  PUT $Pin-Type       $Pin-Num       $Description   $Units
      $Rate           $Base          $Percentage    #Amount
      #Lines-Before   #Lines-After   $Retro-SW      $Sep-Extra-Pd
    INTO Body-Data(#I)

  #debugd show 'Array row:     ' #I
  #debugd show 'Pin Type:      ' $Pin-Type
  #debugd show 'Sep. Extra Pd: ' $Sep-Extra-Pd
  #debugd show 'Pin Num:       ' $Pin-Num
  #debugd show 'Description:   ' $Description
  #debugd show 'Units:         ' $Units
  #debugd show 'Rate:          ' $Rate
  #debugd show 'Base:          ' $Base
  #debugd show 'Percentage:    ' $Percentage
  #debugd show 'Amount:        ' #Amount
  #debugd show 'Lines Before:  ' #Lines-Before
  #debugd show 'Lines After:   ' #Lines-After
  #debugd show 'Retro Ind.:    ' $Retro-SW
      
  If #Body-Data-Row = {BODY-DATA-SIZE}
     !ERROR
  End-If

end-procedure


!************************************************************************
! Procedure Store-Footer-Data
!************************************************************************
begin-procedure Store-Footer-Data
#debug do Fin-Debug-Msg('Store-Footer-Data')

  If $Retro-SW = 'Y' and $Retro-Type = '01'
     Let $Found = 'N'
     Let #I = 1
     #debugd show 'Last Footer Row: ' #Footer-Data-Row
     While #I <= #Footer-Data-Row and $Found = 'N'
       #debugd show 'Index: ' #I
       GET $Pin-Num-Tmp        $Description-Tmp   #Amount-Tmp
           #Lines-Before-Tmp   #Lines-After-Tmp   $Retro-SW-Tmp
         FROM Footer-Data(#I)
 
       #debugd show 'Stored pin nun:   ' $Pin-Num-Tmp
       #debugd show 'Searched pin num: ' $Pin-Num
       If $Pin-Num-Tmp = $Pin-Num
          Let #Amount =  #Amount + #Amount-Tmp
          Let $Found = 'Y'
       Else
          Let #I = #I + 1       
       End-If
     End-While
     If $Found <> 'Y'
        Let #Footer-Data-Row = #I
     End-If
  Else
     Let #Footer-Data-Row = #Footer-Data-Row + 1
     Let #I = #Footer-Data-Row
  End-If

  PUT $Pin-Num        $Description   #Amount
      #Lines-Before      #Lines-After   $Retro-SW
    INTO Footer-Data(#I)

  #debugd show 'Array row:    ' #I
  #debugd show 'Pin Num:      ' $Pin-Num
  #debugd show 'Description:  ' $Description
  #debugd show 'Amount:       ' #Amount
  #debugd show 'Lines Before: ' #Lines-Before
  #debugd show 'Lines After:  ' #Lines-After
  #debugd show 'Retro Ind.:   ' $Retro-SW
  #debugd show 'Retro Type:   ' $Retro-Type

  If #Footer-Data-Row = {FOOTER-DATA-SIZE}
     !ERROR
  End-If

end-procedure


!**********************************************************************!
! PROCEDURE:     GET-EMPL-CTG-DESCR                                    !
! DESCRIPTION:   GET EMPLOYEE CATEGORIZATION DESCR                     !
!**********************************************************************!
BEGIN-PROCEDURE Get-Empl-Ctg-Descr
#debug do Fin-Debug-Msg('Get-Empl-Ctg-Descr')

Let $Empl-Ctg-Descr   =  ''
Let $Empl-SS-Grp-Cd   =  ''
Let $emp-ss-grp-descr =  ''
Let $Empl-SS-Grp      =  ''
  
BEGIN-SELECT on-error=SQL-Error
CTG.EFFDT
CTG.DESCR
CTG.SCHEME_ID_ESP
CTG.SOC_SEC_WRK_GRP_CD
SOCSEC.EFFDT
SOCSEC.DESCR50
  let $EMPL_CTG_L1-DESCR = rtrim(&CTG.DESCR, ' ')
  Do Get_Related_EMPL_CTG_L1(&CTG.EFFDT,&JOB.EMPL_CTG,&JOB.LABOR_AGREEMENT,&JOB.SETID_LBR_AGRMNT)
  let $Empl-Ctg-Descr = $EMPL_CTG_L1-DESCR

  let $RGM_BSE_ESP_TBL-DESCR50 = rtrim(&SOCSEC.DESCR50, ' ')
  Do Get_Related_RGM_BSE_ESP_TBL(&JOB.CURRENCY_CD,&SOCSEC.EFFDT,&CTG.SCHEME_ID_ESP,&CTG.SOC_SEC_WRK_GRP_CD)
  let $emp-ss-grp-descr = $RGM_BSE_ESP_TBL-DESCR50

  LET $Empl-SS-Grp-Cd   =  &CTG.SOC_SEC_WRK_GRP_CD
  let $Empl-SS-Grp      =  $Empl-SS-Grp-Cd || ' ' || $emp-ss-grp-descr

FROM PS_EMPL_CTG_L1 CTG
    ,PS_RGM_BSE_ESP_TBL SOCSEC

WHERE CTG.SETID = &JOB.SETID_LBR_AGRMNT
 AND  CTG.LABOR_AGREEMENT = &JOB.LABOR_AGREEMENT
 AND  CTG.EMPL_CTG = &JOB.EMPL_CTG
 AND  CTG.EFFDT = (SELECT MAX(EFFDT) FROM PS_EMPL_CTG_L1
                   WHERE SETID = CTG.SETID
                    AND  LABOR_AGREEMENT = CTG.LABOR_AGREEMENT
                    AND  EMPL_CTG = CTG.EMPL_CTG
                    AND  EFFDT <= &PRE.SEG_END_DT)
 AND  SOCSEC.SCHEME_ID_ESP = CTG.SCHEME_ID_ESP
 AND  SOCSEC.CURRENCY_CD = &JOB.CURRENCY_CD
 AND  SOCSEC.EFFDT = (SELECT MAX(EFFDT) FROM PS_RGM_BSE_ESP_TBL
                      WHERE SCHEME_ID_ESP = SOCSEC.SCHEME_ID_ESP
                       AND  CURRENCY_CD = SOCSEC.CURRENCY_CD
                       AND  EFFDT <= &PRE.SEG_END_DT)
 AND  SOCSEC.SOC_SEC_WRK_GRP_CD = CTG.SOC_SEC_WRK_GRP_CD
END-SELECT

!  #debugd show 'Action: ' &JOB.ACTION
!  #debugd show 'Action reason: ' &JOB.ACTION_REASON
  #debugd show 'Labor Agreement Setid: ' &JOB.SETID_LBR_AGRMNT
  #debugd show 'Labor Agreement:       ' &JOB.LABOR_AGREEMENT
  #debugd show 'Empl Categorization:   ' &JOB.EMPL_CTG
  #debugd show 'Currency:              ' &JOB.CURRENCY_CD
  #debugd show 'As Of Date:            ' &PRE.SEG_END_DT
  #debugd show 'Social Security Group: ' $Empl-SS-Grp-Cd
  #debugd show 'Descr:                 ' $Empl-Ctg-Descr
  #debugd show '                       ' $emp-ss-grp-descr

END-PROCEDURE


!**********************************************************************!
! PROCEDURE:     GET-EMPL-NID                                          !
! DESCRIPTION:   GET EMPLOYEE NATIONAL ID                              !
!**********************************************************************!
BEGIN-PROCEDURE  Get-Empl-Nid
#debug do Fin-Debug-Msg('Get-Empl-Nid')

let $Empl-DNI      =  ''
let $Empl-SSN      =  ''
let $Empl-NIE      =  ''
let $empl-DNI-NIE  =  ''

let $National_type =  'DNI'
let $sql-statement = 'GPESPYS1.SQR,Get-Empl-Nid,Select,PS_PERS_NID (DNI)'
BEGIN-SELECT on-error=SQL-Error
NID1.NATIONAL_ID
  let $Empl-Dni-NIE = rtrim(&NID1.NATIONAL_ID, ' ')

FROM PS_PERS_NID NID1

WHERE NID1.EMPLID = $Emplid
 AND  NID1.COUNTRY = &ADDRESS.COUNTRY
 AND  NID1.NATIONAL_ID_TYPE = $National_type
END-SELECT


let $National_type = 'SSN'
let $sql-statement = 'GPESPYS1.SQR,Get-Empl-Nid,Select,PS_PERS_NID (SSN)'
BEGIN-SELECT on-error=SQL-Error
NID2.NATIONAL_ID
  let $Empl-SSN = rtrim(&NID2.NATIONAL_ID, ' ')

FROM PS_PERS_NID NID2

WHERE NID2.EMPLID = $Emplid
 AND  NID2.COUNTRY = &ADDRESS.COUNTRY
 AND  NID2.NATIONAL_ID_TYPE = $National_type
END-SELECT

If $Empl-Dni-NIE = ''
   let $National_type = 'NIE'
   let $sql-statement = 'GPESPYS1.SQR,Get-Empl-Nid,Select,PS_PERS_NID (NIE)'
BEGIN-SELECT on-error=SQL-Error
NID3.NATIONAL_ID
  let $Empl-Dni-NIE = rtrim(&NID3.NATIONAL_ID, ' ')

FROM PS_PERS_NID NID3

WHERE NID3.EMPLID = $Emplid
 AND  NID3.COUNTRY = &ADDRESS.COUNTRY
 AND  NID3.NATIONAL_ID_TYPE = $National_type
END-SELECT
End-If

#debugd show 'Employee id: ' $Emplid
#debugd show 'Country:     ' &ADDRESS.COUNTRY
#debugd show 'DNI:         ' $Empl-DNI
#debugd show 'SSN:         ' $Empl-SSN

END-PROCEDURE


!************************************************************************
! Procedure COMPANY-TBL
!************************************************************************
begin-procedure COMPANY-TBL
#debug do Fin-Debug-Msg('COMPANY-TBL')

let $Cmpny-Descr      =  ''
let $Cmpny-DescrShort =  ''
let $Cmpny-Address1   =  ''
let $Cmpny-Address2   =  ''
let $Cmpny-City       =  ''
let $Cmpny-Postal     =  ''
let $Cmpny-County     =  ''
let $Cmpny-Ssn        =  ''
let $Cmpny-State      =  ''

let $sql-statement = 'GPESPYS1.SQR,COMPANY_TBL  STATE_NAME_TBL,Select,PS_COMPANY_TBL'
begin-SELECT on-error=SQL-Error
CMPNY.EFFDT
CMPNY.DESCR
CMPNY.DESCRSHORT
CMPNY.ADDR_FIELD1
CMPNY.ADDRESS1
CMPNY.ADDR_FIELD2
CMPNY.POSTAL
CMPNY.FISCAL_ID_CD
ST.DESCR
CMPNY.CITY
  let $COMPANY_TBL-DESCR =  rtrim(&CMPNY.DESCR, ' ')
  let $COMPANY_TBL-DESCRSHORT =  rtrim(&CMPNY.DESCRSHORT, ' ')
  Do Get_Related_COMPANY_TBL(&JOB.COMPANY,&CMPNY.EFFDT)
  let $Cmpny-Descr      =  $COMPANY_TBL-DESCR
  let $Cmpny-DescrShort =  $COMPANY_TBL-DESCRSHORT

  let $Cmpny-Field1   =  rtrim(&CMPNY.ADDR_FIELD1,' ')
  let $Cmpny-Address1 =  rtrim(&CMPNY.ADDRESS1,' ')
  let $Cmpny-Field2   =  rtrim(&CMPNY.ADDR_FIELD2,' ')
  let $Cmpny-Postal   =  rtrim(&CMPNY.POSTAL,' ')
  let $Cmpny-State    =  rtrim(&ST.DESCR,' ')
  let $Cmpny-FiscalId =  rtrim(&CMPNY.FISCAL_ID_CD,' ')

  let $Cmpny-Address  =  $Cmpny-Field1 || '/' || $Cmpny-Address1 || ',' || $Cmpny-Field2
  let $Cmpny_City     =  rtrim(&CMPNY.CITY,' ')

FROM PS_COMPANY_TBL CMPNY, PS_STATE_NAMES_TBL ST

WHERE CMPNY.COMPANY = &JOB.COMPANY
  AND CMPNY.EFFDT = (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL CMPNY2
                      WHERE CMPNY.COMPANY = CMPNY2.COMPANY
                       AND  CMPNY2.EFFDT <= &JOB.EFFDT)
  AND CMPNY.EFF_STATUS <> 'I'
  AND ST.COUNTRY = CMPNY.COUNTRY
  AND ST.STATE = CMPNY.STATE
end-SELECT


let $sql-statement = 'GPESPYS1.SQR,COMPANY-TBL,Select,'
begin-SELECT on-error=SQL-Error
D.SSN_EMPLOYER
  let $Cmpny-Ssn = rtrim(&D.SSN_EMPLOYER,' ')

FROM PS_JOB_JR D
WHERE D.EMPLID = $Emplid
AND D.EMPL_RCD = #Empl-Rcd
AND D.EFFDT = &JOB.EFFDT
end-SELECT

  #debugd show 'Company Name:       '  $Cmpny-Descr
  #debugd show 'Company Short Name: '  $Cmpny-DescrShort
  #debugd show 'Company Address:    '  $Cmpny-Address
  #debugd show 'City:               '  $Cmpny-City
  #debugd show 'Postal:             '  $Cmpny-Postal
  #debugd show 'County:             '  $Cmpny-County
  #debugd show 'Company SSN:        '  $Cmpny-Ssn
  #debugd show 'Company Fiscal ID:  '  $Cmpny-FiscalID

end-procedure


!************************************************************************************
! Procedure Get-Extra-Pd-Descr
!************************************************************************************
BEGIN-PROCEDURE Get-Extra-Period-Descr
#debug do Fin-Debug-Msg('Get-Extra-Period-Descr')

Let $Extra-Pd-Descr-Prt =  ''
Let #Pymnt-Pd           =  $Month

let $sql-statement = 'GPESPYS1.SQR,PS_GPES_LBR_XTR_PD,Select,'
begin-SELECT on-error=SQL-Error
X.EFFDT
X.DESCR
  let $GPES_LBR_XTR_PD-DESCR = rtrim(&X.DESCR, ' ')
  Do Get_Related_GPES_LBR_XTR_PD(&X.EFFDT, #Pymnt-Pd, &JOB.LABOR_AGREEMENT, &JOB.SETID_LBR_AGRMNT)
  let $Extra-Pd-Descr-Prt = $GPES_LBR_XTR_PD-DESCR

FROM PS_GPES_LBR_XTR_PD X
WHERE X.SETID = &JOB.SETID_LBR_AGRMNT
 AND  X.LABOR_AGREEMENT = &JOB.LABOR_AGREEMENT
 AND  X.GPES_PYMNT_PD = #Pymnt-Pd
 AND  X.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPES_LBR_XTR_PD
                 WHERE SETID = X.SETID
                  AND  LABOR_AGREEMENT = X.LABOR_AGREEMENT
                  AND  GPES_PYMNT_PD = X.GPES_PYMNT_PD
                  AND  EFFDT <= &PRE.SEG_END_DT)
end-SELECT

#debugd show 'Labor Agreement Setid:    ' &JOB.SETID_LBR_AGRMNT
#debugd show 'Labor Agreement:          ' &JOB.LABOR_AGREEMENT
#debugd show 'Labor Agreement Effdt:    ' &X.EFFDT
#debugd show 'Extra Period Payment Pd.: ' #Pymnt-Pd
#debugd show 'Extra Period Description: ' $Extra-Pd-Descr-Prt

end-procedure


!************************************************************************************
! Procedure Get-month-name
!************************************************************************************
BEGIN-PROCEDURE Get-month-name
#debug do Fin-Debug-Msg('Get-month-name')
  evaluate $Month
    when = '01'
      move 'Enero'    to $MonthName
    when = '02'
      move 'Febrero'  to $MonthName
    when = '03'
      move 'Marzo'    to $MonthName
    when = '04'
      move 'Abril'    to $MonthName
    when = '05'
      move 'Mayo'     to $MonthName
    when = '06'
      move 'Junio'    to $MonthName
    when = '07'
      move 'Julio'    to $MonthName
    when = '08'
      move 'Agosto'    to $MonthName
    when = '09'
      move 'Septiembre'    to $MonthName
    when = '10'
      move 'Octubre'    to $MonthName
    when = '11'
      move 'Noviembre'    to $MonthName
    when = '12'
      move 'Diciembre'    to $MonthName
  end-evaluate

  let $Period-Bgn-Dt =  $BEGIN-DAY || ' de ' || $Monthname || ' de ' || $year
  let $Period-End-Dt =  $end-day   || ' de ' || $Monthname || ' de ' || $year

end-procedure


!************************************************************************************
!Procudure Get-Run-Type
!************************************************************************************
BEGIN-PROCEDURE Get-run-type
#debug do Fin-Debug-Msg('Get-run-type')

Let $descripcion-runtype = ''
Let $runtype = 'MENSUAL'

let $sql-statement = 'GPESPYS1.SQR,Select,PS_GP_RUN_TYPE'
begin-SELECT on-error=SQL-Error
BRT.RUN_TYPE
BRT.DESCR

    let $GP_RUN_TYPE-DESCR = rtrim(&BRT.DESCR, ' ')
    let $runtype = rtrim(&BRT.RUN_TYPE, ' ')
    Do Get_Related_GP_RUN_TYPE(&BRT.RUN_TYPE)
    let $descripcion-runtype = $GP_RUN_TYPE-DESCR

FROM  PS_GP_CALENDAR ART, PS_GP_RUN_TYPE BRT
WHERE ART.GP_PAYGROUP = $GP-Paygroup
 AND  ART.CAL_ID = $Cal-ID
 AND  BRT.RUN_TYPE = ART.RUN_TYPE
end-select

#debugd show 'Paygroup:             ' $GP-Paygroup
#debugd show 'Calendar ID:          ' $Cal-ID
#debugd show 'Run Type Description: ' $descripcion-runtype

end-procedure


!************************************************************************************
!BEGIN-PROCEDURE Get-PAYROLL-LAYOUT
!************************************************************************************
!
!BEGIN-SELECT
!A.EMPLID
!      Let $Emplid = rtrim(&A.EMPLID,'')
!A.GPES_PAYSLIP_ID
!      Let $LAYOUT= rtrim(&A.GPES_PAYSLIP_ID,'')
!FROM PS_GPES_PSLIP_TMP A, PS_GPES_PAYSLIP_HD B
!WHERE A.EFFDT_USED =   (SELECT MAX(A_ED.EFFDT_USED) FROM PS_GPES_PSLIP_TMP A_ED
!      WHERE A.GPES_FIELDNAME1 = A_ED.GPES_FIELDNAME1
!      AND A.GPES_FIELDNAME2 = A_ED.GPES_FIELDNAME2
!      AND A.GPES_FIELDNAME3 = A_ED.GPES_FIELDNAME3
!      AND A.GPES_FIELDNAME4 = A_ED.GPES_FIELDNAME4
!      AND A.GPES_FIELDNAME5 = A_ED.GPES_FIELDNAME5
!      AND A.EMPLID = A_ED.EMPLID
!      AND A.EMPL_RCD = A_ED.EMPL_RCD
!      AND A.GP_PAYGROUP = A_ED.GP_PAYGROUP
!      AND A.CAL_ID = A_ED.CAL_ID
!      AND A.CAL_RUN_ID = A_ED.CAL_RUN_ID
!      AND A.RSLT_SEG_NUM = A_ED.RSLT_SEG_NUM
!      AND A.GPES_PAYSLIP_ID = A_ED.GPES_PAYSLIP_ID
!      AND A_ED.EFFDT_USED <= A.PYMT_DT)
!AND A.GPES_PAYSLIP_ID = B.GPES_PAYSLIP_ID
!AND B.EFFDT =          (SELECT MAX(B_ED.EFFDT) FROM PS_GPES_PAYSLIP_HD B_ED
!            WHERE B.GPES_PAYSLIP_ID = B_ED.GPES_PAYSLIP_ID
!      AND B_ED.EFFDT <= A.EFFDT_USED)
!end-select
!end-procedure
!


!************************************************************************
! Procedure Rst-Body-Vars
!************************************************************************
begin-procedure Rst-Body-Vars
#debug do Fin-Debug-Msg('Rst-Body-Vars')

Let $Pin-Type       =  ''
Let $Pin-Num        =  0
Let $Description    =  ''
let $Units          =  ''
let $Rate           =  ''
let $Base           =  ''
let $Percentage     =  ''
let $Amount         =  ''
let #Lines-Before   =  0
let #Lines-After    =  0

end-procedure


!************************************************************************
! Procedure PRINT-PAYSLIP
!************************************************************************
begin-procedure PRINT-PAYSLIP
#debug do Fin-Debug-Msg('PRINT-PAYSLIP')

  #define START_HEADER_ROW         03
  #define START_BODY_ROW           11
  #define START_FOOTER_ROW         65
  
  #define REPORT_BOX_HEIGTH        82
  #define REPORT_BOX_WIDTH        127
  #define HEADER_BOX_HEIGTH         7
  #define HEADER_L_BOX_WIDTH       60
  #define HEADER_R_BOX_WIDTH       63
  #define BODY_BOX_HEIGTH          62
  #define BODY_BOX_WIDTH          124
  #define FOOTER_BOX_HEIGTH         9
  #define FOOTER_BOX_WIDTH        124

  #define E_D_DESCRIPTION          10
  #define E_D_CANTIDAD             48
  #define PORCENTAJE               54
  #define E_D_BASE                 60
  #define E_D_TOTAL                90
  #define APR_DESCRIPCION          10
  #define APORTACION_PCT           60
  #define APORTACION_TOTAL         90
  #define TOTAL_APORTACION         90
  #define TOTAL_DEVENGOS           110


  If #Header-Data-Row <> 0 
 !    Let $Extra-Period-Prt =  'N'
    Do Print-Header
    let #Linea        =  {START_BODY_ROW}
    Let $Sep-Extra-Pd =  'Y'
    Do Print-Body-Header
    Do Print-Body
    If $Separate-Extra-Pd =  'N'
      Let $Sep-Extra-Pd     =  'N'
      Let $Extra-Period-Prt =  'Y'
      Let #linea            =  #linea + 2
      do Body-Control
      Do Print-Body-Header
      Do Print-Body
    End-if
    Do print-signoff-area
    Do print-footer
    do GP-ePay-Guide !if ePay installed write Guide data for each payslip
    Let #eEnding-Page = #eEnding-Page + 1
    new-page  
  end-if

end-procedure


!************************************************************************
! Procedure Body-Control
!************************************************************************
begin-procedure Body-Control
#debug do Fin-Debug-Msg('Body-Control')

  if #linea >= {START_FOOTER_ROW} - 3
     Print 'Continua en la siguiente pagina ...' (71,80)
     Do print-footer
     let #eEnding-Page = #eEnding-Page + 1
     new-page
     do Print-Header
     let #linea = {START_BODY_ROW}
     do Print-Body-Header
  end-if

end-procedure


!************************************************************************
! Procedure Print-Header
!************************************************************************
begin-procedure Print-Header
#debug do Fin-Debug-Msg('Print-Header')

  GET $Cmpny-Descr-Prt   $Cmpny-DescrShort-Prt  $Empl-Name-Prt       $Cmpny-Address-Prt
      $Empl-DNI-NIE-Prt  $Matricula-Nbr-Prt     $Cmpny-FiscalID-Prt
      $Empl-SSN-Prt      $Empl-Ctg-Descr-Prt    $Cmpny-SSN-Prt
      $Empl-SS-Grp-Prt   $RunFinalized-Prt      $Retro-SW-Prt        $Extra-Period-Hdr
    FROM Header-Data(#Header-Data-Row)
  
  Let $Extra-Period-Prt = $Extra-Period-Hdr

  GRAPHIC ( 1, 1,{REPORT_BOX_WIDTH})    box {REPORT_BOX_HEIGTH}        !Page frame
  GRAPHIC ( 1, 2,{REPORT_BOX_WIDTH})    box {REPORT_BOX_HEIGTH}        !Page frame
  GRAPHIC (+1, 3,{HEADER_L_BOX_WIDTH})  box {HEADER_BOX_HEIGTH}  5  8  !Header left frame
  GRAPHIC (  ,64,{HEADER_R_BOX_WIDTH})  box {HEADER_BOX_HEIGTH}  5  8  !Header right frame
  GRAPHIC (10, 3,{BODY_BOX_WIDTH})      box {BODY_BOX_HEIGTH}    5     !Body frame
  GRAPHIC (73, 3,{FOOTER_BOX_WIDTH})    box {FOOTER_BOX_HEIGTH}  5  5  !Footer frame
  
  !---------------------------------------------
  ! PRINTS HEADER LEFT FRAME
  !---------------------------------------------
  print 'Empresa:    '                       ({START_HEADER_ROW},  5)
  print $Cmpny-Descr-Prt                     (  , 16)  bold
  print 'Domicilio: '                        (+1,  5)
  print $Cmpny-Address-Prt                   (  , 16)
  print 'C.I.F.: '                           (+1,  5)
  print $Cmpny-FiscalID-Prt                  (  , 16)
  print 'Cdigo de cuenta de cotizacin'     (+1,  5)
  print 'Seguridad Social:'                  (+1,  5)
  print $Cmpny-SSN-Prt                       (  , 25)

  !---------------------------------------------
  ! PRINTS HEADER RIGHT FRAME
  !---------------------------------------------
  print 'Trabajador: '                       ({START_HEADER_ROW}, 65)
  print $Empl-Name-Prt                       (  , 78)  bold
  print 'N.I.F.: '                           (+1, 65)
  print $Empl-DNI-NIE-Prt                    (  , 75)
  print 'N.Matrcula: '                      (  , 90)
  print $Matricula-Nbr-Prt                   (  ,105)  EDIT 00000
  print 'Nmero de Afiliacin a la '         (+1, 65)
  print 'Seguridad Social:'                  (  , +1)
  print $Empl-SSN-Prt                        (  ,110)  EDIT 'XX/XXXXXXXX/XX'
  print 'Categora o grupo profesional: '    (+1, 65)
  print $Empl-Ctg-Descr-Prt                  (  , 97)
  print 'Grupo de Cotizacin: '              (+1, 65)
  print $Empl-SS-Grp-Prt                     (  , 86)

IF $Pye-Calc-Stat < '70'  
    ALTER-PRINTER point-size=90
    print 'Borrador'                         (43, 15)
    foreground = ('gray')
    ALTER-PRINTER  point-size=7.2
END-IF

END-PROCEDURE


!************************************************************************
! Procedure Print-Body-Header
!************************************************************************
begin-procedure Print-Body-Header
#debug do Fin-Debug-Msg('Print-Body-Header')

  !---------------------------------------------
  ! PRINTS BODY HEADER
  !---------------------------------------------
  #debugd show 'Print extra period header ind: ' $Extra-Period-Hdr
  #debugd show 'Print Option:                  ' $PrintOptn-Selected
  #debugd show 'Retro Type:                    ' $Retro-Type
  #debugd show 'Retro Flag:                    ' $Retro-SW-Prt
  print 'Periodo de liquidacin:  '          (#linea,  5)
  If $Extra-Period-Prt = 'Y'
    If ($PrintOptn-Selected = '1' or $PrintOptn-Selected = '2'
           or ($PrintOptn-Selected = '3' and $Retro-Type = '02'))
       or $Retro-SW-Prt = 'N'
      print $Extra-Pd-Descr-Prt             (  , 32)
    Else
      print $Period-Bgn-Dt-Prt              (  , 32)
      print ' al '                          (  , +1)
      print $Period-End-Dt-Prt              (  , +1)
     End-If
  Else
     print $Period-Bgn-Dt-Prt               (  , 32)
     print ' al '                           (  , +1)
     print $Period-End-Dt-Prt               (  , +1)

     print ' Tipo de Paga: '                (  , +1)
     print $descripcion-runtype             (  , +1)
  End-If


  !---------------------------------------------
  ! PRINTS EARNINGS DETAIL HEADERS
  !---------------------------------------------
  Let #linea = #linea + 2
  print 'Descripcin '                       (#linea, {E_D_DESCRIPTION})
  print 'Unidades '                          (  , 45)
  print 'Bases'                              (  , 65)
  print 'Total'                              (  , 92)
  Let #Linea = #Linea + 2

END-PROCEDURE


!************************************************************************
! Procedure Print-Body
!************************************************************************
begin-procedure Print-Body
#debug do Fin-Debug-Msg('Print-Body')
 
  let #total-devengo    =  0
  let #total-aportacion =  0
  let #total-deduccion  =  0
  
  !---------------------------------------------
  ! PRINTS EARNINGS DETAIL LINES
  !---------------------------------------------

  Evaluate $PrintOptn-Selected
    When = '1'
      print 'DEVENGOS '                      (#linea,  8)  BOLD
      Break
    When = '2'
      print 'ATRASOS DE DEVENGOS '           (#linea,  8)  BOLD
      Break
    When = '3'
      evaluate $Retro-SW-Prt
        when = 'Y'
          print 'ATRASOS DE DEVENGOS '       (#linea,  8)  BOLD
          Break
        when = 'N'
          print 'DEVENGOS '                  (#linea,  8)  BOLD
          Break
      end-evaluate
  end-evaluate
  Let #linea = #linea + 2
  do Body-Control

  Let #I = 1
  While #I <= #Body-Data-Row
    GET $Pin-Type-Prt      $Pin-Num-Prt       $Description-Prt   $Units-Prt
        $Rate-Prt             $Base-Prt          $Percentage-Prt     #Amount-Prt
        #Lines-Before-Prt  #Lines-After-Prt   $Retro-SW-Prt      $Sep-Extra-Pd-Prt
      FROM Body-Data(#I)

      If $Pin-Type-Prt = 'ER' and $Sep-Extra-Pd = $Sep-Extra-Pd-Prt
      let #Linea = #Linea + #Lines-Before-Prt
      do Body-Control
      LET #total-devengo  =  #total-devengo + #Amount-Prt
      Let $Amount-Prt     =  #Amount-Prt
      print $Description-Prt           (#linea,{E_D_DESCRIPTION})
      print $Units-Prt                 (      ,{E_D_CANTIDAD})     edit B99.99
      print $Rate-Prt                  (      ,{E_D_BASE})         edit B,999,999.99
      print $Amount-Prt                (      ,{E_D_TOTAL})        edit B,999,999.99
      let #linea          =  #linea + #Lines-After-Prt
    End-If

    Let #I = #I + 1
  End-While


  !---------------------------------------------
  ! PRINTS EARNINGS SUMMARY LINE
  !---------------------------------------------
  let #linea = #linea + 1
  do Body-Control
  PRINT 'TOTAL DEVENGADO '             (#linea,30)  BOLD
  LET $total-devengo = #total-devengo
  PRINT $total-devengo                 (      ,{TOTAL_DEVENGOS})  BOLD  EDIT B9,999,999.99
  let #linea = #linea + 1


  !---------------------------------------------
  ! PRINTS SOCIAL SECURITY CONTRIBUTIONS HEADERS
  !---------------------------------------------
  Let #linea = #Linea + 1
  do Body-Control
  Evaluate $PrintOptn-Selected
    When = '1'
      print 'DEDUCCIONES '                 (#linea,8)  BOLD
    When = '2'
      print 'DEDUCCIONES DE ATRASOS '      (#linea,8)  BOLD
    When = '3'
      If $Retro-SW-Prt = 'Y'
         print 'DEDUCCIONES DE ATRASOS '   (#linea,8)  BOLD
      Else
         print 'DEDUCCIONES '              (#linea,8)  BOLD
      end-if
  End-Evaluate

  If $Extra-Period-Prt = 'N'
     LET #linea =  #linea + 2
     do Body-Control
     PRINT 'Aportacin del trabajador a las cotizaciones '      (#linea, 10)
     print 'de Seg. Social y conceptos de recaudacin conjunta' (      , +1)
     Let #linea = #Linea + 1


  !---------------------------------------------
  ! PRINTS SOCIAL SECURITY CONTRIBUTIONS LINES
  !---------------------------------------------
     Let #I = 1
     While #I <= #Body-Data-Row
       GET $Pin-Type-Prt      $Pin-Num-Prt       $Description-Prt   $Units-Prt
           $Rate-Prt            $Base-Prt             $Percentage-Prt    #Amount-Prt
              #Lines-Before-Prt  #Lines-After-Prt   $Retro-SW-Prt      $Sep-Extra-Pd-Prt
         FROM Body-Data(#I)
 
       If $Pin-Type-Prt = 'AP' and $Sep-Extra-Pd = $Sep-Extra-Pd-Prt
         let #Linea            =  #Linea + #Lines-Before-Prt
         do Body-Control
           LET #total-aportacion =  #total-aportacion + #Amount-Prt
         Let $Amount-Prt       =  #Amount-Prt

         PRINT $Description-Prt           (#linea,{E_D_DESCRIPTION})
         PRINT $Base-Prt                  (      ,{E_D_base})      edit B,999,999.99
         PRINT $Percentage-Prt            (      ,{E_D_cantidad})  edit B99.99
         print $Amount-Prt                (      ,{E_D_TOTAL})     edit B,999,999.99

         let #linea          =  #linea + #Lines-After-Prt
       End-If

       Let #I = #I + 1
     End-While


  !---------------------------------------------
  ! PRINTS SOCIAL SECURITY CONTRIBUTIONS SUMMARY
  !---------------------------------------------
     let #linea = #linea + 1
     do Body-Control
     PRINT 'TOTAL APORTADO'               (#linea,30)   BOLD
     Let $TOTAL-aportacion = #TOTAL-aportacion
     PRINT $TOTAL-aportacion              (      ,{TOTAL_DEVENGOS})  BOLD  EDIT B9,999,999.99
  End-If
  

  !---------------------------------------------
  ! PRINTS TAX WITHHOLDING (I.R.P.F.) HEADER
  !---------------------------------------------
  Let #linea = #Linea + 2
  do Body-Control
  PRINT 'Impuesto sobre la renta de las personas fisicas'      (#linea, 10)
  Let #linea = #Linea + 1


  !---------------------------------------------
  ! PRINTS TAX WITHHOLDING (I.R.P.F.) DETAIL
  !---------------------------------------------
  Let #I = 1
  While #I <= #Body-Data-Row
    GET $Pin-Type-Prt      $Pin-Num-Prt       $Description-Prt   $Units-Prt
        $Rate-Prt          $Base-Prt          $Percentage-Prt    #Amount-Prt
        #Lines-Before-Prt  #Lines-After-Prt   $Retro-SW-Prt      $Sep-Extra-Pd-Prt
      FROM Body-Data(#I)
 
    If $Pin-Type-Prt = 'IR' and $Sep-Extra-Pd = $Sep-Extra-Pd-Prt
      let #Linea      =  #Linea + #Lines-Before-Prt
      do Body-Control
      let #total-deduccion = #total-deduccion + #Amount-Prt
      Let $Amount-Prt =  #Amount-Prt

      PRINT $Description-Prt           (#linea,{E_D_DESCRIPTION})
      PRINT $Base-Prt                  (      ,{e_d_base})        EDIT B,999,999.99
      PRINT $Percentage-Prt            (      ,{e_d_cantidad})    EDIT B99.99
      PRINT $Amount-Prt                (      ,{e_d_TOTAL})       EDIT B,999,999.99

      let #linea          =  #linea + #Lines-After-Prt
    End-If

    Let #I = #I + 1
  End-While


  !---------------------------------------------
  ! PRINTS OTHER DEDUCTIONS HEADER
  !---------------------------------------------
  Let #linea = #Linea + 1
  do Body-Control
  Let #linea-prev = #linea


  !---------------------------------------------
  ! PRINTS OTHER DEDUCTIONS DETAIL LINES
  !---------------------------------------------
  Let #I = 1
  While #I <= #Body-Data-Row
    GET $Pin-Type-Prt      $Pin-Num-Prt       $Description-Prt   $Units-Prt
        $Rate-Prt          $Base-Prt          $Percentage-Prt    #Amount-Prt
        #Lines-Before-Prt  #Lines-After-Prt   $Retro-SW-Prt      $Sep-Extra-Pd-Prt
      FROM Body-Data(#I)
 
    If $Pin-Type-Prt = 'DD' and $Sep-Extra-Pd = $Sep-Extra-Pd-Prt
       If #linea-prev = #linea
         PRINT 'Otras Deducciones'         (#linea, 10)
         Let #Linea = #Linea + 1
      end-if
      let #Linea      =  #Linea + #Lines-Before-Prt
      do Body-Control
      let #total-deduccion = #total-deduccion + #Amount-Prt
      Let $Amount-Prt =  #Amount-Prt

      print $Description-Prt           (#linea,{E_D_DESCRIPTION})
      PRINT $Units-Prt                 (      ,{E_D_CANTIDAD})    edit B99.99
      PRINT $Rate-Prt                  (      ,{E_D_BASE})        edit B,999,999.99
      print $Amount-Prt                (      ,{E_D_TOTAL})       edit B,999,999.99

      let #linea      =  #linea + #Lines-After-Prt
    End-If

    Let #I = #I + 1
  End-While


  !---------------------------------------------
  ! PRINTS DEDUCTIONS SUMMARY
  !---------------------------------------------
  Let #linea = #Linea + 1
  do Body-Control
  PRINT 'TOTAL A DEDUCIR '             (#linea,30)  BOLD
  LET $total-deduccion = #total-deduccion
  PRINT $total-deduccion               (      ,{TOTAL_DEVENGOS})  BOLD  EDIT B9,999,999.99
  Let #linea = #Linea + 1


  !---------------------------------------------
  ! PRINTS NET TO PAY AMOUNT
  !---------------------------------------------
  LET #net-to-pay      =  #total-devengo - (#total-deduccion + #total-aportacion)
  LET $net-to-pay      =  #net-to-pay
  let $liquido-pesetas =  #net-to-pay * 166.386

  Let #linea = #Linea + 1
  do Body-Control
  PRINT 'LIQUIDO TOTAL A PERCIBIR '    (#Linea,30) BOLD
  PRINT #net-to-pay                    (      ,{TOTAL_DEVENGOS})  BOLD  EDIT B9,999,999.99
  Let #enet-to-pay = #net-to-pay
  Let #linea = #Linea + 1

END-PROCEDURE


!************************************************************************
! Procedure print-signoff-area
!************************************************************************
begin-procedure print-signoff-area
#debug do Fin-Debug-Msg('print-signoff-area')

  !---------------------------------------------
  ! PRINTS SIGNOFF AREA & DATE
  !---------------------------------------------

  let $linea-fecha = $Cmpny_City || ', ' || $end-day || ' de ' || $Monthname || ' de ' || $year

  let #linea = {START_FOOTER_ROW}
  print 'Firma y sello de la empresa'           (#linea, 10)
  print-image logo                              (+1, 15)
  print $linea-fecha                            (  , 90)
  print 'Recib'                                (+1,100)

END-PROCEDURE


!************************************************************************
! Procedure print-footer
!************************************************************************
begin-procedure print-footer
#debug do Fin-Debug-Msg('print-footer')
  !---------------------------------------------
  ! PRINTS SOCIAL SECURITY CONTRIBUTION BASES
  !---------------------------------------------
  let #linea = {START_FOOTER_ROW} + 8
  ALTER-PRINTER point-size=5.4
  print 'DETERMINACION DE LAS BASES DE COTIZACION A LA SEGURIDAD SOCIAL Y CONCEPTOS DE RECAUDACION CONJUNTA Y DE LA BASE SUJETA A RETENCION DEL I.R.P.F.'        (#linea,  5)  bold
  ALTER-PRINTER point-size=7.2
  let #linea = #linea + 1

  !---------------------------------------------
  ! PRINTS ACCUMULATORS FOOTER LINES
  !---------------------------------------------
  Let #J = 1
  While #J <= #Footer-Data-Row and #J <= {FOOTER_BOX_HEIGTH} - 2
    GET $Pin-Num-Prt-FT       $Description-Prt-FT   #Amount-Prt-FT
        #Lines-Before-Prt-FT  #Lines-After-Prt-FT   $Retro-SW-Prt-FT
      FROM Footer-Data(#J)
 
    let #Linea         =  #Linea + #Lines-Before-Prt-FT
    Let $Amount-Prt-FT =  #Amount-Prt-FT
    print $Description-Prt-FT         (#linea,{E_D_DESCRIPTION})
    print $Amount-Prt-FT              (      ,{E_D_TOTAL})       edit B,999,999.99
    let #linea      =  #linea + #Lines-After-Prt-FT

    Let #J = #J + 1
  End-While

END-PROCEDURE


!--------------------------------------------------------------------!
! Debug Msg                                                          !
!--------------------------------------------------------------------!
begin-procedure Fin-Debug-Msg($procedure_name)
   display ' '
   display '----------------------------------'
   display $procedure_name
   #debugt date-time () {Native-DateTime} &SysDateTime
   #debugt move &SysDateTime to $SysDateTime
   #debugt show 'TIMING, ' $procedure_name ', ' $SysDateTime
   display ' '
end-procedure ! Fin-Debug-Msg


!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Init                                             *
! Initialize variables that well use for ePay processing              *
!***********************************************************************
begin-procedure GP-ePay-Init
#debug do Fin-Debug-Msg('GP-ePay-Init')

Do Check-ePay-installed ($ePay_Installed)

 If $ePay_Installed = 'Y'
   Move 'GPESPYS1' to $ReportID    
     
   let #eV4 =  To_number($prcs_process_instance)

   !* have the Output Working Directory from the Process parameters table
   do Get-Output-Wrk-Directory(#eV4, $PRCSOUTPUTDIR, $PRCSNAME)

   !* have the URL id from the Payslip option table
   do Get-ePay-URLid ('ESP', $eV18)

   !* Global ePay variable settings used in GP-ePay procedures in this SQR
   let $eV1 = $prcs_oprid
   let $eV2 = $prcs_run_cntl_id
   let $eV3 = $ReportID          !used for proc name instaed of prcsname from output wrk dir
      
   ! Open the file for writing epay control data
   ! Let $GP_PSLP_CTLFILE   = $eV3 || '.txt'
   ! Let $FILELAYOUT = 'GP_SS_PSLP_TMP'
   ! Do Open-ePay-Guide-DataFile($PRCSOUTPUTDIR,$GP_PSLP_CTLFILE)

   ! when we do not pass a control file
   !Let $GP_PSLP_CTLFILE = ' '
   !Let $FILELAYOUT = ' '

 End-If

end-procedure !GP-ePay-Init


!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Guide                                            *
! Insert one row per payslip into the temporary guide table for ePay   *
!***********************************************************************

begin-procedure GP-ePay-Guide
#debug do Fin-Debug-Msg('GP-ePay-Guide')

 If $ePay_Installed = 'Y'

   let $eV5  =  rtrim($Prev-Emplid, ' ')
   let $eV5  =  ltrim($eV5, ' ')
   let $eV6  =  rtrim($Calendar-Selected,' ')   
   let $eV6  =  ltrim($eV6,' ')
   let $eV7  =  'GPESP'
   String $eCal-ID $eGP-Paygroup #Empl-Rcd #Rslt-Seg-Num by '_' into $ePayslip-ID
   let $eV8  =  rtrim($ePayslip-ID, ' ')                                   ! gp epay payslip id
   let $eV9  =  $ePymt-Dt
   let $eV10 =  $ePeriod-End-Dt
   let $eV11 =  $ePeriod-Bgn-Dt
   let #eV12 =  #enet-to-pay                                               ! net pay
   
   let $ePayslip-Descr = rtrim($Cmpny-DescrShort-Prt, ' ') || '-' || rtrim($Cal-Run-Descr, ' ')
   let $ePayslip-Descr = Substr($ePayslip-Descr, 1, 30)
   let $eV13 = $ePayslip-Descr                                             ! payslip description
   if $eV13 = ''
     let $eV13 = 'No DESCRIPTION'
   end-if 
   let $eV14 = rtrim($runtype, ' ')
   let $eV15 = 'ORIG'                                                      ! payslip status ORIGINAL
   let $eV16 = $eV5 || '_' || $eV6 || '_' ||$eV7 || '_' ||$eV8 || '.pdf'   ! sysfilename of the payslip pdf
   let $eV17 = $eV16                                                       !userfilename  - what the payee sees filename as
   let #eV19 = #eBeginning-Page                                            !begin page number of payslip in output report
   let #eV20 = #eEnding-Page                                               !end page number of payslip in output report

   ! ($eV1,$eV2,$eV3,#eV4,$eV5,$eV6 ,$eV7,$eV8,$eV9,$eV10,$eV11,#eV12,$eV13,$eV14,$eV15,$eV16,$eV17,$eV18,#eV19,#eV20)
   ! ($XV1,$XV2,$XV3,#XV4,$XV5,$XV65,$XV6,$XV7,$XV8,$XV9 ,$XV10,#XV11,$XV12,$XV13,$XV14,$XV15,$XV16,$XV17,#XV18,#XV19)
   #DEBUG1 LET $WKW1 = EDIT(length($ev1),'999')
   #DEBUG1 String $ev1 '- long' $WKW1 '/ 30' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV1/$XV1: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev2),'999')
   #DEBUG1 String $ev2 '- long' $WKW1 '/ 30)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV2/$XV2: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev3),'999')
   #DEBUG1 String $ev3 '- long' $WKW1 '/ 12)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV3/$XV3: ' $wkw

   #DEBUG1 String #ev4 by '' Into $WKW1
   #DEBUG1 LET $WKW2 = EDIT(length($WKW1),'999')
   #DEBUG1 String $WKW1 '- long' $WKW2 '/ 10)' By ' ' Into $wkw
   #DEBUG1 SHOW '#EV4/#XV4: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev5),'999')
   #DEBUG1 String $ev5 '- long' $WKW1 '/ 11)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV5/$XV5: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev6),'999')
   #DEBUG1 String $ev6 '- long' $WKW1 '/ 18)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV6/$XV65: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev7),'999')
   #DEBUG1 String $ev7 '- long' $WKW1 '/ 6)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV7/$XV6: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev8),'999')
   #DEBUG1 String $ev8 '- long' $WKW1 '/ 40)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV8/$XV7: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev9),'999')
   #DEBUG1 String $ev9 '- long' $WKW1 '/ 10)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV9/$XV8: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev10),'999')
   #DEBUG1 String $ev10 '- long' $WKW1 '/ 10)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV10/$XV9: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev11),'999')
   #DEBUG1 String $ev11 '- long' $WKW1 '/ 10)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV11/$XV10: ' $wkw

   #DEBUG1 String #ev12 by '' Into $WKW1
   #DEBUG1 LET $WKW2 = EDIT(length($WKW1),'999')
   #DEBUG1 String $WKW1 '- long' $WKW2 '/ 11; 8.2)' By ' ' Into $wkw
   #DEBUG1 SHOW '#EV12/#XV11: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev13),'999')
   #DEBUG1 String $ev13 '- long' $WKW1 '/ 30)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV13/$XV12: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev14),'999')
   #DEBUG1 String $ev14 '- long' $WKW1 '/ 10)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV14/$XV13: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev15),'999')
   #DEBUG1 String $ev15 '- long' $WKW1 '/ 4)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV15/$XV14: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev16),'999')
   #DEBUG1 String $ev16 '- long' $WKW1 '/ 128)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV16/$XV15: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev17),'999')
   #DEBUG1 String $ev17 '- long' $WKW1 '/ 64)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV17/$XV16: ' $wkw

   #DEBUG1 LET $WKW1 = EDIT(length($ev18),'999')
   #DEBUG1 String $ev18 '- long' $WKW1 '/ 30)' By ' ' Into $wkw
   #DEBUG1 SHOW '$EV18/$XV17: ' $wkw

   #DEBUG1 String #ev19 by '' Into $WKW1
   #DEBUG1 LET $WKW2 = EDIT(length($WKW1),'999')
   #DEBUG1 String $WKW1 '- long' $WKW2 '/ 8)' By ' ' Into $wkw
   #DEBUG1 SHOW '#EV19/#XV18: ' $wkw

   #DEBUG1 String #ev20 by '' Into $WKW1
   #DEBUG1 LET $WKW2 = EDIT(length($WKW1),'999')
   #DEBUG1 String $WKW1 '- long' $WKW2 '/ 8)' By ' ' Into $wkw
   #DEBUG1 SHOW '#EV20/#XV19: ' $wkw


   !Input:OPRID, RUN_CNTL_ID, PROCNAME, DATAINST, EMPLID, CAL_RUN_ID, SRCPRODUCT, 
   !GP_PSLP_ID,PYMT_DT,PRD_END_DT,PRD_BGN_DT,#NET_PAY,Payslip DESCR,RUN_TYPE, 
   !PSLP_STATUS,ATTACHSYSFILENAME, ATTACHUSERFILE,FILEURLID, BGNPGNBR,ENDPGNBR
   do Insert-ePay-Guide-Data($eV1,$eV2,$eV3,#eV4,$eV5,$eV6,$eV7,$eV8,$eV9,$eV10,$eV11,#eV12,$eV13,$eV14,$eV15,$eV16,$eV17,$eV18,#eV19,#eV20)
 !  do Write-ePay-Guide-Data($eV1,$eV2,$eV3,#eV4,$eV5,$eV6,$eV7,$eV8,$eV9,$eV10,$eV11,#eV12,$eV13,$eV14,$eV15,$eV16,$eV17,$eV18,#eV19,#eV20)
 End-If

end-procedure ! GP-ePay-Guide


!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Control                                          *
! Insert one row per report into the epay run control table            *
!***********************************************************************
begin-procedure GP-ePay-Control
#debug do Fin-Debug-Msg('GP-ePay-Control')

 If $ePay_Installed = 'Y'

   let $rptid = lower($ReportID)
   let $eCV7 = $rptid || '_' || $prcs_process_instance || '.PDF'
   let $eCV8  = rtrim($PRCSOUTPUTDIR, ' ')

   ! Input:OPRID,RUN_CNTL_ID,PROCNAME,SPLIT_IND,ATCH_IND,CTLFILE,SOURCEFILE, 
   ! SOURCELOC,WORKINGLOC,FILELAYOUT,DATAINST,FILEURLID,CLEANUP
 
   do Insert-ePay-RunControl($eV1,$eV2,$eV3,'Y', 'Y', ' ', $eCV7,$eCV8,' ',' ',#eV4,$eV18,'Y')

 End-If

end-procedure !GP-ePay-Control


!***********************************************************************
! Get-CAL-RUN-DESCR                                                         *
!***********************************************************************
begin-procedure Get-CAL-RUN-DESCR
#debug do Fin-Debug-Msg('Get-CAL-RUN-DESCR')

let $Cal-Run-Descr =  ''

let $sql-statement = 'GPESPYS1.sqr,Select , Get-CAL-RUN-DESCR'
BEGIN-SELECT ON-ERROR=SQL-Error
A.DESCR               &Cal-Run-Descr
  let $Cal-Run-Descr = rtrim(&Cal-Run-Descr, ' ')
  !If $PSOptions_Language_Cd <> $curr_language_cd
  !  Do Get-CAL-RUN-DESCR-LANG
  !End-if
  
FROM   PS_GP_CAL_RUN  A
WHERE   A.CAL_RUN_ID       = $Calendar-Selected
END-SELECT
end-procedure


!************************************************************************
! Procedure Get-CAL-RUN-DESCR-LANG
!************************************************************************
!BEGIN-PROCEDURE Get-CAL-RUN-DESCR-LANG
!#debug do Fin-Debug-Msg('Get-CAL-RUN-DESCR-LANG')
!
!let $sql-statement = 'GPESPYS1.sqr,Select , Get-CAL-RUN-DESCR-LANG'
!BEGIN-SELECT on-error=SQL-Error
!CALRUNLNG.DESCR
!
!  LET $Cal-Run-Descr = rtrim(&CALRUNLNG.DESCR, ' ')
!
!FROM PS_GP_CAL_RUN_LANG CALRUNLNG
!  WHERE CALRUNLNG.CAL_RUN_ID = $Calendar-Selected
!   AND  CALRUNLNG.LANGUAGE_CD = $curr_language_cd
!END-SELECT
!
!END-PROCEDURE
!
!



!***************************************************************
!Include SQCs
!***************************************************************
#Include 'sqlerr.sqc'
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
!#Include 'datemath.sqc'  !Routines for date calculation
#Include 'gpsspslp.sqc'

