!***********************************************************************
!  GPUSBK03.SQR - GPUS Garnishment Transmittal Report                  !
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/21:01:41:12                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'   !Set environment
#include 'setup32.sqc'  !page layout and printer definition
#include 'number.sqc'   !Routines to format numbers
#include 'datetime.sqc' !Routines for date and time formatting
#include 'prcsapi.sqc'  !Replaces stdapi.sqc

#define ARRAY_SIZE 20000              ! approximate size of the array
#define COLR 169

!#define DEBUGAPP001
!**********************************************************************
!**********************************************************************
!       Program
!**********************************************************************
!**********************************************************************
Begin-Program
  DO INIT-DATETIME
  DO INIT-NUMBER
  DO INIT-REPORT
  DO GET-PARAMETERS
  DO GET-RECIPIENTS
  DO CLEANUP-TABLE
  DO STDAPI-TERM
End-program

!**********************************************************************
!**********************************************************************
!       Init-Report
!**********************************************************************
!**********************************************************************
begin-procedure Init-Report

  MOVE 'GPUSBK03' to $ReportID
  MOVE 'Garnishment Transmittal Report' to $ReportTitle
  MOVE 'PeopleSoft' to $PeopleSoft

  DO STDAPI-INIT

  IF $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
  END-IF

  IF $PRCS_PROCESS_INSTANCE = ''
  ELSE
     ! Get the Process Instance of AE process GPUS_CTX_EFT
     LET #AE_process_instance = #prcs_process_instance - 1
  END-IF


#define Col1   1
#define Col2   14
#define Col3   27
#define Col4   38
#define Col5   76
#define Col6   102
#define Col7   115
#define Col8   128
#define Col9   142
#define Col10  154

LET $Curr_Language_Cd = 'ENG'
DO Init_Report_Translation ($ReportID, $curr_language_cd)

DO Get_Field_Information ($ReportID, 'PAY_ENTITY'   , $Pay_Entity_Title, #DW)
DO Get_Field_Information ($ReportID, 'PYMT_DT'      , $PaymentDt_Title , #DW)
DO Get_Field_Information ($ReportID, 'RECIPIENT'    , $Recipient_Title , #DW)
DO Get_Field_Information ($ReportID, 'SRC_BANK'     , $Src_Bank_Title  , #DW)
DO Get_Field_Information ($ReportID, 'EMPLID'       , $EMPLID_Title    , #DW)
DO Get_Field_Information ($ReportID, 'EMPL_RCD'     , $EMPLRCD_Title   , #DW)
DO Get_Field_Information ($ReportID, 'NAME'         , $NAME_Title      , #DW)
DO Get_Field_Information ($ReportID, 'GP_AMT'       , $AMT_Title       , #DW)
DO Get_Field_Information ($ReportID, 'TOTAL_AMOUNT' , $Total_Amt_Title , #DW)
DO Get_Field_Information ($ReportID, 'RUN_DATE'     , $RunDate_Title   , #DW)
DO Get_Field_Information ($ReportID, 'RUN_TIME'     , $RunTime_Title   , #DW)
DO Get_Field_Information ($ReportID, 'PAGE_NO'      , $PageNo_Title    , #DW)
DO Get_Field_Information ($ReportID, 'REPORT_ID'    , $ReportID_Title  , #DW)
    
    
DO print-heading

end-procedure !Init-Report

!**********************************************************************
!**********************************************************************
!       Create-Arrays
!**********************************************************************
!**********************************************************************
begin-setup
   declare-variable
      date $d
   end-declare
   create-array
    name=recipient_info
    size={ARRAY_SIZE}
    field=recipient_id:char
    field=recipient_nm:char
    field=src_bank_id:char
    field=pay_entity:char
    field=src_bank_nm:char
    field=emplid:char
    field=empl_rcd:char
    field=name:char
    field=amount:number
end-setup

!**********************************************************************
!**********************************************************************
!       Heading of the Report
!**********************************************************************
!**********************************************************************
begin-procedure print-heading

   PRINT $PeopleSoft     () center
   PRINT $ReportTitle    (+1,1) center
   
   PRINT $ReportID_Title (+2,5)
   PRINT ':'             (0,14)
   PRINT $ReportID       (0,20)
   
   PRINT $PageNo_Title   (0,153)
   PRINT ': '            (0,161)
   page-number ()
   
   !PRINT 'Run Date: '   (+1,153)
   PRINT $RunDate_Title  (+1,153)
   PRINT ': '            (0,161)
   LET $d = $current-date
   PRINT $d () edit 'mm/dd/yyyy'
   
   !PRINT 'Run Time: '   (+1,153)
   PRINT $RunTime_Title  (+1,153)
   PRINT ': '            (0,161)
   PRINT $d () edit 'HH:MI:SS'
   PRINT '-' (+1,5,{COLR}) fill

end-procedure ! print-heading


!**********************************************************************
!**********************************************************************
!       Heading2 of the Report
!**********************************************************************
!**********************************************************************
begin-procedure print-heading2
   
   IF &recipient_id <> ''
      PRINT $Recipient_Title  (+1,5)
      PRINT ': '              (0,18)
      PRINT &recipient_id     (0,20)
      PRINT $Recipient_Name (0,33)
   END-IF

   IF &src_bank_id <> ''
      PRINT $Src_Bank_Title (+1,5)
      PRINT ': '            (0,18)
      PRINT &src_bank_id    (0,20)
      PRINT &src_bank_nm    (0,33)
   END-IF
   
   IF &pay_entity <> ''
      PRINT $Pay_Entity_Title (+1,5)
      PRINT ': '              (0,18)
      PRINT &pay_entity       (0,20)
   ELSE
      IF $PayEntity <> ''
         PRINT $Pay_Entity_Title (+1,5)
         PRINT ': '              (0,18)
         PRINT $PayEntity        (0,20)
      END-IF
   END-IF
   
   IF &payment_date <> ''
      PRINT 'Payment Date : '    (+1,5)
      PRINT $PMTDt               (0,20) !edit 'MM/DD/YYYY'
   ELSE
      IF $PaymentDt <> ''
         PRINT 'Payment Date : ' (+1,5)
         PRINT $RC_PMTDT         (0,20) !edit 'MM/DD/YYYY'
      END-IF
   END-IF

   IF &debit_date <> ''
      PRINT 'Debit Date   : '    (+1,5)
      PRINT $DBTDt               (0,20) !edit 'MM/DD/YYYY'
   ELSE
      IF $DebitDT <> ''
         PRINT 'Debit Date   : ' (+1,5)
         PRINT $RC_DebitDT       (0,20) !edit 'MM/DD/YYYY'
      END-IF
   END-IF

   PRINT '-' (+1,5,{COLR}) fill
   
   PRINT $EMPLID_Title       (+1,5)
   PRINT $EMPLRCD_Title      (,23)
   PRINT $NAME_Title         (,40)
   PRINT $AMT_Title          (,73)
   
   PRINT '-' (+1,5,{COLR}) fill

end-procedure ! print-heading2


begin-footing 1

end-footing

!
!***********************************************************************
! Get-Parameters from CTX Run Control table                            *
!***********************************************************************
begin-procedure Get-Parameters

BEGIN-SELECT

RC.PAY_ENTITY
RC.DEBIT_DT
RC.PAYMENT_DT
RC.GPUS_RCP_OPTN
RC.COMPANY
RC.FEDERAL_EIN

   LET $PayEntity = &RC.PAY_ENTITY
   LET $DebitDt  = &RC.DEBIT_DT
   Do Format-DateTime($DebitDt, $RC_DebitDT, {DEFDATE},'','')
   LET $PaymentDt = &RC.PAYMENT_DT
   Do Format-DateTime($PaymentDt, $RC_PMTDT, {DEFDATE},'','')
   LET $RCPTOption = &RC.GPUS_RCP_OPTN
   LET $CompanyCD = &RC.COMPANY
   LET $FEIN = &RC.FEDERAL_EIN

FROM PS_GPUS_CTX_RC RC
WHERE RC.OPRID           = $PRCS_OPRID
      AND RC.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID

END-SELECT
end-procedure ! Get-Parameters


!***********************************************************************
! Get-Recipient-Name from PS_RECIPIENT table
!***********************************************************************
Begin-Procedure Get-Recipient-Name
Begin-SELECT
A.DESCR

     Let $Recipient_Name = &A.DESCR
     
FROM PS_RECIPIENT A
WHERE A.RECIPIENT_ID = &recipient_id
End-SELECT
End-Procedure



!**********************************************************************
!**********************************************************************
! Get Detail Garnishment Payment Data from GPUS_CTX_RPT
! Generate Report GPUSBK03
!**********************************************************************
!**********************************************************************
begin-procedure get-recipients

LET #cnt      = 0
LET #Total    = 0
LET #SubTotal = 0

BEGIN-SELECT

DISTINCT A.SRC_BANK_ID &src_bank_id () On-Break print=never Level=1 
A.RECIPIENT_ID         &recipient_id () On-Break print=never Level=2
A.RECIPIENT_TAG        &recipient_tag
A.RECIPIENT_NAME       &recipient_nm
A.PAY_ENTITY           &pay_entity 
A.GPUS_DEST_BANK_NM    &src_bank_nm 
A.EMPLID               &emplid
A.EMPL_RCD             &empl_rcd
A.CHECK_DT             &payment_date
A.DEBIT_DT             &debit_date
A.GPUS_PAYEE_NAME      &name
A.GP_PMT_AMT           &amount 

  LET recipient_info.src_bank_id(#cnt) = &src_bank_id
  LET recipient_info.recipient_id(#cnt) = &recipient_id
  LET recipient_info.recipient_nm(#cnt) = &recipient_nm
  
  DO Get-Recipient-Name
  
  Let $Payment_Dt = &payment_date
  Do Format-DateTime($Payment_Dt, $PMTDt, {DEFDATE},'','')
  Let $Debit_Dt = &debit_date
  Do Format-DateTime($Debit_Dt, $DBTDt, {DEFDATE},'','')
  
 
  IF #cnt = 0
     DO print-heading2
  END-IF
  position (+1)  ! Advance to the next line

  !
  ! Print in different page if SRC Bank changed or Recipient changed     
  !
  IF #cnt > 0 
     LET #i = #cnt - 1
     IF &src_bank_id <> recipient_info.src_bank_id(#i)
 
        PRINT '-' (+0,5,{COLR}) fill
        PRINT 'Sub Total:' (+1, 57) 
        PRINT #SubTotal (,67) Edit 999,999,999.99
        LET #SubTotal = 0

        new-page
        do print-heading
        do print-heading2
        position (+1)  ! Advance to the next line
     END-IF

     IF &recipient_id <> recipient_info.recipient_id(#i) 

        PRINT '-' (+0,5,{COLR}) fill
        PRINT 'Sub Total:' (+1, 57) 
        PRINT #SubTotal (,67) Edit 999,999,999.99
        LET #SubTotal = 0

        new-page
        do print-heading
        do print-heading2
        position (+1)  ! Advance to the next line
     END-IF
  END-IF
  
  !
  ! Print Detail Information   
  !
  PRINT &emplid (,5)
  PRINT &empl_rcd (,22) Edit 99
  PRINT &name (,40)
  PRINT &amount (,71) Edit 999,999.99

  !LET recipient_info.recipient_nm(#cnt) = &recipient_nm
  LET recipient_info.pay_entity(#cnt) = &pay_entity
  LET recipient_info.src_bank_nm(#cnt) = &src_bank_nm
  LET recipient_info.emplid(#cnt) = &emplid
  LET recipient_info.empl_rcd(#cnt) = &empl_rcd
  LET recipient_info.name(#cnt) = &name
  LET recipient_info.amount(#cnt) = &amount
  LET #cnt = #cnt + 1
  LET #Total = #Total + &amount
  LET #SubTotal = #SubTotal + &amount

FROM PS_GPUS_CTX_RPT A 
WHERE A.PROCESS_INSTANCE = #AE_process_instance

GROUP BY A.SRC_BANK_ID,A.PAY_ENTITY,A.DEBIT_DT,A.CHECK_DT,A.EMPLID,A.EMPL_RCD,A.RECIPIENT_ID,A.RECIPIENT_TAG,A.RECIPIENT_NAME,A.GPUS_DEST_BANK_NM,A.GPUS_PAYEE_NAME,A.GP_PMT_AMT
ORDER BY A.SRC_BANK_ID,A.PAY_ENTITY,A.DEBIT_DT,A.CHECK_DT,A.EMPLID,A.EMPL_RCD,A.RECIPIENT_ID,A.RECIPIENT_TAG,A.RECIPIENT_NAME,A.GPUS_DEST_BANK_NM,A.GPUS_PAYEE_NAME,A.GP_PMT_AMT

END-SELECT
    
  if #cnt = 0
     DO print-heading2
     position (+1)  ! Advance to the next line
     PRINT 'NO DATA SELECTED' (+1, 80)  
     position (+1)  ! Advance to the next line
  else
     position (+1)  ! Advance to the next line
     PRINT '-' (+0,5,{COLR}) fill
     PRINT 'Sub Total:' (+1, 57) 
     PRINT #SubTotal (,67) Edit 999,999,999.99
     LET #SubTotal = 0
  end-if
  
  ! Print Total Amount
  position (+1)  ! Advance to the next line
  PRINT '=' (+0,5,{COLR}) fill
  PRINT 'Total:' (+1, 61) 
  PRINT #Total (,67) Edit 999,999,999.99

end-procedure  !get-recipients

!**********************************************************************
!**********************************************************************
!  Delete Rows for current Process Instance from Table GPUS_CTX_RPT 
!**********************************************************************
!**********************************************************************
begin-procedure Cleanup-Table

begin-sql
delete from PS_GPUS_CTX_RPT
where PROCESS_INSTANCE = #AE_process_instance
end-sql

end-procedure !Trancate-Table


!***************************************************************
!Include SQCs
!***************************************************************
#include 'stdapi.sqc'    ! UPDATE PROCESS API
#include 'curdttim.sqc'  ! GET-CURRENT-DATETIME PROCEDURE
#include 'datetime.sqc'  ! ROUTINES FOR DATE AND TIME FORMATTING
#include 'number.sqc'    ! ROUTINES TO FORMAT NUMBERS
#Include 'readxlat.sqc'  ! Read-Translate-Table procedure
#Include 'sqlerr.sqc'
#Include 'sqrtrans.sqc'  ! Routines to Translate SQR
#include 'datemath.sqc'  ! ROUTINES FOR DATEMATH
#include 'getbunam.sqc'
#include 'getdptnm.sqc'
#include 'getlocnm.sqc'
#include 'useprntr.sqc'
!**********************************************************************
!       END SQR PROGRAM GPUSBK03.sqr
!**********************************************************************
