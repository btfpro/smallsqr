!*****************************************************
! BAS002: Benefit Program Mailing Labels (4-up)
!*****************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/11:17:59:55                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'    !Set Database Environment

Begin-Setup
  #Include 'ptpsp125.sqc'
End-Setup

!**********************************
Begin-Program
!**********************************

  do Init-Report

  display 'Report Begin at: ' noline
  do display-time

  do Main-Report

  display 'Report Ended at: ' noline
  do display-time

  do Reset
  do StdAPI-Term

End-Program

!**********************************
Begin-Procedure Init-Report
!**********************************

  do Init-DateTime
  do Get-Current-DateTime
  do StdApi-Init

  let $ReportID = 'BAS002'
  do Get-Report-Language
  let $ReportTitle = 'Benefit Program Mailing Labels (4-up)'
  display $ReportID    NoLine
  display ': '         NoLine
  display $ReportTitle
  display ''

  if $prcs_process_instance = ''
    do Ask-Values
  else
    do Get-Values
  end-if

End-Procedure

!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BAS002 program ids
  do Init_Report_Translation($ReportID, $curr_language_cd)

  do Get_Field_Information ('BAS002', 'LOCATION',     $BAS002_LOCATION,    #CW)
  do Get_Field_Information ('BAS002', 'DEPARTMENT',   $BAS002_DEPARTMENT,  #CW)

  do Append_Report_Translation ('STDHDGTR')
  do Get_Field_Information ('STDHDGTR', 'STDHDG_END_REP', $stdhdg_end_rep, #CW)

End-Procedure

!**********************************
Begin-Procedure Ask-Values
!**********************************

 do Ask-Schedule-Id
 do Ask-Form-Type
 do Ask-Form-Date
 do Set-Where-Clause

End-Procedure

!**********************************
Begin-Procedure Get-Values
!**********************************

 do Select-Parameters
 do Get-Schedule-Id
 do Get-Form-Type
 do Get-Form-Date
 do Set-Where-Clause

End-Procedure


!**********************************
Begin-Procedure Set-Where-Clause
!**********************************

 if $Form_Type = 'E'
   let $StatClause = '(P.EVENT_STATUS = ''O'') And '
   let $StatClause = $StatClause || 'P.BAS_PROCESS_STATUS IN (''NT'',''ET'',''EE'')'
   let $DateClause = '(P.OPTION_NOTIFY_DT >= ''' || $Form_Date || ''')'
 else
   let $StatClause = 'P.EVENT_STATUS IN (''O'',''C'') And '
   let $StatClause = $StatClause || 'P.BAS_PROCESS_STATUS IN (''EE'',''FE'')'
   let $DateClause = '(P.CONFIRM_NOTIFY_DT >= ''' || $Form_Date || ''')'
 end-if

End-Procedure

!**********************************
Begin-Procedure Ask-Form-Type
!**********************************

 let $Form_Type = ''
 While (($Form_Type <> 'E') And ($Form_Type <> 'C'))
   input $Form_Type
     'Print labels for which Form type (''E''nrollment or ''C''onfirm)'
   let $Form_Type = RTRIM(UPPER($Form_Type), ' ')
 End-While

End-Procedure

!**********************************
Begin-Procedure Ask-Form-Date
!**********************************

  let $DateOK = 'N'
  While ($DateOK = 'N')

    input $Form_Date 'Print labels for Forms produced on-or-after {Prompt-Date} (leave blank for Today''s date)'
    let $Form_Date = RTRIM($Form_Date, ' ')

    if ($Form_Date = '')
      move $AsOfToday to $Form_Date
    else
      move {PDelimiter} to $DDelimiter
      do Format-DateTime($Form_Date,$Form_Date,{Prompt-Mask},'','native')
    end-if

    do Validate-Native-Date($Form_Date,$DateOK)

  End-While

End-Procedure

!**********************************
Begin-Procedure Main-Report
!**********************************

 columns 1 32 63 94  !beginning column of each row of mailing labels

Begin-SELECT DISTINCT
P.SCHED_ID
P.EMPLID
P.EVENT_DT
P.BENEFIT_PROGRAM
D.NAME
  let $Name = RTRIM(&D.Name, ' ')
D.LANG_CD
  if $prcs_language_option = 'R'
!-- if Language Code not defined for the Recipient level of translation
!-- then use the system default - $Language_Cd.
    if rtrim(&D.Lang_Cd,' ') = ''
      if $curr_language_cd <> $Language_Cd
        let $curr_language_cd = $Language_Cd
        do Report-Translation
      end-if
    else
      if &D.Lang_Cd <> $curr_language_cd
        let $curr_language_cd = &D.Lang_Cd
        do Report-Translation
      end-if
    end-if
  end-if

J.BUSINESS_UNIT

J.LOCATION
  if rtrim(&J.Location,' ') <> $Location
      or &J.BUSINESS_UNIT <> $Busunit
    let $Busunit = &J.BUSINESS_UNIT
    let $Recname = 'LOCATION_TBL'
    do Get-Setid($Busunit, $Recname, $Setid)
    let $Location = rtrim(&J.Location,' ')
    do Get-Location-Name
  end-if

J.DEPTID
  if rtrim(&J.DeptID,' ') <> $DeptID
      or &J.BUSINESS_UNIT <> $BU
    let $BU = &J.BUSINESS_UNIT
    let $DeptID = rtrim(&J.DeptID,' ')
    do Get-Department-Name
  end-if

  print $Name                           (0,1)

  print $BAS002_LOCATION                (+1,1,9)
  print &J.Location                     (0,14,5)
  print '-- '                           (0,20)
  print $LocationAbbrv                  (0,0,9)

  print $BAS002_DEPARTMENT              (+1,1,11)
  print &J.DeptID                       (0,14,5)
  print '-- '                           (0,20)
  print $DeptNameAbbrv                  (0,0,9)

  POSITION                              (+3)

  if #current-line >= 76
    NEXT-COLUMN GOTO-TOP=1 AT-END=NEWPAGE
  end-if

D.LAST_NAME_SRCH
D.FIRST_NAME_SRCH

FROM  PS_BAS_PARTIC      P,
      PS_JOB             J,
      PS_PERSONAL_DT_FST D

WHERE P.PROCESS_IND    = 'N'
AND   P.SCHED_ID       LIKE $Sched_Id
AND   [$StatClause]
AND   [$DateClause]
AND J.EMPLID    = P.EMPLID
AND J.EMPL_RCD = P.EMPL_RCD
AND J.EFFDT     = P.JOB_EFFDT
AND J.EFFSEQ    = P.JOB_EFFSEQ
AND D.EMPLID    = P.EMPLID

ORDER BY P.BENEFIT_PROGRAM ASC,
         J.LOCATION ASC,
         J.DEPTID ASC,
         D.LAST_NAME_SRCH ASC, D.FIRST_NAME_SRCH ASC

End-SELECT

End-Procedure

!*************************************
Begin-Procedure Display-Time
!*************************************

  do Get-Current-DateTime
  display $AsOfToday NoLine
  display ', '       NoLine
  display $AsOfNow
  display ''

End-Procedure

!**********************************

#Include 'reset1.sqc'     !Reset the printer
#Include 'getsetid.sqc'  !Get-Setid procedure
#Include 'getdptnm.sqc'  !Get-Department-Name procedure
#Include 'getlocnm.sqc'  !Get-Location-Name procedure
#Include 'validdt.sqc'   !Validate date routine
#Include 'datetime.sqc'  !Format-DateTime procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getsched.sqc'  !Ask-Schedule-ID procedure

#Include 'stdapi.sqc'    !Update Process API
#Include 'bnrunctl.sqc'  !Get application run control parameters
#Include 'bngetval.sqc'  !Get BEN values mask routines
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language

!**************************** End Of Report *********************************
