!**********************************************************************!
!  gpusw5up :  W-5 EIC Payees Report                       (USA)       !
!**********************************************************************!
!                                                                      !
!               Confidentiality Information:                           !
!                                                                      !
!                                                                      !
! This module contains confidential and proprietary information        !
! of Oracle; it is not to be copied, reproduced, or transmitted        !
! in any form, by any means, in whole or in part, nor is it to         !
! be used for any purpose other than that for which it is              !
! expressly provided under the applicable license agreement.           !
!                                                                      !
! Copyright (C) 2006 Oracle. All Rights Reserved.                      !
!                                                                      !
!**********************************************************************!
!                                                                      !
!          $Date:  2006/07/21:02:04:59                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      !
!**********************************************************************!


#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'GPUSW5UP' to $ReportID
  move 'W-5 EIC Payees Report' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-EandG                                                          !HP99999
  do Stdapi-Init
  do Get-Current-DateTime
  do Initialization
end-procedure

begin-procedure Initialization

     do Select-Parameters
     let $Udate    = $GPUS_RPT_TAX_RC.ThruDate
     let $A        = ''''
     let $UdateA   = $A || $Udate || $A
     let $Duedt  = $GPUS_RPT_TAX_RC.AsOfDate
     do Format-DateTime($Duedt, $DueDate, {DEFYMD}, '', '')
     let $Scompany = $GPUS_RPT_TAX_RC.Company
     let $Scope    = $GPUS_RPT_TAX_RC.GPUS_RPT_SEL_CRIT


!--- Get A Jan-01 Date For The Year
let $Jan01dt  = ''

do DTU-PARSE-DATE($DueDate, #due_year, #due_mm, #due_dd)

let #due_year = #due_year - 1
do dtu-format-date(#due_year, 01, 01, $JanDueDt)
do Convert-From-DTU-Date($JanDueDt, $Jan01dt)


move ' ' to $PEMPLID
move 0 to #I
move 'Y' to $Notify
move 'GPUS_EIC_STATUS' to $Fieldname
move 'N' to $Fieldvalue
do Read-Translate-Table
move $XlatLongName to $Nonestatus
end-procedure

begin-heading 9
  #Include 'stdhdg01.sqc'
end-heading

begin-procedure Process-Main
Do GPUSW5UP-Headings
if $Scope='01'
   do All-Employees
else
   do By-Company
end-if
end-procedure

begin-procedure All-Employees
begin-SELECT
A.EMPLID,
A.COMPANY,
A.EFFDT,
A.GPUS_FWT_STATUS,
A.GPUS_FWT_MAR_ST,
A.GPUS_FWT_ALLOW,
A.GPUS_FWT_ADD_AMT,
A.GPUS_FWT_ADD_PCT,
A.GPUS_FUT_EXEMPT,
A.GPUS_EIC_STATUS,
A.GPUS_UPDATE_CD,
A.GPUS_W5_STATUS,
A.GPUS_W4_STATUS,
A.GPUS_LETTER_RCV,
A.GPUS_ALLOW_LIMIT

           let $PEMPLID            = &A.EMPLID
           let $PEFFDT             = &A.EFFDT
           let $PCOMPANY           = &A.COMPANY
           let $PFWTSTATUS         = &A.GPUS_FWT_STATUS
           let $PMARSTATUS         = &A.GPUS_FWT_MAR_ST
           let #PFWTALLW           = &A.GPUS_FWT_ALLOW
           let #PFWTAMT            = &A.GPUS_FWT_ADD_AMT
           let #PFWTPCT            = &A.GPUS_FWT_ADD_PCT
           let $PFUT_EXEMPT        = &A.GPUS_FUT_EXEMPT
           let $PEIC_STATUS        = &A.GPUS_EIC_STATUS
           let $PW4PRCSS           = &A.GPUS_W4_STATUS
           let $PW5PRCSS           = &A.GPUS_W5_STATUS
           let $PLETTER_RECVD      = &A.GPUS_LETTER_RCV
           let #PALLOW_LIMIT       = &A.GPUS_ALLOW_LIMIT

          do Print-Rows

FROM PS_GPUS_FWT_EE A
WHERE A.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPUS_FWT_EE A1
                   WHERE A1.EMPLID           = A.EMPLID
                     AND A1.COMPANY          = A.COMPANY
                     AND A1.EFFDT            <= $Duedt
                     AND A1.EFFDT            >= $JAN01DT)

    AND A.GPUS_W5_STATUS   = '02'
    AND A.GPUS_EIC_STATUS  <> 'N'
ORDER BY
A.EMPLID,
A.COMPANY,
A.EFFDT
end-SELECT

end-procedure

begin-procedure By-Company
if RTRIM($SCompany,' ')=''
   print 'Error: Scope selected is By Company, but no company was entered' (+1,3)
   Stop Quiet
end-if
begin-SELECT
B.EMPLID,
B.COMPANY,
B.EFFDT,
B.GPUS_FWT_STATUS,
B.GPUS_FWT_MAR_ST,
B.GPUS_FWT_ALLOW,
B.GPUS_FWT_ADD_AMT,
B.GPUS_FWT_ADD_PCT,
B.GPUS_FUT_EXEMPT,
B.GPUS_EIC_STATUS,
B.GPUS_UPDATE_CD,
B.GPUS_W5_STATUS,
B.GPUS_W4_STATUS,
B.GPUS_LETTER_RCV,
B.GPUS_ALLOW_LIMIT

              let $PEMPLID            = &B.EMPLID
              let $PEFFDT             = &B.EFFDT
              let $PCOMPANY           = &B.COMPANY
              let $PFWTSTATUS         = &B.GPUS_FWT_STATUS
              let $PMARSTATUS         = &B.GPUS_FWT_MAR_ST
              let #PFWTALLW           = &B.GPUS_FWT_ALLOW
              let #PFWTAMT            = &B.GPUS_FWT_ADD_AMT
              let #PFWTPCT            = &B.GPUS_FWT_ADD_PCT
              let $PFUT_EXEMPT        = &B.GPUS_FUT_EXEMPT
              let $PEIC_STATUS        = &B.GPUS_EIC_STATUS
              let $PW4PRCSS           = &B.GPUS_W4_STATUS
              let $PW5PRCSS           = &B.GPUS_W5_STATUS
              let $PLETTER_RECVD      = &B.GPUS_LETTER_RCV
              let #PALLOW_LIMIT       = &B.GPUS_ALLOW_LIMIT

             do Print-Rows

FROM PS_GPUS_FWT_EE B
WHERE B.COMPANY = $SCompany
  AND B.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPUS_FWT_EE B1
                   WHERE B1.EMPLID          = B.EMPLID
                     AND B1.COMPANY         = B.COMPANY
                     AND B1.EFFDT           <= $Duedt
                     AND B1.EFFDT           >= $JAN01DT)
  AND B.GPUS_W5_STATUS  = '02'
  AND B.GPUS_EIC_STATUS <> 'N'
ORDER BY
B.COMPANY,
B.EMPLID,
B.EFFDT
end-SELECT

end-procedure


begin-procedure Print-Rows


                print $PEMPLID                  (+1,31)
                print $PCOMPANY                 (,46)
                do Convert-To-DTU-Date($PEFFDT, $PEFFDTFMT)
                print $PEFFDTFMT                (,57)
                move  $PEIC_STATUS  to $Fieldvalue
                do Read-Translate-Table
                print $XlatLongName                (,85)

end-Procedure



!*********************************************************************************************
Begin-Procedure GPUSW5UP-Headings

   print 'REPORT MODE: Identify employees that need to have their federal tax records modified to set EIC status to None'   (+1,1)
   print '             because they failed to file a new W-5 form by a user-specified date.'                           (+1,1)

if $Scope='02'
   print 'SCOPE: By Company'    (+2,1)
else
   print 'SCOPE: All Employees' (+2,1)
end-if
print 'Employee ID'             (+3,31)
print 'Company'                 (,46)
print 'Effective Date'          (,57)
print 'EIC Status'              (,85)
End-Procedure

#Include 'gpuspctl.sqc'  !Select-Parameters procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'geteandg.sqc'  !Get-EandG procedure                           !HP99999
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'rotname1.sqc'  !Rotate-Name procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date calculation and formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'readxlat.sqc'  !Read XLAT values