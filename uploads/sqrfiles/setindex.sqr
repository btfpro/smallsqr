! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
! SETINDEX - Set the Index Overrides in PSIDXDDLPARM                   *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************
#define REPORTID         'SETINDEX'
#include 'setenv.sqc'                   ! Set environment procedure
#include 'setup01.sqc'                  ! Init printer and page size

begin-report
   do Initialize
   do Report
   do Terminate
end-report

!----------------------------------------------------------------------!
! Procedure: Initialize                                                !
! Descr:     Perform initialization procedure.                         !
!----------------------------------------------------------------------!
begin-procedure Initialize

   do Init-DateTime
   do Init-Number
   move {REPORTID} to $ReportID
   move 'Set Index Storage overrides in PSIDXDDLPARM' to $ReportTitle
   move '' to $AuditDescr
   let #UpdCount = 0
   let #UpdCount1 = 0
   let #UpdCount2 = 0
   let #UpdCount3 = 0
   let #UpdCount4 = 0
   let #UpdCount5 = 0
   let #UpdCount6 = 0
   let #TotalCount = 0
   let $RecName = ' '
   display $ReportTitle
   Show ' '
   Show  ' Table PSIDXDDLPARM column PARMVALUE has been updated with the'
   Show  ' Current Values found in the system catalog table.  The total'
   Show  ' number of records updated appears at the bottom of this report.'
   Show  ' '

#IFDEF ORACLE
   let #platformID = 2
   Show  'Indexname         Init       Next       Maxext     Pct    Pctfree TablespaceName'
   Show  '----------------- ---------- ---------- ---------- ------ ------- --------------'
#ENDIF

end-procedure


!----------------------------------------------------------------------!
! Procedure: Report                                                    !
! Descr:     Perform main report procedure.                            !
!----------------------------------------------------------------------!
begin-procedure Report

! Used for version control and to allow re-cache only for records tha
! have been updated

begin-SQL on-error=SQL-Error
 UPDATE PSLOCK
 SET VERSION= VERSION+1
 WHERE OBJECTTYPENAME = 'RDM'
end-SQL

begin-SELECT on-error=SQL-Error
V.version &V.Version
   let #NewVersionrdm = &V.Version
FROM PSLOCK V
WHERE OBJECTTYPENAME = 'RDM'
end-SELECT

Do  Get-Default-Values

let $update_done = 'N'

begin-SELECT  ON-ERROR=SQL-ERROR
P.RecName       &P.RecName
P.SQLTableName  &P.SQLTableName
Q.DDLCount      &Q.DDLCount
Q.INDEXID       &Q.IndexId

    if substr(&P.SQLTableName,1,10) = '          '
       let $RecName   = 'PS_'|| substr(&P.RecName,1,15)
     else
       let $Recname   = &P.SQLTableName
    end-if

    let $IndexName = 'PS'|| &Q.INDEXID || &P.RecName

    Do  Read-Sys-Catalog
    Do  Update-Version

FROM PSRECDEFN   P, PSINDEXDEFN Q
WHERE P.RECTYPE = 0
  AND P.RECNAME = Q.RECNAME
ORDER BY P.RECNAME, Q.INDEXID

end-SELECT

if $update_done = 'Y'

begin-SQL on-error=SQL-Error
 UPDATE PSVERSION
 SET VERSION= VERSION+1
 WHERE OBJECTTYPENAME IN ('RDM','SYS')
end-SQL

end-if

end-procedure

!----------------------------------------------------------------------!
! Procedure: Terminate                                                 !
! Descr:     Perform termination procedure.                            !
!----------------------------------------------------------------------!
begin-procedure Terminate

   Show ' '
   Show 'Indexes Updated: ' #UpdCount edit 9999999
   Show ' '

#Ifdef Debuga
   Show 'Index overrides updated for Init Extent Values: ' #UpdCount1 edit 9999999
   Show 'Index overrides updated for Next Extent Values: ' #UpdCount2 edit 9999999
   Show 'Index overrides updated for Max Extent Values : ' #UpdCount3 edit 9999999
   Show 'Index overrides updated for PctFree Values    : ' #UpdCount4 edit 9999999
   Show 'Index overrides updated for Pct Incr Values   : ' #UpdCount5 edit 9999999
   Show 'Index overrides updated for Index Tablespaces : ' #UpdCount6 edit 9999999
#Endif

   Show 'Ending SQR.'

   do Commit-Transaction

end-procedure


!-----------------------------------------------------------------------------!
! Procedure: Get-Default-Values                                               !
! Descr:     Get the Default values from PSDDLDEFPARMS for indices            !
!-----------------------------------------------------------------------------!
begin-procedure Get-Default-Values

!-----------------------------------------------------------------------------!
!SELECT * FROM PSDDLDEFPARMS WHERE PLATFORMID = 2 AND STATEMENT_TYPE = 2
!
!STATEMENT_TYPE PLATFORMID SIZING_SET PARMNAME PARMVALUE
!-------------- ---------- ---------- -------- ------------
!             2          2          0 BITMAP   Don't need this one, nothing to compare on catalog
!             2          2          0 INDEXSPC PSINDEX
!             2          2          0 INIT     40000
!             2          2          0 MAXEXT   UNLIMITED
!             2          2          0 NEXT     100000
!             2          2          0 PCT      0
!             2          2          0 PCTFREE  10
!
!7 rows selected.
!
!-----------------------------------------------------------------------------!
!
!begin-SELECT On-Error=SQL-Error
!A.PARMNAME        &A.PARMNAME
!A.PARMVALUE       &A.PARMVALUE
!
!    let $PARMNAME  = &A.PARMNAME
!    let $BITMAPVALUE = &A.PARMVALUE
!
!FROM PSDDLDEFPARMS A
!WHERE PLATFORMID = 2
!AND STATEMENT_TYPE = 2
!AND PARMNAME = 'BITMAP'
!end-SELECT
!
!-----------------------------------------------------------------------------!



begin-SELECT On-Error=SQL-Error
B.PARMNAME        &B.PARMNAME
B.PARMVALUE       &B.PARMVALUE

    let $PARMNAME  = &B.PARMNAME
    let $INDEXSPCVALUE = &B.PARMVALUE

FROM PSDDLDEFPARMS B
WHERE PLATFORMID = 2
AND STATEMENT_TYPE = 2
AND PARMNAME = 'INDEXSPC'
end-SELECT


begin-SELECT On-Error=SQL-Error
C.PARMNAME        &C.PARMNAME
C.PARMVALUE       &C.PARMVALUE

    let $PARMNAME  = &C.PARMNAME
    let $INITVALUE = to_char(1024 * CEIL(to_number(&C.PARMVALUE)/1024))


FROM PSDDLDEFPARMS C
WHERE PLATFORMID = 2
AND STATEMENT_TYPE = 2
AND PARMNAME = 'INIT'
end-SELECT


begin-SELECT On-Error=SQL-Error
D.PARMNAME        &D.PARMNAME
D.PARMVALUE       &D.PARMVALUE

    let $PARMNAME  = &D.PARMNAME
        If &D.PARMVALUE = 'UNLIMITED'
       let $MAXEXTVALUE = '2147483645'
    end-if

FROM PSDDLDEFPARMS D
WHERE PLATFORMID = 2
AND STATEMENT_TYPE = 2
AND PARMNAME = 'MAXEXT'
end-SELECT


begin-SELECT On-Error=SQL-Error
E.PARMNAME        &E.PARMNAME
E.PARMVALUE       &E.PARMVALUE

    let $PARMNAME  = &E.PARMNAME
    let $NEXTVALUE = to_char(1024 * CEIL(to_number(&E.PARMVALUE)/1024))

FROM PSDDLDEFPARMS E
WHERE PLATFORMID = 2
AND STATEMENT_TYPE = 2
AND PARMNAME = 'NEXT'
end-SELECT


begin-SELECT On-Error=SQL-Error
F.PARMNAME        &F.PARMNAME
F.PARMVALUE       &F.PARMVALUE

    let $PARMNAME  = &F.PARMNAME
    let $PCTVALUE = &F.PARMVALUE

FROM PSDDLDEFPARMS F
WHERE PLATFORMID = 2
AND STATEMENT_TYPE = 2
AND PARMNAME = 'PCT'
end-SELECT


begin-SELECT On-Error=SQL-Error
G.PARMNAME        &G.PARMNAME
G.PARMVALUE       &G.PARMVALUE

    let $PARMNAME  = &G.PARMNAME
    let $PCTFREEVALUE = &G.PARMVALUE


FROM PSDDLDEFPARMS G
WHERE PLATFORMID = 2
AND STATEMENT_TYPE = 2
AND PARMNAME = 'PCTFREE'
end-SELECT

!-----------------------------------------------------------------------------!
!
! After this procedure, The default INDEX PSDDLDEFPARMS
! WHERE PLATFORMID = 2 AND STATEMENT_TYPE = 2 are stored in the following variables
!
!    let $BITMAPVALUE   = to_char(&A.PARMVALUE) Don't need this one, nothing to compare on catalog
!    let $INDEXSPCVALUE = to_char(&B.PARMVALUE)
!    let $INITVALUE     = to_char(&C.PARMVALUE)
!    let $MAXEXTVALUE   = to_char(&D.PARMVALUE)
!    let $NEXTVALUE     = to_char(&E.PARMVALUE)
!    let $PCTVALUE      = to_char(&F.PARMVALUE)
!    let $PCTFREEVALUE  = to_char(&G.PARMVALUE)
!
!
#Ifdef Debugb
show '$INDEXSPCVALUE/$INITVALUE/$NEXTVALUE/$MAXEXTVALUE/$PCTVALUE/$PCTFREEVALUE:'
show  $INDEXSPCVALUE '/' $INITVALUE '/' $NEXTVALUE '/' $MAXEXTVALUE '/' $PCTVALUE '/' $PCTFREEVALUE
#Endif

!-----------------------------------------------------------------------------!

end-procedure


!-----------------------------------------------------------------------------!
! Procedure: Read-Sys-Catalog                                                 !
! Descr:     Get the Primary and Secondary Quanties and Stogroup from catalog !
!-----------------------------------------------------------------------------!
begin-procedure Read-Sys-Catalog

#IFDEF ORACLE

begin-SELECT On-Error=SQL-Error
Y.INITIAL_EXTENT &Y.INIT
Y.NEXT_EXTENT    &Y.NEXT
Y.MAX_EXTENTS    &Y.MAXEXT
Y.PCT_INCREASE   &Y.PCT
Y.PCT_FREE       &Y.PCTFREE
Y.TABLESPACE_NAME &Y.TSNAME

    let $Initialex = to_char(&Y.INIT)
    let $Nextex    = to_char(&Y.NEXT)
    let $MaxEx     = to_char(&Y.MAXEXT)
    let $Pct       = to_char(&Y.PCT)
    let $Pctfree   = to_char(&Y.PCTFREE)
    let $Tsname    = &Y.TSNAME

    let $update_done = 'Y'
    Do  Update-PSIDXDDLPARM

FROM USER_INDEXES Y
WHERE INDEX_NAME = $IndexName
end-SELECT

#ENDIF


end-procedure

!-----------------------------------------------------------------------------!
! Procedure: Update-Version                                                   !
!-----------------------------------------------------------------------------!
begin-procedure Update-Version

begin-SQL
UPDATE PSRECDEFN
   SET VERSION = #NewVersionrdm

WHERE RECNAME = &P.Recname
end-Sql

end-procedure


!-----------------------------------------------------------------------!
! Procedure: Update-PSIDXDDLPARM                                        !
! Descr:     Check to see if an Override already Exists for the         !
!            platform. If no override exists, Insert an override for    !
!            Primary Qty, SecQty, StoGroup and increment DDLCount in    !
!            PSINDEXDEFN for the Index.                                 !
!            If at least one override exists, delete and insert the      !
!            override for the Index and do not update DDLCount in INDEXDEFN  !
!-----------------------------------------------------------------------!
begin-procedure Update-PSIDXDDLPARM

    if $DBName = ''
        let $DBName =$def_dbname
    end-if

let $parmexist = ' '

begin-Select LOOPS=1 On-Error=SQL-Error
X.PARMVALUE

   let $parmexist = 'Y'

FROM PSIDXDDLPARM X
WHERE RECNAME = &P.RECNAME
AND   INDEXID = &Q.INDEXID
end-Select

begin-SQL  On-Error=SQL-Error
  DELETE FROM PSIDXDDLPARM
  WHERE RECNAME = &P.RECNAME
    AND INDEXID = &Q.INDEXID
    AND PLATFORMID = #platformID
    AND SIZINGSET  = 0
end-SQL

!-----------------------------------------------------------------------------!
! Insert Overrides - ORACLE
!-----------------------------------------------------------------------------!
!
! After this procedure, The default INDEX PSDDLDEFPARMS
! WHERE PLATFORMID = 2 AND STATEMENT_TYPE = 2 are stored in the following variables
!
!    let $INDEXSPCVALUE = to_char(&B.PARMVALUE)
!    let $INITVALUE     = to_char(&C.PARMVALUE)
!    let $MAXEXTVALUE   = to_char(&D.PARMVALUE)
!    let $NEXTVALUE     = to_char(&E.PARMVALUE)
!    let $PCTVALUE      = to_char(&F.PARMVALUE)
!    let $PCTFREEVALUE  = to_char(&G.PARMVALUE)
!

#Ifdef Debugc
    show '$INITVALUE/$NEXTVALUE/$MAXEXTVALUE/$PCTVALUE/$PCTFREEVALUE/$INDEXSPCVALUE/:'
    show  $INITVALUE '/' $NEXTVALUE '/' $MAXEXTVALUE '/' $PCTVALUE '/' $PCTFREEVALUE '/' $INDEXSPCVALUE/
    show '$IndexName/$Initialex/$Nextex/$Maxex/$pct/$pctfree/$tsname:'
    show  $IndexName '/' $Initialex '/' $Nextex '/' $Maxex '/' $Pct '/' $Pctfree '/' $Tsname
#Endif

#Ifdef Debugd
    show '$Initialex/$INITVALUE:'
    show $Initialex '/' $INITVALUE
    show '$Maxex/$MAXEXTVALUE:'
    show $Maxex '/' $MAXEXTVALUE
    show '$Nextex/$NEXTVALUE:'
    show $Nextex '/' $NEXTVALUE
    show '$Pctfree/$PCTFREEVALUE:'
    show $Pctfree '/' $PCTFREEVALUE
    show '$Pct/$PCTVALUE:'
    show $Pct '/' $PCTVALUE
    show '$Tsname/$INDEXSPCVALUE:'
    show $Tsname '/' $INDEXSPCVALUE
#Endif

!-----------------------------------------------------------------------------!

let #TotalCount = 0


if to_number($Initialex) > to_number($INITVALUE)
    begin-SQL On-Error=SQL-Error
     INSERT INTO PSIDXDDLPARM
        (RECNAME, INDEXID, PLATFORMID, SIZINGSET, PARMNAME, PARMVALUE)
     VALUES
        (&P.RECNAME, &Q.INDEXID, #platformID, 0, 'INIT',$Initialex)
     end-SQL
     add +1 to #TotalCount
     add +1 to #UpdCount1
end-if

if $Nextex    <> '0'
  if $Nextex  <> $NEXTVALUE
    begin-SQL On-Error=SQL-Error
     INSERT INTO PSIDXDDLPARM
        (RECNAME, INDEXID, PLATFORMID, SIZINGSET, PARMNAME, PARMVALUE)
     VALUES
        (&P.RECNAME, &Q.INDEXID, #platformID, 0, 'NEXT',$Nextex)
    end-SQL
    add +1 to #TotalCount
    add +1 to #UpdCount2
  end-if
end-if

if $Maxex     <> $MAXEXTVALUE
  begin-SQL On-Error=SQL-Error
    INSERT INTO PSIDXDDLPARM
       (RECNAME, INDEXID, PLATFORMID, SIZINGSET, PARMNAME, PARMVALUE)
    VALUES
       (&P.RECNAME, &Q.INDEXID, #platformID, 0, 'MAXEXT',$Maxex)
    end-SQL
    add +1 to #TotalCount
    add +1 to #UpdCount3
end-if

if $Pctfree   <> $PCTFREEVALUE
  begin-SQL On-Error=SQL-Error
    INSERT INTO PSIDXDDLPARM
       (RECNAME, INDEXID, PLATFORMID, SIZINGSET, PARMNAME, PARMVALUE)
    VALUES
       (&P.RECNAME, &Q.INDEXID, #platformID, 0, 'PCTFREE',$Pctfree)
    end-SQL
    add +1 to #TotalCount
    add +1 to #UpdCount4
end-if

if $Pct       <> $PCTVALUE
  begin-SQL On-Error=SQL-Error
    INSERT INTO PSIDXDDLPARM
       (RECNAME, INDEXID, PLATFORMID, SIZINGSET, PARMNAME, PARMVALUE)
    VALUES
       (&P.RECNAME, &Q.INDEXID, #platformID, 0, 'PCT',$Pct)
    end-SQL
    add +1 to #TotalCount
    add +1 to #UpdCount5
end-if

if $Tsname <> ''
 if $Tsname    <> $INDEXSPCVALUE
  begin-SQL On-Error=SQL-Error
    INSERT INTO PSIDXDDLPARM
     (RECNAME, INDEXID, PLATFORMID, SIZINGSET, PARMNAME, PARMVALUE)
    VALUES
     (&P.RECNAME, &Q.INDEXID, #platformID, 0, 'INDEXSPC',$Tsname)
    end-SQL
    add +1 to #TotalCount
    add +1 to #UpdCount6
 end-if
end-if

#Ifdef Debuge
   show '#Totalcount :' #Totalcount edit 9999999
#Endif


if #TotalCount <> 0
  if $parmexist <> 'Y'
    let #ddlcount = &Q.DDLCount + 1
     Begin-SQL On-Error=SQL-Error
       UPDATE PSINDEXDEFN
          SET DDLCOUNT = #ddlcount
        WHERE RECNAME = &P.RecName
          AND INDEXID = &Q.IndexId
     end-SQL
  end-if
  show $IndexName  '      ' $Initialex '      ' $Nextex '          ' $Maxex ' ' $Pct '      ' $PctFree '      ' $Tsname
  add +1 to #UpdCount
end-if


end-procedure


#include 'curdttim.sqc'
#include 'datetime.sqc'
#include 'number.sqc'
#include 'prcsapi.sqc'
