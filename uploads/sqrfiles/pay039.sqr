!***********************************************************************
!  PAY039.SQR:HR Accounting Line Report                                *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:36:49                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization
#include 'paycfprt.sqc'!Print ChartField functions

!************
begin-report
!************

  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


!***************************
begin-procedure Init-Report
!***************************

  move 'PAY039' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
  do Get-Run-Control


  move 'HR Accounting Line Report' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '
  do REPORT-TRANSLATION

  !------------------------------------------------------------------------------------------------------!
  ! Dynamic ChartFields variables used by the Common CF Print SQC                                        !
  !------------------------------------------------------------------------------------------------------!
  let $DynCF_ChartFields_Option = 'A'        !CF Configuration - 'P' = Pending Changes, 'A' - Current
  let $DynCF_LabelPosn_Option = 'T'          !ChartField Label Position - 'T' = above, 'S' = side
  let $DynCF_LabelUnderLine = 'N'            !ChartField Label Underline - 'Y' = Print, 'N' = Suppress
  let $DynCF_Alignment_Option = 'L'          !ChartField Label Alignment - 'L' = Left, 'R' = Right, 'C' = Center
  let $DynCF_CF_Headings = 'N'               !ChartField Label Heading - 'Y' = Print, 'N' = Suppress
  let $DynCF_LabelName_Option = 'S'          !ChartField Label - 'L' = LongName, 'S' = ShortName
  let $DynCF_Include_Affiliate = 'Y'         !Include Affiliate - 'Y' = Yes, 'N' = No
  let $DynCF_LanguageCd = $language_cd       !ChartField Language Code
  let #DynCF_StrCol = 28                     !ChartField Starting Column
  let #DynCF_EndCol = 170                    !ChartField Ending Column

  do Create-CF-Array
  do Add-to-Exclude-CF-List ('ACCOUNT')
  do Add-to-Exclude-CF-List ('DEPTID')
  do Build-CF-Array

  let #StrLine    = 1
  !------------------------------------------------------------------------------------------------------!
  ! End of Dynamic ChartFields Variables setup                                                           !
  !------------------------------------------------------------------------------------------------------!

end-procedure


!****************
begin-heading 11
!****************

#Include 'stdhdgtr.sqc'

  let $buhr_line = $PAY039_BU_HR||'  '||$business_unit_hr
  let $bugl_line = $PAY039_BU_GL||'  '||$business_unit_gl
  let $jt_line   = $PAY039_JRNL_TMPLT||'    '||$journal_template

  let $runid_line    = $PAY039_RUN_ID||'   '||$run_id
  let $company_line  = $PAY039_COMPANY||'  '||$company
  let $paygroup_line = $PAY039_PAYGROUP||' '||$paygroup
  let $end_dt_line   = $PAY039_END_DT||' '||$pay_end_dt

  if rtrim($run_id,' ') <> ''
     let $right_hdr_line_1 = $runid_line
     let $right_hdr_line_2 = ' '
     let $right_hdr_line_3 = ' '
  else
     let $right_hdr_line_1 = $company_line
     let $right_hdr_line_2 = $paygroup_line
     let $right_hdr_line_3 = $end_dt_line
  end-if


  let #right_margin = {ColR} - 2



  print $buhr_line              (+2,1)
  print $right_hdr_line_1       (,#right_margin)
  print $bugl_line              (+1,1)
  print $right_hdr_line_2       (,#right_margin)
  print $jt_line                (+1,1)
  print $right_hdr_line_3       (,#right_margin)


  print $PAY039_GL_HDG1       (+1,33,22)
  print $PAY039_PY_HDG1       (,81)
  print $PAY039_GL_HDG2       (,102)

  print $PAY039_CURR_CD       (,122)
  print $PAY039_CONVERSION    (,142)

  print $PAY039_LINE_DESCR    (+1,1,30)
  print $PAY039_ACCOUNT       (,+2,10)
  print $PAY039_DEPT          (,+2,10)
  print $PAY039_CHECK         (,+2,10)
  print $PAY039_CHECK_DT      (,+2,10)
  print $PAY039_FOREIGN_AMT   (,+2,15)
  print $PAY039_MONETARY_AMT  (,+6,16)
  print $PAY039_PY_HDG2       (,+5,7)
  print $PAY039_GL_HDG3       (,+2,3)
  print $PAY039_MULTIPLIER    (,+2,15)
  print $PAY039_DIVISOR       (,+2,15)

end-HEADING

!*********************************
begin-procedure REPORT-TRANSLATION
!*********************************

  let $language_cd = $PRCS_LANGUAGE_CD
  do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PAY039', 'ACCOUNT',          $PAY039_ACCOUNT,     #DW)
  do Get_Field_Information ('PAY039', 'AFFILIATE',        $PAY039_AFFILIATE,   #DW)
  do Get_Field_Information ('PAY039', 'ALT_ACCT',         $PAY039_ALT_ACCT,    #DW)
  do Get_Field_Information ('PAY039', 'AMOUNT',           $PAY039_AMOUNT,      #DW)
  do Get_Field_Information ('PAY039', 'BUDGET_REF',       $PAY039_BUDGET_REF,  #DW)
  do Get_Field_Information ('PAY039', 'BU_GL',            $PAY039_BU_GL,       #DW)
  do Get_Field_Information ('PAY039', 'BU_HR',            $PAY039_BU_HR,       #DW)
  do Get_Field_Information ('PAY039', 'CHARTFIELD1',      $PAY039_CHARTFIELD1, #DW)
  do Get_Field_Information ('PAY039', 'CHARTFIELD2',      $PAY039_CHARTFIELD2, #DW)
  do Get_Field_Information ('PAY039', 'CHARTFIELD3',      $PAY039_CHARTFIELD3, #DW)
  do Get_Field_Information ('PAY039', 'CHARTFLD_DTL',     $PAY039_CHARTFLD_DTL,#DW)
  do Get_Field_Information ('PAY039', 'CHECK',            $PAY039_CHECK,       #DW)
  do Get_Field_Information ('PAY039', 'CHECK_DT',         $PAY039_CHECK_DT,    #DW)
  do Get_Field_Information ('PAY039', 'CLASS',            $PAY039_CLASS,       #DW)
  do Get_Field_Information ('PAY039', 'COMPANY',          $PAY039_COMPANY,     #DW)
  do Get_Field_Information ('PAY039', 'CONVERSION',       $PAY039_CONVERSION,  #DW)
  do Get_Field_Information ('PAY039', 'CURR_CD',          $PAY039_CURR_CD,     #DW)
  do Get_Field_Information ('PAY039', 'DEPT',             $PAY039_DEPT,        #DW)
  do Get_Field_Information ('PAY039', 'DIVISOR',          $PAY039_DIVISOR,     #DW)
  do Get_Field_Information ('PAY039', 'END_DT',           $PAY039_END_DT,      #DW)
  do Get_Field_Information ('PAY039', 'FOREIGN_AMT',      $PAY039_FOREIGN_AMT, #DW)
  do Get_Field_Information ('PAY039', 'FUND',             $PAY039_FUND,        #DW)
  do Get_Field_Information ('PAY039', 'GL_HDG1',          $PAY039_GL_HDG1,     #DW)
  do Get_Field_Information ('PAY039', 'GL_HDG2',          $PAY039_GL_HDG2,     #DW)
  do Get_Field_Information ('PAY039', 'GL_HDG3',          $PAY039_GL_HDG3,     #DW)
  do Get_Field_Information ('PAY039', 'JRNL_TMPLT',       $PAY039_JRNL_TMPLT,  #DW)
  do Get_Field_Information ('PAY039', 'LINE_DESCR',       $PAY039_LINE_DESCR,  #DW)
  do Get_Field_Information ('PAY039', 'MONETARY_AMT',     $PAY039_MONETARY_AMT,#DW)
  do Get_Field_Information ('PAY039', 'MULTIPLIER',       $PAY039_MULTIPLIER,  #DW)
  do Get_Field_Information ('PAY039', 'OPER_UNIT',        $PAY039_OPER_UNIT,   #DW)
  do Get_Field_Information ('PAY039', 'PAYGROUP',         $PAY039_PAYGROUP,    #DW)
  do Get_Field_Information ('PAY039', 'PRODUCT',          $PAY039_PRODUCT,     #DW)
  do Get_Field_Information ('PAY039', 'PROGRAM',          $PAY039_PROGRAM,     #DW)
  do Get_Field_Information ('PAY039', 'PROJECT',          $PAY039_PROJECT,     #DW)
  do Get_Field_Information ('PAY039', 'PY_HDG1',          $PAY039_PY_HDG1,     #DW)
  do Get_Field_Information ('PAY039', 'PY_HDG2',          $PAY039_PY_HDG2,     #DW)
  do Get_Field_Information ('PAY039', 'REPORT_TITLE',     $REPORTTITLE,        #DW)
  do Get_Field_Information ('PAY039', 'RUN_ID',           $PAY039_RUN_ID,      #DW)
  do Get_Field_Information ('PAY039', 'TOTAL',            $PAY039_TOTAL,       #DW)

end-procedure

!****************************
begin-procedure Process-Main
!****************************

    let #total_foreign_amount  = 0
    let #total_monetary_amount = 0
!   let #grd_total_foreign_amt = 0
!   let #grd_total_mnetary_amt = 0

begin-SELECT

A.RUN_DT                () on-break  print=never before=get-run-id level = 1
A.SEQNUM                () on-break  print=never before=get-run-id level = 1
A.BUSINESS_UNIT         () on-break  print=never after=change-page level = 1
A.BUSINESS_UNIT_GL
A.APPL_JRNL_ID
A.LINE_DESCR            (+1,1,30) on-break print=always skiplines = 2 after=print-totals level = 2
A.ACCOUNT               (,+2,10)
A.DEPTID                (,+2,10)
A.PROJECT_ID
A.PRODUCT
A.FUND_CODE
A.PROGRAM_CODE
A.CLASS_FLD
A.AFFILIATE
A.OPERATING_UNIT
A.ALTACCT
A.BUDGET_REF
A.CHARTFIELD1
A.CHARTFIELD2
A.CHARTFIELD3
A.AFFILIATE_INTRA1
A.AFFILIATE_INTRA2
A.JRNL_LN_REF           (,+2,10)
A.ACCOUNTING_DT
    do Format-Datetime (&A.accounting_dt, $AcctDate, {DEFMDY},'','')
    print $AcctDate     (,+2,10)
A.FOREIGN_AMOUNT        (,+2,19)  edit 999999999999999.99
A.MONETARY_AMOUNT       (,+2,19) edit 999999999999999.99
A.FOREIGN_CURRENCY      (,+4,3)
A.CURRENCY_CD           (,+4,3)
A.RATE_MULT             (,+2,15)
A.RATE_DIV              (,+1,15)

    let $PROJECT_ID         = RTRIM(&A.PROJECT_ID, ' ')
    let $PRODUCT            = RTRIM(&A.PRODUCT, ' ')
    let $FUND_CODE          = RTRIM(&A.FUND_CODE, ' ')
    let $PROGRAM_CODE       = RTRIM(&A.PROGRAM_CODE, ' ')
    let $CLASS_FLD          = RTRIM(&A.CLASS_FLD, ' ')
    let $AFFILIATE          = RTRIM(&A.AFFILIATE, ' ')
    let $OPERATING_UNIT     = RTRIM(&A.OPERATING_UNIT, ' ')
    let $ALTACCT            = RTRIM(&A.ALTACCT, ' ')
    let $BUDGET_REF         = RTRIM(&A.BUDGET_REF, ' ')
    let $CHARTFIELD1        = RTRIM(&A.CHARTFIELD1, ' ')
    let $CHARTFIELD2        = RTRIM(&A.CHARTFIELD2, ' ')
    let $CHARTFIELD3        = RTRIM(&A.CHARTFIELD3, ' ')
    let $AFFILIATE_INTRA1   = RTRIM(&A.AFFILIATE_INTRA1, ' ')
    let $AFFILIATE_INTRA2   = RTRIM(&A.AFFILIATE_INTRA2, ' ')


    do Format-CF-Print-Line ($DynCF_Filler, $DynCF_Filler, $PROJECT_ID, $PRODUCT, $FUND_CODE, $PROGRAM_CODE,
                             $CLASS_FLD, $AFFILIATE, $OPERATING_UNIT, $ALTACCT, $BUDGET_REF, $CHARTFIELD1,
                             $CHARTFIELD2, $CHARTFIELD3, $AFFILIATE_INTRA1, $AFFILIATE_INTRA2)

    if   $PROJECT_ID       <> ''
      or $PRODUCT          <> ''
      or $FUND_CODE        <> ''
      or $PROGRAM_CODE     <> ''
      or $CLASS_FLD        <> ''
      or $AFFILIATE        <> ''
      or $OPERATING_UNIT   <> ''
      or $ALTACCT          <> ''
      or $BUDGET_REF       <> ''
      or $CHARTFIELD1      <> ''
      or $CHARTFIELD2      <> ''
      or $CHARTFIELD3      <> ''
      or $AFFILIATE_INTRA1 <> ''
      or $AFFILIATE_INTRA2 <> ''

        print $PAY039_CHARTFLD_DTL     (+1,6,20)
        let #AdvLineNo = 0
        do Print-CF-Labels-and-Values (#AdvLineNo, #StrLine)
    end-if

    let $business_unit_hr = &A.BUSINESS_UNIT
    let $business_unit_gl = &A.BUSINESS_UNIT_GL
    let $journal_template = &A.APPL_JRNL_ID
    do Format-Datetime (&A.RUN_DT, $run_date, {DEFMDY},'','')
    move &A.SEQNUM to $run_seq
    let $run_id = &B.Run_id

    let #total_foreign_amount   = #total_foreign_amount   + &A.FOREIGN_AMOUNT
    let #total_monetary_amount  = #total_monetary_amount  + &A.MONETARY_AMOUNT

!   let #grd_total_foreign_amt  = #grd_total_foreign_amt  + &A.FOREIGN_AMOUNT
!   let #grd_total_mnetary_amt  = #grd_total_mnetary_amt  + &A.MONETARY_AMOUNT


FROM PS_HR_ACCTG_LINE A
[$WhereParam]
ORDER BY A.RUN_DT,A.SEQNUM,A.BUSINESS_UNIT, A.LINE_DESCR, A.JRNL_LN_REF


end-SELECT

end-procedure


!****************************
begin-procedure print-totals
!****************************

  print '-' (+1,81,19) fill
  print '-' (,102,19) fill

  print $PAY039_TOTAL (+1,1)
  print #total_foreign_amount  (,80)  edit 9999999999999999.99
  print #total_monetary_amount (,101)  edit 9999999999999999.99

  let #total_foreign_amount  = 0
  let #total_monetary_amount = 0

end-procedure print-totals


!*********************************
begin-procedure print-grand-total
!*********************************

!  print '-' (+1,81,19) fill
!  print '-' (,102,19)   fill

!  print 'Grand Totals '        (+1,1)
!  print #grd_total_foreign_amt (,80)   edit 99999999999999999.99
!  print #grd_total_mnetary_amt (,101)   edit 99999999999999999.99

end-procedure print-grand-total



!***************************
begin-procedure change-page
!***************************

 new-page

end-procedure change-page


!**************************
begin-procedure get-run-id
!**************************

begin-SELECT

B.RUN_ID
B.COMPANY
B.PAYGROUP
B.PAY_END_DT

  let $run_id      = &B.RUN_ID
  let $company     = &B.COMPANY
  let $paygroup    = &B.PAYGROUP
  do Format-Datetime (&B.PAY_END_DT, $pay_end_dt, {DEFMDY},'','')


FROM PS_GL_GEN_HISTORY B
WHERE B.RUN_DT = &A.RUN_DT AND B.SEQNUM = &A.SEQNUM

end-SELECT

end-procedure get-run-id

!******************************
begin-procedure Get-Run-Control
!******************************

  let $WhereParam = ''

begin-select
RC.RUN_DT
RC.THRU_DT_RUN
RC.BUSINESS_UNIT_GL

  let $RC_RunDt     =  ltrim(rtrim(&RC.RUN_DT,' '),' ')
  let $RC_ThruDt    =  ltrim(rtrim(&RC.THRU_DT_RUN,' '),' ')
  let $RC.BusUnitGL =  ltrim(rtrim(&RC.BUSINESS_UNIT_GL,' '),' ')

  if $RC_RunDt = ''
      let $WhereParam = ''
  else
      let $WhereParam = 'WHERE A.RUN_DT BETWEEN ' || '''' || $RC_RunDt || '''' ||
                        ' AND ' || '''' || $RC_ThruDt || ''''
  end-if

  if $RC.BusUnitGL <> ''
      if $WhereParam = ''
          let $WhereParam = 'WHERE A.BUSINESS_UNIT_GL = ' || '''' || $RC.BusUnitGL || ''''
      else
          let $WhereParam = $WhereParam || ' AND A.BUSINESS_UNIT_GL = ' || '''' || $RC.BusUnitGL || ''''
      end-if
  end-if

from PS_RUN_CNTL_PAY039 RC
where RC.OPRID = $prcs_oprid
  and RC.RUN_CNTL_ID = $prcs_run_cntl_id
end-select

end-procedure Get-Run-Control


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language


