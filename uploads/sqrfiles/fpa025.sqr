!***********************************************************************
!  FPA025:  Status on appraisals (done or to be done)                  *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/07/19:12:27:15                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#Define FPMLEGALSTA_TBL
#Include 'rellang.sqc'        ! Related Language File

#include 'setenv.sqc'         ! Set environment
#include 'setup01.sqc'        ! Printer and page-size initialization


BEGIN-REPORT

Do Init-DateTime
Do Init-Number

    do Get-Current-DateTime()
    display  'AsOfToday :' noline
    display  $AsOfToday

do Init-Report
do FOND-DE-PAGE
do Stdapi-Term

END-REPORT



!***************************************************************
!
!
!       Init-Report
!
!
!***************************************************************
BEGIN-PROCEDURE  Init-Report

  move 'FPA025' to $ReportID

  do Stdapi-Init

  let $language_cd = $CURR_LANGUAGE_CD

  do Init_Report_Translation($ReportID, $language_cd)
  do Append_Report_Translation($ReportID)

  do Get_Field_Information($ReportID, 'REPORT_TITLE', $ReportTitle, #DW)

  do Security-Param       !SQR security

 if $prcs_process_instance = ''
    INPUT $FromDate 'Enter start review date'
    INPUT $ThruDate 'Enter end review date'
    INPUT $Corps    'Enter corps'
    INPUT $Emplid   'Enter reviewer'
    INPUT $YesNo    'Employees reviewed (Y/N)'
 else
    do FP-Select-Parameters
    do Select-Parameters
 end-if

 do Convert-To-DTU-Date($FromDate ,$dt_cmp_FromDate)
 !let $FromDate = $dt_cmp_FromDate

 do Convert-To-DTU-Date($ThruDate ,$dt_cmp_ThruDate)
 !let $ThruDate = $dt_cmp_ThruDate

END-PROCEDURE Init-Report



!***************************************************************
!
!
!       Get-Values
!
!
!***************************************************************

BEGIN-PROCEDURE  Get-Values

do FP-Get-From-Thru-Date
do Get-Corps
do Get-Emplid
do Get-YesNo

END-PROCEDURE Get-Values



!*********************************
!
!      FOND-DE-PAGE
!
!*********************************

BEGIN-PROCEDURE FOND-DE-PAGE

! display $YesNo
! display $corps

position (+1,1)
let $titre = $ReportTitle
uppercase $titre
print $titre () center

position (+1,1)
   do Get_Field_Information($ReportID, 'PERIOD'  , $PERIOD_LBL  , #DW)
   do Get_Field_Information($ReportID, 'TO'  , $TO_LBL  , #DW)
let $periode = $PERIOD_LBL || ' ' || $dt_cmp_FromDate || ' ' || $TO_LBL || ' ' || $dt_cmp_ThruDate
uppercase $periode
print $periode () center

position (+5,1)

   do Get_Field_Information($ReportID, 'CORPS'  , $CORPS_LBL   , #DW)
   do Get_Field_Information($ReportID, 'NAME'   , $NAME_LBL    , #DW)
   do Get_Field_Information($ReportID, 'POS'    , $POS_LBL     , #DW)
   do Get_Field_Information($ReportID, 'NOTE'   , $NOTE_LBL    , #DW)
   do Get_Field_Information($ReportID, 'EVAL'   , $EVAL_LBL    , #DW)
   do Get_Field_Information($ReportID, 'STAT'   , $STAT_LBL    , #DW)

print $CORPS_LBL  (,5,)
print $NAME_LBL   (,35,)
print $POS_LBL    (,69,)
if $YesNo  = 'Y'
        print $NOTE_LBL (,85,7)
end-if
print '|'      (,25,1)
print '|'      (,64,1)
print '|'      (,80,1)
print '|'      (+1,12,1)
print '|'      (,25,1)
print '|'      (,64,1)
print '|'      (,80,1)
print $EVAL_LBL  (,7,)
print $STAT_LBL (,68,)

print '_' (+1,5,90) Fill

BEGIN-SQL
 DELETE FROM PS_FPMREVIEWER_TMP WHERE OPRID = $prcs_oprid
END-SQL

BEGIN-SELECT
F.EP_APPRAISAL_ID

 let #ReviewerMax = 1
 do Get-Reviewers(&F.EP_APPRAISAL_ID, ' ', #ReviewerMax)
 get $ReviewerId from Reviewers(1) ReviewerId
 let $ReviewerId = RTRIM($ReviewerId, ' ')
 move &F.EP_APPRAISAL_ID to $appraisalid B9999999
 if $emplid = '' or $emplid = $ReviewerId
   do Insert-ReviewerID($appraisalid, $ReviewerId)
 end-if

from PS_EP_APPR F, PS_FPAEECAREER G
where F.EMPLID = G.EMPLID
and F.EMPL_RCD = G.EMPL_RCD
and F.PERIOD_BEGIN_DT >= {DATEIN-PREFIX}$FromDate{DATEIN-SUFFIX}
and F.PERIOD_END_DT <= {DATEIN-PREFIX}$ThruDate{DATEIN-SUFFIX}
and G.EFFDT = (select max(G2.EFFDT) from PS_FPAEECAREER G2
    where G2.EMPLID = G.EMPLID
    and G2.EMPL_RCD = G.EMPL_RCD
    and G2.EFFDT <= F.PERIOD_END_DT)
and G.EFFSEQ = (select max(G3.EFFSEQ) from PS_FPAEECAREER G3
    where G3.EMPLID = G.EMPLID
    and G3.EMPL_RCD = G.EMPL_RCD
    and G3.EFFDT = G.EFFDT)
and NOT EXISTS (SELECT 'X' FROM PS_FPAREVIEW_HIST FPAHIST WHERE FPAHIST.EP_APPRAISAL_ID = F.EP_APPRAISAL_ID)
END-SELECT

if $YesNo  = 'Y'
        do SELECT-AGENT
else
        do SELECT-AGENTN
end-if

BEGIN-SQL
 DELETE FROM PS_FPMREVIEWER_TMP WHERE OPRID = $prcs_oprid
END-SQL

END-PROCEDURE FOND-DE-PAGE


!*********************************
!
!       SELECT-AGENT
!
!*********************************

 BEGIN-PROCEDURE SELECT-AGENT

! Select rated employees
!-----------------------

display 'select-agent'



! Dynamic where clause
!---------------------

let $select-where =
      '{DATEIN-PREFIX}''' || $FromDate  ||'''' ||'{DYN-DateIn-Suffix} <= A.PERIOD_BEGIN_DT
and {DATEIN-PREFIX}'|| '''' || $ThruDate ||'''' ||'{DYN-DateIn-Suffix} >= A.PERIOD_END_DT' ||
' and   E.EMPLID = A.EMPLID
and   E.EMPL_RCD = A.EMPL_RCD
and   E.EFFDT =
( select max(E1.EFFDT) from PS_FPAEECAREER E1
where E1.EMPLID = A.EMPLID
and   E1.EMPL_RCD = A.EMPL_RCD
and E1.EFFDT <= A.PERIOD_END_DT)' ||
' and   A.EP_REVIEW_TYPE <> ''*''
and   C.EMPLID = A.EMPLID
and   F.EMPLID = A.EMPLID
and   F.EMPL_RCD = A.EMPL_RCD
and   H.OPRID = ''' || $prcs_oprid ||
''' and H.EP_APPRAISAL_ID = A.EP_APPRAISAL_ID
and H.REVIEWER_ID = D.EMPLID'

let $select-where =  $select-where ||
' and   F.EFFDT =
       (select max(EFFDT)
        from PS_FPAEELEGALSTAT
        where EMPLID= A.EMPLID
        and EMPL_RCD = A.EMPL_RCD
        and EFFDT <= A.PERIOD_END_DT)
and   G.FP_LEGALSTAT_CD = F.FP_LEGALSTAT_CD
and   G.EFF_STATUS = ''A''
and   G.EFFDT = '

let $select-where =  $select-where ||
      '( select max(EFFDT)
        from PS_FPMLEGALSTA_TBL
        where FP_LEGALSTAT_CD = F.FP_LEGALSTAT_CD
        and EFFDT <= F.EFFDT)'

if $corps <> '' and $corps <> ' '
let $select-where =  $select-where ||
' and   E.FP_CORPS_CD = ''' || $corps ||''' '
end-if

display $select-where


BEGIN-SELECT
E.FP_CORPS_CD,
D.NAME,
C.NAME,
A.EP_RATING,
G.DESCRSHORT,
G.EFFDT,                      !Translation purpose
G.FP_LEGALSTAT_CD             !Translation purpose

!        display 'print verticale1'
        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

        print &E.FP_CORPS_CD (,5,20) ON-BREAK PRINT=change
                                          set = 1
                                          level = 1
                                          after = EDIT-CORPS

!        display 'print verticale2'
        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

        print &D.NAME (,7,20) ON-BREAK PRINT=change
                                          set = 1
                                          level = 2
                                          save = $nom_eval
                                          after= EDIT-EVAL

!        display 'print verticale3'
        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

        print &C.NAME (,27,20) ON-BREAK PRINT=change
                                          set = 1
                                          level = 3
                                          after= EDIT-AGENT
!        display &A.EP_RATING

        Let $FPMLEGALSTA_TBL-DESCRSHORT = &G.DESCRSHORT              !
        Do Get_Related_FPMLEGALSTA_TBL(&G.EFFDT,&G.FP_LEGALSTAT_CD)  !Translation function
        print $FPMLEGALSTA_TBL-DESCRSHORT   (,67,10)                 !

        print &A.EP_RATING              (,83,)

        display 'End of the loop'

from PS_EP_APPR A,
     PS_PERSON_NAME C ,  PS_PERSON_NAME D , PS_FPAEECAREER E ,
     PS_FPAEELEGALSTAT F,  PS_FPMLEGALSTA_TBL G, PS_FPMREVIEWER_TMP H,
     PS_FPFAST_PERS_VW2 SCRTY                                           !SQR security

where   [$select-where]
and     C.EMPLID = SCRTY.EMPLID                                         !
and F.EMPL_RCD = SCRTY.EMPL_RCD                                         !SQR security
        [$SecurityClause]                                               !
and NOT EXISTS (SELECT 'X' FROM PS_FPAREVIEW_HIST FPAHIST2 WHERE FPAHIST2.EP_APPRAISAL_ID = A.EP_APPRAISAL_ID)
order by  E.FP_CORPS_CD, D.NAME, C.NAME

END-SELECT

END-PROCEDURE SELECT-AGENT


!*********************************
!
!       SELECT-AGENTN
!
!*********************************

 BEGIN-PROCEDURE SELECT-AGENTN

! Select non-rated employees
!---------------------------

display 'select-agentn'
! Dynamic where clause

!---------------------
let $select-where =
'Y.EMPLID not in
        (select EMPLID from  PS_EP_APPR
        where {DATEIN-PREFIX}'|| '''' || $FromDate  ||'''' ||'{DYN-DateIn-Suffix} <= PERIOD_BEGIN_DT
         and  {DATEIN-PREFIX}'|| '''' || $ThruDate ||'''' ||'{DYN-DateIn-Suffix} >= PERIOD_END_DT )
and   Y.EMPLID = X.EMPLID
and   Y.EMPLID = W.EMPLID
and   Y.EMPL_RCD = W.EMPL_RCD
and   W.EFFDT =  '


let $select-where =  $select-where ||
 '(select max(EFFDT)
        from PS_FPAEELEGALSTAT
        where EMPLID= Y.EMPLID
        and EMPL_RCD = Y.EMPL_RCD
        and EFFDT <=Y.EFFDT)
and   Z.FP_LEGALSTAT_CD = W.FP_LEGALSTAT_CD
and   Z.EFF_STATUS = ''A''
and   Z.EFFDT = '

let $select-where =  $select-where ||
     ' ( select max(EFFDT)
        from PS_FPMLEGALSTA_TBL
        where FP_LEGALSTAT_CD = W.FP_LEGALSTAT_CD
        and   EFFDT <= W.EFFDT) '

if $corps <> '' and $corps <> ' '
let $select-where =  $select-where ||
' and   Y.FP_CORPS_CD = ''' || $corps ||''' '
end-if

display $select-where

BEGIN-SELECT

Y.FP_CORPS_CD,
X.NAME,
Z.DESCRSHORT,
Z.EFFDT,                      !Translation purpose
Z.FP_LEGALSTAT_CD             !Translation purpose

        display 'print verticale1'
        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

        print &Y.FP_CORPS_CD (+2,5,20) ON-BREAK PRINT=change
                                          set = 1
                                          level = 1
                                          before= EDIT-CORPS

        display 'print verticale2'
        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

        print &X.NAME (+1,27,20) ON-BREAK PRINT=change
                                          set = 1
                                          level = 2
                                          !save = $nom_agent
                                          after= EDIT-AGENT

        Let $FPMLEGALSTA_TBL-DESCRSHORT = &Z.DESCRSHORT              !
        Do Get_Related_FPMLEGALSTA_TBL(&Z.EFFDT,&Z.FP_LEGALSTAT_CD)  !Translation function
        print $FPMLEGALSTA_TBL-DESCRSHORT   (,67,10)                 !

        display 'print verticale3'
        print '|'      (+1,25,1)
        print '|'      (,64,1)
        print '|'      (,80,1)

from    PS_PERSON_NAME X, PS_FPAEECAREER Y , PS_FPAEELEGALSTAT W,
        PS_FPMLEGALSTA_TBL Z,
        PS_FPFAST_PERS_VW2 SCRTY           !SQR security

where   [$select-where]
and     Y.EMPLID = SCRTY.EMPLID            !
and Y.EMPL_RCD = SCRTY.EMPL_RCD   !SQR security
        [$SecurityClause]                  !

order by Y.FP_CORPS_CD,X.NAME

END-SELECT

END-PROCEDURE SELECT-AGENTN



!*********************************
!
!       EDIT-CORPS
!
!*********************************

BEGIN-PROCEDURE EDIT-CORPS

display 'edit-corps'

print '|'      (,25,1)
print '|'      (,64,1)
print '|'      (,80,1)

END-PROCEDURE EDIT-CORPS



!*********************************
!
!       EDIT-EVAL
!
!*********************************

BEGIN-PROCEDURE EDIT-EVAL

display 'edit-eval'

print '|'      (,25,1)
print '|'      (,64,1)
print '|'      (,80,1)

END-PROCEDURE EDIT-EVAL



!*********************************
!
!       EDIT-AGENT
!
!*********************************

BEGIN-PROCEDURE EDIT-AGENT

print '|'      (,25,1)
print '|'      (,64,1)
print '|'      (,80,1)

END-PROCEDURE EDIT-AGENT

!-------------------------------------------
! Procedure Insert-ReviewerID(&F.EP_APPRAISAL_ID, $emplid)
!-------------------------------------------
! Description : Store the reviewer ID for each appraisal
! In  : $appraisalid, $emplid
! Out : none
!-------------------------------------------
BEGIN-PROCEDURE Insert-ReviewerID($appraisalid, $emplid)

BEGIN-SQL On-Error=SQL-Error
INSERT INTO PS_FPMREVIEWER_TMP
(OPRID,
 EP_APPRAISAL_ID,
 REVIEWER_ID)
 values ($_prcs_oprid,
 $appraisalid,
 $emplid)
END-SQL
END-PROCEDURE Insert-ReviewerID


#Include 'fptestdt.sqc'       ! Routines for date and time formatting
#include 'curdttim.sqc'         ! Get-Current-DateTime procedure
#include 'stdapi.sqc'           ! Routines to update run status
#Include 'hrrnctl1.sqc'         ! Get run control parameter values
#Include 'fprnctl1.sqc'         ! Get run control parameter values
#Include 'fpgetval.sqc'         ! Get values mask routines
#Include 'hrgetval.sqc'         ! Get values mask routines
#Include 'eputils.sqc'          ! Get reviewer API fonctions
#Include 'number.sqc'           ! Routines to format numbers
#Include 'hrsecty.sqc'          ! Get SQR Security parameters
#Include 'sqrtrans.sqc'         ! Translate SQR strings to given language
