!***********************************************************************
!  PER801SG : Created Based on a requirement from Singapore Payroll    * 
!            Employee Listing By user accepted Report - type           *
!            By (D)epartment                                           *
!               Employee (I)d                                          *
!               Employee (N)ame                                        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!                                                                      
!          $Date:  2006/07/19:15:12:32                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
#include 'setenv.sqc' !Set environment

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-setup

#Include 'ptset02a.sqc'  !Printer and page-size initialization
end-setup

begin-procedure Init-Report

  move 'Employees Listing'  to $ReportTitle
  move 'PER801SG' to $ReportID
  display $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
    do GetUserOptions             ! Accept the Ordering of the Report
  else
    do Select-Parameters
  end-if
end-procedure


begin-procedure Get-Values
  move &RUN_CNTL_HR.EMP_LIST_CRITERIA to $ReportType
  uppercase $ReportType
end-procedure


begin-heading 7
  #Include 'stdhdg01.sqc'

  IF $ReportType = 'D' or $ReportType = 'd'
    PRINT 'Department Name'  (+2,1,30)
    PRINT 'Empl Id'          (0,+2,11)
    PRINT 'Employee Name'    (0,+2,40)
    PRINT 'Sex'              (0,+2,3)
    PRINT 'Birth Date'       (0,+2,10)
    PRINT 'Job Title'        (0,+2)
    PRINT '-'                (+1,1,176) FILL
  END-IF

  IF $ReportType = 'I' or $ReportType = 'i'
    PRINT 'Empl Id'          (+2,1,11)
    PRINT 'Employee Name'    (0,+2,40)
    PRINT 'Sex'              (0,+2,3)
    PRINT 'Birth Date'       (0,+2,10)
    PRINT 'Department Name'  (0,+2,30)
    PRINT 'Job Title'        (0,+2)
    PRINT '-'                (+1,1,176) FILL
  END-IF

  IF $ReportType = 'N' or $ReportType = 'n'
    PRINT 'Employee Name'    (+2,1,40)
    PRINT 'Empl Id'          (0,+2,11)
    PRINT 'Sex'              (0,+2,3)
    PRINT 'Birth Date'       (0,+2,10)
    PRINT 'Department Name'  (0,+2,30)
    PRINT 'Job Title'        (0,+2)
    PRINT '-'                (+1,1,176) FILL
  END-IF

end-heading


begin-procedure Process-Main

  Let #TotalCount = 0

  IF $ReportType = 'D' or $ReportType = 'd'
     Show (17,1) 'Generating Employees Listing by  Department '
     Do OrderByDepartment
  END-IF
  IF $ReportType = 'I' OR $ReportType = 'I'
     Show (17,1) 'Generating Employees Listing by ID '
     Do OrderByID
  END-IF
  IF $ReportType = 'N' OR $ReportType = 'N '
     Do OrderByName
     Show (17,1) 'Generating Employees Listing by Name '
  END-IF
  Show (+3,1) ''

  PRINT   '.' (+2,40,90) Fill
  PRINT   'Total No of Employees in the Organisation ' (0,40)
  PRINT   #TotalCount                                  (0,+55)  EDIT 999,999,999


End-Procedure


!---------Procedure to List the Employees Ordered by Department Name
begin-procedure OrderByDepartment
begin-SELECT
DEPTNAME
EMPLID
NAME
SEX
BIRTHDATE &BirthDate
   Do  Format-DateTime(&BirthDate,$Bdate,{DEFDMY},'','')
JOBTITLE

 PRINT &DeptName          (+1,1,30)  ON-BREAK  PRINT=change/top-page
                                                SKIPLINES=2
 PRINT &Emplid            (0,+2,11)
 PRINT &Name              (0,+2,40)
 PRINT &Sex               (0,+3,1)
 PRINT $Bdate             (0,+3,10)
 PRINT &JobTitle          (0,+2)

 Add 1 to #TotalCount

FROM  PS_EMPLOYEES
ORDER BY 1,2
end-SELECT

end-procedure


!---------Procedure to List the Employees ordered by Employee ID
begin-procedure OrderById
begin-SELECT
DEPTNAME  &I_DeptName
EMPLID    &I_Emplid
NAME      &I_Name
SEX       &I_Sex
BIRTHDATE &I_BirthDate
   Do  Format-DateTime(&I_BirthDate,$Bdate,{DEFDMY},'','')
JOBTITLE  &I_JobTitle

 PRINT &I_Emplid            (+1,1,11)
 PRINT &I_Name              (0,+2,40)
 PRINT &I_Sex               (0,+3,1)
 PRINT $Bdate               (0,+3,10)
 PRINT &I_DeptName          (0,+2,30)
 PRINT &I_JobTitle          (0,+2)

 Add 1 to #TotalCount

FROM  PS_EMPLOYEES
ORDER BY 2
end-SELECT

end-procedure


!---------Procedure to List the Employees ordered by Employee Name
begin-procedure OrderByName
display 'procedure OrderByName'
begin-SELECT
DEPTNAME  &N_DeptName
EMPLID    &N_Emplid
NAME      &N_Name
SEX       &N_Sex
BIRTHDATE &N_BirthDate
   Do  Format-DateTime(&N_BirthDate,$Bdate,{DEFDMY},'','')
JOBTITLE  &N_JobTitle

 PRINT &N_Name              (+1,1,40)
 PRINT &N_Emplid            (0,+2,11)
 PRINT &N_Sex               (0,+3,1)
 PRINT $Bdate               (0,+3,10)
 PRINT &N_DeptName          (0,+2,30)
 PRINT &N_JobTitle          (0,+2)

 Add 1 to #TotalCount

FROM  PS_EMPLOYEES
ORDER BY 3
end-SELECT

end-procedure


begin-procedure GetUserOptions
  Let $ReportType = ' '
  Show (1,1)   CLEAR-SCREEN

  REVERSE      (07,01) 'REPORT TYPE'
  NORMAL       (09,05) 'By ('
  BOLD         (0,0)  'D'
  NORMAL       (0,0)  ')epartment'
  NORMAL       (10,05) 'By Employee ('
  BOLD         (0,0)  'I'
  NORMAL       (0,0)   ')d'
  NORMAL       (11,05) 'By Employee ('
  BOLD         (0,0)  'N'
  NORMAL       (0,0)   ')ame'

  Accept_Input:
        Show (13,01) 'Choose : '

        input $ReportType 'Select Report Ordering' MAXLEN=1 TYPE=Char STATUS = #RtypStat

        IF #RtypStat != 0
            Show (13,41) Clear-line Beep
            goto Accept_input
        END-IF

        IF $ReportType != 'D'  AND $ReportType != 'd'  AND
           $ReportType != 'I'  AND $ReportType != 'i'  AND
           $ReportType != 'N'  AND $ReportType != 'n'
           Show (13,41) Clear-line Beep
           goto Accept_input
        END-IF

end-procedure


begin-procedure select-parameters

BEGIN-SELECT

RUN_CNTL_HR.EMP_LIST_CRITERIA

   do Get-Values

from PS_RUN_CNTL_HR RUN_CNTL_HR

where RUN_CNTL_HR.OPRID = $prcs_oprid
  and RUN_CNTL_HR.RUN_CNTL_ID = $prcs_run_cntl_id
end-select

end-procedure


 ! Include 'hrrnctl1.sqc'   !Get Parameters

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status

