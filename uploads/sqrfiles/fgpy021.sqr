!***********************************************************************
! Federal Government Payroll                                           *
! FGPY021:  1523 Supplemental Semiannual Headcount Report              *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:03:50:33                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!  $Date    :: 12/21/2000  3:53p  $                                    *
!                                                                      *
!***********************************************************************
! Description:                                                         *
!                                                                      *
! Program designed to run on an HP Printer please set the command      *
! '-printer:hp' in your sqr command line when submitting.              *
!                                                                      *
! Core funtionality based off of calculations performed in generating  *
! RITS Interface GHPAY001.SQR                                          *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup31.sqc'   !Printer and page-size initialization
!#Include 'stdhdg03.sqc'

!begin-setup
!#define page_max_cols 125
!#define page_max_lines 125
!page-size 125 125

!DECLARE PRINTER
!    TYPE        = HPLASERJET
!    ORIENTATION = PORTRAIT
!    TOP-MARGIN  = .5
!    LEFT-MARGIN = .5
!    FONT        = 4
!    POINT-SIZE  = 7.5
!    lineS-INCH = 8
!end-setup

begin-report

  do Init-Report
  do Process-2812
  do Write-2812-records
  do Employee-Count
  DO Get-Company-Info
  do Print-2812-report
  do Stdapi-Term

end-report

begin-procedure Employee-Count
begin-select 
COUNT(distinct EC.EMPLID) &Total_Employees
 let #Total_Employees = &Total_Employees
FROM PS_PAY_CHECK EC,
     PS_PAY_CALENDAR CA
WHERE CA.RUN_ID          = $RC.Run_ID
  AND EC.COMPANY         = $COMPANY
  AND EC.PAY_END_DT      = $PAY_END_DT
  AND EC.PAYGROUP        = CA.PAYGROUP
  AND CA.PAY_CONFIRM_RUN = 'Y'

end-select

end-procedure
!**********************************************************
begin-procedure head
!*****************************************************
  let #CL = #current-line

  PRINT 'Semi-Annual Headcount Report ' (0,45) bold

  graphic (+1,1,60)  box 6 2


   let #CL = #current-line

   print '1. Address (Dept., Bureau, Location, and Zip)'  (0,3)
  graphic (0,61,30)  box 2 2
   print '2. Payroll Office Number'  (0,63)
  graphic (0,91,30)  box 2 2
   print '3. Report Number'          (0,93)
   print &A.GVT_AGY_LOC_CODE                  (+1,66)
   print $RC.REPORT_ID       (0,98)
  graphic (+1,61,60) box 2 2
   !print '4. Telephone Number'       (0,63)
   !graphic (0,91,30)  box 2 2
   print '4. Date Payroll Paid'      (0,63)
   !print &A.contact_phone              (+1,66)
   print  &A2.CHECK_DT               (+1,98) EDIT YYYY-MM-DD !***
  graphic (+1,61,60) box 1 2 #shad
   print '5. Pay period' (0,83)
  graphic (+1,61,30) box 2 2
   print 'From ' (0,63)
   print &A2.PAY_BEGIN_DT (0,70)  EDIT YYYY-MM-DD
  graphic (0,91,30) box 2 2
   print 'To ' (0,95)
   print &A2.PAY_END_DT (0,102)   EDIT YYYY-MM-DD
  graphic (+1,1,60) box 6 2
  graphic (+1,61,30) box 2 2
   print '7. Name of Preparer(Print)' (0,63)
  graphic (0,91,30) box 2 2
   print '8. Telephone Number' (0,93)
  print $preparer (+1,63)
  print $tel_number (0,93)
  graphic (+1,61,60) box 1 2
   print '9. I certify that the items listed herein are correct.' (0,63)
  graphic (+1,61,40) box 2 2
   print  'Signature of Authorized Officer' (0,63)
  graphic (0,101,20) box 2 2
 print 'Date' (0,103)
 ! graphic (+2,61,60) box 2 2
 !   add 6 to #CL
   POSITION (3,1)
   print &a.gvt_address1                  (+1,15)
   print &a.gvt_address2                  (+1,15)
   print &a.gvt_address3                  (+1,15)
   let $city_state = rtrim(&a.gvt_address_city,' ') || ', '
       || rtrim(&a.gvt_address_state,' ') || '  ' || &a.gvt_address_zip
   print $city_state                     (+1,15)
      add 6 to #CL
   print 'To:' (#CL,3)
   print 'Office of Personnel Management' (+1,15)
   print 'ATTN: Funds Control Branch'     (+1,15)
   print 'Post Office Box 582'            (+1,15)
   print 'Washington, DC 20044-0582'      (+1,15)
   print ' ' (+1,1)

end-procedure !head

!**************************
begin-procedure Init-Report
!**************************

  move 'PAY1523' to $ReportID
  move '1523 Supplemental Semi-Annual Headcount Report' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  if $Prcs_Process_Instance = ''

   input $RC.Run_ID maxlen=3 'Enter Payroll Run ID' type=char
   input $RC.REPORT_ID maxlen=8 'Report ID' type=char
   input $RC.GVT_ORIGINAL_SUPP maxlen=3 'Original/Supplemental' type=char
!SMW   input $RC.GVT_2812_FILE maxlen=12 'Enter 2812 file Name' type=char
!SMW   input $RC.GVT_2812A_FILE maxlen=12 'Enter 2812A file Name' type=char

   uppercase $RC.Run_ID

  else

    do Select-Run-Control-Data

  end-if

!***** Degree of Shading

  let #shad=12

!***** Page 1 Column Placement

! Page 1 Text Placement
  let #p1 = 3
  let #p2 = 46
  let #p3 = 79
  let #p4 = 109

! Page 1 Box Width
  let #b=30
! Box Columns
  let #b1=1
  let #b2=31
  let #b3=61
  let #b4=91

!***** Page 2 Column Text Placement
  let #c1 = 3
  let #c2 = 44
  let #c3 = 63
  let #c4 = 92
  let #c5 = 112
  let #c6 = 35

! Page 2 Column Placement
  let #cp1 = 1
  let #cp2 = 41
  let #cp3 = 61
  let #cp4 = 81
  let #cp5 = 101
  let #cp6 = 31

! Page 2 Column Width
  let #cw1 = 30
  let #cw2 = 20
  let #cw3 = 20
  let #cw4 = 20
  let #cw5 = 20
  let #cw6 = 10


end-procedure     !Init-Report


!***********************************************************************
!                                                                      *
!  Function:     Select-Run-Control-Data                               *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************

begin-procedure Select-Run-Control-Data

begin-select

RNC.OPRID
RNC.RUN_ID
RNC.COMPANY
   MOVE &RNC.RUN_ID TO $RC.Run_ID
   MOVE &RNC.COMPANY TO $RC.REPORT_ID
   uppercase $RC.Run_ID
from PS_GVT_RUN_CNTL RNC
where RNC.OPRID       = $prcs_oprid
and   RNC.RUN_CNTL_ID = $prcs_run_cntl_id


end-select


end-procedure


!***************************
begin-procedure Get-Company-Info
!***************************
SHOW $RC.Run_ID
SHOW $PAY_END_DT
begin-select

A.GVT_AGY_LOC_CODE
A.CONTACT_PHONE
A.GVT_ADDRESS1
A.GVT_ADDRESS2
A.GVT_ADDRESS3
A.GVT_ADDRESS_CITY
A.GVT_ADDRESS_STATE
A.GVT_ADDRESS_ZIP

 FROM PS_GVT_INTRFC_INFO A
  WHERE A.EFFDT =
        (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL
        WHERE A.COMPANY = COMPANY
          AND EFFDT <= $PAY_END_DT)
    AND A.COMPANY = $RC.REPORT_ID

end-select

end-procedure Get-Company-Info

!***************************
begin-procedure Call-Job
!***************************

begin-select distinct
BT.GVT_RETIRE_PLAN
BT.GVT_ELIG_FEHB
   evaluate $Called-By
    when = 'Contributions'
    evaluate &BT.GVT_RETIRE_PLAN
     when = '1'
      add #DED_CUR to #2-11-nbr
      break
     when = 'K'
      add #DED_CUR to #3-4-nbr
      break
     when = '6'
      add #DED_CUR to #Special_Contributions
      break
     when = 'C'
      add #DED_CUR to #Regular_Offset_Contributions
      break
     when = 'E'
      add #DED_CUR to #Special_Offset_Contributions
      break
     when = 'M'
      add #DED_CUR to #LEO_Contributions
      break
     when = 'L'
      add #DED_CUR to #AT_Controller_Contributions
      break
    end-evaluate !end for Contributions

 when = 'CSRS'
  evaluate &BT.GVT_RETIRE_PLAN
  when = '1'
     let #2-10-nbr = #2-10-nbr + #DED_CUR
     add #CALCULATED_BASE to #CSRS_BASE
    if #DED_NOT_TAKEN > 0
     add 1 to #No_CSRS_Count
    else
     add 1 to #CSRS_Count
    end-if
   break
  when = 'C'
    let #Regular_Offset = #Regular_Offset + #DED_CUR
    add #CALCULATED_BASE to #Regular_Offset_BASE
   if #DED_NOT_TAKEN > 0
    add 1 to #No_Regular_Offset_Count
   else
    add 1 to #Regular_Offset_Count
   end-if
   break
  when = '6'
    let #Special = #Special + #DED_CUR
    add #CALCULATED_BASE to #Special_BASE
   if #DED_NOT_TAKEN > 0
    add 1 to #No_Special_Count
   else
    add 1 to #Special_Count
   end-if
  break
  when = 'E'
    let #SpecialOffset = #SpecialOffset + #DED_CUR
    add #CALCULATED_BASE to #SpecialOffset_BASE
   if #DED_NOT_TAKEN > 0
    add 1 to #No_SpecialOffset_Count
   else
    add 1 to #SpecialOffset_Count
   end-if
  break
  end-evaluate ! CSRS
 break
 when = 'FEHB'
  if &BT.GVT_ELIG_FEHB = 'TSV'
   add 1 to #FEHB_Full_Premium
   add #DED_CUR to #FEHB_Full_Premium_Count
  else
     add #DED_CUR to #2-3-nbr
     add 1 to #FEHB_Count
  end-if
  break
 when = 'FERS'

  if &BT.GVT_RETIRE_PLAN = 'K'
     let #3-3-nbr = #3-3-nbr + #DED_CUR
     add #CALCULATED_BASE to #FERS_BASE
    if #DED_NOT_TAKEN > 0
     add 1 to #No_FERS_Count
    else
     add 1 to #FERS_Count
    end-if
  end-if
  if &BT.GVT_RETIRE_PLAN = 'L'
    let #AT_Controller = #AT_Controller + #DED_CUR
    add #CALCULATED_BASE to #AT_Controller_BASE
   if #DED_NOT_TAKEN > 0
    add 1 to #No_AT_Controller_Count
   else
    add 1 to #AT_Controller_Count
   end-if
  end-if

  if &BT.GVT_RETIRE_PLAN = 'M'
    let #LEO = #LEO + #DED_CUR
    add #CALCULATED_BASE to #LEO_BASE
   if #DED_NOT_TAKEN > 0
    add 1 to #No_LEO_Count
   else
    add 1 to #LEO_Count
   end-if
  end-if

  break
 when = 'Military-Deposit'
  ! Check to see if its FERS
   if &BT.GVT_RETIRE_PLAN='K' or &BT.GVT_RETIRE_PLAN='L' or
      &BT.GVT_RETIRE_PLAN='M' or &BT.GVT_RETIRE_PLAN='N'
    add 1 to #Military_Deposit_Count_FERS
    add #ded_cur to #Military_Deposit_FERS
   else
  ! Check to see if its CSRS
   if &BT.GVT_RETIRE_PLAN='1' or &BT.GVT_RETIRE_PLAN='C' or
      &BT.GVT_RETIRE_PLAN='R' or &BT.GVT_RETIRE_PLAN='T' or
      &BT.GVT_RETIRE_PLAN='W' or &BT.GVT_RETIRE_PLAN='6'

     add 1 to #Military_Deposit_Count_CSRS
     add #ded_cur to #Military_Deposit_CSRS

    end-if
   end-if
  break


   end-evaluate ! Called-By

FROM
    PS_GVT_JOB BT,
    PS_PAY_CHECK PC
WHERE
      PC.COMPANY          = $COMPANY
  AND PC.PAY_END_DT       = $PAY_END_DT
  AND PC.PAYGROUP         = $PAYGROUP
  AND PC.PAGE_NUM            = #PAGEN
  AND PC.LINE_NUM            = #LINEN
  AND PC.EMPLID           = BT.EMPLID
  AND BT.EFFDT      =
       (SELECT  MAX (JOB1.EFFDT)
        FROM    PS_GVT_JOB     JOB1
        WHERE   JOB1.EMPLID         =   BT.EMPLID
        AND     JOB1.EMPL_RCD      =   BT.EMPL_RCD
        AND     JOB1.EFFDT          <=  $PAY_END_DT)
  AND BT.EFFSEQ     =
       (SELECT  MAX (JOB2.EFFSEQ)
        FROM    PS_GVT_JOB     JOB2
        WHERE   JOB2.EMPLID         =   BT.EMPLID
        AND     JOB2.EMPL_RCD      =   BT.EMPL_RCD
        AND     JOB2.EFFDT          =   BT.EFFDT)
 AND  BT.EMPL_RCD  =
       (SELECT   MAX(JOB3.EMPL_RCD)
        FROM PS_GVT_JOB         JOB3
        WHERE   JOB3.EMPLID         =   BT.EMPLID
        AND     JOB3.EFFDT          =   BT.EFFDT)

end-select
end-procedure     !Retirement-Withholding_CSRS

!***************************
begin-procedure Process-2812
!***************************


!******** Retrieve On-Cycle Deductions

begin-select

A1.COMPANY
A1.PAYGROUP
A1.CALCULATED_BASE
A1.DEDCD
A1.DED_CLASS
A1.PLAN_TYPE
A1.DED_NOT_TAKEN
A1.DED_CUR
A1.PAGE_NUM
A1.LINE_NUM
SUM(A1.DED_CUR)  &DED_SUM1
A2.PAY_END_DT
 LET $PAY_END_DT_CALC = EDIT(&A2.PAY_END_DT, 'YYYY-MM-DD')
 Let #year=SUBSTR($PAY_END_DT_CALC,1,4)
 Let #month_day=SUBSTR($PAY_END_DT_CALC,6,2) || SUBSTR($PAY_END_DT_CALC,9,2)
A2.PAY_BEGIN_DT
A2.CHECK_DT
   let $COMPANY       = &A1.COMPANY
   let $DEDCD         = &A1.DEDCD
   let #PAGEN         = &A1.PAGE_NUM
   let #LINEN         = &A1.LINE_NUM
   let $PAYGROUP      = &A1.PAYGROUP
   let $DED_CLASS     = &A1.DED_CLASS
   let $PLAN_TYPE     = &A1.PLAN_TYPE
   let $PAY_END_DT    = &A2.PAY_END_DT
   let $PAY_BEGIN_DT  = &A2.PAY_BEGIN_DT
   let #DED_CUR = &DED_SUM1
   let #CALCULATED_BASE = &A1.CALCULATED_BASE
   let #DED_NOT_TAKEN = &A1.DED_NOT_TAKEN

   do Process-2812-deduction

FROM PS_PAY_DEDUCTION A1,
     PS_PAY_CALENDAR A2

WHERE A2.RUN_ID          = $RC.Run_ID
  AND A2.PAY_CONFIRM_RUN = 'Y'
  AND A1.COMPANY         = A2.COMPANY
  AND A1.PAYGROUP        = A2.PAYGROUP
  AND A1.PAY_END_DT      = A2.PAY_END_DT
  AND A1.OFF_CYCLE       = 'N'
  AND A1.COMPANY = $RC.REPORT_ID

GROUP BY A1.COMPANY,
  A1.PAYGROUP,
  A1.DEDCD,
  A1.DED_CLASS,
  A1.PLAN_TYPE,
  A1.DED_NOT_TAKEN,
  A2.PAY_END_DT,
  A2.PAY_BEGIN_DT,
  A2.CHECK_DT,
  A1.PAGE_NUM,
  A1.LINE_NUM,
  A1.DED_CUR,
  A1.CALCULATED_BASE


end-select

!******** Retrieve Off-Cycle Deductions

begin-select

B1.DEDCD
B1.DED_CLASS
B1.PLAN_TYPE
SUM(B1.DED_CUR)  &DED_SUM2

   let $DEDCD         = &B1.DEDCD
   let $DED_CLASS     = &B1.DED_CLASS
   let $PLAN_TYPE     = &B1.PLAN_TYPE
   let #DED_CUR = &DED_SUM2

   do Process-2812-deduction

FROM PS_PAY_DEDUCTION B1
WHERE B1.OFF_CYCLE = 'Y'
  AND EXISTS (SELECT 'X'
              FROM   PS_PAY_CALENDAR CAL1,
                     PS_GVT_CONFIRM_TBL CON1
              WHERE  CAL1.RUN_ID         = $RC.Run_ID
              AND    B1.COMPANY          = CAL1.COMPANY
              AND    B1.PAYGROUP         = CAL1.PAYGROUP
              AND    B1.PAY_END_DT       = CAL1.PAY_END_DT
              AND    B1.COMPANY          = CON1.COMPANY
              AND    B1.PAYGROUP         = CON1.PAYGROUP
              AND    B1.OFF_CYCLE        = CON1.OFF_CYCLE
              AND    B1.PAY_END_DT      <= CON1.PAY_END_DT
              AND    B1.PAGE_NUM            = CON1.PAGE_NUM
              AND    B1.COMPANY          = $RC.REPORT_ID
              AND   (CON1.GVT_CONFIRM_DT >= CAL1.PAY_BEGIN_DT
              AND    CON1.GVT_CONFIRM_DT <= CAL1.PAY_END_DT))

GROUP BY B1.DEDCD,
  B1.DED_CLASS,
  B1.PLAN_TYPE

end-select


end-procedure     !Process-2812

!*************************************
begin-procedure OptionB-Age
!*************************************

 let $pay_end_calc = edit(&a2.pay_end_dt, 'YYYY-MM-DD')
 do dtu-add-days($pay_end_calc, 1 , $next_pay_begin_date)
 LET $NEWBEGIN = substr($next_pay_begin_date,6,5)
 IF $NEWBEGIN = '02-29'
    LET $newbegin = '03-01'
 END-IF

! SHOW 'date ' $next_pay_begin_dt

begin-select distinct
C.EMPLID
D.BIRTHDATE

 LET $bdate = edit(&D.BIRTHDATE, 'yyyy-mm-dd')

 let #bdate = edit(&D.BIRTHDATE, 'mmdd')
 let #begin_dt = edit(&a2.pay_begin_dt, 'mmdd')
 let #end_dt = edit(&a2.pay_end_dt, 'mmdd')

 if #bdate >=  #begin_dt
 AND #bdate <=  #end_dt
    let $byear = SUBSTR($bdate,1,4)
    let $bdate = $BYEAR || '-' || $newbegin
 end-if

 !SHOW #BEGIN_DT ' ' #END_DT ' ' #BDATE ' ' $BDATE

  Let #byear=SUBSTR($bdate,1,4)          !(&D.BIRTHDATE,1,4)

  Let #bmonth_day=SUBSTR($bdate,6,2) || SUBSTR($bdate,9,2)
  Let #age=#year-#byear

 if #month_day - #bmonth_day < 0
     subtract 1 from #age
!          SHOW 'GOT HERE ' #MONTH_DAY ' ' #BMONTH_DAY ' ' &C.EMPLID ' ' #AGE
  end-if

 let #1-12-nbr = #1-12-nbr + #DED_CUR
 if #age < 35
    if &A1.DED_NOT_TAKEN > 0
   add 1 to #NoLT35
    else
   add 1 to #NLT35
  add &A1.DED_CUR to #BLT35
!     SHOW 'DED CUR ' &A1.DED_CUR ' ' &C.EMPLID

   end-if
  end-if

  if #age < 40 and #age >34
   if &A1.DED_NOT_TAKEN > 0
   add 1 to #No39
    else
   add 1 to #N39
   add &A1.DED_CUR to #B39
   end-if
  end-if


  if #age < 50 and #age > 44
   if &A1.DED_NOT_TAKEN > 0
   add 1 to #No49
    else
   add 1 to #N49
   add &A1.DED_CUR to #B49
   end-if
  end-if


  if #age < 45 and #age > 39
   if &A1.DED_NOT_TAKEN > 0
   add 1 to #No44
    else
   add 1 to #N44
   add &A1.DED_CUR to #B44
   end-if
  end-if

  if #age < 55 and #age > 49
   if &A1.DED_NOT_TAKEN > 0
   add 1 to #No54
    else
   add 1 to #N54
   add &A1.DED_CUR to #B54
   end-if
  end-if

 if #age > 59
   if &A1.DED_NOT_TAKEN > 0
   add 1 to #NoGT59
    else
   add 1 to #NGT59
   add &A1.DED_CUR to #BGT59
   end-if
  end-if

  if #age < 60 and #age > 54
   if &A1.DED_NOT_TAKEN > 0
   add 1 to #No59
    else
   add 1 to #N59
   add &A1.DED_CUR to #B59
   end-if
  end-if

FROM PS_PAY_CHECK C,
     PS_GVT_PERS_DATA D
WHERE
      C.COMPANY          = $COMPANY
  AND C.PAY_END_DT       = $PAY_END_DT
  AND C.PAYGROUP         = $PAYGROUP
  AND C.PAGE_NUM            = #PAGEN
  AND C.LINE_NUM            = #LINEN
  AND C.EMPLID           = D.EMPLID
  AND D.EFFDT            = (SELECT MAX(EFFDT)
                            FROM PS_GVT_PERS_DATA
                            WHERE EMPLID = D.EMPLID)

end-select
end-procedure !OptionB-Age

!*************************************
begin-procedure Process-2812-deduction
!*************************************

!******** Only process RITS Deductions - GVT_DIST_DEST = 'R'

begin-select

E1.GVT_REPORTING_CD

   do Move-to-2812-extract-field

FROM PS_DEDUCTION_TBL E1

WHERE E1.PLAN_TYPE     = $PLAN_TYPE
  AND E1.DEDCD         = $DEDCD
  AND E1.GVT_DIST_DEST = 'R'
  AND E1.EFFDT =
      (SELECT MAX(E2.EFFDT)
       FROM   PS_DEDUCTION_TBL E2
       WHERE  E2.PLAN_TYPE = E1.PLAN_TYPE
  AND  E2.DEDCD    = E1.DEDCD
  AND  E2.EFFDT   <= $PAY_END_DT)

end-select

end-procedure     !Process-2812-deduction


!*****************************************
begin-procedure Move-to-2812-extract-field
!*****************************************

  !* Move value to appropriate Field based on Report CD & DED_CLASS

  evaluate $DED_CLASS

    when = 'A'
    when = 'B'
       let $GVT_REPORTING_CD=&E1.GVT_REPORTING_CD
       evaluate &E1.GVT_REPORTING_CD

   when = 'BA'
     let #1-9-nbr = #1-9-nbr + #DED_CUR
    if &A1.DED_NOT_TAKEN > 0
     add 1 to #No_Basic_Count
    else
     add 1 to #Basic_Count
    end-if
    break
   when = 'ST'
     let #1-11-nbr = #1-11-nbr + #DED_CUR
    if &A1.DED_NOT_TAKEN > 0
     add 1 to #No_OptionA_Count
    else
     add 1 to #OptionA_Count
    end-if
    break
   when = 'AD'
     do OptionB-Age
     break
   when = 'FA'
     let #1-13-nbr = #1-13-nbr + #DED_CUR
    if &A1.DED_NOT_TAKEN > 0
     add 1 to #No_OptionC_Count
    else
     add 1 to #OptionC_Count
    end-if
    break
   when = 'PR'
    if &A1.DED_NOT_TAKEN > 0
        add 1 to #No_PostRetirement_Count
    else
        add 1 to #PostRetirement_Count
        let #1-14-nbr = #1-14-nbr + #DED_CUR
    end-if
    break
   when = 'FH'
     let $Called-By = 'FEHB'
     do Call-Job
     break
   when = 'CS'
    let $Called-By = 'CSRS'
    do Call-Job
    break
   when = 'CR'
     let #2-13-nbr = #2-13-nbr + #DED_CUR
     add 1 to #CivilianService_Credit_Count
     break
   when = 'FE'
     let $Called-By = 'FERS'
     do Call-Job
    break
   when = 'FR'
     let #3-5-nbr = #3-5-nbr + #DED_CUR
     add &A1.CALCULATED_BASE to #ReserveTech_BASE
    if &A1.DED_NOT_TAKEN > 0
     add 1 to #No_ReserveTech_Count
    else
     add 1 to #ReserveTech_Count
    end-if
    break
   when = 'FS'
     let #3-7-nbr = #3-7-nbr + #DED_CUR
     break
   when = 'MD'
     let $Called-By = 'Military-Deposit'
     do Call-Job
     break
       end-evaluate !&E1.GVT_REPORTING_CD

       break

    when-other

       evaluate &E1.GVT_REPORTING_CD
   when = 'BA'
     let #1-10-nbr = #1-10-nbr + #DED_CUR
     break
   when = 'FH'
     let #2-4-nbr = #2-4-nbr + #DED_CUR
     break
   when = 'CS'
     let $Called-By = 'Contributions'
    do Call-Job
     break

   when = 'CO'
     let #2-12-nbr = #2-12-nbr + #DED_CUR
     add 1 to #No_Salary_Offset_CSRS
     break
   when = 'FE'
     let $Called-By = 'Contributions'
    do Call-Job
     break
   when = 'FR'
     let #3-6-nbr = #3-6-nbr + #DED_CUR
     break
   when = 'FS'
     let #3-8-nbr = #3-8-nbr + #DED_CUR
     break
   when = 'FO'
     let #3-9-nbr = #3-9-nbr + #DED_CUR
     add 1 to #No_Salary_Offset_FERS

       end-evaluate !&E1.GVT_REPORTING_CD

  end-evaluate     !$DED_CLASS

end-procedure   !Move-to-2812-extract-field

!*********************************
begin-procedure Write-2812-records
!*********************************

  !***** Calculate Total Fields


  let #1-15-nbr = #1-9-nbr + #1-11-nbr + #1-12-nbr + #1-13-nbr
                  + #1-10-nbr  !weinberg - added basic life contributions
  let #1-16-nbr = #1-10-nbr
  let #1-17-nbr = #1-15-nbr + #1-16-nbr

  let #2-7-nbr  = #2-3-nbr + #FEHB_Full_Premium
                + #2-4-nbr !weinberg - added health contributions
  let #2-8-nbr  = #2-4-nbr + #2-6-nbr
  let #2-9-nbr  = #2-7-nbr + #2-8-nbr
  let #2-14-nbr = #2-10-nbr + #2-13-nbr
  let #2-15-nbr = #2-11-nbr + #2-12-nbr
  let #2-16-nbr = #2-14-nbr + #2-15-nbr

  let #3-10-nbr = #Military_Deposit_FERS + #Military_Deposit_CSRS
  let #3-11-nbr = #3-3-nbr + #3-5-nbr + #3-7-nbr + #3-10-nbr
  let #3-12-nbr = #3-4-nbr + #3-6-nbr + #3-8-nbr + #3-9-nbr
  let #3-13-nbr = #3-11-nbr + #3-12-nbr
  let #3-14-nbr = #2-14-nbr + #3-11-nbr
  let #3-15-nbr = #2-15-nbr + #3-12-nbr
  let #3-16-nbr = #3-14-nbr + #3-15-nbr
  let #3-17-nbr = #1-15-nbr + #2-7-nbr + #3-14-nbr
  let #3-18-nbr = #1-16-nbr + #2-8-nbr + #3-15-nbr
  let #3-19-nbr = #3-17-nbr + #3-18-nbr

  let #FEHB_Total_Count = #FEHB_Full_Premium_Count + #FEHB_Count
  let #total_retirement_deductions_made= (#3-5-nbr + #CivilianService_Credit_Count +
      #PostRetirement_Count + #CSRS_Count + #FERS_Count + #LEO_Count + #Special_Count +
      #SpecialOffset_Count + #AT_Controller_Count +#Military_Deposit_Count_FERS +
      #Military_Deposit_Count_CSRS + #Regular_Offset_Count )

  let #total_retirement_deductions_notmade = (#No_ReserveTech_count + #No_PostRetirement_Count +
      #No_CSRS_Count + #No_FERS_Count + #No_Salary_Offset_FERS + #No_Salary_Offset_CSRS +
      #No_LEO_Count + #No_Special_Count + #No_AT_Controller_Count + #No_SpecialOffset_Count
      + #No_Regular_Offset_Count)

  let #total_retirement = ( #2-13-nbr + #2-12-nbr +
      #2-10-nbr + #2-11-nbr + #LEO + #Special + #Special_Contributions
      + #Special_Offset_Contributions + #AT_Controller_Contributions +
      #SpecialOffset + #AT_Controller + #Military_Deposit_FERS + #LEO_Contributions +
      #Military_Deposit_CSRS + #Regular_Offset + #Regular_Offset_Contributions +
      #3-9-nbr + #3-3-nbr + #3-4-nbr + #3-5-nbr + #3-6-nbr )


  let #grand_total = #total_retirement + #1-15-nbr + #2-7-nbr

  do Init-Number



end-procedure       !Write-2812-file


!********************************
begin-procedure Print-2812-report
!********************************

  !****** Print 2812 page
  do head
graphic (+1,1,120) box 1 2 100
  let #middle=#b * 2
graphic (+1,#b1,#middle) box 1 2 #shad
graphic (0,#b3,#middle) box 1 2
  print 'Number Enrolled'            (0,85)
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2
  print 'Benefit Category'           (0,11,16)
  print 'Dollar Amount'              (0,40)
  print 'Deductions Made'            (0,70)
  print 'No Deductions Made'         (0,97)
graphic (+1,#b1,#b) box 2 2 #shad
graphic (0,#b2,#b)  box 2 2 #shad
graphic (0,#b3,#b)  box 2 2 #shad
graphic (0,#b4,#b)  box 2 2 #shad
  print 'A. LIFE INSURANCE'             (0,#p1)
graphic (+2,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2
  print '1. Basic Life'              (0,#p1,14)
  LET #1-9-nbr = #1-9-nbr + #1-10-nbr  !WEINBERG - ADDED WITHHOLDINGS AND CONTRIBUTIONS
  print #1-9-nbr        (0,#p2,15)   edit 999,999,999.99mi
  print #Basic_Count  (0,#p3,7)    edit 999,999
  print #No_Basic_Count  (0,#p4,7)    edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2
  print '2. Standard - Option A'     (0,#p1,22)
  print #1-11-nbr       (0,#p2,15)   edit 999,999,999.99mi
  print #OptionA_Count  (0,#p3,7)    edit 999,999
  print #No_OptionA_Count (0,#p4,7)  edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2 #shad
graphic (0,#b3,#b)  box 1 2 #shad
graphic (0,#b4,#b)  box 1 2 #shad
  print '3. Additional - Option B'   (0,#p1,24)
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

 print '  a. To age 35' (0,#p1)
 print #BLT35 (0,#p2) edit 999,999,999.99mi
 print #NLT35 (0,#p3) edit 999,999
 print #NoLT35 (0,#p4) edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2
 print '  b. Age 35 to 39' (0,#p1)
 print #B39 (0,#p2) edit 999,999,999.99mi
 print #N39 (0,#p3) edit 999,999
 print #No39 (0,#p4) edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

 print '  c. Age 40 to 44' (0,#p1)
 print #B44 (0,#p2) edit 999,999,999.99mi
 print #N44 (0,#p3) edit 999,999
 print #No44 (0,#p4) edit 999,999

graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

 print '  d. Age 45 to 49' (0,#p1)
 print #B49 (0,#p2) edit 999,999,999.99mi
 print #N49 (0,#p3) edit 999,999
 print #No49 (0,#p4) edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

 print '  e. Age 50 to 54' (0,#p1)
 print #B54 (0,#p2) edit 999,999,999.99mi
 print #N54 (0,#p3) edit 999,999
 print #No54 (0,#p4) edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

 print '  f. Age 55 to 59' (0,#p1)
 print #B59 (0,#p2) edit 999,999,999.99mi
 print #N59 (0,#p3) edit 999,999
 print #No59 (0,#p4) edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

 print '  g. Age 60 & up' (0,#p1)
 print #BGT59 (0,#p2) edit 999,999,999.99mi
 print #NGT59 (0,#p3) edit 999,999
 print #NoGT59 (0,#p4) edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

  print '4. Family - Option C'       (0,#p1)
  print #1-13-nbr       (0,#p2,15)  edit 999,999,999.99mi
  print #OptionC_Count  (0,#p3,7)   edit 999,999
  print #No_OptionC_Count (0,#p4,7) edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2

  print '5. Post-Retirement'         (0,#p1)
  print #1-14-nbr       (0,#p2,15)  edit 999,999,999.99mi
  print #PostRetirement_Count (0,#p3,7) edit 999,999
  print #No_PostRetirement_Count (0,#p4,7) edit 999,999

graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2 #shad
graphic (0,#b4,#b)  box 1 2 #shad


  print '6. Total Life Insurance'    (0,#p1)
  print #1-15-nbr       (0,#p2,15)  edit 999,999,999.99mi
graphic (+1,#b1,#b) box 2 2 #shad
graphic (0,#b2,#b)  box 2 2 #shad
graphic (0,#b3,#b)  box 2 2 #shad
graphic (0,#b4,#b)  box 2 2 #shad

  print 'B. HEALTH BENEFITS'            (0,#p1)
graphic (+2,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2 #shad

  print '1. Regular'                 (0,#p1)
  let #2-3-nbr = #2-3-nbr + #2-4-nbr ! weinberg added contributions
  print #2-3-nbr       (0,#p2,15)   edit 999,999,999.99mi
  print #FEHB_COUNT    (0,#p3,15)   edit 999,999
graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2 #shad
  print '2. Payers of Full Premiums' (0,#p1)
  print #FEHB_Full_Premium       (0,#p2,15)   edit 999,999,999.99mi
  print #FEHB_Full_Premium_Count (0,#p3,7)   edit 999,999

graphic (+1,#b1,#b) box 1 2
graphic (0,#b2,#b)  box 1 2
graphic (0,#b3,#b)  box 1 2
graphic (0,#b4,#b)  box 1 2 #shad

  print '3. Total Health Benefits'   (0,#p1)
  print #2-7-nbr       (0,#p2,15)   edit 999,999,999.99mi
  print #FEHB_Total_Count  (0,#p3,7)   edit 999,999


 ! new-page
! do head
graphic (+1,1,120) box 1 2 100
! Page 2 Retirement Info Page
graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2
graphic ( 0,#cp4,40)   box 2 2
graphic ( 0,#cp6,#cw6) box 2 2 #shad

  print 'Benefit Category'              (0,#c1)
  print ' Dollar'                        (0,#c2)
  print 'Aggregate'                     (0,#c3)
  print '        Number Enrolled'       (0,#c4)
  print 'Amount'                        (+1,#c2)
  print 'Base Salary'                   (0,#c3)
  print 'Deductions Made'               (0,85)
  print 'No Deductions Made'            (0,102)
graphic (+1,#cp1,#cw1) box 1 2 #shad
graphic ( 0,#cp2,#cw2) box 1 2 #shad
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print 'C. RETIREMENT'                 (0,#c1)
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2 #shad
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2
  print '1. CSRS'                        (0,#c1)
  print 'CPDF Code'                      (0,32)
!  print 'Deductions Made'               (0,#c4)
!  print 'No Deductions Made'            (0,#c5)
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2
graphic ( 0,#cp6,#cw6) box 1 2

  print 'a. Regular Withholdings'          (0,#c1)
  print #2-10-nbr       (0,#c2,15)   edit 999,999,999.99mi
  print #CSRS_Base      (0,#c3,15)   edit 999,999,999.99mi
  print #CSRS_Count     (0,#c4,7)    edit 999,999
  print #No_CSRS_Count  (0,#c5,7)    edit 999,999
  print '1,R'                             (0,#c6)

graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2

  print 'b. Regular Contributions'          (0,#c1)
  print #2-11-nbr       (0,#c2,15)  edit 999,999,999.99mi
  print '1,R'                               (0,#c6)

graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2
graphic ( 0,#cp6,#cw6) box 1 2

  print 'c. Special Withholdings'          (0,#c1)
  print #Special             (0,#c2,15) edit 999,999,999.99mi
  print #Special_BASE        (0,#c3,15) edit 999,999,999.99mi
  print #Special_Count       (0,#c4,7)  edit 999,999
  print #No_Special_Count    (0,#c5,7)  edit 999,999
  print '6,T'                             (0,#c6)

graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2

  print 'd. Special Contributions'          (0,#c1)
  print #Special_Contributions              (0,#c2,15) edit 999,999,999.99mi
  print '6,T'                               (0,#c6)


graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2
graphic ( 0,#cp4,#cw4) box 2 2
graphic ( 0,#cp5,#cw5) box 2 2
graphic ( 0,#cp6,#cw6) box 2 2

  print 'e. Regular Withholdings for'       (0,#c1)
  print '   Offset Employees' (+1,#c1)
  print #Regular_Offset                     (0,#c2,15)  edit 999,999,999.99mi
  print #Regular_Offset_BASE                (0,#c3,15)  edit 999,999,999.99mi
  print #Regular_Offset_Count               (0,#c4,7)  edit 999,999
  print #No_Regular_Offset_Count            (0,#c5,7)  edit 999,999
  print ' C'                                (0,#c6)

graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2 #shad
graphic ( 0,#cp4,#cw4) box 2 2 #shad
graphic ( 0,#cp5,#cw5) box 2 2 #shad
graphic ( 0,#cp6,#cw6) box 2 2

  print 'f. Regular Contributions for'       (0,#c1)
  print '   Offset Employees' (+1,#c1)
  print #Regular_Offset_Contributions       (0,#c2,15)  edit 999,999,999.99mi
  print ' C'                             (0,#c6)
graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2
graphic ( 0,#cp4,#cw4) box 2 2
graphic ( 0,#cp5,#cw5) box 2 2
graphic ( 0,#cp6,#cw6) box 2 2

  print 'g. Special Withholdings for'       (0,#c1)
  print '   Offset Employees' (+1,#c1)
  print #SpecialOffset                     (0,#c2,15)  edit 999,999,999.99mi
  print #SpecialOffset_BASE                (0,#c3,15)  edit 999,999,999.99mi
  print #SpecialOffset_Count               (0,#c4,7)   edit 999,999
  print #No_SpecialOffset_Count            (0,#c5,7)   edit 999,999
  print ' E'                               (0,#c6)

graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2 #shad
graphic ( 0,#cp4,#cw4) box 2 2 #shad
graphic ( 0,#cp5,#cw5) box 2 2 #shad
graphic ( 0,#cp6,#cw6) box 2 2

  print 'h. Special Contributions for'     (0,#c1)
  print '   Offset Employees' (+1,#c1)
  print #Special_Offset_Contributions      (0,#c2,15)  edit 999,999,999.99mi
  print ' E'                               (0,#c6)

graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print 'i. Salary Offset'         (0,#c1)
  print #2-12-nbr       (0,#c2,15)   edit 999,999,999.99mi
  print #No_Salary_Offset_CSRS (0,#c5,7) edit 999,999

graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print 'j. Military Deposit'        (0,#c1)
  print #Military_Deposit_CSRS       (0,#c2,15)  edit 999,999,999.99mi
  print #Military_Deposit_Count_CSRS (0,#c4,7) edit 999,999
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print 'k. Civilian Service Credit'    (0,#c1)
  print #2-13-nbr       (0,#c2,15)  edit 999,999,999.99mi
  print #CivilianService_Credit_Count (0,#c4,7) edit 999,999
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2 #shad
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print '2. FERS'                      (0,#c1)
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2
graphic ( 0,#cp6,#cw6) box 1 2

  print 'a. Regular Withholdings'      (0,#c1)
  print #3-3-nbr        (0,#c2,15)   edit 999,999,999.99mi
  print #FERS_Base      (0,#c3,15)   edit 999,999,999.99mi
  print #FERS_Count     (0,#c4,7)    edit 999,999
  print #No_FERS_Count  (0,#c5,7)    edit 999,999
  print ' K'                             (0,#c6)
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2

  print 'b. Regular Contributions'     (0,#c1)
  print #3-4-nbr       (0,#c2,15)    edit 999,999,999.99mi
  print ' K'                             (0,#c6)
graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2
graphic ( 0,#cp4,#cw4) box 2 2
graphic ( 0,#cp5,#cw5) box 2 2
graphic ( 0,#cp6,#cw6) box 2 2

  print 'c. Reserve Technicians'        (0,#c1)
  print '   Withholdings'               (+1,#c1)
  print #3-5-nbr       (0,#c2,15)   edit 999,999,999.99mi
  print #ReserveTech_Base      (0,#c3,15)   edit 999,999,999.99mi
  print #ReserveTech_Count     (0,#c4,7)    edit 999,999
  print #No_ReserveTech_Count  (0,#c5,7)    edit 999,999
  print ' N'                             (0,#c6)
graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2 #shad
graphic ( 0,#cp4,#cw4) box 2 2 #shad
graphic ( 0,#cp5,#cw5) box 2 2 #shad
graphic ( 0,#cp6,#cw6) box 2 2

  print 'd. Reserve Technicians'        (0,#c1)
  print '   Contributions'              (+1,#c1)
  print #3-6-nbr       (0,#c2,15)   edit 999,999,999.99mi
  print ' N'                             (0,#c6)

! Leftovers from RITS has to do with a 28 day 4 week work period.  For some reason OPM
! Doesn't seem to want details on these individual despite the information in more detail
! on all the rest.

!  print ' - Special'                 (+2,1,10)
!  print #3-7-nbr       (0,#c2,15)   edit 999,999,999.99mi
!  print #3-8-nbr       (0,#c3,15)   edit 999,999,999.99mi

graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2
graphic ( 0,#cp4,#cw4) box 2 2
graphic ( 0,#cp5,#cw5) box 2 2
graphic ( 0,#cp6,#cw6) box 2 2

  print 'e. A/T Controllers'    (0,#c1)
  print '   Witholdings    '    (+1,#c1)
  print #AT_Controller          (0,#c2,15) edit 999,999,999.99mi
  print #AT_Controller_BASE     (0,#c3,15) edit 999,999,999.99mi
  print #AT_Controller_Count    (0,#c4,7)  edit 999,999
  print #No_AT_Controller_Count (0,#c5,7)  edit 999,999
  print ' L'                             (0,#c6)
graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2 #shad
graphic ( 0,#cp4,#cw4) box 2 2 #shad
graphic ( 0,#cp5,#cw5) box 2 2 #shad
graphic ( 0,#cp6,#cw6) box 2 2

  print 'f. A/T Controllers' (0,#c1)
  print '   Contributions  ' (+1,#c1)
  print #AT_Controller_Contributions       (0,#c2,15) edit 999,999,999.99mi
  print ' L'                             (0,#c6)
graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2
graphic ( 0,#cp4,#cw4) box 2 2
graphic ( 0,#cp5,#cw5) box 2 2
graphic ( 0,#cp6,#cw6) box 2 2

  print 'g. Law Enforcement/Fire-' (0,#c1)
  print '   fighters Witholdings ' (+1,#c1)
  print #LEO                (0,#c2,15) edit 999,999,999.99mi
  print #LEO_BASE           (0,#c3,15) edit 999,999,999.99mi
  print #LEO_Count          (0,#c4,7)  edit 999,999
  print #No_LEO_Count       (0,#c5,7)  edit 999,999
  print ' M'                             (0,#c6)
graphic (+1,#cp1,#cw1) box 2 2
graphic ( 0,#cp2,#cw2) box 2 2
graphic ( 0,#cp3,#cw3) box 2 2 #shad
graphic ( 0,#cp4,#cw4) box 2 2 #shad
graphic ( 0,#cp5,#cw5) box 2 2 #shad
graphic ( 0,#cp6,#cw6) box 2 2

  print 'h. Law Enforcement/Fire-' (0,#c1)
  print '   fighters Contributions' (+1,#c1)
  print #LEO_Contributions          (0,#c2,15) edit 999,999,999.99mi
  print ' M'                             (0,#c6)

graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print 'i. Salary Offset'         (0,#c1)
  print #3-9-nbr       (0,#c2,15)   edit 999,999,999.99mi
  print #No_Salary_Offset_FERS (0,#c5,7) edit 999,999
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2

  print 'j.' (0,#c1)
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print 'k. Military Deposit'        (0,#c1)
  print #Military_Deposit_FERS       (0,#c2,15)  edit 999,999,999.99mi
  print #Military_Deposit_Count_FERS (0,#c4,7) edit 999,999
graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print '3. Total Retirement'          (0,#c1)

  print #total_retirement       (0,#c2,15)  edit 999,999,999.99mi
  print #total_retirement_deductions_made (0,#c4,7) edit 999,999
  print #total_retirement_deductions_notmade (0,#c5,7) edit 999,999

graphic (+1,#cp1,#cw1) box 1 2
graphic ( 0,#cp2,#cw2) box 1 2
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2 #shad
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2 #shad

  print 'Grand Total'                (0,#c1,11)
  print #grand_total      (0,#c2,15)    edit 999,999,999.99mi

graphic (+1,#cp1,#cw1) box 1 2 #shad
graphic ( 0,#cp2,#cw2) box 1 2 #shad
graphic ( 0,#cp3,#cw3) box 1 2 #shad
graphic ( 0,#cp4,#cw4) box 1 2
graphic ( 0,#cp5,#cw5) box 1 2 #shad
graphic ( 0,#cp6,#cw6) box 1 2 #shad
  print 'Total Employees (and/or Annuitants) on Payroll' (0,#c1)
  print #total_employees (0,#c4) edit 999,999
end-procedure     !Print-2812-report


!**********************************
begin-procedure Delete-run-control
!**********************************

begin-SQL

DELETE FROM PS_GVT_RUN_CNTL

WHERE RUN_CNTL_ID = $Prcs_Run_Cntl_ID

end-SQL

end-procedure     !Delete-run-control

!#Include 'ghrunctl.sqc'  !Get run control parameter values
!#Include 'ghovrpch.sqc'  !Over Punch routine
#Include 'reset1.sqc'     !Reset printer
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'curdttim.sqc'  !Routines to get current date/time
!#Include 'useprntr.sqc'  !
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'datemath.sqc'
