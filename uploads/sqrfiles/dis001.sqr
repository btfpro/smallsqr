!*******************************************
!  DIS001:  Disability Report (FRA) Global *
!*******************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                $Date:  2006/02/06:20:45:30                           !
!             $Release:  HR9                                           !
!            $Revision:  102                                           !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

Begin-Setup
#Include 'ptpsl177.sqc'  !Printer and page-size initialization
End-Setup

#define ESTAB_TBL
#define INSEE_TBL_FRA
#define PERSONAL_DT_FST
#include 'rellang.sqc'
#define RELLANG_PROG

!***********************************************************************

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Term
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term

end-report

begin-heading 6

  #Include 'stdhdgtr.sqc'

end-heading

begin-procedure Init-Report

  move 'DIS001' to $ReportID
  move 'French Disability Report' to $ReportTitle
  do Stdapi-Init

  let $language_cd = $CURR_LANGUAGE_CD

  if $prcs_process_instance = ''
        !No Prompt
  else
    do Select-Parameters-FRA
  end-if

  do Init_Printer
  do Init_Report_Translation($ReportID, $language_cd)
  do Append_Report_Translation($ReportID)

end-procedure

!***********************************************************************
!  Get Values
!***********************************************************************

begin-procedure Get-Values

let $language_cd = $PRCS_LANGUAGE_CD
   do Get-DIS001-Parm

end-procedure

!***********************************************************************
!  Main: Creates the Disability Report
!***********************************************************************

begin-procedure Process-Main

  let $RegRegion = 'FRA'

  move 0 to #Action
  move -1 to #priorStartPosn

  do Get-Translat-Info   ! Global

  do D1-1   ! Estab Identity

  do D1-2
  do D1-3

  print ' ' (+5,1)  ! Jumps 5 Lines

  do D1-1   ! Estab Identity
  do D2-2
  do D2-4

end-procedure

!***********************************************************************
!  Header: Retrieves Establishment Data
!***********************************************************************

begin-procedure D1-1

begin-SELECT ON-ERROR=SQL-Error
AA.SIREN_CD_FRA       &SIREN_CD_FRA
AA.NIC_CODE_FRA       &NIC_CD
AA.APE_INDSTRY_CD_FRA &APE_INDSTRY_CD_FRA
A.DESCR              &ESTAB_NAME
A.ADDRESS1           &ADDRESS1
A.ADDRESS2           &ADDRESS2
A.ADDRESS3           &ADDRESS3
A.ADDRESS4           &ADDRESS4
A.POSTAL             &POSTAL
A.CITY               &CITY

A.ESTABID             &ESTABID
{DATEOUT-PREFIX}A.EFFDT{DATEOUT-SUFFIX} &A.EFFDT

   let $ESTAB_TBL-ADDRESS1 =      &ADDRESS1
   let $ESTAB_TBL-ADDRESS2 =      &ADDRESS2
   let $ESTAB_TBL-ADDRESS3 =      &ADDRESS3
   let $ESTAB_TBL-ADDRESS4 =      &ADDRESS4
   let $ESTAB_TBL-CITY     =      &CITY
   let $ESTAB_TBL-DESCR    =      &ESTAB_NAME

   do Get_Related_ESTAB_TBL(&A.EFFDT, &ESTABID)

   let $Addr1 =      $ESTAB_TBL-ADDRESS1
   let $Addr2 =      $ESTAB_TBL-ADDRESS2
   let $Addr3 =      $ESTAB_TBL-ADDRESS3
   let $Addr4 =      $ESTAB_TBL-ADDRESS4
   let $City =       $ESTAB_TBL-CITY
   let $EstabName =  $ESTAB_TBL-DESCR

   do Print-Estab-Info

from PS_ESTAB_TBL A,
     PS_REG_REGION_TBL R, PS_ESTAB_TBL_FRA AA
where A.ESTABID=$ESTABID
  and AA.ESTABID = A.ESTABID
  and A.REG_REGION = R.REG_REGION
  and R.COUNTRY = $RegRegion
  and A.EFFDT = (select MAX(EFFDT)
                  from PS_ESTAB_TBL B
                  where A.ESTABID = B.ESTABID
                  and A.REG_REGION = R.REG_REGION
                  and R.COUNTRY = $RegRegion)
  and AA.EFFDT = A.EFFDT
  and A.EFF_STATUS = 'A'
  and A.EFFDT < $AsOfDate

end-SELECT
end-procedure

!***********************************************************************
!  Gets the strings printed in this report (Globalization)
!***********************************************************************

begin-procedure Get-Translat-Info

   do Get_Field_Information($ReportID, 'SIRET_CD',             $SIRET_CD,                 #DW)
   do Get_Field_Information($ReportID, 'APE_CD',               $APE_CD,                   #DW)
   do Get_Field_Information($ReportID, 'ESTAB_NAME',           $ESTAB_NAME,               #DW)
   do Get_Field_Information($ReportID, 'ADDRESS',              $ADDRESS,                  #DW)
   do Get_Field_Information($ReportID, 'POSTAL',               $POSTAL,                   #DW)
   do Get_Field_Information($ReportID, 'CITY',                 $CITY_LBL,                 #DW)

   do Get_Field_Information($ReportID, 'MEN',                  $MEN,                      #DW)
   do Get_Field_Information($ReportID, 'WOMEN',                $WOMEN,                    #DW)
   do Get_Field_Information($ReportID, 'TOTAL',                $TOTAL,                    #DW)

   do Get_Field_Information($ReportID, 'CATEGORY_NUMBER',      $CATEGORY_NUMBER,          #DW)
   do Get_Field_Information($ReportID, 'EMPLOY_BY_CATEGORY',   $EMPLOY_BY_CATEGORY,       #DW)
   do Get_Field_Information($ReportID, 'PCS_CATEGORY',         $PCS_CATEGORY,             #DW)

   do Get_Field_Information($ReportID, 'CODE',                 $CODE,                     #DW)
   do Get_Field_Information($ReportID, 'PCS',                  $PCS,                      #DW)
   do Get_Field_Information($ReportID, 'NUMBER',               $NUMBER,                   #DW)

   do Get_Field_Information($ReportID, 'SUBJECT_NUMBER',       $SUBJECT_NUMBER,           #DW)
   do Get_Field_Information($ReportID, 'T1',                   $T1,                       #DW)
   do Get_Field_Information($ReportID, 'BENEF_NUMBER',         $BENEF_NUMBER,             #DW)
   do Get_Field_Information($ReportID, 'TOTAL_EMPLOY1',        $TOTAL_EMPLOY1,            #DW)
   do Get_Field_Information($ReportID, 'TOTAL_EMPLOY2',        $TOTAL_EMPLOY2,            #DW)
   do Get_Field_Information($ReportID, 'T2',                   $T2,                       #DW)
   do Get_Field_Information($ReportID, 'SUBJECT',              $SUBJECT,                  #DW)

   do Get_Field_Information($ReportID, 'YES',                  $YES,                      #DW)
   do Get_Field_Information($ReportID, 'NO',                   $NO,                       #DW)
   do Get_Field_Information($ReportID, 'OR',                   $OR,                       #DW)

   do Get_Field_Information($ReportID, 'BIRTH_YEAR1',          $BIRTH_YEAR1,              #DW)
   do Get_Field_Information($ReportID, 'EMPLOY_YEAR1',         $EMPLOY_YEAR1,             #DW)
   do Get_Field_Information($ReportID, 'NUM_OF_CTGRY1',        $NUM_OF_CTGRY1,            #DW)
   do Get_Field_Information($ReportID, 'NAME',                 $NAME,                     #DW)
   do Get_Field_Information($ReportID, 'SEX',                  $SEX,                      #DW)
   do Get_Field_Information($ReportID, 'BIRTH_YEAR2',          $BIRTH_YEAR2,              #DW)
   do Get_Field_Information($ReportID, 'EMPLOY_YEAR2',         $EMPLOY_YEAR2,             #DW)
   do Get_Field_Information($ReportID, 'JOB',                  $JOB,                      #DW)
   do Get_Field_Information($ReportID, 'NUM_OF_CTGRY2',        $NUM_OF_CTGRY2,            #DW)

   do Get_Field_Information($ReportID, 'COTOREP_DISAB',        $COTOREP_DISAB,            #DW)
   do Get_Field_Information($ReportID, 'ACCIDENT_DISAB1',      $ACCIDENT_DISAB1,          #DW)
   do Get_Field_Information($ReportID, 'WAR_DISAB1',           $WAR_DISAB1,               #DW)
   do Get_Field_Information($ReportID, 'ASSIM_WAR_DISAB1',     $ASSIM_WAR_DISAB1,         #DW)
   do Get_Field_Information($ReportID, 'CATGRY',               $CATGRY,                   #DW)
   do Get_Field_Information($ReportID, 'LESS25',               $LESS25,                   #DW)
   do Get_Field_Information($ReportID, 'TRAINING',             $TRAINING,                 #DW)
   do Get_Field_Information($ReportID, 'ACCIDENT_DISAB2',      $ACCIDENT_DISAB2,          #DW)
   do Get_Field_Information($ReportID, 'PENSION_DISAB1',       $PENSION_DISAB1,           #DW)
   do Get_Field_Information($ReportID, 'WAR_DISAB2',           $WAR_DISAB2,               #DW)
   do Get_Field_Information($ReportID, 'DISABILITY',           $DISABILITY,               #DW)
   do Get_Field_Information($ReportID, 'MORE50',               $MORE50,                   #DW)
   do Get_Field_Information($ReportID, 'MORE500',              $MORE500,                  #DW)
   do Get_Field_Information($ReportID, 'PREV',                 $PREV,                     #DW)
   do Get_Field_Information($ReportID, 'ACCIDENT_DISAB3',      $ACCIDENT_DISAB3,          #DW)
   do Get_Field_Information($ReportID, 'PENSION_DISAB2',       $PENSION_DISAB2,           #DW)
   do Get_Field_Information($ReportID, 'WAR_DISAB3',           $WAR_DISAB3,               #DW)
   do Get_Field_Information($ReportID, 'AP',                   $AP,                       #DW)
   do Get_Field_Information($ReportID, 'IMPRO',                $IMPRO,                    #DW)
   do Get_Field_Information($ReportID, 'CAT',                  $CAT,                      #DW)
   do Get_Field_Information($ReportID, 'CDTD',                 $CDTD,                     #DW)
   do Get_Field_Information($ReportID, 'CFP',                  $CFP,                      #DW)
   do Get_Field_Information($ReportID, 'IPP_RATE',             $IPP_RATE,                 #DW)

   do Get_Field_Information($ReportID, 'TOTAL_UNITS1',         $TOTAL_UNITS1,             #DW)
   do Get_Field_Information($ReportID, 'NUM_UNITS1',           $NUM_UNITS1,               #DW)
   do Get_Field_Information($ReportID, 'TOTAL_UNITS2',         $TOTAL_UNITS2,             #DW)
   do Get_Field_Information($ReportID, 'COEF1',                $COEF1,                    #DW)
   do Get_Field_Information($ReportID, 'NUM_UNITS2',           $NUM_UNITS2,               #DW)
   do Get_Field_Information($ReportID, 'TOTAL_UNITS3',         $TOTAL_UNITS3,             #DW)
   do Get_Field_Information($ReportID, 'COEF2',                $COEF2,                    #DW)

   do Get_Field_Information($ReportID, 'ERR1',                 $ERR1,                     #DW)

end-procedure

!***********************************************************************
!  Prints Establishement data
!***********************************************************************

begin-procedure Print-Estab-Info

   print $SIRET_CD                     (+1,1,8)

   let $SIRET_CODE = &SIREN_CD_FRA||&NIC_CD
   print $SIRET_CODE                   (,10,19)
   print $APE_CD                       (,30,9)
   print &APE_INDSTRY_CD_FRA           (,40)
   print $ESTAB_NAME                   (+1,1,23)
   print $EstabName                    (,25)
   print $ADDRESS                      (+1,1)
   if rtrim($Addr1, ' ') <> ''
      print $Addr1                     (+1,1)
        end-if
   if rtrim($Addr2, ' ') <> ''
      print $Addr2                     (+1,1)
        end-if
   if rtrim($Addr3, ' ') <> ''
      print $Addr3                     (+1,1)
        end-if
   if rtrim($Addr4, ' ') <> ''
      print $Addr4                     (+1,1)
        end-if
   print $POSTAL                       (+1,1,12)
   print &POSTAL                       (,14,10)
   print $CITY_LBL                         (,25,8)
   print $City                         (,34)

end-procedure

!***********************************************************************
!  Prints # Males and # Females
!***********************************************************************

begin-procedure D1-2

   move 0 to #Action    ! D1-3 prints nothing and only do a count
   do D1-3
   print $MEN                    (+2,1)
   print #GlobalCountMale        (,+1)
   print $WOMEN                  (,+5)
   print #GlobalCountFemale      (,+1)
   let #TotalCount = #GlobalCountMale + #GlobalCountFemale
   print $TOTAL                  (,+5)
   print #TotalCount             (,+1)
   move 1 to #Action

end-procedure

!***********************************************************************
!  Counts Males and Females for each PCS Code
!  if #Action = 1 prints the results for each PCS Code
!  if #Action <> 1 Only performs a count
!***********************************************************************

begin-procedure D1-3

move 0 to #GlobalCountMale
move 0 to #GlobalCountFemale
move 0 to #CountMale
move 0 to #CountFemale
let $CurrentJobCode = ''

if #Action = 1
   do Print-Banner
end-if

do Define-PrimJob-Clause('BB', $AsOfDate, $PRIM_JOB_CLAUSE)

begin-SELECT ON-ERROR=SQL-Error

BB.JOBCODE           &B! For platform compatibility
DD.DESCRLONG         &JOBCODE
{DATEOUT-PREFIX}DD.EFFDT{DATEOUT-SUFFIX}             &INSEE_DT
AA.INSEE_CD_FRA      &PCS_CODE
CC.SEX               &SEX

   let $INSEE_TBL_FRA-DESCRLONG = &JOBCODE

   do Get_Related_INSEE_TBL_FRA(&INSEE_DT, &PCS_CODE)

   let $JobCodeDescr = $INSEE_TBL_FRA-DESCRLONG

   if $JobCodeDescr <> $CurrentJobCode AND #Action = 1
      if $CurrentJobCode <> ''
         do Print-Data
      end-if
      let $CurrentJobCode = $JobCodeDescr
      let $CurrentPCSCode = &PCS_CODE
      move 0 to #CountMale
      move 0 to #CountFemale
   end-if

   if &SEX = 'F'
      Add 1 to #GlobalCountFemale
      Add 1 to #CountFemale
   end-if

   if &SEX = 'M'
      Add 1 to #GlobalCountMale
      Add 1 to #CountMale
   end-if

from PS_JOBCODE_TBL AA,
     PS_JOB BB,
     PS_PERSONAL_DT_FST CC,
     PS_INSEE_TBL_FRA DD,
     PS_CNT_ACTIVE_VW FF,
     PS_REG_REGION_TBL RR

where AA.EFFDT = (select MAX(EFFDT)
                  from   PS_JOBCODE_TBL
                  where  EFFDT <= $InseeDt
                    and  AA.JOBCODE = JOBCODE
                    and  AA.SETID = SETID)
  and AA.EFF_STATUS = 'A'
  and BB.EMPL_RCD = [$PRIM_JOB_CLAUSE]
  and BB.EFFDT = (select MAX(EFFDT)
                  from   PS_JOB
                  where  EFFDT <= $AsOfDate
                    and  BB.EMPLID = EMPLID
                    and  BB.EMPL_RCD = EMPL_RCD)
  and CC.EMPLID = BB.EMPLID
  and BB.EMPL_CLASS NOT IN ('G','I','T')
  and BB.REG_TEMP <> 'T'
  and BB.JOBCODE = AA.JOBCODE
  and DD.INSEE_CD_FRA = AA.INSEE_CD_FRA
  and DD.EFFDT = (select MAX(EFFDT)
                  from   PS_INSEE_TBL_FRA
                  where  EFFDT <= $InseeDt
                    and  INSEE_CD_FRA = DD.INSEE_CD_FRA)
  and FF.EMPLID = BB.EMPLID
  and FF.CONTRACT_NUM = BB.CONTRACT_NUM
  and FF.CONTRACT_TYPE <> 'APP'
  and BB.ACTION NOT IN ('TER','RET')
  and BB.ESTABID = $ESTABID
  and BB.REG_REGION = RR.REG_REGION
  and RR.COUNTRY = $RegRegion
order by BB.JOBCODE

end-SELECT

   if $CurrentJobCode <> '' AND (#CountMale > 0 OR #CountFemale > 0) AND #Action = 1
      do Print-Data
   end-if

   if (#GlobalCountFemale > 0 OR #GlobalCountMale > 0) AND #Action = 1
      do Print-End-Banner
   end-if

end-procedure

!***********************************************************************
!  Print data retrieved from D1-3
!***********************************************************************

begin-procedure Print-Banner

   print $CATEGORY_NUMBER           (+3,85)
   print $EMPLOY_BY_CATEGORY        (+1,1,83)
   print $PCS_CATEGORY              (,85,22)
   print $MEN                       (,108,9)
   print $WOMEN                     (,118)
   print ' '                        (+1,1)   ! Jumps a line

end-procedure

!***********************************************************************
!  Print data retrieved from D1-3
!***********************************************************************

begin-procedure Print-Data

   print $CurrentJobCode (+1,1,85)
   print $CurrentPCSCode (,90,18)
   print #CountMale      (,109)  EDIT 9999
   print #CountFemale    (,119)  EDIT 9999

end-procedure

!***********************************************************************
!  Print data retrieved from D1-3
!***********************************************************************

begin-procedure Print-End-Banner

   print $TOTAL                (+1,102,6)
   print #GlobalCountMale      (,109)  EDIT 99999
   print #GlobalCountFemale    (,119)  EDIT 99999

end-procedure

!***********************************************************************

begin-procedure Get-Company-Std-Hours

begin-SELECT ON-ERROR=SQL-Error
STD_HRS_DEFAULT      &STD_COMPANY_HRS
STD_HRS_FREQUENCY    &STD_HRS_FREQUENCY

   let #StdCompanyHrs = &STD_COMPANY_HRS
   let $StdHrsFrequency = &STD_HRS_FREQUENCY

from PS_INSTALLATION
end-SELECT

end-procedure

!***********************************************************************
!  Counts people doing a special ability job
!***********************************************************************

begin-procedure D2-2

   move 0 to #ToAdd
   move 0 to #CountSpecial
   move 0 to #CountNormal

   do Get-Company-Std-Hours

   move 0 to #PeopleCount
   let $CurrentPCSCode = ''

   print $CODE         (+2,1)

   print $PCS          (+1,1,5)
   print $NUMBER       (,7)
   print ' '           (+1,1)

   do Get-Disability-Rpt-Rate
   do Define-PrimJob-Clause('BB', $AsOfDate, $PRIM_JOB_CLAUSE)

begin-SELECT ON-ERROR=SQL-Error

BB.JOBCODE                                               &A! For platform compatibility
BB.EMPLID                                                &EMPLID
{DATEOUT-PREFIX}BB.EFFDT{DATEOUT-SUFFIX}         &EFFDT

   if not IsNull(&EFFDT)
      do Convert-To-DTU-Date(&EFFDT, $DTUEFFDT)
   end-if
BB.ACTION                                                &ACTION
BB.FULL_PART_TIME                                        &FULL_PART_TIME
BB.STD_HOURS                                             &STD_HOURS
   let #StdHrs = &STD_HOURS
BB.FTE                                                   &FTE
   let #Fte = &FTE
DD.SPEC_ABILITY_FRA                                      &SPECIAL_ABILITY
DD.INSEE_CD_FRA                                          &PCS
FF.CONTRACT_TYPE                                         &CONTRACT_TYPE

   if &PCS <> $CurrentPCSCode AND &SPECIAL_ABILITY = 'Y'
      if $CurrentPCSCode <> '' and #PeopleCount <> 0
         print $CurrentPCSCode   (+1,1,6)
         print #PeopleCount      (,8)    EDIT  999.9
      end-if
      let $CurrentPCSCode = &PCS
      move 0 to #PeopleCount
   end-if

   Move 0 to #ToAdd

   if &CONTRACT_TYPE = 'CDD' OR &CONTRACT_TYPE = 'SAI'
      do CountByEmployee
      if #MonthsWorked = 13
         Add 1 to #ToAdd
      end-if
      if #MonthsWorked <> -1
         let #ToAdd = #MonthsWorked / 12
      end-if
   end-if

   if &CONTRACT_TYPE = 'CDI'
      if &FULL_PART_TIME = 'F'
         Add 1 to #ToAdd
      else
         if (#Fte > 0)
            let #ToAdd = #Fte
         else
            if(#StdCompanyHrs > 0)
               let #ToAdd = (#StdHrs / #StdCompanyHrs)
            else
                print $ERR1 (+1,1)
            end-if
         end-if
      end-if
   end-if

   if &SPECIAL_ABILITY = 'Y'
      Add #ToAdd to #PeopleCount
      Add #ToAdd to #CountSpecial
   else
      Add #ToAdd to #CountNormal
   end-if

from PS_JOBCODE_TBL AA,
     PS_JOB BB,
     PS_INSEE_TBL_FRA DD,
     PS_CNT_ACTIVE_VW FF,
     PS_REG_REGION_TBL RR

where AA.EFFDT = (select MAX(EFFDT)
                  from   PS_JOBCODE_TBL
                  where  EFFDT <= $InseeDt
                    and  AA.JOBCODE = JOBCODE
                    and  AA.SETID = SETID)
  and AA.EFF_STATUS = 'A'
  and BB.JOBCODE = AA.JOBCODE
  and BB.EMPL_RCD = [$PRIM_JOB_CLAUSE]
  and BB.EFFDT = (select MAX(EFFDT)
                  from   PS_JOB
                  where  EFFDT <= $AsOfDate
                    and  BB.EMPLID = EMPLID
                                        and  BB.EMPL_RCD = EMPL_RCD)
  and BB.EFFSEQ = (select MAX(EFFSEQ)
                   from   PS_JOB
                   where  BB.EFFDT = EFFDT
                     and  BB.EMPLID = EMPLID
                     and  BB.EMPL_RCD = EMPL_RCD)
  and BB.EMPL_CLASS NOT IN ('I','T')
  and BB.ESTABID = $ESTABID
  and BB.REG_REGION = RR.REG_REGION
  and RR.COUNTRY = $RegRegion
  and DD.INSEE_CD_FRA = AA.INSEE_CD_FRA
  and DD.EFFDT = (select MAX(EFFDT)
                  from   PS_INSEE_TBL_FRA
                  where  EFFDT <= $InseeDt
                    and  INSEE_CD_FRA = DD.INSEE_CD_FRA)
  and FF.EMPLID = BB.EMPLID
  and FF.CONTRACT_NUM = BB.CONTRACT_NUM
  and ((FF.CONTRACT_TYPE = 'CDI' and BB.ACTION NOT IN ('TER','RET'))
        or (FF.CONTRACT_TYPE IN ('CDD','SAI') and BB.ACTION IN ('TER','RET','HIR','REH','REC')))

order by BB.JOBCODE

end-SELECT

   if $CurrentPCSCode <> '' and #PeopleCount <> 0
      print $CurrentPCSCode (+1,1,6)
      print #PeopleCount   (,8)    EDIT  999.9
   end-if

   let #CountTotal = #CountNormal + #CountSpecial

   print $SUBJECT_NUMBER      (+2,1,38)
   print $T1                  (,40,3)
   print #CountTotal          (,44)   EDIT 99999
   print $BENEF_NUMBER        (,65)

   print $TOTAL_EMPLOY1       (+1,1)
   print $TOTAL_EMPLOY2       (+1,1,38)
   print $T2                  (,40,3)
   print #CountSpecial        (,44)   EDIT 99999
   let $strT1T2 = '(' || $T1 || '-' || $T2 || ')'
   let $strTotal = $strT1T2 || 'x'
   print $strTotal (,67)
   print #DisabilityRate (,+1)        EDIT 9.99
   print ' = '           (,+1)
   let #Benef = #CountNormal * #DisabilityRate
   print #Benef               (,83)   EDIT 999.99

   print ' '                  (+1,1)

   print $SUBJECT             (+1,1,35)
   print $strT1T2             (,37)
   print #CountNormal         (,44)   EDIT 99999

end-procedure

!***********************************************************************
!  Gets the disability report rate
!***********************************************************************

begin-procedure Get-Disability-Rpt-Rate

begin-SELECT ON-ERROR=SQL-Error
A.DISAB_RPT_RATE_FRA    &DISAB_RPT_RATE_FRA

   move &DISAB_RPT_RATE_FRA to $DisabilityRate 999.99

   let #DisabilityRate = &DISAB_RPT_RATE_FRA
   if #DisabilityRate <= 0
      move 1 to #DisabilityRate
   end-if
FROM
        PS_EXT_PARM_FRA A
WHERE
        A.EFFDT = (SELECT MAX(B.EFFDT)
                FROM  PS_EXT_PARM_FRA B
                WHERE B.EFFDT <= $AsOfDate
                AND B.EXT_PARM_CD_FRA = A.EXT_PARM_CD_FRA)
end-SELECT

end-procedure

!***********************************************************************
!  For a 'CDD' or a 'SAI' determine when his contract is finished
!***********************************************************************

begin-procedure CountByEmployee

   do dtu-parse-date($DTUAsOfDate, #CurrentYear, #unused, #unused)
   let $Years = to_char(#CurrentYear)
   let $DTUBeginingYear = $Years || '-01-01'

   do Diff-Date($EFFDT, $BeginingYear, #unused, #unused, #days)

if #days < 0
   if &ACTION <> 'TER' AND &ACTION <> 'RET'
      !Count for the year
      move 13 to #MonthsWorked
   else
      ! Doesn't count anymore
      move -1 to #MonthsWorked
   end-if
else
   do Find-Next-Event
end-if

end-procedure


!***********************************************************************
!  Used in case of an employee hired, fired and rehired in the same year
!***********************************************************************

begin-procedure Find-Next-Event

   move 0 to #MonthsWorked
   let $DTUEndDate = ''

   if &ACTION = 'HIR' OR &ACTION = 'REH' OR &ACTION = 'REC'
      ! Count from &EFFDT to the end of the year
      do Diff-Date($AsOfDate, $EFFDT, #unused, #Months, #unused)

      Add #Months to #MonthsWorked
   else
      ! Count Stops at this date
      let $DTUEndDate = $DTUEFFDT
   end-if

   Move 0 to #flag

  do Convert-From-DTU-Date($DTUBeginingYear, $BeginingYear)
  do Define-PrimJob-Clause('BB', &EFFDT, $PRIM_JOB_CLAUSE)

begin-SELECT ON-ERROR=SQL-Error

{DATEOUT-PREFIX}BB.EFFDT{DATEOUT-SUFFIX}   &EFFDT1
   if not IsNull(&EFFDT1)
      do Convert-To-DTU-Date(&EFFDT1, $DTUEFFDT1)
   end-if

BB.ACTION                                          &ACTION1

   if &ACTION1 = 'HIR' OR &ACTION1 = 'REH' OR &ACTION1 = 'REC'
      if($DTUEndDate = '')
         print 'Error' (+2,1)
      else
         ! Count from &EFFDT1 to $EndDate
         do Diff-Date($EndDate, $EFFDT1, #unused, #Months, #unused)
         Add #Months to #MonthsWorked
         let $DTUEndDate = ''
      end-if
   else
      ! Count Stops at this date
      let $DTUEndDate = $DTUEFFDT
   end-if
   move 1 to #flag
from
     PS_JOB BB,
     PS_CNT_ACTIVE_VW FF,
     PS_REG_REGION_TBL RR

where BB.EMPL_RCD = [$PRIM_JOB_CLAUSE]
  and BB.EFFDT < &EFFDT
  and BB.EFFDT >= $BeginingYear
  and BB.EFFSEQ = (select MAX(EFFSEQ)
                   from   PS_JOB
                   where  BB.EFFDT = EFFDT
                     and  BB.EMPLID = EMPLID
                     and  BB.EMPL_RCD = EMPL_RCD)
  and BB.EMPL_CLASS NOT IN ('I','T')
  and BB.EMPLID = &EMPLID
  and BB.ESTABID = $ESTABID
  and BB.REG_REGION = RR.REG_REGION
  and RR.COUNTRY = $RegRegion
  and BB.ACTION IN ('TER','RET','HIR','REC','REH')
  and FF.EMPLID = BB.EMPLID
  and FF.CONTRACT_NUM = BB.CONTRACT_NUM
  and FF.CONTRACT_TYPE IN ('CDD','SAI')

end-SELECT

   if #flag = 0 and $DTUEndDate <> '' !Nothing found
      do Diff-Date($EndDate, $BeginingYear, #unused, #Months, #unused)
      Add #Months to #MonthsWorked
   end-if
end-procedure

!***********************************************************************
!  Prints the list of Disable workers
!***********************************************************************

begin-procedure D2-4

begin-SELECT ON-ERROR=SQL-Error

A.EMPLID                                                       &D2.EMPLID
A.NAME                                                         &D2_NAME
A.SEX                                                          &D2_SEX
{DATEOUT-PREFIX}A.BIRTHDATE{DATEOUT-SUFFIX}            &D2_BIRTHDATE
   if not IsNull(&D2_BIRTHDATE)
      do Convert-To-DTU-Date(&D2_BIRTHDATE, $DTUD2_BIRTHDATE)
   end-if
   do Get_Orig_Hire_Dt
   if not IsNull(&D2_ORIG_HIRE_DT)
      do Convert-To-DTU-Date(&D2_ORIG_HIRE_DT, $DTUD2_ORIG_HIRE_DT)
   end-if
B.FULL_PART_TIME                                               &D2_FULL_PART_TIME
B.STD_HOURS                                                    &D2_STD_HOURS
   let #StdHrs = &D2_STD_HOURS
B.FTE                                                          &D2_FTE
   let #Fte = &D2_FTE
D.DESCRLONG                                                    &D2_DESCR
{DATEOUT-PREFIX}D.EFFDT{DATEOUT-SUFFIX}                &INSEE_DT2
D.INSEE_CD_FRA                                                 &D2_PCS_CODE_FRA
F.DISABLE_TYPE_FRA                                             &D2_DISABLE_TYPE_FRA
F.DIS_COTOREP_RT_FRA                                           &D2_DIS_COTOREP_RT_FRA
F.DIS_PRIOR_PLC_FRA                                            &D2_DIS_PRIOR_PLC_FRA
F.PLACEMENT_TYPE_FRA                                           &D2_PLACEMENT_TYPE_FRA
F.DIS_IPP_RT_FRA                                               &D2_DIS_IPP_RT_FRA

   let $D2_NAME = &D2_NAME

   let $INSEE_TBL_FRA-DESCRLONG = &D2_DESCR
   do Get_Related_INSEE_TBL_FRA(&INSEE_DT2, &D2_PCS_CODE_FRA)
   let $D2_DESCR = $INSEE_TBL_FRA-DESCRLONG

   if &D2_FULL_PART_TIME = 'F'
      Move 1 to #Coeff
   else
      if (#Fte > 0)
         let #Coeff = #Fte
      else
         if(#StdCompanyHrs > 0)
            let #Coeff = (#StdHrs / #StdCompanyHrs)
         else
            print $ERR1 (+1,1)
         end-if
      end-if
   end-if

   do dtu-parse-date($DTUD2_BIRTHDATE, #BirthYear, #unused, #unused)

   do dtu-parse-date($DTUD2_ORIG_HIRE_DT, #OrigHireYear, #unused, #unused)

   do Print-Banner1
   do Print-Data1
   do Print-Banner2
   do Print-Data2
   do Print-Banner3
   do Print-Data3

from    PS_PERSONAL_DT_FST A,
        PS_JOB B,
        PS_JOBCODE_TBL C,
        PS_INSEE_TBL_FRA D,
        PS_DISABILITY_FRA F,
        PS_DISABILITY I,
        PS_REG_REGION_TBL R

where   I.DISABLED = 'Y'
   and  B.CONTRACT_NUM <> ' '
   and  I.EMPLID = A.EMPLID
   and  A.EMPLID = B.EMPLID
   and  B.JOBCODE = C.JOBCODE
   and  B.ESTABID = $ESTABID
   and  B.REG_REGION = R.REG_REGION
   and  R.COUNTRY = $RegRegion
   and  B.EFFDT = (select MAX(EFFDT)
                  from   PS_JOB
                  where  EFFDT <= $AsOfDate
                    and  B.EMPLID = EMPLID
                    and  B.EMPL_RCD = EMPL_RCD)
   and  B.EFFSEQ = (select MAX(EFFSEQ)
                   from   PS_JOB
                   where  B.EFFDT = EFFDT
                     and  B.EMPLID = EMPLID
                     and  B.EMPL_RCD = EMPL_RCD)
   and  C.EFFDT = (select MAX(EFFDT)
                  from   PS_JOBCODE_TBL
                  where  EFFDT <= $InseeDt
                    and  C.JOBCODE = JOBCODE
                    and  C.SETID = SETID)
   and C.EFF_STATUS = 'A'
   and D.INSEE_CD_FRA = C.INSEE_CD_FRA
   and D.EFFDT = (select MAX(EFFDT)
                 from   PS_INSEE_TBL_FRA
                 where  EFFDT <= $InseeDt
                   and  INSEE_CD_FRA = D.INSEE_CD_FRA)
   and A.EMPLID = F.EMPLID

end-SELECT

end-procedure
!****************************************************************************
!---------Get Orig Hire Date-------------------------------------------
!****************************************************************************
begin-procedure Get_Orig_Hire_Dt
begin-select
{DATEOUT-PREFIX}ORH.ORIG_HIRE_DT{DATEOUT-SUFFIX}         &D2_ORIG_HIRE_DT
from PS_ORIG_HIRE_VW ORH
where ORH.EMPLID = &D2.EMPLID
and ORH.PER_ORG = 'EMP'
end-select
end-procedure Get_Orig_Hire_Dt
!***********************************************************************
!  Prints data retrieved from D2-4
!***********************************************************************

begin-procedure Print-Data1

   print $D2_NAME               (+1,3,42)
   print &D2_SEX                (,48,8)
   print #BirthYear             (,57) EDIT 9999
   print #OrigHireYear          (,70) EDIT 9999
   print $D2_DESCR              (,80,43)
   print &D2_PCS_CODE_FRA       (,128)

end-procedure

!***********************************************************************
!  Prints data retrieved from D2-4
!***********************************************************************

begin-procedure Print-Data2

   move 0 to #MdHandicapImportance   !Md = Modifier
   move 0 to #MdHandicapAge
   Move 0 to #MdPrevPlacement
   Move 0 to #MdTraining
   Move 0 to #MdHandicapRate

   Evaluate &D2_DISABLE_TYPE_FRA
   When = 'COT'
      Evaluate &D2_DIS_COTOREP_RT_FRA
      When = 'A'
         print 'X' (+1,3)
         move 0 to #MdHandicapImportance   !Md = Modifier
         break
      When = 'B'
         print 'X' (+1,6)
         move 0.5 to #MdHandicapImportance
         break
      When = 'C'
         print 'X' (+1,9)
         move 1 to #MdHandicapImportance
         break
      When-Other
!         let $ErrorStr = 'Catégorie de handicap:' || &D2_DIS_COTOREP_RT_FRA || ' Inconnu'
!         print $ErrorStr (+1,1)
         break
      end-Evaluate

      do dtu-add-years($DTUD2_BIRTHDATE, 25, $DTUDateCmp1)
      do dtu-add-years($DTUD2_BIRTHDATE, 50, $DTUDateCmp2)

      do Diff-Date($AsOfDate, $DateCmp1, #unused, #unused, #days1)
      do Diff-Date($AsOfDate, $DateCmp2, #unused, #unused, #days2)

      if #days1 < 0 OR #days2 >= 0
         print $YES  (,18,11)
         move 0.5 to #MdHandicapAge
      else
         print $NO  (,18,11)
      end-if

      print '???'     (,30,8)
      Move 0 to #MdTraining

      if &D2_DIS_PRIOR_PLC_FRA = 'Y'
         Move 1 to #MdPrevPlacement
         Evaluate &D2_PLACEMENT_TYPE_FRA
         When = 'AP'
            print 'X' (,39)
            break
         When = 'IMPR'
            print 'X' (,44)
            break
         When = 'CAT'
            print 'X' (,49)
            break
         When = 'CDTD'
            print 'X' (,53)
            break
         When = 'CFP'
            print 'X' (,58)
            break
         When-Other
!            let $ErrorStr = 'Type de placement:' || &D2_PLACEMENT_TYPE_FRA || ' Inconnu'
!            print $ErrorStr (+1,1)
            break
         end-Evaluate
      end-if

      print $NO                  (,71,19)
      print $NO                  (,91,14)
      print $NO                  (,106,14)
      print $NO                  (,121)
      break
   When = 'VAM'
      print &D2_DIS_IPP_RT_FRA     (+1,71,19)

      Evaluate &D2_DIS_IPP_RT_FRA
      When > 66.66
      When <= 85
         move 0.5 to #MdHandicapRate
         break
      When > 85
         move 1.5 to #MdHandicapRate
         break
      end-Evaluate

      print $NO                  (,91,14)
      print $NO                  (,106,14)
      print $NO                  (,121)
      break
   When = 'TPI'
      print $NO                  (+1,71,19)
      print $YES                  (,91,14)
      print $NO                  (,106,14)
      print $NO                  (,121)
      break
   When = 'TPM'
      print $NO                  (+1,71,19)
      print $NO                  (,91,14)
      print $YES                  (,106,14)
      print $NO                  (,121)
      break
   When = 'FEM'
   When = 'ORP'
   When = 'VGR'
      print $NO                  (+1,71,19)
      print $NO                  (,91,14)
      print $NO                  (,106,14)
      print $YES                  (,121)
      break
   When-Other
!      let $ErrorStr = 'Type d invalidité :' || &D2_DISABLE_TYPE_FRA || ' Inconnu'
!      print $ErrorStr (+1,1)
      break
   end-Evaluate

end-procedure

!***********************************************************************
!  Prints data retrieved from D2-4
!***********************************************************************

begin-procedure Print-Data3

   Move 0 to #MdHireDate

   print '1'                     (+1,1)
   print '+'                     (,3)
   print #MdHandicapImportance   (,5)     EDIT 9.9
   print '+'                     (,14)
   print #MdHandicapAge          (,18)    EDIT 9.9
   print '+'                     (,25)
   print #MdTraining             (,30)    EDIT 9.9
   print '+'                     (,37)
   print #MdPrevPlacement        (,48)    EDIT 9.9
   print $OR                    (,63)
   print #MdHandicapRate         (,66)    EDIT 9.9
   print '+'                     (,70)

   do dtu-add-years($DTUD2_ORIG_HIRE_DT, 2, $DTUDateCmp)
   do Diff-Date($AsOfDate, $DateCmp, #unused, #unused, #days1)

   if #days1 < 0
      Move 1 to #MdHireDate
   end-if

   print #MdHireDate             (,74)    EDIT 9.9
   print '='                     (,80)

   let #SubTotal = 1 + #MdHandicapImportance + #MdHandicapAge + #MdPrevPlacement + #MdTraining + #MdHandicapRate + #MdHireDate

   print #SubTotal               (,88)    EDIT 9.9
   print 'x'                     (,102)
   print #Coeff                  (,108)   EDIT 9.99
   print '='                     (,119)

   let #Total = #SubTotal * #Coeff

   print #Total                  (,124)   EDIT 9.99

end-procedure

!***********************************************************************
!  Prints data retrieved from D2-4
!***********************************************************************

begin-procedure Print-Banner1

   print ' '               (+2,3,43)
   print ' '               (,47,7)
   print $BIRTH_YEAR1      (,55,12)
   print $EMPLOY_YEAR1     (,68,11)
   print ' '               (,80,43)
   print $NUM_OF_CTGRY1    (,124)

   print $NAME             (+1,3,43)
   print $SEX              (,47,7)
   print $BIRTH_YEAR2      (,55,12)
   print $EMPLOY_YEAR2     (,68,11)
   print $JOB              (,80,43)
   print $NUM_OF_CTGRY2    (,124)

end-procedure

!***********************************************************************
!  Prints data retrieved from D2-4
!***********************************************************************

begin-procedure Print-Banner2

   print $COTOREP_DISAB       (+2,3,59)
   print $OR                  (,63,4)
   print $ACCIDENT_DISAB1     (,68,14)
   print $OR                  (,83,5)
   print ' '                  (,88,11)
   print $OR                  (,100,4)
   print $WAR_DISAB1          (,105,8)
   print $OR                  (,114,4)
   print $ASSIM_WAR_DISAB1    (,119)

   print $CATGRY              (+1,3,12)
   print $LESS25              (,16,10)
   print $TRAINING            (,27,40)
   print $ACCIDENT_DISAB2     (,68,19)
   print $PENSION_DISAB1      (,88,16)
   print $WAR_DISAB2          (,105,13)
   print $WAR_DISAB1          (,119)

   print $DISABILITY          (+1,3,14)
   print $MORE50              (,18,9)
   print $MORE500             (,28,12)
   print $PREV                (,40,27)
   print $ACCIDENT_DISAB3     (,68,19)
   print $PENSION_DISAB2      (,88,16)
   print $WAR_DISAB3          (,105,13)
   print $WAR_DISAB3          (,119)

   print 'A'                  (+1,3,2)
   print 'B'                  (,6,2)
   print 'C'                  (,9,29)
   print $AP                  (,39,2)
   print $IMPRO               (,42,5)
   print $CAT                 (,48,3)
   print $CDTD                (,52,4)
   print $CFP                 (,57,11)
   print $IPP_RATE            (,69)

end-procedure

!***********************************************************************
!  Prints data retrieved from D2-4
!***********************************************************************

begin-procedure Print-Banner3

   print ' '                (+2,71,8)
   print $TOTAL_UNITS1      (,80,22)
   print ' '                (,103,16)
   print $NUM_UNITS1        (,120)

   print $EMPLOY_YEAR1      (+1,71,8)
   print $TOTAL_UNITS2      (,80,22)
   print $COEF1             (,103,16)
   print $NUM_UNITS2        (,120)

   print $EMPLOY_YEAR2      (+1,71,8)
   print $TOTAL_UNITS3      (,80,22)
   print $COEF2             (,103,16)
   print $TOTAL_UNITS2      (,120)

end-procedure

!***********************************************************************

#include 'hrctlfra.sqc'  !Get run control parameter values
#include 'hrgetfra.sqc'  !Get values mask routines
#include 'stdapi.sqc'    !Routines to update run status

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'useprntr.sqc'
#Include 'sqrtrans.sqc'
#Include 'datemath.sqc'
#Include 'getprimj.sqc'  !Routines for selecting EE's primary job

