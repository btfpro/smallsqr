!**************************************************************************
! PER071JP : Actual Headcount Report                                      *
!**************************************************************************
!                                                                         *
!               Confidentiality Information:                              *
!                                                                         *
!                                                                         *
! This module contains confidential and proprietary information           *
! of Oracle; it is not to be copied, reproduced, or transmitted           *
! in any form, by any means, in whole or in part, nor is it to            *
! be used for any purpose other than that for which it is                 *
! expressly provided under the applicable license agreement.              *
!                                                                         *
! Copyright (C) 2006 Oracle. All Rights Reserved.                         *
!                                                                         *
!**************************************************************************
!                                                                         *
!          $Date:  2006/07/19:14:57:35                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                         *
!**************************************************************************
!                                                                         *
!**************************************************************************
!This report displays the Headcount Goals and Actual Headcounts dept wise *
!                                                                         *
!**************************************************************************
Begin-setup

#define  REPORTID  'PER071JP'
#include 'setenv.sqc'      !Set environment
#include 'ptset02.sqc'     !Init printer and page sizeLandscape

end-setup

begin-program
    do Init-DateTime
    do Init-Number
    do Get-Current-DateTime
    do Define-Prcs-Vars
    do Init-Report
    do Process-Main
    do Delete-From-AE-Tables
    do Update-Prcs-Run-Status
end-program

!*************************************************************************!
! Init-Report
!*************************************************************************!
begin-procedure Init-Report

!***********************************************************************
! Translations procedures & Strings Table retrievals.                  *
!***********************************************************************

  move {REPORTID} to $ReportID
  let #Error_Total = 0
  do Get-Run-Control-Parms
  do Get-Run-Cntl-Acq
  do get-setid
  do Get-Tree-Node
  do get-org-plan-date


  do Init_Report_Translation ('PER071JP', $curr_language_cd)
  do Append_Report_Translation ('PER071JP')

  do Get_Field_Information ('PER071JP' ,'PER071JP_TITLE'    ,$PER071JP_Title          ,#DW)
  do Get_Field_Information ('PER071JP' ,'ORG_PLAN'          ,$org_plan_lbl            ,#DW)
  do Get_Field_Information ('PER071JP' ,'EFF_DATE'          ,$org_date_lbl            ,#DW)
  do Get_Field_Information ('PER071JP' ,'BUNIT'             ,$BUnit_lbl               ,#DW)
  do Get_Field_Information ('PER071JP' ,'DEPT'              ,$dept_lbl                ,#DW)
  do Get_Field_Information ('PER071JP' ,'LVL'               ,$Lvl_lbl                 ,#DW)
  do Get_Field_Information ('PER071JP' ,'ORG_PLAN1'         ,$org_plan1_lbl           ,#DW)
  do Get_Field_Information ('PER071JP' ,'ACTUAL'            ,$Actual_lbl              ,#DW)
  do Get_Field_Information ('PER071JP' ,'HCNT'              ,$Headcount_lbl           ,#DW)
  do Get_Field_Information ('PER071JP' ,'TREE_NAME'         ,$TREE_NAME_lbl           ,#DW)
  do Get_Field_Information ('PER071JP' ,'NODE'              ,$NODE_lbl                ,#DW)
  
  if $hc_type_JPN = 'S'
   do Get_Field_Information ('PER071JP' ,'SUPV'              ,$HC_type_lbl            ,#DW)
  end-if

  if $hc_type_JPN = 'J'
   do Get_Field_Information ('PER071JP' ,'JOB_CODE'          ,$HC_type_lbl            ,#DW)
  end-if
  
  IF $hc_type_JPN = 'G'
   do Get_Field_Information ('PER071JP' ,'SAL_GRADE'         ,$HC_type_lbl            ,#DW)
  end-if
  
  move $PER071JP_Title to $ReportTitle
  let $ReportDate            = $AsOfToday          ! Printed in header
  let $ReportTime            = $AsOfNow            ! Printed in header

end-procedure Init-Report

!*************************************************************************!
! Get-Run-Cntl-Acq
!*************************************************************************!
begin-procedure Get-Run-Cntl-Acq

  let $org_plan_JPN  = ''
  let $org_date_JPN  = ''
  let $hc_type_JPN  = ''
  let $Business_Unit  = ''
  let $TREE_NAME  = ''
  let $TREE_NODE  = ''
  let $EFFDT_JPN  = ''
  let $setid  = ''

begin-SELECT

RC.BUSINESS_UNIT
RC.ORG_PLAN_JPN
RC.TREE_NAME
RC.TREE_NODE
RC.EFFDT_JPN
RC.SETID

  let $org_plan_JPN  = &RC.ORG_PLAN_JPN
  let $Business_Unit = &RC.BUSINESS_UNIT
  let $TREE_NAME     = &RC.TREE_NAME
  let $TREE_NODE     = &RC.TREE_NODE
  let $EFFDT_JPN     = &RC.EFFDT_JPN
  !let $setid         = &RC.SETID

FROM
PS_RC_IDO_JPN RC

WHERE
RC.OPRID = $prcs_oprid
AND RC.RUN_CNTL_ID = $prcs_run_cntl_id

end-SELECT
end-procedure !Get-Run-Cntl-Acq

!*************************************************************************!
! get-setid
!*************************************************************************!
!*************************************************************************!
! Get SETID
!*************************************************************************!
begin-procedure get-setid
begin-select
CR.SETID
 let $setid = &CR.SETID
FROM PS_SET_CNTRL_REC CR
WHERE CR.RECNAME = 'JOBCODE_TBL' AND CR.SETCNTRLVALUE = $Business_Unit
end-select
end-procedure !get-setid

!*************************************************************************!
! Get-Tree-Node
!*************************************************************************!
begin-procedure Get-Tree-Node
  let #tree_node_num = 0
  let #tree_node_num_end = 0
if rtrim($TREE_NODE, ' ') = ''
  let #tree_node_num = 0
  let #tree_node_num_end = 9999999999
else
begin-SELECT
A.TREE_NODE_NUM
A.TREE_NODE_NUM_END

  let #tree_node_num = &A.TREE_NODE_NUM
  let #tree_node_num_end = &A.TREE_NODE_NUM_END
  
FROM PSTREENODE A

WHERE A.SETID = $setid
AND A.TREE_NAME = $TREE_NAME
AND A.TREE_NODE = $TREE_NODE
AND A.EFFDT = $EFFDT_JPN
end-SELECT
end-if

end-procedure !Get-Tree-Node

!*************************************************************************!
! get-org-plan-date
!*************************************************************************!

begin-procedure get-org-plan-date
begin-select
A.HC_TYPE_JPN
A.ORG_DATE_JPN
 let $org_date_JPN_In  = &A.ORG_DATE_JPN
 do Convert-To-DTU-Date ($org_date_JPN_In, $org_date_JPN)
 let $AsOfDate      = &A.ORG_DATE_JPN
 let $hc_type_JPN   = &A.HC_TYPE_JPN
 
FROM PS_IDO_ORGPLAN_JPN A
WHERE A.ORG_PLAN_JPN = $org_plan_JPN
end-select
end-procedure !get-org-plan-date

begin-procedure Process-Main
  if $hc_type_JPN = 'S'
   do Get-suplvl-data
  end-if

  if $hc_type_JPN = 'J'
   do Get-jobcode-data
  end-if

  if $hc_type_JPN = 'G'
   do Get-grade-data
  end-if
end-procedure !Process-Main

begin-procedure Get-suplvl-data

begin-select DISTINCT
D.TREE_LEVEL_NUM
I.DEPTID 
I.SUPV_LVL_ID 

 let #tree_lvl_num = &D.TREE_LEVEL_NUM
 let $DeptID        = &I.DEPTID
 let $SUPV_LVL_ID   = &I.SUPV_LVL_ID
 
 let $BU = $Business_Unit
 
 do Get-Department-Name
 do Get-Inactive-Dept
 do get-setid-dept
 do Get-Supvlvl-Name
 do Get-HCDEPT-headcount-suplvl
 do Get-Actual-headcount-suplvl
 do Print_Line

FROM PS_IDO_NEW_ORG_JPN I ,PSTREENODE D
WHERE I.JOBINSTANCE = #prcs_job_instance
AND I.BUSINESS_UNIT = $Business_Unit
AND D.TREE_NODE = I.DEPTID
AND D.SETID = $setid
AND D.TREE_NAME = $TREE_NAME
AND D.TREE_NODE_NUM BETWEEN #tree_node_num AND #tree_node_num_end
AND D.EFFDT = $EFFDT_JPN
UNION
SELECT
H1.TREE_LEVEL_NUM,
H.DEPTID,
H.SUPV_LVL_ID 
FROM PS_IDO_HC_DEPT_JPN H ,PSTREENODE H1
WHERE H.ORG_PLAN_JPN = $org_plan_JPN
AND H.BUSINESS_UNIT = $Business_Unit
AND H1.TREE_NODE = H.DEPTID
AND H1.SETID = $setid
AND H1.TREE_NAME = $TREE_NAME
AND H1.TREE_NODE_NUM BETWEEN #tree_node_num AND #tree_node_num_end
AND H1.EFFDT = $EFFDT_JPN
ORDER BY 2,3
end-select
end-procedure !Get-suplvl-data

!*************************************************************************!
! Get-HCDEPT-headcount-suplvl
!*************************************************************************!
begin-procedure Get-HCDEPT-headcount-suplvl
 let #HCDEPT-headcount = 0
begin-select
C.HEADCOUNT_JPN
 let #HCDEPT-headcount = &C.HEADCOUNT_JPN 
FROM PS_IDO_HC_DEPT_JPN C
WHERE  C.ORG_PLAN_JPN = $org_plan_JPN
AND C.BUSINESS_UNIT = $Business_Unit
AND C.DEPTID = $DeptID
AND C.SUPV_LVL_ID = $SUPV_LVL_ID
end-select
end-procedure !Get-HCDEPT-headcount-suplvl

!*************************************************************************!
! Get-Actual-headcount-suplvl
!*************************************************************************!
begin-procedure Get-Actual-headcount-suplvl
 let #Actual-headcount = 0
begin-select
COUNT(*) &ACT_COUNT
 let #Actual-headcount = &ACT_COUNT
FROM PS_IDO_NEW_ORG_JPN A
WHERE A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
AND A.DEPTID = $DeptID
AND A.SUPV_LVL_ID = $SUPV_LVL_ID
end-select
end-procedure !Get-Actual-headcount-suplvl

!*************************************************************************!
! Get-jobcode-data
!*************************************************************************!
begin-procedure Get-jobcode-data
begin-select DISTINCT
T1.TREE_LEVEL_NUM
J.DEPTID
J.JOBCODE

 let #tree_lvl_num = &T1.TREE_LEVEL_NUM
 let $DeptID        = &J.DEPTID
 let $JobCode       = &J.JOBCODE
 let $BU = $Business_Unit
 
 do Get-Department-Name
 do Get-Inactive-Dept
 do Get-Job-Title
 do Get-HCDEPT-headcount-jobcode
 do Get-Actual-headcount-jobcode
 do Print_Line

FROM PS_IDO_NEW_ORG_JPN J, PSTREENODE T1
WHERE J.JOBINSTANCE = #prcs_job_instance
AND J.BUSINESS_UNIT = $Business_Unit
AND T1.TREE_NODE = J.DEPTID
AND T1.SETID = $setid
AND T1.TREE_NAME = $TREE_NAME
AND T1.TREE_NODE_NUM BETWEEN #tree_node_num AND #tree_node_num_end
AND T1.EFFDT = $EFFDT_JPN
UNION
SELECT 
H2.TREE_LEVEL_NUM,
IHC.DEPTID, 
IHC.JOBCODE
FROM PS_IDO_HC_DEPT_JPN IHC, PSTREENODE H2
WHERE IHC.ORG_PLAN_JPN = $org_plan_JPN
AND IHC.BUSINESS_UNIT = $Business_Unit
AND H2.TREE_NODE = IHC.DEPTID
AND H2.SETID = $setid
AND H2.TREE_NAME = $TREE_NAME
AND H2.TREE_NODE_NUM BETWEEN #tree_node_num AND #tree_node_num_end
AND H2.EFFDT = $EFFDT_JPN
ORDER BY 2,3
end-select
end-procedure !Get-jobcode-data

!*************************************************************************!
! Get-HCDEPT-headcount-jobcode
!*************************************************************************!
begin-procedure Get-HCDEPT-headcount-jobcode
 let #HCDEPT-headcount = 0
begin-select
C1.HEADCOUNT_JPN

 let #HCDEPT-headcount = &C1.HEADCOUNT_JPN
 
FROM PS_IDO_HC_DEPT_JPN C1
WHERE  C1.ORG_PLAN_JPN = $org_plan_JPN
AND C1.BUSINESS_UNIT = $Business_Unit
AND C1.DEPTID = $DeptID
AND C1.JOBCODE = $JobCode
end-select
end-procedure !Get-HCDEPT-headcount-jobcode

!*************************************************************************!
! Get-Actual-headcount-jobcode
!*************************************************************************!
begin-procedure Get-Actual-headcount-jobcode
 let #Actual-headcount = 0
begin-select
COUNT(*) &ACT_COUNT1
 let #Actual-headcount = &ACT_COUNT1
FROM
PS_IDO_NEW_ORG_JPN A
WHERE 
A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
AND A.DEPTID = $DeptID
AND A.JOBCODE = $JobCode
end-select
end-procedure !Get-Actual-headcount-jobcode

!*************************************************************************!
! Get-grade-data
!*************************************************************************!
begin-procedure Get-grade-data
begin-select DISTINCT
T2.TREE_LEVEL_NUM
G.DEPTID 
G.SAL_ADMIN_PLAN 
G.GRADE 

 let #tree_lvl_num = &T2.TREE_LEVEL_NUM
 let $DeptID        = &G.DEPTID
 let $GRADE          = &G.GRADE
 
 let $BU = $Business_Unit
 
 do Get-Department-Name
 do Get-Inactive-Dept
 do Get-Grade-Name
 do Get-HCDEPT-headcount-grade
 do Get-Actual-headcount-grade
 do Print_Line

FROM PS_IDO_NEW_ORG_JPN G,PSTREENODE T2
WHERE JOBINSTANCE = #prcs_job_instance
AND BUSINESS_UNIT = $Business_Unit
AND T2.TREE_NODE = G.DEPTID
AND T2.SETID = $setid
AND T2.TREE_NAME = $TREE_NAME
AND T2.TREE_NODE_NUM BETWEEN #tree_node_num AND #tree_node_num_end
AND T2.EFFDT = $EFFDT_JPN
UNION
SELECT
H3.TREE_LEVEL_NUM,
IHC1.DEPTID, 
IHC1.SAL_ADMIN_PLAN,
IHC1.GRADE
FROM PS_IDO_HC_DEPT_JPN IHC1, PSTREENODE H3
WHERE IHC1.ORG_PLAN_JPN = $org_plan_JPN
AND IHC1.BUSINESS_UNIT = $Business_Unit
AND H3.TREE_NODE = IHC1.DEPTID
AND H3.SETID = $setid
AND H3.TREE_NAME = $TREE_NAME
AND H3.TREE_NODE_NUM BETWEEN #tree_node_num AND #tree_node_num_end
AND H3.EFFDT = $EFFDT_JPN
ORDER BY 2,3,4
end-select
end-procedure !Get-grade-data

!*************************************************************************!
! Get-HCDEPT-headcount-grade
!*************************************************************************!
begin-procedure Get-HCDEPT-headcount-grade
 let #HCDEPT-headcount = 0
begin-select
C2.HEADCOUNT_JPN

 let #HCDEPT-headcount = &C2.HEADCOUNT_JPN
 
FROM PS_IDO_HC_DEPT_JPN C2
WHERE  C2.ORG_PLAN_JPN = $org_plan_JPN
AND C2.BUSINESS_UNIT = $Business_Unit
AND C2.DEPTID = $DeptID
AND C2.SAL_ADMIN_PLAN = &G.SAL_ADMIN_PLAN
AND C2.GRADE = &G.GRADE
end-select
end-procedure !Get-HCDEPT-headcount-grade

!*************************************************************************!
! Get-Actual-headcount-grade
!*************************************************************************!
begin-procedure Get-Actual-headcount-grade
 let #Actual-headcount = 0
begin-select
COUNT(*) &ACT_COUNT2
 let #Actual-headcount = &ACT_COUNT2
FROM PS_IDO_NEW_ORG_JPN A
WHERE A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
AND A.DEPTID = $DeptID
AND A.SAL_ADMIN_PLAN = &G.SAL_ADMIN_PLAN
AND A.GRADE = &G.GRADE
end-select
end-procedure !Get-Actual-headcount-grade

!*************************************************************************!
! get-setid-dept
!*************************************************************************!
begin-procedure get-setid-dept
begin-select
A5.SETID
 let $SETID_DEPT = &A5.SETID
FROM PS_SET_CNTRL_REC A5
WHERE A5.RECNAME = 'SUPVSR_LVL_TBL'
AND A5.SETCNTRLVALUE = $Business_Unit
end-select
end-procedure !get-setid-dept

!*************************************************************************!
! Get-Inactive-Dept
!*************************************************************************!

begin-procedure Get-Inactive-Dept

begin-select
EFF_STATUS
FROM PS_DEPT_TBL 
WHERE SETID = $Business_Unit
AND DEPTID = $DeptID
AND EFFDT = (SELECT MAX(EFFDT) 
FROM PS_DEPT_TBL 
WHERE SETID = $Business_Unit
AND DEPTID = $DeptID
AND EFFDT <= &A.ORG_DATE_JPN)
end-select

end-procedure !Get-Inactive-Dept

begin-heading 12
#include 'stdhdgtr.sqc'

   print $org_plan_lbl (+1,1,22)
   print $org_plan_JPN (0,25,4)
   print $org_date_lbl (+1,1,12)
   print $org_date_JPN (0,16,10)
   print $BUnit_lbl (+1,1,15)
   print $Business_Unit (0,16,6)
   print $TREE_NAME_lbl (+1,1,15)
   print $TREE_NAME (0,16,15)
   print $NODE_lbl (+1,1,15)
   print $TREE_NODE (0,16,15)
   
   print $org_plan1_lbl (+1,127,30)
   print $Actual_lbl (0,150,20)
   print '-' (+1,100,45) FILL
   print '-' (0,150,10) FILL
   
   print $Lvl_lbl (+1,1,4)
   print $dept_lbl (0,5,40)
   print $HC_type_lbl (0,100,30)
   print $Headcount_lbl (0,135,10)
   print $Headcount_lbl (0,150,10)
   print '-' (+1,1,60) FILL
   print '-' (0,100,30) FILL
   print '-' (0,135,10) FILL
   print '-' (0,150,10) FILL
   print '' (,5, 40) 

end-heading
!*************************************************************************!
! Print_Line
!*************************************************************************!
begin-procedure Print_Line
  print #tree_lvl_num (+1,1, 4) edit 99
  print $DeptID (0,5,10) 
  print $DeptName (0,20,40) 
  if $hc_type_JPN = 'S'
   print $SUPV_LVL_ID (0,100,8)
   print $SupvlvlName (0,110,20) 
  end-if
  
  if $hc_type_JPN = 'J' 
   print $JobCode (0,100,8)
   print $JobTitle (0,110,20)   
  end-if
  if $hc_type_JPN = 'G'   
   let $hc_type_JPN_data = ltrim(rtrim(&G.SAL_ADMIN_PLAN,' '),' ')
   let $GRADE = ltrim(rtrim($GRADE,' '),' ')
   if $hc_type_JPN_data <> '' or $GRADE <> '' 
    concat '/' with $hc_type_JPN_data
    concat $GRADE with $hc_type_JPN_data
   end-if
   print $hc_type_JPN_data  (0,100,30) 
  end-if
  print #HCDEPT-headcount (0,137,10) edit 9999
  print #Actual-headcount (0,152,10) edit 9999
  if &EFF_STATUS = 'I'
   print 'Inactive Department' (0,157,20)
  end-if

end-procedure !Print_Line

!*************************************************************************!
! Delete-From-AE-Tables
!*************************************************************************!

begin-procedure Delete-From-AE-Tables

begin-sql
Delete from PS_IDO_NEW_ORG_JPN
Where JOBINSTANCE = #prcs_job_instance
end-sql

end-procedure !Delete-From-AE-Tables

!*************************************************************************!
! SQC Files for called procedures
!*************************************************************************!
#include 'prcsdef.sqc'       !Update Process Request variable declare
#include 'prcsapi.sqc'       !Update Process Request API
#include 'hrsecty.sqc'       !Get SQR Security parameters
#include 'curdttim.sqc'      !Get-Current-DateTime procedure
#include 'datetime.sqc'      !Routines for date and time formatting
#include 'number.sqc'        !Routines to format numbers
#include 'sqrtrans.sqc'      !Translate SQR strings to given language
#include 'datemath.sqc'      !Date conversion procedures
#include 'getsupjp.sqc'     ! Get Supervisor Level Name
#Include 'getempnm.sqc'     ! Get Employee Name
#include 'getdptnm.sqc'     ! Get Department Name from the Department Table
#include 'getjobtl.sqc'     ! Get Job Title
#include 'getgrdjp.sqc'     ! Get Grade Name
