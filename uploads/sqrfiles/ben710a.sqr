!***********************************************************************
!  BEN710A: Leave Plan Tables 2 and 3
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:08:48:48                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

#define col_sep                  1   !Column Separator
#define col_sep2                 2   !Column Separator
#define col_plan_type            5   !Plan Type
#define col_plan_name           10   !Plan Name
#define col_ben_plan             7   !Ben Plan
#define col_ben_pln_name        30   !Benefit Plan Name
#define col_eff_date            11   !Eff Date
#define col_serv_intervl        11   !Service Interval
#define col_serv_rate_val       21   !Service Rate Values
#define col_serv_bonus_val      21   !Service Bonus Values
#define col_pay_vs_time          4   !Pay vs Time
#define col_pay_at_term          4   !Pay at Term
#define col_term_pay_pr          7   !Term Pay %
#define col_neg_bal              5   !Neg Bal?
#define col_first_yr_rt_val     21   !First Year Rate Values

begin-program
  do Init-Report

!--- 'LEAVE PLAN TABLES 2 & 3'
  do Get-Report-Language
  let $ReportTitle  = $BEN710A-REPORTTITLE
  display ' '
  display $ReportTitle
  display ' '

  do Process-Main

  do Reset
  do StdAPI-Term
end-program

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  let $ReportID  = 'BEN710A'

  do Get-Current-DateTime
  do StdAPI-Init
end-procedure

!**********************************
! This procedure is used to read language-dependent text from the database.
!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BEN710A program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN710A', 'REPORT_TITLE',  $BEN710A-REPORTTITLE,   #CW)
  do Get_Field_Information ('BEN710A', 'SRV_RT_VAL',    $BEN710A-SRV-RT-VAL,    #CW)
  do Get_Field_Information ('BEN710A', 'SRV_BONUS_VAL', $BEN710A-SRV-BONUS-VAL, #CW)
  do Get_Field_Information ('BEN710A', 'FRST_YR_RT_VAL',$BEN710A-FRST-YR-RT-VAL,#CW)
  do Get_Field_Information ('BEN710A', 'TERM',          $BEN710A-TERM,          #CW)
  do Get_Field_Information ('BEN710A', 'SERVICE',       $BEN710A-SERVICE,       #CW)
  do Get_Field_Information ('BEN710A', 'LENGTH',        $BEN710A-LENGTH,        #CW)
  do Get_Field_Information ('BEN710A', 'VS',            $BEN710A-VS,            #CW)
  do Get_Field_Information ('BEN710A', 'AT',            $BEN710A-AT,            #CW)
  do Get_Field_Information ('BEN710A', 'INTERVAL',      $BEN710A-INTERVAL,      #CW)
  do Get_Field_Information ('BEN710A', 'OF_SERV_HRS_ERN', $BEN710A-OF-SERV-HRS-ERN,#CW)
  do Get_Field_Information ('BEN710A', 'TIME',          $BEN710A-TIME,          #CW)
  do Get_Field_Information ('BEN710A', 'TERM',          $BEN710A-TERM,          #CW)
  do Get_Field_Information ('BEN710A', 'PERCENT',       $BEN710A-PERCENT,       #CW)
  do Get_Field_Information ('BEN710A', 'BAL',           $BEN710A-BAL,           #CW)
  do Get_Field_Information ('BEN710A', 'NEG',           $BEN710A-NEG,           #CW)
  do Get_Field_Information ('BEN710A', 'EMPL_MNTH',     $BEN710A-EMPL-MNTH,     #CW)
  do Get_Field_Information ('BEN710A', 'MNTH_HRS_ERN_EL', $BEN710A-MNTH-HRS-ERN-EL,#CW)

  do Get_Field_Information ('BEN1',   'EFFECTIVE',    $BEN1-EFFECTIVE,     #CW)
  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,          #CW)
  do Get_Field_Information ('BEN1',   'BENEFIT',      $BEN1-BENEFIT,       #CW)
  do Get_Field_Information ('BEN1',   'DATE',         $BEN1-DATE,          #CW)
  do Get_Field_Information ('BEN1',   'PAY',          $BEN1-PAY,           #CW)
  do Get_Field_Information ('BEN1',   'NAME',         $BEN1-NAME,          #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-TYPE,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN_NAME',    $BEN1-PLAN-NAME ,    #CW)

End-Procedure

!***************
begin-HEADING 10
!***************
  #Include 'stdhdgtr.sqc'

!Column Heading line 1
  print ''                       (+2,1,{col_plan_type})
  print ''                       (0,+{col_sep},{col_plan_name})
  print ''                       (0,+{col_sep},{col_ben_plan})
  print ''                       (0,+{col_sep},{col_ben_pln_name})
  print ''                       (0,+{col_sep},{col_eff_date})
  print ''                       (0,+{col_sep},{col_serv_intervl})
! 'Service Rate Values'
  print $BEN710A-SRV-RT-VAL      (0,+{col_sep},{col_serv_rate_val})
! 'Service Bonus Values'
  print $BEN710A-SRV-BONUS-VAL   (0,+{col_sep2},{col_serv_bonus_val})
  print ''                       (0,+{col_sep2},{col_pay_vs_time})
  print ''                       (0,+{col_sep},{col_pay_at_term})
  print ''                       (0,+{col_sep},{col_term_pay_pr})
  print ''                       (0,+{col_sep2},{col_neg_bal})
! 'First Year Rate Values'
  print $BEN710A-FRST-YR-RT-VAL  (0,+{col_sep2},{col_first_yr_rt_val})

!Column Heading line 2
  print ''                       (+1,1,{col_plan_type})
  print ''                       (0,+{col_sep},{col_plan_name})
  print ''                       (0,+{col_sep},{col_ben_plan})
  print ''                       (0,+{col_sep},{col_ben_pln_name})
  print ''                       (0,+{col_sep},{col_eff_date})
  print ''                       (0,+{col_sep},{col_serv_intervl})
  print '-'                      (0,+{col_sep},{col_serv_rate_val})    FILL
  print '-'                      (0,+{col_sep2},{col_serv_bonus_val})  FILL
  print $BEN1-PAY                (0,+{col_sep2},{col_pay_vs_time})
  print $BEN1-PAY                (0,+{col_sep},{col_pay_at_term})
  print $BEN710A-TERM            (0,+{col_sep},{col_term_pay_pr})
  print ''                       (0,+{col_sep2},{col_neg_bal})
  print '-'                      (0,+{col_sep2},{col_first_yr_rt_val}) FILL

!Column Heading line 3
  print $BEN1-PLAN               (+1,1,{col_plan_type})
  print $BEN1-PLAN               (0,+{col_sep},{col_plan_name})
  print $BEN1-BENEFIT            (0,+{col_sep},{col_ben_plan})
  print $BEN1-BENEFIT            (0,+{col_sep},{col_ben_pln_name})
  print $BEN1-EFFECTIVE          (0,+{col_sep},{col_eff_date})
  print $BEN710A-SERVICE         (0,+{col_sep},{col_serv_intervl})
  print $BEN710A-LENGTH          (0,+{col_sep},{col_serv_rate_val})
  print $BEN710A-LENGTH          (0,+{col_sep2},{col_serv_bonus_val})
  print $BEN710A-VS              (0,+{col_sep2},{col_pay_vs_time})
  print $BEN710A-AT              (0,+{col_sep},{col_pay_at_term})
  print $BEN1-PAY                (0,+{col_sep},{col_term_pay_pr})
  print $BEN710A-NEG             (0,+{col_sep2},{col_neg_bal})
  print $BEN710A-EMPL-MNTH       (0,+{col_sep2},{col_first_yr_rt_val})

!Column Heading line 4
  print $BEN1-TYPE               (+1,1,{col_plan_type})
  print $BEN1-NAME               (0,+{col_sep},{col_plan_name})
  print $BEN1-PLAN               (0,+{col_sep},{col_ben_plan})
  print $BEN1-PLAN-NAME          (0,+{col_sep},{col_ben_pln_name})
  print $BEN1-DATE               (0,+{col_sep},{col_eff_date})
  print $BEN710A-INTERVAL        (0,+{col_sep},{col_serv_intervl})
  print $BEN710A-OF-SERV-HRS-ERN (0,+{col_sep},{col_serv_rate_val})
  print $BEN710A-OF-SERV-HRS-ERN (0,+{col_sep2},{col_serv_bonus_val})
  print $BEN710A-TIME            (0,+{col_sep2},{col_pay_vs_time})
  print $BEN710A-TERM            (0,+{col_sep},{col_pay_at_term})
  print $BEN710A-PERCENT         (0,+{col_sep},{col_term_pay_pr})
  print $BEN710A-BAL             (0,+{col_sep2},{col_neg_bal})
  print $BEN710A-MNTH-HRS-ERN-EL (0,+{col_sep2},{col_first_yr_rt_val})

!Column Heading line 5
  print '='               (+1,1,{col_plan_type})                 FILL
  print '='               (0,+{col_sep},{col_plan_name})         FILL
  print '='               (0,+{col_sep},{col_ben_plan})          FILL
  print '='               (0,+{col_sep},{col_ben_pln_name})      FILL
  print '='               (0,+{col_sep},{col_eff_date})          FILL
  print '='               (0,+{col_sep},{col_serv_intervl})      FILL
  print '='               (0,+{col_sep},{col_serv_rate_val})     FILL
  print '='               (0,+{col_sep2},{col_serv_bonus_val})   FILL
  print '='               (0,+{col_sep2},{col_pay_vs_time})      FILL
  print '='               (0,+{col_sep},{col_pay_at_term})       FILL
  print '='               (0,+{col_sep},{col_term_pay_pr})       FILL
  print '='               (0,+{col_sep2},{col_neg_bal})          FILL
  print '='               (0,+{col_sep2},{col_first_yr_rt_val})  FILL

end-HEADING

!***********************************************************************
!  PROC Process Main
!         Main Procedure
!***********************************************************************

begin-procedure Process-Main
begin-SELECT

  NEXT-LISTING NEED=3

A.PLAN_TYPE                       (1,1,2) on-break level=1
  move 'PLAN_TYPE'  to $FieldName
  move &A.Plan_Type to $FieldValue
  do Read-Translate-Table
  print $XlatShortName            (1,7,10)  on-break level=1
A.BENEFIT_PLAN                    (1,18,6) on-break level=2
  move &A.Benefit_Plan to $Benefit_Plan
  move &A.Plan_Type    to $Plan_Type
  do Get-Benefit-Name
  print $BenefitName              (1,26,30) on-break level=2
A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                      (1,57,10)
A.SERVICE_INTERVAL
  move 'SERVICE_INTERVAL'  to $FieldName
  move &A.Service_Interval to $FieldValue
  do Read-Translate-Table
  print $XlatShortName            (1,69,11)
B.SERVICE_INTERVALS               (1,81,6)  EDIT 99,999
B.HOURS_EARNED                    (1,89,11)  EDIT 9999.999999
A.PAY_VS_LEAVE                    (1,127,1)
A.PAY_AT_TERMINATION              (1,133,1)
A.PAY_TERM_PCT                    (1,138,6) EDIT 999.99
A.HRS_GO_NEGATIVE                 (1,146,1)

  do Get-Other-Records

FROM PS_LEAVE_PLAN_TBL A,
     PS_LEAVE_RATE_TBL B
WHERE B.BENEFIT_PLAN = A.BENEFIT_PLAN
  and B.PLAN_TYPE    = A.PLAN_TYPE
  and B.EFFDT        = A.EFFDT
Order by A.PLAN_TYPE,
         A.BENEFIT_PLAN,
         A.EFFDT,
         B.SERVICE_INTERVALS,
         B.HOURS_EARNED

end-SELECT
end-procedure

!***********************************************************************
!  Get all Others
!***********************************************************************
begin-procedure Get-Other-Records

   move 1 to #line
   position (#line)

begin-SELECT
D.BENEFIT_PLAN
D.PLAN_TYPE
D.EMPLOYMENT_MONTH           (,154,2) EDIT 99
D.HOURS_EARNED               (,158,11) EDIT 9999.999999
D.MONTH_ELIGIBLE             (,172,2) EDIT 99

   position (+1)

FROM PS_LEAVE_SRVYR_TBL D
WHERE D.BENEFIT_PLAN = &A.Benefit_Plan
  and D.PLAN_TYPE    = &A.Plan_Type
  and D.EFFDT        = &A.Effdt
Order by D.BENEFIT_PLAN,
         D.PLAN_TYPE,
         D.EMPLOYMENT_MONTH,
         D.HOURS_EARNED,
         D.MONTH_ELIGIBLE

end-SELECT

   move 1 to #line
   position (#line)

begin-SELECT
C.BENEFIT_PLAN
C.PLAN_TYPE
C.SERVICE_INTERVALS         (,105,6) EDIT 99,999
C.HOURS_EARNED              (,113,11) EDIT 9999.999999

   position (+1)

FROM PS_LEAVE_ADDL_TBL C
WHERE C.BENEFIT_PLAN = &A.Benefit_Plan
  and C.PLAN_TYPE    = &A.Plan_Type
  and C.EFFDT        = &A.Effdt
Order by C.BENEFIT_PLAN,
         C.PLAN_TYPE,
         C.SERVICE_INTERVALS,
         C.HOURS_EARNED

end-SELECT
end-procedure

!**********************************************************************
#Include 'getbennm.sqc'  !Get-Benefit-Name procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!************************ End Of Report ********************************
