!********************************************************
!  PAY130: Re-Open/Close Year-End Off-Cycle Calendar    *
!********************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:45:35                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup31.sqc'   !Printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY130' to $ReportID
  move 'Open/Close Year-End Off-Cycle Calendar' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Get-Current-DateTime

  do Stdapi-Init

  do Get-Calendar-Year-Id

  if $Prcs_Process_Instance = ''
     do Prompts
  else
     do Select-US-Year-End-Parameters
     do Convert-Parameters
  end-if
end-procedure


begin-procedure Process-Main
  do Get-Current-Year-Calendar
  if $Current-Year-Found = 'Y'
    do Get-Year-End-Calendar
    if $Year-End-Found = 'Y'
      if $Open_Close_Flag = 'O'
        if &B.Pay_Off_Cycle_Cls = 'Y'
          move 'N' to $Pay_Off_Cycle_Cls
          do Update-Year-End-Calendar
          do Update-HR-Calendar-Xref
          if $Update-Error = 'N'
            display 'Year-End Calendar has been opened.'
          end-if
        else
          display 'Year-End Calendar is already open.'
        end-if
      else
        if &B.Pay_Off_Cycle_Cls = 'N'
          move 'Y' to $Pay_Off_Cycle_Cls
          do Update-Year-End-Calendar
          if $Update-Error = 'N'
            Display  'Year-End Calendar has been closed.'
          end-if
        else
          display 'Year-End Calendar is already closed.'
        end-if
      end-if
    else
      display 'Year-End Calendar not found.'
    end-if
  else
    display 'Current Year Calendar not found.'
  end-if
end-procedure


begin-procedure Get-Current-Year-Calendar
  let $Current-Year-Found = 'N'
begin-SELECT
MAX(A1.BALANCE_YEAR)      &Current-Year
  let #Current-Year = &Current-Year
  let $Current-Year-Found = 'Y'
FROM PS_PAY_CALENDAR A, PS_PAY_CAL_BAL_ID A1
WHERE A.COMPANY = A1.COMPANY AND A.PAYGROUP = A1.PAYGROUP
  AND A.PAY_END_DT = A1.PAY_END_DT AND A1.BALANCE_ID = $Calendar_Year_Id
 AND A.COMPANY       = $Company
 AND  A.PAYGROUP      = $PayGroup
 AND  A.PAY_SHEETS_RUN = 'Y'
end-SELECT
end-procedure


begin-procedure Get-Year-End-Calendar
  let $Year-End-Found = 'N'
begin-SELECT
B.PAY_END_DT
B.PAY_OFF_CYCLE_CLS
  let $Year-End-Found = 'Y'
FROM PS_PAY_CALENDAR B
WHERE B.COMPANY       = $Company
 AND  B.PAYGROUP      = $PayGroup
 AND  B.PAY_END_DT    =
            (SELECT MAX(C.PAY_END_DT)
             FROM   PS_PAY_CALENDAR C, PS_PAY_CAL_BAL_ID C1
             WHERE C.COMPANY = C1.COMPANY AND C.PAYGROUP = C1.PAYGROUP
  AND C.PAY_END_DT = C1.PAY_END_DT AND C1.BALANCE_ID = $Calendar_Year_Id
              AND C.COMPANY       = B.COMPANY
              AND   C.PAYGROUP      = B.PAYGROUP
              AND   C1.BALANCE_YEAR < #Current-Year)
end-SELECT
end-procedure


begin-procedure Update-Year-End-Calendar
  let $Update-Error = 'N'
begin-SQL  on-error=Bad-Update
UPDATE PS_PAY_CALENDAR
SET PAY_OFF_CYCLE_CLS = $PAY_OFF_CYCLE_CLS
WHERE COMPANY    = $COMPANY
 AND  PAYGROUP   = $PAYGROUP
 AND  PAY_END_DT = &B.PAY_END_DT
end-SQL
end-procedure


begin-procedure Bad-Update
  let $Update-Error = 'Y'
end-procedure


! Update cross reference used by HR Wage Progression if matching entry found
begin-procedure Update-HR-Calendar-Xref
begin-SQL ON-ERROR=Ignore
UPDATE PS_HR_PAY_CAL_XREF
SET PAY_OFF_CYCLE_CLS = 'N'
WHERE COMPANY    = $COMPANY
 AND  PAYGROUP   = $PAYGROUP
 AND  PAY_END_DT = &B.PAY_END_DT
 AND  PAY_OFF_CYCLE_CLS = 'Y'
end-SQL
end-procedure


begin-procedure Prompts
  display ' '
  input $Company maxlen=10 'Enter Company' type=char
  uppercase $Company
  display ' '
  input $PayGroup maxlen=10 'Enter Pay Group' type=char
  uppercase $PayGroup
  display ' '
  input $Open_Close_Flag maxlen=1 'Enter "O" to Open or "C" to Close' type=char
  uppercase $Open_Close_Flag
  display ' '
end-procedure

begin-procedure Convert-Parameters

  move $RC_US_YE.Company          to $Company
  move $RC_US_YE.PayGroup         to $PayGroup
  move $RC_US_YE.Open_Close_Flag  to $Open_Close_Flag

end-procedure


!**********************
begin-procedure Ignore
!**********************

! dummy procedure to avoid SQL error message
end-procedure



#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'txrnctl2.sqc'  !U.S. Year End Process Scheduler Run Controls
#Include 'stdapi.sqc'    !Update Process API

#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
