!***********************************************************************
!  PAY717: Earnings Program Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:04:01                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup31.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY717' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language
  move $Pay717_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY717 program ids
  do Init_Report_Translation($ReportID,$language_cd)

  do Get_Field_Information ('PAY717', 'REPORT_TITLE', $Pay717_ReportTitle, #dummy_width)
  do Get_Field_Information ('PAY717', 'ERN_PRGM1',    $Pay717_Ern_Prgm1,   #dummy_width)
  do Get_Field_Information ('PAY717', 'EFFDT1',       $Pay717_EffDt1,      #dummy_width)
  do Get_Field_Information ('PAY717', 'DESCRSHORT1',  $Pay717_DescrShort1, #dummy_width)
  do Get_Field_Information ('PAY717', 'ERNCD1',       $Pay717_ErnCd1,      #dummy_width)
  do Get_Field_Information ('PAY717', 'ERN_PRGM2',    $Pay717_Ern_Prgm2,   #dummy_width)
  do Get_Field_Information ('PAY717', 'EFFDT2',       $Pay717_EffDt2,      #dummy_width)
  do Get_Field_Information ('PAY717', 'DESCRLONG2',   $Pay717_DescrLong2,  #dummy_width)
  do Get_Field_Information ('PAY717', 'DESCRSHORT2',  $Pay717_DescrShort2, #dummy_width)
  do Get_Field_Information ('PAY717', 'ERNCD2',       $Pay717_ErnCd2,      #dummy_width)
  do Get_Field_Information ('PAY717', 'ERN_DESCR2',   $Pay717_Ern_Descr2,  #dummy_width)

end-procedure


begin-HEADING 7

  #Include 'stdhdgtr.sqc'

  print $Pay717_Ern_Prgm1   (+2,1)
  print $Pay717_EffDt1      (0,10)
  print $Pay717_DescrShort1 (0,58)
  print $Pay717_ErnCd1      (0,71)

  print $Pay717_Ern_Prgm2   (+1,1)
  print $Pay717_EffDt2      (0,10)
  print $Pay717_DescrLong2  (0,26)
  print $Pay717_DescrShort2 (0,58)
  print $Pay717_ErnCd2      (0,71)
  print $Pay717_Ern_Descr2  (0,78)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

A.ERN_PROGRAM          (+1,1)  on-break level=1 skiplines=1
A.EFFDT
A.EFF_STATUS

  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out           (0,10)  on-break level=1


  if &A.Eff_Status = 'I'
    print 'I'          (0,22)  on-break level=1
  else
    print ' '          (0,22)
  end-if

A.DESCR                ! (0,26)  on-break level=1
A.DESCRSHORT           ! (0,58)  on-break level=1
          move &A.DESCR      to $PRNT_DESCR
          move &A.DESCRSHORT to $PRNT_DESCRSHORT
        if $Curr_language_cd <> $Psoptions_Language_Cd
        do Lang-Record-Details
        end-if
          print $PRNT_DESCR           (0,26)  on-break level=1
          print $PRNT_DESCRSHORT      (0,58)  on-break level=1

B.ERNCD                (0,71)
C.EFFDT
C.DESCR                ! (0,78)
          move &C.DESCR      to $PRNT_DESCR1
        if $Curr_language_cd <> $Psoptions_Language_Cd
        do Lang-Record-Details1
        end-if
          print $PRNT_DESCR1      (0,78)

FROM PS_ERN_PROGRAM_TBL A,
     PS_ERN_PROGRAM_DEF B,
     PS_EARNINGS_TBL    C
Where A.ERN_PROGRAM = B.ERN_PROGRAM
  and A.EFFDT       = B.EFFDT
  and B.ERNCD       = C.ERNCD
  and C.EFFDT =
                (SELECT MAX(C2.EFFDT)
                 FROM   PS_EARNINGS_TBL C2
                 WHERE  C2.ERNCD = C.ERNCD
                   and  C2.EFFDT <= A.EFFDT)

ORDER BY A.ERN_PROGRAM,
         A.EFFDT,
         A.DESCR,
         A.DESCRSHORT

end-SELECT

end-procedure


BEGIN-PROCEDURE Lang-Record-Details
BEGIN-SELECT
LA.DESCR
LA.DESCRSHORT

        let $PRNT_DESCR = Cond(Rtrim(&LA.DESCR,' ')='', &A.DESCR, &LA.DESCR)
        let $PRNT_DESCRSHORT = Cond(Rtrim(&LA.DESCRSHORT,' ')='', &A.DESCRSHORT, &LA.DESCRSHORT)

FROM PS_ERN_PROGRAM_LNG LA
WHERE LA.ERN_PROGRAM    = &A.ERN_PROGRAM
  AND LA.LANGUAGE_CD    = $Curr_language_cd
  AND LA.EFFDT          = (SELECT MAX(EFFDT) FROM PS_ERN_PROGRAM_LNG
                            WHERE ERN_PROGRAM    = &A.ERN_PROGRAM
                              AND LANGUAGE_CD    = $Curr_language_cd
                              AND EFFDT         <= &A.EFFDT)
END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Lang-Record-Details1
BEGIN-SELECT
LC.DESCR

        let $PRNT_DESCR1 = Cond(Rtrim(&LC.DESCR,' ')='', &C.DESCR, &LC.DESCR)

FROM PS_EARNINGS_LANG LC
WHERE LC.ERNCD       = &B.ERNCD
  AND LC.LANGUAGE_CD = $Curr_language_cd
  AND LC.EFFDT       = (SELECT MAX(EFFDT) FROM PS_EARNINGS_LANG
                         WHERE ERNCD       = &B.ERNCD
                           AND LANGUAGE_CD = $Curr_language_cd
                           AND EFFDT      <= &C.EFFDT)
END-SELECT
END-PROCEDURE

!********************************************************************
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language

