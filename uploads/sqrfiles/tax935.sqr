!***********************************************************************
!  TAX935:    Form 1096 - 1099R Transmittal - Totals Report            *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:07:31:10                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#include 'setup31.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'TAX935' to $ReportID
  move 'Form 1096 - 1099R Transmittal - Totals Report'
    to $ReportTitle
  display ''
  display 'Printing 1096 Transmittal Report'

  do Init-DateTime
  do Init-Number
  do Get-Calendar-Year-Id
  do Stdapi-Init
  do Get-Current-DateTime
  do Initialization

  if $PRCS_Process_Instance = ''
     do Prompts
  else
     do Select-US-Year-End-Parameters
     do Convert-Parameters
  end-if

end-procedure


begin-heading 9
  #Include 'stdhdg01.sqc'
  print 'Company'              (3,1)
  print &A.Company             (,+2)
  print &A.Descr               (0,+2)
  let $TaxYear = 'Tax Year ' || $CalYear
  print $TaxYear               (3,)    center
  print 'Federal EIN '         (4,1)
  move &A.Federal_EIN to $A.Federal_EIN 099999999
  print $A.Federal_EIN         (,+2)   edit xx-xxxxxxx
  print 'Dist Code(s):'        (5,1)
  print $DistribCd             (,+2)
  print 'Earn Code(s):'        (6,1)
  print $HeaderEarns           (,+2)
  print '1096'                 (+3,3)
  print 'Box'                  (+1,3)
  print 'Description'          (,10)
  print 'Contents / Amount'    (,65)
end-heading

begin-procedure Initialization
  move '1' to $MMLZero
  move '1' to $Year4
  do Format-DateTime($AsOfToday, $910Date, {DEFYMD},'','')

  move $910Date to $AsOfYear xxxx   !extract the current year
  move $AsOfYear  to #AsOfYear
  move #AsOfYear  to #saveAsOfYear
  subtract 1    from #AsOfYear
  do Get-Tax-Reporting-Run-Controls

  move &TX.Balance_Year    to $AsOfDate
  let $AsOfDate = $AsOfDate || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP}, '', 'native')

  if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''
    display ''
    display 'Current Year is not one greater than Tax Reporting Year.'
    display 'Current Year is    ' noline
    display  #saveAsOfYear  9999
    display 'Tax Reporting Year ' noline
    display &TX.Balance_Year
    input $Answer maxlen=1 'Do you want to continue? (Y/N)'
    uppercase $Answer
    if $Answer <> 'Y'
      stop
    end-if
  end-if

  move &TX.Balance_Year to $CalYear 9999
end-procedure

begin-procedure Process-Main
begin-SELECT
A.COMPANY
A.DESCR
A.FEDERAL_EIN
A.ADDRESS1
A.ADDRESS2
A.CITY
A.STATE
A.POSTAL
A.COUNTRY

  move 'N' to $EmployeeFound
  do Get-1099R-Count

  if #Co_1099R_Count > 0
    do Print-1096
    NEW-PAGE
  end-if

FROM  PS_COMPANY_TBL A
#ifdef TAXTEST
WHERE TAX_REPORT_TYPE = '2'
#else
WHERE TAX_REPORT_TYPE = 'R'  !1099-R Reporting Company
#endif
  AND EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
ORDER BY COMPANY
end-SELECT
end-procedure


begin-procedure Get-1099R-Count
  move 0 to #Co_1099R_Count
  move 0 to #Co_Grs_YTD
  move 0 to #Co_Tax_YTD
begin-SELECT
EB.EMPLID
#ifdef DB2ALL
DECIMAL(SUM(EB.GRS_YTD),15,3) &EB.Grs_YTD
#else
SUM(EB.GRS_YTD) &EB.Grs_YTD
#endif

  add 1 to #Co_1099R_Count
  add &EB.Grs_YTD to #Co_Grs_YTD

  do Get-Tax-Data

FROM PS_EARNINGS_BAL EB
WHERE EB.COMPANY           = &A.Company
  AND EB.ERNCD            IN [$SelectedEarns]
  AND EB.BALANCE_YEAR     = &TX.Balance_Year
  AND EB.BALANCE_ID       = $Calendar_Year_Id
  AND EB.GRS_YTD           > 0
  AND EB.BALANCE_PERIOD           =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_EARNINGS_BAL
       WHERE  EMPLID            = EB.EMPLID
         AND  COMPANY           = EB.COMPANY
         AND  BALANCE_YEAR     = EB.BALANCE_YEAR
         AND  BALANCE_ID       = EB.BALANCE_ID
         AND  EMPL_RCD          = EB.EMPL_RCD
         AND  SPCL_BALANCE      = EB.SPCL_BALANCE
         AND  ERNCD             = EB.ERNCD)
GROUP BY EB.EMPLID
ORDER BY EB.EMPLID
end-SELECT
end-procedure

begin-procedure Get-Tax-Data
begin-SELECT
D.TAX_YTD

  add &D.Tax_YTD to #Co_Tax_YTD

FROM  PS_TAX_BALANCE D
WHERE D.EMPLID        = &EB.EmplID
  AND D.COMPANY       = &A.Company
  AND D.STATE         = '$U'
  AND D.TAX_CLASS     = 'H'   !Withholding
  AND D.BALANCE_YEAR = &TX.Balance_Year
  AND D.BALANCE_ID    = $Calendar_Year_Id
  AND D.BALANCE_PERIOD =
      (SELECT MAX(BALANCE_PERIOD)
         FROM PS_TAX_BALANCE
        WHERE EMPLID        = D.EMPLID
          AND COMPANY       = D.COMPANY
          AND BALANCE_YEAR = D.BALANCE_YEAR
          AND  BALANCE_ID  = D.BALANCE_ID
          AND STATE         = D.STATE
          AND TAX_CLASS     = D.TAX_CLASS)
end-SELECT
end-procedure

begin-procedure Print-1096
  print '      Filer''s name                 '      (+2,4)
  print &A.Descr                                     (,65)

  let $City = RTRIM(&A.City,' ')
  print '      Filer''s address              '  (+2,4)
  print &A.Address1                              (,65)
  if RTRIM(&A.Address2, ' ') <> ''
    print &A.Address2                            (+1,65)
  end-if
  print $City                                    (+1,65)
  print &A.State                                 ()       edit ,bxx
!  print &A.Postal                                (,+1)

  Move &A.Postal to $Zip
  move $Zip            to $ZipExtsn ~~~~~~xxxx
  if &A.Country <> 'USA'
    Print $Zip                                   (,+1)
    print &A.Country                             (,+1)
  else
    if instr($Zip,'-',6) = 0
    and $ZipExtsn <> '    '
      let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
    end-if
    Print $Zip                                   (,+1)
  end-if

  print '      Contact person                 '  (+2,4)
  print '(not maintained in database)'           (,65)

  print '      Telephone number               '  (+2,4)
  print '(not maintained in database)'           (,65)

  move &A.Federal_EIN to $FedEIN 099999999
  print '1     Employer identification number '      (+2,4)
  print $FedEIN                                      (,65)   edit xx-xxxxxxx

  print '3     Total number of forms          '      (+2,4)
  print #Co_1099R_Count                              (,75)    edit 999,999

  move '99999999999999.99' to $Fmt

  print '4     Federal income tax withheld (1099-R Box 4)'    (+2,4)
  do Format-Number(#Co_Tax_YTD, $out, $Fmt)
  print $out                                         (,65)

  print '5     Total amount reported (1099-R Box 1)'  (+2,4)
  do Format-Number(#Co_Grs_YTD, $out, $Fmt)
  print $out                                         (,65)
end-procedure

begin-procedure Prompts
  while $DistribCd = ''
    input $DistribCd 'Enter Distribution Code for this run'
    uppercase $DistribCd
  end-while

  display 'Enter Earnings Code or hit ENTER to stop selecting'
  move '(''' to $SelectedEarns
  move ' ' to $SelectedErnCd
  while $SelectedErnCd <> ''
    input $SelectedErnCd 'Enter Earnings Code or hit ENTER to stop selecting'
    uppercase $SelectedErnCd
    if $SelectedErnCd <> ''
      let $SelectedEarns = $SelectedEarns || $SelectedErnCd || ''','''
      let $HeaderEarns = $HeaderEarns || $SelectedErnCd || ','
    end-if
  end-while

  let $SelectedEarns = SUBSTR($SelectedEarns,1,LENGTH($SelectedEarns) - 2)
  let $HeaderEarns = SUBSTR($HeaderEarns,1,LENGTH($HeaderEarns) - 1)
  let $SelectedEarns = $SelectedEarns || ')'
  if $SelectedEarns = ')'
    display 'At least one earnings code must be entered'
    display 'Program ended'
    stop
  end-if
end-procedure



begin-procedure Convert-Parameters

  move $RC_US_YE.Distrib_Code       to $DistribCd

  do Read-Earnings-Codes

end-procedure


begin-procedure Read-Earnings-Codes

move '(''' to $SelectedEarns
move ' ' to $SelectedErnCd

begin-SELECT
C.ERNCD

  move &C.Erncd           to $SelectedErnCd

  let $SelectedEarns = $SelectedEarns || $SelectedErnCd || ''','''
  let $HeaderEarns = $HeaderEarns || $SelectedErnCd || ','

FROM  PS_RC_TAX935 C
WHERE C.OPRID         = $Prcs_OprID
  AND C.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
end-SELECT


 let $SelectedEarns = SUBSTR($SelectedEarns,1,LENGTH($SelectedEarns) - 2)
 let $HeaderEarns = SUBSTR($HeaderEarns,1,LENGTH($HeaderEarns) - 1)
 let $SelectedEarns = $SelectedEarns || ')'

end-procedure


#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'txrnctl2.sqc'  !US Year End Process Scheduler Run Controls
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
