!***********************************************************************
!  TAX002SD:  Qtrly State Employee Wage List - SOUTH DAKOTA            *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:05:22                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Get USA variables

begin-setup

#include 'prt002.sqc'  !Printer and page-size initialization

end-setup

begin-report
  do Init-Report

  do Process-Main

  if #Employees_On_Page > 0 and #Employees_On_Page < 20
    move 'Y' to $PrintCompanyTotal
    do Page-Complete
  end-if

  do Stdapi-Term

end-report

begin-procedure Init-Report
  display 'Quarterly Employee Wage List - South Dakota'

  do Init-DateTime
  do Init-Number

  do Get-Tax-Reporting-Run-Controls

  move $PeriodEndDate to $AsOfDate  !for included procedures
  do Get-Calendar-Year-Id
  do Stdapi-Init

end-procedure

begin-heading 14
  print 'Date Quarter Ended '     (4,56)
  move '0' to $year4
  do Format-DateTime($PeriodEndDate, $out, {DEFMDY}, '', '')
  print $out                      (6,61)
  print &Employer_ID_SUT          (8,28)
  print $CompanyName              (9,28)
  print $Compnyadd1               (+1,28)
  let $field = RTRIM($Compnycity, ' ')
  print $field                    (+1,28)
  print &CT.State                 ()      edit ,bxx
  move  &CT.Postal                to $Zip
  move  &CT.Postal                to $ZipExtsn ~~~~~~xxxx
  if instr($Zip,'-',6) = 0
  and $ZipExtsn <> '    '
    let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if
  print $Zip                      (0,+2)
  print 'SSN'                     (14,6)
  print 'Name'                    (0,28)
  print 'Total Wages'             (0,52)
  print 'Excess Wages'            (0,66)
  print 'Page Total  '            (57,38)
  do Format-Number(#PageNLGrs, $out, '99999999.99')
  print $out                      (57,52)
  do Format-Number(#PageExcess, $out, '99999999.99')
  print $out                      (57,67)
  if $PrintCompanyTotal = 'Y'
    print 'Report Total '         (59,38)
    do Format-Number(#CoNLGrs, $out, '99999999.99')
    print $out                    (59,52)
    do Format-Number(#CoExcess, $out, '99999999.99')
    print $out                    (59,67)
  end-if
end-heading

begin-procedure Process-Main

  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

begin-SELECT
C.COMPANY () on-break print=never before=Company-Change
C.STATE
C.EMPLID
C.TAX_CLASS
C.LOCALITY
C.TXGRS_QTD
C.NLGRS_QTD

D1.NATIONAL_ID
PN.LAST_NAME
PN.FIRST_NAME
PN.MIDDLE_NAME

  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_PERSON_NAME PN,
      PS_PERS_NID D1, PS_COMPANY_TBL A
WHERE C.EMPLID            = D1.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND PN.EMPLID           = C.EMPLID
#ifdef TAXTEST
  AND C.STATE             = 'CA'
#else
  AND C.STATE             = 'SD'
#endif
  AND C.LOCALITY          = ' '
  AND C.TAX_CLASS         = 'U'                ! Unemployment
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = &TX.Balance_Year
  AND C.BALANCE_QTR       = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD    =
      (SELECT MAX(C1.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C1
       WHERE  C1.EMPLID        = C.EMPLID
         AND  C1.COMPANY       = C.COMPANY
         AND  C1.BALANCE_ID    = C.BALANCE_ID
         AND  C1.BALANCE_YEAR  = C.BALANCE_YEAR
         AND  C1.BALANCE_QTR   = C.BALANCE_QTR
         AND  C1.STATE         = C.STATE
         AND  C1.LOCALITY      = C.LOCALITY
         AND  C1.TAX_CLASS     = C.TAX_CLASS)
  AND C.NLGRS_QTD > 0
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
ORDER BY C.COMPANY, D1.NATIONAL_ID, C.STATE, C.LOCALITY, C.TAX_CLASS
end-SELECT
end-procedure

begin-procedure Process-Tax-Balance
  move &C.EmplID       to $priorEmplID
  move &D1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
    print 'Applied For'          (+2,6)
  else
    print &D1.NATIONAL_ID        (+2,6) edit xxxbxxbxxxx
  end-if

  let $LastName   = rtrim(&PN.LAST_NAME, ' ')
  let $FirstName  = rtrim(&PN.FIRST_NAME, ' ')
  let $MidInitial = {ps-substr}(&PN.MIDDLE_NAME,1,1)

  print $FirstName               (0,28)
  print $MidInitial              (0,+1)
  print $LastName                (0,+1,20)

  move &C.NLGrs_QTD to #NLGrs_QTD
  do Format-Number(#NLGrs_QTD, $out, '99999999.99')
  print $out                     (0,52)
  let #Excess = &C.NLGrs_QTD - &C.TxGrs_QTD
  do Format-Number(#Excess, $out, '99999999.99')
  print $out                     (0,67)

  add #NLGrs_QTD to #PageNLGrs
  add #Excess    to #PageExcess
  add #NLGrs_QTD to #CoNLGrs
  add #Excess    to #CoExcess
  add 1 to #Employees_On_Page

  if #Employees_On_Page = 20
    do Page-Complete
  end-if
end-procedure

begin-procedure Page-Complete
  add 1 to  #PAGE_NUM
  new-page
  move 0 to #PageNLGrs
  move 0 to #PageExcess
  move 0 to #Employees_On_Page
end-procedure

begin-procedure Company-Change
  if #Employees_On_Page < 20 and #Employees_On_Page > 0
    move 'Y' to $PrintCompanyTotal
    do Page-Complete
    move 'N' to $PrintCompanyTotal
  end-if
  move 0 to #PAGE_NUM
  move 0 to #CoNLGrs
  move 0 to #CoExcess
  move &C.Company to $Company
  do Get-Company-Data
#ifdef TAXTEST
  move 'CA' to $State
#else
  move 'SD' to $State
#endif
  do Get-State-Tax-Data             !Employer ID
end-procedure

#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API

