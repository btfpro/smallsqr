!***********************************************************************
!  PUHRG75: Pre-Upgrade Conversion: From Release 8SP1     *
!            Reports EEO Job Groups in the Jobcode table that do not   *
!            exist in the EEO Job Group table.                         *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/25:11:22:09                                 !
!       $Release:  HR9                                                 !
!      $Revision:  104                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   ! Printer and page-size initialization

!***********************************************************************

begin-program
        do Init-DateTime
        do Get-Current-DateTime
        do Init-Report
        display 'Report began at: ' NoLine
        do Display-Time
        do Process-Main
        do Reset
        do Stdapi-Term
        display 'Report ended at: ' NoLine
        do Display-Time
end-program

begin-heading 7
#include 'stdhdg01.sqc'

if $processing = 'JOBCODE.EEO_JOB_GROUP'
  print 'JOBCODE TABLE EEO_JOB_GROUP REPORT'            (+2,0,0) CENTER
  print 'SETID'                                         (+2,1,7)
  print 'JOBCODE'                                       (,+2,12)
  print 'EFFDT'                                         (0,+2,11)
  print 'DESCR'                                         (,+2,30)
  print 'EEO_JOB_GROUP'                                 (,+2,15)

 end-if

if $processing = 'DISTINCT_JOBCODE.EEO_JOB_GROUP'
  print 'DISTINCT JOBCODE TABLE EEO_JOB_GROUP REPORT'   (+2,0,0) CENTER
  print 'EEO_JOB_GROUP'                                 (+2,1,15)

 end-if

end-heading

!***********************************************************************
!  Function:     Init Report
!  Description:  Prompt for run-time parameters
!***********************************************************************

begin-procedure Init-Report

  move 'PUHRG75' to $ReportID
  move 'Upgrade Conversion Report ' to $ReportTitle
  display $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if

end-procedure Init-Report

begin-procedure Get-Values
!No Prompts
end-procedure Get-Values

!***********************************************************************
!  Function:     Process-Main
!  Description:  This is the main report procedure.  Calls select
!                routines for data in the PS_JOBCODE_TBL.
!***********************************************************************

begin-procedure Process-Main

  let $processing = 'JOBCODE.EEO_JOB_GROUP'
  do Select-Jobcode_eeo_group
  new-page

  let $processing = 'DISTINCT_JOBCODE.EEO_JOB_GROUP'
  do Select-Distinct_Jobcode_eeo_group
  new-page

end-procedure Process-Main

!***********************************************************************
!  Procedure:    Select-Jobcode_eeo_group
!  Description:  This is a main report procedure.  Selects and prints
!                all of the EEO_JOB_GROUP rows in the JOBCODE_TBL
!                that do not exist in the EEO_JOB_GRP_TBL.
!***********************************************************************

begin-procedure Select-Jobcode_eeo_group
let #COUNTER = 0

begin-SELECT ON-ERROR=SQL-Error

JOBCD.SETID                                             (+1,1, 7)
JOBCD.JOBCODE                                           (0,+2,12)
{DATETIMEOUT-PREFIX}JOBCD.EFFDT{DATETIMEOUT-SUFFIX}     (0,+2,11)
JOBCD.DESCR                                             (0,+2,30)
JOBCD.EEO_JOB_GROUP                                     (0,+2,15)

  let #COUNTER = #COUNTER + 1

FROM PS_JOBCODE_TBL JOBCD WHERE JOBCD.EEO_JOB_GROUP <> ' ' AND JOBCD.EEO_JOB_GROUP NOT IN (SELECT EEO_JOB_GROUP FROM PS_EEO_JOB_GRP_TBL)
 ORDER BY JOBCD.SETID, JOBCD.JOBCODE, JOBCD.DESCR, JOBCD.EEO_JOB_GROUP

end-SELECT

print 'Number of rows selected:  '           (+2,1,26)
print #COUNTER                               (,+1,)

end-procedure Select-Jobcode_eeo_group

!***********************************************************************
!  Procedure:    Select-Distinct_Jobcode_eeo_group
!  Description:  This is the main report procedure.  Selects and prints
!                the distinct EEO_JOB_GROUP that is in the JOBCODE_TBL,
!                but not the EEO_JOB_GRP_TBL.
!***********************************************************************

begin-procedure Select-Distinct_Jobcode_eeo_group
let #COUNTER = 0

begin-SELECT ON-ERROR=SQL-Error

DISTINCT EEOJOBCD.EEO_JOB_GROUP  (+1,1, 15)

  let #COUNTER = #COUNTER + 1

FROM PS_JOBCODE_TBL EEOJOBCD WHERE EEOJOBCD.EEO_JOB_GROUP <> ' 'AND EEOJOBCD.EEO_JOB_GROUP NOT IN (SELECT EEO_JOB_GROUP FROM PS_EEO_JOB_GRP_TBL)
 ORDER BY  EEOJOBCD.EEO_JOB_GROUP

end-SELECT

print 'Number of rows selected:  '                    (+2,1,26)
print #COUNTER                                        (,+1,)

end-procedure Select-Distinct_Jobcode_eeo_group


!*************************************
Begin-Procedure Display-Time
!*************************************

  do Get-Current-DateTime
  display $AsOfToday NoLine
  display ', '       NoLine
  display $AsOfNow
  display ''

End-Procedure

!*************************************


!***********************************************************************
! SQC Files for called procedures
!***********************************************************************
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Routines to update run status
#include 'prcsapi.sqc'   !Update Process API
#Include 'reset.sqc'     !Reset printer procedure
#include 'hrrnctl1.sqc'  !Get run control parameter values
#Include 'number.sqc'    !Routines to format numbers
