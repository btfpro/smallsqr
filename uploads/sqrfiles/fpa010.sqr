!***********************************************************************
!  FPA010:  Promotion Simulation                                       *
!                                                                      *
!  results in FPAEECAREERBIS                                           *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!                                                                      
!          $Date:  2006/07/19:12:26:00                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'         ! Set environment
#include 'setup01.sqc'        ! Printer and page-size initialization


BEGIN-REPORT

Do Init-DateTime
Do Init-Number

        do Get-Current-DateTime()

do Init-Report
do Proc-Main
do Stdapi-Term

END-REPORT

begin-heading 5
#Include 'stdhdg01.sqc'

   do Get_Field_Information($ReportID, 'EMPLID'  , $EMPLID_LBL  , #DW)
   do Get_Field_Information($ReportID, 'NAME'    , $NAME_LBL    , #DW)
   do Get_Field_Information($ReportID, 'EMPLRCD' , $EMPLRCD_LBL , #DW)
   do Get_Field_Information($ReportID, 'EFFDT'   , $EFFDT_LBL   , #DW)
   do Get_Field_Information($ReportID, 'ANOM'    , $ANOM_LBL    , #DW)


 print $EMPLID_LBL        (+1,1)
 print $NAME_LBL          (,13)
 print $EMPLRCD_LBL       (,50)
 print $EFFDT_LBL         (,62)
 print $ANOM_LBL          (,79)

end-heading

!***************************************************************
!
!
!       Init-Report
!
!
!***************************************************************

BEGIN-PROCEDURE  Init-Report

  move 'FPA010' to $ReportID

  do Stdapi-Init

  let $language_cd = $CURR_LANGUAGE_CD

  do Init_Report_Translation($ReportID, $language_cd)
  do Append_Report_Translation($ReportID)

  do Get_Field_Information($ReportID, 'REPORT_TITLE', $ReportTitle, #DW)

  do Security-Param       !SQR security

if $prcs_process_instance = ''

        INPUT $FromDate         'Enter start period'
        INPUT $ThruDate         'Enter end period'
        INPUT $Corps            'Enter corps'
        INPUT $OprId            'Enter operator class'
else
        do FP-Select-Parameters
        do Select-Parameters
end-if

END-PROCEDURE Init-Report


!***************************************************************
!
!
!       Get-Values
!
!
!***************************************************************

BEGIN-PROCEDURE  Get-Values

do Get-From-Thru-Date
do Get-Corps
do Get-OprId

END-PROCEDURE Get-Values



!*********************************************************************
!
!       Proc-Main
!
!               Select Civil Servant where :
!               Career Instance Open
!               Automatic Increment Authorized for the status
!               Body is the same than the selected body
!               Expected Promotion Date is into the selected period
!
!*********************************************************************
BEGIN-PROCEDURE Proc-Main

let $sql-statement = 'FPA010.SQR,Proc-Main,Select,PS_FPAEECAREER'
BEGIN-SELECT On-Error=SQL-Error

A.EMPLID                                            &Emplid
A.EMPL_RCD                                          &Empl_rcd
{DATEOUT-PREFIX}A.EFFDT{DATEOUT-SUFFIX}             &Effdt
A.EFFSEQ                                            &Sequence
A.FP_SETID_RANK                                     &Setid_rank
A.FP_RANK_CD                                        &Rank_cd
A.FP_STEP_CD                                        &Step_cd
{DATEOUT-PREFIX}A.FP_FOR_PROM_DT{DATEOUT-SUFFIX}    &For_Prom_dt
{DATEOUT-PREFIX}A.FP_STEP_END_DT{DATEOUT-SUFFIX}    &Step_End_dt
A.FP_STFBDG_LVL1                                    &STFBDG_LVL1
A.FP_STFBDG_LVL3                                    &STFBDG_LVL3
A.FP_SANCTION                                       &Sanction
A.FP_DOWN_GRA                                       &Down_Gra
A.FP_CHG_IND                                        &Chg_Sal
A.FP_CATG_FP                                        &Catg_FP
A.FP_CATG_LEVEL                                     &Catg_Level
{DATEOUT-PREFIX}A.FP_RK_ENT_DT{DATEOUT-SUFFIX}      &RK_Ent_Dt
{DATEOUT-PREFIX}A.FP_CHG_COR_DT{DATEOUT-SUFFIX}     &Chg_Cor_dt
{DATEOUT-PREFIX}A.FP_NOMINATION_DT{DATEOUT-SUFFIX}  &Nomination_dt
A.FP_PRO_ACCESS                                     &Pro_Access
D.FP_MATRIX_TYP                                     &Matrix_Typ
D.FP_MATRIX_CD                                      &Matrix_Cd
D.FP_SCALE_CD                                       &Scale_cd
F.FP_EQUIV_STEP                                     &Equiv_Step     ! Sub-step
A.FP_SETID_CA                                       &Setid_ca       ! CAE
A.FP_COMM_ACNT_ID                                   &Comm_acnt_id   !
F.FP_NX_STP_CD                                      &Nx_Stp_cd
G.NAME                                              &Name

        Do Fitrage-Statut

        if $flag_ok = 'TRUE'

            #IFDEF debugi
                Display 'start'
            #ENDIF

            Do Var-Init             !Variables Init
            Do Reset-Bis            !Reset Civil Servant CAREER-BIS table
            Do Search-Pos-Stat      !Select Civil Servant position of the Expected Promotion Date

            #IFDEF debugi
                Display 'End'
            #ENDIF

        end-if

from PS_FPAEECAREER A, PS_FPMRANK_TBL D, PS_FPMSALMATRX_TBL E, PS_FPMSALSTEP_TBL F, PS_PERSON_NAME G
    ,PS_FPAEELEGALSTAT L, PS_FPFAST_PERS_VW2 SCRTY      !SQR security

where A.FP_CORPS_CD =  $Corps
and   A.FP_FOR_PROM_DT >= {DATEIN-PREFIX}$FromDate{DATEIN-SUFFIX}
and   A.FP_FOR_PROM_DT <= {DATEIN-PREFIX}$ThruDate{DATEIN-SUFFIX}
and   A.FP_CLD_INST = 'N'
and   A.FP_CNL_ROW  = 'N'
and   A.FP_INST_STAT = 'R'

and   G.EMPLID      = A.EMPLID

and   D.SETID       = A.FP_SETID_RANK
and   D.FP_RANK_CD  = A.FP_RANK_CD
and   D.EFF_STATUS  = 'A'
and   D.EFFDT =
        (select  MAX(EFFDT)
                from    PS_FPMRANK_TBL
                where   FP_RANK_CD = A.FP_RANK_CD
                and     SETID      = A.FP_SETID_RANK
                and EFFDT <= A.EFFDT)

and   E.FP_MATRIX_TYP = D.FP_MATRIX_TYP
and   E.EFF_STATUS = 'A'
and   E.FP_MATRIX_CD  = D.FP_MATRIX_CD
and   E.FP_SCALE_CD   = D.FP_SCALE_CD
and   E.EFFDT =
       (select  MAX(EFFDT)
                from  PS_FPMSALMATRX_TBL
                where FP_MATRIX_TYP = D.FP_MATRIX_TYP
                and   FP_MATRIX_CD  = D.FP_MATRIX_CD
                and   FP_SCALE_CD   = D.FP_SCALE_CD
                and   EFFDT <= A.EFFDT)

and   F.FP_MATRIX_TYP = E.FP_MATRIX_TYP
and   F.FP_MATRIX_CD  = E.FP_MATRIX_CD
and   F.FP_SCALE_CD   = E.FP_SCALE_CD
and   F.FP_STEP_CD    = A.FP_STEP_CD
and   F.EFFDT =
        (select MAX(EFFDT)
                from   PS_FPMSALSTEP_TBL
                where  FP_MATRIX_TYP = E.FP_MATRIX_TYP
                and    FP_MATRIX_CD  = E.FP_MATRIX_CD
                and    FP_SCALE_CD   = E.FP_SCALE_CD
                and    FP_STEP_CD    = A.FP_STEP_CD
                and    EFFDT <= A.EFFDT)
and F.FP_AUTO_PROM = 'Y'                !Promotion authorized for this step

and A.EMPLID = SCRTY.EMPLID             !
and A.EMPL_RCD = SCRTY.EMPL_RCD         !SQR security
[$SecurityClause]                       !

AND (L.EMPLID = A.EMPLID                !SQR security FPAEELEGALSTAT
AND L.EMPL_RCD = A.EMPL_RCD             !SQR security FPAEELEGALSTAT
AND L.FP_HIR_DOS_SIT = '02')            !SQR security FPAEELEGALSTAT

END-SELECT

END-PROCEDURE Proc-Main



!***************************************************************
!
!    select with the status PS_FPAEESTATUS, PS_FPMSTATUSEE_TBL
!
!    Fitrage-Statut
!
!***************************************************************
BEGIN-PROCEDURE Fitrage-Statut

let $flag_ok = 'FALSE'

let $sql-statement = 'FPA010.SQR,Fitrage-Statut,Select,PS_FPAEESTATUS,PS_FPMSTATUSEE_TBL'
BEGIN-SELECT On-Error=SQL-Error
'X'
 let $flag_ok = 'TRUE'

from PS_FPAEESTATUS B, PS_FPMSTATUSEE_TBL C

where B.EMPLID =    &Emplid
and   B.EMPL_RCD =  &Empl_rcd
and   B.EFFDT =
        (select  MAX(EFFDT)
               from    PS_FPAEESTATUS
               where   EMPLID = &Emplid
               and     EMPL_RCD = &Empl_rcd
               and     EFFDT <= {DATEIN-PREFIX}&Effdt{DATEIN-SUFFIX})
and   C.FP_STATUSEE_CD = B.FP_STATUSEE_CD
and   C.EFF_STATUS = 'A'
and   C.EFFDT =
        (select  MAX(EFFDT)
               from    PS_FPMSTATUSEE_TBL
               where   EFFDT <= B.EFFDT
               and FP_STATUSEE_CD = B.FP_STATUSEE_CD )
and   C.FP_AUTO_PROM = 'Y'              !Automatic Increment Authorized for the status

END-SELECT

END-PROCEDURE Fitrage-Statut

!***************************************************************
!
!
!  Reset-Bis
!
!  Reset Civil Servant CAREER-BIS table
!
!***************************************************************

BEGIN-PROCEDURE Reset-Bis

let $sql-statement = 'FPA010.SQR, Reset-Bis,Delete,PS_FPAEECAREERBIS'
BEGIN-SQL On-Error=SQL-Error

delete from PS_FPAEECAREERBIS

where EMPLID =      &Emplid
and   EMPL_RCD =    &Empl_rcd

END-SQL


END-PROCEDURE Reset-Bis

!***************************************************************
!
!  Search-Pos-Stat
!
!  Select Civil Servant position of the Expected Promotion Date
!  If Automatic Increment Authorized, updating Career Bis
!
!***************************************************************

BEGIN-PROCEDURE Search-Pos-Stat

#ifdef debugi
    display 'Search-Pos-Stat begin'
    display &Emplid
    display &Empl_rcd
    display &For_Prom_dt
    display &Sequence
#endif

let $sql-statement = 'FPA010.SQR,Search-Pos-Stat,Select,PS_FPAEELEGALSTAT'
BEGIN-SELECT On-Error=SQL-Error

A.EMPLID

    #ifdef debugi
        Display 'Search-Pos-Stat loop &Emplid:' noline
        Display '&Emplid:' noline
        Display &Emplid
    #endif

        !******************************************
        !Check no error
        !******************************************
        Do Fault-Search($emplid,#empl_rcd,$For_Prom_dt,$rank,$step,
                             $sanction,$ColDecIncr,$name,$effdt,$empl_rcd,
                            $Fault,$nx_sanction,$nx_down_gra)
        let $nx_sanction = 'N'
        let $nx_down_gra = 'N'

    #ifdef debugi
        Display 'Fault: ' noline
        Display $Fault
    #endif

        !******************************************
        !If no error: Promotion Simulation
        !******************************************
        IF $Fault='N'
                do Updt-Career-Bis      !Update of Career Bis
        end-if

from  PS_FPAEELEGALSTAT A, PS_FPMLEGALSTA_TBL B

where A.EMPLID = &Emplid        !Automatic Increment Authorized
and   A.EMPL_RCD = &Empl_rcd    !for the Civil Servant Position
and   A.FP_INST_STAT = 'R'      !Real
and   A.EFFDT =
        (select max(C.EFFDT)
         from  PS_FPAEELEGALSTAT C
         where C.EMPLID = &Emplid
         and   C.EMPL_RCD = &Empl_rcd
         and   C.FP_INST_STAT = 'R'
         and   C.EFFDT <= {DATEIN-PREFIX}$For_Prom_dt{DATEIN-SUFFIX})
and   B.FP_LEGALSTAT_CD  = A.FP_LEGALSTAT_CD
and   B.EFF_STATUS = 'A'
and   B.FP_PROM_STEP = 'Y'
and   B.EFFDT =
        (select max(B1.EFFDT)
         from  PS_FPMLEGALSTA_TBL B1
         where B1.FP_LEGALSTAT_CD = B.FP_LEGALSTAT_CD
         and   B1.EFFDT <= B.EFFDT)

END-SELECT

END-PROCEDURE Search-Pos-Stat



!***************************************************************
!
!
!   Updt-Career-Bis
!
!   Insert a new record in Career-Bis
!
!   The field LAST_UPDATE_DATE is the process date
!   and will be use for the Automatic Increment Process
!
!***************************************************************

BEGIN-PROCEDURE Updt-Career-Bis

#IFDEF DEBUGi

    Display 'Updt-Career-Bis begin'
    Display '&Emplid :'         noline
    Display &Emplid                     !EMPLID
    Display '&Empl_rcd :'       noline
    Display &Empl_rcd                   !EMPL_RCD
    Display '&For_Prom_dt :'    noline
    Display &For_Prom_dt                !EFFDT
    Display '&Actn_Step :'      noline
    Display $Actn_Step                  !ACTION
    Display '$Action_Des :'     noline
    Display $Action_Des                 !FP_ACTION_DES
    Display '$Reasn_Step :'     noline
    Display $Reasn_Step                 !ACTION_REASON
    Display '$Rank_cd :'        noline
    Display &Rank_cd                    !FP_RANK_CD
    Display '$Rank_Des :'       noline
    Display $Rank_Des                   !FP_RANK_DESC
    Display '$Nx_Stp_cd :'      noline
    Display &Nx_Stp_cd                  !FP_STEP_CD
    Display '&Step_End_dt :'    noline
    Display &Step_End_dt                !FP_STEP_END_DT
    Display '$RK_Ent_Dt :'      noline
    Display &RK_Ent_Dt                  !FP_RK_ENT_DT
    Display '$Corps :'          noline
    Display $Corps                      !FP_CORPS_CD
    Display '$Corps_Des :'      noline
    Display $Corps_Des                  !FP_CORPS_DES
    Display '$Chg_Cor_dt :'     noline
    Display &Chg_Cor_dt                 !FP_CHG_COR_DT
    Display '$Catg_FP :'        noline
    Display &Catg_FP                    !FP_CATG_FP
    Display '$Catg_Level :'     noline
    Display &Catg_Level                 !FP_CATG_LEVEL
    Display '$Nomination_dt :'  noline
    Display &Nomination_dt              !FP_NOMINATION_DT
    Display '$Nx_Sanction :'    noline
    Display $Nx_Sanction                !FP_SANCTION
    Display '$Nx_Down_Gra :'    noline
    Display $Nx_Down_Gra                !FP_DOWN_GRA
    Display '&Pro_Access :'     noline
    Display &Pro_Access                 !FP_PRO_ACCESS
    Display '&STFBDG_LVL1 :'    noline
    Display &STFBDG_LVL1                !FP_STFBDG_LVL1
    Display '&STFBDG_LVL3 :'    noline
    Display &STFBDG_LVL3                !FP_STFBDG_LVL3

#ENDIF

!***************************************************************
! Insert a new record in Career-Bis
!***************************************************************

let $sql-statement = 'FPA010.SQR, Updt-Career-Bis ,Insert,PS_FPAEECAREERBIS'
BEGIN-SQL On-Error=SQL-Error

INSERT INTO PS_FPAEECAREERBIS
        (EMPLID,
        EMPL_RCD,
        EFFDT,
        EFFSEQ,
        FP_CONVERSION_ID,
        NAME,
        FP_CONV_STAT,
        FP_ACTION_REF,
        FP_NOTIF_REF,
        FP_CNL_ROW,
        FP_INST_STAT,
        ACTION,
        FP_ACTION_DES,
        ACTION_REASON,
        FP_RANK_CD,
        FP_RANK_DESC,
        FP_STEP_CD,
        FP_STEP_NO,
        FP_GROSS_IND,
        FP_INCS_IND,
        FP_PROM_LCK,
        FP_SENTOSTPDY,
        FP_SENTOSTPMN,
        FP_SENTOSTPYR,
        FP_SENINSTPDY,
        FP_SENINSTPMN,
        FP_SENINSTPYR,
        FP_TRUESEN_DY,
        FP_TRUESEN_MN,
        FP_TRUESEN_YR,
        FP_SENTRANSDY,
        FP_SENTRANSMN,
        FP_SENTRANSYR,
        FP_SENKEPT_DY,
        FP_SENKEPT_MN,
        FP_SENKEPT_YR,
        FP_SENRAN_DY,
        FP_SENRAN_MN,
        FP_SENRAN_YR,
        FP_SENCOR_DY,
        FP_SENCOR_MN,
        FP_SENCOR_YR,
        FP_DURSTEP_DY,              !
        FP_DURSTEP_MN,              !
        FP_DURSTEP_YR,              !
        FP_DURRAN_DY,               !
        FP_DURRAN_MN,               !
        FP_DURRAN_YR,               !
        FP_DURCOR_DY,               !
        FP_DURCOR_MN,               !
        FP_DURCOR_YR,               !
        FP_SENEQSTP_DY,             ! Seniority
        FP_SENEQSTP_MN,             !
        FP_SENEQSTP_YR,             !
        FP_SENENTRK_DY,             !
        FP_SENENTRK_MN,             !
        FP_SENENTRK_YR,             !
        FP_SENENTCRP_DY,            !
        FP_SENENTCRP_MN,            !
        FP_SENENTCRP_YR,            !
        FP_SENENTCTG_DY,            !
        FP_SENENTCTG_MN,            !
        FP_SENENTCTG_YR,            !
        FP_SEN_CALC,
        FP_CLD_INST,
        FP_CLD_STEP,
        FP_FOR_PROM_DT,
        FP_STEP_END_DT,
        FP_RK_ENT_DT,
        FP_CORPS_CD,
        FP_CORPS_DES,
        FP_CHG_COR_DT,
        FP_CATG_FP,
        FP_TRAINEE,
        FP_TRNE_POSN,
        FP_CATG_LEVEL,
        FP_CHG_IND,
        FP_APPL_SAL_DT,
        FP_ROW_CD,
        FP_NOMINATION_DT,
        FP_RK_TRIALPD,
        FP_RK_PD_END_DT,
        FP_SANCTION,
        FP_DOWN_GRA,
        FP_UPDT_PR_DT,
        FP_PROM_OK,
        FP_PROPD_BRK,
        FP_PRO_ACCESS,
        FP_SETID_RANK,
        FP_NX_STP_CD,
        FP_EQUIV_STEP,              ! Sub-step
        FP_STFBDG_LVL1,
        FP_STFBDG_LVL3,
        FP_SETID_CA,                ! CAE
        FP_COMM_ACNT_ID,            !
        FP_RETROSPECT,
        FP_TBL_CD,
        LAST_UPDATE_DATE)
VALUES

        (&Emplid,                                       !EMPLID
        &Empl_rcd,                                      !EMPL_RCD
        {DATEIN-PREFIX}$For_Prom_dt{DATEIN-SUFFIX},     !EFFDT
        0,                                              !EFFSEQ
        ' ',                                            !FP_CONVERSION_ID
        &Name,                                          !NAME
        ' ',                                            !FP_CONV_STAT
        ' ',                                            !FP_ACTION_REF,
        ' ',                                            !FP_NOTIF_REF,
        'N',                                            !FP_CNL_ROW,
        'R',                                            !FP_INST_STAT,
        $Actn_Step,                                     !ACTION,
        $Action_Des,                                    !FP_ACTION_DES
        $Reasn_Step,                                    !ACTION_REASON,
        &Rank_cd,                                       !FP_RANK_CD
        $Rank_Des,                                      !FP_RANK_DESC
        &Nx_Stp_cd,                                     !FP_STEP_CD,
        #Step_No,                                       !FP_STEP_NO
        0,                                              !FP_GROSS_IND
        0,                                              !FP_INCS_IND
        'N',                                            !FP_PROM_LCK,
        0,                                              !FP_SENTOSTPDY,
        0,                                              !FP_SENTOSTPMN,
        0,                                              !FP_SENTOSTPYR,
        0,                                              !FP_SENINSTPDY,
        0,                                              !FP_SENINSTPMN,
        0,                                              !FP_SENINSTPYR,
        0,                                              !FP_TRUESEN_DY,
        0,                                              !FP_TRUESEN_MN,
        0,                                              !FP_TRUESEN_YR,
        0,                                              !FP_SENTRANSDY,
        0,                                              !FP_SENTRANSMN,
        0,                                              !FP_SENTRANSYR,
        0,                                              !FP_SENKEPT_DY,
        0,                                              !FP_SENKEPT_MN,
        0,                                              !FP_SENKEPT_YR,
        0,                                              !FP_SENRAN_DY,
        0,                                              !FP_SENRAN_MN,
        0,                                              !FP_SENRAN_YR,
        0,                                              !FP_SENCOR_DY,
        0,                                              !FP_SENCOR_MN,
        0,                                              !FP_SENCOR_YR,
        0,                                              !FP_DURSTEP_DY,
        0,                                              !FP_DURSTEP_MN,
        0,                                              !FP_DURSTEP_YR,
        0,                                              !FP_DURRAN_DY,
        0,                                              !FP_DURRAN_MN,
        0,                                              !FP_DURRAN_YR,
        0,                                              !FP_DURCOR_DY,
        0,                                              !FP_DURCOR_MN,
        0,                                              !FP_DURCOR_YR,
        0,                                              !FP_SENEQSTP_DY,
        0,                                              !FP_SENEQSTP_MN,
        0,                                              !FP_SENEQSTP_YR,
        0,                                              !FP_SENENTRK_DY,
        0,                                              !FP_SENENTRK_MN,
        0,                                              !FP_SENENTRK_YR,
        0,                                              !FP_SENENTCRP_DY,
        0,                                              !FP_SENENTCRP_MN,
        0,                                              !FP_SENENTCRP_YR,
        0,                                              !FP_SENENTCTG_DY
        0,                                              !FP_SENENTCTG_MN
        0,                                              !FP_SENENTCTG_YR
        'Y',                                            !FP_SEN_CALC,
        'N',                                            !FP_CLD_INST,
        'N',                                            !FP_CLD_STEP,
        {DATEIN-PREFIX}$For_Prom_dt{DATEIN-SUFFIX},     !FP_FOR_PROM_DT
        {DATEIN-PREFIX}$Step_End_dt{DATEIN-SUFFIX},     !FP_STEP_END_DT
        {DATEIN-PREFIX}$Rk_Ent_Dt{DATEIN-SUFFIX},       !FP_RK_ENT_DT
        $Corps,                                         !FP_CORPS_CD,
        $Corps_Des,                                     !FP_CORPS_DES,
        {DATEIN-PREFIX}$Chg_Cor_dt{DATEIN-SUFFIX},      !FP_CHG_COR_DT,
        &Catg_FP,                                       !FP_CATG_FP,
        'N',                                            !FP_TRAINEE,
        'B',                                            !FP_TRNE_POSN,
        &Catg_Level,                                    !FP_CATG_LEVEL,
        'N',                                            !FP_CHG_IND,
        {DATEIN-PREFIX}$For_Prom_dt{DATEIN-SUFFIX},     !FP_APPL_SAL_DT,
        'A',                                            !FP_ROW_CD,
        {DATEIN-PREFIX}$Nomination_dt{DATEIN-SUFFIX},   !FP_NOMINATION_DT,
        'N',                                            !FP_RK_TRIALPD,
        {DATEIN-PREFIX}$For_Prom_dt{DATEIN-SUFFIX},     !FP_RK_PD_END_DT
        $Nx_Sanction,                                   !FP_SANCTION,
        $Nx_Down_Gra,                                   !FP_DOWN_GRA,
        'N',                                            !FP_UPDT_PR_DT,
        'N',                                            !FP_PROM_OK,
        'N',                                            !FP_PROPD_BRK,
        &Pro_Access,                                    !FP_PRO_ACCESS,
        &Setid_rank,                                    !FP_SETID_RANK
        ' ',                                            !FP_NX_STP_CD
        &Equiv_Step,                                    !FP_EQUIV_STEP      Sub-Step
        &STFBDG_LVL1,                                   !FP_STFBDG_LVL1
        &STFBDG_LVL3,                                   !FP_STFBDG_LVL3
        &Setid_ca,                                      !FP_SETID_CA        CAE
        &Comm_acnt_id,                                  !FP_COMM_ACNT_ID
        'N',                                            !FP_RETROSPECT,
        'C',                                            !FP_TBL_CD,
        {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX})       !LAST_UPDATE_DATE

END-SQL

!***************************************************************
!Update Career Bis:
!       Effdt =  Expected Promotion Date
!       Sequence=0
!       Ref Request and Employee Orders Ref: Empty
!       Action and action reason: Default value
!       Step : next step
!       Seniority : 0
!       Seniority Calculation: ‘Y’
!       Expected Advancement Date : Empty
!       PPd Expected End Date : Empty
!       Instance Terminated : open
!       Compensation : Y
!       Row Origin Code: Automatic : ‘A’
!       Sanction and demotion code : calculation
!       Modify Advancement Date Code : 'N'
!       Retroactivity Code : 'N'
!       Advancement Flag : 'N'
!       Promo Prob Period Suspension : 'N'
!       Date of last update: current date
!***************************************************************

#IFDEF debugi
    Display 'Updt-Career-Bis End'
#ENDIF

END-PROCEDURE Updt-Career-Bis



!***************************************************************
!
! Var-Init   Variables Init
!
!***************************************************************

BEGIN-PROCEDURE Var-Init

!***************************************************************
!Select default reason, action code and description
!for the Automatic Increment
!***************************************************************

let $rank = rtrim(&Rank_cd,' ')
let $step = rtrim(&Step_cd,' ')
let $sanction = rtrim(&Sanction,' ')
let #empl_rcd = &Empl_rcd
Do Convert-To-DTU-Date (&Effdt,$Effdt)
move ' ' to $ColDecIncr

let $Effdt_nat = &Effdt
do Get-Action-Step ($Effdt_nat,$Actn_Step,$Reasn_Step,$Action_Des )
do Get-Corps-Des ($Corps,$Effdt_nat,$Corps_Des )

let $rank_cd = rtrim(&Rank_cd,' ')
do Get-Rank-Des ($rank_cd,$Effdt_nat,$Rank_Des )
let $matrix_typ = rtrim(&Matrix_Typ,' ')
let $matrix_cd = rtrim(&Matrix_Cd,' ')
let $scale_cd = rtrim(&Scale_cd,' ')
let $Nx_Stp_cd = rtrim(&Nx_Stp_cd,' ')
do Get-Step-No ($Matrix_Typ,$Matrix_Cd,$Scale_cd,$Nx_Stp_cd,$Effdt_nat,#Step_No,$equiv_step)

Let $Fault = 'N'
Let $Emplid = rtrim(&Emplid,' ')
let $Name=substr(&Name,1,35)
Let $Empl_rcd = To_Char(&Empl_rcd)

let $For_Prom_dt  = &For_Prom_dt
let $Rk_Ent_Dt = &Rk_Ent_Dt
let $Chg_Cor_dt = &Chg_Cor_dt
let $Nomination_dt = &Nomination_dt

END-PROCEDURE Var-Init



#Include 'fptestdt.sqc'     ! Routines for date and time formatting (FPS)
#Include 'fpckflt.sqc'        ! Routines for promotion errors (FPS)
#Include 'fpparam.sqc'        ! Routines for  Parameters Tables (FPS)
#include 'curdttim.sqc'       ! Get-Current-DateTime procedure
#include 'stdapi.sqc'         ! Routines to update run status
#Include 'hrrnctl1.sqc'       ! Get run control parameter values
#Include 'fprnctl1.sqc'       ! Get run control parameter values
#Include 'fpgetval.sqc'       ! Get values mask routines (FPS)
#Include 'hrgetval.sqc'       ! Get values mask routines
#Include 'number.sqc'         ! Routines to format numbers
#Include 'hrsecty.sqc'        ! Get SQR Security parameters
#include 'sqrtrans.sqc'       ! Translate SQR strings to given language

!#include 'rellang.sqc'        ! Call in fpparam.sqc
