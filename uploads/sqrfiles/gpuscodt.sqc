!**********************************************************************!
!  gpuscodt: Get Reporting Company & Company data                      !
!**********************************************************************!
!                                                                      !
!               Confidentiality Information:                           !
!                                                                      !
!                                                                      !
! This module contains confidential and proprietary information        !
! of Oracle; it is not to be copied, reproduced, or transmitted        !
! in any form, by any means, in whole or in part, nor is it to         !
! be used for any purpose other than that for which it is              !
! expressly provided under the applicable license agreement.           !
!                                                                      !
! Copyright (C) 2006 Oracle. All Rights Reserved.                      !
!                                                                      !
!**********************************************************************!
!                                                                      !
!          $Date:  2006/07/21:01:42:56                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      !
!**********************************************************************!

!*******************************!
begin-procedure Get-Company
!*******************************!
begin-SELECT
C1.GPUS_YE_RPT_CO
  move &C1.GPUS_YE_RPT_CO to $Company
  do Get-Company-Data
FROM PS_GPUS_YE_CO_TBL C1
WHERE C1.GPUS_TAX_EIN = (SELECT GPUS_TAX_EIN
                         FROM PS_GPUS_YE_CO_TBL C2
                         WHERE C2.GPUS_YE_RPT_CO = (SELECT GPUS_YE_RPT_CO
                                                    FROM PS_GPUS_YE_CO_EIN E1
                                                    WHERE E1.GPUS_TAX_EIN = $Tax_EIN
                                                      AND E1.EFFDT = (SELECT MAX (EE1.EFFDT)
                                                                      FROM PS_GPUS_YE_CO_EIN EE1
                                                                      WHERE EE1.GPUS_TAX_EIN = E1.GPUS_TAX_EIN
                                                                       AND EE1.GPUS_YE_RPT_CO = E1.GPUS_YE_RPT_CO
                                                                       AND EE1.EFFDT <= $AsOfDate))
                           AND C2.EFFDT = (SELECT MAX (CC2.EFFDT)
                                           FROM PS_GPUS_YE_CO_TBL CC2
                                           WHERE CC2.GPUS_YE_RPT_CO =C2.GPUS_YE_RPT_CO
                                             AND CC2.EFFDT <= $AsOfDate))
  AND C1.EFFDT = (SELECT MAX (CC1. EFFDT)
                 FROM PS_GPUS_YE_CO_TBL CC1
                 WHERE CC1.GPUS_YE_RPT_CO =C1.GPUS_YE_RPT_CO
                  AND CC1.EFFDT <= $AsOfDate)
end-SELECT

end-procedure

!*******************************!
begin-procedure Get-Company-Data
!*******************************!

  if rtrim($Company,' ') = ''
    move '' to $CompanyName
    goto GetCompanyExit
  end-if
  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
CT.GPUS_YE_RPT_CO,CT.GPUS_TAX_EIN
CT.DESCR,
CT.ADDRESS1,CT.ADDRESS2,CT.ADDRESS3,CT.ADDRESS4,CT.CITY,CT.STATE,CT.POSTAL,CT.COUNTRY
CT.EFFDT

  LET $CO_FOUND = 'Y'


FROM  PS_GPUS_YE_CO_TBL CT
WHERE CT.GPUS_YE_RPT_CO = $Company
  AND CT.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_GPUS_YE_CO_TBL
       WHERE  GPUS_YE_RPT_CO = $Company
         AND  EFFDT  <= $AsOfDate)
end-SELECT

     let $CompanyName      = &CT.Descr
     let $Compnyadd1       = rtrim(&CT.Address1,' ')
     let $Compnyadd2       = rtrim(&CT.Address2,' ')
     let $Compnyadd3       = rtrim(&CT.Address3,' ')
     let $Compnyadd4       = rtrim(&CT.Address4,' ')
     let $Compnycity       = rtrim(&CT.City,' ')
     let $Basedate         = &CT.Effdt

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!

    if $curr_language_cd <> $Psoptions_Language_Cd
       do Get-Rel-Lang-Comp
    end-if

GetCompanyExit:

end-procedure


!********************************!
begin-procedure Get-Rel-Lang-Comp
!********************************!

begin-SELECT
CTL.DESCR,CTL.DESCRSHORT

   if rtrim(&CTL.Descr,' ') <> ''
        let $CompanyName      = rtrim(&CTL.Descr,' ')
   end-if

   if rtrim(&CTL.DescrShort,' ') <> ''
        let $CompanyNameAbbrv = rtrim(&CTL.DescrShort,' ')
   end-if


FROM  PS_COMPNY_TBL_LANG CTL
WHERE CTL.COMPANY     = $Company
  AND CTL.LANGUAGE_CD = $curr_language_cd
  AND CTL.EFFDT       =
     (SELECT MAX(EFFDT)
      FROM   PS_COMPNY_TBL_LANG
      WHERE  COMPANY     = $Company
        AND  LANGUAGE_CD = CTL.LANGUAGE_CD
        AND  EFFDT      <= $Basedate)
end-SELECT

end-procedure

#Include 'curdttim.sqc'  !Get-Current-DateTime procedure