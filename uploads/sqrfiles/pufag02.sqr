!----------------------------------------------------------------------!
! Program Name: PUFAG02.SQR - Identify Duplicate Guarantor Data        !
!----------------------------------------------------------------------!
!                                                                      !
!                      Confidentiality Information:                    !
!                                                                      !
! This module is the confidential and proprietary information of       !
! PeopleSoft, Inc.; it is not to be copied, reproduced, or transmitted !
! in any form, by any means, in whole or in part, nor is it to be used !
! for any purpose other than that for which it is expressly provided   !
! without the written permission of PeopleSoft.                        !
!                                                                      !
! Copyright (c) 1988-2007 PeopleSoft, Inc. All Rights Reserved         !
!                                                                      !
!**********************************************************************!
!                                                                      !
!      $Date::                  $                                      !
!  $Revision::                  $                                      !
!  $Workfile::                  $                                      !
!                                                                      !
!----------------------------------------------------------------------!
! Description: Identify Duplicate Guarantor Information.               !
!              Tables: GUARANTOR_TBL and LN_DESTPROF_TBL               !
!----------------------------------------------------------------------!

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and page-size initialization

#define TRUE    1
#define FALSE   0


!***********************************************************************
! Setup                                          
!***********************************************************************
begin-setup

    declare-variable
       integer #GUAR_UNQ_ID 
    end-declare

end-setup


!***********************************************************************
! Report Begin                                          
!***********************************************************************
begin-report

  do Init-DateTime
  do Init-Report
  do Stdapi-Init
  do Process-Main
  do Exit-Routine

end-report

!***********************************************************************
! Heading 1                                          
!***********************************************************************
begin-heading 1

#Include 'stdhdg01.sqc'

  print '-'            (+2, 5,57) fill

  print 'Unique ID'    (+1, 5, 9)
  print 'Guarantor ID' (  ,+3,12)
  print 'Description'  (  ,+3,30)

  print '-'            (+1, 5,57) fill

  print ' '            (+1, 1,10) 

end-heading

!***********************************************************************
! Initialize Report                                  
!***********************************************************************
begin-procedure Init-Report

  do Get-Current-DateTime

  move 'PUFAG02' to $ReportID
  move 'Identify Duplicate Guarantor Information' to $ReportTitle

  display $ReportID noline
  display ' - ' noline
  display $ReportTitle
  display ' '

  string 'Program Began: ' $AsOfToDay $AsOfNow by ' ' into $DateTimeMsg
  display $DateTimeMsg
  display ' '

end-procedure !Init-Report

!************************************************************************
! Exit Routine
!************************************************************************
begin-procedure Exit-Routine

  If #current-line > 65
     new-page
     print ' '                          (+6,1)
  end-if

  If #current-line < 6
     print ' '                          (+6,1,1)
  end-if

  do StdApi-Term
  do Get-Current-DateTime
  string 'Program Ended: ' $AsOfToDay $AsOfNow by ' ' into $DateTimeMsg
  display $DateTimeMsg
  display ' '

  print 'End of Report  '                (+4,1)
  print $DateTimeMsg                     (+2,1)

end-procedure !Exit-Routine

!***********************************************************************
! Main Process                                       
!***********************************************************************
begin-procedure Process-Main

  let #ReportStart = {TRUE}

BEGIN-SELECT DISTINCT
A.GUAR_OE_CD           &A.GUAR_OE_CD

  Move &A.GUAR_OE_CD   to $GUAR_OE_CD

  do Count-OE-Code
  
  if #DUPICATE_ROW_COUNT > 1 
     let #NewLenderId = {TRUE}
     do Select-Guarantor-Data
  end-if
  
FROM PS_GUARANTOR_TBL A,
     PS_LN_DESTPROF_TBL B 
WHERE A.GUAR_UNQ_ID = B.GUAR_UNQ_ID 
  AND B.LOAN_PROGRAM <> 'D' 
  AND B.GUAR_UNQ_ID  <> 0
ORDER BY A.GUAR_OE_CD
END-SELECT                                                              

end-procedure !Process-Main


!***********************************************************************
! Count number of guarantor with same OE
!***********************************************************************
begin-procedure Count-OE-Code

BEGIN-SELECT 
COUNT(*)                   &DUPICATE_ROW_COUNT

  Move &DUPICATE_ROW_COUNT to #DUPICATE_ROW_COUNT

FROM PS_GUARANTOR_TBL
WHERE GUAR_OE_CD = $GUAR_OE_CD
END-SELECT 

end-procedure !Count-OE-Code


!***********************************************************************
! Select from PS_GUARANTOR_TBL                           
!***********************************************************************
begin-procedure Select-Guarantor-Data

BEGIN-SELECT DISTINCT
GUAR_UNQ_ID           &GUAR_UNQ_ID
DESCR                 &DESCR

  Move &GUAR_UNQ_ID   to #GUAR_UNQ_ID
  Move &DESCR         to $DESCR

  do Print-To-Report
  
FROM PS_GUARANTOR_TBL
WHERE GUAR_OE_CD = $GUAR_OE_CD
END-SELECT 

end-procedure !Select-Guarantor-Data


!***********************************************************************
! Print Report                                       
!***********************************************************************
begin-procedure Print-To-Report

  If #ReportStart = {TRUE}
     print ''             (+6, 1,1)
     let #ReportStart = {FALSE}
  end-if

  If #NewLenderId = {TRUE}
     print ''             (+1, 1,1)
  End-If

  let #NewLenderId = {FALSE}

  If #current-line > 75
     new-page
     print ''             (+7, 1,1)
  end-if

  let $GUAR_UNQ_ID = #GUAR_UNQ_ID
   
  print $GUAR_UNQ_ID      (+1, 5, 9)
  print $GUAR_OE_CD       ( , +3, 9)
  print $DESCR            ( , +6,30)
  
end-procedure ! Print-To-Report

!----------------------------------------------------!
! Includes                                           !
!----------------------------------------------------!
#include 'stdapi.sqc'    !Routines to update run status
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
