!**************************************
!  GetDedBl:  Get Deduction Balances  *
!**************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:01:52                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Get-Deduction-Balances

  evaluate $Country
    when = 'USA'
      do Get-USA-Deduction-Balances
    when = 'CAN'
      do Get-CAN-Deduction-Balances
  end-evaluate

end-procedure


begin-procedure Get-USA-Deduction-Balances
  move $AsOfYYYY  to #AsOfYYYY
  move $AsOfMonth to #AsOfMonth
  move $AsOfQtr   to #AsOfQtr
begin-SELECT
DB.DED_MTD,DB.DED_QTD,DB.DED_YTD,DB.BALANCE_QTR,DB.BALANCE_PERIOD
FROM  PS_DEDUCTION_BAL DB
WHERE EMPLID        = $EmplID
  AND COMPANY       = $Company
  AND PLAN_TYPE     = $PlanType
  AND DEDCD         = $DedCd
  AND DED_CLASS     = $DedClass
  AND BALANCE_YEAR = #AsOfYYYY
  AND BALANCE_ID   = $Calendar_Year_Id
  AND BENEFIT_RCD_NBR = #Benefit-Rcd-Nbr
  AND BALANCE_PERIOD =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_DEDUCTION_BAL
       WHERE  EMPLID        = $EmplID
         AND  COMPANY       = $Company
         AND  PLAN_TYPE     = $PlanType
         AND  DEDCD         = $DedCd
         AND  DED_CLASS     = $DedClass
         AND  BALANCE_YEAR  = #AsOfYYYY
         AND  BALANCE_ID    = $Calendar_Year_Id
         AND  BENEFIT_RCD_NBR = #Benefit-Rcd-Nbr
         AND  BALANCE_PERIOD  <= #AsOfMonth)
end-SELECT
  move &DB.Ded_MTD to #DedMTD
  move &DB.Ded_QTD to #DedQTD
  move &DB.Ded_YTD to #DedYTD
!Use of this procedure presumes that "Get-Quarter-Month-Codes" and
! "Get-Calendar-Year-Id" are also used
  if #AsOfMonth <> &DB.Balance_Period
    move 0 to #DedMTD
  end-if
  if #AsOfQtr <> &DB.Balance_Qtr
    move 0 to #DedQTD
  end-if
end-procedure


begin-procedure Get-CAN-Deduction-Balances
  move $AsOfYYYY  to #AsOfYYYY
  move $AsOfMonth to #AsOfMonth
  move $AsOfQtr   to #AsOfQtr
begin-SELECT
CDB.DED_MTD,CDB.DED_QTD,CDB.DED_YTD,CDB.BALANCE_QTR,CDB.BALANCE_PERIOD

  move &CDB.BALANCE_PERIOD   to #MONTHCD
  move &CDB.BALANCE_QTR      to #QTRCD
  move &CDB.DED_MTD  to #MTD
  move &CDB.DED_QTD  to #QTD
  move &CDB.DED_YTD  to #YTD

  do Accumulate-Deductions

FROM  PS_CAN_DED_BALANCE CDB
WHERE CDB.EMPLID           = $EmplID
  AND CDB.COMPANY          = $Company
  AND CDB.PLAN_TYPE        = $PlanType
  AND CDB.DEDCD            = $DedCd
  AND CDB.DED_CLASS        = $DedClass
  AND CDB.DED_SLSTX_CLASS  = $DedSlsTxClass
  AND CDB.BALANCE_ID       = $Calendar_Year_Id
  AND CDB.BALANCE_YEAR     = #AsOfYYYY
  AND CDB.BENEFIT_RCD_NBR  = #Benefit-Rcd-Nbr
  AND CDB.BALANCE_PERIOD =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_CAN_DED_BALANCE
       WHERE  EMPLID          = CDB.EMPLID
         AND  COMPANY         = CDB.COMPANY
         AND  PLAN_TYPE       = CDB.PLAN_TYPE
         AND  DEDCD           = CDB.DEDCD
         AND  DED_CLASS       = CDB.DED_CLASS
         AND  DED_SLSTX_CLASS = CDB.DED_SLSTX_CLASS
         AND  BALANCE_ID      = CDB.BALANCE_ID
         AND  BALANCE_YEAR    = CDB.BALANCE_YEAR
         AND  BENEFIT_RCD_NBR = CDB.BENEFIT_RCD_NBR
         AND  WAGE_LOSS_PLAN  = CDB.WAGE_LOSS_PLAN
         AND  PROVINCE        = CDB.PROVINCE
         AND  BALANCE_PERIOD  <= #AsOfMonth)
end-SELECT

end-procedure


begin-procedure Accumulate-Deductions
  if #MonthCd = #AsOfMonth
    add #MTD to #DedMTD
  end-if

  if #QtrCd = #AsOfQtr
    add #QTD to #DedQTD
  end-if

  add #YTD   to #DedYTD

end-procedure





