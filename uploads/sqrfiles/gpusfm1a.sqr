!***********************************************************************
!  gpusfm1a:  Federal W-2 Audit Report                                 *
!***********************************************************************
!                                                                     !
!               Confidentiality Information:                          !
!                                                                     !
!                                                                     !
! This module contains confidential and proprietary information       !
! of Oracle; it is not to be copied, reproduced, or transmitted       !
! in any form, by any means, in whole or in part, nor is it to        !
! be used for any purpose other than that for which it is             !
! expressly provided under the applicable license agreement.          !
!                                                                     !
! Copyright (C) 2006 Oracle. All Rights Reserved.                     !
!                                                                     !
!*********************************************************************!
!                                                                     !
!          $Date:  2006/07/21:01:44:45                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                     !
!*********************************************************************!

#include 'setenv.sqc' !Set environment
#include 'setup02.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Select-Reports
  do Stdapi-Term
end-report

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Initialization
end-procedure


begin-heading 9
  #Include 'stdhdg01.sqc'
  print 'SSN'                            (+2,1)
  print 'FWT Gross'                       (0,17)
  print 'FWT Tax'                         (0,35)
  print 'FICA Gross'                      (0,48)
  print 'FICA Tax'                        (0,66)
  print 'Medi Wages'                      (0,80)
  print 'Medi Tax'                        (0,98)
  print 'FICA Tips'                       (0,113)
  print 'EIC'                             (0,135)
  print 'Dpnd Care'                       (0,145)
  print '401k'                            (0,166)

  print '403b'                           (+1,26)
  print '408k6'                           (0,41)
  print '457b'                            (0,58)
  print '501c18d'                         (0,71)
  print 'Military'                        (0,86)
  print 'NQ 457 Y'                        (0,102)
  print 'NQ 457 N'                        (0,123)
  print 'Combat Pay'                      (0,134)
  print 'GTL ER Cost'                     (0,147)
  print 'NS Stock Op'                     (0,163)

  
  print 'Def NQ Comp'                    (+1,19)
  print 'Thrd Pty Tax'                    (0,35)
 
  print 'Alloc TIPS '                    (+1,16)
  print 'Unc Tax TIPS'                    (0,30)
  print 'Med Sav Act'                     (0,47)
  print '408p Retire'                     (0,62)
  print 'Adopt Exp '                      (0,82)
  print 'Unc SS GTL'                      (0,96)
  print 'Unc Med GTL'                     (0,111)
  print '409A Inc NQ'                     (0,127)
  print 'Civil Status'                    (0,145)
  print 'Spouse SSN'                      (0,159)

  print 'Fed WG - PR'                    (+1,21)
  print 'Comm - PR'                       (0,35)
  print 'Allow - PR'                      (0,54)
  print 'TIPS - PR'                       (0,67)
  print 'Tot Comp-PR'                     (0,83)
  print 'Tax - PR'                        (0,99)
  print 'Retire - PR'                     (0,115)
  print 'Fed WG - TY'                     (0,131)
  print 'Fed TX - TY'                     (0,147)
  print '             '                   (0,158)


  move '  ' to $FullLine
  print $FullLine                        (+1,176)

end-heading

begin-procedure Initialization
  display ''
  display 'Federal W-2 Audit Report '
  display ' '
  move 'Federal W-2 Audit Report' to $ReportTitle
  move 'GPUSFM1A' to $ReportID
  do Get-Current-DateTime

  If $Prcs_Process_Instance = ''
    Display 'Report cannot be run outside of PeopleSoft, please use process scheduler.'
    Stop
  End-if

  do Convert-Parameters

  let $AsOfDate = $CAL_YEAR || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP}, '', 'native')

  move '' to $ErName
end-procedure

begin-procedure Select-Reports

Begin-Select

RC960.EMPLR_PIN
RC960.GPUS_XMITTER_ID
    do Get-Transmitter
RC960.CONTACT_NAME
RC960.TELEPHONE_NBR
RC960.EXTENSION
RC960.EMAIL_ADDR_MMREF
RC960.FAX
RC960.GPUS_NOTIFY_METHOD

FROM PS_GPUS_YE_MMREFPA RC960
End-Select

  if rtrim(&RC960.CONTACT_NAME, ' ') = ''
    Show 'MMREF Parameters must be set up on SETUP Menu.'
    Show 'Re-execute after establishing MMREF Parameters.'
    Stop
  End-if

  move &RC960.EMAIL_ADDR_MMREF to $RC960.EMAIL_ADDR
  uppercase $RC960.EMAIL_ADDR


  let $FileIn = 'W2REPORT'
      do Open-File

  if #filestat = 0
      do Read-Tape-File
  end-if

end-procedure

begin-procedure Open-File

  #ifdef NT
  #define FILEPATH files\
  #end-if

  #ifdef UNIX
  #define FILEPATH files/
  #end-if

  Let $prcs_path = $prcsoutputdir
  Let $Flag ='Y'
  Let #st = 1
  Find  'log_output'  In  $prcs_path  #st   #Loc
  Let #Len = Length($prcs_path )
  Let $Str1= Substr($prcs_path, 0, #Loc)
  Let $Str2=  $Str1 || '{FILEPATH}'
  let $FileID = $Str2 || $FileIn
  open $FileID as 1 for-reading record=512
    status=#filestat
  if #filestat != 0
    display $FileID noline
    display ' not found '
    display ' '
  end-if

end-procedure

begin-procedure Process-RA-Record
  do Print-RA-Record
end-procedure

begin-procedure Process-RE-Record
  if $ErName <> ''
    if $TotalsOnly = 'N'
      new-page
      move ' ' to $Col1Hdr1
      move ' ' to $Col2Hdr1
      move ' ' to $Col3Hdr1
      move ' ' to $Col4Hdr1
      move ' ' to $Col5Hdr1
      move ' ' to $Col6Hdr1
      move ' ' to $Col1Hdr2
      move ' ' to $Col2Hdr2
      move ' ' to $Col3Hdr2
      move ' ' to $Col4Hdr2
      move ' ' to $Col5Hdr2
      move ' ' to $Col6Hdr2
    end-if
  end-if

  next-listing  NEED = 24
  do Print-RE-Record

  if $TotalsOnly = 'N'
    move $ErName              to $Employer
    new-page
    move 'Last'             to $Col1Hdr1
    move 'Name'             to $Col1Hdr2
    move 'First'            to $Col2Hdr1
    move 'Name'             to $Col2Hdr2
    move 'Middle'           to $Col3Hdr1
    move 'Name'             to $Col3Hdr2
    move 'SSN'              to $Col4Hdr2
    move '              '   to $Col5Hdr1
    move '   State Wages'   to $Col5Hdr2
    move '              '   to $Col6Hdr1
    move '   State Wages'   to $Col6Hdr2
  end-if
end-procedure

begin-procedure Process-RS-Record
  do Print-RS-Record
end-procedure


begin-procedure Process-RO-Record
  do Print-RO-Record
end-procedure

begin-procedure Process-Employer-Record
  display 'Employer Record Found'
  extract $Employer from $TaxRecord 40 57
  extract $TaxJurisdiction from $TaxRecord 220 1
end-procedure

begin-procedure Process-RF-Record
   do Print-RF-Record
end-procedure


begin-procedure Process-RT-Record
   do Print-RT-Record
end-procedure

begin-procedure Process-RU-Record
  do Print-RU-Record
end-procedure

begin-procedure Print-Employer-Totals
   !Dummy
end-procedure

begin-procedure Print-File-Totals
  print 'File Totals - Wages: ' (+2,1)
  print  #F_StateWages (0,22) edit 999,999,999.99   !State Taxable Wages
  print 'Taxes: ' (+1,15)
  print  #F_StateTax   (0,22) edit 999,999,999.99   !State Taxes
  move 0 to #F_StateWages
  move 0 to #F_StateTax
  move '' to $ErName
end-procedure

begin-procedure Get-Transmitter
begin-SELECT
X.DESCR

FROM  PS_GPUS_YE_CO_TBL X
WHERE X.EFFDT = (SELECT MAX (Y.EFFDT) FROM PS_GPUS_YE_CO_TBL Y WHERE Y.EFFDT <= $AsOfDate AND Y.GPUS_YE_RPT_CO = &RC960.GPUS_XMITTER_ID)
  AND X.GPUS_YE_RPT_CO = &RC960.GPUS_XMITTER_ID

end-SELECT
  move &X.Descr to $TrName
end-procedure


begin-procedure Convert-Parameters
begin-SELECT
RC.CALENDAR_YEAR
RC.GPUS_TOTALS_ONLY

FROM  PS_GPUS_RC_MMREF RC
WHERE RC.OPRID = $Prcs_OprID
  AND RC.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-SELECT

  move &RC.CALENDAR_YEAR           to $Cal_Year
  move &RC.GPUS_TOTALS_ONLY        to $TotalsOnly

end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'gpusrdfl.sqc'  !Read MMREF file
#Include 'stdapi.sqc'    !Update Process API
