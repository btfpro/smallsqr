!********************************************************
!  hrhl01.sqc - New HR Standard Header - Landscape      !
!********************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!***********************************************************************
!                                                                      *
!                $Date:  2006/01/16:19:55:08                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      *
!***********************************************************************
! This file is necessary for release 8 revised reports including       *
! PER027.SQR, PER027B.SQR, PER027C.SQR, and PER025.SQR                 *
!***********************************************************************
!
! This SQC is designed to print a landscape report with the report
! title the page numbers, and the word 'CONFIDENTIAL' in large letters.
!
! You must not have a SETUP routine, nor a HEADER or FOOTER if you
! choose to use this SQC.
!
! Suggested naming routine for new setup/header/footer routines:
!
! First three letters 'hrh' designating that this is owned by 'HR' and
!     is a 'H'eader file.
!
!
! Fourth letter should designated either 'l'andscape or 'p'ortrait
!
! Fifth & Sixth should be numbers, starting with 01 first the first
!     and incrementing each time a new one is added.
!----------------------------------------------------------------------
!                           USAGE
!----------------------------------------------------------------------
! Unless otherwise noted, a blank, null, or zero value will simply
! result in nothing being displayed for the parameter.
!
! NOTE: Do not include 'adformat.sqc' in your SQR if you use this
!       header file - it is already included, and including it again
!       will result in a compile error.
!
! The values can be changed at any time. The most recent value set when
! the page ends will be used.
!
! $Confidential - Place a value in this variable if you want the
!                 word confidential to print in big letters along with
!                 notice at the bottom warning not to distribute
! $ReportTitle  - A string value with the name of the report
! #PageCount    - A numeric variable with the current page number. This
!                 is done this way should you wish to override SQR's
!                 built in page numbering. If this value is '0', the
!                 SQC will use the default SQR page number.
!
!                 The following information is needed for the entity that
!                 this report is being run for (company,dept, etc.) if you
!                 wish the entity's name and/or address to print out in
!                 the header:
!
!                 $NAME                = From Global address subrecord
!                 $ADDRESS1            = From Global address subrecord
!                 $ADDRESS2            = From Global address subrecord
!                 $ADDRESS3            = From Global address subrecord
!                 $ADDRESS4            = From Global address subrecord
!                 $NUM1                = From Global address subrecord
!                 $NUM2                = From Global address subrecord
!                 $HOUSE_TYPE          = From Global address subrecord
!                 $POSTAL              = From Global address subrecord
!                 $CITY                = From Global address subrecord
!                 $STATE               = From Global address subrecord
!                 $COUNTY              = From Global address subrecord
!                 $COUNTRY             = From Global address subrecord
!                 $GEO_CODE            = From Global address subrecord
!                 $IN_CITY_LIMIT       = From Global address subrecord
!----------------------------------------------------------------------



!----------------------------------------------------------------------
!---------Setup--------------------------------------------------------
!----------------------------------------------------------------------
! Here, we describe a printer for use in printing our form. NOTE: this
! SQR does not support line printers. You must print this report
! via an SPF file to a laser or inkjet printer.
!-----------------------------------------------------------------------
Begin-Setup
#include 'setupdb.sqc'
DECLARE-PRINTER thePrinter
    ! Printer doesn't matter, because we're going to turn the -ziv flag on
    ! in the process definition. This should work with any printer since
    ! SQR Viewer will print through any Windows print driver...
    type=HP                             ! Hewlett-Packard
    font=4                              ! Helvetica Font
    point-size=10                       ! Default font size of 10 pts
END-DECLARE

DECLARE-LAYOUT landscape                ! A layout called 'landscape'
    paper-size=(11,8.5)                 ! Letter size paper
    orientation=landscape               ! Landscape orientation
    left-margin=.25                     ! Left margin is .25 inches
    top-margin=.2                       ! Top Margin is .2 inches
    right-margin=.25                    ! Right margin of .25 inches
    bottom-margin=.25                   ! Bottom margin of .25 inches
    line-height=12
END-DECLARE

DECLARE-REPORT landscapeReport
    layout=landscape
    printer-type=HP                     ! Hewlett-Packard
END-DECLARE

#define PAGE_ORIENTATION LANDSCAPE

! These are left in for legacy support only
! -----------------------------------------
#define PAGE_MAX_COLS    105
#define PAGE_MAX_LINES   60
End-Setup

!----------------------------------------------------------------------
!---------Header-------------------------------------------------------
!----------------------------------------------------------------------
! Print required information at top of each page. We are NOT using
! the standard PeopleSoft headers for this report
!
! We are using a proportional font here, so some of the alignments
! may appear strange.
!----------------------------------------------------------------------
Begin-Heading 8

#IFDEF DEBUGX
  DISPLAY 'FLOW: Header'
#ENDIF

DISPLAY '   $NAME: ' NOLINE
DISPLAY $NAME

! Print the title of the report in big
! letters
!-----------------------------------------
ALTER-PRINTER
    font=4
    point-size=18



PRINT $ReportTitle                                              (2,#TitlePosition) BOLD



! Format the date (strip off the time) and
! print it. Note: This header will print
! the Run Date equal to the System Date (Today's
! Date). This header will also print the As Of
! Date or the From - Thru Date as defined in the
! Run Control Parameter.
!-----------------------------------------
ALTER-PRINTER
    point-size=8

DO Format-DateTime($AsOfToday, $SystemDtOutput, {DEFDATE},'','')
LET $RunDateOutput = 'Run Date: ' || $SystemDtOutput
Print $RunDateOutput                                            (5,#RightPosition)


EVALUATE $date-indicator   !Date parameters as defined in the Run Control Page
    WHEN = '1'     !As of Date
       DO Format-DateTime($AsOfDate, $AsOfOutput, {DEFDATE},'','')
       IF NOT ISBLANK($AsOfOutput)
       LET $HeaderOutput = 'As of ' || $AsOfOutput
       END-IF
       BREAK
    WHEN = '2'
       DO Format-DateTime($FromDate, $FromOutput, {DEFDATE},'','')
       DO Format-DateTime($ThruDate, $ThruOutput, {DEFDATE},'','')
       LET $HeaderOutput = 'From ' || $FromOutput || ' Thru ' || $ThruOutput
       BREAK

    WHEN-OTHER
       DO Format-DateTime($AsOfToday, $SystemDtOutput, {DEFDATE},'','')
       LET $HeaderOutput = 'Printed: ' || $SystemDtOutput
       BREAK
END-EVALUATE

PRINT $HeaderOutput                                             (8,#RightPosition) BOLD
!**********Begin Incident 554682005 ***********
IF $SQR_name = 'PER025' AND $ReportChoice = '1'
 LET $HeaderOutput1 = 'For Company ' || $COMPANY
 PRINT $HeaderOutput1                                         (9,#RightPosition) BOLD
END-IF
!**********End Incident 554682005 ***********

! Check to see if customer page number was
! provided, else use SQR page numbering
!-----------------------------------------
IF #PageCount != 0
    LET $PageCount = TO_CHAR(#PageCount)
    LET $HeaderOutput = 'Page: ' || $PageCount
    PRINT $HeaderOutput                                         (6,#RightPosition)
ELSE
    page-number                                                 (6,#RightPosition)  'Page No.  '
END-IF

! Print out how the report was run
! --------------------------------
LET $ReportRunOut = ''
EVALUATE $ReportChoice
   WHEN = '1' ! By Company
      LET $ReportRunOut = 'Run for Establishments in Company'
      BREAK
   WHEN = '2' ! Single Establishment
      LET $ReportRunOut = 'Run for Single Establishment'
      BREAK
   WHEN = '3' ! HQ Unit and Controlled Estabs
      LET $ReportRunOut = 'Run for HQ & Controlled Estabs'
      BREAK
   WHEN = '4' ! All Establishments
      LET $ReportRunOut = 'Run for All Establishments'
      BREAK
END-EVALUATE

PRINT $ReportRunOut                                            (7, #RightPosition) BOLD

! Print formatted name/address information
!-----------------------------------------
DO ADFORMAT

if $ADDLINE1 <> ''
    ALTER-PRINTER
        point-size = 12
    print $ADDLINE1  (4,#AddressPosition)
    ALTER-PRINTER
        point-size = 8
end-if

if $ADDLINE2 <> ''
    print $ADDLINE2  (+1,#AddressPosition)
end-if
if $ADDLINE3 <> ''
    print $ADDLINE3  (+1,#AddressPosition)
end-if
if $ADDLINE31 <> ''
    print $ADDLINE31 (+1,#AddressPosition)
end-if
if $ADDLINE4 <> ''
    print $ADDLINE4  (+1,#AddressPosition)
end-if
if $ADDLINE5 <> ''
    print $ADDLINE5  (+1,#AddressPosition)
end-if
if $ADDLINE6 <> ''
    print $ADDLINE6  (+1,#AddressPosition)
end-if


! Print CONFIDENTIAL in big letters
!-----------------------------------------

If $Confidential != ''
    ALTER-PRINTER
        point-size=36

    PRINT 'CONFIDENTIAL'    (3,#ConfidentialPosition) BOLD

END-IF

! Print "Not to be submitted"
!-----------------------------------------

If $No_Submission != ''
     ALTER-PRINTER
        point-size=16
    PRINT 'Informational Report - DO NOT SUBMIT'    (8,1) BOLD CENTER

END-IF


! Set back to the default setting for the document
ALTER-PRINTER
    point-size=10

End-Heading

!----------------------------------------------------------------------
!---------Footer-------------------------------------------------------
!----------------------------------------------------------------------
! The 'fine print' at the bottom of the page
!----------------------------------------------------------------------

Begin-Footing 2
#IFDEF DEBUGX
  DISPLAY 'FLOW: Footer'
#ENDIF


! If the Confidental flag is set, print warning
!-----------------------------------------------
IF $Confidential != ''
    ALTER-PRINTER
        point-size=7

    PRINT 'This form contains sensitive or proprietary information. Please do not distribute.' (2,#TitlePosition)

END-IF

! Print Copyright notice
!-----------------------------------------------
ALTER-PRINTER
    point-size=5

do Format-DateTime($AsOfToday, $AsOfOutput, {DEFDATE},'','')

let $TheYear =  {ps-substr}($AsOfOutput, 7, 4)

LET $FooterOutput = $ReportTitle || ' copyright ' || $TheYear || ' PeopleSoft, Inc.'
PRINT $FooterOutput                                             (2,#CopyrightPosition)

ALTER-PRINTER
    point-size=10

End-Footing


!----------------------------------------------------------------------
!--------Use-Standard-Layout-------------------------------------------
!----------------------------------------------------------------------
! This should never be called more than once per page, and should
! generally only be called once at the beginning of the report
!----------------------------------------------------------------------
Begin-Procedure Use-Standard-Layout
   USE-REPORT landscapeReport
   LET #TitlePosition = 2
   LET #RightPosition = 88
   LET #AddressPosition = 7
   LET #ConfidentialPosition = 69
   LET #CopyrightPosition = 80
End-Procedure Use-Standard-Layout

#include    'adformat.sqc'              ! Address Formatting
