!***********************************************************************
!  PAY715: Print contents of Garnishment Rule/Exempt/Operands Table    *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/04/06:10:58:20                           
!             $Release:  HR9                                           
!            $Revision:  102                                           
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY715CN' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language
  move $Pay715CN_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY715CN program ids
  do Init_Report_Translation($ReportID,$language_cd)

  do Get_Field_Information ('PAY715CN', 'REPORT_TITLE',  $Pay715CN_ReportTitle,   #dummy_width)
  do Get_Field_Information ('PAY715CN', 'REPORT_TITLE2', $Pay715CN_ReportTitle2,  #dummy_width)
  do Get_Field_Information ('PAY715CN', 'GARN_DE_DEFN1', $Pay715CN_Garn_DE_Defn1, #dummy_width)
  do Get_Field_Information ('PAY715CN', 'MAINT_RESP1',   $Pay715CN_Maint_Resp1,   #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EFFDT1',        $Pay715CN_EffDt1,        #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_CALC1',      $Pay715CN_Ex_Calc1,      #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_HRS1',       $Pay715CN_Ex_Hrs1,       #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_PCT1',       $Pay715CN_Ex_Pct1,       #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_AMT1',       $Pay715CN_Ex_Amt1,       #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_DEP_EXCL1',  $Pay715CN_Ex_Dep_Excl1,  #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_DEP_AMT1',   $Pay715CN_Ex_Dep_Amt1,   #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_ALW_AMT1',   $Pay715CN_Ex_Alw_Amt1,   #dummy_width)
  do Get_Field_Information ('PAY715CN', 'PROV',          $Pay715CN_Prov,          #dummy_width)
  do Get_Field_Information ('PAY715CN', 'RULE_ID2',      $Pay715CN_Rule_Id2,      #dummy_width)
  do Get_Field_Information ('PAY715CN', 'DESCRLONG2',    $Pay715CN_DescrLong2,    #dummy_width)
  do Get_Field_Information ('PAY715CN', 'GARN_DE_DEFN2', $Pay715CN_Garn_DE_Defn2, #dummy_width)
  do Get_Field_Information ('PAY715CN', 'MAINT_RESP2',   $Pay715CN_Maint_Resp2,   #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EFFDT2',        $Pay715CN_EffDt2,        #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_CALC2',      $Pay715CN_Ex_Calc2,      #dummy_width)
  do Get_Field_Information ('PAY715CN', 'VAR_FREQ2',     $Pay715CN_Var_Freq2,     #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_HRS2',       $Pay715CN_Ex_Hrs2,       #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_PCT2',       $Pay715CN_Ex_Pct2,       #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_AMT2',       $Pay715CN_Ex_Amt2,       #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_DEP_EXCL2',  $Pay715CN_Ex_Dep_Excl2,  #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_DEP_AMT2',   $Pay715CN_Ex_Dep_Amt2,   #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_ALW_AMT2',   $Pay715CN_Ex_Alw_Amt2,   #dummy_width)

  do Get_Field_Information ('PAY715CN', 'EX_MIN',        $Pay715CN_Ex_Min,      #dummy_width)
  do Get_Field_Information ('PAY715CN', 'EX_MAX',        $Pay715CN_Ex_Max,      #dummy_width)
  do Get_Field_Information ('PAY715CN', 'STEP',          $Pay715CN_Step,        #dummy_width)
  do Get_Field_Information ('PAY715CN', 'CALC_TYPE',     $Pay715CN_Calc_Type,   #dummy_width)
  do Get_Field_Information ('PAY715CN', 'ELEMENT1',      $Pay715CN_Element1,    #dummy_width)
  do Get_Field_Information ('PAY715CN', 'AMOUNT',        $Pay715CN_Amount,      #dummy_width)
  do Get_Field_Information ('PAY715CN', 'PERCENT',       $Pay715CN_Percent,     #dummy_width)
  do Get_Field_Information ('PAY715CN', 'MATHEMATICAL',  $Pay715CN_Math,        #dummy_width)
  do Get_Field_Information ('PAY715CN', 'OPERATOR',      $Pay715CN_Operator,    #dummy_width)
  do Get_Field_Information ('PAY715CN', 'ELEMENT2',      $Pay715CN_Element2,    #dummy_width)
  do Get_Field_Information ('PAY715CN', 'CONDITION',     $Pay715CN_Condition,   #dummy_width)

end-procedure

begin-HEADING 7

  #Include 'stdhdgtr.sqc'

  move 'Canada' to $CountryName
  print $CountryName                 (3,)        center

  if $Print-Operands-Hdr = 'Y'
      print $Pay715CN_Amount         (5,106)
      print $Pay715CN_Amount         (0,139)
      print $Pay715CN_Garn_DE_Defn1  (+1,52)
      print $Pay715CN_EffDt1         (0,64)
      print $Pay715CN_Percent        (0,106)
      print $Pay715CN_Math           (0,118)
      print $Pay715CN_Percent        (0,139)
  else
      print $Pay715CN_Garn_DE_Defn1  (6,52)
      print $Pay715CN_Maint_Resp1    (0,64)
      print $Pay715CN_EffDt1         (0,71)
      print $Pay715CN_Ex_Calc1       (0,81)
      print $Pay715CN_Ex_Pct1        (0,93)
      print $Pay715CN_Ex_Amt1        (0,106)
      print $Pay715CN_Ex_Amt1        (0,114)
      print $Pay715CN_Ex_Dep_Excl1   (0,124)
      print $Pay715CN_Ex_Dep_Amt1    (0,135)
      print $Pay715CN_Ex_Alw_Amt1    (0,147)
      print $Pay715CN_Ex_Amt1        (0,159)
      print $Pay715CN_Ex_Amt1        (0,168)
  end-if

  print $Pay715CN_Prov               (+1,1)

  if $Print-Operands-Hdr = 'Y'
      print $Pay715CN_Rule_Id2       (0,8)
      print $Pay715CN_DescrLong2     (0,20)
      print $Pay715CN_Garn_DE_Defn2  (0,52)
      print $Pay715CN_EffDt2         (0,64)
      print $Pay715CN_Step           (0,75)
      print $Pay715CN_Calc_Type      (0,81)
      print $Pay715CN_Element1       (0,94)
      print $Pay715CN_Step           (0,106)
      print $Pay715CN_Operator       (0,118)
      print $Pay715CN_Element2       (0,128)
      print $Pay715CN_Step           (0,139)
      print $Pay715CN_Condition      (0,151)
  else
      print $Pay715CN_Rule_Id2       (0,8)
      print $Pay715CN_DescrLong2     (0,20)
      print $Pay715CN_Garn_DE_Defn2  (0,52)
      print $Pay715CN_Maint_Resp2    (0,64)
      print $Pay715CN_EffDt2         (0,71)
      print $Pay715CN_Ex_Calc2       (0,81)
      print $Pay715CN_Var_Freq2      (0,93)
      print $Pay715CN_Ex_Pct2        (0,106)
      print $Pay715CN_Ex_Amt2        (0,114)
      print $Pay715CN_Ex_Dep_Excl2   (0,124)
      print $Pay715CN_Ex_Dep_Amt2    (0,135)
      print $Pay715CN_Ex_Alw_Amt2    (0,147)
      print $Pay715CN_Ex_Min         (0,159)
      print $Pay715CN_Ex_Max         (0,168)
  end-if

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main

 Move 'N' to $Print-Operands-Hdr
 do Process-Rules

 NEW-PAGE
 move $Pay715CN_ReportTitle2 to $ReportTitle
 move 'Y' to $Print-Operands-Hdr

 do Process-Garn-Operands

END-PROCEDURE


BEGIN-PROCEDURE Process-Rules
BEGIN-SELECT

A.STATE_GARN_LAW
  print &A.State_Garn_Law  (+1,1,5)
A.GARN_RULE_ID             (0,+2,10)
A.DESCR                    (0,+2,30)
A.GARN_DE_DEFNID           (0,+2,10)
A.MAINT_RESPONSBLTY        (0,+4,3)
A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out               (0,+1)
A.EFF_STATUS
  if &A.Eff_Status = 'I'
    print 'I'              (0,0,1)
  else
    print ' '              (0,0,1)
  end-if

  do Get-Exempt-Records

A.DESCRLONG
  if RTRIM(&A.DescrLong,' ') <> ''
    print &A.DescrLong     (+2,20)      wrap 140 20
                                        ON=<10> STRIP=<13>
    POSITION (+1)
  end-if

S.COUNTRY

FROM  PS_GARN_RULE_TBL A, PS_STATE_NAMES_TBL S
WHERE A.STATE_GARN_LAW = S.STATE
  AND S.COUNTRY = 'CAN'
ORDER BY S.COUNTRY, A.STATE_GARN_LAW, A.GARN_RULE_ID, A.EFFDT DESC
END-SELECT
END-PROCEDURE

BEGIN-PROCEDURE Get-Exempt-Records
  move 0 to #LineIncr
BEGIN-SELECT
B.GARN_EX_CALC
  move 'GARN_EX_CALC'     to $FieldName
  move &B.Garn_Ex_Calc    to $FieldValue
  do Read-Translate-Table
  print $XlatShortName     (+#LineIncr,81,10)
B.GARN_VAR_FREQUENCY
  move 'GARN_VAR_FREQUENCY'   to $FieldName
  move &B.Garn_Var_Frequency  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName     (0,+2,10)
B.GARN_EX_PCT
  if &B.Garn_Ex_Calc = 'C' and &B.Garn_Ex_Pct = 0
    do Format-Number(&B.Garn_Ex_Pct, $out, '999.99')
  else
    do Format-Number(&B.Garn_Ex_Pct, $out, 'b99.99')
  end-if
  print $out               (0,+2,6)
B.GARN_EX_AMT
  do Format-Number(&B.Garn_Ex_Amt, $out, 'b99999.99')
  print $out               (0,+2,9)
B.GARN_EX_DEP_EXCL         (0,+5,6)     EDIT B99
B.GARN_EX_DEP_AMT
  do Format-Number(&B.Garn_Ex_Dep_Amt, $out, 'b99999.99')
  print $out               (0,+3,9)
B.GARN_EX_ALW_AMT
  do Format-Number(&B.Garn_Ex_Alw_Amt, $out, 'b99999.99')
  print $out               (0,+2,9)
B.GARN_EX_MIN
  do Format-Number(&B.Garn_Ex_Min, $out, 'b99999.99')
  print $out               (0,+3,9)
B.GARN_EX_MAX
  do Format-Number(&B.Garn_Ex_Max, $out, 'b99999.99')
  print $out               (0,+2,9)

  move 1 to #LineIncr
FROM  PS_GARN_EXEMPT_TBL B
WHERE B.STATE_GARN_LAW    = &A.State_Garn_Law
  AND B.GARN_RULE_ID      = &A.Garn_Rule_ID
  AND B.EFFDT             = &A.EffDt
ORDER BY B.GARN_EX_CALC
END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Process-Garn-Operands
BEGIN-SELECT
C.STATE_GARN_LAW
C.GARN_RULE_ID
C.DESCR
C.GARN_DE_DEFNID
C.EFFDT

  print &C.State_Garn_Law  (+1,1)
  print &C.GARN_RULE_ID    (0,8)
  print &C.DESCR           (0,20)
  print &C.GARN_DE_DEFNID  (0,52)

  do Format-DateTime(&C.EFFDT, $out, {DEFDATE}, '', '')
  print $out                   (0,64)

  do Get-Garn-Operands

D.COUNTRY

FROM  PS_GARN_RULE_TBL C, PS_STATE_NAMES_TBL D
WHERE C.STATE_GARN_LAW = D.STATE
  AND D.COUNTRY = 'CAN'
ORDER BY D.COUNTRY, C.STATE_GARN_LAW, C.GARN_RULE_ID, C.EFFDT DESC
END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Get-Garn-Operands
  move 0 to #LineIncr
BEGIN-SELECT
E.GARN_STEP
  do Format-Number(&E.GARN_STEP, $out, '999')
  print $out                   (+#LineIncr,76)

E.GARN_CALC_TYPE
  if &E.GARN_CALC_TYPE = 'D'
      print 'DE for Rule'      (0,81)
  else
      move 'GARN_CALC_TYPE'   to $FieldName
      move &E.GARN_CALC_TYPE  to $FieldValue
      do Read-Translate-Table
      print $XlatShortName     (0,81)
  end-if

E.GARN_ELEMENT1
E.GARN_ELEMENT2
E.GARN_OPERATOR
E.GARN_AMOUNT1
E.GARN_AMOUNT2
E.GARN_PERCENT1
E.GARN_PERCENT2
E.GARN_STEP1
E.GARN_STEP2
E.GARN_CONDITION

  move 'GARN_ELEMENT1'   to $FieldName
  move &E.GARN_ELEMENT1  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,94)

  If RTRIM(&E.GARN_ELEMENT1,' ') = 'P'
      do Format-Number(&E.GARN_PERCENT1, $out, '999.99')
      print $out               (0,110)
  else
      if RTRIM(&E.GARN_ELEMENT1,' ') = 'Q'
          do Format-Number(&E.GARN_AMOUNT1, $out, 'B999999.99')
          print $out           (0,106)
      else
          if RTRIM(&E.GARN_ELEMENT1,' ') = 'S'
              do Format-Number(&E.GARN_STEP1, $out, '999')
              print $out       (0,110)
          end-if
      end-if
  end-if

  move 'GARN_OPERATOR'   to $FieldName
  move &E.GARN_OPERATOR  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,120)

  move 'GARN_ELEMENT2'   to $FieldName
  move &E.GARN_ELEMENT2  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName         (0,128)

  If RTRIM(&E.GARN_ELEMENT2,' ') = 'P'
      do Format-Number(&E.GARN_PERCENT2, $out, '999.99')
      print $out               (0,143)
  else
      if RTRIM(&E.GARN_ELEMENT2,' ') = 'Q'
          do Format-Number(&E.GARN_AMOUNT2, $out, 'B9999999.99')
          print $out           (0,138)
      else
          if RTRIM(&E.GARN_ELEMENT2,' ') = 'S'
              do Format-Number(&E.GARN_STEP2, $out, '999')
              print $out       (0,143)
          end-if
      end-if
  end-if

  move 'GARN_CONDITION'   to $FieldName
  move &E.GARN_CONDITION  to $FieldValue
  do Read-Translate-Table
  print $XlatLongName          (0,151,26)

  move 1 to #LineIncr

E.STATE_GARN_LAW
E.GARN_RULE_ID
E.EFFDT

FROM  PS_GARN_OPERANDS E
WHERE E.STATE_GARN_LAW = &C.STATE_GARN_LAW
  AND E.GARN_RULE_ID = &C.GARN_RULE_ID
  AND E.EFFDT = &C.EFFDT
ORDER BY E.STATE_GARN_LAW, E.GARN_RULE_ID, E.EFFDT, E.GARN_STEP
END-SELECT
END-PROCEDURE


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
