!************************************
!  FGSF75:  Print the SF175         *
!************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2006/07/19:12:22:21                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment

#define PageWidth  192
#define PageLength 254

begin-setup

#include 'setupdb.sqc'
 declare-variable
    date $REQUESTDT
 end-declare

declare Printer           !Declare printer characteristics.
    type=HPLASERJET       !Types are POSTSCRIPT, HPLASERJET.
    orientation=Portrait  !Orientations are PORTRAIT or LANDSCAPE.
    top-margin  = .20
    left-margin = .05
    font        =  4      !Font Number
    point-size  =  3
    char-size   =  3
    line-size   =  3

page-size {PageLength} {PageWidth}
end-setup

begin-REPORT
  do Init-DateTime
  do Init-Report
  do Print-SF75-Form
  do Get-SF75-Data
  do Print-SF75-Data
  do Stdapi-Term
end-REPORT
!-------------------------------------------
begin-procedure Init-Report

  move 'SF75' to $ReportID
  move 'Request For Preliminary Employment Data' to $ReportTitle
  display $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
        input $EMPLID MAXLEN=11 'Enter Employee ID' TYPE=CHAR
        move 'Y' to $DateOK
        input $REQDT 'Enter Date For Request ({Prompt-Date})'
        if $REQDT = ''
           do Get-Current-DateTime
           move $AsOfToday to $REQDT
        else
           move {PDelimiter} to $DDelimiter
           do Format-DateTime($REQDT,$REQUESTDT,{Prompt-Mask},'','native')
        end-if

        do Validate-Native-Date($REQUESTDT,$DateOK)

        if $DateOK = 'N'
           display '**** This is an invalid date, please reenter ****'
        end-if
  else
    do Select-Parameters
  end-if
end-procedure

begin-procedure Select-Parameters

display $prcs_oprid
display $prcs_run_cntl_id

begin-select
RC.EMPLID
RC.REQUEST_DT
RC.GVT_INFO_EMPLID
RC.NAME
RC.PHONE
RC.JOBTITLE
RC.ADDRESS1
RC.ADDRESS2
RC.ADDRESS3
RC.CITY
RC.STATE
RC.POSTAL
RC.ADDRESS1_OTHER
RC.ADDRESS2_OTHER
RC.ADDRESS3_OTHER
RC.CITY_OTHER
RC.STATE_OTHER
RC.POSTAL_OTHER
RC.GVT_TO_NAME
RC.GVT_TO_TITLE
RC.DESCRLONG
        move &RC.GVT_INFO_EMPLID to $EMPLID
        move &RC.REQUEST_DT      to $REQUESTDT
FROM PS_RUN_FGSF75 RC
WHERE   RC.OPRID = $prcs_oprid
AND     RC.RUN_CNTL_ID = $prcs_run_cntl_id
end-select
end-procedure

!-------------------------------------------


begin-procedure Print-SF75-Data
let $birthdate  = substr(datetostr(&PD.BIRTHDATE),1,11)
let $hire_dt    = substr(datetostr(&E.HIRE_DT),1,11)
let $request_dt = substr(datetostr(&RC.REQUEST_DT),1,11)
print &PD.NAME                                          (122, 2) BOLD
print &NID.NATIONAL_ID                                  (122, 96) edit 'xxx-xx-xxxx' BOLD
print $birthdate                                        (122, 149) BOLD
print &POS.DESCR                                        (134, 2) BOLD
string &JF.GVT_PAY_PLAN &POS.GVT_OCC_SERIES by '/' into $tmp
print $tmp                                              (134, 96) BOLD
string &J.GRADE &JF.GVT_STEP by '/' into $tmp
print $tmp                                              (134, 149) BOLD
print &com.descr                                        (146, 2) BOLD
print &POS.GVT_POI                                      (146, 96) BOLD
print $hire_dt                                          (158, 2) BOLD
print $request_dt                                       (26, 146) BOLD
print &RC.GVT_TO_NAME                                   (54, 15) BOLD

if &RC.GVT_TO_TITLE <> ' '
   print &RC.GVT_TO_TITLE                               (+4, 15) BOLD
end-if

if &RC.ADDRESS1_OTHER <> ' '
   print &RC.ADDRESS1_OTHER                             (+4, 15) BOLD
end-if

if &RC.ADDRESS2_OTHER <> ' '
   print &RC.ADDRESS2_OTHER                             (+4, 15) BOLD
end-if

if &RC.ADDRESS3_OTHER <> ' '
   print &RC.ADDRESS3_OTHER                             (+4, 15) BOLD
end-if

string &RC.CITY_OTHER ', ' &RC.STATE_OTHER ' ' &RC.POSTAL_OTHER by '' into $tmp
print $tmp                                              (+4,15) BOLD


print &RC.NAME                                          (169, 2) BOLD
print &RC.JOBTITLE                                      (169, 96) BOLD

print &RC.PHONE                                         (183, 96) BOLD

print ' '                                       (221, 1)
if &RC.ADDRESS1 <> ' '
   print &RC.ADDRESS1                           (+4, 15) BOLD
end-if

if &RC.ADDRESS2 <> ' '
   print &RC.ADDRESS2                           (+4, 15) BOLD
end-if

if &RC.ADDRESS3 <> ' '
   print &RC.ADDRESS3                           (+4, 15) BOLD
end-if

string &RC.CITY ', ' &RC.STATE ' ' &RC.POSTAL by '' into $tmp
print $tmp                                              (+4,15) BOLD

Move &RC.DESCRLONG to $PrnText
move 2 to #PrnStartCol
move 190 to #PrnStartRow
move 120 to #MaxWrapWidth
move 4 to #MoveLinesDown
move 5 to #MaxPrnLines

move 'Y' to $PrintBold
do PrintWrap

end-procedure

#include 'fgsf75d.sqc'
#include 'fgsf75f.sqc'
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines to format datetimes
#Include 'number.sqc'    !Routine to format numbers
#include 'validdt.sqc'          ! Validate date routine
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'fgprtwrp.sqc'
