!***********************************************************************
!  CTX915V:    PRINT RL-2   Summary Report                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2012, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2012/12/24:12:41:50                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  885414                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size init
#Include 'ctxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#define TAXFORM_BOX
#include 'rellang.sqc'

begin-heading 5
  #Include 'stdhdgtr.sqc'
  print $TaxYear           (3,,)      center
end-heading

begin-report

  do Init-Report

  do Process-Main

  close 4

#ifdef PRCSSCHD
  do StdAPI-Term
#endif

end-report

begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
#ifdef PRCSSCHD
  do StdAPI-Init
#endif
  do Initialization


end-procedure

begin-procedure Initialization

#define ColR 120

  move 'N' to $SummaryAtEnd

  do Get-Current-DateTime

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($AsOfToday, $AsOfDateYMD, {DEFYMD},'','')
  move $AsOfDateYMD to $AsOfYear xxxx

  move $AsOfYear  to #AsOfYear
  subtract 1 from #AsOfYear

  do Get-Can-Tax-Processing-Params

  move &TX.Balance_Year to #TaxYear
  move #TaxYear to $TaxYear 9999

  let $AsOfDate = $TaxYear || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP},'','native')

   if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''
    display ''
    display 'Current Year is ' noline
    display $AsOfYear
    display 'Tax Reporting Year is ' noline
    display $TaxYear
    display 'Current Year is not one greater than Tax Reporting Year.'
    input $Answer maxlen=1 'Current Year is not one greater than Tax Reporting Year. Do you want to continue? (Y/N)'
    uppercase $Answer

    if $Answer <> 'Y'
      stop
    end-if
  end-if

  do Open-Files

  move 'CTX915V' to $ReportID
  do Get-Report-Language          ! Procedure that decides the report language
                                  ! and then calls 'Report-Translation' procedure
                                  ! that reads the language dep. text.

  move $Ctx915v_Title_Rv2_summ to $ReportTitle
  display 'PRINTING RL-2 SUMMARY DATA '

  do Array-Create
  do Load-Box-Arrays
  do Format-Messages

end-procedure

!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
!Read language-dependent text from the database.

begin-procedure Report-Translation        ! Read from CTX915V  program ids

   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('CTX915V', 'TITLE_RV2_SUMM' , $Ctx915v_Title_Rv2_summ , #dummy_width)
   do Get_Field_Information ('CTX915V', 'RV2_SUMM_ORIG'  , $Ctx915v_Rv2_summ_orig  , #dummy_width)
   do Get_Field_Information ('CTX915V', 'RV2_SUMM_AMEND' , $Ctx915v_Rv2_summ_amend , #dummy_width)
   do Get_Field_Information ('CTX915V', 'RV2_SUMM_CANCEL', $Ctx915v_Rv2_summ_cancel, #dummy_width)
   do Get_Field_Information ('CTX915V', 'EMPLER_NAME'    , $Ctx915v_Empler_name    , #dummy_width)
   do Get_Field_Information ('CTX915V', 'EMPLER_QID'     , $Ctx915v_Empler_qid     , #dummy_width)
   do Get_Field_Information ('CTX915V', 'TAX_YEAR'       , $Ctx915v_Tax_year       , #dummy_width)
   do Get_Field_Information ('CTX915V', 'TOT_RV_SLIPS'   , $Ctx915v_Tot_rv_slips   , #dummy_width)

end-procedure


begin-procedure Open-Files

#ifdef MPE
    open '{IMPORTPREFIX}CTX910V1.DAT' as 4 for-reading record=250
#else
    open '{IMPORTPREFIX}CTX910V1{IMPORTSUFFIX}' as 4 for-reading record=250
#endif

end-procedure


begin-procedure Array-Create

  create-array name=PrR2_Area size=60 -
    field=PR2_Box:char                -
    field=PR2_Descr:char

end-procedure

begin-procedure Load-Box-Arrays

    move 0 to #i

begin-SELECT
M.TAXFORM_ID
M.BOX
M.DESCR
M.EFFDT

   let $M.BOX = RTRIM(&M.BOX, ' ')
   let $TAXFORM_BOX-DESCR = &M.DESCR

   do Get_Related_TAXFORM_BOX(&M.BOX,&M.EFFDT,&M.TAXFORM_ID)

   put $M.Box $TAXFORM_BOX-DESCR into PrR2_Area(#i)
         PR2_Box PR2_Descr
       add 1 to #i
       move #i to #max_R2boxes


FROM PS_TAXFORM_BOX M
WHERE TAXFORM_ID = 'V' AND
      EFFDT = (SELECT MAX(EFFDT)
               FROM PS_TAXFORM_BOX
               WHERE TAXFORM_ID = 'V'
                 AND EFFDT <= $AsOfDate)
ORDER BY M.BOX ASC
end-SELECT

end-procedure

begin-procedure Format-Messages

  move 0 to #i

  while #i <= #max_R2boxes

        get $PrBox $PrDescr from PrR2_Area(#i)
                   PR2_Box PR2_Descr
        let $MsgBox = RTRIM($PrBox, ' ')
        let $MsgSum   = RTRIM($PrDescr, ' ')

        evaluate $MsgBox
        when = 'A'
           let $SumA = $MsgSum
           break
        when = 'B'
           let $SumB = $MsgSum
           break
        when = 'C'
           let $SumC = $MsgSum
           break
        when = 'D'
           let $SumD = $MsgSum
           break
        when = 'E'
           let $SumE = $MsgSum
           break
        when = 'F'
           let $SumF = $MsgSum
           break
        when = 'G'
           let $SumG = $MsgSum
           break
        when = 'H'
           let $SumH = $MsgSum
           break
        when = 'I'
           let $SumI = $MsgSum
           break
        when = 'J'
           let $SumJ = $MsgSum
           break
        when = 'K'
           let $SumK = $MsgSum
           break
        when = 'L'
           let $SumL = $MsgSum
           break
        when = 'M'
           let $SumM = $MsgSum
           break
        when = 'O'
           let $SumO = $MsgSum
           break
        when-other
           break

        end-evaluate

        add 1 to #i

  end-while

end-procedure


begin-procedure Process-Main

  while 1 = 1

   if $SummaryAtEnd = 'Y'
      break
   else
      do Read-Summary
      if $SummaryAtEnd <> 'Y'
         do Print-Summary
      end-if
   end-if
  end-while

end-procedure

begin-procedure Read-Summary

  let #TotalSupplementary = 0
  let #TotalLAPayment     = 0
  let #TotalMBenefits     = 0
  let #TotalOPayments     = 0
  let #TotalRefRRSP       = 0
  let #TotalAmtPriorD     = 0
  let #TotalRExcessRRSP   = 0
  let #TotalRecRevRRP     = 0
  let #TotalOtherIncome   = 0
  let #TotEntlDed         = 0
  let #TotQITWithheld     = 0
  let #TotTaxPaid         = 0
  let #TotIncDeath        = 0
  let #TotWthdrwlLLP      = 0
  let #TotWthdrwlHBP      = 0

  read 4 into $SumInput:250

  if #end-file = 1
    move 'Y' to $SummaryAtEnd
  else

    let $Employer_Name      = substr($SumInput,1,30)  !  1-30  Summary record
    let $TaxYear            = substr($SumInput,31,4)  ! 31-34
    let $TotalSupplementary = substr($SumInput,35,7)  ! 35-41
    let $TotalLAPayment     = substr($SumInput,42,13) ! 42-54  Box A
    let $TotalMBenefits     = substr($SumInput,55,11) ! 55-65  Box B
    let $TotalOPayments     = substr($SumInput,66,11) ! 66-76  Box C
    let $TotalRefRRSP       = substr($SumInput,77,11) ! 77-87  Box D
    let $TotalAmtPriorD     = substr($SumInput,88,11) ! 88-98  Box E
    let $TotalRExcessRRSP   = substr($SumInput,99,11) ! 99-109 Box F
    let $TotalRecRevRRP     = substr($SumInput,110,13)!110-122 Box G
    let $TotalOtherIncome   = substr($SumInput,123,11)!123-133 Box H
    let $TotEntlDed         = substr($SumInput,134,11)!134-144 Box I
    let $TotQITWithheld     = substr($SumInput,145,11)!145-155 Box J
    let $TotIncDeath        = substr($SumInput,156,13)!156-168 Box K
    let $TotWthdrwlLLP      = substr($SumInput,169,13)!169-181 Box L
    let $TotTaxPaid         = substr($SumInput,182,11)!182-192 Box M
    let $TotWthdrwlHBP      = substr($SumInput,193,15)!193-207 Box O
    let $ReptID             = substr($SumInput,208,16)!208-223 no box
    let $ReportType         = substr($SumInput,224,1) !224-224 original/amended/cancelled

    move $TotalSupplementary to #TotalSupplementary
    move $TotalLAPayment     to #TotalLAPayment
    move $TotalMBenefits     to #TotalMBenefits
    move $TotalOPayments     to #TotalOPayments
    move $TotalRefRRSP       to #TotalRefRRSP
    move $TotalAmtPriorD     to #TotalAmtPriorD
    move $TotalRExcessRRSP   to #TotalRExcessRRSP
    move $TotalRecRevRRP     to #TotalRecRevRRP
    move $TotalOtherIncome   to #TotalOtherIncome
    move $TotEntlDed         to #TotEntlDed
    move $TotQITWithheld     to #TotQITWithheld
    move $TotTaxPaid         to #TotTaxPaid
    move $TotIncDeath        to #TotIncDeath
    move $TotWthdrwlLLP      to #TotWthdrwlLLP
    move $TotWthdrwlHBP      to #TotWthdrwlHBP

  end-if

end-procedure

begin-procedure Print-Summary

     evaluate $ReportType
     when = 'O'
       print   $Ctx915v_Rv2_summ_orig      (+1,1)
       break
     when = 'A'
       print   $Ctx915v_Rv2_summ_amend     (+1,1)
       break
     when = 'C'
       print   $Ctx915v_Rv2_summ_cancel    (+1,1)
       break
     when-other
       break
     end-evaluate

     print    $Ctx915v_Empler_name                              (+2,1)
     print    $Employer_Name                                    (0,50)
     print    $Ctx915v_Empler_qid                               (+1,1)
     print    $ReptID                                           (0,50)
     print    $Ctx915v_Tax_year                                 (+1,1)
     print    $TaxYear                                          (0,50)
     print    $Ctx915v_Tot_rv_slips                             (+1,1)
     print    #TotalSupplementary                               (0,50)  edit 9999999999
     print    $SumA                                             (+1,1)
     print    #TotalLAPayment                                   (0,50)  edit 9999999999.99
     print    $SumB                                             (+1,1)
     print    #TotalMBenefits                                   (0,50)  edit 9999999999.99
     print    $SumC                                             (+1,1)
     print    #TotalOPayments                                   (0,50)  edit 9999999999.99
     print    $SumD                                             (+1,1)
     print    #TotalRefRRSP                                     (0,50)  edit 9999999999.99
     print    $SumE                                             (+1,1)
     print    #TotalAmtPriorD                                   (0,50)  edit 9999999999.99
     print    $SumF                                             (+1,1)
     print    #TotalRExcessRRSP                                 (0,50)  edit 9999999999.99
     print    $SumG                                             (+1,1)
     print    #TotalRecRevRRP                                   (0,50)  edit 9999999999.99
     print    $SumH                                             (+1,1)
     print    #TotalOtherIncome                                 (0,50)  edit 9999999999.99
     print    $SumI                                             (+1,1)
     print    #TotEntlDed                                       (0,50)  edit 9999999999.99
     print    $SumJ                                             (+1,1)
     print    #TotQITWithheld                                   (0,50)  edit 9999999999.99
     print    $SumK                                             (+1,1)
     print    #TotIncDeath                                      (0,50)  edit 9999999999.99
     print    $SumL                                             (+1,1)
     print    #TotWthdrwlLLP                                    (0,50)  edit 9999999999.99
     print    $SumM                                             (+1,1)
     print    #TotTaxPaid                                       (0,50)  edit 9999999999.99
     print    $SumO                                             (+1,1)
     print    #TotWthdrwlHBP                                    (0,47)  edit 9999999999999.99

     new-page

end-procedure


#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getdptnm.sqc'  !Get-Department-Name
#ifdef PRCSSCHD
#Include 'ctxrctl1.sqc' ! Get-Can-Tax YE Report Parameters
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls
#endif
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
