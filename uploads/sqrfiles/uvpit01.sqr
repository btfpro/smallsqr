!***********************************************************************
!  UVPIT01:  9 Upgrade - List all Payroll Interface record field       *
!            definitions that are no longer valid.                     *
!                                                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      !
!                $Date:  2006/05/24:13:10:49                           !
!             $Release:  HR9                                           !
!            $Revision:  101                                           !
!                                                                      !
!**********************************************************************!

#include 'setenv.sqc'    ! Set environment
#Include 'setup31.sqc'   ! Printer and page-size initialization


!***********************************************************************
begin-program
!***********************************************************************

    do Init-Report
    do Process-Main
    do Reset
    do Stdapi-Term

end-program



!***********************************************************************
begin-heading 4
!***********************************************************************

  #include 'stdhdg01.sqc'

end-heading

!***********************************************************************
begin-procedure PIPSRECFLD-heading
!***********************************************************************

  print 'The following fields defined in PS Tables are no longer found'  (+2,1,70)
  print 'in their corresponding records as given on this list:'          (+1,1,70)

  print 'PI System ID'                  (+2,1,12)  Underline
  print 'Recname'                       (0,+3,15)  Underline
  print 'Fieldname'                     (0,+3,18)  Underline

end-procedure

!***********************************************************************
begin-procedure PIFIELD-heading
!***********************************************************************

  print 'The following fields defined in PI Field Tbl are no longer'     (+2,1,70)
  print 'found in their corresponding records as given on this list:'    (+1,1,70)

  print 'PI System ID'                  (+2,1,12)  Underline
  print 'PI FieldID'                    (0,+3,10)  Underline
  print 'Recname'                       (0,+3,15)  Underline
  print 'Field1-Name'                   (0,+3,18)  Underline
  print 'Field2-Name'                   (0,+3,18)  Underline
  print 'Field3-Name'                   (0,+3,18)  Underline

end-procedure

!***********************************************************************
begin-procedure PIINSTANCE-heading
!***********************************************************************

  print 'The following fields defined in PI Instance Tbl are no longer'  (+2,1,70)
  print 'found in their corresponding records as given on this list:'    (+1,1,70)

  print 'PI System ID'                  (+2,1,12)  Underline
  print 'InstanceID'                    (0,+3,10)  Underline
  print 'Recname'                       (0,+3,15)  Underline
  print 'Field1-Name'                   (0,+3,18)  Underline

end-procedure

!***********************************************************************
!  Function:     Init Report
!  Description:  Prompt for run-time parameters
!***********************************************************************

begin-procedure Init-Report

  do Init-DateTime
  do Get-Current-DateTime

  let $ReportID    = 'UVPIT01'
  let $ReportTitle = 'Payroll Interface - Invalid Record/Field References'
  show ' '
  show $ReportID ': '  $ReportTitle
  show ' '

  do Stdapi-Init

end-procedure Init-Report


!***********************************************************************
!  Function:     Process-Main
!  Description:  This is the main report procedure.
!***********************************************************************

begin-procedure Process-Main

! The following section will locate fields that are in PS Tables
! definition but are no longer found in the corresponding record
! in Peoplesoft database.


  do PIPSRECFLD-heading
  print ' '     (+1,1)

begin-SELECT
A.PI_SYSTEM_ID
A.RECNAME
A.FIELDNAME

   print &A.PI_SYSTEM_ID     (+1,1,12)
   print &A.RECNAME          (0,+3,15)
   print &A.FIELDNAME        (0,+3,18)

FROM PS_PI_PSREC_FLD A
WHERE NOT EXISTS
(SELECT 'X' FROM PSRECFIELDALL B
 WHERE B.RECNAME   = A.RECNAME
   AND B.FIELDNAME = A.FIELDNAME)
ORDER BY A.PI_SYSTEM_ID, A.RECNAME, A.FIELDNAME
end-SELECT


! The following section will locate fields that are in PI Field Tbl
! definition but are no longer found in the corresponding record
! in Peoplesoft database.

  new-page
  do PIFIELD-heading
  print ' '     (+1,1)

begin-SELECT
A1.PI_SYSTEM_ID
A1.PI_FIELD_ID
A1.PS_RECORD_NM
A1.PS_FIELD1_NM

   print &A1.PI_SYSTEM_ID     (+1,1,12)
   print &A1.PI_FIELD_ID      (0,+3,10)
   print &A1.PS_RECORD_NM     (0,+3,15)
   print &A1.PS_FIELD1_NM     (0,+3,18)

FROM PS_PI_FIELD_TBL A1
WHERE NOT EXISTS
(SELECT 'X' FROM PSRECFIELDALL B1
 WHERE B1.RECNAME   = A1.PS_RECORD_NM
   AND B1.FIELDNAME = A1.PS_FIELD1_NM)
  AND A1.PS_RECORD_NM <> ' '
  AND A1.PS_FIELD1_NM <> ' '
ORDER BY A1.PI_SYSTEM_ID, A1.PI_FIELD_ID
end-SELECT

begin-SELECT
A2.PI_SYSTEM_ID
A2.PI_FIELD_ID
A2.PS_RECORD_NM
A2.PS_FIELD2_NM

   print &A2.PI_SYSTEM_ID     (+1,1,12)
   print &A2.PI_FIELD_ID      (0,+3,10)
   print &A2.PS_RECORD_NM     (0,+3,15)
   print ' '                  (0,+3,18)
   print &A2.PS_FIELD2_NM     (0,+3,18)

FROM PS_PI_FIELD_TBL A2
WHERE NOT EXISTS
(SELECT 'X' FROM PSRECFIELDALL B2
 WHERE B2.RECNAME   = A2.PS_RECORD_NM
   AND B2.FIELDNAME = A2.PS_FIELD2_NM)
  AND A2.PS_RECORD_NM <> ' '
  AND A2.PS_FIELD2_NM <> ' '
ORDER BY A2.PI_SYSTEM_ID, A2.PI_FIELD_ID
end-SELECT

begin-SELECT
A3.PI_SYSTEM_ID
A3.PI_FIELD_ID
A3.PS_RECORD_NM
A3.PS_FIELD3_NM

   print &A3.PI_SYSTEM_ID     (+1,1,12)
   print &A3.PI_FIELD_ID      (0,+3,10)
   print &A3.PS_RECORD_NM     (0,+3,15)
   print ' '                  (0,+3,18)
   print ' '                  (0,+3,18)
   print &A3.PS_FIELD3_NM     (0,+3,18)

FROM PS_PI_FIELD_TBL A3
WHERE NOT EXISTS
(SELECT 'X' FROM PSRECFIELDALL B3
 WHERE B3.RECNAME   = A3.PS_RECORD_NM
   AND B3.FIELDNAME = A3.PS_FIELD3_NM)
  AND A3.PS_RECORD_NM <> ' '
  AND A3.PS_FIELD3_NM <> ' '
ORDER BY A3.PI_SYSTEM_ID, A3.PI_FIELD_ID
end-SELECT


! The following section will locate fields that are in PI Instance Tbl
! definition but are no longer found in the corresponding record
! in Peoplesoft database.

  new-page
  do PIINSTANCE-heading
  print ' '     (+1,1)

begin-SELECT
A4.PI_SYSTEM_ID
A4.PI_INSTANCE_ID
A4.PS_RECORD_NM
A4.PS_FIELD1_NM

   print &A4.PI_SYSTEM_ID     (+1,1,12)
   print &A4.PI_INSTANCE_ID   (0,+3,10)
   print &A4.PS_RECORD_NM     (0,+3,15)
   print &A4.PS_FIELD1_NM     (0,+3,18)

FROM PS_PI_INSTANCE_TBL A4
WHERE NOT EXISTS
(SELECT 'X' FROM PSRECFIELDALL B1
 WHERE B1.RECNAME   = A4.PS_RECORD_NM
   AND B1.FIELDNAME = A4.PS_FIELD1_NM)
  AND A4.PS_RECORD_NM <> ' '
  AND A4.PS_FIELD1_NM <> ' '
ORDER BY A4.PI_SYSTEM_ID, A4.PI_INSTANCE_ID
end-SELECT


end-procedure Process-Main


!***********************************************************************
! SQC Files for called procedures
!***********************************************************************
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Routines to update run status
#Include 'reset.sqc'     !Reset printer procedure

