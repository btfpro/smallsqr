!***********************************************************************
!  BEN703: Disability Plan Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                $Date:  2006/03/21:15:00:39                           
!             $Release:  HR9                                           
!            $Revision:  101                                           
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

#define col_sep                  1   !Column Separator
#define col_plan_type            5   !Plan Type
#define col_plan_name           11   !Plan Name
#define col_ben_plan_id          9   !Benefit Plan ID
#define col_ben_plan_name       31   !Benefit Plan Name
#define col_eff_date            11   !Effective Date
#define col_sal_repl            15   !Salary Replacement %
#define col_formula             10   !Formula ID

!***********
begin-program
!***********

  do Init-Report
  do Get-Report-Language
!-- 'DISABILITY PLAN TABLE'
  let $ReportTitle  = $BEN703-REPORTTITLE
  display ' '
  display $ReportTitle
  display ' '

  do Process-Main

  do Reset
  do StdAPI-Term
end-program

!**************************
begin-procedure Init-Report
!**************************

  do Init-DateTime
  do Init-Number
  let $ReportID = 'BEN703'

  do Get-Current-DateTime
  do StdAPI-Init
end-procedure

!**********************************
Begin-Procedure Report-Translation
!**********************************

  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN703', 'REPORT_TITLE', $BEN703-REPORTTITLE,#CW)
  do Get_Field_Information ('BEN703', 'SALARY',       $BEN703-SALARY,     #CW)
  do Get_Field_Information ('BEN703', 'REPLCMNT',     $BEN703-REPLCMNT,   #CW)

  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,      #CW)
  do Get_Field_Information ('BEN1',   'BENEFIT',      $BEN1-BENEFIT,   #CW)
  do Get_Field_Information ('BEN1',   'MAX',          $BEN1-MAX,       #CW)
  do Get_Field_Information ('BEN1',   'EFFECTIVE',    $BEN1-EFFECTIVE, #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-TYPE,      #CW)
  do Get_Field_Information ('BEN1',   'NAME',         $BEN1-NAME,      #CW)
  do Get_Field_Information ('BEN1',   'PLAN_ID',      $BEN1-PLAN-ID,   #CW)
  do Get_Field_Information ('BEN1',   'PLAN_NAME',    $BEN1-PLAN-NAME, #CW)
  do Get_Field_Information ('BEN1',   'DATE',         $BEN1-DATE,      #CW)
  do Get_Field_Information ('BEN1',   'FORMULA',      $BEN1-FORMULA,   #CW)
  do Get_Field_Information ('BEN1',   'ID',           $BEN1-FORMULA-ID, #CW)

End-Procedure

!**************
begin-HEADING 7
!**************

  #Include 'stdhdgtr.sqc'

!Column Heading line 1
  print $BEN1-PLAN               (+2,1,{col_plan_type})
  print $BEN1-PLAN               (0,+{col_sep},{col_plan_name})
  print $BEN1-BENEFIT            (0,+{col_sep},{col_ben_plan_id})
  print $BEN1-BENEFIT            (0,+{col_sep},{col_ben_plan_name})
  print $BEN1-EFFECTIVE          (0,+{col_sep},{col_eff_date})
  print $BEN703-SALARY           (0,+{col_sep},{col_sal_repl})
  print $BEN1-FORMULA            (0,+{col_sep},{col_formula})

!Column Heading line 2
  print $BEN1-TYPE               (+1,1,{col_plan_type})
  print $BEN1-NAME               (0,+{col_sep},{col_plan_name})
  print $BEN1-PLAN-ID            (0,+{col_sep},{col_ben_plan_id})
  print $BEN1-PLAN-NAME          (0,+{col_sep},{col_ben_plan_name})
  print $BEN1-DATE               (0,+{col_sep},{col_eff_date})
  print $BEN703-REPLCMNT         (0,+{col_sep},{col_sal_repl})
  print $BEN1-FORMULA-ID         (0,+{col_sep},{col_formula})

 !Column Heading line 3
  print '='               (+1,1,{col_plan_type})              FILL
  print '='               (0,+{col_sep},{col_plan_name})      FILL
  print '='               (0,+{col_sep},{col_ben_plan_id})    FILL
  print '='               (0,+{col_sep},{col_ben_plan_name})  FILL
  print '='               (0,+{col_sep},{col_eff_date})       FILL
  print '='               (0,+{col_sep},{col_sal_repl})       FILL
  print '='               (0,+{col_sep},{col_formula})        FILL

end-HEADING

!***********************************************************************
!  PROC Process Main
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main

BEGIN-SELECT
PLAN_TYPE                  (+1,1,{col_plan_type})
  move 'PLAN_TYPE' to $FieldName
  move &Plan_Type to $FieldValue
  do Read-Translate-Table
  print $XlatShortName     (0,+{col_sep},{col_plan_name})
BENEFIT_PLAN               (0,+{col_sep},{col_ben_plan_id})
  move &benefit_plan to $benefit_plan
  move &plan_type    to $plan_type
  do Get-Benefit-Name
  print $BenefitName       (0,+{col_sep},{col_ben_plan_name})
EFFDT  &EffDt
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out               (0,+{col_sep},{col_eff_date})
SALARY_REPLACE_PCT
  do Format-Number(&salary_replace_pct, $out, 'b999.99')
  print $out               (0,+{col_sep},{col_sal_repl})
BN_FORMULA_ID
   print &BN_FORMULA_ID    (0,+{col_sep},{col_formula})

FROM PS_DISBLTY_PLN_TBL
ORDER BY PLAN_TYPE, BENEFIT_PLAN, EFFDT DESC

END-SELECT

END-PROCEDURE

!*****************************************************************
#Include 'getbennm.sqc'  !Get-Benefit-Name procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!*****************************************************************
