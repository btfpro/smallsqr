!***********************************************************************
!  Retroactive Payment Interest Earning Limit Report                   *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:53:50                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and page-size initialization

#define col_sep                  1   !Column Separator
#define col_empl_name           25   !Employee Name
#define col_national_id         11   !National ID
#define col_int_earns           23   !Total Interest

!***********************************************************************
!  PAY306RT:  Begin Report                                              *
!***********************************************************************
begin-report

  do Init-Report
  do Get-Run-Year
  do Process-Main
  do Stdapi-Term

end-report
!***********************************************************************
!  Init Report Procedure:  Do Report Time and others for the heading   *
!***********************************************************************
begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
  let $ReportID = 'PAY306RT' 
  let $ReportTitle  = 'Retroactive Payment Interest Earning Limit Report'
  let $first_page = 'Y'
  display  'Report ID: ' noline
  display $ReportTitle

end-procedure
!***********************************************************************
!  Heading:  Do Report Heading for Each New Page or New Company        *
!***********************************************************************
begin-heading 8

#Include 'stdhdgtr.sqc'

   DO Company_Name

   print 'Year Reported: ' (+2, 1)
   print $REPORTING-YEAR (0,22,10)

!Column Heading line 1
  print 'Employee Name'           (+2,1,{col_empl_name})
  print 'National ID'             (0,+{col_sep},{col_national_id})
  print 'Total Interest Earnings' (0,+{col_sep},{col_int_earns})

!Column Heading line 2
  print '='                 (+1,1,{col_empl_name})               Fill
  print '='                 (0,+{col_sep},{col_national_id})     Fill
  print '='                 (0,+{col_sep},{col_int_earns})       Fill
 
   move &a.company to $company

end-heading
!***********************************************************************
!  Get Run Year Procedure:  Get Report Year from Run Control Panel or  *
!                          from user input                             *
!***********************************************************************
begin-procedure Get-Run-Year

  if $Prcs_Process_Instance = ''
    do Process-Run-Year
  else
    do Select-Parameters
  end-if

  LET $YEAR-START = $REPORTING-YEAR
  LET $YEAR-END = $REPORTING-YEAR
  CONCAT '-01-01' WITH $YEAR-START
  CONCAT '-12-31' WITH $YEAR-END
  DO Convert-From-DTU-Date($YEAR-START,$SQL-YEAR-START)
  DO Convert-From-DTU-Date($YEAR-END,$SQL-YEAR-END)
  show 'Reporting Year ' $SQL-YEAR-START ' ' $SQL-YEAR-END

end-procedure
!***********************************************************************
!  Process Run Year Procedure:  Get Report Year from User Input        *
!***********************************************************************
begin-procedure Process-Run-Year

  move 0 to #Found
  while #Found = 0
    input $REPORTING-YEAR 'Enter Reporting Year'
    if $REPORTING-YEAR = ''
      stop
    else
      move 1 to #Found
    end-if
  end-while

end-procedure
!***********************************************************************
!  Main Procedure:  Read Data from Database tables and call print      *
!                   detail for each leave request of each employee.    *
!***********************************************************************
begin-procedure Process-Main

  LET #AGENCY_INT_TOTAL = 0
  LET #EMPL_INT_TOTAL = 0

begin-SELECT
A.COMPANY () ON-BREAK Print = Never Before = Create_new_page 
                                    After = Agency_Totals
                                    SAVE = $PREV_COMP 
A.EMPLID () ON-BREAK Print = Never After = Employee_Total
                                    save = $PREV_EMPLID
A.PAYGROUP,
B.NAME, 
A.AMOUNT_DIFF,
D.NATIONAL_ID

     ADD &A.AMOUNT_DIFF TO #EMPL_INT_TOTAL
     LET $EMPL_NAME = &B.NAME
     LET $NATIONAL_ID = &D.NATIONAL_ID

  #ifdef debugx
     SHOW &A.EMPLID ' ' #EMPL_INT_TOTAL 
  #endif

FROM PS_RETROPAY_EARNS A,
     PS_GVT_PERS_DATA B,
     PS_PAY_CALENDAR C, 
     PS_GVT_PERS_NID D

WHERE A.EMPLID    = B.EMPLID 
  AND A.EMPL_RCD = B.EMPL_RCD
  AND A.AMOUNT_DIFF > 0
  AND A.COMPANY = C.COMPANY
  AND A.PAYGROUP = C.PAYGROUP
  AND A.PAY_END_DT = C.PAY_END_DT
  AND B.EMPLID    = D.EMPLID
  AND B.EMPL_RCD = D.EMPL_RCD
  AND B.EFFDT     = D.EFFDT
  AND B.EFFSEQ    = D.EFFSEQ
  AND C.CHECK_DT >= $SQL-YEAR-START 
  AND C.CHECK_DT <= $SQL-YEAR-END
  AND A.RETROPAY_ERNCD IN
      (SELECT GVT_ERNCD_INTEREST
       FROM PS_PAYGROUP_TBL)
  AND B.EFFDT =
       (SELECT MAX(EFFDT)
        FROM PS_GVT_PERS_DATA
        WHERE EMPLID  = B.EMPLID
          AND EMPL_RCD = B.EMPL_RCD)        
  AND  B.EFFSEQ =
       (SELECT MAX(EFFSEQ)
        FROM PS_GVT_PERS_DATA
        WHERE  EMPLID  = A.EMPLID
          AND EMPL_RCD = B.EMPL_RCD
          AND EFFDT = B.EFFDT)        

 ORDER BY A.COMPANY,
          A.PAYGROUP, 
          A.EMPLID

end-SELECT

end-procedure
!***********************************************************************
! Employee Total Procedure:  Write total retropay interest for each employee 
!***********************************************************************
begin-procedure Employee_Total

     SHOW 'LIMIT AMOUNT = '#LIMIT_AMT

     IF #EMPL_INT_TOTAL > #LIMIT_AMT
        LET #AGENCY_INT_TOTAL = #AGENCY_INT_TOTAL + #EMPL_INT_TOTAL
        print $EMPL_NAME            (+1,1,{col_empl_name})
        print $NATIONAL_ID          (0,+2,{col_national_id})
        print #EMPL_INT_TOTAL       (0,+13,{col_int_earns}) edit 9999999.99
     END-IF
     MOVE 0 TO #EMPL_INT_TOTAL       
     
end-procedure
!***********************************************************************
!  Company Name Procedure:  Using Company Id to get Comapny Name       *
!***********************************************************************
begin-procedure Company_Name

Begin-Select
D.DESCR
  print 'Agency: '          (4,1)
  print $Company            (0,13)
  print &D.DESCR            (0,17)

FROM PS_COMPANY_TBL D
WHERE D.COMPANY   = $Company
  AND D.EFFDT =
       (SELECT MAX(EFFDT)
        FROM   PS_COMPANY_TBL
        WHERE  COMPANY  = D.COMPANY)
end-SELECT

end-procedure
!***********************************************************************
!  Agency Total Procedure:  Write accumulated total for each agency    *
!***********************************************************************
begin-procedure Agency_Totals

   move $PREV_COMP to $Company
   print 'Agency: '       (+3,1)
   print $PREV_COMP       (0,13)
   print 'Total'          (0,18)
   print #AGENCY_INT_TOTAL (0,48,) edit 999,999,999.99

   LET #AGENCY_INT_TOTAL = 0
   
end-procedure
!***********************************************************************
!  Create New Page Procedure: Create new page for each company (agency)*
!***********************************************************************
begin-procedure Create_new_page

  if $first_page = 'Y'
     move &A.COMPANY to $Company
     let $first_page = 'N'
  else
     New-Page
  end-if

end-procedure
!***********************************************************************
!  The following are include sqc files                                 *
!***********************************************************************
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#include 'pay306rt.sqc'  !Select Run Contrl Parameters
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'datemath.sqc'  !Do date manipulation
!*---------------------------------------------------------------------*
