! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
!***********************************************************************
! BATTIMES: PeopleTools 128 Trace for AE from table to report          *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************
! Program Descr: This program reports stored batch timings from tables *
!                PS_BAT_TIMINGS_LOG and PS_BAT_TIMINGS_DTL             *
! Parameters   : <dbname> D <process_instance> for detailed timings    *
!                report for specific run.                              *
! Parameters   : <dbname> S <run_control_id> for sumnmary timings      *
!                report for given run control id.                      *
!***********************************************************************

Begin-setup
#define REPORTID         'BATTIMES'
#include 'setenv.sqc'        ! Set environment
#include 'ptset01.sqc'       ! Init printer and page size
end-setup

!----------------------------------------------------------------------!
! Report:    BATTIMES: PS Batch Timings Report                         !
! Descr:     Print out PS_BAT_TIMINGS_LOG and PS_BAT_TIMINGS_DTL data  !
!----------------------------------------------------------------------!

begin-report
    do Initialize
    do Report-Main
    do Terminate
    do Commit-Transaction
end-report

!----------------------------------------------------------------------!
! Procedure: Initialize                                                !
!----------------------------------------------------------------------!

begin-procedure Initialize   ! Get Source Database Information

do Init-DateTime
do Init-Number
do Define-Prcs-Vars
do Get-Run-Control-Parms
do Get-Current-DateTime

move {REPORTID} to $ReportID
move 'PeopleSoft Stored Batch Timings Report '  to $ReportTitle
display $ReportTitle

let $t_run_cntl_id = ' '
let #t_process_instance = 0

begin-SELECT
BAT_REPORTTYPE        &t_choice
BAT_RUNCNTLID         &t_run_cntl_id
PRCSINSTANCE          &t_process_instance

 let $t_choice = &t_choice
 let $t_run_cntl_id = &t_run_cntl_id
 let #t_process_instance = &t_process_instance

 if $t_choice = 'D' or $t_choice = 'd'
    move 'PeopleSoft Stored Batch Timings Detail Report '  to $ReportTitle
    display 'Generating report for Process Instance ' NOLINE
    display #t_process_instance
 else
    move 'PeopleSoft Stored Batch Timings Summary Report '  to $ReportTitle
    display 'Generating report for Run Control ID ' NOLINE
    display $t_run_cntl_id
 end-if

FROM PS_BATRUNCNTL
WHERE RUN_CNTL_ID = $prcs_run_cntl_id
AND OPRID = $prcs_oprid

end-SELECT

end-procedure

!----------------------------------------------------------------------!
! Procedure: Terminate
!----------------------------------------------------------------------!


begin-procedure Terminate
    if #prcs_process_instance > 0
       do Update-Prcs-Run-Status
    end-if
    do Reset
end-procedure

!----------------------------------------------------------------------!
! Procedure: Report-Main                                               !
!----------------------------------------------------------------------!

begin-procedure Report-Main

print 'PeopleTools 8.1' (+1,1)
print 'Copyright (c) 1988-2009 PeopleSoft, Inc.' (+1,1)
print 'All Rights Reserved' (+1,1)
print 'All timings in seconds' (+1,1)

print  $ReportTitle (+2) center

begin-SELECT
RUN_CNTL_ID
PROCESS_INSTANCE
PROCESS_NAME
OPRID
TIME_ELAPSED/1000            &TIME_ELAPSED
TIME_IN_PC/1000              &TIME_IN_PC
TIME_IN_SQL/1000             &TIME_IN_SQL
100*(TIME_IN_SQL/TIME_ELAPSED) &PERC_IN_SQL
100*(TIME_IN_PC/TIME_ELAPSED)  &PERC_IN_PC
TRACE_LEVEL
{DateTimeOut-Prefix}BEGINDTTM{DateTimeOut-Suffix} &BEGINDTTM
{DateTimeOut-Prefix}ENDDTTM{DateTimeOut-Suffix}  &ENDDTTM
TRACE_LEVEL_SAM

 let $fld_run_control_id = &RUN_CNTL_ID
 let #fld_process_instance = &PROCESS_INSTANCE
 let $fld_process_name = &PROCESS_NAME
 let $fld_oprid = &OPRID
 let $fld_run_cntl_id = &RUN_CNTL_ID
 let #fld_time_elapsed = &TIME_ELAPSED
 let #fld_time_in_pc = &TIME_IN_PC
 let #fld_perc_in_pc = &PERC_IN_PC
 let #fld_time_in_sql = &TIME_IN_SQL
 let #fld_perc_in_sql = &PERC_IN_SQL
 let #fld_trace_level = &TRACE_LEVEL
 let $fld_begindttm = &BEGINDTTM
 let $fld_enddttm = &ENDDTTM
 let #fld_trace_level_sam = &TRACE_LEVEL_SAM


 print 'Process Name                    : ' (+2,1)
 print $fld_process_name (+0,+2)
 print 'Process Instance                : ' (+0,60)
 do Format-Number(#fld_process_instance , $out, '8888888888')
 print $out (+0,+2)
 print 'Run Control ID                  : ' (+1,1)
 print $fld_run_control_id (+0,+2)
 print 'Operator ID                     : ' (+0,60)
 print $fld_oprid (+0,+2)
 print 'Total run time                  : ' (+1,1)
 do Format-Number(#fld_time_elapsed , $out, '88888888.8')
 print $out (+0,+2)
 print 'Run Start                       : ' (+0,60)
 do Format-DateTime($fld_begindttm, $out, {DEFDATE}, 'time', '')
 print $out (+0,+2)
 do Format-DateTime($fld_enddttm, $out, {DEFDATE}, 'time', '')
 print 'Run Complete                    : ' (+1,60)
 print $out (+0,+2)
 print 'Total time in application SQL   : ' (+1,1)
 do Format-Number(#fld_time_in_sql , $out, '88888888.8')
 print $out (+0,+2)
 print 'Percent time in application SQL : ' (+0,60)
 do Format-Number(#fld_perc_in_sql , $out, '88888.8')
 print $out (+0,+2)
 print 'Total time in PeopleCode        : ' (+1,1)
 do Format-Number(#fld_time_in_pc , $out, '88888888.8')
 print $out (+0,+2)
 print 'Percent time in PeopleCode      : ' (+0,60)
 do Format-Number(#fld_perc_in_pc , $out, '88888.8')
 print $out (+0,+2)
 print 'AE Trace Setting                : ' (+1,1)
 do Format-Number(#fld_trace_level , $out, '8888888888')
 print $out (+0,+2)
 print 'PeopleTools SQL Trace Setting   : ' (+0,60)
 do Format-Number(#fld_trace_level_sam , $out, '8888888')
 print $out (+0,+2)

 if #t_process_instance <> 0
    print '*' (+1,1,121) fill
    do Report-Details
 end-if

 print '*' (+1,1,121) fill

FROM PS_BAT_TIMINGS_LOG
WHERE RUN_CNTL_ID = $t_run_cntl_id
OR PROCESS_INSTANCE =#t_process_instance
ORDER BY PROCESS_INSTANCE

end-SELECT


end-procedure

!----------------------------------------------------------------------!
! Procedure: Report-Details                                            !
!----------------------------------------------------------------------!

begin-procedure Report-Details

print ' C o m p i l e ' (+2,26)
print ' E x e c u t e ' (+0,55)
print '   F e t c h   ' (+0,85)
print 'Total     ' (+0,112)
print 'SQL Statement' (+1, 1)
print 'Count     ' (+0,22)
print 'Time      ' (+0,37)
print 'Count     ' (+0,52)
print 'Time      ' (+0,67)
print 'Count     ' (+0,82)
print 'Time      ' (+0,97)
print 'Time      ' (+0,112)
print '----------' (+1,22)
print '----------' (+0,37)
print '----------' (+0,52)
print '----------' (+0,67)
print '----------' (+0,82)
print '----------' (+0,97)
print '----------' (+0,112)

move ' ' to $bat_program_name_bak
move 0 to #bat_sql_time

begin-select

BAT_PROGRAM_NAME
DETAIL_ID
COMPILE_COUNT
COMPILE_TIME/1000  &COMPILE_TIME
EXECUTE_COUNT
EXECUTE_TIME/1000  &EXECUTE_TIME
FETCH_COUNT
FETCH_TIME/1000    &FETCH_TIME
(COMPILE_TIME+EXECUTE_TIME+FETCH_TIME)/1000    &LINE_TIME
BULK_INSERT
CURRENCY_ROUND
EXECUTE_EDITS

 let $fld_bat_program_name = &BAT_PROGRAM_NAME
 let $fld_detail_id = &DETAIL_ID
 let #fld_compile_count = &COMPILE_COUNT
 let #fld_compile_time = &COMPILE_TIME
 let #fld_execute_count = &EXECUTE_COUNT
 let #fld_execute_time = &EXECUTE_TIME
 let #fld_fetch_count = &FETCH_COUNT
 let #fld_fetch_time = &FETCH_TIME
 let #fld_line_time = &LINE_TIME
 let $fld_bulk_insert = &BULK_INSERT
 let $fld_currency_round = &CURRENCY_ROUND
 let $fld_execute_edits = &EXECUTE_EDITS

 if $fld_bat_program_name != $fld_bat_program_name_bak
    if #bat_sql_time > 0
       print '----------'  (+1,112)
       do Format-Number(#bat_sql_time , $out, '99999999.9')
       print $out (+1,112)
       move 0 to #bat_sql_time
    end-if
    move $fld_bat_program_name to $fld_bat_program_name_bak
    print $fld_bat_program_name  (+1,1)
    print '------------'  (+1,1)
 end-if

 let #bat_sql_time = #bat_sql_time + #fld_line_time

 if $fld_bulk_insert    <> 'N'
   print '*Bulk Insert:' (+1, 1)
   let $fld_detail_id = '*'||$fld_detail_id
 end-if
 if $fld_currency_round <> 'N'
   print '*Curr Round:'  (+1, 1)
   let $fld_detail_id = '*'||$fld_detail_id
 end-if
 if $fld_execute_edits  <> 'N'
   print '*Exec Edit:'  (+1, 1)
   let $fld_detail_id = '*'||$fld_detail_id
 end-if

 print $fld_detail_id (+1,1)

 if $fld_bat_program_name  != 'PeopleCode  '
    do Format-Number(#fld_compile_count , $out, '9999999999')
    print $out (+0, 22)
    do Format-Number(#fld_compile_time , $out, '99999999.9')
    print $out (+0, 37)
 end-if

 do Format-Number(#fld_execute_count , $out, '9999999999')
 print $out (+0, 52)
 do Format-Number(#fld_execute_time , $out, '99999999.9')
 print $out (+0, 67)
 do Format-Number(#fld_fetch_count , $out, '9999999999')
 print $out (+0, 82)
 do Format-Number(#fld_fetch_time , $out, '99999999.9')
 print $out (+0, 97)
 do Format-Number(#fld_line_time , $out, '99999999.9')
 print $out (+0, 112)

FROM PS_BAT_TIMINGS_DTL
WHERE PROCESS_INSTANCE = #fld_process_instance
AND BAT_DTL_TYPE <> 'P'
ORDER BY  BAT_PROGRAM_NAME, DETAIL_ID

end-select

if #bat_sql_time > 0
   print '----------'  (+1,112)
   do Format-Number(#bat_sql_time , $out, '99999999.9')
   print $out (+1,112)
   move 0 to #bat_sql_time
else
   print 'No SQL Statements Timed' (+2,0) center
end-if

print '*' (+1,1,121) fill
print 'Call' (+2,22)
print 'Non-SQL' (+0,37)
print 'SQL' (+0,52)
print 'Total' (+0,67)
print 'PeopleCode Action'  (+1, 1)
print 'Count' (+0,22)
print 'Time' (+0,37)
print 'Time' (+0,52)
print 'Time' (+0,67)
print '----------'  (+1,22)
print '----------'  (+0,37)
print '----------'  (+0,52)
print '----------'  (+0,67)

move ' ' to $bat_program_name_bak
move 0 to #bat_program_time

begin-select

BAT_PROGRAM_NAME        &BAT_PROGRAM_NAME2
DETAIL_ID               &DETAIL_ID2
PEOPLECODECOUNT
PEOPLECODESQLTIME/1000  &PEOPLECODESQLTIME
PEOPLECODETIME/1000     &PEOPLECODETIME

 let $fld_bat_program_name = &BAT_PROGRAM_NAME2
 let $fld_detail_id = &DETAIL_ID2
 let #fld_pcode_count = &PEOPLECODECOUNT
 let #fld_sql_time = &PEOPLECODESQLTIME
 let #fld_pcode_time = &PEOPLECODETIME

 if $fld_bat_program_name != $fld_bat_program_name_bak
    if #bat_program_time > 0
       print '----------'  (+1,37)
       print '----------'  (+0,52)
       print '----------'  (+0,67)
       do Format-Number(#bat_program_time , $out, '99999999.9')
       print $out (+1,37)
       do Format-Number(#bat_sql_time , $out, '99999999.9')
       print $out (+0,52)
       let #bat_total_time = #bat_program_time + #bat_sql_time
       do Format-Number(#bat_total_time , $out, '99999999.9')
       print $out (+0,67)
       move 0 to #bat_program_time
       move 0 to #bat_sql_time
    end-if
    move $fld_bat_program_name to $fld_bat_program_name_bak
    print $fld_bat_program_name  (+2,1)
    print '------------'  (+1,1)
 end-if

 let #bat_program_time = #bat_program_time + #fld_pcode_time
 let #bat_sql_time = #bat_sql_time + #fld_sql_time
 print $fld_detail_id (+1,1)
 do Format-Number(#fld_pcode_count , $out, '9999999999')
 print $out (+0, 22)
 do Format-Number(#fld_pcode_time , $out, '99999999.9')
 print $out (+0, 37)
 do Format-Number(#fld_sql_time , $out, '99999999.9')
 print $out (+0, 52)
 let #fld_total_time = #fld_pcode_time + #fld_sql_time
 do Format-Number(#fld_total_time , $out, '99999999.9')
 print $out (+0, 67)

FROM PS_BAT_TIMINGS_DTL
WHERE PROCESS_INSTANCE = #fld_process_instance
AND BAT_DTL_TYPE = 'P'
ORDER BY  BAT_PROGRAM_NAME, DETAIL_ID

end-select

if #bat_program_time > 0

   print '----------'  (+1,37)
   print '----------'  (+0,52)
   print '----------'  (+0,67)
   do Format-Number(#bat_program_time , $out, '99999999.9')
   print $out (+1,37)
   do Format-Number(#bat_sql_time , $out, '99999999.9')
   print $out (+0,52)
   let #bat_total_time = #bat_program_time + #bat_sql_time
   do Format-Number(#bat_total_time , $out, '99999999.9')
   print $out (+0,67)

   print '*' (+1,1,121) fill
   print ' E x e c u t e ' (+2,55)
   print 'PeopleCode BuiltIn/Method' (+1, 1)
   print 'Count     ' (+0,52)
   print 'Time      ' (+0,67)
   print '----------' (+1,52)
   print '----------' (+0,67)

begin-select

DETAIL_ID               &DETAIL_ID3
EXECUTE_COUNT           &EXECUTE_COUNT3
EXECUTE_TIME/1000       &EXCUTE_TIME3       

 let $fld_detail_id = &DETAIL_ID3
 let #fld_execute_count = &EXECUTE_COUNT3
 let #fld_execute_time = &EXCUTE_TIME3        

 print $fld_detail_id (+1,1)
 do Format-Number(#fld_execute_count , $out, '9999999999')
 print $out (+0, 52)
 do Format-Number(#fld_execute_time , $out, '99999999.9')
 print $out (+0, 67)

FROM PS_BAT_TIMINGS_FN
WHERE PROCESS_INSTANCE = #fld_process_instance
ORDER BY DETAIL_ID 

end-select

else
   print 'No PeopleCode Actions Timed' (+2,37)
end-if

let #bat_program_time = 0

end-procedure

!----------------------------------------------------------------------!

#include 'sqlerr.sqc'
#include 'reset.sqc'            ! Reset printer
#include 'curdttim.sqc'         ! Current Date and Time
#include 'chgampus.sqc'         ! Change Ampersand to Underscore
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#include 'prcsapi.sqc'   !Update Process Request API
#include 'prcsdef.sqc'   !Update Process Request variable declaration
#include 'tranctrl.sqc'     ! Transaction control

!----------------------------------------------------------------------!
