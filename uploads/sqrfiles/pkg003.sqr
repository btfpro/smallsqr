!***********************************************************************
!  PKG003:      Salary Packaging - Models Due for Review               *
!                                                                      *
!  Description: List of Salary Packages due for review                 *
!               within specified period                                *
!***********************************************************************

!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:15:14:13                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

begin-setup

#include 'setenv.sqc'    !Set environment
#Include 'ptset02.sqc'  ! Landscape Printer and page-size initialization

end-setup

begin-report
  do Init-DateTime
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-heading 9
  #include 'stdhdg01.sqc'
  
  print $Head     (+1,70)
  print $Select   (0,+2)
  if $Head1 > ' '
     print $Head1     (+1,70)
     print $Select1   (0,+2)
  end-if

  print 'From Date: ' (+1,1) 
  print $FromDate     (0,0)
  print 'To Date:   ' (+1,1) 
  print $ThruDate     (0,0)


  print $Header1_1   (+2,1,15)
  print $Header1_2   (0,+2,30)
  print $Header1_3   (0,+2,10)
  print $Header1_4   (0,+2,18)
  print $Header1_5   (0,+2,10)
  print $Header1_6   (0,+2,20)

  print $Header2_1   (+1,1,15)
  print $Header2_2   (0,+2,30)
  print $Header2_3   (0,+2,10)
  print $Header2_4   (0,+2,18)
  print $Header2_5   (0,+2,10)
  print $Header2_6   (0,+2,20)
  

end-heading

!**************************************************************************
!
! Procedure: Init-Report
!
! Description: Initialise header and run control variables.
!
!**************************************************************************
begin-procedure Init-Report

  move 'PKG003' to $ReportID
  move 'Salary Packaging - Packages Due for Review' to
       $ReportTitle
  display $ReportTitle

  do Stdapi-Init
  display '$prcs_process_instance: ' noline
  display $prcs_process_instance

  if $prcs_process_instance = ''
    ! no SQRW prompt
  else
    do Select-Parameters
  end-if

  do Format-DateTime($RC.From_Dt,$FromDate,{DEFDMY},'','')
  do Format-DateTime($RC.Thru_Dt,$ThruDate,{DEFDMY},'','')

  let $Head1   = ' '
  let $Select1 = ' '
  If $RC.Pkg_ProcessType > ' '
     evaluate $RC.Pkg_ProcessType
     when = 'D'
       let $Where_Para1 = 'JOB.DEPTID = ''' || $RC.Department || ''' '
       let $Head   = 'Department: '
       let $Select = $RC.Department
       break
     when = 'C'
       let $Where_Para1 = 'JOB.COMPANY = ''' || $RC.Company || ''' '
       let $Head   = 'Company:  '
       let $Select = $RC.Company
       break
     when = 'P'
       let $Where_Para1 = 'JOB.COMPANY = ''' || $RC.Company ||
                       ''' AND JOB.PAYGROUP = ''' || $RC.Paygroup || ''' '
       let $Head    = 'Company:  '
       let $Select  = $RC.Company
       let $Head1   = 'Paygroup: '
       let $Select1 = $RC.Paygroup
      break
    end-evaluate
  else
     evaluate $RC.Pkg_ProcessTp_GP
     when = 'D'
       let $Where_Para1 = 'JOB.DEPTID = ''' || $RC.Department || ''' '
       let $Head    = 'Department: '
       let $Select  = $RC.Department
       break
     when = 'P'
       let $Where_Para1 = 'JOB.GP_PAYGROUP = ''' || $RC.GP_Paygroup || ''' '  
       let $Head   = 'Paygroup: '
       let $Select = $RC.GP_Paygroup                      
      break
    end-evaluate
  end-if



end-procedure

!**************************************************************************
!
! Procedure: Process-Main
!
! Description: Main processing section
!
!**************************************************************************
begin-procedure Process-Main

  ! do Get-PSOPTIONS
  
   let $Header1_1 = 'Employee ID'
   let $Header1_2 = 'Name'
   let $Header1_3 = 'Empl Rcd#'
   let $Header1_4 = 'Package Eff Date'
   let $Header1_5 = 'Model No#'
   let $Header1_6 = 'Package Review Date'
   let $Header2_1 = '-----------'
   let $Header2_2 = '----'
   let $Header2_3 = '---------'
   let $Header2_4 = '------------------'
   let $Header2_5 = '---------'
   let $Header2_6 = '-------------------'


   do Print-Models


end-procedure


!**************************************************************************
!
! Procedure: Print-Models
!
! Description: This procedure will select and print Package Models due for
!              review within the identified dates
!
!**************************************************************************
begin-procedure Print-Models

  let $sql-statement = 'SELECT PKG_MDL_TBL'

  begin-SELECT                                              
PMT.EMPLID                             ( +1, 1)
PER.NAME                               ( 0, 17)  
PMT.EMPL_RCD                           ( 0, 51) edit '99'
PMT.EFFDT
  do Format-DateTime(&PMT.EffDt,$Pkg_EffDt,{DEFDMY},'','')
  print $Pkg_EffDt                     ( 0, 64)
PMT.EFFSEQ                             ( 0, 84)
PMT.REVIEW_DT
  do Format-DateTime(&PMT.Review_Dt,$Review,{DEFDMY},'','')
  print $Review                        ( 0, 97)
 
  FROM PS_PKG_MDL_TBL PMT, PS_PERSON_NAME PER, 
  PS_JOB JOB, PS_JOB_AUS JJR
  WHERE PER.EMPLID      = JOB.EMPLID
     AND PMT.REVIEW_DT BETWEEN $RC.FROM_DT AND $RC.THRU_DT
     AND PMT.PKG_STATUS = 'C'
     AND PER.EMPLID     = PMT.EMPLID
     AND JOB.EMPLID     = PMT.EMPLID
     AND JOB.EMPL_RCD   = PMT.EMPL_RCD
     AND JJR.EMPLID     = JOB.EMPLID
     AND JJR.EMPL_RCD   = JOB.EMPL_RCD
     AND JJR.SALARY_PACKAGED = 'Y'
     AND JOB.EFFDT =
        (SELECT MAX(JOB1.EFFDT) FROM PS_JOB JOB1
        WHERE JOB.EMPLID    = JOB1.EMPLID
          AND JOB.EMPL_RCD  = JOB1.EMPL_RCD
          AND JOB1.EFFDT   <= $AsOfToday)
    AND JOB.EFFSEQ =
        (SELECT MAX(JOB2.EFFSEQ) FROM PS_JOB JOB2
        WHERE JOB.EMPLID    = JOB2.EMPLID
          AND JOB.EMPL_RCD  = JOB2.EMPL_RCD
          AND JOB.EFFDT     = JOB2.EFFDT)
    AND JJR.EFFDT       = JOB.EFFDT
    AND JJR.EFFSEQ      = JOB.EFFSEQ
    AND [$Where_Para1]
  ORDER BY PMT.EMPLID

 end-SELECT

 print '** End of Report ** ' (+1, 70)

end-procedure


 ! #Include 'getpsopt.sqc'  !Get-PSOPTIONS procedure
#Include 'pkgrnctl.sqc'  !Select-Parameters procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'stdapi.sqc'    !Routines to Update Run Status

