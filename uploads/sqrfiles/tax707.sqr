!***********************************************************************
!  TAX707: Print contents of State Tax Rate Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:06:27:46                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup31.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'TAX707' to $ReportID
  move 'State Tax Rate Table' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
end-procedure

begin-HEADING 7
  #Include 'stdhdg01.sqc'

  print '      Eff   '                                   (+2,1)

  print 'State Date            Low Gross       '         (+1,1)
  print 'Low Tax      Rate        Credit    Tax Status'  ()

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

STATE                 (+1,2,2)   on-break level=1 skiplines=2
EFFDT
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out          (0,+3,12)  on-break level=2
LOW_GROSS
  do Format-Number(&Low_Gross, $out, '9999999.99')
  print $out          (0,+3,12)
LOW_TAX
  do Format-Number(&Low_Tax, $out, '999999.99')
  print $out          (0,+3,10)
TAX_RT
  do Format-Number(&Tax_Rt, $out, '0.999999mi')
  print $out          (0,+3)
CREDIT_AMOUNT
  do Format-Number(&Credit_Amount, $out, '99999.99mi')
  print $out          (0,+3)
TAX_STATUS
  move 'TAX_STATUS'        to $FieldName
  move &Tax_Status         to $FieldValue
  do Read-Translate-Table
  print $XlatLongName       (0,+3,30)  on-break level=3

FROM PS_STATE_TAXRT_TBL

ORDER BY STATE, EFFDT DESC, TAX_STATUS, LOW_GROSS DESC

END-SELECT
END-PROCEDURE

#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
