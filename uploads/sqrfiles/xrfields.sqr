! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
! XRFAPFL: Cross-Reference -- Field Listing                            *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

#define Field_Type_Sign 3
#define C_FieldName     1
#define C_Type          20
#define C_Length        25
#define C_Format        33
#define C_Label_Id      41
#define C_LongName      60
#define C_LongName_End  91
#define C_ShortName     93
#define C_ShortName_End 108
#define C_Default_Label 110

begin-setup
#include 'ptset01.sqc'         ! Init printer and page size
end-setup

begin-report
    do Initialize
    do Report
    do Terminate
    do Commit-Transaction
end-report

begin-procedure Initialize            ! Get Source Database Information
    do Init-DateTime
    do Init-Number
    do Define-Prcs-Vars
    do Get-Run-Control-Parms
    move 'XRFIELDS' to $ReportID
    move 'Cross Reference -- Field Listing' to $ReportTitle
#ifdef debugx
    SHOW 'Report Title: ' $ReportTitle
#endif
    do Get-Current-DateTime
    do Create_Code_Arrays
end-procedure

begin-procedure Terminate
    if #prcs_process_instance > 0
        do Update-Prcs-Run-Status
    end-if
    do Reset
end-procedure

begin-heading 8
   #include 'stdhdg01.sqc'
   print 'Field Name'                      (7,{C_FieldName})
   print 'Type'                            (7,{C_Type})
   print 'Length'                          (7,{C_Length})
   print 'Format'                          (7,{C_Format})
   print 'Label Id'                        (7,{C_Label_Id})
   print 'Long Name'                       (7,{C_LongName})
   print 'Short Name'                      (7,{C_ShortName})
   print 'Default'                         (7,{C_Default_Label})
   print '------------------'              (8,{C_FieldName})
   print '----'                            (8,{C_Type})
   print '------'                          (8,{C_Length})
   print '------'                          (8,{C_Format})
   print '------------------'              (8,{C_Label_Id})
   print '------------------------------'  (8,{C_LongName})
   print '----------'                      (8,{C_ShortName})
   print '-------'                         (8,{C_Default_Label})
end-heading

!***********************************************************************
! Function:    Create_Code_Arrays                                      *
!                                                                      *
! Description: Load translation values into the arrays used to         *
!              interpret codes for reporting.                          *
!                                                                      *
!***********************************************************************
begin-procedure Create_Code_Arrays
   create-array  NAME=Type_Codes  SIZE=10  FIELD=Type_Name:char
   put 'Char'  into Type_Codes(0)
   put 'Long'  into Type_Codes(1)
   put 'Nbr '  into Type_Codes(2)
   put 'Sign'  into Type_Codes(3)
   put 'Date'  into Type_Codes(4)
   put 'Time'  into Type_Codes(5)
   put 'Dttm'  into Type_Codes(6)
   put 'SRec'  into Type_Codes(7)
   put 'Img '  into Type_Codes(8)
   put 'Vers'  into Type_Codes(9)

   create-array  NAME=Format_Codes  SIZE=17  FIELD=Format_Name:char
   put '    '  into Format_Codes(0)
   put 'Name'  into Format_Codes(1)
   put 'Phone' into Format_Codes(2)
   put 'ZipPC' into Format_Codes(3)
   put 'SSN'   into Format_Codes(4)
   put 'Rtn'   into Format_Codes(5)
   put 'Mixed' into Format_Codes(6)
   put 'Cent'  into Format_Codes(7)
   put 'Num'   into Format_Codes(8)
   put 'SIN'   into Format_Codes(9)
   put 'PhnIn' into Format_Codes(10)
   put 'ZPCIn' into Format_Codes(11)
   put 'Scnds' into Format_Codes(12)
   put 'McroS' into Format_Codes(13)
   put 'CtSec' into Format_Codes(14)
   put 'CmSec' into Format_Codes(15)
   put 'Upper' into Format_Codes(16)
end-procedure

begin-procedure Report          ! Main report processing
begin-SELECT
A.FIELDNAME                      (+1,{C_FIELDNAME})
   move &A.FIELDNAME to $FieldNames
A.FIELDTYPE
   move &A.FieldType to #FieldType
   GET $Type_Name from TYPE_CODES(#FIELDTYPE) TYPE_NAME
   print $Type_Name            (0,{C_Type})
A.FORMAT
   move &A.Format to #Format
   GET $Format_Name from FORMAT_CODES(#FORMAT) FORMAT_NAME
   if #Format = 0 and #FieldType = 0
      move 'Upper' to $Format_Name
   end-if
   print $Format_Name          (0,{C_Format})
A.LENGTH
   move &A.Length to #Length
A.DECIMALPOS
   move &A.DecimalPos to #DecimalPos
   move #Length     to #Prt_Length
   if &A.FieldType = {Field_Type_Sign}
       SUBTRACT 1          from #Prt_Length
   end-if
   if #DecimalPos > 0
       SUBTRACT 1          from #Prt_Length
       SUBTRACT #DecimalPos from #Prt_Length
       if #DecimalPos > 9
       MULTIPLY 100    TIMES #Prt_Length
       ADD #DecimalPos to #Prt_Length
       DIVIDE 100      INTO #Prt_Length
       do Format-Number(#Prt_Length, $out, '999.99')
       print $out   (0,{C_Length})
       else
       MULTIPLY 10     TIMES #Prt_Length
       ADD #DecimalPos to #Prt_Length
       DIVIDE 10       INTO #Prt_Length
       do Format-Number(#Prt_Length, $out, '999.9')
       print $out   (0,{C_Length})
       end-if
   else
       do Format-Number(#Prt_Length, $out, '999')
       print $out   (0,{C_Length})
   end-if
!B.LONGNAME                   (0,{C_LONGNAME})
!B.SHORTNAME                  (0,{C_SHORTNAME})
   let #row = 0
   do GetLongShortName($FieldNames)
FROM PSDBFIELD A  !, PSDBFLDLABL B
! WHERE A.FIELDNAME = B.FIELDNAME
ORDER BY A.FIELDNAME
end-SELECT
end-procedure


begin-procedure GetLongShortName($FieldNames)

begin-SELECT

B.LABEL_ID              (+#Row,{C_Label_Id})
B.LONGNAME              (0,{C_LONGNAME},{C_LongName_End})
B.SHORTNAME             (0,{C_SHORTNAME}, {C_SHORTNAME_End})
B.DEFAULT_LABEL

    if &B.Default_Label = 1
        print '*'       (0,{C_Default_Label})
    end-if

    let #row = 1

FROM PSDBFLDLABL B
WHERE B.FIELDNAME=$FieldNames
!ORDER BY A.FIELDNAME
end-SELECT

end-procedure

#include 'reset.sqc'     ! Reset printer
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#include 'prcsapi.sqc'   !Update Process Request API
