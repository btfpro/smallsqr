!***********************************************************************
! MXSTP005.SQR   STPS DC-2B Training and Development Reistration       *
!                           Application.                               *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:13:49:59                                 *
!       $Release:  HR9                                                 *
!      $Revision:  101                                                 *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'   !Set environment

Begin-Setup
#include 'setupdb.sqc'
 Declare-Printer   THIS_REPORT
  Type=HPLASERJET
  Font=5
  Point-Size=14
  Pitch=17
  Symbol-Set=19U     ! ASCII symbol set
 End-Declare
 
 Declare-Layout THIS_REPORT
  Orientation=Portrait
  Top-Margin= .35
  Left-Margin=.15
  Max-Lines=82
  Max-Columns=130
  Char-Width=6.5   !4.3    ! 7.2
  Line-Height=9     ! 72/printer_point-size
 End-Declare
 
 Declare-Report THIS_REPORT
  Printer-Type=HPLASERJET
  LayOut=THIS_REPORT
 End-Declare

End-Setup

begin-report
  move '1' to $ReportDateType
  move '1' to $Reportyear4
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Get-Current-DateTime
!  do Declare-Printer
  do Init-Report
  do Select-Parameters
  do Report
  date-time () hh:mi:ss &timeEnded
  display 'Report Ended: ' noline
  display &timeEnded
  do Stdapi-Term
  !do Reset
end-report

begin-procedure Init-Report
  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan
  move 'MXSTP005' to $ReportID
  let $ReportTitle = 'STPS DC-2B Solicitud de Registro Sistema de Capacitacion'
  display $ReportTitle
end-procedure Init-Report

begin-heading 1
  position  (1)
end-heading

!begin-procedure Declare-Printer
!  let #fontstd  = 5
!  let #sizestd  = 6
!  let #fontsm   = 5
!  let #sizesm   = 8
!  let #fontmed  = 5
!  let #sizemed  = 10
!  let #fontgr   = 5
!  let #sizegr   = 12
!  let #fontxgr  = 5
!  let #sizexgr  = 14
!  declare printer
!    type          = HPLASERJET
!    orientation   = portrait
!    top-margin    = 0.35
!    left-margin   = 0.0   !0.5
!    symbol-set    = 19U
!    font          = 5
!    font-style    = fixed
!    point-size    = 14 !12
!    line-size     = 9
!end-procedure Declare-Printer

begin-procedure Report
  do Get-Report-Data
  do Get-Estabid-Data
  do Get-Training-Data
  do Get-Course-Data
end-procedure Report

begin-procedure Select-Parameters
begin-select
RC.COMPANY
RC.ESTABID
RC.TRN_PROGRAM
  let $Company    = &RC.COMPANY
  let $ESTABID  = rtrim(&RC.ESTABID,' ')
  let $Train_Prog = &RC.TRN_PROGRAM

FROM PS_RUN_STPS_MEX RC
WHERE RC.OPRID       = $Prcs_OprID
  AND RC.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-select
end-procedure Select-Parameters

begin-procedure Get-Report-Data
begin-select
C.DESCR
CM.REP_NAME_MEX
  let $Comp_Descr = &C.DESCR
  let $Rep_Name  = &CM.REP_NAME_MEX
  do Get-Comp-Descr-Lang

FROM PS_COMPANY_TBL C,
     PS_COMPANY_TBL_MEX CM

WHERE C.COMPANY = $Company
and C.COMPANY = CM.COMPANY
and C.EFFDT = (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL
               WHERE COMPANY = C.COMPANY
               AND EFFDT <= $AsofToday)
and CM.EFFDT = C.EFFDT
end-select
end-procedure Get-Report-Data

begin-procedure Get-Comp-Descr-Lang
begin-SELECT
CTL.DESCR
  if rtrim(&CTL.Descr,' ') <> ''
     let $Comp_Descr  = rtrim(&CTL.Descr,' ')
  end-if
FROM  PS_COMPNY_TBL_LANG CTL
WHERE CTL.COMPANY     = $Company
  AND CTL.LANGUAGE_CD = 'ESP'
  AND CTL.EFFDT       =
     (SELECT MAX(EFFDT)
      FROM   PS_COMPNY_TBL_LANG
      WHERE  COMPANY     = $Company
        AND  LANGUAGE_CD = CTL.LANGUAGE_CD
        AND  EFFDT      <= $AsofToday)
end-SELECT
end-procedure Get-Comp-Descr-Lang

begin-procedure Get-Estabid-Data
begin-select
IMS.ESTABID
IMS.ADDRESS1
IMS.ADDRESS2
IMS.ADDRESS3
IMS.ADDRESS4
IMS.CITY
IMS.POSTAL
IMS.STATE
  let $Address2 = rtrim(&IMS.ADDRESS1,' ') || ' ' || rtrim(&IMS.ADDRESS2,' ')
  let $Address3 = &IMS.ADDRESS3
  let $Municip  = &IMS.ADDRESS4
  let $City     = rtrim(&IMS.CITY,' ')
  let $Postal   = substr(&IMS.POSTAL,1,5)
  let $Postal1  = substr($POSTAL,1,1)
  let $Postal2  = substr($POSTAL,2,1)
  let $Postal3  = substr($POSTAL,3,1)
  let $Postal4  = substr($POSTAL,4,1)
  let $Postal5  = substr($POSTAL,5,1)
  let $State    = &IMS.STATE
  do Get-State-Name
  do Get-Phones
from PS_ESTAB_TBL IMS
Where IMS.ESTABID = $ESTABID
and IMS.EFFDT = (SELECT MAX(EFFDT) FROM PS_ESTAB_TBL
               WHERE ESTABID = IMS.ESTABID
               AND EFF_STATUS = 'A'
               AND EFFDT <= $AsofToday)
end-select
end-procedure Get-Estabid-Data

begin-procedure Get-State-Name
begin-select
SN.DESCR
  let $State_Desc = &SN.DESCR

FROM PS_STATE_NAMES_TBL SN
WHERE SN.COUNTRY = 'MEX'
  AND SN.STATE = $State
end-select
end-procedure Get-State-Name

begin-procedure Get-Phones
begin-select
PHONE
  let $phone = $Phone || '  ' || rtrim(&PHONE,' ')

FROM PS_COMP_PHONE_TBL
WHERE COMPANY = $Company
end-select
end-procedure Get-Phones

begin-procedure Get-Training-Data
begin-select
T.DESCR
  let $Prog_Desc = &T.DESCR
FROM PS_TRN_PROGRM_TBL T
WHERE T.TRN_PROGRAM = $Train_Prog
and T.COURSE_STATUS = 'A'
end-select

begin-select
COUNT(*) &Course_Total
  move &Course_Total to #Page_Total
FROM PS_TRN_PROGRM_CRSE
WHERE TRN_PROGRAM = $Train_Prog
end-select
end-procedure Get-Training-Data

begin-procedure Get-Course-Data
begin-select
TP.COURSE
TP.COURSE_SEQUENCE
CT.DESCR
  let $Course = &TP.COURSE
  let $Course_Descr = &CT.DESCR
  do Get-Course-Content
  do Print-Format
  new-page

FROM PS_TRN_PROGRM_CRSE TP,
     PS_COURSE_TBL CT
WHERE TP.TRN_PROGRAM = $Train_Prog
AND TP.COURSE = CT.COURSE
ORDER BY TP.COURSE_SEQUENCE
end-select
end-procedure Get-Course-Data

begin-procedure Get-Course-Content
begin-select
CD.DESCRLONG
  let $Course_Content = &CD.DESCRLONG

FROM PS_COURSE_DESCR CD
WHERE CD.COURSE = $Course
AND CD.COURSE_DESCR_TYPE = 'CC'
AND CD.EFFDT = (SELECT MAX(EFFDT) FROM PS_COURSE_DESCR
                WHERE COURSE = $Course
                AND COURSE_DESCR_TYPE = 'CC'
                AND EFFDT <= $AsOfToday)
end-select
end-procedure Get-Course-Content

begin-procedure Print-Format
print $Comp_Descr           (15,3)
print $Address2             (18,3)
print $Address3             (19,3)
print $City                 (22,3)
print $Postal1              (22,30)
print $Postal2              (22,33)
print $Postal3              (22,36)
print $Postal4              (22,39)
print $Postal5              (22,42)
print $Municip              (22,48,28)
print $State_Desc           (25,3)
print $Phone                (25,39)
print $Prog_Desc            (28,3)

print #page-count           (32,6)  edit 08
print #Page_Total           (32,12) edit 08

print $Course               (39,3)
print $Course_Descr         (39,26) bold
print $Course_Content       (+2,26) wrap 55 15

print $Rep_Name             (68,18)
let $City_End = $City || ', ' || $State
print $City_End             (74,20)
let $City_End = ''
do Convert-To-DTU-Date($AsOfToday,$AsOfToday1)
let $Today_Y1 = substr($AsOfToday1,3,1)
let $Today_Y2 = substr($AsOfToday1,4,1)
let $Today_M1 = substr($AsOfToday1,6,1)
let $Today_M2 = substr($AsOfToday1,7,1)
let $Today_D1 = substr($AsOfToday1,9,1)
let $Today_D2 = substr($AsOfToday1,10,1)
print $Today_Y1             (74,51)
print $Today_Y2             (74,54)
print $Today_M1             (74,57)
print $Today_M2             (74,60)
print $Today_D1             (74,63)
print $Today_D2             (74,66)
end-procedure Print-Format

!****************************************************************
#Include 'tranctrl.sqc'  !Common Transaction Control Procedures
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#include 'datemath.sqc'  !Date math routines
#include 'askcalyr.sqc'  !Ask-Calendar-Year
