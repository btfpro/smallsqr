!***********************************************************************
!  TAX002AZ:  Qtrly State Employee Wage Listing - ARIZONA              *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:44:19                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Get USA variables

begin-setup

#include 'prt002.sqc'  !Printer and page-size initialization

end-setup

begin-report
  do Init-Report

  do Process-Main

  if #Employees_On_Page > 0 and #Employees_On_Page < 25
    do Page-Complete
  end-if

  do Stdapi-Term
end-report

begin-procedure Init-Report
  display 'Quarterly Employee Wage Listing - Arizona'

  do Init-DateTime
  do Init-Number

  do Get-Tax-Reporting-Run-Controls

  move $PeriodEndDate to $AsOfDate  !for included procedures

  do Stdapi-Init

  do Get-Calendar-Year-Id

end-procedure

begin-heading 6
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    move &Employer_ID_SUT to $ID
    do Calc-Check-Digit
    print &Employer_ID_SUT         (+1,48)
    print $CheckDigit              (+0,64)
  else
    move &Employer_ID_SWT to $ID
    do Calc-Check-Digit
    print &Employer_ID_SWT         (+1,48)
    print $CheckDigit              (+0,64)
  end-if
  do Format-DateTime($PeriodEndDate, $out, {DEFCMP}, '', '')
  let $Month = substr($out,5,2)
  let $Day   = substr($out,7,2)
  let $Year  = substr($out,3,2)
  print $Month                     (+2,48)
  print $Day                       (+0,53)
  print $Year                      (+0,58)
  do Format-Number(#PageTotal, $PageTotal, '99999999.99')
  print $PageTotal                 (59,60)
end-heading

begin-procedure Process-Main

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT

  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
C.COMPANY () on-break print=never before=Company-Change
C.STATE
C.EMPLID
C.TAX_CLASS
C.LOCALITY
C.NLGRS_QTD

D1.NATIONAL_ID
PN.NAME

  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_NAMES PN, PS_PERS_NID D1, PS_COMPANY_TBL A
WHERE C.EMPLID            = D1.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND PN.EMPLID           = C.EMPLID
#ifdef TAXTEST
  AND C.STATE             = 'CA'
#else
  AND C.STATE             = 'AZ'
#end-if
  AND C.LOCALITY          = ' '
  AND C.TAX_CLASS         = 'U'                 ! Unemployment
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = &TX.Balance_Year
  AND C.BALANCE_QTR       = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD    =
      (SELECT MAX(C1.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C1
       WHERE  C1.EMPLID        = C.EMPLID
         AND  C1.COMPANY       = C.COMPANY
         AND C1.BALANCE_ID     = C.BALANCE_ID
         AND C1.BALANCE_YEAR   = C.BALANCE_YEAR
         AND C1.BALANCE_QTR    = C.BALANCE_QTR
         AND  C1.STATE         = C.STATE
         AND  C1.LOCALITY      = C.LOCALITY
         AND  C1.TAX_CLASS     = C.TAX_CLASS)
  AND C.NLGRS_QTD     > 0
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
  AND PN.NAME_TYPE = 'PRI'
  AND PN.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PN.EMPLID
         AND  PX.NAME_TYPE = PN.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfDate)
ORDER BY C.COMPANY, D1.NATIONAL_ID, C.STATE, C.LOCALITY, C.TAX_CLASS
end-SELECT
end-procedure

begin-procedure Process-Tax-Balance
  move &D1.NATIONAL_ID to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
    print 'Applied For'            (+2,7)
  else
    print &D1.NATIONAL_ID          (+2,7)
  end-if

  print &PN.Name                    (0,30,22)

  move &C.NLGrs_QTD to #NLGrs_QTD
  do Format-Number(#NLGrs_QTD, $NLGrs_QTD, '99999999.99')
  print $NLGrs_QTD                 (0,60)

  add #NLGrs_QTD to #PageTotal
  add 1 to #Employees_On_Page

  if #employees_on_page = 25
    do Page-Complete
  end-if
end-procedure

begin-procedure Page-Complete
  add 1 to #PAGE_NUM
  new-page
  move 0 to #PageTotal
  move 0 to #Employees_On_Page
end-procedure

begin-procedure Company-Change
  if #Employees_On_Page < 25 and #Employees_On_Page > 0
    do Page-Complete
  end-if
  move 0 to #PAGE_NUM
  move &C.Company to $Company
  do Get-Company-Data
#ifdef TAXTEST
  move 'CA' to $State
#else
  move 'AZ' to $State
#endif
  do Get-State-Tax-Data             !Employer ID
end-procedure

begin-procedure Calc-Check-Digit
  move 0 to #holdCheckDigit

  extract $digit from $ID 0 1
  move $digit to #digit
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if

  extract $digit from $ID 1 1
  move $digit to #digit
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if

  extract $digit from $ID 2 1
  move $digit to #digit
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if

  extract $digit from $ID 3 1
  move $digit to #digit
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if

  extract $digit from $ID 4 1
  move $digit to #digit
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if

  extract $digit from $ID 5 1
  move $digit to #digit
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if

  extract $digit from $ID 6 1
  move $digit to #digit
  add #digit to #holdCheckDigit
  if #holdCheckDigit > 9
    subtract 10 from #holdCheckDigit
  end-if

  move #holdCheckDigit to #tempDigit
  move 10 to #CheckDigit
  subtract #tempDigit from #CheckDigit
  do Format-Number(#CheckDigit, $CheckDigit, '9')
end-procedure

#Include 'rotname2.sqc'  !Rotate-Name procedure
#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
