!********************************************************
!  OHS009:   Incident Detail                            *
!********************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:11:10                                 *
!       $Release:  HR9                                                 *
!      $Revision:  101                                                 *
!                                                                      *
!***********************************************************************
! Modification History                                                 *
!                                                                      *
! Date        Incident     Description                                 *
!                                                                      *
! 14-Apr-2005 1213540000 - Report fields re-aligned to address issues  *
!                          on HP laser printing.                       *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
Begin-Setup
#Include 'ptpsl177.sqc'  !Printer and page-size initialization
End-Setup
!64000000
!#Define ColR 80  !Column # referenced by Standard Headings
#Define ColR 130  !Column # referenced by Standard Headings

!64000000 all ALTER-PRINTER commented out

!*************************************************************
BEGIN-REPORT
!*************************************************************

   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Init-Report
   do Process-Main
   do Reset
   do Stdapi-Term

END-REPORT


!*************************************************************
BEGIN-HEADING  4
!*************************************************************

ALTER-PRINTER  FONT = 4    POINT-SIZE = 10             !1213540000
!---Commented for fix 592653 Starts
!#include 'stdhdg01.sqc'
!---Commented for fix 592653 Ends

!---Added for Resolution 592653 Starts

    print '            '    (1,1)
    print $Ohs009_STDHDG_CO_NM   ()          center
    print $Ohs009_STDHDG_REP_ID    (+1,1)
    print $ReportID         (, +2)
    print $ReportTitle      ()          center
    let #RptCol = {ColR} - 2
    print $Ohs009_STDHDG_PAGE_NO      (0,#RptCol)
    print #page-count       (0,+5)      edit 88888888  !1213540000
    print $Ohs009_STDHDG_RUN_DT     (+1,#RptCol)
    print $ReportDate       (, +5)                     !1213540000
    print $Ohs009_STDHDG_RUN_TM     (+1,#RptCol)
    print $ReportTime       (, +5)                     !1213540000

!---Added for resolution 592653 Ends

END-HEADING

!******************************
Begin-Setup
!******************************

#define Laser Postscript

Declare-Printer OSH009_printer
    Type=HPLASERJET         ! Types are POSTSCRIPT, HPLASERJET,
    Font=4
    Point-Size=10

end-declare

Declare-Layout OHS009_layout
    Orientation=Landscape
    Top-Margin= .4

    !64000000  increasing the size of the margin
    !Left-Margin=.60         ! or LINEPRINTER
    Left-Margin=.75         ! or LINEPRINTER
end-declare

Declare-Report OHS009
  layout=OHS009_layout
end-declare

End-Setup

!*************************************************************
BEGIN-PROCEDURE Init-Report
!*************************************************************

  move 'OHS009' to $ReportID

  do Stdapi-Init
  do Get-Report-Language
  move $Ohs009_ReportTitle to $ReportTitle
  display $ReportTitle

  if $prcs_process_instance = ''
     do Ask-OHS-Parm
  else
     do Select-Parameters
  end-if

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Values
!*************************************************************

   do Get-OHS-Parm

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-OHS-Parm
!*************************************************************

   let $Incident = RTRIM(&RUN_CNTL_OHS.Incident_Nbr, ' ')

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Ask-OHS-Parm
!*************************************************************

   input $Incident 'Incident Number'

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Report-Translation
!*************************************************************
 !64000000
  !Should be ok to comment because the report should only produce one output file.
  !Use-report OHS009

  Do Init_Report_Translation($ReportId, $Language_Cd)
  Do Get_Field_Information ('OHS009','REPORT_TITLE',       $Ohs009_ReportTitle,        #CW)
  Do Get_Field_Information ('OHS009','INCIDENT_NBR',       $Ohs009_Incident_nbr,       #CW)
  Do Get_Field_Information ('OHS009','INCIDENT_TYPE',      $Ohs009_Incident_type,      #CW)
  Do Get_Field_Information ('OHS009','LOCATION',           $Ohs009_Location,           #CW)
  Do Get_Field_Information ('OHS009','INCIDENT_DT',        $Ohs009_Incident_dt,        #CW)
  Do Get_Field_Information ('OHS009','INCIDENT_TIME',      $Ohs009_Incident_time,      #CW)
  Do Get_Field_Information ('OHS009','DT_REPORTED',        $Ohs009_Dt_reported,        #CW)
  Do Get_Field_Information ('OHS009','TIME_REPORTED',      $Ohs009_Time_reported,      #CW)
  Do Get_Field_Information ('OHS009','DT_RECORDED',        $Ohs009_Dt_recorded,        #CW)
  Do Get_Field_Information ('OHS009','TIME_RECORDED',      $Ohs009_Time_recorded,      #CW)
  Do Get_Field_Information ('OHS009','REPORTED_TO',        $Ohs009_Reported_to,        #CW)
  Do Get_Field_Information ('OHS009','REPORTED_BY_EMPL',   $Ohs009_Reported_by_empl,   #CW)
  Do Get_Field_Information ('OHS009','RESULT_INJURY_ILL',  $Ohs009_Result_injury_ill,  #CW)
  Do Get_Field_Information ('OHS009','EMPLOYER_PREMISES',  $Ohs009_Employer_premises,  #CW)
  Do Get_Field_Information ('OHS009','RECURRENCE',         $Ohs009_Recurrence,         #CW)
  Do Get_Field_Information ('OHS009','INVESTIGATED',       $Ohs009_Investigated,       #CW)
  Do Get_Field_Information ('OHS009','HAZARD_ID',          $Ohs009_Hazard_id,          #CW)
  Do Get_Field_Information ('OHS009','INCIDENT_CAUSE',     $Ohs009_Incident_cause ,    #CW)
  Do Get_Field_Information ('OHS009','ACTION_SEQUENCE',    $Ohs009_Action_sequence,    #CW)
  Do Get_Field_Information ('OHS009','CORRECTIVE_PREVENT', $Ohs009_Corrective_prevent, #CW)
  Do Get_Field_Information ('OHS009','ACTION_CATEGORY',    $Ohs009_Action_category,    #CW)
  Do Get_Field_Information ('OHS009','RESPONSIBLE_ID',     $Ohs009_Responsible_id,     #CW)
  Do Get_Field_Information ('OHS009','EST_COMPLETION_DT',  $Ohs009_Est_completion_dt,  #CW)
  Do Get_Field_Information ('OHS009','ACT_COMPLETION_DT',  $Ohs009_Act_completion_dt,  #CW)
  Do Get_Field_Information ('OHS009','ACTION_STATUS',      $Ohs009_Action_status,      #CW)
  Do Get_Field_Information ('OHS009','INCIDENT_PERSONS',   $Ohs009_Incident_persons,   #CW)
  Do Get_Field_Information ('OHS009','WITNESS_ID',         $Ohs009_Witness_id,         #CW)
  Do Get_Field_Information ('OHS009','WITNESS_NAME',       $Ohs009_Witness_name,       #CW)
  Do Get_Field_Information ('OHS009','CONFIRM_STATEMENT',  $Ohs009_Confirm_statement,  #CW)
  Do Get_Field_Information ('OHS009','INVOLVED_ID',        $Ohs009_Involved_id,        #CW)
  Do Get_Field_Information ('OHS009','INVOLVED_NAME',      $Ohs009_Involved_name,      #CW)
  Do Get_Field_Information ('OHS009','ACT_COMPLETION_DT',  $Ohs009_Act_completion_dt,  #CW)
  Do Get_Field_Information ('OHS009','DT_FILED_USA',       $Ohs009_Date_filed_usa,     #CW)
  Do Get_Field_Information ('OHS009','INJURY_TREATMENT',   $Ohs009_Injury_treatment,   #CW)
  Do Get_Field_Information ('OHS009','ILLNESS_TYPE',       $Ohs009_Illness_type,       #CW)
  Do Get_Field_Information ('OHS009','ILLNESS_RESULT',     $Ohs009_Illness_result,     #CW)
  Do Get_Field_Information ('OHS009','AWAY_FROM_WORKDAYS', $Ohs009_Away_from_workdays, #CW)
  Do Get_Field_Information ('OHS009','RESTRICTD_WORKDAYS', $Ohs009_Restrictd_workdays, #CW)
  Do Get_Field_Information ('OHS009','INJURY_NATURE_CD',   $Ohs009_Injury_nature_cd,   #CW)
  Do Get_Field_Information ('OHS009','PRIMARY_INJURY',     $Ohs009_Primary_injury,     #CW)
  Do Get_Field_Information ('OHS009','INJURY_SOURCE_CD',   $Ohs009_Injury_source_cd,   #CW)
  Do Get_Field_Information ('OHS009','ACCIDENT_TYPE_CD',   $Ohs009_Accident_type,      #CW)
  Do Get_Field_Information ('OHS009','UNSAFE_ACT_CD',      $Ohs009_Unsafe_act_cd,      #CW)
  Do Get_Field_Information ('OHS009','BODY_PART_CD',       $Ohs009_Body_part_cd,       #CW)
  Do Get_Field_Information ('OHS009','PHYSICIAN_NAME',     $Ohs009_Physician_name,     #CW)
  Do Get_Field_Information ('OHS009','HOSPITAL',           $Ohs009_Hospital,           #CW)
  Do Get_Field_Information ('OHS009','PHONE',              $Ohs009_Phone,              #CW)
  Do Get_Field_Information ('OHS009','CARE_TYPE',          $Ohs009_Care_type,          #CW)
  Do Get_Field_Information ('OHS009','DIAGNOSIS_CD',       $Ohs009_Diagnosis,          #CW)
  Do Get_Field_Information ('OHS009','INJURED_PERSONS',    $Ohs009_Injuries,           #CW)

!---Added for Resolution 592653 Starts

  Do Get_Field_Information ('OHS009','STDHDG_CO_NM',       $Ohs009_STDHDG_CO_NM,       #CW)
  Do Get_Field_Information ('OHS009','STDHDG_END_REP',     $stdhdg_end_rep,            #CW)
  Do Get_Field_Information ('OHS009','STDHDG_PAGE_NO',     $Ohs009_STDHDG_PAGE_NO,     #CW)
  Do Get_Field_Information ('OHS009','STDHDG_REP_ID',      $Ohs009_STDHDG_REP_ID,      #CW)
  Do Get_Field_Information ('OHS009','STDHDG_RUN_DT',      $Ohs009_STDHDG_RUN_DT,      #CW)
  Do Get_Field_Information ('OHS009','STDHDG_RUN_TM',      $Ohs009_STDHDG_RUN_TM,      #CW)

!---Added for Resolution 592653 Ends

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Process-Main
!*************************************************************

BEGIN-SELECT

A.INCIDENT_NBR
A.INCIDENT_TYPE

    move 'INCIDENT_TYPE'  to $FieldName
    move &A.incident_type to $FieldValue
    do Read-Translate-Table
    move $XlatShortName  to  $incident_type

A.LOCATION

A.LOCATION_NAME

A.INCIDENT_DT

   do Format-DateTime(&A.INCIDENT_DT, $incident_dt, {DEFMDY},'','')

{DateTimeOut-Prefix}A.INCIDENT_TIME{DateTimeOut-Suffix} &A.INCIDENT_TIME

   if length(&A.INCIDENT_TIME) > 15
      move '1' to $Year4
      move '1' to $TLZero
      do Format-DateTime(&A.INCIDENT_TIME, $inc_tm,{DEFMDY},'time','')
      let $inc_tm = {ps-substr}($inc_tm,12,20)
      do Format-Times($inc_tm, $inc_tm)
   else
      do Format-Times(&A.INCIDENT_TIME, $inc_tm)
   end-if

   extract $incident_time from $inc_tm 0 8

A.DT_REPORTED

   do Format-DateTime(&A.DT_REPORTED, $dt_reported, {DEFMDY},'','')

{DateTimeOut-Prefix}A.TIME_REPORTED{DateTimeOut-Suffix} &A.TIME_REPORTED

   if length(&A.TIME_REPORTED) > 15
      move '1' to $Year4
      move '1' to $TLZero
      do Format-DateTime(&A.TIME_REPORTED, $rpt_tm,{DEFMDY},'time','')
      let $rpt_tm = {ps-substr}($rpt_tm,12,20)
      do Format-Times($rpt_tm, $rpt_tm)
   else
      do Format-Times(&A.TIME_REPORTED, $rpt_tm)
   end-if

   extract $time_reported from $rpt_tm 0 8

A.DT_RECORDED

   do Format-DateTime(&A.DT_RECORDED, $dt_recorded, {DEFMDY},'','')

{DateTimeOut-Prefix}A.TIME_RECORDED{DateTimeOut-Suffix} &A.TIME_RECORDED

   if length(&A.TIME_RECORDED) > 15
      move '1' to $Year4
      move '1' to $TLZero
      do Format-DateTime(&A.TIME_RECORDED, $rcd_tm,{DEFMDY},'time','')
      let $rcd_tm = {ps-substr}($rcd_tm,12,20)
      do Format-Times($rcd_tm, $rcd_tm)
   else
      do Format-Times(&A.TIME_RECORDED, $rcd_tm)
   end-if

   extract $time_recorded from $rcd_tm 0 8

A.REPORTED_TO

  Do Get-Emplid-Name(&a.reported_to, $reported_to_name)

A.REPORTED_BY_EMPL
A.REPORTED_BY_NONEMP

     if not isnull(rtrim(&a.reported_by_empl,' '))
        move &a.reported_by_empl to $reported_by_id
        do Get-Emplid-Name($reported_by_id,$reported_by_name)
     else
        move &a.reported_by_nonemp to $reported_by_id
        do Get-Nonempl-Name($reported_by_id,$reported_by_name)
     end-if

A.RESULT_INJURY_ILL
A.EMPLOYER_PREMISES
A.RECURRENCE
A.INVESTIGATED

   move &a.incident_nbr      to $Incident_nbr
   move &a.location          to $location
   move &a.location_name     to $location_name
   move &a.reported_to       to $reported_to
   move &a.result_injury_ill to $result_injury_ill
   move &a.recurrence        to $recurrence
   move &a.employer_premises to $employer_premises
   move &a.investigated      to $investigated

   do Print-Incident-Data
   do Get-Incident-Actn

   !ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
   print $Ohs009_Incident_persons         (+3,1)     bold
   !ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
   do Get-Incident-Witness

   !ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
   print $Ohs009_Injuries                 (+3,1)     bold
   !ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
   do Get-Injury-Data

FROM PS_INCIDENT_DATA A
WHERE A.INCIDENT_NBR = $Incident

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE   Get-Incident-Actn
!*************************************************************

BEGIN-SELECT

IA.HAZARD_ID
IA.INCIDENT_CAUSE
IA.ACTION_SEQUENCE
IA.CORRECTIVE_PREVENT
IA.ACTION_CATEGORY
IA.ACTION_STATUS

    move 'ACTION_STATUS'  to $FieldName
    move &ia.action_status to $FieldValue
    do Read-Translate-Table
    move $XlatShortName  to  $action_status

IA.RESPONSIBLE_ID

    move &ia.responsible_id   to  $responsible_id
    do Get-Emplid-Name($responsible_id,$responsible_name)

IA.EST_COMPLETION_DT

    do Format-DateTime(&IA.EST_COMPLETION_DT, $est_completion_dt, {DEFMDY},'','')

IA.ACT_COMPLETION_DT

    do Format-DateTime(&IA.ACT_COMPLETION_DT, $act_completion_dt, {DEFMDY},'','')

    move &ia.hazard_id          to $hazard_id
    move &ia.incident_cause     to $incident_cause
    move &ia.action_sequence    to #action_sequence
    move &ia.corrective_prevent to $corrective_prevent
    move &ia.action_category    to $action_category

    do Print-Incident-Action
    let $responsible_name = ' '

FROM PS_INC_ACTION_C_VW IA
WHERE IA.INCIDENT_NBR = &a.incident_nbr

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Get-Incident-Witness
!*************************************************************

BEGIN-SELECT

IP.EMPLID
IP.NON_EMPLOYEE_ID
IP.CONFIRM_STATEMENT

   if not isnull(rtrim(&ip.emplid,' '))
      move &ip.emplid to $witness_id
      do Get-Emplid-Name($witness_id,$wname)
   else
      move &ip.non_employee_id to $witness_id
      do Get-Nonempl-Name($witness_id,$wname)
   end-if

   extract $witness_name from $wname 0 30
   move &ip.confirm_statement to $confirm_statement

   do Print-Incident-Witness

FROM PS_INCIDENT_PEOPLE IP
WHERE IP.INCIDENT_NBR = &a.incident_nbr
AND IP.INCIDENT_ROLE = 'IW'

END-SELECT


BEGIN-SELECT

IC.IP_EMPLID_CAN
IC.IP_NON_EMPLID_CAN

   if not isnull(rtrim(&ic.ip_emplid_can,' '))
      move &ic.ip_emplid_can to $witness_id
      do Get-Emplid-Name($witness_id,$wname)
   else
      move &ic.ip_non_emplid_can to $witness_id
      do Get-Nonempl-Name($witness_id,$wname)
   end-if

   extract $witness_name from $wname 0 30
   move '' to $confirm_statement
   do Print-Incident-Witness

FROM PS_INJ_PEOPLE_CAN IC
WHERE IC.INCIDENT_NBR = &a.incident_nbr
AND IC.INJURY_ROLE_CAN = 'IW'

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE   Get-Injury-Data
!*************************************************************

BEGIN-SELECT

B.EMPLID
B.EMPL_RCD
B.NON_EMPLOYEE_ID

    if not isnull(rtrim(&b.emplid,' '))
       let $involved_id = &b.emplid
       let #empl_rcd = &b.EMPL_RCD
       do Get-Emplid-Name($involved_id,$inv_name)
    else
       let $involved_id = &b.non_employee_id
       do Get-Nonempl-Name($involved_id,$inv_name)
    end-if

    extract $involved_name from $inv_name 0 30

B.DT_REPORTED

    do Format-DateTime(&B.DT_REPORTED, $inj_dt_reported, {DEFMDY},'','')

{DateTimeOut-Prefix}B.TM_REPORTED{DateTimeOut-Suffix} &B.TM_REPORTED

   if length(&B.TM_REPORTED) > 15
      move '1' to $Year4
      move '1' to $TLZero
      do Format-DateTime(&B.TM_REPORTED, $rptd_tm,{DEFMDY},'time','')
      let $rptd_tm = {ps-substr}($rptd_tm,12,20)
      do Format-Times($rptd_tm, $rptd_tm)
   else
      do Format-Times(&B.TM_REPORTED, $rptd_tm)
   end-if

    extract $inj_tm_reported from $rptd_tm 0 8

B.DT_FILED_USA

    do Format-DateTime(&B.DT_FILED_USA, $dt_filed_usa, {DEFMDY},'','')

B.DT_FILED_GER

    do Format-DateTime(&B.DT_FILED_GER, $dt_filed_ger, {DEFMDY},'','')

    if isnull($dt_filed_usa)
       let $dt_filed = $dt_filed_ger
    else
       let $dt_filed = $dt_filed_usa
    end-if

B.INJURY_TREATMENT

    move 'INJURY_TREATMENT'  to $FieldName
    move &b.injury_treatment to $FieldValue
    do Read-Translate-Table
    move $XlatShortName  to  $injury_treatment

B.ILLNESS_TYPE

    move 'ILLNESS_TYPE'  to $FieldName
    move &b.illness_type to $FieldValue
    do Read-Translate-Table
    move $XlatShortName  to $illness_type

B.ILLNESS_RESULT

    move 'ILLNESS_RESULT'  to $FieldName
    move &b.illness_result to $FieldValue
    do Read-Translate-Table
    move $XlatShortName  to $illness_result

B.AWAY_FROM_WORKDAYS
B.RESTRICTD_WORKDAYS

       move &b.away_from_workdays  to #away_from_workdays

  !64000000
       !move &b.restrictd_workdays  to #restricted_workdays
       move &b.restrictd_workdays  to #restrictd_workdays

       let #count = #count + 1

       Do Print-Injury-Data
       Do Get-Nature-Injury
       Do Get-Source-Injury
       Do Get-Acc-Type
       Do Get-Unsafe-Act
       Do Get-Body-Part
       Do Get-Hospital

FROM PS_INJURY_ILL B
WHERE B.INCIDENT_NBR = $Incident

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Nature-Injury
!*************************************************************

BEGIN-SELECT

NOI.INJURY_NATURE_CD
NOI.PRIMARY_INJURY

    move &noi.injury_nature_cd to $injury_nature_cd
    move &noi.primary_injury   to $primary_injury

    do Get-Nature-Inj-Descr
    do Print-Nature-Injury

FROM PS_INJURY_ILL_NOI NOI
WHERE NOI.INCIDENT_NBR = &a.incident_nbr
AND ((NOI.EMPLID = &b.emplid
AND NOI.EMPL_RCD = &b.EMPL_RCD AND NOI.NON_EMPLOYEE_ID = ' ')
OR (NOI.EMPLID = ' ' AND NOI.EMPL_RCD = 0
AND NOI.NON_EMPLOYEE_ID = &b.non_employee_id))

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Source-Injury
!*************************************************************

BEGIN-SELECT

SOI.INJURY_SOURCE_CD
SOI.PRIMARY_INJURY

    move &soi.injury_source_cd to $injury_source_cd
    move &soi.primary_injury   to $primary_injury

    do Get-Source-Inj-Descr
    do Print-Source-Injury

FROM PS_INJURY_ILL_SOI SOI
WHERE SOI.INCIDENT_NBR = &a.incident_nbr
AND ((SOI.EMPLID = &b.emplid
AND SOI.EMPL_RCD = &b.EMPL_RCD AND SOI.NON_EMPLOYEE_ID = ' ')
OR (SOI.EMPLID = ' ' AND SOI.EMPL_RCD = 0
AND SOI.NON_EMPLOYEE_ID = &b.non_employee_id))

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Acc-Type
!*************************************************************

BEGIN-SELECT

ACC.ACCIDENT_TYPE_CD
ACC.PRIMARY_INJURY

   move &acc.accident_type_cd to $accident_type_cd
   move &acc.primary_injury   to $primary_injury

   do Get-Acc-Type-Descr
   do Print-Acc-Type

FROM PS_INJURY_ILL_ACC ACC
WHERE ACC.INCIDENT_NBR = &a.incident_nbr
AND ((ACC.EMPLID = &b.emplid
AND ACC.EMPL_RCD = &b.EMPL_RCD AND ACC.NON_EMPLOYEE_ID = ' ')
OR (ACC.EMPLID = ' ' AND ACC.EMPL_RCD = 0
AND ACC.NON_EMPLOYEE_ID = &b.non_employee_id))

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Unsafe-Act
!*************************************************************

BEGIN-SELECT

ACT.UNSAFE_ACT_CD
ACT.PRIMARY_INJURY

   move &act.unsafe_act_cd    to $unsafe_act_cd
   move &act.primary_injury   to $primary_injury

   do Get-Unsafe-Act-Descr
   do Print-Unsafe-Act

FROM PS_INJURY_ILL_ACT ACT
WHERE ACT.INCIDENT_NBR = &a.incident_nbr
AND ((ACT.EMPLID = &b.emplid
AND ACT.EMPL_RCD = &b.EMPL_RCD AND ACT.NON_EMPLOYEE_ID = ' ')
OR (ACT.EMPLID = ' ' AND ACT.EMPL_RCD = 0
AND ACT.NON_EMPLOYEE_ID = &b.non_employee_id))

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Body-Part
!*************************************************************

BEGIN-SELECT

BP.BODY_PART_CD
BP.PRIMARY_INJURY

   move &bp.body_part_cd    to $body_part_cd
   move &bp.primary_injury  to $primary_injury

   do Get-Body-Part-Descr
   do Print-Body-Part

FROM PS_INJURY_ILL_BP BP
WHERE BP.INCIDENT_NBR = &a.incident_nbr
AND ((BP.EMPLID = &b.emplid
AND BP.EMPL_RCD = &b.EMPL_RCD AND BP.NON_EMPLOYEE_ID = ' ')
OR (BP.EMPLID = ' ' AND BP.EMPL_RCD = 0
AND BP.NON_EMPLOYEE_ID = &b.non_employee_id))

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Hospital
!*************************************************************

BEGIN-SELECT

H.PHYSICIAN_NAME

    move &h.physician_name  to $pname
    extract $physician_name from $pname 0 30

H.HOSPITAL
H.CARE_TYPE

    move 'CARE_TYPE'  to $FieldName
    move &h.care_type to $FieldValue
    do Read-Translate-Table
    move $XlatLongName  to  $care_type

H.PHONE

   let $physician_name =  {ps-substr}(&h.physician_name,1,30)
   move &h.hospital         to $hospital
   move &h.phone            to $phone

   Do Print-Hospital
   Do Get-Diagnosis

FROM PS_INJURY_ILL_ADDR H
WHERE H.INCIDENT_NBR = &a.incident_nbr
AND ((H.EMPLID = &b.emplid
AND H.EMPL_RCD = &b.EMPL_RCD AND H.NON_EMPLOYEE_ID = ' ')
OR (H.EMPLID = ' ' AND H.EMPL_RCD = 0
AND H.NON_EMPLOYEE_ID = &b.non_employee_id))

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Diagnosis
!*************************************************************

BEGIN-SELECT

D.DIAGNOSIS_CD

   move &d.diagnosis_cd to $diagnosis_cd

   do Get-Diagnosis-Descr
   do Print-Diagnosis

FROM PS_INJURY_ILL_DIAG D
WHERE D.INCIDENT_NBR = &a.incident_nbr
AND   D.CARE_TYPE = &H.CARE_TYPE
AND ((D.EMPLID = &b.emplid
AND D.EMPL_RCD = &b.EMPL_RCD AND D.NON_EMPLOYEE_ID = ' ')
OR (D.EMPLID = ' ' AND D.EMPL_RCD = 0
AND D.NON_EMPLOYEE_ID = &b.non_employee_id))

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Emplid-Name($emplid,:$empl_name)
!*************************************************************

BEGIN-SELECT
!**********************Begin SQR NAME DISPLAY CHANGE****************
  !P.NAME
P.NAME_DISPLAY
  !move &p.name to $empl_name
   move &p.name_display to $empl_name
!**********************End SQR NAME DISPLAY CHANGE****************
FROM PS_PERSON_NAME P
WHERE P.EMPLID    = $emplid
 AND P.NAME_TYPE  = 'PRI'

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE Get-Nonempl-Name($non_emplid,:$non_empl_name)
!*************************************************************

BEGIN-SELECT


!**********************Begin SQR NAME DISPLAY CHANGE****************
  !N.NAME
N.NAME_DISPLAY
   !move &n.name to $non_empl_name
    move &n.name_display to $non_empl_name
!**********************End SQR NAME DISPLAY CHANGE****************


FROM PS_NE_PERSONAL_DTA N
WHERE N.NON_EMPLOYEE_ID = $non_emplid

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Get-Nature-Inj-Descr
!*************************************************************

BEGIN-SELECT

ND.DESCR100

   let $inj_nature_descr = {ps-substr}(&nd.descr100,1,50)

FROM PS_INJURY_NATR_TBL  ND
WHERE ND.INJURY_NATURE_CD = $injury_nature_cd
  AND ND.EFFDT = (SELECT MAX(ND1.EFFDT)
                   FROM PS_INJURY_NATR_TBL ND1
                   WHERE ND1.INJURY_NATURE_CD = ND.INJURY_NATURE_CD
                   AND ND1.EFFDT <= $AsOfDate)
  AND ND.EFF_STATUS = 'A'

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Get-Source-Inj-Descr
!*************************************************************

BEGIN-SELECT

SD.DESCR100

   let $inj_source_descr = {ps-substr}(&sd.descr100,1,50)

FROM PS_INJURY_SRCE_TBL  SD
WHERE SD.INJURY_SOURCE_CD = $injury_source_cd
AND SD.EFFDT = (SELECT MAX(SD1.EFFDT)
                   FROM PS_INJURY_SRCE_TBL  SD1
                   WHERE SD1.INJURY_SOURCE_CD = SD.INJURY_SOURCE_CD
                   AND SD1.EFFDT <= $AsOfDate)
AND SD.EFF_STATUS = 'A'

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Get-Acc-Type-Descr
!*************************************************************

BEGIN-SELECT

ACD.DESCR100

   let $acc_type_descr = {ps-substr}(&acd.descr100,1,50)

FROM PS_ACCDNT_TYPE_TBL  ACD
WHERE ACD.ACCIDENT_TYPE_CD = $accident_type_cd
AND ACD.EFFDT = (SELECT MAX(ACD1.EFFDT)
                   FROM PS_ACCDNT_TYPE_TBL  ACD1
                   WHERE ACD1.ACCIDENT_TYPE_CD = ACD.ACCIDENT_TYPE_CD
                   AND ACD1.EFFDT <= $AsOfDate)
AND ACD.EFF_STATUS = 'A'

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Get-Unsafe-Act-Descr
!*************************************************************

BEGIN-SELECT

UD.DESCR100

   let $unsafe_act_descr = {ps-substr}(&ud.descr100,1,50)

FROM PS_UNSAFE_ACT_TBL  UD
WHERE UD.UNSAFE_ACT_CD = $unsafe_act_cd
AND UD.EFFDT = (SELECT MAX(UD1.EFFDT)
                   FROM PS_UNSAFE_ACT_TBL  UD1
                   WHERE UD1.UNSAFE_ACT_CD = UD.UNSAFE_ACT_CD
                   AND UD1.EFFDT <= $AsOfDate)
AND UD.EFF_STATUS = 'A'


END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Get-Body-Part-Descr
!*************************************************************

BEGIN-SELECT

BD.DESCR100

   let $body_part_descr = {ps-substr}(&bd.descr100,1,50)

FROM PS_BODY_PART_TBL  BD
WHERE BD.BODY_PART_CD = $body_part_cd
AND BD.EFFDT = (SELECT MAX(BD1.EFFDT)
                   FROM PS_BODY_PART_TBL  BD1
                   WHERE BD1.BODY_PART_CD = BD.BODY_PART_CD
                   AND BD1.EFFDT <= $AsOfDate)
AND BD.EFF_STATUS = 'A'

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Get-Diagnosis-Descr
!*************************************************************

BEGIN-SELECT

DD.DESCR

   move &dd.descr  to  $diagnosis_descr

FROM PS_DIAGNOSIS_TBL  DD
WHERE DD.DIAGNOSIS_CD = $diagnosis_cd
AND DD.EFFDT = (SELECT MAX(DD1.EFFDT)
                   FROM PS_DIAGNOSIS_TBL  DD1
                   WHERE DD1.DIAGNOSIS_CD = DD.DIAGNOSIS_CD
                   AND DD1.EFFDT <= $AsOfDate)
AND DD.EFF_STATUS = 'A'

END-SELECT

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Print-Incident-Data
!*************************************************************
ALTER-PRINTER  FONT = 4    POINT-SIZE = 10             !1213540000
print $Ohs009_Incident_nbr          (+4,1)         bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $Incident_nbr                 (0,+6)             !1213540000 - Incident no.

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Incident_type         (0,+9)        bold !1213540000 - Incident type
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $incident_type                (0,+5)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Location              (0,+7)        bold !1213540000 - Location ID and Name
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $location                     (0,+3)             !1213540000
print $location_name                (0,+4)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Incident_dt           (+2,1)        bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $incident_dt                  (0,+8)             !1213540000 - Incident date/time
print $incident_time                (0,+3)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Dt_reported           (+2,1)        bold !1213540000 - Reported date/time
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $dt_reported                  (0,+8)             !1213540000
print $time_reported                (0,+3)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Dt_recorded           (+2,1)        bold !1213540000 - Recorded date/time
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $dt_recorded                  (0,+8)             !1213540000
print $time_recorded                (0,+3)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
!print $Ohs009_Incident_time        (+2,1)        bold !1213540000
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
!print $incident_time               (0,+3)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
!print $Ohs009_Time_reported        (0,+6)        bold !1213540000
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
!print $time_reported               (0,+2)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
!print $Ohs009_Time_recorded        (0,+6)        bold !1213540000
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
!print $time_recorded               (0,+2)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Reported_to           (+2,1)        bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $reported_to                  (0,+24)            !1213540000 - Reported to
print $reported_to_name             (0,+4)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Reported_by_empl      (+2,1)        bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $reported_by_id               (0,+11)            !1213540000 - Reported by
print $reported_by_name             (0,+4)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Result_injury_ill     (+2,1)        bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $result_injury_ill            (0,+3)             !1213540000 - Resulted in Injury

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Employer_premises     (0,+8)       bold  !1213540000 - Occurred on Company Property
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $employer_premises            (0,+9)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Recurrence            (0,+5)       bold  !1213540000 - Recurrence
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $recurrence                   (0,+5)             !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Investigated          (0,+4)       bold  !1213540000 - Investigated
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $investigated                 (0,+4)             !1213540000

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Print-Incident-Action
!*************************************************************
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Incident_cause          (+2,1)     bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $incident_cause                 (0,+4)           !1213540000 - Cause

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Corrective_prevent      (0,+2)     bold  !1213540000 - Corrective Action
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $corrective_prevent             (0,+5)           !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Action_sequence         (+2,1)     bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print #action_sequence                (0,+6)           !1213540000 - Action Sequence

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Action_category         (0,+5)     bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $action_category                (0,+5)           !1213540000 - Action Category

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Responsible_id          (0,+7)     bold  !1213540000 - Responsible Person
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $responsible_id                 (0,+6)           !1213540000
print $responsible_name               (0,+4)           !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Est_completion_dt       (+2,1)     bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $est_completion_dt              (0,+7)           !1213540000 - Estimated Completion Date

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Act_completion_dt       (0,+9)     bold  !1213540000 - Actual Completion Date
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $act_completion_dt              (0,+7)           !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Action_status           (0,+4)     bold  !1213540000 - Action Status
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $action_status                  (0,+4)           !1213540000

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Print-Incident-Witness
!*************************************************************
!64000000 : add
print $Ohs009_Incident_persons         (+3,1)     bold

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Witness_id            (+2,3)       bold
print $Ohs009_Witness_name          (0,+4)       bold
print $Ohs009_Confirm_statement     (0,+23)      bold  !1213540000 - Confirm Statement
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10

print $witness_id                   (+1,3)
print $witness_name                 (0,+3)             !1213540000 - Witness Name
print $confirm_statement            (0,+10)

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE    Print-Injury-Data
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11

print $Ohs009_Involved_id          (+3,3)           bold
print $Ohs009_Involved_name        (0,+3)           bold
print $Ohs009_Dt_reported          (0,52,13)        bold  !1213540000 - Injury headers
!print $Ohs009_Time_reported       (0,65,13)        bold  !1213540000
print $Ohs009_Date_filed_usa       (0,80,11)        bold
print $Ohs009_Injury_treatment     (0,97)           bold  !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10

print $involved_id                 (+1,3)
print $involved_name               (0,+3)
print $inj_dt_reported             (0,52,11)              !1213540000 - Reported Date
print $inj_tm_reported             (0,65,11)              !1213540000 - Reported Time
print $dt_filed                    (0,80,11)
print $injury_treatment            (0,97)                 !1213540000 - Treatment

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Illness_type         (+2,6)           bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $illness_type                (0,+7)                 !1213540000 - Illness type

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Illness_result       (0,+17)          bold  !1213540000 - Result
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $illness_result              (0,+4)                 !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Away_from_workdays   (+2,6)           bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print #away_from_workdays          (0,+10)                !1213540000 - Days Away from Work

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Restrictd_workdays   (0,+13)          bold  !1213540000 - Restricted Workdays
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print #restrictd_workdays          (0,+7)                 !1213540000

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE     Print-Nature-Injury
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Injury_nature_cd     (+2,6,16)          bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $injury_nature_cd            (0,25)                 !1213540000 - Nature of Injury
print $inj_nature_descr            (0,+5)                 !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Primary_injury       (0,+13)          bold  !1213540000 - Primary
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $primary_injury              (0,+4)                 !1213540000

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE     Print-Source-Injury
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Injury_source_cd     (+2,6,16)          bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $injury_source_cd            (0,25)                 !1213540000 - Source of Injury
print $inj_source_descr            (0,+5)                 !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Primary_injury       (0,+13)          bold  !1213540000 - Primary
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $primary_injury              (0,+4)                 !1213540000 

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE     Print-Acc-Type
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Accident_type        (+2,6,16)          bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $accident_type_cd            (0,25)                 !1213540000 - Accident Type
print $acc_type_descr              (0,+5)                 !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Primary_injury       (0,+13)          bold  !1213540000 - Primary
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $primary_injury              (0,+4)                 !1213540000

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE     Print-Unsafe-Act
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Unsafe_act_cd        (+2,6,16)          bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $unsafe_act_cd               (0,25)                 !1213540000 - Unsafe Act
print $unsafe_act_descr            (0,+5)                 !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Primary_injury       (0,+13)          bold  !1213540000 - Primary
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $primary_injury              (0,+4)                 !1213540000 

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE     Print-Body-Part
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Body_part_cd         (+2,6,16)          bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $body_part_cd                (0,25)                 !1213540000 - Body Part
print $body_part_descr             (0,+5)                 !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Primary_injury       (0,+13)          bold  !1213540000 - Primary
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $primary_injury              (0,+4)                 !1213540000 

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE     Print-Hospital
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Physician_name       (+2,6)         bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $physician_name              (0,+10)                !1213540000 - Physician

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Hospital             (0,+7)          bold   !1213540000 - Hospital
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $hospital                    (0,+3)                 !1213540000

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Care_type            (+1,6)          bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $care_type                   (0,+10)                !1213540000 - Care Type

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Phone                (0,+7)          bold   !1213540000 - Phone
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $phone                       (0,+6)                 !1213540000

END-PROCEDURE

!*************************************************************
BEGIN-PROCEDURE     Print-Diagnosis
!*************************************************************

!ALTER-PRINTER  FONT = 4   POINT-SIZE = 11
print $Ohs009_Diagnosis            (+2,10)          bold
!ALTER-PRINTER  FONT = 4   POINT-SIZE = 10
print $diagnosis_cd                (0,+6)                 !1213540000 - Diagnosis
print $diagnosis_descr             (0,+4)                 !1213540000

END-PROCEDURE

!**************************************************
begin-procedure Format-Times ($timeIn, :$timeOut)
!**************************************************

  extract $time-hour   from $timeIn 0 2
  extract $time-min    from $timeIn 3 2
  let #time-hour     = $time-hour
  if #time-hour >= 12
    move 'PM' to $am-pm
  else
    move 'AM' to $am-pm
  end-if
  if #time-hour > 12
    subtract 12 from #time-hour
    let $timeOut   = #time-hour
  else
    let $timeOut   = $time-hour
  end-if
  let $timeOut   = ltrim($timeOut, '0')
  if #time-hour < 10
    let $timeOut   = substr($timeOut,1,1)
  else
    let $timeOut   = substr($timeOut,1,2)
  end-if
  if #time-hour < 10
     let $timeOut = '0' || $timeOut
  end-if
  concat ':'        with $timeOut
  concat $time-min with $timeOut
  concat ' '        with $timeOut
  concat $am-pm     with $timeOut

end-procedure


#include 'stdapi.sqc'    !Routine to update run status
#include 'ohsrnctl.sqc'  !Get run control parameter values
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for Date Calculations
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language

