!***********************************************************************
!  FPA320.SQC : Step Increment                                         *
!                                                                      *
!  Insert new career row                                               *
!  Parameters :                                                        *
!               $Corps                                                 *
!               $OprId                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/07/19:12:42:25                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!******************************************************************************************
!
!
!       FPA320-Main
!
!       Select Employees where Automatic (Sub)Step Increment = 'Y' (indicator FP_PROM_OK)
!
!
!******************************************************************************************
BEGIN-PROCEDURE FPA320-Main



#IFDEF DEBUGI
    Display 'FPA320-Main ; Body: ' noline
    display $Corps noline
    display '; $OprId: ' noline
    display $OprId
#ENDIF

!*********************************************************************
!Individual and collective orders editing
!Select Order Ref and collective orders
!Select default action and reason action for automatic Step Increment
!*********************************************************************
do Select-Sys-Step ($EditIdvDec,$EditColDec,#ColDecIncr,#RefCol_ps,$Actn_Step,$Reasn_Step)

!***************************************************************
!When collective order edition: update of PSoft Ref Collective Order
!                               into the FPS system table
!***************************************************************
IF $EditColDec='Y'
        Let #Num = #RefCol_ps + 1
        Do Updt-Sys-RefColPS (#Num)
        Let $RefCol_ps = To_Char (#RefCol_ps)
        Let $RefCol_ps = Lpad($RefCol_ps,8,'0')
Else
        Let $RefCol_ps = ' '
        Let #RefCol_ps = 0
end-if


!If retro, use indicator to check that FP_PROM_OK = Y in FPAEECAREER record
!init indicator
!--------------------------------------------------------------------------

#IFDEF RETRO

    move 'N' to $FPA320_OK

#END-IF


!If retro, build dynamic variable 'where' according to the calling routine
!and end date =  end date retro
!build dynamic variable 'var_from'
!--------------------------------------------------------------------------

move 'PS_FPAEECAREER CC2,PS_FPMRANK_TBL K2,PS_FPMSALMATRX_TBL M2,PS_FPMSALSTEP_TBL E2, PS_PERSON_NAME Y2' to $var_from

let $var_where = ''

#IFDEF RETRO

    !build dynamic variable 'var_from'
    !---------------------------------

    move #empl_rcd to $empl_rcd

    let $var_where =  ' and   CC2.EMPLID  = ''' || $Emplid  || ''''
    let $var_where =  $var_where  ||
            ' and   CC2.EMPL_RCD  = ' || $Empl_rcd
    let $var_where =  $var_where  ||
            ' and CC2.EFFDT <= {DATEIN-PREFIX}''' || $date-fin-retro  || ''''
    let $var_where =  $var_where  || '{DYN-DateIn-Suffix}'

#ELSE

    let $var_from = $var_from || ', PS_FPAEELEGALSTAT A, PS_FPFAST_PERS_VW2 SCRTY'          !SQR security

    let $var_where =  ' and CC2.FP_CORPS_CD = ''' || $Corps  || ''''

    let $var_where =  $var_where  || ' and CC2.EMPLID = SCRTY.EMPLID ' || $SecurityClause   !SQR security
    let $var_where =  $var_where  || ' AND (A.EMPLID = CC2.EMPLID AND A.EMPL_RCD = CC2.EMPL_RCD AND A.FP_HIR_DOS_SIT = ''02'') '    !SQR security FPAEELEGALSTAT

#END-IF

#IFDEF DEBUGI
    display $var_from
    display $var_where
#ENDIF

let $sql-statement = 'FPA320.SQC, FPA320-Main,Select,PS_FPAEECAREER'
BEGIN-SELECT On-Error=SQL-Error

CC2.EMPLID                                              &CC2.Emplid
CC2.EMPL_RCD                                            &CC2.Empl_rcd
{DATEOUT-PREFIX}CC2.EFFDT{DATEOUT-SUFFIX}               &CC2.Effdt
CC2.EFFSEQ                                              &CC2.Sequence
CC2.FP_SETID_RANK                                       &CC2.Setid
CC2.FP_RANK_CD                                          &CC2.Rank_cd
CC2.FP_STEP_CD                                          &CC2.Step_cd
{DATEOUT-PREFIX}CC2.FP_FOR_PROM_DT{DATEOUT-SUFFIX}      &CC2.For_Prom_dt
{DATEOUT-PREFIX}CC2.FP_STEP_END_DT{DATEOUT-SUFFIX}      &CC2.Step_End_dt
CC2.FP_STFBDG_LVL1                                      &CC2.FP_STFBDG_LVL1
CC2.FP_STFBDG_LVL3                                      &CC2.FP_STFBDG_LVL3
{DATEOUT-PREFIX}CC2.FP_RK_ENT_DT{DATEOUT-SUFFIX}        &CC2.RK_Ent_Dt
CC2.FP_CORPS_CD                                         &CC2.Corps_Cd
{DATEOUT-PREFIX}CC2.FP_CHG_COR_DT{DATEOUT-SUFFIX}       &CC2.Chg_Cor_dt
CC2.FP_CATG_FP                                          &CC2.Catg_FP
CC2.FP_CATG_LEVEL                                       &CC2.Catg_Level
{DATEOUT-PREFIX}CC2.FP_NOMINATION_DT{DATEOUT-SUFFIX}    &CC2.Nomination_dt
CC2.FP_SANCTION                                         &CC2.Sanction
CC2.FP_DOWN_GRA                                         &CC2.Down_Gra
CC2.FP_PRO_ACCESS                                       &CC2.Pro_Access
K2.JOBCODE                                              &K2.JobCode
K2.FP_NONE_MATRX                                        &K2.None_Matrx
K2.FP_MATRIX_TYP                                        &K2.Matrix_Typ
K2.FP_MATRIX_CD                                         &K2.Matrix_Cd
K2.FP_SCALE_CD                                          &K2.Scale_cd
E2.FP_EQUIV_STEP                                        &E2.Equiv_Step
CC2.FP_SETID_CA                                         &CC2.Setid_ca
CC2.FP_COMM_ACNT_ID                                     &CC2.Comm_acnt_id
E2.FP_NX_STP_CD                                         &E2.Nx_Stp_cd
Y2.NAME                                                 &Y2.Name


   !********** Init variables ***********
   let $Emplid = rtrim(&CC2.Emplid,' ')
   let #Empl_rcd = &CC2.empl_rcd
   Do Convert-To-DTU-Date(&CC2.effdt,$Effdt_dtu)           ! SQR format
   let $Effdt_nat = &CC2.effdt                             ! Native format
   !**************************************************

        #IFDEF DEBUGI
            Display '**********************************************'
            Display 'Emplid:' Noline
            Display &CC2.Emplid
            Display 'Empl_rcd:' Noline
            Display &CC2.Empl_rcd
            Display &CC2.Effdt
        #ENDIF

        !******************************************
        ! If individual or collective order edition :
        ! Update of the Last Order Number in the system table
        !******************************************

        !******************************************
        ! Increment of the last order number
        !******************************************

        IF $EditIdvDec='Y' or $EditColDec='Y'
                Let #Num = #ColDecIncr + 1
                Do Updt-Sys-ColDecIncr (#Num)
               !Let $ColDecIncr = To_Char (#ColDecIncr)
                Let $ColDecIncr = To_Char (#Num)
                Let $ColDecIncr = Lpad($ColDecIncr,8,'0')
        Else
                Let $ColDecIncr = ' '
        end-if

        Do Var-Init              !Init Variables
        !******************************************
        ! Check no error
        !******************************************

        Do Fault-Search($emplid,#empl_rcd,$test_dt,$rank,$step,$sanction,
                        $ColDecIncr,$name,$effdt,$empl_rcd,$Fault,$nx_sanction,$nx_down_gra)
        let $nx_sanction = 'N'
        let $nx_down_gra = 'N'

        #IFDEF DEBUGI
            Display 'Anomalie :' noline
            Display $Fault
        #ENDIF
        !******************************************
        !If no error: Step Increment process
        !******************************************
        IF $Fault='N'

            do Insert-Career        !Insert row in career

            do Fpa320-Updt-Career   !Update previous row
                                    !Close Row and lauch calculation
                                    !(Sub)Step Actual End Date


            ! If no retro, update FPAEEUPDROW record
            !---------------------------------------
            #IFNDEF RETRO

                do Insert-Fpaeeupdrow($Emplid,#Empl_rcd,$Effdt_nat,$AsOfToday)

                #IFDEF DEBUGI
                    display 'Maj de la table FPAEEUPDROW'
                #ENDIF

            #END-IF


            !If retro, build dynamic variable 'where' according to the calling routine
            !and end date =  end date retro
            !build dynamic variable 'var_from'
            !--------------------------------------------------------------------------
            #IFDEF RETRO

                Let $FPA320_OK = 'Y'

            #END-IF


            !If Individual or Collective orders editing
            !then Insert row in Orders record
            !------------------------------------------
            IF $EditIdvDec='Y' or $EditColDec='Y'

                Do Insert-Notif
                Let #ColDecIncr = #ColDecIncr + 1

            END-IF

        END-IF

from [PS_FPAEECAREER CC2,PS_FPMRANK_TBL K2,
     PS_FPMSALMATRX_TBL M2, PS_FPMSALSTEP_TBL E2,PS_FPAEELEGALSTAT A, PS_FPFAST_PERS_VW2 SCRTY, PS_PERSON_NAME Y2:$var_from]

where Y2.EMPLID = CC2.EMPLID
and   CC2.FP_PROM_OK = 'Y'
and   CC2.FP_CLD_INST = 'N'
and   K2.SETID      = CC2.FP_SETID_RANK
and   K2.FP_RANK_CD = CC2.FP_RANK_CD
and   K2.EFF_STATUS = 'A'
and   K2.EFFDT =
        (select  MAX(EFFDT)
                from    PS_FPMRANK_TBL
                where   FP_RANK_CD = CC2.FP_RANK_CD
                and     SETID      = CC2.FP_SETID_RANK
                and     EFFDT      <= CC2.EFFDT)
and   M2.FP_MATRIX_TYP = K2.FP_MATRIX_TYP
and   M2.EFF_STATUS    = 'A'
and   M2.FP_MATRIX_CD  = K2.FP_MATRIX_CD
and   M2.FP_SCALE_CD   = K2.FP_SCALE_CD
and   M2.EFFDT =
        (select  MAX(EFFDT)
                from  PS_FPMSALMATRX_TBL
                where FP_MATRIX_TYP = K2.FP_MATRIX_TYP
                and   FP_MATRIX_CD  = K2.FP_MATRIX_CD
                and   FP_SCALE_CD   = K2.FP_SCALE_CD
                and   EFFDT <= CC2.EFFDT)
and   E2.FP_MATRIX_TYP = M2.FP_MATRIX_TYP
and   E2.FP_MATRIX_CD  = M2.FP_MATRIX_CD
and   E2.FP_SCALE_CD   = M2.FP_SCALE_CD
and   E2.FP_STEP_CD    = CC2.FP_STEP_CD
and   E2.EFFDT =
        (select MAX(EFFDT)
                from   PS_FPMSALSTEP_TBL
                where  FP_MATRIX_TYP = M2.FP_MATRIX_TYP
                and    FP_MATRIX_CD  = M2.FP_MATRIX_CD
                and    FP_SCALE_CD   = M2.FP_SCALE_CD
                and    FP_STEP_CD    = CC2.FP_STEP_CD
                and    EFFDT <= CC2.EFFDT)

[$var_where]

order by CC2.EMPLID, CC2.EMPL_RCD, CC2.EFFDT, CC2.EFFSEQ

END-SELECT

END-PROCEDURE FPA320-Main



!***************************************************************
!Insert-Career Insert row in Career record:
!       Effdt = Expected promotion date
!       Sequence=0
!       Call ref: empty
!       Notif ref : new number (system record)
!       Delete row: 'N'
!       Instance status : 'R' Actual
!       Action and action reason: default value
!       Grade: idem
!       Step : next step
!       Seniorities : 0
!       Seniority calculation indicator : ‘Y’
!       Instance terminated: 'N'
!       Expected promotion date: empty
!       Grade entry date: idem
!       Body : idem
!       Body entry date : idem
!       Civil Service category: idem
!       Trainee : 'N'
!       Probationary period state: no ('B')
!       Category level: idem
!       FPS Table code: 'C'
!       Row origin code: Automatic ‘A’
!       Appointment date: idem
!       PPd Expected End date : Empty
!       Sanction and demotion Code: calculating
!       Modify advancement date : 'N'
!       Retro code: 'N'
!       Advancement code : 'N'
!       Promo Prob period suspension: 'N'
!       Access mode: idem
!       Date of last update: current date
!***************************************************************
BEGIN-PROCEDURE Insert-Career

let $sql-statement = 'FPA320.SQR,Career ,Insert,PS_FPAEECAREER'
BEGIN-SQL On-Error=SQL-Error

INSERT INTO PS_FPAEECAREER
        (EMPLID,
        EMPL_RCD,
        EFFDT,
        EFFSEQ,
        FP_ACTION_REF,
        FP_NOTIF_REF,
        FP_CNL_ROW,
        FP_INST_STAT,
        ACTION,
        ACTION_REASON,
        FP_RANK_CD,
        FP_STEP_CD,
        FP_STEP_NO,
        FP_GROSS_IND,
        FP_INCS_IND,
        FP_PROM_LCK,
        FP_SENTOSTPDY,
        FP_SENTOSTPMN,
        FP_SENTOSTPYR,
        FP_SENINSTPDY,
        FP_SENINSTPMN,
        FP_SENINSTPYR,
        FP_TRUESEN_DY,
        FP_TRUESEN_MN,
        FP_TRUESEN_YR,
        FP_SENTRANSDY,
        FP_SENTRANSMN,
        FP_SENTRANSYR,
        FP_SENKEPT_DY,
        FP_SENKEPT_MN,
        FP_SENKEPT_YR,
        FP_SENRAN_DY,
        FP_SENRAN_MN,
        FP_SENRAN_YR,
        FP_SENCOR_DY,
        FP_SENCOR_MN,
        FP_SENCOR_YR,
        FP_DURSTEP_DY,
        FP_DURSTEP_MN,
        FP_DURSTEP_YR,
        FP_DURRAN_DY,
        FP_DURRAN_MN,
        FP_DURRAN_YR,
        FP_DURCOR_DY,
        FP_DURCOR_MN,
        FP_DURCOR_YR,
        FP_SENEQSTP_DY,
        FP_SENEQSTP_MN,
        FP_SENEQSTP_YR,
        FP_SENENTRK_DY,
        FP_SENENTRK_MN,
        FP_SENENTRK_YR,
        FP_SENENTCRP_DY,
        FP_SENENTCRP_MN,
        FP_SENENTCRP_YR,
        FP_SENENTCTG_DY,
        FP_SENENTCTG_MN,
        FP_SENENTCTG_YR,
        FP_SEN_CALC,
        FP_CLD_INST,
        FP_CLD_STEP,
        FP_FOR_PROM_DT,
        FP_STEP_END_DT,
        FP_RK_ENT_DT,
        FP_CORPS_CD,
        FP_CHG_COR_DT,
        FP_CATG_FP,
        FP_TRAINEE,
        FP_TRNE_POSN,
        FP_CATG_LEVEL,
        FP_CHG_IND,
        FP_APPL_SAL_DT,
        FP_ROW_CD,
        FP_NOMINATION_DT,
        FP_RK_TRIALPD,
        FP_RK_PD_END_DT,
        FP_SANCTION,
        FP_DOWN_GRA,
        FP_UPDT_PR_DT,
        FP_PROM_OK,
        FP_PROPD_BRK,
        FP_PRO_ACCESS,
        FP_SETID_RANK,
        FP_NX_STP_CD,
        FP_EQUIV_STEP,
        FP_STFBDG_LVL1,
        FP_STFBDG_LVL3,
        FP_SETID_CA,
        FP_COMM_ACNT_ID,
        FP_RETROSPECT,
        FP_TBL_CD,
        LAST_UPDATE_DATE)
VALUES

       (&CC2.Emplid,                                          !EMPLID
        &CC2.Empl_rcd,                                        !EMPL_RCD
        {DATEIN-PREFIX}&CC2.For_Prom_dt{DATEIN-SUFFIX},       !EFFDT
        0,                                                    !EFFSEQ
        ' ',                                                  !FP_ACTION_REF,
        $ColDecIncr,                                          !FP_NOTIF_REF,
        'N',                                                  !FP_CNL_ROW,
        'R',                                                  !FP_INST_STAT,
        $Actn_Step,                                           !ACTION,
        $Reasn_Step,                                          !ACTION_REASON,
        &CC2.Rank_cd,                                         !FP_RANK_CD
        &E2.Nx_Stp_cd,                                        !FP_STEP_CD,
        #Step_No,                                             !FP_STEP_NO
        0,                                                    !FP_GROSS_IND
        0,                                                    !FP_INCS_IND
       'N',                                                   !FP_PROM_LCK,
        0,                                                    !FP_SENTOSTPDY,
        0,                                                    !FP_SENTOSTPMN,
        0,                                                    !FP_SENTOSTPYR,
        0,                                                    !FP_SENINSTPDY,
        0,                                                    !FP_SENINSTPMN,
        0,                                                    !FP_SENINSTPYR,
        0,                                                    !FP_TRUESEN_DY,
        0,                                                    !FP_TRUESEN_MN,
        0,                                                    !FP_TRUESEN_YR,
        0,                                                    !FP_SENTRANSDY,
        0,                                                    !FP_SENTRANSMN,
        0,                                                    !FP_SENTRANSYR,
        0,                                                    !FP_SENKEPT_DY,
        0,                                                    !FP_SENKEPT_MN,
        0,                                                    !FP_SENKEPT_YR,
        0,                                                    !FP_SENRAN_DY,
        0,                                                    !FP_SENRAN_MN,
        0,                                                    !FP_SENRAN_YR,
        0,                                                    !FP_SENCOR_DY,
        0,                                                    !FP_SENCOR_MN,
        0,                                                    !FP_SENCOR_YR,
        0,                                                    !FP_DURSTEP_DY,
        0,                                                    !FP_DURSTEP_MN,
        0,                                                    !FP_DURSTEP_YR,
        0,                                                    !FP_DURRAN_DY,
        0,                                                    !FP_DURRAN_MN,
        0,                                                    !FP_DURRAN_YR,
        0,                                                    !FP_DURCOR_DY,
        0,                                                    !FP_DURCOR_MN,
        0,                                                    !FP_DURCOR_YR,
        0,                                                    !FP_SENEQSTP_DY,
        0,                                                    !FP_SENEQSTP_MN,
        0,                                                    !FP_SENEQSTP_YR,
        0,                                                    !FP_SENENTRK_DY,
        0,                                                    !FP_SENENTRK_MN,
        0,                                                    !FP_SENENTRK_YR,
        0,                                                    !FP_SENENTCRP_DY,
        0,                                                    !FP_SENENTCRP_MN,
        0,                                                    !FP_SENENTCRP_YR,
        0,                                                    !FP_SENENTCTG_DY,
        0,                                                    !FP_SENENTCTG_MN,
        0,                                                    !FP_SENENTCTG_YR,
        'Y',                                                  !FP_SEN_CALC,
        'N',                                                  !FP_CLD_INST,
        'N',                                                  !FP_CLD_STEP,
        null,                                                 !FP_FOR_PROM_DT
        {DATEIN-PREFIX}&CC2.Step_End_dt{DATEIN-SUFFIX},       !FP_STEP_END_DT
        {DATEIN-PREFIX}&CC2.RK_Ent_Dt{DATEIN-SUFFIX},         !FP_RK_ENT_DT
        &CC2.Corps_Cd,                                        !FP_CORPS_CD,
        {DATEIN-PREFIX}&CC2.Chg_Cor_dt{DATEIN-SUFFIX},        !FP_CHG_COR_DT,
        &CC2.Catg_FP,                                         !FP_CATG_FP,
        'N',                                                  !FP_TRAINEE,
        'B',                                                  !FP_TRNE_POSN,
        &CC2.Catg_Level,                                      !FP_CATG_LEVEL,
        'N',                                                  !FP_CHG_IND,
        {DATEIN-PREFIX}&CC2.For_Prom_dt{DATEIN-SUFFIX},       !FP_APPL_SAL_DT,
        'A',                                                  !FP_ROW_CD,
        {DATEIN-PREFIX}&CC2.Nomination_dt{DATEIN-SUFFIX},     !FP_NOMINATION_DT,
        'N',                                                  !FP_RK_TRIALPD,
        null,                                                 !FP_RK_PD_END_DT
        $Nx_Sanction,                                         !FP_SANCTION,
        $Nx_Down_Gra,                                         !FP_DOWN_GRA,
        'N',                                                  !FP_UPDT_PR_DT,
        'N',                                                  !FP_PROM_OK,
        'N',                                                  !FP_PROPD_BRK,
        &CC2.Pro_Access,                                      !FP_PRO_ACCESS,
        &CC2.Setid,                                           !FP_SETID_RANK
        ' ',                                                  !FP_NX_STP_CD
        $equiv_step,                                          !FP_EQUIV_STEP (substep)
        &CC2.FP_STFBDG_LVL1,                                  !FP_STFBDG_LVL1
        &CC2.FP_STFBDG_LVL3,                                  !FP_STFBDG_LVL3
        &CC2.Setid_ca,                                        !FP_SETID_CA
        &CC2.Comm_acnt_id,                                    !FP_COMM_ACNT_ID
        'N',                                                  !FP_RETROSPECT,
        'C',                                                  !FP_TBL_CD,
        {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX})             !LAST_UPDATE_DATE


END-SQL

#IFDEF DEBUGI
    display 'insert end'
    display  &CC2.For_Prom_dt
    display  $ColDecIncr
    display 'FPA320.SQR,Career ,Insert,PS_FPAEECAREER end'
#ENDIF

END-PROCEDURE Insert-Career



!***************************************************************
!Insert-Notif: Insert row in Employee Orders record
!       Order Ref: $ColDecIncr
!       return: 'N'
!       Psoft Table code : 'C'
!       Edit individual order:$EditIdvDec
!       Edit collective order:$EditColDec
!       Edit indic order (request):'N'
!       Edit coll order (request):'N'
!       Individual order edit Date :Null
!       Collective order edit Date :Null
!       collective order :Null
!       Ref PSoft collective order:$RefCol_ps
!       Order Abrogation : 'N'
!       Order Abrogation Date :Null
!       Edit Order Abrogation : 'N'
!       Action: default value
!       Provisional Editing : 'N'
!       Final Editing : 'N'
!       No Arrete Ind:Null
!       Date of last update: current date
!***************************************************************
BEGIN-PROCEDURE Insert-Notif

#IFDEF DEBUGI
    Display 'Insert Notif'
    Display &CC2.Emplid
    Display &CC2.Empl_rcd
    Display $ColDecIncr
    Display ' '
    Display 'C'
    Display $EditIdvDec
    Display $EditColDec
    Display 'N'
    Display 'N'
    Display ' '
    Display $RefCol_ps
    Display 'N'
    Display 'N'
    Display $Actn_Step
    Display 'N'
    Display 'N'
    Display ' '
    Display $AsOfToday
#ENDIF

let $sql-statement = 'FPA320.SQC,Insert-Notif ,Insert,PS_FPAEENOTICE'
BEGIN-SQL On-Error=SQL-Error

INSERT
INTO PS_FPAEENOTICE
        (EMPLID,
        EMPL_RCD,
        FP_NOTIF_REF,
        FP_RETURN_R,
        FP_TBL_CD,
        FP_EDITIDVDEC,
        FP_EDITCOLDEC,
        FP_IDV_ON_REQ,
        FP_COL_ON_REQ,
        FP_COLDEC_NBR,
        FP_REFCOL_PS,
        FP_DEC_ABROG,
        FP_EDIT_ABROG,
        ACTION,
        FP_EDT_TMP,
        FP_EDT_FNL,
        FP_IDVDEC_NBR,
        LAST_UPDATE_DATE)
VALUES

      ( &CC2.Emplid,            !EMPLID
        &CC2.Empl_rcd,          !EMPL_RCD
        $ColDecIncr,            !FP_NOTIF_REF,
        ' ',                    !FP_RETURN_R,
        'C',                    !FP_TBL_CD,
        $EditIdvDec,            !FP_EDITIDVDEC,
        $EditColDec,            !FP_EDITCOLDEC,
        'N',                    !FP_IDV_ON_REQ,
        'N',                    !FP_COL_ON_REQ,
        ' ',                    !FP_COLDEC_NBR,
        #RefCol_ps,             !FP_REFCOL_PS,
        'N',                    !FP_DEC_ABROG,
        'N',                    !FP_EDIT_ABROG,
        $Actn_Step,             !ACTION,
        'N',                    !FP_EDT_TMP,
        'N',                    !FP_EDT_FNL,
        ' ',                    !FP_IDVDEC_NBR,
        {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX})      !LAST_UPDATE_DATE


END-SQL

END-PROCEDURE Insert-Notif


!***************************************************************
!
! Fpa320-Updt-Career   (Occurence de depart)
!                      Fermeture Occurrence
!                      Calcul Dt Fin reelle d'echelon
!                      Top Avancement = N
!
!***************************************************************

BEGIN-PROCEDURE Fpa320-Updt-Career

do convert-to-DTU-Date(&CC2.For_Prom_dt,$For_Prom_Dt)
do dtu-subtract-days($For_Prom_Dt,1,$Step_end_dt)
do convert-from-DTU-Date($Step_end_dt,$Step_end_dt_nat)

let $sql-statement = 'FPA320.SQR, Fpa320-Updt-Career ,Update,PS_FPAEECAREER'
BEGIN-SQL On-Error=SQL-Error

update PS_FPAEECAREER
set  FP_CLD_INST = 'Y',
FP_CLD_STEP = 'Y',
FP_PROM_OK = 'N',
FP_STEP_END_DT = {DATEIN-PREFIX}$Step_end_dt_nat{DATEIN-SUFFIX},
LAST_UPDATE_DATE = {DATEIN-PREFIX}$AsOfToday{DATEIN-SUFFIX}
where EMPLID = &CC2.Emplid
and   EMPL_RCD = &CC2.Empl_rcd
and   EFFDT = {DATEIN-PREFIX}&CC2.Effdt{DATEIN-SUFFIX}
and   EFFSEQ = &CC2.Sequence

END-SQL

END-PROCEDURE


!***************************************************************
!
! Var-Init   Init variables
!
!***************************************************************

BEGIN-PROCEDURE Var-Init

Let $Fault = 'N'
Let $Emplid = rtrim(&CC2.Emplid,' ')
let $Name=substr(&Y2.Name,1,35)
Do Convert-To-DTU-Date (&CC2.Effdt,$Effdt)
Let $Empl_rcd = To_Char(&CC2.Empl_rcd)
let #empl_rcd =  &CC2.Empl_rcd
Do Convert-To-DTU-Date (&CC2.For_Prom_dt,$for_prom_dt)
Do Convert-From-DTU-Date ($for_prom_dt,$test_dt)
let $sanction = rtrim(&CC2.Sanction,' ')
let $rank = rtrim(&CC2.Rank_cd,' ')
let $step = rtrim(&CC2.Step_cd,' ')
Do Convert-From-DTU-Date ($Effdt,$Effdt_nat)
let $matrix_typ = rtrim(&K2.Matrix_Typ,' ')
let $matrix_cd = rtrim(&K2.Matrix_Cd,' ')
let $scale_cd = rtrim(&K2.Scale_cd,' ')
let $Nx_Stp_cd = rtrim(&E2.Nx_Stp_cd,' ')

if $scale_cd = ''
    let $scale_cd = ' '
end-if

display $Matrix_Typ
display $Matrix_cd
display $scale_cd
display $nx_stp_cd
display $effdt_nat
do Get-Step-No ($Matrix_Typ,$Matrix_Cd,$Scale_cd,$Nx_Stp_cd,$Effdt_nat,#Step_No,$equiv_step)
display $equiv_step

if $equiv_step = ''
    let $equiv_step = ' '
end-if

END-PROCEDURE Var-Init



! SQR using this SQC must have the following includes
! ***************************************************

!#include 'setenv.sqc'         ! Set environment
!#include 'setup01.sqc'        ! Printer and page-size initialization
!#include 'curdttim.sqc'       ! Get-Current-DateTime procedure
!#include 'stdapi.sqc'         ! Routines to update run status
!#Include 'number.sqc'         ! Routines to format numbers
!#Include 'fptestdt.sqc'     ! Routines for date and time formatting (FPS)
!#include 'fpjob.sqc'          ! Insert-Fpaeeupdrow procedure (FPS)
!#Include 'fpckflt.sqc'        ! Routines for promotion errors (FPS)
!#Include 'fpparam.sqc'        ! Routines for  Parameters Tables (FPS)

!#Include 'rellang.sqc'        ! Related Language File - call by fpparam.sqc
