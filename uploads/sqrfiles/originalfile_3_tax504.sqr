!*********************************************************************
!  TAX504:  Update Fed_Tax_Data Pension_Plan for W 2 Printing        *
!*********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/03/21:08:11:05                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  698735                                              !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
! HP00001       Add PublicSector indicator                             *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup01.sqc'  !printer and page-size initialization

begin-report
  do Init-DateTime
  do Init-Number
  do Init-Report

  do Get-Calendar-Year-Id
  display ''
  display 'Updating W2 Check Boxes on Fed Tax Data '
  display '                         '
  do Initialization
  do Read-Run-Control
  do Read-Plan-DedCd
  do Read-Third-Party-Sick-Pay

  if &RC.PY_YE_PROCESS = 'Y'
    let $Empl_Status = ''
  else
    do Read-Empl-Status
  end-if

  do Process-Pension_Plans
  do Stdapi-Term
end-report


begin-procedure Init-Report
  move 'TAX504' to $ReportID
  move 'Retirement Plan and Third Party Sick Pay' to $ReportTitle

  do Stdapi-Init
end-procedure


begin-heading 9
   #Include 'stdhdg01.sqc'

   print $TaxYear                                                 (3,)  center

   if $Prev_Company <> ''
      print 'Company:'            (+3, 1)
      print $Company_Descr        (0, 10)
      do Print-EE-Header
   end-if

end-heading


begin-procedure Initialization
  do Get-Current-DateTime
  move '1' to $MMLZero
  move '1' to $Year4
  do Format-DateTime($AsOfToday, $910Date, {DEFYMD},'','')

  move $910Date to $AsOfYear xxxx   !extract the current year
  move $AsOfYear  to #AsOfYear
  subtract 1    from #AsOfYear
  do Get-Tax-Reporting-Run-Controls
  if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''
    display ''
    display 'Current Year is not one greater than Tax Reporting Year.'
  end-if

  move &TX.Balance_Year to $CalYear 9999
  let $TaxYear = 'Tax Year ' || $CalYear

  let $Pension_Plan_Found = 'N'
  let $Deferred_Comp = 'N'
  let $Deceased = 'N'
  let $Third_Party_Sick = 'N'
  let $Prev_Company = ''

  let #RunCtl_LineCount = 0


  create-array name=PlanTypeDedCd size=50 -
    field=PlanType:char -
    field=DedCode:char

  create-array name=ThirdPartySick size=50 -
    field=ThirdPartySickPay:char

end-procedure


begin-procedure Read-Run-Control

begin-SELECT
RC.PY_YE_PROCESS
RC.TAX_EFFDT

  let $AsOfDate = &RC.TAX_EFFDT

  print 'Effective Date for Updates:'          (+1, 1)
  do Format-DateTime(&RC.TAX_EFFDT, $out, {DEFDATE}, '', '')
  print $out                                   (0, 30)
  print 'Year End Process:'                    (+1, 1)
  print &RC.PY_YE_PROCESS                      (0, 20)

FROM PS_RC_TAX504_PRCS RC
WHERE RC.OPRID     = $Prcs_OprID
AND RC.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-SELECT
end-procedure


begin-procedure Process-Pension_Plans

begin-SELECT
A.COMPANY
A.DESCR

  do Read-Employee-Data

FROM  PS_COMPANY_TBL A
WHERE TAX_REPORT_TYPE = '2'  !W-2 Reporting Company
  AND EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
ORDER BY A.COMPANY
end-SELECT

end-procedure


begin-procedure Read-Employee-Data

begin-SELECT
D.EMPLID
D.DT_OF_DEATH
F.COMPANY

  let #ActiveEmplCount = 0
  if RTRIM(&RC.PY_YE_PROCESS, ' ') = 'N'
     do Select-Employees
     move &ActiveEmplCount to #ActiveEmplCount
  end-if

  if RTRIM(&RC.PY_YE_PROCESS, ' ') = 'Y'
     or (RTRIM(&RC.PY_YE_PROCESS, ' ') = 'N'
                and #ActiveEmplCount = 0)

    if $Prev_Company <> &F.COMPANY
       new-page
       let $Prev_Company = &F.COMPANY
    end-if

    let $Company_Descr = &A.DESCR

    if #PlanDedCount > 0
      do Get-Deductions
    end-if

    if #ThirdPartyCount > 0
      do Get-Third-Party-Sick-Pay
    end-if

    if isnull(rtrim(&D.DT_OF_DEATH,''))
      move 'N' to $Deceased
    else
      move 'Y' to $Deceased
    end-if

    do Update-Pension_Plan

  end-if

FROM   PS_PERSON D, PS_FED_TAX_DATA F, PS_INSTALLATION IN1       !HP00001
WHERE F.EMPLID        = D.EMPLID
  AND F.COMPANY       = &A.Company
  AND F.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_FED_TAX_DATA
       WHERE  EMPLID  = F.EMPLID
         AND  COMPANY = &A.Company
         AND  EFFDT  <= $AsOfDate)

  AND EXISTS
    (SELECT 'x'
    FROM PS_TAX_BALANCE C
    WHERE C.COMPANY       = &A.Company
      AND C.EMPLID        = D.EMPLID
      AND (C.TAX_CLASS IN ('H','D','F','C')                             !HP99999
           OR (IN1.PUBLIC_SECTOR = 'Y'                                  !HP00001
               AND C.TAX_CLASS = 'A'))                                  !HP99999
      AND C.BALANCE_YEAR = &TX.Balance_Year
      AND C.BALANCE_ID    = $Calendar_Year_Id
      AND C.BALANCE_PERIOD =
          (SELECT MAX(BALANCE_PERIOD)
           FROM   PS_TAX_BALANCE
           WHERE  EMPLID        = C.EMPLID
             AND  COMPANY       = C.COMPANY
             AND  BALANCE_YEAR = C.BALANCE_YEAR
             AND  BALANCE_ID    = C.BALANCE_ID
             AND  STATE         = C.STATE
             AND (LOCALITY      = C.LOCALITY
              OR  LOCALITY IS NULL and C.LOCALITY IS NULL)
             AND  TAX_CLASS     = C.TAX_CLASS)
      AND (C.TXGRS_YTD > 0  OR  C.TAX_YTD > 0) )
ORDER BY F.COMPANY ASC, F.EMPLID ASC
end-SELECT

end-procedure


begin-procedure Select-Employees

begin-SELECT
COUNT(*)        &ActiveEmplCount
FROM PS_JOB J
WHERE J.EMPLID = &D.EMPLID
AND J.EFFDT =
    (SELECT MAX(J1.EFFDT)
     FROM PS_JOB J1
     WHERE J1.EMPLID = J.EMPLID
       AND J1.EMPL_RCD = J.EMPL_RCD
       AND J1.EFFDT <= &RC.TAX_EFFDT)
AND J.EFFSEQ =
    (SELECT MAX(J2.EFFSEQ)
     FROM PS_JOB J2
     WHERE J2.EMPLID = J.EMPLID
       AND J2.EMPL_RCD = J.EMPL_RCD
       AND J2.EFFDT = J.EFFDT)
[$Empl_Status]
end-SELECT
end-procedure


begin-procedure Read-Plan-DedCd


move 0 to #PlanDedCount


begin-SELECT
E.PLAN_TYPE
E.DEDCD
DT.DESCR

     if #PlanDedCount = 0
        do Print-PlanDed-Header
     end-if

     let #RunCtl_LineCount = #RunCtl_LineCount + 1
     if #RunCtl_LineCount > 50
        new-page
        do Print-PlanDed-Header
        let #RunCtl_LineCount = 0
     end-if

     print &E.PLAN_TYPE            (+1, 1)
     move 'PLAN_TYPE' to $FieldName
     move &E.PLAN_TYPE to $FieldValue
     do Read-Translate-Table
     print $XlatLongName           (0, 17)
     print &E.DEDCD                (0, 49)
     print &DT.DESCR               (0, 67)

         add 1 to #PlanDedCount


         PUT &E.PLAN_TYPE  INTO PLANTYPEDEDCD(#PLANDEDCOUNT) PLANTYPE
         PUT &E.DEDCD  INTO PLANTYPEDEDCD(#PLANDEDCOUNT) DEDCODE

FROM  PS_RC_TAX504 E, PS_DEDUCTION_TBL DT
WHERE E.OPRID         = $Prcs_OprID
  AND E.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
  AND E.PLAN_TYPE <> ' '
  AND DT.PLAN_TYPE = E.PLAN_TYPE
  AND DT.DEDCD = E.DEDCD
  AND DT.EFFDT =
      (SELECT MAX(DT1.EFFDT)
      FROM PS_DEDUCTION_TBL DT1
      WHERE DT1.PLAN_TYPE = DT.PLAN_TYPE
      AND DT1.DEDCD       = DT.DEDCD
      AND DT1.EFFDT       <= &RC.TAX_EFFDT)
ORDER BY E.PLAN_TYPE ASC, E.DEDCD ASC
end-SELECT

end-procedure

begin-procedure Get-Deductions

  move 'N' to $Pension_Plan_Found
  move 'N' to $Deferred_Comp
  move 1 to #NextPlanDed

  while #NextPlanDed <= #PlanDedCount  and $Deferred_Comp = 'N'
      get $PlanType $DedCode from PLANTYPEDEDCD(#NextPlanDed)
      let $PlanType = RTRIM($PlanType,' ')
      let $DedCode = RTRIM($DedCode,' ')
      do Read-Balances

      add 1 to #NextPlanDed
  end-while
end-procedure


begin-procedure Read-Balances

begin-SELECT

#ifdef DB2ALL
DECIMAL(SUM(DB.DED_YTD),15,3)   &DB.Ded_YTD
#else
SUM(DB.DED_YTD)         &DB.Ded_YTD
#endif

DB.DED_CLASS

    if $Deferred_Comp <> 'Y'
       if &DB.DED_CLASS = 'B'
         move 'Y' to $Deferred_Comp
       end-if
       move 'Y' to $Pension_Plan_Found
    end-if


FROM  PS_DEDUCTION_BAL DB
WHERE DB.COMPANY           = &A.Company
  AND DB.PLAN_TYPE         = $PlanType
  AND DB.DEDCD             = $DedCode
  AND DB.EMPLID            = &D.EmplID
  AND DB.BALANCE_YEAR     = &TX.Balance_Year
  AND DB.BALANCE_ID       = $Calendar_Year_Id
  AND DB.DED_YTD           > 0
  AND DB.BALANCE_PERIOD           =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_DEDUCTION_BAL
       WHERE  EMPLID            = DB.EMPLID
         AND  COMPANY           = DB.COMPANY
         AND  BALANCE_YEAR      = DB.BALANCE_YEAR
         AND  BALANCE_ID        = DB.BALANCE_ID
         AND  PLAN_TYPE         = DB.PLAN_TYPE
         AND  BENEFIT_RCD_NBR   = DB.BENEFIT_RCD_NBR
         AND  DEDCD             = DB.DEDCD
         AND  DED_CLASS         = DB.DED_CLASS)
GROUP BY DB.DED_CLASS
end-SELECT
end-procedure

begin-procedure Update-Pension_Plan

   let $Fed_Tax_Found = 'N'

begin-SELECT
X.COMPANY
X.EMPLID
X.EFFDT

   let $Fed_Tax_Found = 'Y'
FROM PS_FED_TAX_DATA X
WHERE X.COMPANY       = &A.Company
  AND X.EMPLID        = &D.EmplID
  AND X.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_FED_TAX_DATA
       WHERE  EMPLID  = X.EMPLID
         AND  COMPANY = &A.Company
         AND  EFFDT  = $AsOfDate)
end-SELECT

  if $Fed_Tax_Found = 'Y'

     do Update-Fed-Tax-Data
  else
     do Select-Fed-Tax-Data
     do Insert-New-Fed-Row
     do Insert-New-St-Row
     do Insert-New-Loc-Row
  end-if

  print &D.EMPLID                     (+1, 1)
  do Format-DateTime($AsOfDate, $out, {DEFDATE}, '', '')
  print $out                          (0, 14)
  print $Pension_Plan_Found           (0, 28)
  print $Deferred_Comp                (0, 37)
  print $Deceased                     (0, 47)
  print $Third_Party_Sick             (0, 57)

  if $Fed_Tax_Found = 'Y'
     print 'Existing employee tax data updated.'   (0, 70)
  else
     print 'New employee tax data inserted.'        (0, 70)
  end-if

  let $Pension_Plan_Found = 'N'
  let $Deferred_Comp = 'N'
  let $Deceased = 'N'
  let $Third_Party_Sick = 'N'

end-procedure

begin-procedure Update-Fed-Tax-Data
begin-SQL

UPDATE PS_FED_TAX_DATA
SET PENSION_PLAN   = $Pension_Plan_Found,
    DEFERRED_COMP  = $Deferred_Comp,
    DECEASED       = $Deceased,
    THIRDPARTY_SICKPAY = $Third_Party_Sick,
    LAST_ACTION    = 'C',
    LAST_UPDATE_DATE = $AsOfToday
WHERE COMPANY   = &X.Company
  AND EMPLID    = &X.EmplID
  AND EFFDT     = &X.EffDt

end-SQL

end-procedure

begin-procedure Select-Fed-Tax-Data
begin-SELECT
T.EFFDT,
T.SPECIAL_FWT_STATUS,
T.FWT_MAR_STATUS,
T.FWT_ALLOWANCES,
T.FWT_ADDL_AMT,
T.FWT_ADDL_PCT,
T.FUT_EXEMPT,
T.EIC_STATUS,
T.STATUTORY_EE,
T.LEGAL_REP,
T.USE_TOTAL_WAGE,
T.W4_PRCSS,
T.W5_PRCSS,
T.LOCK_IN_RECVD,
T.LOCK_IN_LIMIT,
T.DATE_OF_ENTRY,
T.COUNTRY,
T.FORM_1001_RECD,
T.FORM_1001_SUBMT_DT,
T.FORM_1001_EXP_DT,
T.FORM_8233_RECD,
T.FORM_8233_SUBMT_DT,
T.FORM_8233_EXP_DT,
T.TAXPAYER_ID_NO,
T.TREATY_EXP_DT,
T.TREATY_ID,
T.FORM_1078_RECD,
T.FORM_1078_SUBMT_DT,
T.W4_NAME_CHECK,
T.PY_FLAG_FOR_IRS,
T.PY_FLAG_FOR_IRS_DT,
T.MARRIED_FILE_SINGL,
T.NRA_EXEMPT_WHRULE,
T.NQDC_W2_RPTG

FROM PS_FED_TAX_DATA T
WHERE T.COMPANY       = &A.Company
  AND T.EMPLID        = &D.EmplID
  AND T.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_FED_TAX_DATA T1
       WHERE  T1.EMPLID  = &D.EMPLID
         AND  T1.COMPANY = &A.Company
         AND  T1.EFFDT  < $AsOfDate)
end-SELECT
end-procedure

begin-procedure Insert-New-Fed-Row

  if isnull(rtrim(&T.DATE_OF_ENTRY,''))
    move ''  to  $T_DATE_OF_ENTRY
  else
    move &T.DATE_OF_ENTRY  to  $T_DATE_OF_ENTRY
  end-if
 
  if isnull(rtrim(&T.FORM_1001_SUBMT_DT,''))
    move ''  to  $T_FORM_1001_SUBMT_DT
  else
    move &T.FORM_1001_SUBMT_DT  to  $T_FORM_1001_SUBMT_DT
  end-if
 
  if isnull(rtrim(&T.FORM_1001_EXP_DT,''))
    move ''  to  $T_FORM_1001_EXP_DT
  else
    move &T.FORM_1001_EXP_DT  to  $T_FORM_1001_EXP_DT
  end-if
 
  if isnull(rtrim(&T.FORM_8233_SUBMT_DT,''))
    move ''  to  $T_FORM_8233_SUBMT_DT
  else
    move &T.FORM_8233_SUBMT_DT  to  $T_FORM_8233_SUBMT_DT
  end-if
 
  if isnull(rtrim(&T.FORM_8233_EXP_DT,''))
    move ''  to  $T_FORM_8233_EXP_DT
  else
    move &T.FORM_8233_EXP_DT  to  $T_FORM_8233_EXP_DT
  end-if
 
  if isnull(rtrim(&T.TREATY_EXP_DT,''))
    move ''  to  $T_TREATY_EXP_DT
  else
    move &T.TREATY_EXP_DT  to $T_TREATY_EXP_DT
  end-if
 
  if isnull(rtrim(&T.FORM_1078_SUBMT_DT,''))
    move ''  to  $T_FORM_1078_SUBMT_DT
  else
    move &T.FORM_1078_SUBMT_DT  to  $T_FORM_1078_SUBMT_DT
  end-if
 
  if isnull(rtrim(&T.PY_FLAG_FOR_IRS_DT,''))
    move ''  to $T_PY_FLAG_FOR_IRS_DT
  else
    move &T.PY_FLAG_FOR_IRS_DT  to $T_PY_FLAG_FOR_IRS_DT
  end-if

BEGIN-SQL
INSERT INTO PS_FED_TAX_DATA
(EMPLID,
COMPANY,
EFFDT,
SPECIAL_FWT_STATUS,
FWT_MAR_STATUS,
FWT_ALLOWANCES,
FWT_ADDL_AMT,
FWT_ADDL_PCT,
FUT_EXEMPT,
EIC_STATUS,
STATUTORY_EE,
DECEASED,
PENSION_PLAN,
LEGAL_REP,
DEFERRED_COMP,
USE_TOTAL_WAGE,
LAST_ACTION,
W4_PRCSS,
W5_PRCSS,
LOCK_IN_RECVD,
LOCK_IN_LIMIT,
LAST_UPDATE_DATE,
DATE_OF_ENTRY,
COUNTRY,
FORM_1001_RECD,
FORM_1001_SUBMT_DT,
FORM_1001_EXP_DT,
FORM_8233_RECD,
FORM_8233_SUBMT_DT,
FORM_8233_EXP_DT,
TAXPAYER_ID_NO,
TREATY_EXP_DT,
TREATY_ID,
FORM_1078_RECD,
FORM_1078_SUBMT_DT,
THIRDPARTY_SICKPAY,
W4_NAME_CHECK,
PY_FLAG_FOR_IRS,
PY_FLAG_FOR_IRS_DT,
CREATION_DT,
MARRIED_FILE_SINGL,
NRA_EXEMPT_WHRULE,
NQDC_W2_RPTG )
VALUES
(&D.EMPLID,
&A.COMPANY,
$Asofdate,
&T.SPECIAL_FWT_STATUS,
&T.FWT_MAR_STATUS,
&T.FWT_ALLOWANCES,
&T.FWT_ADDL_AMT,
&T.FWT_ADDL_PCT,
&T.FUT_EXEMPT,
&T.EIC_STATUS,
&T.STATUTORY_EE,
$Deceased,
$Pension_Plan_Found,
&T.LEGAL_REP,
$Deferred_Comp,
&T.USE_TOTAL_WAGE,
'C',
&T.W4_PRCSS,
&T.W5_PRCSS,
&T.LOCK_IN_RECVD,
&T.LOCK_IN_LIMIT,
$AsOfToday,
$T_DATE_OF_ENTRY,
&T.COUNTRY,
&T.FORM_1001_RECD,
$T_FORM_1001_SUBMT_DT,
$T_FORM_1001_EXP_DT,
&T.FORM_8233_RECD,
$T_FORM_8233_SUBMT_DT,
$T_FORM_8233_EXP_DT,
&T.TAXPAYER_ID_NO,
$T_TREATY_EXP_DT,
&T.TREATY_ID,
&T.FORM_1078_RECD,
$T_FORM_1078_SUBMT_DT,
$Third_Party_Sick,
&T.W4_NAME_CHECK,
&T.PY_FLAG_FOR_IRS,
$T_PY_FLAG_FOR_IRS_DT,
$AsOfToday,
&T.MARRIED_FILE_SINGL,
&T.NRA_EXEMPT_WHRULE,
&T.NQDC_W2_RPTG  )
END-SQL

end-procedure

begin-procedure Insert-New-St-Row
begin-SELECT
S.STATE,
S.RESIDENT,
S.NON_RES_DECLARED,
S.SPECIAL_SWT_STATUS,
S.SWT_MAR_STATUS,
S.SWT_ALLOWANCES,
S.SWT_ADDL_ALLOWNCES,
S.WAGE_PLAN_CD,
S.ANNL_EXEMPTION_AMT,
S.PERCENT_OF_FWT,
S.SWT_ADDL_AMT,
S.SWT_ADDL_PCT,
S.SDI_STATUS,
S.SUT_EXEMPT,
S.UI_JURISDICTION,
S.ADDL_AMT_ADJ,
S.WT4A_FILED,
S.LOCK_IN_RECVD,
S.LOCK_IN_LIMIT,
S.RETIREMENT_PLAN,
S.EIC_STATUS_ST

  do Insert-State-Record

FROM PS_STATE_TAX_DATA S
WHERE S.EMPLID = &D.EMPLID
AND S.COMPANY  = &A.COMPANY
AND S.EFFDT    = &T.EFFDT
end-Select
end-procedure

begin-procedure Insert-State-Record
begin-sql
INSERT INTO PS_STATE_TAX_DATA
(EMPLID,
COMPANY,
EFFDT,
STATE,
RESIDENT,
NON_RES_DECLARED,
SPECIAL_SWT_STATUS,
SWT_MAR_STATUS,
SWT_ALLOWANCES,
SWT_ADDL_ALLOWNCES,
WAGE_PLAN_CD,
ANNL_EXEMPTION_AMT,
PERCENT_OF_FWT,
SWT_ADDL_AMT,
SWT_ADDL_PCT,
SDI_STATUS,
SUT_EXEMPT,
UI_JURISDICTION,
ADDL_AMT_ADJ,
WT4A_FILED,
LOCK_IN_RECVD,
LOCK_IN_LIMIT,
RETIREMENT_PLAN,
EIC_STATUS_ST,
CREATION_DT)
VALUES
(&D.EMPLID,
&A.COMPANY,
$AsOfDate,
&S.STATE,
&S.RESIDENT,
&S.NON_RES_DECLARED,
&S.SPECIAL_SWT_STATUS,
&S.SWT_MAR_STATUS,
&S.SWT_ALLOWANCES,
&S.SWT_ADDL_ALLOWNCES,
&S.WAGE_PLAN_CD,
&S.ANNL_EXEMPTION_AMT,
&S.PERCENT_OF_FWT,
&S.SWT_ADDL_AMT,
&S.SWT_ADDL_PCT,
&S.SDI_STATUS,
&S.SUT_EXEMPT,
&S.UI_JURISDICTION,
&S.ADDL_AMT_ADJ,
&S.WT4A_FILED,
&S.LOCK_IN_RECVD,
&S.LOCK_IN_LIMIT,
&S.RETIREMENT_PLAN,
&S.EIC_STATUS_ST,
$AsOfToday)
end-sql
end-procedure

begin-procedure Insert-New-Loc-Row
begin-SELECT
L.STATE,
L.LOCALITY,
L.RESIDENT,
L.LWT_ALLOWANCES,
L.LWT_AMT,
L.LWT_PCT,
L.LOCALITY_LINK,
L.SPECIAL_LWT_STATUS,
L.LWT_MAR_STATUS

  do Insert-Local-Record

FROM PS_LOCAL_TAX_DATA L
WHERE L.EMPLID  = &D.EMPLID
  AND L.COMPANY = &A.COMPANY
  AND L.EFFDT   = &T.EFFDT
end-SELECT
end-procedure

begin-procedure Insert-Local-Record
begin-sql
INSERT INTO PS_LOCAL_TAX_DATA
(EMPLID,
COMPANY,
EFFDT,
STATE,
LOCALITY,
RESIDENT,
LWT_ALLOWANCES,
LWT_AMT,
LWT_PCT,
LOCALITY_LINK,
SPECIAL_LWT_STATUS,
LWT_MAR_STATUS,
CREATION_DT)
VALUES
(&D.EMPLID,
&A.COMPANY,
$AsOfDate,
&L.STATE,
&L.LOCALITY,
&L.RESIDENT,
&L.LWT_ALLOWANCES,
&L.LWT_AMT,
&L.LWT_PCT,
&L.LOCALITY_LINK,
&L.SPECIAL_LWT_STATUS,
&L.LWT_MAR_STATUS,
$AsOfToday)
end-sql
end-procedure


begin-procedure Read-Third-Party-Sick-Pay


move 0 to #ThirdPartyCount



begin-SELECT
EA.ERNCD
ET.DESCR

  if #ThirdPartyCount = 0
     do Print-ThirdParty-Header
  end-if

  let #RunCtl_LineCount = #RunCtl_LineCount + 1
  if #RunCtl_LineCount > 50
     do Print-ThirdParty-Header
     let #RunCtl_LineCount = 0
  end-if

  print &EA.ERNCD               (+1, 1)
  print &ET.DESCR               (0, 17)

         add 1 to #ThirdPartyCount

         PUT &EA.ERNCD INTO THIRDPARTYSICK(#THIRDPARTYCOUNT) THIRDPARTYSICKPAY

FROM  PS_RC_TAX504_ERN EA, PS_EARNINGS_TBL ET
WHERE EA.OPRID         = $Prcs_OprID
  AND EA.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
  AND EA.ERNCD <> ' '
  AND EA.ERNCD = ET.ERNCD
  AND ET.EFFDT =
      (SELECT MAX(ET1.EFFDT)
      FROM PS_EARNINGS_TBL ET1
      WHERE ET1.ERNCD = ET.ERNCD
      AND ET1.EFFDT   <= &RC.TAX_EFFDT)
ORDER BY EA.ERNCD ASC
end-SELECT

end-procedure

begin-procedure Get-Third-Party-Sick-Pay


  move 'N' to $Third_Party_Sick
  move 1 to #NextThirdPartySick

  while #NextThirdPartySick <= #ThirdPartyCount  and $Third_Party_Sick = 'N'
      get $ThirdPartySickPay from THIRDPARTYSICK(#NextThirdPartySick)
      let $ThirdPartySickPay = RTRIM($ThirdPartySickPay,' ')
      do Read-Earnings-Balances

      add 1 to #NextThirdPartySick
  end-while
end-procedure

begin-procedure Read-Earnings-Balances

begin-SELECT

ER.GRS_YTD
ER.HRS_YTD
ER.ERNCD


    if $Third_Party_Sick <> 'Y'

       move 'Y' to $Third_Party_Sick

    end-if


FROM  PS_EARNINGS_BAL ER
WHERE ER.COMPANY           = &A.Company
  AND ER.ERNCD             = $ThirdPartySickPay
  AND ER.EMPLID            = &D.EmplID
  AND ER.BALANCE_YEAR     = &TX.Balance_Year
  AND ER.BALANCE_ID       = $Calendar_Year_Id
  AND (ER.HRS_YTD           > 0
       OR ER.GRS_YTD        > 0)
  AND ER.BALANCE_PERIOD           =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_EARNINGS_BAL
       WHERE  EMPLID            = ER.EMPLID
         AND  COMPANY           = ER.COMPANY
         AND  BALANCE_YEAR      = ER.BALANCE_YEAR
         AND  BALANCE_ID        = ER.BALANCE_ID
         AND  ERNCD             = ER.ERNCD)

end-SELECT
end-procedure


begin-procedure Read-Empl-Status

  let #EEStatusCount = 0
  let $First_Empl_Status = 'Y'
  let $Empl_Status = 'AND J.EMPL_STATUS NOT IN ('

begin-SELECT
STAT.EMPL_STATUS

  if $First_Empl_Status = 'Y'
    let $Empl_Status = $Empl_Status || '''' || &STAT.EMPL_STATUS || ''''
    let $First_Empl_Status = 'N'
  else
    let $Empl_Status = $Empl_Status || ', ''' || &STAT.EMPL_STATUS || ''''
  end-if

  if #EEStatusCount = 0
     do Print-EESTatus-Header
  end-if

  add 1  to  #EEStatusCount

  let #RunCtl_LineCount = #RunCtl_LineCount + 1
  if #RunCtl_LineCount > 50
     do Print-EEStatus-Header
     let #RunCtl_LineCount = 0
  end-if

  move 'EMPL_STATUS' to $FieldName
  move &STAT.EMPL_STATUS to $FieldValue
  do Read-Translate-Table
  print $XlatLongName                  (+1, 1)

FROM PS_RC_TAX504_EE_ST STAT
WHERE STAT.OPRID         = $Prcs_OprID
  AND STAT.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
end-SELECT

  let $Empl_Status = $Empl_Status || ')'

  if #EEStatusCount = 0
    let $Empl_Status = ''
  end-if

end-procedure


begin-procedure Print-PlanDed-Header

  print 'Plan Type'          (+4, 1)
  print 'Description'        (0, 17)
  print 'Deduction Code'     (0, 49)
  print 'Description'        (0, 67)
  print ' '                  (+1, 1)

end-procedure


begin-procedure Print-ThirdParty-Header

  print 'Earnings Code'      (+4, 1)
  print 'Description'        (0, 17)
  print ' '                  (+1, 1)

end-procedure


begin-procedure Print-EEStatus-Header

  print 'Payroll Status'     (+4, 1)
  print ' '                  (+1, 1)

end-procedure


begin-procedure Print-EE-Header

  print 'Effective'                (+2, 14)
  print 'Pension'                  (0, 28)
  print 'Deferred'                 (0, 37)
  print 'Deceased'                 (0, 47)
  print 'Third Party'              (0, 57)

  print 'Emplid'                   (+1, 1)
  print 'Date'                     (0, 14)
  print 'Plan'                     (0, 28)
  print 'Comp'                     (0, 37)
  print 'Sick'                     (0, 57)
  print 'Action'                   (0, 70)
  print ' '                        (+1, 1)

end-procedure


#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'reset.sqc'     !Reset Printer
