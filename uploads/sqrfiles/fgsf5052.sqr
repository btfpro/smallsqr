!***********************************************************************
!  FGSF5052:  Print Standard Form 50 or Standard Form 52               *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2006/07/19:12:19:15                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'      !Set environment

#define Year4      '1'   !Changing year to YYYY
#define DDelimiter '-'   !Changing date separator to dashes

Begin-Setup
#include 'ptset01.sqc'
End-Setup

Begin-Report

do Init-DateTime
do Init-Number
do Get-Current-DateTime
do Init-Report

do Process-Main

do Commit-Transaction
do Stdapi-Term

End-Report


!***********************************************************************
!                                                                      *
!  Function:     Init-Report                                           *
!                                                                      *
!  Description:  Initializes data for the beginning of the report      *
!                processing.                                           *
!                                                                      *
!***********************************************************************
Begin-Procedure Init-Report

move 'GHPARRPT'                to $ReportID
move 'SF50/SF52 Print Program' to $ReportTitle
!display $ReportTitle

let $first_form = 'Y'

do Stdapi-Init

do Select-PAR-Run-Control-Data

End-Procedure


!***********************************************************************
!                                                                      *
!  Function:     Process-Main                                          *
!                                                                      *
!  Description:  Performs the main processing of the program.          *
!                                                                      *
!***********************************************************************
Begin-Procedure Process-Main

move &run_cntl_par.gvt_form_type  to $gvt_form_type
move &run_cntl_par.gvt_wip_record to $gvt_wip_record
move &run_cntl_par.gvt_par_copy1  to $gvt_par_copy1
move &run_cntl_par.gvt_par_copy2  to $gvt_par_copy2
move &run_cntl_par.gvt_par_copy3  to $gvt_par_copy3
move &run_cntl_par.gvt_par_copy4  to $gvt_par_copy4

!if $gvt_wip_record = 'USFED'
    let $gvt_par_type = 'P'
!else
!    let $gvt_par_type = 'R'
!end-if

if  &run_cntl_par.gvt_par_prt_mthd  =  'O'
    do  Process-Online-Select-PAR-Print
else
    do  Process-Batch-Select-PAR-Print
end-if

End-Procedure


!***********************************************************************
!                                                                      *
!  Function:     Process-Online-Select-PAR-Print                       *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Process-Online-Select-PAR-Print

Begin-Select

SEL.EMPLID
SEL.EMPL_RCD
SEL.EFFDT
SEL.EFFSEQ
SEL.GVT_WIP_RECORD



    let  $emplid   =  &sel.emplid
    let  #empl_rcd =  &sel.EMPL_RCD
    let  $effdt    =  &sel.effdt
    let  #effseq   =  &sel.effseq
    let  $gvt_wip_record  = &sel.gvt_wip_record

!    if   $gvt_wip_record = 'USFED'
         let $gvt_par_type = 'P'
!    else
  !       let $gvt_par_type = 'R'
!    end-if

    do Process-Par-Nbr

from  PS_GVT_PAR_PRT   SEL

where SEL.OPRID       = $prcs_oprid
and   SEL.RUN_CNTL_ID = $prcs_run_cntl_id

End-Select

End-Procedure


!***********************************************************************
!                                                                      *
!  Function:     Process-Batch-Select-PAR-Print                        *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Process-Batch-Select-PAR-Print

evaluate  $gvt_par_type
    when  =  'P'
        let $record_alias = 'BJOB.'
        break
    when  =  'R'
        let $record_alias = 'BREC.'
        break
end-evaluate

do  Build-Batch-Where-Clause

evaluate  $gvt_par_type
    when  =  'P'
        do  Select-Batch-Job
        break
    when  =  'R'
        do  Select-Batch-Recruit
        break
end-evaluate

End-Procedure


!***********************************************************************
!                                                                      *
!  Function:     Build-Batch-Where-Clause                              *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Build-Batch-Where-Clause

let $batch_where1 = ' '
let $batch_where2 = ' '

!***********************************************************************
!                  Process Empl ID Data                                *
!***********************************************************************
if  rtrim(&run_cntl_par.emplid, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim (&run_cntl_par.emplid, ' ')  ||  '%'
    let $batch_where1 = $batch_where1    ||
                        $record_alias    ||
                        'EMPLID like ''' ||
                        $where_data      ||
                        ''' '

end-if

!***********************************************************************
!                  Process SSN Data                                    *
!***********************************************************************
if  rtrim(&run_cntl_par.ssn1, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim (&run_cntl_par.ssn1, ' ')  ||  '%'
    let $batch_where1 = $batch_where1         ||
                        $record_alias         ||
                        'SSN like '''         ||
                        $where_data           ||
                        ''' '

end-if

!***********************************************************************
!                  Process Name Data                                   *
!***********************************************************************
if  rtrim(&run_cntl_par.name1, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim (&run_cntl_par.name1, ' ')  ||  '%'
    let $batch_where1 = $batch_where1         ||
                        $record_alias         ||
                        'NAME like '''        ||
                        $where_data           ||
                        ''' '

end-if

!***********************************************************************
!                  Process NOA CODE                                    *
!***********************************************************************
if  rtrim(&run_cntl_par.gvt_noa_code, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim (&run_cntl_par.gvt_noa_code, ' ')  ||  '%'
    let $batch_where1 = $batch_where1         ||
                        $record_alias         ||
                        'GVT_NOA_CODE like '''        ||
                        $where_data           ||
                        ''' '

end-if

!***********************************************************************
!                  Process BUSINESS UNIT                               *
!***********************************************************************

if  rtrim(&run_cntl_par.business_unit, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim(&run_cntl_par.business_unit, ' ')  ||  '%'
    let $batch_where1 = $batch_where1         ||
                        $record_alias         ||
                        'BUSINESS_UNIT  like '''        ||
                        $where_data           ||
                        ''' '
end-if


!***********************************************************************
!                  Process Dept Data                                   *
!***********************************************************************
if  rtrim(&run_cntl_par.deptid, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim (&run_cntl_par.deptid, ' ')  ||  '%'
    let $batch_where1 = $batch_where1         ||
                        $record_alias         ||
                        'DEPTID like '''      ||
                        $where_data           ||
                        ''' '

end-if

!***********************************************************************
!                  Process Location Data                               *
!***********************************************************************
if  rtrim(&run_cntl_par.location, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim (&run_cntl_par.location, ' ')  ||  '%'
    let $batch_where1 = $batch_where1         ||
                        $record_alias         ||
                        'LOCATION like '''    ||
                        $where_data           ||
                        ''' '

end-if



!***********************************************************************
!                  Process Current PAR Status                          *
!***********************************************************************
if  rtrim(&run_cntl_par.gvt_par_cur_status, ' ') <> ''

    if $batch_where1 = ' '
        let $batch_where1 = ' where '
    else
        let $batch_where1 = $batch_where1  ||  ' and '
    end-if

    let $where_data   = rtrim (&run_cntl_par.gvt_par_cur_status, ' ')
    let $batch_where1  = $batch_where1               ||
                        $record_alias              ||
                        'GVT_PAR_CUR_STATUS = '''  ||
                        $where_data                ||
                        ''' '

end-if

!***********************************************************************
!                  Process Action Date Range                           *
!***********************************************************************
if  &run_cntl_par.gvt_actn_bgn_dt              <> ''
and &run_cntl_par.gvt_actn_end_dt              <> ''
and rtrim (&run_cntl_par.gvt_wip_status, ' ')  <> ''

    let $where_data1  = &run_cntl_par.gvt_wip_status
    let $where_data2  = &run_cntl_par.gvt_actn_bgn_dt
    let $where_data3  = &run_cntl_par.gvt_actn_end_dt

    let $batch_where2 = $batch_where2             ||
                        $record_alias             ||
                        'GVT_WIP_STATUS = '''     ||
                        $where_data1              ||
                        ''' and '                 ||
                        $record_alias             ||
                        'GVT_DATE_WRK between ''' ||
                        $where_data2              ||
                        ''' and '''               ||
                        $where_data3              ||
                        ''' '

end-if

!***********************************************************************
!                  Build Final Where Clause                            *
!***********************************************************************

if $batch_where2 = ' ' and
   $batch_where = $batch_where1
   if $gvt_par_type = 'P' and
      $select_record = 'PS_GVT_JOB_PRT_VW2'
   else
      let $select_record = 'PS_GVT_REC_PRT_VW2'
   end-if
else
if $batch_where2 <> ' '
  if $batch_where1 <> ' '
    let $batch_where = $batch_where1  ||  ' and '  ||  $batch_where2
  else
    let $batch_where = ' where '  ||  $batch_where2
  end-if
else
   let $batch_where = $batch_where1
end-if
   if $gvt_par_type = 'P'
      let $select_record = 'PS_GVT_JOB_PRT_VW2'
   else
      let $select_record = 'PS_GVT_REC_PRT_VW2'
   end-if
end-if

End-Procedure

!***********************************************************************
!                                                                      *
!  Function:     Select-Batch-Job                                      *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Select-Batch-Job

Begin-Select

BJOB.EMPLID
BJOB.EMPL_RCD
BJOB.EFFDT
BJOB.EFFSEQ

    let  $emplid   =  &bjob.emplid
    let  #empl_rcd =  &bjob.EMPL_RCD
    let  $effdt    =  &bjob.effdt
    let  #effseq   =  &bjob.effseq

    do Process-PAR-Nbr

from   [$select_record]    BJOB

[$batch_where]

End-Select

End-Procedure


!***********************************************************************
!                                                                      *
!  Function:     Select-Batch-Recruit                                  *
!                                                                      *
!  Description:                                                        *
!                                                                      *
!***********************************************************************
Begin-Procedure Select-Batch-Recruit

Begin-Select

BREC.JOB_REQ_NBR

    let  $job_requisition  =  &brec.JOB_REQ_NBR

    do Process-PAR-Nbr

from   [$select_record]    BREC

[$batch_where]

End-Select

End-Procedure


#include 'fgparctl.sqc'  !Get run control parameter values
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'tranctrl.sqc'  !Routines for Platform portable commit
#Include 'fgparprt.sqc'  !Routine to Print SF50 or SF52
