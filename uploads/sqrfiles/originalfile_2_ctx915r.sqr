!***********************************************************************
!  CTX915R:    PRINT RL-1 Summary Report                               *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2007/02/20:21:19:09                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  694792                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size init
#Include 'ctxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#define TAXFORM_BOX
#include 'rellang.sqc'

begin-heading 5
  #Include 'stdhdgtr.sqc'
  print $TaxYear           (3,,)      center
end-heading

begin-report

  do Init-Report

  do Process-Main

  close 4

#ifdef PRCSSCHD
  do StdAPI-Term
#endif

end-report

begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
#ifdef PRCSSCHD
  do StdAPI-Init
#endif
  do Initialization

end-procedure

begin-procedure Initialization

#define ColR 120

  move 'N' to $SummaryAtEnd

  do Get-Current-DateTime

  move '1' to $Year4
  move '-' to $DDelimiter
  do Format-DateTime($AsOfToday, $AsOfDateYMD, {DEFYMD},'','')
  move $AsOfDateYMD to $AsOfYear xxxx

  move $AsOfYear  to #AsOfYear
  subtract 1 from #AsOfYear

  do Get-Can-Tax-Processing-Params

  move &TX.Balance_Year to #TaxYear
  move #TaxYear to $TaxYear 9999

  let $AsOfDate = $TaxYear || '1231'
  do Format-DateTime($AsOfDate, $AsOfDate, {DEFCMP},'','native')

   if &TX.Balance_Year <> #AsOfYear and $Prcs_Process_Instance = ''

    display ''
    display 'Current Year is ' noline
    display $AsOfYear
    display 'Tax Reporting Year is ' noline
    display $TaxYear
    display 'Current Year is not one greater than Tax Reporting Year.'
    input $Answer maxlen=1 'Current Year is not one greater than Tax Reporting Year. Do you want to continue? (Y/N)'
    uppercase $Answer

    if $Answer <> 'Y'
      stop
    end-if
  end-if

  do Open-Files

  move 'CTX915R' to $ReportID
  do Get-Report-Language          ! Procedure that decides the report language
                                  ! and then calls 'Report-Translation' procedure
                                  ! that reads the language dep. text.

  move $Ctx915r_Title_rv1_summ to $ReportTitle
  display 'PRINTING RL-1 SUMMARY DATA '

  do Array-Create
  do Load-Box-Arrays
  do Format-Messages

end-procedure

!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
!Read language-dependent text from the database.

begin-procedure Report-Translation        ! Read from CTX915R  program ids

   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('CTX915R', 'TITLE_RV1_SUMM' , $Ctx915r_Title_rv1_summ , #dummy_width)
   do Get_Field_Information ('CTX915R', 'RV1_SUMM_ORIG'  , $Ctx915r_Rv1_summ_orig  , #dummy_width)
   do Get_Field_Information ('CTX915R', 'RV1_SUMM_AMEND' , $Ctx915r_Rv1_summ_amend , #dummy_width)
   do Get_Field_Information ('CTX915R', 'RV1_SUMM_CANCEL', $Ctx915r_Rv1_summ_cancel, #dummy_width)
   do Get_Field_Information ('CTX915R', 'EMPLER_NAME'    , $Ctx915r_Empler_name    , #dummy_width)
   do Get_Field_Information ('CTX915R', 'EMPLER_QID'     , $Ctx915r_Empler_qid     , #dummy_width)
   do Get_Field_Information ('CTX915R', 'TAX_YEAR'       , $Ctx915r_Tax_year       , #dummy_width)
   do Get_Field_Information ('CTX915R', 'TOT_RV_SLIPS'   , $Ctx915r_Tot_rv_slips   , #dummy_width)

end-procedure

begin-procedure Open-Files

    open '{IMPORTPREFIX}CTX910R1{IMPORTSUFFIX}' as 4 for-reading record=325

end-procedure


begin-procedure Array-Create

  create-array name=PrR1_Area size=200 -
    field=PR1_Box:char                 -
    field=PR1_Descr:char

end-procedure

begin-procedure Load-Box-Arrays

    move 0 to #i

begin-SELECT
M.TAXFORM_ID
M.BOX
M.DESCR
M.EFFDT

   let $M.BOX = RTRIM(&M.BOX, ' ')
   let $TAXFORM_BOX-DESCR = &M.DESCR

   do Get_Related_TAXFORM_BOX(&M.BOX,&M.EFFDT,&M.TAXFORM_ID)

   put $M.Box $TAXFORM_BOX-DESCR into PrR1_Area(#i)
         PR1_Box PR1_Descr
       add 1 to #i
       move #i to #max_R1boxes


FROM PS_TAXFORM_BOX M
WHERE TAXFORM_ID = 'R' AND
      EFFDT = (SELECT MAX(EFFDT)
               FROM PS_TAXFORM_BOX
               WHERE TAXFORM_ID = 'R'
                 AND EFFDT <= $AsOfDate)
ORDER BY M.BOX ASC
end-SELECT

end-procedure

begin-procedure Format-Messages

  move 0 to #i

  while #i <= #max_R1boxes

        get $PrBox $PrDescr from PrR1_Area(#i)
                   PR1_Box PR1_Descr
        let $MsgBox = RTRIM($PrBox, ' ')
        let $MsgSum   = RTRIM($PrDescr, ' ')

        evaluate $MsgBox
        when = 'A'
           let $SumA = $MsgSum
           break
        when = 'B'
           let $SumB = $MsgSum
           break
        when = 'C'
           let $SumC = $MsgSum
           break
        when = 'D'
           let $SumD = $MsgSum
           break
        when = 'E'
           let $SumE = $MsgSum
           break
        when = 'F'
           let $SumF = $MsgSum
           break
        when = 'G'
           let $SumG = $MsgSum
           break
        when = 'H'
           let $SumH = $MsgSum
           break
        when = 'I'
           let $SumI = $MsgSum
           break
        when = 'J'
           let $SumJ = $MsgSum
           break
        when = 'K'
           let $SumK = $MsgSum
           break
        when = 'L'
           let $SumL = $MsgSum
           break
        when = 'M'
           let $SumM = $MsgSum
           break
        when = 'N'
           let $SumN = $MsgSum
           break
        when = 'O'
           let $SumO = $MsgSum
           break
        when = 'P'
           let $SumP = $MsgSum
           break
        when = 'Q'
           let $SumQ = $MsgSum
           break
        when = 'R'
           let $SumR = $MsgSum
           break
        when = 'S'
           let $SumS = $MsgSum
           break
        when = 'T'
           let $SumT = $MsgSum
           break
        when = 'U'
           let $SumU = $MsgSum
           break
        when = 'V'
           let $SumV = $MsgSum
           break
        when = 'W'
           let $SumW = $MsgSum
           break
        when-other
           break

        end-evaluate

        add 1 to #i

  end-while

end-procedure


begin-procedure Process-Main

  while 1 = 1

   if $SummaryAtEnd = 'Y'
      break
   else
      do Read-Summary
      if $SummaryAtEnd <> 'Y'
         do Print-Summary
      end-if
   end-if
  end-while

end-procedure

begin-procedure Read-Summary

  read 4 into $SumInput:325

  if #end-file = 1
    move 'Y' to $SummaryAtEnd
  else
    let $Employer_Name        = substr($SumInput,1,30)  !  1-30
    let $TaxYear              = substr($SumInput,31,4)  ! 31-34
    let $TotalSupplementary   = substr($SumInput,35,7)  ! 35-41
    let $TotalIncome          = substr($SumInput,42,13) ! 42-54  BoxA
    let $TotalEeQPPContrib    = substr($SumInput,55,11) ! 55-65  BoxB
    let $TotalEeUIPrem        = substr($SumInput,66,11) ! 66-76  BoxC
    let $TotalRegPPContrib    = substr($SumInput,77,11) ! 77-87  BoxD
    let $TotalIncomeTax       = substr($SumInput,88,13) ! 88-100 BoxE
    let $TotalUnionDues       = substr($SumInput,101,11)!101-111 BoxF
    let $TotalPensionable_Earns = substr($SumInput,112,13) !112-124 BoxG
    let $TotalQPIPPrem        = substr($SumInput,125,11)!125-135 BoxH
    let $TotalQPIPInsEarn     = substr($SumInput,136,13)!136-148 BoxI
    let $TotalPrivHealthContrib = substr($SumInput,149,11) !149-159 BoxJ
    let $TotalTravel          = substr($SumInput,160,11)!160-170 BoxK
    let $TotalOtherBenefits   = substr($SumInput,171,11)!171-181 BoxL
    let $TotalCommissions     = substr($SumInput,182,11)!182-192 BoxM
    let $TotalDonations       = substr($SumInput,193,11)!193-203 BoxN
    let $TotalOtherIncome     = substr($SumInput,204,11)!204-214 BoxO
    let $TotMultiEmplAmt      = substr($SumInput,215,11)!215-225 BoxP
    let $TotDeferredPay       = substr($SumInput,226,11)!226-236 BoxQ
    let $TotIndian            = substr($SumInput,237,11)!237-247 BoxR
    let $TotTip               = substr($SumInput,248,11)!248-258 BoxS
    let $TotTipsAllocated     = substr($SumInput,259,11)!259-269 BoxT
    let $TotPhasedRetire      = substr($SumInput,270,11)!270-280 BoxU
    let $TotalAccom           = substr($SumInput,281,11)!281-291 BoxV
    let $TotalUseErAuto       = substr($SumInput,292,11)!292-302 BoxW
    let $ReportingID          = substr($SumInput,303,16)!303-318 NoBox
    let $ReportType           = substr($SumInput,319,1) !319-319 original/amended


    move $TotalSupplementary       to #TotalSupplementary
    move $TotalIncome              to #TotalIncome
    move $TotalEeQPPContrib        to #TotalEeQPPContrib
    move $TotalEeUIPrem            to #TotalEeUIPrem
    move $TotalRegPPContrib        to #TotalRegPPContrib
    move $TotalIncomeTax           to #TotalIncomeTax
    move $TotalUnionDues           to #TotalUnionDues
    move $TotalPensionable_Earns   to #TotalPensionable_Earns
    move $TotalQPIPPrem            to #TotalQPIPPrem
    move $TotalQPIPInsEarn         to #TotalQPIPInsEarn
    move $TotalPrivHealthContrib   to #TotalPrivHealthContrib
    move $TotalTravel              to #TotalTravel
    move $TotalOtherBenefits       to #TotalOtherBenefits
    move $TotalCommissions         to #TotalCommissions
    move $TotalDonations           to #TotalDonations
    move $TotalOtherIncome         to #TotalOtherIncome
    move $TotMultiEmplAmt          to #TotMultiEmplAmt
    move $TotDeferredPay           to #TotDeferredPay
    move $TotIndian                to #TotIndian
    move $TotTip                   to #TotTip
    move $TotTipsAllocated         to #TotTipsAllocated
    move $TotPhasedRetire          to #TotPhasedRetire
    move $TotalAccom               to #TotalAccom
    move $TotalUseErAuto           to #TotalUseErAuto

  end-if



end-procedure

begin-procedure Print-Summary

     evaluate $ReportType
     when = 'O'
       print   $Ctx915r_Rv1_summ_orig      (+1,1)
       break
     when = 'A'
       print   $Ctx915r_Rv1_summ_amend     (+1,1)
       break
     when = 'C'
       print   $Ctx915r_Rv1_summ_cancel    (+1,1)
       break
     when-other
       break
     end-evaluate

     print    $Ctx915r_Empler_name         (+1,1)
     print    $Employer_Name               (0,40)
     print    $Ctx915r_Empler_qid          (+1,1)
     print    $ReportingID                 (0,40)
     print    $Ctx915r_Tax_year            (+1,1)
     print    $TaxYear                     (0,40)
     print    $Ctx915r_Tot_rv_slips        (+1,1)
     print    #TotalSupplementary          (0,40)  edit 9999999999
     print    $SumA                        (+1,1)
     print    #TotalIncome                 (0,40)  edit 9999999999.99
     print    $SumB                        (+1,1)
     print    #TotalEeQPPContrib           (0,40)  edit 9999999999.99
     print    $SumC                        (+1,1)
     print    #TotalEeUIPrem               (0,40)  edit 9999999999.99
     print    $SumD                        (+1,1)
     print    #TotalRegPPContrib           (0,40)  edit 9999999999.99
     print    $SumE                        (+1,1)
     print    #TotalIncomeTax              (0,40)  edit 9999999999.99
     print    $SumF                        (+1,1)
     print    #TotalUnionDues              (0,40)  edit 9999999999.99
     print    $SumG                        (+1,1)
     print    #TotalPensionable_Earns      (0,40)  edit 9999999999.99
     print    $SumH                        (+1,1)
     print    #TotalQPIPPrem               (0,40)  edit 9999999999.99
     print    $SumI                        (+1,1)
     print    #TotalQPIPInsEarn            (0,40)  edit 9999999999.99
     print    $SumJ                        (+1,1)
     print    #TotalPrivHealthContrib      (0,40)  edit 9999999999.99
     print    $SumK                        (+1,1)
     print    #TotalTravel                 (0,40)  edit 9999999999.99
     print    $SumL                        (+1,1)
     print    #TotalOtherBenefits          (0,40)  edit 9999999999.99
     print    $SumM                        (+1,1)
     print    #TotalCommissions            (0,40)  edit 9999999999.99
     print    $SumN                        (+1,1)
     print    #TotalDonations              (0,40)  edit 9999999999.99
     print    $SumO                        (+1,1)
     print    #TotalOtherIncome            (0,40)  edit 9999999999.99
     print    $SumP                        (+1,1)
     print    #TotMultiEmplAmt             (0,40)  edit 9999999999.99
     print    $SumQ                        (+1,1)
     print    #TotDeferredPay              (0,40)  edit 9999999999.99
     print    $SumR                        (+1,1)
     print    #TotIndian                   (0,40)  edit 9999999999.99
     print    $SumS                        (+1,1)
     print    #TotTip                      (0,40)  edit 9999999999.99
     print    $SumT                        (+1,1)
     print    #TotTipsAllocated            (0,40)  edit 9999999999.99
     print    $SumU                        (+1,1)
     print    #TotPhasedRetire             (0,40)  edit 9999999999.99
     print    $SumV                        (+1,1)
     print    #TotalAccom                  (0,40)  edit 9999999999.99
     print    $SumW                        (+1,1)
     print    #TotalUseErAuto              (0,40)  edit 9999999999.99

     new-page

end-procedure


#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getdptnm.sqc'  !Get-Department-Name
#ifdef PRCSSCHD
#Include 'ctxrctl1.sqc' ! Get-Can-Tax YE Report Parameters
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Process Scheduler Run Controls
#endif
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
