!***********************************************************************
! UVPYT05: Selects and prints data from the ADDL_PAY_DATA table for    *
!  1099R Retirees with an End Date where the maximum EFFDT is after    *
!  the retirement date.  This means that some of data belong to the    *
!  employment history and some to the retirement history.  This data   *
!  is too cumbersome to convert.  Therefore, our recommendation is     *
!  to manually convert the data by assigning the next available        *
!  EMPL_RCD value for the retiree to the retirement rows.              *
!***********************************************************************
! Report Name:  UVPYT05.SQR - 1099R Upgrade Conversion Report          *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!----------------------------------------------------------------------
!
!          $Date:  2007/05/01:15:02:50                                   !
!       $Release:  HR9                                                   !
!    $Resolution:  706685                                                !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   !Printer and page-size initialization

!***********************************************************************

begin-program
        do Init-DateTime
        do Get-Current-DateTime
        do Init-Report
        display 'Report began at: ' NoLine
        do Display-Time
        do Process-Main
        do Reset
        do Stdapi-Term
        display 'Report ended at: ' NoLine
        do Display-Time
end-program

begin-heading 7
#include 'stdhdg01.sqc'

 PRINT 'EMPLID'                  (+1,1,11)
 PRINT 'EMPL_RCD'                (0,+2,+10)
 PRINT 'ERNCD'                   (0,+2,+8)
 PRINT 'EFFDT'                   (0,+2,+10)
 PRINT 'EARNINGS_END_DT'         (0,+4,+15)
 PRINT 'Retirement Dt'           (0,+5,+13)

end-heading

!***********************************************************************
!  Function:     Init Report
!  Description:  Prompt for run-time parameters
!***********************************************************************

begin-procedure Init-Report

  move 'UVPYT05' to $ReportID
  move '1099R ADDL_PAY_DATA after Retirement with End Date' to $ReportTitle
  display $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
    !No Prompt
  end-if

end-procedure Init-Report

begin-procedure Get-Values
!No Prompts
end-procedure Get-Values

!***********************************************************************
!  Function:     Process-Main
!  Description:  This is the main report procedure.  Calls select routines
!                for data in the ADDL_PAY_DATA table.
!***********************************************************************

begin-procedure Process-Main

  do GET-RET_ADDL_PAY_ERNCD

end-procedure Process-Main



!***********************************************************************
!  Procedure:    GET-RET_ADDL_PAY_ERNCD
!  Description:  This is the main report procedure.  Selects and prints
!                data from the ADDL_PAY_DATA table for 1099R retirees 
!                having an EARNINGS_END_DT and a maximum EFFDT after 
!                the retirement date.  The conversion process will not
!                modify the data for these rows.
!***********************************************************************

begin-procedure GET-RET_ADDL_PAY_ERNCD
 let #COUNTER = 0

begin-SELECT ON-ERROR=SQL-Error
A.EMPLID                                (+1,1,11)
A.EMPL_RCD                              (0,+2,+10)
A.ERNCD                                 (0,+2,+8)
A.EFFDT                    
  do Format-DateTime(&A.EFFDT, $out, {DEFDATE}, '', '')
  print $out                    (0,+2,10)
A.EARNINGS_END_DT        
  do Format-DateTime(&A.EARNINGS_END_DT, $out, {DEFDATE}, '', '')
  print $out                    (0,+4,10)
B.EFFDT                      
  do Format-DateTime(&B.EFFDT, $out, {DEFDATE}, '', '')
  print $out                    (0,+10,10)

 let #COUNTER = #COUNTER + 1

FROM PS_ADDL_PAY_DATA A
, PS_JOB B
, PS_COMPANY_TBL C
WHERE A.EMPLID = B.EMPLID
AND A.EMPL_RCD = B.EMPL_RCD
AND B.COMPANY = C.COMPANY
AND A.EFFDT = (SELECT MAX(A1.EFFDT)
  FROM PS_ADDL_PAY_DATA A1
  WHERE A1.EMPLID = A.EMPLID
  AND A1.EMPL_RCD = A.EMPL_RCD
  AND A1.ERNCD = A.ERNCD)
AND C.EFFDT = (SELECT MAX(C1.EFFDT)
  FROM PS_COMPANY_TBL C1
  WHERE C1.COMPANY = C.COMPANY
  AND C1.EFFDT <= $AsOfToday)
AND C.EFF_STATUS = 'A'
AND C.TAX_REPORT_TYPE = 'R'
AND B.EFFDT = (SELECT MIN(B1.EFFDT)
  FROM PS_JOB B1
  WHERE B1.EMPLID = B.EMPLID
  AND B1.COMPANY = B.COMPANY)
AND A.EARNINGS_END_DT IS NOT NULL
AND A.EFFDT > B.EFFDT

END-SELECT

 let $COUNTER = EDIT(#COUNTER, '999')

 print 'Number of rows selected:  '                      (+2,1,26)
 print $COUNTER                                          (,+1,)

end-procedure GET-RET_ADDL_PAY_ERNCD


!*************************************
Begin-Procedure Display-Time
!*************************************

  do Get-Current-DateTime
  display $AsOfToday NoLine
  display ', '       NoLine
  display $AsOfNow
  display ''

End-Procedure

!*************************************


!***********************************************************************
! SQC Files for called procedures
!***********************************************************************
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Routines to update run status
#include 'prcsapi.sqc'   !Update Process API
#Include 'reset.sqc'     !Reset printer procedure


