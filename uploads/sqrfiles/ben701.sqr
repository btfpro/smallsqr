!***********************************************************************
!  BEN701: Flexible Spending Account Table                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:08:46:33                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#define FREQUENCY_TBL
#include 'setenv.sqc'   !Set environment
#include 'rellang.sqc'  !Related Language File


Begin-Setup
  #Include 'ptpsl177.sqc'
End-Setup

#define col_sep                  1   !Column Separator
#define col_plan_type            5   !Plana Type
#define col_plan_name           11   !Plan Name
#define col_ben_plan_id          9   !Benefit Plan ID
#define col_ben_plan_name       31   !Benefit Plan Name
#define col_eff_date            11   !Effective Date
#define col_pay_friq            11   !Pay Friquency
#define col_emplr_contr         14   !Employer Contribution
#define col_flat_amt             8   !Falt Amount
#define col_emplr_limit          9   !Employer Limit %
#define col_emple_contr         14   !% of Employee Contribution
#define col_emplr_limit_amt     15   !Employer Limit Amount

!***********
begin-program
!***********
  do Init-Report
  do Get-Report-Language
  let $ReportTitle  = $BEN701-REPORTTITLE
!-- 'FLEXIBLE SPENDING ACCOUNT TABLE'
  display ' '
  display $ReportTitle
  display ' '

  do Process-Main

  do Reset
  do StdAPI-Term
end-program

!**************************
begin-procedure Init-Report
!**************************
  do Init-DateTime
  do Init-Number
  let $ReportID = 'BEN701'

  do Get-Current-DateTime
  do StdAPI-Init
end-procedure

!**********************************
! This procedure is used to read language-dependent text from the database.
!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BEN701 program ids
  do Init_Report_Translation($ReportID, $Language_Cd)
  do Append_Report_Translation('BEN1')

  do Get_Field_Information ('BEN701', 'REPORT_TITLE', $BEN701-REPORTTITLE, #CW)
  do Get_Field_Information ('BEN701', 'OF_EMPLOYEE',  $BEN701-OF-EMPLOYEE, #CW)
  do Get_Field_Information ('BEN701', 'LIM_AMT',      $BEN701-LIM-AMT,     #CW)
  do Get_Field_Information ('BEN701', 'LIMIT',        $BEN701-LIMIT,       #CW)
  do Get_Field_Information ('BEN701', 'CONTRIB',      $BEN701-CONTRIB,     #CW)

  do Get_Field_Information ('BEN1',   'EMPLOYER',     $BEN1-EMPLOYER,      #CW)
  do Get_Field_Information ('BEN1',   'NAME',         $BEN1-NAME,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN',         $BEN1-PLAN,          #CW)
  do Get_Field_Information ('BEN1',   'BENEFIT',      $BEN1-BENEFIT,       #CW)
  do Get_Field_Information ('BEN1',   'EFFECTIVE',    $BEN1-EFFECTIVE,     #CW)
  do Get_Field_Information ('BEN1',   'TYPE',         $BEN1-TYPE,          #CW)
  do Get_Field_Information ('BEN1',   'PAY',          $BEN1-PAY,           #CW)
  do Get_Field_Information ('BEN1',   'FLAT',         $BEN1-FLAT,          #CW)
  do Get_Field_Information ('BEN1',   'PLAN_ID',      $BEN1-PLAN-ID,       #CW)
  do Get_Field_Information ('BEN1',   'PLAN_NAME',    $BEN1-PLAN-NAME,     #CW)
  do Get_Field_Information ('BEN1',   'DATE',         $BEN1-DATE,          #CW)
  do Get_Field_Information ('BEN1',   'AMT',          $BEN1-AMT,           #CW)
  do Get_Field_Information ('BEN1',   'FREQ_TYPE',    $BEN1-FREQUENCY,     #CW)

End-Procedure

!***************
begin-HEADING 7
!***************
  #Include 'stdhdgtr.sqc'

!Column Heading line 1
  print $BEN1-PLAN          (+1,1,{col_plan_type})
  print $BEN1-PLAN          (0,+{col_sep},{col_plan_name})
  print $BEN1-BENEFIT       (0,+{col_sep},{col_ben_plan_id})
  print $BEN1-BENEFIT       (0,+{col_sep},{col_ben_plan_name})
  print $BEN1-EFFECTIVE     (0,+{col_sep},{col_eff_date})
  print $BEN1-PAY           (0,+{col_sep},{col_pay_friq})
  print $BEN1-EMPLOYER      (0,+{col_sep},{col_emplr_contr})
  print $BEN1-FLAT          (0,+{col_sep},{col_flat_amt})
  print $BEN1-EMPLOYER      (0,+{col_sep},{col_emplr_limit})
  print $BEN701-OF-EMPLOYEE (0,+{col_sep},{col_emple_contr})
  print $BEN1-EMPLOYER      (0,+{col_sep},{col_emplr_limit_amt})

!Column Heading line 2
  print $BEN1-TYPE          (+1,1,{col_plan_type})
  print $BEN1-NAME          (0,+{col_sep},{col_plan_name})
  print $BEN1-PLAN-ID       (0,+{col_sep},{col_ben_plan_id})
  print $BEN1-PLAN-NAME     (0,+{col_sep},{col_ben_plan_name})
  print $BEN1-DATE          (0,+{col_sep},{col_eff_date})
  print $BEN1-FREQUENCY     (0,+{col_sep},{col_pay_friq})
  print $BEN701-CONTRIB     (0,+{col_sep},{col_emplr_contr})
  print $BEN1-AMT           (0,+{col_sep},{col_flat_amt})
  print $BEN701-LIMIT       (0,+{col_sep},{col_emplr_limit})
  print $BEN701-CONTRIB     (0,+{col_sep},{col_emple_contr})
  print $BEN701-LIM-AMT     (0,+{col_sep},{col_emplr_limit_amt})

!Column Heading line 3
  print '='          (+1,1,{col_plan_type})               FILL
  print '='          (0,+{col_sep},{col_plan_name})       FILL
  print '='          (0,+{col_sep},{col_ben_plan_id})     FILL
  print '='          (0,+{col_sep},{col_ben_plan_name})   FILL
  print '='          (0,+{col_sep},{col_eff_date})        FILL
  print '='          (0,+{col_sep},{col_pay_friq})        FILL
  print '='          (0,+{col_sep},{col_emplr_contr})     FILL
  print '='          (0,+{col_sep},{col_flat_amt})        FILL
  print '='          (0,+{col_sep},{col_emplr_limit})     FILL
  print '='          (0,+{col_sep},{col_emple_contr})     FILL
  print '='          (0,+{col_sep},{col_emplr_limit_amt}) FILL


end-HEADING

!***********************************************************************
!  PROC Process Main
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

B.PLAN_TYPE                   (+1,1,4)

  move 'PLAN_TYPE' to $FieldName
  move &B.Plan_Type to $FieldValue
  do Read-Translate-Table
  print $XlatShortName      (0,+2,10)

B.BENEFIT_PLAN                (0,+2,8)

  move &B.Benefit_Plan to $Benefit_Plan
  move &B.Plan_Type    to $Plan_Type
  do Get-Benefit-Name
  print $BenefitName        (0,+2,30)

B.EFFDT  &B.EffDt
  do Format-DateTime(&B.EffDt, $out, {DEFDATE}, '', '')
  print $out                (0,+2,10)
F.EFFDT
F.FREQUENCY_ID
F.DESCRSHORT

   move &F.DESCRSHORT    to $FREQUENCY_TBL-DESCRSHORT
   do Get_Related_FREQUENCY_TBL(&F.EFFDT,&F.FREQUENCY_ID)
   print $FREQUENCY_TBL-DESCRSHORT  (0,+2,10)

B.EMPLR_CONTRBUTN
  move 'EMPLR_CONTRBUTN' to $FieldName
  move &B.Emplr_Contrbutn  to $FieldValue
  do Read-Translate-Table
  print $XlatShortName      (0,+2,12)
B.FLAT_CONTRB_AMT
  do Format-Number(&B.Flat_Contrb_Amt, $out, 'b999.99')
  print $out                (0,+2,7)
B.EMPLR_CONTRB_PCT
  do Format-Number(&B.Emplr_Contrb_Pct, $out, 'b99.999')
  print $out                (0,+3,7)
B.PCT_EMPL_CONTRBUTN
  do Format-Number(&B.Pct_Empl_Contrbutn, $out, 'b99.999')
  print $out                (0,+8,7)
B.EMPLR_CONTRB_AMT
  do Format-Number(&B.Emplr_Contrb_Amt, $out, 'b999.99')
  print $out                (0,+7,7)

FROM PS_FSA_BENEF_TBL B,
     PS_FREQUENCY_TBL F
WHERE B.PAY_FREQUENCY = F.FREQUENCY_ID
  AND F.EFF_STATUS       = 'A'
  AND F.EFFDT            =
    (SELECT MAX(F1.EFFDT)
       FROM PS_FREQUENCY_TBL F1
      WHERE F1.FREQUENCY_ID   = F.FREQUENCY_ID
        AND F1.EFF_STATUS     = F.EFF_STATUS
        AND F1.EFFDT         <= B.EFFDT)

ORDER BY B.PLAN_TYPE, B.BENEFIT_PLAN, B.EFFDT DESC

END-SELECT
END-PROCEDURE

!******************************************************************
#Include 'getbennm.sqc'  !Get-Benefit_Plan-Name procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!******************************************************************
