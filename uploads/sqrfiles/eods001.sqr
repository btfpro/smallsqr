!***********************************************************************
! EO_DS001: Directory Audit Report                                     *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:10:33:48                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

!begin-setup
#include 'setup01.sqc'         ! Init printer and page size
!end-setup

begin-report
    do Initialize
    do Report
    do Terminate
    do Commit-Transaction
end-report

begin-procedure Initialize            ! Get Source Database Information
    do Init-DateTime
    do Init-Number
    do Define-Prcs-Vars
    do Get-Run-Control-Parms
    do Get-MapName
    move 'EO_DS001' to $ReportID
    move 'Directory Audit Report' to $ReportTitle
#ifdef debugx
    SHOW 'Report Title: ' $ReportTitle
#endif
    do Get-Current-DateTime
end-procedure

begin-procedure Get-MapName
begin-SELECT
DSMAPNAME &DSMAPNAME
FROM  PS_EO_RUN_CNTL_DS
WHERE OPRID = $prcs_oprid
AND   RUN_CNTL_ID = $prcs_run_cntl_id
end-SELECT
end-procedure




begin-procedure Terminate
    if #prcs_process_instance > 0
        do Update-Prcs-Run-Status
    end-if
    do Reset
end-procedure

begin-heading 5
   #include 'stdhdg01.sqc'
  print 'Map Name: ' (+1,1)
  print &DSMAPNAME   (0,+1)
end-heading

begin-procedure Report          ! Main report processing
begin-SELECT
COM.DSDN          &DSDN
COM.DSATTRIBUTE   &DSATTR
COM.DSVALUE       &DSVAL
COM.DS_ERROR      &ERROR

  let $DSDN = &DSDN
  let $DSDN = RTRIM($DSDN, ' ')
  let $DSATTR = &DSATTR
  let $DSATTR = RTRIM($DSATTR, ' ')
  let $DSVAL = &DSVAL
  let $DSVAL = RTRIM($DSVAL, ' ')
  let $ERROR = &ERROR
  let $ERROR = RTRIM($ERROR, ' ')

  print 'Directory DN: '           (+1,1)
  print $DSDN                      (0,22)
  print 'Directory Attribute: '    (+1,1)
  print $DSATTR                    (0,22)
  print 'Directory Value: '        (+1,1)
  print $DSVAL                     (0,22)
  print 'Error: '                  (+1,1)
  print $ERROR                      (0,22)
  move 'DS_ERROR' to $FieldName
  move $ERROR  to $FieldValue
  do   Read-Translate-Table
  print $XlatLongName               (0,+3)
  print '-'                         (+1,1,125) fill


FROM PS_EO_DSCOMPARE COM
WHERE COM.DSMAPNAME = &DSMAPNAME
ORDER BY COM.DSDN, COM.DSATTRIBUTE, COM.DSVALUE, COM.DS_ERROR
end-SELECT
end-procedure

!***********************************************************************
! Function:    Start_New_Page                                          *
!                                                                      *
! Description: Forces to a new page.  Used when Language Code changes. *
!                                                                      *
!***********************************************************************
begin-procedure Start_New_Page
   new-page
end-procedure


#include 'reset.sqc'           !Reset printer
#include 'curdttim.sqc'        !Current Date and Time
#Include 'datetime.sqc'        !Routines for date and time formatting
#Include 'number.sqc'          !Routines to format numbers
#include 'prcsapi.sqc'         !Update Process Request API
#include 'prcsdef.sqc'         !Update Process Request variable declaration
#include 'tranctrl.sqc'        !Transaction control
#include 'readxlat.sqc'        ! Read Translate Table
