!**********************************************************************!
!  gpusw2so:  Control W2 Sort                                          !
!**********************************************************************!
!                                                                      !
!               Confidentiality Information:                           !
!                                                                      !
!                                                                      !
! This module contains confidential and proprietary information        !
! of Oracle; it is not to be copied, reproduced, or transmitted        !
! in any form, by any means, in whole or in part, nor is it to         !
! be used for any purpose other than that for which it is              !
! expressly provided under the applicable license agreement.           !
!                                                                      !
! Copyright (C) 2006 Oracle. All Rights Reserved.                      !
!                                                                      !
!**********************************************************************!
!                                                                      !
!          $Date:  2006/07/21:02:02:05                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      !
!**********************************************************************!

#Include 'usarpt.sqc'      !Get USA variables
Begin-Procedure W2-Sort

  evaluate $SortCode1
    when = '40'
      move 'PayGroup: '     to $SortField
      DO W2-Sort-PG&DEPT
    when = '20'
      move 'Dept ID:  '     to $SortField
      Do W2-Sort-PG&DEPT
    when = '30'
      move 'Mail Drop:'     to $SortField
      do GET-MAIL-DROP
  end-evaluate
  move '' to $priorSortValue
End-Procedure

Begin-Procedure W2-Sort-PG&DEPT
  move 'AND T.EMPLID NOT IN (''' to $T.SelectedEEs
  let $NO_EMPL = 'Y'

Begin-Select
J.GP_PAYGROUP
J.DEPTID
S.EMPLID
S.GPUS_YE_LNAME
S.SSN

  move &S.Emplid to $Emplid
  let $NO_EMPL = 'n'

  evaluate $SortCode1
    when = '40'
      move &J.GP_PayGroup      to $SortValue
    when = '20'
      move &J.DeptID        to $SortValue

  end-evaluate
  if $SortValue <> $priorSortValue
    if $priorSortValue <> '' and $SortEmployeeFound = 'Y'
      NEW-PAGE
      move 'N' to $SortEmployeeFound
    end-if
      move $SortValue to $priorSortValue
      move $priorSortValue to $priorSortValueYE30
  end-if
  let $T.SelectedEEs = $T.SelectedEEs ||  &S.EMPLID || ''','''


  let $E.SelectedEEs = 'AND E.EMPLID = ''' || &S.EMPLID || ''''
  do Read-Employee-Data

FROM  PS_GPUS_YE_EE5_VW S,
      PS_JOB J
WHERE  S.GPUS_TAX_EIN = &A.GPUS_TAX_EIN
  AND   J.EMPLID = S.EMPLID
  AND J.EFFDT = (SELECT MAX(JJ.EFFDT) FROM   PS_JOB JJ WHERE  JJ.EMPLID =  J.EMPLID
         AND JJ.EFFDT   <= $ASOFDATE)
  AND J.EMPL_RCD = (SELECT MAX(JJ1.EMPL_RCD) FROM   PS_JOB JJ1 WHERE  JJ1.EMPLID = J.EMPLID
        AND  JJ1.EFFDT  <= $ASOFDATE)
  AND J.EFFSEQ = (SELECT MAX(JJJ.EFFSEQ) FROM   PS_JOB JJJ WHERE  JJJ.EMPLID  = J.EMPLID
        AND  JJJ.EMPL_RCD = J.EMPL_RCD AND  JJJ.EFFDT <= $ASOFDATE )
  [$S.SelectedEEs]
  [$fromtoS]           !NULL string if STREAM NUMBER option not used
  [$Sort1]

End-Select
 if $NO_EMPL = 'n'
        let $T.SelectedEEs = SUBSTR($T.SelectedEEs,1,LENGTH($T.SelectedEEs) - 2)
        let $T.SelectedEEs = $T.SelectedEEs || ')'
 else
 let $T.SelectedEEs = ''
 end-if

Begin-Select
T.EMPLID
T.SSN
T.GPUS_YE_LNAME

   let $E.SelectedEEs = 'AND E.EMPLID = ''' || &T.EMPLID || ''''
   do Read-Employee-Data

FROM PS_GPUS_YE_EE5_VW T
where T.GPUS_CAL_YEAR    = &TX.CALENDAR_YEAR
  AND T.GPUS_TAX_EIN   = &A.GPUS_TAX_EIN
  [$T.SelectedEmpl]
  [$T.SelectedEEs]          !NULL string if SELECT EEs option not used
  [$fromtoT]                !NULL string if STREAM NUMBER option not used
  [$Sort3]
End-Select

End-Procedure

Begin-Procedure GET-MAIL-DROP

   move 'AND T1.EMPLID NOT IN (''' to $T1.SelectedEEs
   let $NO_EMPLOYEE = 'Y'
Begin-Select
G.EMPLID
K.GP_PSLP_MAIL_DROP
H.POSITION_NBR
G.GPUS_YE_LNAME
G.SSN
     if &K.GP_PSLP_MAIL_DROP <> ' '
        move &K.GP_PSLP_MAIL_DROP to $Mail_Drop_Hold
     else
        do GET-MAIL-DROP-FROM-POSITION-DATA
     end-if
     move &G.Emplid to $Emplid
     let $NO_EMPLOYEE = 'N'
     move $Mail_Drop_Hold  to $SortValue
     if $SortValue <> $priorSortValue
    if $priorSortValue <> '' and $SortEmployeeFound = 'Y'
      NEW-PAGE
      move 'N' to $SortEmployeeFound
    end-if
      move $SortValue to $priorSortValue
      move $priorSortValue to $priorSortValueYE30
  end-if
  let $E.SelectedEEs = 'AND E.EMPLID = ''' || &G.EMPLID || ''''

  let $T1.SelectedEEs = $T1.SelectedEEs ||  &G.EMPLID || ''','''


  do Read-Employee-Data

FROM  PS_GPUS_YE_EE5_VW G,
      PS_GP_PSLP_PYE_OPT  K,
      PS_JOB H
WHERE  G.GPUS_TAX_EIN = &A.GPUS_TAX_EIN
  AND H.EMPLID = G.EMPLID
  AND H.EFFDT = (SELECT MAX(JJ.EFFDT) FROM   PS_JOB JJ WHERE  JJ.EMPLID =  H.EMPLID
         AND JJ.EFFDT   <= $ASOFDATE)
  AND H.EMPL_RCD = (SELECT MAX(JJ1.EMPL_RCD) FROM   PS_JOB JJ1 WHERE  JJ1.EMPLID = H.EMPLID
        AND  JJ1.EFFDT  <= $ASOFDATE)
  AND H.EFFSEQ = (SELECT MAX(JJJ.EFFSEQ) FROM   PS_JOB JJJ WHERE  JJJ.EMPLID  = H.EMPLID
        AND  JJJ.EMPL_RCD = H.EMPL_RCD AND  JJJ.EFFDT <= $ASOFDATE )
  AND K.EMPLID = H.EMPLID
  AND K.EMPL_RCD = H.EMPL_RCD

  [$G.SelectedEEs]
  [$fromtoG]           !NULL string if STREAM NUMBER option not used
  [$Sort1]

end-select

  IF $NO_EMPLOYEE = 'N'
           let $T1.SelectedEEs = SUBSTR($T1.SelectedEEs,1,LENGTH($T1.SelectedEEs) - 2)
           let $T1.SelectedEEs = $T1.SelectedEEs || ')'
  else
           let $T1.SelectedEEs = ''
 end-if

 if $DataAudit = 'Y'
 if $SortEmployeeFound = 'Y'
   if $EmployeeFoundInCompany = 'Y'
     new-page
   end-if
 end-if
   move '' to $priorSortValueYE30
 end-if

Begin-Select
T1.EMPLID   &T1.EMPLID
T1.SSN
T1.GPUS_YE_LNAME

   let $E.SelectedEEs = 'AND E.EMPLID = ''' || &T1.EMPLID || ''''
   do Read-Employee-Data

FROM PS_GPUS_YE_EE5_VW T1
where T1.GPUS_CAL_YEAR    = &TX.CALENDAR_YEAR
  AND T1.GPUS_TAX_EIN   = &A.GPUS_TAX_EIN
  [$T1.SelectedEEs]          !NULL string if SELECT EEs option not used
  [$T1.SelectedEmpl]
  [$fromtoT1]                !NULL string if STREAM NUMBER option not used
  [$Sort4]
End-Select
End-Procedure


Begin-Procedure GET-MAIL-DROP-FROM-POSITION-DATA
Begin-Select
POSD.MAIL_DROP

   move &POSD.MAIL_DROP to $Mail_Drop_Hold

FROM PS_POSITION_DATA POSD
WHERE POSD.POSITION_NBR = &H.POSITION_NBR
AND POSD.EFFDT = (SELECT MAX(POSD1.EFFDT)
                  FROM PS_POSITION_DATA POSD1
                  WHERE POSD1.POSITION_NBR = POSD.POSITION_NBR)
end-select
End-Procedure
#include 'gpusaodt.sqc'     !Get-As-Of-Date procedure