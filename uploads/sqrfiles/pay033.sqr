!**********************************************
!  PAY033:  Imputed Income Adjustment Report  *
!**********************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:34:27                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !Printer and page-size initialization

begin-report
  move 'PAY033' to $ReportID
  move 'Imputed Income Adjustments' to $ReportTitle

  do Init-DateTime
  do Init-Number

  do Stdapi-Init

  move 'N' to  $CyclePrompts
  do Payroll-Report-Initialization

  do Stdapi-Term
end-report

begin-heading 8

  #Include 'stdhdg06.sqc'

  print 'Plan'          (+2,15)
  print 'Benefit'       (0,34)
  print 'Ded'           (0,42)
  print 'Type of'       (0,52)
  print 'Taxable'       (0,66)
  print 'Employee'      (0,77)
  print 'YTD'           (0,94)
  print 'Adjustment'    (0,99)
  print 'Companies'     (0,114)
  print 'Paygroups'     (0,124)

  print 'Employee ID'   (+1,1)
  print 'Type'          (0,15)
  print 'Plan'          (0,34)
  print 'Code'          (0,42)
  print 'Adjust'        (0,52)
  print 'Amount'        (0,67)
  print 'Contrib'       (0,78)
  print 'Balance'       (0,90)
  print 'Amount'        (0,103)
  print 'Included'      (0,114)
  print 'Included'      (0,124)

end-heading


begin-procedure Report

begin-SELECT
A.COMPANY    () on-break print=never  level=1 procedure=Company-Change
A.PAY_END_DT () on-break print=never  level=2 procedure=PayEndDate-Change
A.PAYGROUP   () on-break print=never  level=3 procedure=PayGroup-Change
C.EMPLID           (+1,1)

C.PLAN_TYPE
C.BENEFIT_PLAN
C.DEDCD
C.DED_CLASS
C.ONE_TIME_CD
C.WRK_DED
C.ATX_DED
C.YTD_DED
C.DED_ADDL_AMT

  move &C.Plan_Type   to $PlanType
  move 'PLAN_TYPE' to $FieldName
  move $PlanType   to $FieldValue
  do Read-Translate-Table
  print $PlanType        (0,15)
  print $XlatLongName    (0,18)

  print &C.Benefit_Plan  (0,34)
  print &C.DedCd         (0,42)

  move &C.One_Time_Cd   to $FieldValue
  move 'ONE_TIME_CD' to $FieldName
  do Read-Translate-Table
  print $XlatLongName    (0,52)

  move &A.Company  to  $Company
  move &A.PayGroup  to $Paygroup
  move &A.Pay_End_Dt  to $PayEndDate

  do Format-Number(&C.wrk_ded, $out, '999999.99')
  print $out     (0,64)
  do Format-Number(&C.atx_ded, $out, '999999.99')
  print $out     (0,76)
  do Format-Number(&C.ytd_ded, $out, '999999.99')
  print $out     (0,88)
  do Format-Number(&C.Ded_addl_amt, $out, '999999.99')
  print $out     (0,100)

  do Get-Company-PayGroup-Count
  do Format-Number(#Co_Count, $out, '999')
  print $out     (0,116)
  do Format-Number(#PG_Count, $out, '999')
  print $out     (0,126)

FROM  PS_PAY_CALENDAR A,
      PS_IMP_ADJUST   C
WHERE [$SlctCalendar]
  AND C.COMPANY    = A.COMPANY
  AND C.PAYGROUP   = A.PAYGROUP
  AND C.PAY_END_DT = A.PAY_END_DT
ORDER BY A.COMPANY,
         A.PAYGROUP,
         A.PAY_END_DT,
         C.EMPLID,
         C.PLAN_TYPE
end-SELECT

end-procedure


begin-procedure Get-Company-PayGroup-Count
  move 0 to #Co_Count
  move 0 to #PG_Count

begin-SELECT
COUNT(DISTINCT E.COMPANY)   &Co_Count

  move &Co_Count to #Co_Count

FROM  PS_IMP_CALC     E,
      PS_DEDUCTION_CLASS G
WHERE E.EMPLID = &C.EmplID
  AND E.ADJ_CO = &A.Company
  AND E.ADJ_PG = &A.PayGroup
  AND E.ADJ_DT = &A.Pay_End_Dt
  AND G.PLAN_TYPE = &C.Plan_Type
  AND G.DEDCD     = &C.Dedcd
  AND G.DED_CLASS = &C.Ded_Class
  AND G.DED_SLSTX_CLASS = 'B'
  AND G.EFFDT     =
      (SELECT MAX(EFFDT)
       FROM   PS_DEDUCTION_CLASS
       WHERE  PLAN_TYPE  = G.PLAN_TYPE
         AND  DEDCD      = G.DEDCD
         AND  DED_CLASS  = G.DED_CLASS
         AND  DED_SLSTX_CLASS = G.DED_SLSTX_CLASS
         AND  EFFDT     <= E.ADJ_DT)
  AND G.GTL_DPL_EFFECT = E.GTL_DPL_EFFECT
end-SELECT

begin-SELECT
COUNT(DISTINCT E.PAYGROUP)  &PG_Count

  move &PG_Count to #PG_Count

FROM  PS_IMP_CALC     E,
      PS_DEDUCTION_CLASS G
WHERE E.EMPLID = &C.EmplID
  AND E.ADJ_CO = &A.Company
  AND E.ADJ_PG = &A.PayGroup
  AND E.ADJ_DT = &A.Pay_End_Dt
  AND G.PLAN_TYPE = &C.Plan_Type
  AND G.DEDCD     = &C.Dedcd
  AND G.DED_CLASS = &C.Ded_Class
  AND G.DED_SLSTX_CLASS = 'B'
  AND G.EFFDT     =
      (SELECT MAX(EFFDT)
       FROM   PS_DEDUCTION_CLASS
       WHERE  PLAN_TYPE  = G.PLAN_TYPE
         AND  DEDCD      = G.DEDCD
         AND  DED_CLASS  = G.DED_CLASS
         AND  DED_SLSTX_CLASS = G.DED_SLSTX_CLASS
         AND  EFFDT     <= E.ADJ_DT)
  AND G.GTL_DPL_EFFECT = E.GTL_DPL_EFFECT
end-SELECT

end-procedure


begin-procedure Company-Change
  new-page
end-procedure


begin-procedure PayEndDate-Change
  new-page
end-procedure


begin-procedure PayGroup-Change
  new-page
end-procedure


#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'getdatcd.sqc'  !Get-Date-Codes procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'payinit.sqc'   !Report Initialization and Timing
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
