!***********************************************************************
!  PAY704: Print contents of Deduction / Ded Freq Tables
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:04:56:07                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY704' to $ReportID

  do Init-DateTime
  do Init-Number

  do Get-Current-DateTime
  do Stdapi-Init
  do Get-Report-Language   ! Procedure that decides the report language
                           ! and then calls 'Report-Translation' procedure
                           ! that reads the language dep. text.
  move $Pay704_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '
end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY704  program id
   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('PAY704', 'REPORT_TITLE', $Pay704_ReportTitle,  #dummy_width)
   do Get_Field_Information ('PAY704', 'PLAN_TYPE1',   $Pay704_Plan_Type1,   #dummy_width)
   do Get_Field_Information ('PAY704', 'DEDCD1',       $Pay704_DedCd1,       #dummy_width)
   do Get_Field_Information ('PAY704', 'EFFDT1',       $Pay704_EffDt1,       #dummy_width)
   do Get_Field_Information ('PAY704', 'SHORT_DESCR1', $Pay704_Short_Descr1, #dummy_width)
   do Get_Field_Information ('PAY704', 'PR1',          $Pay704_Pr1,          #dummy_width)
   do Get_Field_Information ('PAY704', 'MAX_PYBK1',    $Pay704_Max_PyBk1,    #dummy_width)
   do Get_Field_Information ('PAY704', 'MAXARR_PYBK1', $Pay704_MaxArr_PyBk1, #dummy_width)
   do Get_Field_Information ('PAY704', 'MAXARR_FCT1',  $Pay704_MaxArr_Fct1,  #dummy_width)
   do Get_Field_Information ('PAY704', 'PAYFREQ1',     $Pay704_PayFreq1,     #dummy_width)
   do Get_Field_Information ('PAY704', 'DEDN_PER',     $Pay704_Dedn_Per,     #dummy_width)
   do Get_Field_Information ('PAY704', 'PLAN_TYPE2',   $Pay704_Plan_Type2,   #dummy_width)
   do Get_Field_Information ('PAY704', 'DEDCD2',       $Pay704_DedCd2,       #dummy_width)
   do Get_Field_Information ('PAY704', 'EFFDT2',       $Pay704_EffDt2,       #dummy_width)
   do Get_Field_Information ('PAY704', 'DESCR',        $Pay704_Descr,        #dummy_width)
   do Get_Field_Information ('PAY704', 'SHORT_DESCR2', $Pay704_Short_Descr2, #dummy_width)
   do Get_Field_Information ('PAY704', 'PR2',          $Pay704_Pr2,          #dummy_width)
   do Get_Field_Information ('PAY704', 'MAX_PYBK2',    $Pay704_Max_PyBk2,    #dummy_width)
   do Get_Field_Information ('PAY704', 'MAXARR_PYBK2', $Pay704_MaxArr_PyBk2, #dummy_width)
   do Get_Field_Information ('PAY704', 'MAXARR_FCT2',  $Pay704_MaxArr_Fct2,  #dummy_width)
   do Get_Field_Information ('PAY704', 'PAYFREQ2',     $Pay704_PayFreq2,     #dummy_width)
end-procedure

begin-HEADING 7

  #Include 'stdhdgtr.sqc'

  print $Pay704_Plan_Type1              (+2,2)
  print $Pay704_DedCd1                  (0,9)
  print $Pay704_EffDt1                  (0,18)
  print $Pay704_Short_Descr1            (0,63)
  print $Pay704_Pr1                     (0,76)
  print $Pay704_Max_PyBk1               (0,84)
  print $Pay704_MaxArr_PyBk1            (0,95)
  print $Pay704_MaxArr_Fct1             (0,109)
  print $Pay704_PayFreq1                (0,123)
  print $Pay704_Dedn_Per                (0,135)

  print $Pay704_Plan_Type2              (+1,2)
  print $Pay704_DedCd2                  (0,9)
  print $Pay704_EffDt2                  (0,18)
  print $Pay704_Descr                   (0,30)
  print $Pay704_Short_Descr2            (0,63)
  print $Pay704_Pr2                     (0,76)
  print $Pay704_Max_PyBk2               (0,84)
  print $Pay704_MaxArr_PyBk2            (0,95)
  print $Pay704_MaxArr_Fct2             (0,109)
  print $Pay704_PayFreq2                (0,123)

  print ' 1    2    3    4    5'        (0,135)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

A.PLAN_TYPE                     (+1,2,4)
A.DEDCD                         (0,+3,6)

A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                    (0,+3,10)

A.DESCR                         !(0,30,30)
A.DESCRSHORT                    !(0,+3,10)
          move &A.DESCR      to $PRNT_DESCR
          move &A.DESCRSHORT to $PRNT_DESCRSHORT
        if $Curr_language_cd <> $Psoptions_Language_Cd
        do Lang-Record-Details
        end-if
          print $PRNT_DESCR                (0,30,30)
          print $PRNT_DESCRSHORT           (0,+3,10)

A.DED_PRIORITY                  (0,+3,5)
A.MAX_PAYBACK                   (0,+5,6)
A.MAX_ARREARS_PAYBK             (0,+3,11)    EDIT B9999999.99
A.MAX_ARREARS_FACTOR            (0,+3,11)    EDIT B9999999.99

  do Get-Deduction-Freq

FROM PS_DEDUCTION_TBL A

ORDER BY A.PLAN_TYPE, A.DEDCD, A.EFFDT DESC

END-SELECT
END-PROCEDURE


BEGIN-PROCEDURE Lang-Record-Details
BEGIN-SELECT
LA.DESCR
LA.DESCRSHORT

        let $PRNT_DESCR = Cond(Rtrim(&LA.DESCR,' ')='', &A.DESCR, &LA.DESCR)
        let $PRNT_DESCRSHORT = Cond(Rtrim(&LA.DESCRSHORT,' ')='', &A.DESCRSHORT, &LA.DESCRSHORT)

FROM PS_DEDUCTION_LANG LA
WHERE LA.PLAN_TYPE   = &A.PLAN_TYPE
  AND LA.DEDCD       = &A.DEDCD
  AND LA.LANGUAGE_CD = $Curr_language_cd
  AND LA.EFFDT       = ( SELECT MAX(EFFDT) FROM PS_DEDUCTION_LANG
                         WHERE PLAN_TYPE = &A.PLAN_TYPE
                         AND DEDCD       = &A.DEDCD
                         AND LANGUAGE_CD = $Curr_language_cd
                         AND EFFDT      <= &A.EFFDT )
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC GET-DEDUCTION-FREQ
!         Get deduction frequency associated with each deduction
!***********************************************************************

BEGIN-PROCEDURE Get-Deduction-Freq

  move 0 to #count

BEGIN-SELECT

B.PLAN_TYPE
B.DEDCD
B.EFFDT
F.FREQUENCY_TYPE
  if #count = 0
    print &F.frequency_type      (0,+4,3)
    move 999 to #count
  else
    print &F.frequency_type      (+1,-33,3)
  end-if
B.PAY_PERIOD1                    (0,+9,1)
B.PAY_PERIOD2                    (0,+4,1)
B.PAY_PERIOD3                    (0,+4,1)
B.PAY_PERIOD4                    (0,+4,1)
B.PAY_PERIOD5                    (0,+4,1)

FROM PS_DEDUCTION_FREQ B,  PS_FREQUENCY_TBL  F
WHERE B.PLAN_TYPE        = &A.PLAN_TYPE
  AND B.DEDCD            = &A.DEDCD
  AND B.EFFDT            =
      ( SELECT MAX(EFFDT)
        FROM   PS_DEDUCTION_FREQ
        WHERE  EFFDT = &A.EFFDT)
  AND B.PAY_FREQUENCY    = F.FREQUENCY_ID
  AND F.EFF_STATUS       = 'A'
  AND F.EFFDT            =
      (SELECT MAX(F1.EFFDT)
      FROM PS_FREQUENCY_TBL F1
      WHERE F1.FREQUENCY_ID   = F.FREQUENCY_ID
      AND   F1.EFF_STATUS     = F.EFF_STATUS
      AND   F1.EFFDT         <= B.EFFDT)
END-SELECT
END-PROCEDURE

#Include 'readxlat.sqc'  !get translates for fieldname, value
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
