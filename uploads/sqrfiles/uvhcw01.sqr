!***********************************************************************
!  UVHCW01: Conversion Audit for PER_ORG_INST and PER_ORG_ASGN         *
!                                                                      *
!            Provide the Customer with a Validation report that shows  *
!            invalid relationships amongst these tables after the      *
!            conversion of data.                                       *
!                                                                      *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/08/14:01:54:42                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup01.sqc'   ! Printer and page-size initialization

!***********************************************************************

begin-program
        do Init-DateTime
        do Get-Current-DateTime
        do Init-Report
        display 'Report began at: ' NoLine
        do Display-Time
        do Process-Main
        do Reset
        do Stdapi-Term
        display 'Report ended at: ' NoLine
        do Display-Time
end-program

!***********************************************************************
begin-heading 8
!***********************************************************************
#include 'stdhdg01.sqc'

if $processing = '1'
  print 'Audit the load of PER_ORG_ASGN against PERSON' (+2,1,0)
  print 'EmplID'                              (+2,1,11) Underline
  print ' '                                        (+1,1,1)
end-if

if $processing = '2'
  print 'Audit the load of PER_ORG_ASGN against EMPLOYMENT'        (+2,1,0)
  print 'EmplID'                              (+2,1,11) Underline
  print 'Empl Rcd#'                           (0,+2,11) Underline
  print ' '                                        (+1,1,1)
end-if

if $processing = '3'
  print 'Check number of rows inserted in record PER_ORG_ASGN is correct'      (+2,1,0) Underline
  print 'Customer will need to determine why there are entries in EMPLOYMENT' (+1,1,0)
  PRINT ' that did not get inserted into PER_ORG_ASGN' (,+1)
end-if

end-heading

!***********************************************************************
!  Function:     Init Report
!  Description:  Prompt for run-time parameters
!***********************************************************************
begin-procedure Init-Report
  move 'UVHCW01' to $ReportID
  move 'Audit Report' to $ReportTitle
  display $ReportTitle

  do Stdapi-Init

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if

end-procedure Init-Report

begin-procedure Get-Values
  !No Prompts
end-procedure Get-Values

!***********************************************************************
!  Function:     Process-Main
!  Description:  This is the main report procedure.  
!***********************************************************************

begin-procedure Process-Main
     Do Select-Person
     Do Select-Employment
     Do Select-Count
end-procedure Process-Main

!***********************************************************************
!  Procedure:    Select-Person
!  Description:  Audit the load of PER_ORG_ASGN  against PERSON
!***********************************************************************

begin-procedure Select-Person
!=====================================================!
  Let $processing = '1'
  let #COUNTER = 0
!=====================================================!
begin-SELECT ON-ERROR=SQL-Error
A.EMPLID                               (+1,1,11)
 let #COUNTER = #COUNTER + 1
From  PS_PERSON A
where A.EMPLID not in (select B.EMPLID from PS_PER_ORG_ASGN B)
end-SELECT

 Do Print-Count
end-procedure Select-Person

!***********************************************************************
!  Procedure:    Select-Employment
!  Description:  Audit the load of PER_ORG_ASGN against EMPLOYMENT
!***********************************************************************

begin-procedure Select-Employment
!=====================================================!
  Let $processing = '2'
  let #COUNTER = 0
!=====================================================!
begin-SELECT ON-ERROR=SQL-Error
C.EMPLID                               (+1,1,11)
C.EMPL_RCD                             (0,+2,3)
 let #COUNTER = #COUNTER + 1
FROM PS_EMPLOYMENT C
WHERE C.EMPL_RCD NOT IN
 (SELECT D.EMPL_RCD FROM PS_PER_ORG_ASGN D WHERE C.EMPLID = D.EMPLID)
end-SELECT

  Do Print-Count
end-procedure Select-Employment

!***********************************************************************
!  Procedure:    Select-Count
!  Description:  Check number of rows inserted in record PER_ORG_ASGN is correct
!***********************************************************************

begin-procedure Select-Count
!=====================================================!
  Let $processing = '3'
  let #COUNTER = 0
!=====================================================!
let #COUNTER1 = 0
let #COUNTER2 = 0
let #COUNTER3 = 0
let #COUNTER4 = 0

  Print 'These counts should be the same:' (+2,1)
begin-SELECT ON-ERROR=SQL-Error
COUNT(EMPLID) &SELECT1
 let #COUNTER1 = &SELECT1
FROM PS_PERSON
end-SELECT

begin-SELECT ON-ERROR=SQL-Error
count(distinct EMPLID)  &SELECT2
 let #COUNTER2 = &SELECT2
FROM PS_PER_ORG_ASGN
end-SELECT

 print 'Number of rows selected in PERSON: '  (+2,5)
 print #COUNTER1                               (,+1,)
 print 'Number of distinct EmplID selected in PER_ORG_ASGN: '  (+1,5)
 print #COUNTER2                                     (,+1,)

  Print 'These counts should be the same:' (+3,1)
begin-SELECT ON-ERROR=SQL-Error
count(DISTINCT EMPLID) &SELECT4
  let #COUNTER4 = &SELECT4
FROM PS_EMPLOYMENT
end-SELECT
  print 'Number of rows selected in EMPLOYMENT: '  (+1,5)
  print #COUNTER4                                    (,+1,)
  print 'Number of rows selected in PER_ORG_ASGN: '  (+1,5)
  print #COUNTER2                                    (,+1,)
end-procedure Select-Count


!*************************************
Begin-Procedure Display-Time
!*************************************

  do Get-Current-DateTime
  display $AsOfToday NoLine
  display ', '       NoLine
  display $AsOfNow
  display ''

End-Procedure

!*************************************
Begin-Procedure Print-Count
!*************************************

 print 'Number of rows selected:  '           (+2,1,26)
 print #COUNTER                               (,+1,)

  new-page


  Show 'Number of rows selected:  '  #COUNTER
  display ' '

End-Procedure


!*************************************


!***********************************************************************
! SQC Files for called procedures
!***********************************************************************
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Routines to update run status
#include 'prcsapi.sqc'   !Update Process API
#Include 'reset.sqc'     !Reset printer procedure
#include 'hrrnctl1.sqc'  !Get run control parameter values
#Include 'number.sqc'    !Routines to format numbers
