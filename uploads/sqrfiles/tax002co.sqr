!***********************************************************************
!  TAX002CO:  Qtrly State Employee Wage List - COLORADO                *
!                                                                      *
!             NOTE: "#IFDEF TAXTEST" in main SELECT is used for        *
!                   testing this report against DEMO database          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                $Date:  2006/01/03:17:12:14                           !
!             $Release:  HR9                                           !
!            $Revision:  102                                           !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#Include 'usarpt.sqc'  !Get USA variables

begin-setup

#include 'prt002.sqc'  !Printer and page-size initialization

end-setup

begin-report
  do Init-Report

  do Process-Main
  alter-locale
  decimal-separator = ' '

  if #Employees_On_Page > 0
    do Format-Number(#CompanyTotal, $CompanyTotal, '99999999.99')
    do Page-Complete
  end-if

  do Stdapi-Term

end-report

begin-procedure Init-Report
  display 'Quarterly Employee Wage Listing - Colorado'

  do Init-DateTime
  do Init-Number

  do Get-Tax-Reporting-Run-Controls
  do Get-Calendar-Year-Id
  do Stdapi-Init

  move $PeriodEndDate to $AsOfDate  !for included procedures

  if $prcs_process_instance = ''
    input $ReportDueDate  'Enter Report Due Date ({Prompt-Date})' type=char
    move '1' to $MMLZero
    do Format-DateTime($ReportDueDate, $out, {Prompt-Mask}, '', '')
  else
    do Select-Parameters
    move $RC_QTR_UI.Report_Due_Date to $ReportDueDate
    move '0' to $year4
    do Format-DateTime($ReportDueDate, $ReportDueDate, {DEFMDY}, '', '')
  end-if

end-procedure

begin-heading 25
  if RTRIM(&Employer_ID_SUT, ' ') <> ''
    print &Employer_ID_SUT         (+9,7)
  else
    print &Employer_ID_SWT         (+9,7)
  end-if
  print &TX.Balance_Qtr            (0,29)
  print &TX.Balance_Year           (0,34)
  print $ReportDueDate             (0,49)
  print &CT.Federal_EIN            (+4,49) edit 099999999
  print $CompanyName               (0,2)
  print $Compnyadd1                (+1,2)
  let $field = RTRIM($Compnycity, ' ')
  print $field                     (+1,2)
  print &CT.State                  ()      edit ,bxx
  move  &CT.Postal                 to $Zip
  move  &CT.Postal                 to $ZipExtsn ~~~~~~xxxx

  if instr($Zip,'-',6) = 0
  and $ZipExtsn <> '    '
      let $Zip = substr($Zip,1,5) || '-' || substr($Zip,6,4)
  end-if
  print $Zip                       (0,+2)
  alter-locale
  decimal-separator = ' '
  do Format-Number(#PageTotal, $PageTotal, '99999999.99')
  print $CompanyTotal              (56,22)         !on last page only
  print $PageTotal                 (56,68)

end-heading

begin-procedure Process-Main

begin-SELECT
TEMP_SSN_MASK
FROM PS_INSTALLATION
end-SELECT


  Show 'Calendar Year: ' $Calendar_Year_Id
  Show 'Balance  Year: ' &TX.Balance_Year
  Show 'Balance  Qtr:  ' &TX.Balance_Qtr
  Show 'Asofdate:      ' $Asofdate

begin-SELECT
C.COMPANY () on-break print=never before=Company-Change
C.STATE
C.EMPLID
C.LOCALITY
C.TAX_CLASS
C.NLGRS_QTD

D1.NATIONAL_ID
PN.NAME

  do Process-Tax-Balance

FROM  PS_TAX_BALANCE C, PS_NAMES PN, PS_PERS_NID D1, PS_COMPANY_TBL A
WHERE C.EMPLID            = D1.EMPLID
  AND D1.COUNTRY          = {NID_COUNTRY}
  AND D1.NATIONAL_ID_TYPE = $PAYROLL_NID_TYPE
  AND PN.EMPLID           = C.EMPLID
#ifdef TAXTEST
  AND C.STATE             = 'CA'
#else
  AND C.STATE             = 'CO'
#endif
  AND C.LOCALITY          = ' '
  AND C.TAX_CLASS         = 'U'                    !Unemployment
  AND C.BALANCE_ID        = $Calendar_Year_Id
  AND C.BALANCE_YEAR      = &TX.Balance_Year
  AND C.BALANCE_QTR       = &TX.Balance_Qtr
  AND C.BALANCE_PERIOD    =
      (SELECT MAX(C1.BALANCE_PERIOD)
       FROM   PS_TAX_BALANCE C1
       WHERE  C1.EMPLID        = C.EMPLID
         AND  C1.COMPANY       = C.COMPANY
         AND C1.BALANCE_ID     = C.BALANCE_ID
         AND C1.BALANCE_YEAR   = C.BALANCE_YEAR
         AND C1.BALANCE_QTR    = C.BALANCE_QTR
         AND  C1.STATE         = C.STATE
         AND  C1.LOCALITY      = C.LOCALITY
         AND  C1.TAX_CLASS     = C.TAX_CLASS)
  AND C.NLGRS_QTD     > 0
  AND A.COMPANY = C.COMPANY
  AND A.EFF_STATUS = 'A'
  AND A.TAX_REPORT_TYPE = '2'
  AND A.EFFDT =
      (SELECT MAX(EFFDT)
       FROM PS_COMPANY_TBL
       WHERE  COMPANY = A.COMPANY
         AND  EFFDT  <= $AsOfDate)
  AND PN.NAME_TYPE = 'PRI'
  AND PN.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PN.EMPLID
         AND  PX.NAME_TYPE = PN.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfDate)
ORDER BY C.COMPANY, D1.NATIONAL_ID, C.STATE, C.LOCALITY, C.TAX_CLASS
end-SELECT
end-procedure

begin-procedure Process-Tax-Balance
  add 1 to #Employees_On_Page

  if #Employees_On_Page > 14
    do Page-Complete
    move 1 to #Employees_On_Page
  end-if

  move &D1.NATIONAL_ID   to $S123 xxx       !isolate first 3 digits

  if $S123 = &Temp_SSN_Mask or RTRIM(&D1.NATIONAL_ID, ' ') = ''
    print ' '                    (+2,3)
  else
    print &D1.NATIONAL_ID        (+2,3) edit xxxbxxbxxxx
  end-if

  move &PN.Name to $Name
  do Rotate-Name
  print $Name                   (0,20,20)

  alter-locale
  decimal-separator = ' '
  move &C.NLGrs_QTD to #NLGrs_QTD
  do Format-Number(#NLGrs_QTD, $NLGrs_QTD, '99999999.99')
  print $NLGrs_QTD              (0,68)

  add #NLGrs_QTD to #PageTotal
  add #NLGrs_QTD to #CompanyTotal

end-procedure

begin-procedure Page-Complete
  add 1 to #Page#
  new-page
  move 0 to #PageTotal
end-procedure

begin-procedure Company-Change
  alter-locale
  decimal-separator = ' '

  if #Employees_On_Page > 0
    do Format-Number(#CompanyTotal, $CompanyTotal, '99999999.99')
    do Page-Complete
    move 0 to #Employees_On_Page
  end-if

  move 0  to #Page#
  move 0  to #CompanyTotal
  move '' to $CompanyTotal
  move &C.Company to $Company
  do Get-Company-Data
#ifdef TAXTEST
  move 'CA' to $State
#else
  move 'CO' to $State
#endif
  do Get-State-Tax-Data             !Employer ID
end-procedure

#Include 'rotname1.sqc'  !Rotate-Name procedure
#Include 'taxrnctl.sqc'  !Get-Tax-Reporting-Run-Controls procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getstdta.sqc'  !Get-State-Tax-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers

#Include 'getbalid.sqc'  !Get-Calendar-Year-Id
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl1.sqc'  !Get Run Control for Process Scheduler

