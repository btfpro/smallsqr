!**************************************************************
!  PAY305RT:  Load Paysheet Transaction Report                *
!**************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2006/09/08:15:56:33                                 !
!       $Release:  HR9                                                 !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

begin-report

    do Init-DateTime
    do Init-Number
    do Get-Current-Datetime
    do Get-Language-Codes
    do Init-Report
    do Process-Main
    do Reset
    do Successful-EOJ

end-report


!******************** H E A D I N G      S E C T I O N  *****************

#include 'setup32.sqc'  !Printer and page-size initialization

begin-heading 10



#include 'stdhdg01.sqc'

    print $PAY305RT_PU_Source                (+4,1)
    print $PAY305RT_PU_Txn_Status            (0,20)
    print $PAY305RT_Emplid                   (0,29)
    print $PAY305RT_Empl_Rcd                 (0,73)
    print $PAY305RT_Company                  (0,85)
    print $PAY305RT_Paygroup                 (0,95)
    print $PAY305RT_Pay_End_Dt               (0,107)
    print $PAY305RT_PU_Txn_Type              (0,130)
    print $PAY305RT_Grossup                  (0,138)
    print $PAY305RT_Sepchk                   (0,150)



end-heading

!******************  I N I T I A L I Z A T I O N   *********************

begin-procedure Init-Report

!********  Translations procedures & Strings Table retrievals  *********

  move 'PAY305RT' to $ReportID
  do Define-Prcs-Vars
  do Select-Parameters

  do Init_Report_Translation ('PAY305RT', $curr_language_cd)
  do Append_Report_Translation ('PAY305RT')

  do Get_Field_Information ('PAY305RT' ,'TITLE'             ,$PAY305RT_Title          ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PU_SOURCE'         ,$PAY305RT_PU_Source      ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PU_TXN_STATUS'     ,$PAY305RT_PU_Txn_Status  ,#DW)
  do Get_Field_Information ('PAY305RT', 'EMPLID'            ,$PAY305RT_Emplid         ,#DW)
  do Get_Field_Information ('PAY305RT' ,'EMPL_RCD'          ,$PAY305RT_Empl_Rcd       ,#DW)
  do Get_Field_Information ('PAY305RT' ,'COMPANY'           ,$PAY305RT_Company        ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PAYGROUP'          ,$PAY305RT_Paygroup       ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PAY_END_DT'        ,$PAY305RT_Pay_End_Dt     ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PU_TXN_TYPE'       ,$PAY305RT_PU_Txn_Type    ,#DW)
  do Get_Field_Information ('PAY305RT' ,'GROSSUP'           ,$PAY305RT_Grossup        ,#DW)
  do Get_Field_Information ('PAY305RT' ,'SEPCHK'            ,$PAY305RT_Sepchk         ,#DW)


  move $PAY305RT_Title to $ReportTitle
  display $ReportTitle

end-procedure

!**************************  M A I N   P R O C E S S   ******************

begin-procedure Process-Main           !Main Report Processing

begin-SELECT

A.PU_SOURCE                         (+2,1)  ON-BREAK PRINT=CHANGE/TOP-PAGE
A.PU_TXN_STATUS                     (0,20)
A.EMPLID
A.EMPL_RCD                          (0,73)
A.COMPANY                           (0,85)
A.PAYGROUP                          (0,95)
A.PAY_END_DT
A.PU_TXN_TYPE                       (0,131)
A.GROSSUP                           (0,141)
A.SEPCHK                            (0,152)


  do Get-Employee-Name

  do Format-DateTime(&A.PAY_END_DT, $out, {DEFMDY}, '', '')
  print $out                        (0,107)



FROM PS_PSHUP_TXN A

WHERE (A.PU_SOURCE = $Rqst-Pu-Source-St OR
       A.PU_SOURCE = $Rqst-Pu-Source-Vc OR
       A.PU_SOURCE = $Rqst-Pu-Source-Ex OR
       A.PU_SOURCE = $Rqst-Pu-Source-Ot OR
       A.PU_SOURCE = $Rqst-Pu-Source-Sp OR
       A.PU_SOURCE = $Rqst-Pu-Source-Wi OR
       A.PU_SOURCE = $Rqst-Pu-Source-Er OR
       A.PU_SOURCE = $Rqst-Pu-Source-Rp OR
       A.PU_SOURCE = $Rqst-Pu-Source-Im OR
       A.PU_SOURCE = $Rqst-Pu-Source-Am OR
       A.PU_SOURCE = $Rqst-Pu-Source)  AND
       (A.PU_TXN_STATUS = $Rqst-Pu-Status-A OR
        A.PU_TXN_STATUS = $Rqst-Pu-Status-I)

ORDER BY A.PU_SOURCE,
         A.PU_TXN_STATUS,
         A.COMPANY,
         A.PAYGROUP,
         A.EMPLID,
         A.EMPL_RCD


end-SELECT

end-procedure

!*************  G E T   P E R S O N A L  D A T A  ***************

begin-procedure Get-Employee-Name

begin-SELECT

E.NAME
  let $EmplID = rtrim(&A.EMPLID, ' ')
  let $EmplID_Name = $EmplID || ' ' || &E.NAME
  if length($EmplID_Name) > 43
    let $EmplID_Name = substr($EmplID_Name,1,43)
  end-if
  print $EmplID_Name  (0,29)

FROM PS_PERSON_NAME E

WHERE E.EMPLID = &A.EMPLID

end-SELECT

end-procedure

!********************************************************************

#Include 'prcsapi.sqc'   !Update Process Scheduler Request Record
#Include 'prcsdef.sqc'   !Creates standard Process Scheduler variables
#Include 'pay305rt.sqc'  !Custom Run Control Parameters
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'eoj.sqc'       !End of Job procedure
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'prcslng.sqc'   !Replaces getrplng.sqc
