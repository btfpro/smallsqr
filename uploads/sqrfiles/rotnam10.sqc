!***********************************************************************
!  RotNam10:  Convert from "Last Suffix,First" to "Last First Mid In"  *
!             "First MidIn Last" Remove Title, Suffix, Middle Initial, *
!             and Punctuation, from Name. Will put 2 spaces between    *
!             Last and First Name.                                     *
!                                                                      *
!             Note: This procedure will only find a member of the      *
!             comma-delimited suffixes defined below in the variable   *
!             $SuffixString.  Additional suffixes may be added to the  *
!             variable as necessary.  If two or more suffixes, e.g.    *
!             'JR.' and 'JR', in which the second is a substring of    *
!             the first are included, the longest suffix must be the   *
!             first in the list.                                       *
!                                                                      *
!             If the variable $Remove_Blanks_From_Surname is set to    *
!             'Y' before this procedure is called, any blanks in the   *
!             surname will be removed, e.g. 'ST JOHN' will be formatted*
!             as 'STJOHN'.                                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:05:24:09                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Rotate-Name-for-Mag-Media
  move '' to $Suffix
  move 'N' to $SuffixFound
  uppercase $Name

  let $SuffixString =
    'JR.,JR,SR.,SR,III,II,I,IV,MD,M.D.,CPA,C.P.A.,ESQ.,ESQ,' ||
    '2ND.,2ND,SECOND,3RD.,3RD,THIRD,4TH.,4TH,FOURTH'

  let $Name = rtrim($Name,' ')
  find ',' in $Name 0 #locn
  let $LastName = SUBSTR($Name,1,#locn + 1)
  find ' ' in $LastName 0 #locnblank
  if #locnblank <> -1
    let #i = 1
    while INSTR($SuffixString,',',#i) > 0
      let #j = INSTR($SuffixString,',',#i)
      let $Suffix = SUBSTR($SuffixString,#i,#j - #i)

      let #locnsuffix = INSTR($LastName, $Suffix || ',', 1)
      if #locnsuffix > 0
        let $LastName = SUBSTR($LastName,1,#locnsuffix - 2)
        move 'Y' to $SuffixFound
      end-if
  let #i = #j + 1
      if $SuffixFound = 'Y'
        break
      end-if
    end-while
  end-if

  add 1 to #locn
  extract $FirstName from $Name #locn 40  !40 is the maximum length of Name

  if $Remove_Blanks_From_Surname = 'Y'
    while INSTR($LastName,' ',1)
      let #locnblank = INSTR($LastName,' ',1)
      let $LastName = SUBSTR($LastName,1,#locnblank - 1)   ||
                      SUBSTR($LastName,#locnblank + 1,40)
    end-while
    while INSTR($LastName,'-',1)
      let #locndash = INSTR($LastName,'-',1)
      let $LastName = SUBSTR($LastName,1,#locndash - 1)   ||
                      SUBSTR($LastName,#locndash + 1,40)
    end-while
    while INSTR($LastName,'.',1)
      let #locnper = INSTR($LastName,'.',1)
      let $LastName = SUBSTR($LastName,1,#locnper - 1)   ||
                      SUBSTR($LastName,#locnper + 1,40)
    end-while
    while INSTR($LastName,'''',1)
      let #locnhyphen = INSTR($LastName,'''',1)
      let $LastName = SUBSTR($LastName,1,#locnhyphen - 1)   ||
                      SUBSTR($LastName,#locnhyphen + 1,40)
    end-while
  end-if

  if $SuffixFound <> 'Y'

   let $Name = SUBSTR($LastName,1,LENGTH($LastName) - 1) || '  ' || $FirstName
  else
     let $Name = $LastName || '  ' || $FirstName
 end-if
end-procedure


begin-procedure Rotate-Name
  move '' to $Suffix
  move 'N' to $SuffixFound
  uppercase $Name

  let $SuffixString =
    'JR.,JR,SR.,SR,III,II,I,IV,MD,M.D.,CPA,C.P.A.,ESQ.,ESQ,' ||
    '2ND.,2ND,SECOND,3RD.,3RD,THIRD,4TH.,4TH,FOURTH,PHD,PHD.,P.H.D.'

  let $Name = rtrim($Name,' ')
  let $Sufsw = 'N'
  Unstring $Name by ' ' into $LastN $MidI $FirstN $Suf1 $Suf2
  let $CommaFound = 'N'
  let #k = 1
  while INSTR($MidI,',',#k) > 0
    let #l = INSTR($MidI,',',#k)
    move 'Y' to $CommaFound
    let #k = #l + 1
  end-while

  if $CommaFound = 'Y'
    Unstring $Name by ' ' into $LastN $FirstN $Suf1 $Suf2
  end-if

  if $Suf2 <> ''
     let $Suftest = $Suf2
     let $Sufsw = 'Y'
     do Test-Suffix
     let $Suf2 = $Suftest
  end-if

  if $Suf1 <> '' and $Sufsw = 'N'
     let $Suftest = $Suf1
     let $Sufsw = 'Y'
     do Test-Suffix
     let $Suf1 = $Suftest
  end-if

  if $LastN <> '' and $Sufsw = 'N'
     let $Suftest = $LastN
     let $Sufsw = 'Y'
     do Test-Suffix
     let $LastN = $Suftest
  end-if

  String $LastN $FirstN  by '  ' into $Name
end-procedure

begin-procedure Test-Suffix
  let #i = 1
  while INSTR($SuffixString,',',#i) > 0
    let #j = INSTR($SuffixString,',',#i)
    let $Suffix = SUBSTR($SuffixString,#i,#j - #i)
    let #locnsuffix = INSTR($SufTest, $Suffix, 1)

    if #locnsuffix > 0
      if $SufTest = $Suffix
         move ' ' to $SufTest
         move 'Y' to $SuffixFound
      end-if
    end-if

    let #i = #j + 1
    if $SuffixFound = 'Y'
       break
    end-if
  end-while
end-procedure
