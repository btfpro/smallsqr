! ***************************************************************!
!  This software and related documentation are provided under a!
!  license agreement containing restrictions on use and!
!  disclosure and are protected by intellectual property!
!  laws. Except as expressly permitted in your license agreement!
!  or allowed by law, you may not use, copy, reproduce,!
!  translate, broadcast, modify, license, transmit, distribute,!
!  exhibit, perform, publish or display any part, in any form or!
!  by any means. Reverse engineering, disassembly, or!
!  decompilation of this software, unless required by law for!
!  interoperability, is prohibited.!
!  The information contained herein is subject to change without!
!  notice and is not warranted to be error-free. If you find any!
!  errors, please report them to us in writing.!
!  !
!  Copyright (C) 1988, 2011, Oracle and/or its affiliates.!
!  All Rights Reserved.!
! ***************************************************************!
 
 
!                                                                    !
!***********************************************************************
! PROJLANG.SQR: Update PSPROJECTITEM                                   *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
!***********************************************************************

!----------------------------------------------------------------------!
! Report Name: PROJLANG.SQR -- Inserts into PSPROJECTITEM              !
!----------------------------------------------------------------------!
#include 'setenv.sqc'                   !Set environment procedure
#include 'setup01.sqc'

!----------------------------------------------------------------------!
! Procedure: Main                                                      !
! Desc:      This is the main procedure.                               !
!----------------------------------------------------------------------!

begin-Report
    display 'Updating Projects'
    display '================='
    display ' '

    Try_Again:
    input  $base_lang 'Enter the BASE Language of your TARGET database'
    if $base_lang != ''
       Do Validate_Language
       if  $Language_Cd != $base_lang
         display ' '
         display '** Error - Invalid language code.  Try again **'
         Do Get_Valid_Languages
         display ' '
         goto Try_again
       end-if

       let $Base_Language_cd = $base_lang

       if $Base_Language_Cd != 'ENG'
          do Update-Projects
          do Commit-Transaction
          display 'Projects Updated.'
       else
          display 'Projects do not need updating.'
       end-if
    else
       display 'Done.'
    end-if
end-Report

!***********************************************************************
!                                                                      *
! Function:    Validate_Language                                       *
!                                                                      *
! Description: Validate Language code with PSLANGUAGES table.          *
!                                                                      *
!***********************************************************************
begin-procedure Validate_Language

begin-select on-error=SQL-Error
LANGUAGE_CD  &Language_Cd
    move &Language_Cd       to $Language_Cd
FROM PSLANGUAGES WHERE LANGUAGE_CD = $base_lang
end-select

end-procedure

!***********************************************************************
!                                                                      *
! Function:    Get_Valid_Languages                                     *
!                                                                      *
! Description: Get Valid Language codes in PSLANGUAGES table.          *
!                                                                      *
!***********************************************************************
begin-procedure Get_Valid_Languages

display '** Valid Language Codes are: ' NOLINE

begin-select on-error=SQL-Error
LANGUAGE_CD  &Valid_Language_Cd
    move &Valid_Language_Cd       to $Valid_Language_Cd
    Do Print_Valid_Languages
FROM PSLANGUAGES
end-select

let $Valid_Language_Cd = 'STOPLOOP'
Do Print_Valid_Languages

end-procedure

!***********************************************************************
!                                                                      *
! Function:    Print_Valid_Languages                                   *
!                                                                      *
! Description: Print Valid Language codes in PSLANGUAGES table.        *
!                                                                      *
!***********************************************************************
begin-procedure Print_Valid_Languages

if $Valid_Language_CD != 'STOPLOOP'
   if $Valid_Language_CD != 'ENG'
      display $Valid_Language_Cd NOLINE
      display ' ' NOLINE
   end-if
else
   display ' **'
end-if

end-procedure

!***********************************************************************
!                                                                      *
! Function:    Update-Projects                                         *
!                                                                      *
! Description:                                                         *
!                                                                      *
!***********************************************************************
begin-procedure Update-Projects
let $SQL-Statement='PROJLANG,Update-Projects'

begin-select on-error=SQL-Error
PROJECTNAME            &PROJECTNAME
OBJECTTYPE             &OBJECTTYPE
OBJECTID1              &OBJECTID1
OBJECTVALUE1           &OBJECTVALUE1
OBJECTID2              &OBJECTID2
OBJECTVALUE2           &OBJECTVALUE2
OBJECTID3              &OBJECTID3
OBJECTVALUE3           &OBJECTVALUE3
OBJECTID4              &OBJECTID4
OBJECTVALUE4           &OBJECTVALUE4
NODETYPE               &NODETYPE
SOURCESTATUS           &SOURCESTATUS
TARGETSTATUS           &TARGETSTATUS
UPGRADEACTION          &UPGRADEACTION
TAKEACTION             &TAKEACTION
COPYDONE               &COPYDONE

    move &PROJECTNAME        to $PROJECTNAME
    move &OBJECTTYPE         to #OBJECTTYPE
    move &OBJECTID1          to #OBJECTID1
    move &OBJECTVALUE1       to $OBJECTVALUE1
    move &OBJECTID2          to #OBJECTID2
    move &OBJECTVALUE2       to $OBJECTVALUE2
    move &OBJECTID3          to #OBJECTID3
    move &OBJECTVALUE3       to $OBJECTVALUE3
    move &OBJECTID4          to #OBJECTID4
    move &OBJECTVALUE4       to $OBJECTVALUE4
    move &NODETYPE           to #NODETYPE
    move &SOURCESTATUS       to #SOURCESTATUS
    move &TARGETSTATUS       to #TARGETSTATUS
    move &UPGRADEACTION      to #UPGRADEACTION
    move &TAKEACTION         to #TAKEACTION
    move &COPYDONE           to #COPYDONE

    if $Base_Language_Cd != 'ENG'
        if #ObjectType = 4
            move $Base_Language_Cd to $OBJECTVALUE3
        else
            move $Base_Language_Cd to $OBJECTVALUE2
        end-if
        do Delete-ProjectItem
        do Insert-ProjectItem
    end-if


FROM PSPROJECTITEM
! WHERE PROJECTNAME IN ('PPLTOOLS', 'PPLDELETE', 'PPLTLS75', 'PPLTLS8', 'PPLTLS810') !
!Fix for T-CCARTE-KG3NY -changed the where to be more generic!
WHERE PROJECTNAME LIKE 'PPL%'
AND (OBJECTTYPE = 5 AND OBJECTVALUE2 = 'ENG') OR
    (OBJECTTYPE = 4 AND OBJECTVALUE3 = 'ENG')
end-select
end-procedure


!***********************************************************************
!                                                                      *
! Function:    Get_Base_Language                                       *
!                                                                      *
! Description: Get Base Language code from PSOPTIONS table.            *
!                                                                      *
!***********************************************************************
begin-procedure Get_Base_Language
#debug display 'Module: UPGPR  Procedure: Get_Base_Language'

   let $SQL-Statement='UPGPR,Get_Base_Language,SELECT,PSOPTIONS,Src'

begin-select on-error=SQL-Error
LANGUAGE_CD            &Base_Language_Cd

    move &Base_Language_Cd       to $Base_Language_Cd

FROM PSOPTIONS
end-SELECT
end-procedure


!***********************************************************************
!                                                                      *
! Function:    Insert-ProjectItem                                      *
!                                                                      *
! Description:                                                         *
!                                                                      *
!***********************************************************************
begin-procedure Insert-ProjectItem
let $SQL-Statement='PROJLANG,Insert-ProjectItem'
begin-sql on-error=SQL-Error
   INSERT INTO PSPROJECTITEM
           (PROJECTNAME, OBJECTTYPE,
           OBJECTID1, OBJECTVALUE1, OBJECTID2, OBJECTVALUE2,
           OBJECTID3, OBJECTVALUE3, OBJECTID4, OBJECTVALUE4,
           NODETYPE, SOURCESTATUS, TARGETSTATUS,
           UPGRADEACTION, TAKEACTION, COPYDONE)
       VALUES ($PROJECTNAME, #ObjectType,
           #ObjectId1, $ObjectValue1, #ObjectId2, $ObjectValue2,
           #ObjectId3, $ObjectValue3, #ObjectId4, $ObjectValue4,
           #NodeType, #SourceStatus, #TargetStatus,
           #UpgradeAction, #TakeAction, #COPYDONE)
end-sql
end-procedure

!***********************************************************************
!                                                                      *
! Function:    Delete-ProjectItem                                      *
!                                                                      *
! Description:                                                         *
!                                                                      *
!***********************************************************************
begin-procedure Delete-ProjectItem
let $SQL-Statement='PROJLANG,Delete-ProjectItem'
begin-sql on-error=SQL-Error
   DELETE FROM PSPROJECTITEM WHERE
          PROJECTNAME=$PROJECTNAME AND
          OBJECTTYPE=#ObjectType AND
          OBJECTVALUE1=$ObjectValue1 AND
          OBJECTVALUE2=$ObjectValue2 AND
          OBJECTVALUE3=$ObjectValue3 AND
          OBJECTVALUE4=$ObjectValue4
end-sql
end-procedure

#include 'tranctrl.sqc'      ! Common Transaction Control Procedures
#include 'reset.sqc'         ! Reset printer
#include 'curdttim.sqc'      ! Current Date and Time
#include 'datetime.sqc'      ! Routines for date and time formatting
#include 'number.sqc'        ! Routines to format numbers
#include 'prcsapi.sqc'       ! Update Process Request API
#include 'prcsdef.sqc'       ! Update Process Request variable declaration

