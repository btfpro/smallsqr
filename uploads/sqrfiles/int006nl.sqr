!***********************************************************************
!  INT006NL: Print contents of General Deduction / Genrl Ded Freq Tables
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:13:46:02                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment

Begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report

  do Init-DateTime
  do Init-Number
  move 'INT006NL' to $ReportID

  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language   ! Procedure that decides the report language
                           ! and then calls 'Report-Translation' procedure
                           ! that reads the language dep. text.
  move $INT006NL_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '
end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from INT006NL  program id
   do Init_Report_Translation($ReportID,$Language_Cd)

   do Get_Field_Information ('INT006NL', 'REPORT_TITLE', $INT006NL_ReportTitle, #dummy_width)
   do Get_Field_Information ('INT006NL', 'DEDCD1',       $INT006NL_DedCd1,      #dummy_width)
   do Get_Field_Information ('INT006NL', 'EFFDT1',       $INT006NL_EffDt1,      #dummy_width)
   do Get_Field_Information ('INT006NL', 'CALCTYPE1',    $INT006NL_CalcType1,   #dummy_width)
   do Get_Field_Information ('INT006NL', 'RT_PCT1',      $INT006NL_Rt_Pct1,     #dummy_width)
   do Get_Field_Information ('INT006NL', 'PAYFREQ1',     $INT006NL_PayFreq1,    #dummy_width)
   do Get_Field_Information ('INT006NL', 'ADD_AMT1',     $INT006NL_Add_Amt1,    #dummy_width)
   do Get_Field_Information ('INT006NL', 'DEDCD2',       $INT006NL_DedCd2,      #dummy_width)
   do Get_Field_Information ('INT006NL', 'EFFDT2',       $INT006NL_EffDt2,      #dummy_width)
   do Get_Field_Information ('INT006NL', 'CALCTYPE2',    $INT006NL_CalcType2,   #dummy_width)
   do Get_Field_Information ('INT006NL', 'RT_PCT2',      $INT006NL_Rt_Pct2,     #dummy_width)
   do Get_Field_Information ('INT006NL', 'PAYFREQ2',     $INT006NL_PayFreq2,    #dummy_width)
   do Get_Field_Information ('INT006NL', 'ADD_AMT2',     $INT006NL_Add_Amt2,    #dummy_width)
end-procedure

begin-HEADING 7

  #Include 'stdhdgtr.sqc'

  print $INT006NL_DedCd1                     (+2,25,11)
  print $INT006NL_EffDt1                     (0,37,12)
  print $INT006NL_CalcType1                  (0,50,16)
  print $INT006NL_Rt_Pct1                    (0,67,10)
  print $INT006NL_PayFreq1                   (0,78,9)
  print $INT006NL_Add_Amt1                   (0,88)

  print $INT006NL_DedCd2                     (+1,25,11)
  print $INT006NL_EffDt2                     (0,37,12)
  print $INT006NL_CalcType2                  (0,50,16)
  print $INT006NL_Rt_Pct2                    (0,67,10)
  print $INT006NL_PayFreq2                   (0,78,9)
  print $INT006NL_Add_Amt2                   (0,88)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

A.DEDCD                 (+1,25,9)

A.EFFDT  &A.EffDt
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out            (0,+3,10)

A.DED_CALC              (0,50,2)
  move 'DED_CALC' to $FieldName
  move &A.ded_calc   to $FieldValue
  do Read-Translate-Table
  print $XlatShortName  (0,+1,10)

A.DED_RATE_PCT          (0,+3,8)    EDIT B999.999

  do Get-Deduction-Freq

FROM PS_GEN_DED_TBL_NLD A

ORDER BY A.DEDCD, A.EFFDT DESC

END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC GET-DEDUCTION-FREQ
!         Get deduction frequency associated with each deduction
!***********************************************************************

BEGIN-PROCEDURE Get-Deduction-Freq

  move 0 to #count

BEGIN-SELECT

B.DEDCD
B.EFFDT
B.PAY_FREQUENCY
  if #count = 0
    print &B.pay_frequency       (0,+4,5)
    move 999 to #count
  else
    print &B.pay_frequency       (+1,-19,5)
  end-if
B.DED_ADDL_AMT                   (0,+3,11)     EDIT B9999999.99

FROM PS_GEN_DED_FRQ_NLD B
WHERE B.DEDCD            = &A.dedcd
  AND B.EFFDT            = &A.effdt
END-SELECT

END-PROCEDURE

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language

