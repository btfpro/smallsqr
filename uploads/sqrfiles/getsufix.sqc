!***********************************************************************
!  GetSufix:  Move suffix from name for MMREF-1                        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module is the confidential and proprietary information of       *
! PeopleSoft, Inc.; it is not to be copied, reproduced, or transmitted *
! in any form, by any means, in whole or in part, nor is it to be used *
! for any purpose other than that for which it is expressly provided   *
! without the written permission of PeopleSoft.                        *
!                                                                      *
! Copyright (c) 1988-2001 PeopleSoft, Inc. All Rights Reserved         *
!                                                                      *
!***********************************************************************
!                                                                      *
!      $Date:: 2/05/01 10:23a   $                                      *
!  $Revision:: 1                $                                      *
!  $Workfile:: getsufix.sqc     $                                      *
!                                                                      *
!***********************************************************************
!     Additional suffixes may be added, or existing suffixes may be    *
!     removed or changed.  The first character must be a space.        *
!     All alpha must be in uppercase.                                  *
!     If neccesary, the size of the array may be increased.  If so,    *
!     change the max_suffix_size to contain the new array size.        *
!     The put commands must fill all entries in the array.             *
!     Wherever the suffix(es) is/(are) found in the name the suffix    *
!     will be removed.  The suffix will be moved to $W2_Suffix.        *
!***********************************************************************

begin-procedure Create-gSuffix-Array

  #define max_gsuffix_size 12
  create-array name=sfxarray size = {max_gsuffix_size}
    field=suffix:char=' '

  put ' JR'    into sfxarray(0) suffix
  put ' SR'    into sfxarray(1) suffix
  put ' III'   into sfxarray(2) suffix
  put ' II'    into sfxarray(3) suffix
  put ' IV'    into sfxarray(4) suffix
  put ' ESQ'   into sfxarray(5) suffix
  put ' MD'    into sfxarray(6) suffix
  put ' M.D.'  into sfxarray(7) suffix
  put ' PHD'   into sfxarray(8) suffix
  put ' PH.D.' into sfxarray(9) suffix
  put ''       into sfxarray(10) suffix
  put ''       into sfxarray(11) suffix

end-procedure

begin-procedure Get-Suffix

  move ' ' to $W2_Suffix

  if instr($W2_Name, ' ', 0) > 0

    move $W2_Name to $sfxname
    let #W2len = length($sfxname)
    move 0 to #sfxcount

    while #sfxcount < {max_gsuffix_size}
      get $sfxsuf from sfxarray(#sfxcount) suffix
      let #sfxlen = length($sfxsuf)
      let #sfxpoint = instr($sfxname,$sfxsuf,#W2len - #sfxlen + 1)
      if #sfxpoint > 1
        let $sfxnamex = substr($sfxname,1,#sfxpoint - 1)
        let $sfxname = $sfxnamex
        let #W2len = length($sfxname)
        let $W2_Suffix = substr($sfxsuf,2,#sfxlen)
      end-if
      let #sfxcount = #sfxcount + 1
    end-while

    move $sfxname to $W2_Name

  end-if

end-procedure



