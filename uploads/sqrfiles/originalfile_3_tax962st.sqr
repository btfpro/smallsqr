!***********************************************************************
!  TAX962ST:  Selected State(s) W-2 Audit Report                       *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2007/01/17:11:48:11                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  654914                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

begin-report
  do Init-Report
  do Get-Transmitter
  do Select-Reports
  do Stdapi-Term
end-report

begin-procedure Init-Report
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Initialization
end-procedure

begin-procedure Initialization
  display ''
  display 'Selected State(s) W-2 Audit Report '
  display ' '
  move 'TAX962ST' to $ReportID
  do Get-Current-DateTime

  If $Prcs_Process_Instance = ''
    Display 'Report cannot be run outside of PeopleSoft, please use process scheduler.'
    Stop
  End-if

  do Select-US-Year-End-Parameters
  do Convert-Parameters

  move '' to $ErName
end-procedure

begin-procedure Select-Reports

#ifdef USA
  move 'USA' to $Country
#endif

begin-SELECT
F.STATE       () on-break print=never before=Before-State
                                      after=After-State
G.DESCR

  let $ProcState = rtrim(&F.STATE, ' ')
  if #filestat = 0
    if $FileType = 'D'
        if $ProcState = 'AL'  or
        $ProcState = 'KS'   or
        $ProcState = 'PA'   or
        $ProcState = 'UT'
        do Read-Tape-File
    else

        do Read-Diskette-File
    end-if
    else
      do Read-Tape-File
    end-if
  end-if

FROM PS_RC_TAX960ST  F, PS_STATE_NAMES_TBL G
WHERE F.OPRID              = $Prcs_OprID
  AND F.RUN_CNTL_ID        = $Prcs_Run_Cntl_ID
  AND F.STATE              = G.STATE
  AND $Country             = G.COUNTRY

end-SELECT
end-procedure

begin-procedure Before-State

  let $WRecordFound = 'N'
  let $ORecordFound = 'N'
  let $SRecordFound = 'N'
  let $TRecordFound = 'N'
  let $FRecordFound = 'N'
  let $State = rtrim(&F.STATE, ' ')
  let $FileIn = 'TAX960' || $State
  let $StateName = rtrim(&G.DESCR, '  ')
  let $ReportTitle = 'W-2 Audit Report - ' || $StateName
  display 'Processing ' noline
  display  $StateName
  display ' '

  !  Control printing of 'RW' Record during State processing.

  move 'Y' to $State-Process

  if $FileType = 'D'
      if $State = 'AL' or
         $State = 'KS' or
         $State = 'PA' or
         $State = 'UT'

         do Open-File
      else
      do Open-Diskette
    end-if
  else
    do Open-File
  end-if
end-procedure

begin-procedure After-State
  if $TotalsOnly <> 'N'
    if $SRecordFound = 'Y'
      if $TRecordFound <> 'Y'
        do Print-Employer-Totals
      end-if
    end-if

    if $SRecordFound = 'Y'
      if $FRecordFound <> 'Y'
        do Print-File-Totals
      end-if
    end-if
  else
    if $State-Process = 'Y'
      do Print-Employer-Totals
      do Print-File-Totals
    end-if
  end-if

  if #filestate = 0
    new-page
  end-if
end-procedure

begin-procedure Open-Diskette
  move 1 to #FileExtension
   move #FileExtension to $FileExtension 099
#ifdef OS400
   let $FileExtension = '(D' || $FileExtension || ')'
   let $FileID = '{IMPORTPREFIX}' || $FileIn || $FileExtension
#else
   let $FileID = '{IMPORTPREFIX}' || $FileIn || '.' || $FileExtension
#endif

  if $FileIn = 'TAX960AL'
      open $FileID as 1 for-reading record=512:fixed_nolf
           status=#filestat
  else
      open $FileID as 1 for-reading record=512:fixed
           status=#filestat
  end-if
  if #filestat != 0
    display $FileID noline
    display ' not found '
    display ' '
  end-if
end-procedure

begin-procedure Open-File
  let $FileID = '{IMPORTPREFIX}' || $FileIn || '{IMPORTSUFFIX}'

  if $FileIn = 'TAX960AL'
      open $FileID as 1 for-reading record=512:fixed_nolf
           status=#filestat
  else
      open $FileID as 1 for-reading record=512
           status=#filestat
  end-if
  if #filestat != 0
    display $FileID noline
    display ' not found '
    display ' '
  end-if
end-procedure

begin-procedure Process-RA-Record

    if $TotalsOnly = 'N'
      move ' ' to $Col1Hdr1
      move ' ' to $Col2Hdr1
      move ' ' to $Col3Hdr1
      move ' ' to $Col4Hdr1
      move ' ' to $Col5Hdr1
      move ' ' to $Col6Hdr1
      move ' ' to $Col7Hdr1
      move ' ' to $Col1Hdr2
      move ' ' to $Col2Hdr2
      move ' ' to $Col3Hdr2
      move ' ' to $Col4Hdr2
      move ' ' to $Col5Hdr2
      move ' ' to $Col6Hdr2
      move ' ' to $Col7Hdr2
    end-if

  do Print-RA-Record
end-procedure


begin-procedure Process-RW-Record
  do Print-RW-Record
  let $WRecordFound = 'Y'
end-procedure

begin-procedure Process-RE-Record

  if $SRecordFound = 'Y'
     if $TRecordFound <> 'Y'
        do Print-Employer-Totals
     end-if
  end-if

    if $TotalsOnly = 'N'
      move ' ' to $Col1Hdr1
      move ' ' to $Col2Hdr1
      move ' ' to $Col3Hdr1
      move ' ' to $Col4Hdr1
      move ' ' to $Col5Hdr1
      move ' ' to $Col6Hdr1
      move ' ' to $Col7Hdr1
      move ' ' to $Col1Hdr2
      move ' ' to $Col2Hdr2
      move ' ' to $Col3Hdr2
      move ' ' to $Col4Hdr2
      move ' ' to $Col5Hdr2
      move ' ' to $Col6Hdr2
      move ' ' to $Col7Hdr2
    end-if

  next-listing  NEED = 24
  do Print-RE-Record

  if $TotalsOnly = 'N'
    move $ErName              to $Employer
    new-page
    if $State = 'IN'
      move '           '      to $Col1Hdr1
      move 'Name  '           to $Col1Hdr2
      move '       '          to $Col2Hdr1
      move 'SSN'              to $Col2Hdr2
      move '                ' to $Col3Hdr1
      move '                ' to $Col4Hdr1
      move 'State Wages'   to $Col3Hdr2
      move 'State Taxes'   to $Col4Hdr2
      move '                  ' to $Col5Hdr1
      move '                  ' to $Col6Hdr1
      move '  County Wages' to $Col5Hdr2
      move '  County Taxes' to $Col6Hdr2
      move '                ' to $Col7Hdr1
      move '   Advance EIC  ' to $Col7Hdr2
    else
      if $State = 'OH'
        move '           '      to $Col1Hdr1
        move 'Name  '           to $Col1Hdr2
        move '       '          to $Col2Hdr1
        move 'SSN'              to $Col2Hdr2
        move '                ' to $Col3Hdr1
        move '                ' to $Col4Hdr1
        move 'State Wages'      to $Col3Hdr2
        move 'State Taxes'      to $Col4Hdr2
        move '                  ' to $Col5Hdr1
        move '                  ' to $Col6Hdr1
        move '  District Wages' to $Col5Hdr2
        move '  District Taxes' to $Col6Hdr2
        move '                ' to $Col7Hdr1
        move '                ' to $Col7Hdr2
        else
          move 'Last'             to $Col1Hdr1
          move 'Name'             to $Col1Hdr2
          move 'First'            to $Col2Hdr1
          move 'Name'             to $Col2Hdr2
          move 'Middle'           to $Col3Hdr1
          move 'Name'             to $Col3Hdr2
          move 'SSN'              to $Col4Hdr2
          move '              '   to $Col5Hdr1
          move '   State Wages'   to $Col5Hdr2
          move '              '   to $Col6Hdr1
          move '   State Taxes'   to $Col6Hdr2
          move '                ' to $Col7Hdr1
          move '                ' to $Col7Hdr2
      end-if
    end-if
  end-if
end-procedure

begin-procedure Process-RS-Record
  do Print-RS-Record
  let $SRecordFound = 'Y'
  let $TRecordFound = 'N'
end-procedure

begin-procedure Process-RO-Record
  do Print-RO-Record
  let $ORecordFound = 'Y'
end-procedure

begin-procedure Process-Employer-Record
  do Print-RE-Record
  if $TotalsOnly = 'N'
    move $ErName              to $Employer
    new-page
  end-if
end-procedure

begin-procedure Process-RF-Record
  do Print-RF-Record
  let $FRecordFound = 'Y'
end-procedure

begin-procedure Process-RU-Record
  do Print-RU-Record
end-procedure

begin-procedure Process-RT-Record
  do Print-RT-Record
  let $TRecordFound = 'Y'
end-procedure

begin-procedure Print-Employer-Totals

  move 'Y' to $TRecordFound
  if #T_StateWages <> 0
    or #T_StateTax <> 0
    or #T_LclWages <> 0
    or #T_LclTax   <> 0
    or #T_StateEIC <> 0

    print 'Employer Totals - Wages: ' (+2,1)
    print  #T_StateWages (0,33) edit 99,999,999,999.99    !State Taxable Wages

    print 'Taxes: ' (+1,19)
    print  #T_StateTax   (0,36) edit 999,999,999.99       !State Taxes

    if $CombineStateAndLocal  = 'Y'
      print 'Local Wages: ' (+1,13)
      print  #T_LclWages   (0,36) edit 999,999,999.99     !Local Wages

      print 'Local Tax:   ' (+1,13)
      print  #T_LclTax     (0,36) edit 999,999,999.99     !Local Taxes

      if $State = 'IN'
        print 'State EIC:   ' (+1,13)
        print  #T_StateEIC   (0,36) edit 999,999,999.99   !State EIC
      end-if

    end-if
  end-if

  add #T_StateWages to #F_StateWages
  add #T_StateTax   to #F_StateTax
  add #T_LclWages   to #F_LclWages
  add #T_LclTax     to #F_LclTax
  add #T_StateEIC   to #F_StateEIC

  move 0 to #T_StateWages
  move 0 to #T_StateTax
  move 0 to #T_LclWages
  move 0 to #T_LclTax
  move 0 to #T_StateEIC

end-procedure

begin-procedure Print-File-Totals
  print 'File Totals - Wages: ' (+2,1)
  print  #F_StateWages (0,33) edit 99,999,999,999.99    !State Taxable Wages
  print 'Taxes: ' (+1,15)
  print  #F_StateTax   (0,36) edit 999,999,999.99       !State Taxes
  if $CombineStateAndLocal  = 'Y'
    print 'Local Wages: ' (+1,13)
    print  #F_LclWages   (0,36) edit 999,999,999.99     !Local Wages

    print 'Local Tax:   ' (+1,13)
    print  #F_LclTax     (0,36) edit 999,999,999.99     !Local Taxes
    if $State = 'IN'
      print 'State EIC:   ' (+1,13)
      print  #F_StateEIC   (0,36) edit 999,999,999.99   !State EIC
    end-if
  end-if
  move 0 to #F_StateWages
  move 0 to #F_StateTax
  move 0 to #F_LclWages
  move 0 to #F_LclTax
  move 0 to #F_StateEIC
  move '' to $ErName
end-procedure

Begin-Procedure Print-RW-RO-Headings

  print 'SSN'                            (+2,1)
  print 'FWT Gross'                       (0,17)
  print 'FWT Tax'                         (0,35)
  print 'FICA Gross'                      (0,48)
  print 'FICA Tax'                        (0,66)
  print 'Medi Wages'                      (0,80)
  print 'Medi Tax'                        (0,98)
  print 'FICA Tips'                       (0,113)
  print 'EIC'                             (0,135)
  print 'Dpnd Care'                       (0,145)
  print '401k'                            (0,166)

  print '403b'                           (+1,26)
  print '408k6'                           (0,41)
  print '457b'                            (0,58)
  print '501c18d'                         (0,71)
  print 'Military'                        (0,86)
  print 'NQ 457 Y'                        (0,102)
  print 'NQ 457 N'                        (0,118)
  print 'GTL ER Cost'                     (0,131)
  print 'NS Stock Op'                     (0,147)
  print 'Thrd Pty Tax'                    (0,160)

  print 'Alloc TIPS '                    (+1,16)
  print 'Unc Tax TIPS'                    (0,30)
  print 'Med Sav Act'                     (0,47)
  print '408p Retire'                     (0,62)
  print 'Adopt Exp '                      (0,82)
  print 'Unc SS GTL'                      (0,96)
  print 'Unc Med GTL'                     (0,111)
  print 'Fed WG - PR'                     (0,127)
  print 'Comm - PR'                       (0,145)
  print 'Allow - PR'                      (0,159)

  print 'TIPS - PR'                      (+1,21)
  print 'Tot Comp-PR'                     (0,35)
  print 'Tax - PR'                        (0,54)
  print 'Retire - PR'                     (0,67)
  print 'Fed WG - TY'                     (0,83)
  print 'Fed TX - TY'                     (0,99)
  print '           '                     (0,115)
  print '           '                     (0,131)

  move '  ' to $FullLine
  print $FullLine                        (+1,180)

end-procedure

begin-procedure Get-Transmitter
begin-SELECT
TX.COMPANY, TX.BALANCE_YEAR

FROM PS_TAXRPT_RUNCTL TX
end-SELECT

begin-SELECT
X.DESCR

FROM  PS_W2_COMPANY X
WHERE COMPANY = &TX.Company
  AND X.CALENDAR_YEAR = &TX.Balance_Year
end-SELECT
  move &X.Descr to $TrName
end-procedure


begin-procedure Convert-Parameters

  move $RC_US_YE.Totals_Only to $TotalsOnly

Begin-Select

RC960.REPORTING_MEDIUM
RC960.DISKETTE_TYPE
RC960.EMPLR_PIN
RC960.RESUB_INDICATOR
RC960.RESUB_TLCN
RC960.CONTACT_NAME_SSA
RC960.TELEPHONE_NBR
RC960.PHONE_EXTN
RC960.EMAIL_ADDR_MMREF
RC960.CONTACT_FAX
RC960.NOTIFY_METHOD

FROM PS_TAX960_PARM RC960
End-Select

  if rtrim(&RC960.CONTACT_NAME_SSA, ' ') = ''
    Show 'MMREF Parameters must be set up on SETUP Menu.'
    Show 'Re-execute after establishing MMREF Parameters.'
    Stop
  End-if

  move &RC960.EMAIL_ADDR_MMREF to $RC960.EMAIL_ADDR
  uppercase $RC960.EMAIL_ADDR
  move &RC960.Reporting_Medium to $FileType
end-procedure


#Include 'hdg962.sqc'    !Report and Column Headings
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'rdtap962.sqc'  !Read state mag tape file
#Include 'stdapi.sqc'    !Update Process API
#Include 'txrnctl2.sqc'  !US Year End Process Scheduler Run Controls
