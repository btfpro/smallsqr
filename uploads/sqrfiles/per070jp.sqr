!***********************************************************************
! PER070JP : Open Post Checklist                                       *
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2006 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/19:14:57:02                                 !
!       $Release:  HR9                                                 !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!                                                                      *
!***********************************************************************
!This report compares the Headcount Goals with Actual Headcount and    *
!and generates the open post count department wise. Only depts which   *
!does not match Actual Headcount will be displayed.                    *
!                                                                      *
!***********************************************************************

Begin-setup

#define  REPORTID  'PER070JP'
#include 'setenv.sqc'      !Set environment
#include 'ptset02.sqc'     !Init printer and page sizeLandscape

end-setup


begin-program
    do Init-DateTime
    do Init-Number
    do Get-Current-DateTime
    do Define-Prcs-Vars
    do Init-Report
    do Process-Main
    do Delete-From-AE-Tables
    do Update-Prcs-Run-Status
end-program

!*************************************************************************!
! Init-Report
!*************************************************************************!
begin-procedure Init-Report

!***********************************************************************
! Translations procedures & Strings Table retrievals.                  *
!***********************************************************************

  move {REPORTID} to $ReportID
  let #Error_Total = 0
  do Get-Run-Control-Parms
  do Get-Run-Cntl-Acq
  do get-org-plan-date
  do get-setid
  
  do Init_Report_Translation ('PER070JP', $curr_language_cd)
  do Append_Report_Translation ('PER070JP')

  do Get_Field_Information ('PER070JP' ,'PER070JP_TITLE'    ,$PER070JP_Title          ,#DW)
  do Get_Field_Information ('PER070JP' ,'ORG_PLAN'          ,$org_plan_lbl            ,#DW)
  do Get_Field_Information ('PER070JP' ,'ORG_DATE'          ,$org_date_lbl            ,#DW)
  do Get_Field_Information ('PER070JP' ,'BUNIT'             ,$BUnit_lbl               ,#DW)
  do Get_Field_Information ('PER070JP' ,'DEPT'              ,$dept_lbl                ,#DW)
  do Get_Field_Information ('PER070JP' ,'SUPV_LVL'          ,$supv_lvl_lbl            ,#DW)
  do Get_Field_Information ('PER070JP' ,'JOBCODE'           ,$JOBCODE_lbl             ,#DW)
  do Get_Field_Information ('PER070JP' ,'GRADE'             ,$GRADE_lbl               ,#DW)
  do Get_Field_Information ('PER070JP' ,'LEVEL'             ,$Lvl_lbl                 ,#DW)
  do Get_Field_Information ('PER070JP' ,'POST_COUNT'        ,$Post_Count_lbl          ,#DW)
  
  move $PER070JP_Title to $ReportTitle
  let $ReportDate            = $AsOfToday          ! Printed in header
  let $ReportTime            = $AsOfNow            ! Printed in header

end-procedure Init-Report

!*************************************************************************!
! Get-Run-Cntl-Acq
!*************************************************************************!
!*************************************************************************!
! Get the Run Control Parameters
!*************************************************************************!
begin-procedure Get-Run-Cntl-Acq
  let $org_plan_JPN  = ''
  let $org_date_JPN  = ''
  let $Business_Unit  = ''

begin-SELECT
RC.BUSINESS_UNIT
RC.ORG_PLAN_JPN
RC.SETID
  let $org_plan_JPN  = &RC.ORG_PLAN_JPN
  let $Business_Unit = &RC.BUSINESS_UNIT
  !let $setid         = &RC.SETID

FROM PS_RC_IDO_JPN RC
WHERE RC.OPRID = $prcs_oprid
AND RC.RUN_CNTL_ID = $prcs_run_cntl_id
end-SELECT
end-procedure !Get-Run-Cntl-Acq

!*************************************************************************!
! get-setid
!*************************************************************************!
!*************************************************************************!
! Get SETID
!*************************************************************************!
begin-procedure get-setid
begin-select
CR.SETID
 let $setid = &CR.SETID
FROM PS_SET_CNTRL_REC CR
WHERE CR.RECNAME = 'JOBCODE_TBL' AND CR.SETCNTRLVALUE = $Business_Unit
end-select
end-procedure !get-setid

!*************************************************************************!
! get-org-plan-date
!*************************************************************************!
!*************************************************************************!
! Get the org-plan-date and hc-type-jpn
!*************************************************************************!
begin-procedure get-org-plan-date
begin-select
A.HC_TYPE_JPN
A.ORG_DATE_JPN
 let $org_date_JPN_In  = &A.ORG_DATE_JPN
 do Convert-To-DTU-Date ($org_date_JPN_In, $org_date_JPN)
 let $AsOfDate = &A.ORG_DATE_JPN
 let $hc_type_JPN  = &A.HC_TYPE_JPN
 
FROM PS_IDO_ORGPLAN_JPN A
WHERE A.ORG_PLAN_JPN = $org_plan_JPN
end-select
end-procedure !get-org-plan-date

!*************************************************************************!
! Process-Main
!*************************************************************************!
!*************************************************************************!
! Get Dept Detail based on hc-type-jpn
!*************************************************************************!

begin-procedure Process-Main

  if $hc_type_JPN = 'S'
   do Get-suplvl-data
  end-if

  if $hc_type_JPN = 'J'
   do Get-jobcode-data
  end-if

  if $hc_type_JPN = 'G'
   do Get-grade-data
  end-if
end-procedure !Process-Main

!*************************************************************************!
! Get-suplvl-Data
!*************************************************************************!
!*************************************************************************!
! Get Dept Detail and calculate open count if hc-type-jpn is supervisor lvl
!*************************************************************************!

begin-procedure Get-suplvl-data
begin-select DISTINCT
DEPTID &DEPTID
SUPV_LVL_ID &SUPV_LVL_ID

 let $DeptID        = &DEPTID
 let $SUPV_LVL_ID   = &SUPV_LVL_ID 
 let $BU = $Business_Unit
 
 do Get-HCDEPT-headcount-suplvl
 do Get-Actual-headcount-suplvl
 let #open-count = #HCDEPT-headcount - #Actual-headcount
 
 if #open-count <> 0
  do Get-Department-Name
  do Get-Inactive-Dept
  do get-setid-supv
  do Get-Supvlvl-Name
  do Print_Line
 end-if
 
FROM PS_IDO_NEW_ORG_JPN 
WHERE JOBINSTANCE = #prcs_job_instance
AND BUSINESS_UNIT = $Business_Unit
UNION
SELECT
DEPTID, 
SUPV_LVL_ID 
FROM PS_IDO_HC_DEPT_JPN 
WHERE ORG_PLAN_JPN = $org_plan_JPN
AND BUSINESS_UNIT = $Business_Unit
ORDER BY 1,2
end-select
end-procedure !Get-suplvl-data

!*************************************************************************!
! Get-HCDEPT-headcount-suplvl
!*************************************************************************!
begin-procedure Get-HCDEPT-headcount-suplvl
 let #HCDEPT-headcount = 0
begin-select
C.HEADCOUNT_JPN
 let #HCDEPT-headcount = &C.HEADCOUNT_JPN
FROM PS_IDO_HC_DEPT_JPN C
WHERE  C.ORG_PLAN_JPN = $org_plan_JPN
AND C.BUSINESS_UNIT = $Business_Unit
AND C.DEPTID = $DeptID
AND C.SUPV_LVL_ID = $SUPV_LVL_ID
end-select
end-procedure !Get-HCDEPT-headcount-suplvl

!*************************************************************************!
! Get-Actual-headcount-suplvl
!*************************************************************************!
begin-procedure Get-Actual-headcount-suplvl
 let #Actual-headcount = 0
begin-select
COUNT(*) &ACT_COUNT
 let #Actual-headcount = &ACT_COUNT
FROM PS_IDO_NEW_ORG_JPN A
WHERE A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
AND A.DEPTID = $DeptID
AND A.SUPV_LVL_ID = $SUPV_LVL_ID
end-select
end-procedure !Get-Actual-headcount-suplvl

!*************************************************************************!
! Get-jobcode-data
!*************************************************************************!
begin-procedure Get-jobcode-data
begin-select DISTINCT
J.DEPTID 
J.JOBCODE 

 let $DeptID        = &J.DEPTID
 let $JobCode       = &J.JOBCODE
 let $BU = $Business_Unit
 
 do Get-HCDEPT-headcount-jobcode
 do Get-Actual-headcount-jobcode
 let #open-count = #HCDEPT-headcount - #Actual-headcount
 
 if #open-count <> 0
  do Get-Department-Name
  do Get-Inactive-Dept
  do Get-Job-Title
  do Print_Line
 end-if

FROM PS_IDO_NEW_ORG_JPN J
WHERE JOBINSTANCE = #prcs_job_instance
AND BUSINESS_UNIT = $Business_Unit
UNION
SELECT
DEPTID, 
JOBCODE 
FROM PS_IDO_HC_DEPT_JPN 
WHERE ORG_PLAN_JPN = $org_plan_JPN
AND BUSINESS_UNIT = $Business_Unit
ORDER BY 1,2
end-select
end-procedure !Get-jobcode-data

!*************************************************************************!
! Get-HCDEPT-headcount-jobcode
!*************************************************************************!
begin-procedure Get-HCDEPT-headcount-jobcode
 let #HCDEPT-headcount = 0
begin-select
C1.HEADCOUNT_JPN
 let #HCDEPT-headcount = &C1.HEADCOUNT_JPN
FROM PS_IDO_HC_DEPT_JPN C1
WHERE  C1.ORG_PLAN_JPN = $org_plan_JPN
AND C1.BUSINESS_UNIT = $Business_Unit
AND C1.DEPTID = $DeptID
AND C1.JOBCODE = $JobCode
end-select
end-procedure !Get-HCDEPT-headcount-jobcode

!*************************************************************************!
! Get-Actual-headcount-jobcode
!*************************************************************************!
begin-procedure Get-Actual-headcount-jobcode
 let #Actual-headcount = 0
begin-select
COUNT(*) &ACT_COUNT1
 let #Actual-headcount = &ACT_COUNT1
FROM
PS_IDO_NEW_ORG_JPN A
WHERE 
A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
AND A.DEPTID = $DeptID
AND A.JOBCODE = $JobCode
end-select
end-procedure !Get-Actual-headcount-jobcode

!*************************************************************************!
! Get-grade-data
!*************************************************************************!
begin-procedure Get-grade-data
begin-select DISTINCT
G.DEPTID 
G.SAL_ADMIN_PLAN 
G.GRADE 

 let $DeptID         = &G.DEPTID
 let $SAL_ADMIN_PLAN = &G.SAL_ADMIN_PLAN 
 let $GRADE          = &G.GRADE
 let $BU = $Business_Unit

 do get-setid-grade
 let $SETID_SALARY=$SETID_GRADE

 do Get-HCDEPT-headcount-grade
 do Get-Actual-headcount-grade
 let #open-count = #HCDEPT-headcount - #Actual-headcount
 
 if #open-count <> 0
  do Get-Department-Name
  do Get-Inactive-Dept
  do Get-Grade-Name
  do Print_Line
 end-if
 
FROM PS_IDO_NEW_ORG_JPN G
WHERE JOBINSTANCE = #prcs_job_instance
AND BUSINESS_UNIT = $Business_Unit
UNION
SELECT
DEPTID,
SAL_ADMIN_PLAN, 
GRADE

FROM PS_IDO_HC_DEPT_JPN 
WHERE 
ORG_PLAN_JPN = $org_plan_JPN
AND BUSINESS_UNIT = $Business_Unit
ORDER BY 1,2,3
end-select
end-procedure !Get-grade-data

!*************************************************************************!
! Get-HCDEPT-headcount-grade
!*************************************************************************!
begin-procedure Get-HCDEPT-headcount-grade
 let #HCDEPT-headcount = 0
begin-select
!T2.TREE_LEVEL_NUM
C2.HEADCOUNT_JPN

 let #HCDEPT-headcount = &C2.HEADCOUNT_JPN
 !let #tree_lvl_num = &T2.TREE_LEVEL_NUM
 
FROM PS_IDO_HC_DEPT_JPN C2
!, PSTREENODE T2
WHERE  C2.ORG_PLAN_JPN = $org_plan_JPN
AND C2.BUSINESS_UNIT = $Business_Unit
AND C2.DEPTID = $DeptID
AND C2.SAL_ADMIN_PLAN = &G.SAL_ADMIN_PLAN
AND C2.GRADE = &G.GRADE
!AND T2.TREE_NODE = C2.DEPTID
end-select
end-procedure !Get-HCDEPT-headcount-grade

!*************************************************************************!
! Get-Actual-headcount-grade
!*************************************************************************!
begin-procedure Get-Actual-headcount-grade
 let #Actual-headcount = 0
begin-select
COUNT(*) &ACT_COUNT2
 let #Actual-headcount = &ACT_COUNT2
FROM
PS_IDO_NEW_ORG_JPN A
WHERE
A.JOBINSTANCE = #prcs_job_instance
AND A.BUSINESS_UNIT = $Business_Unit
AND A.DEPTID = $DeptID
AND A.SAL_ADMIN_PLAN = &G.SAL_ADMIN_PLAN
AND A.GRADE = &G.GRADE
end-select
end-procedure !Get-Actual-headcount-grade

!*************************************************************************!
! get-setid-dept
!*************************************************************************!
begin-procedure get-setid-supv
begin-select
A5.SETID
 let $SETID_DEPT = &A5.SETID
FROM
PS_SET_CNTRL_REC A5
WHERE A5.RECNAME = 'SUPVSR_LVL_TBL'
AND A5.SETCNTRLVALUE = $Business_Unit
end-select
end-procedure !get-setid-supv

!*************************************************************************!
! get-setid-grade
!*************************************************************************!
begin-procedure get-setid-grade
begin-select
A6.SETID
 let $SETID_GRADE = &A6.SETID
FROM
PS_SET_CNTRL_REC A6
WHERE A6.RECNAME = 'SAL_GRADE_TBL'
AND A6.SETCNTRLVALUE = $Business_Unit
end-select
end-procedure !get-setid-grade

!*************************************************************************!
! Get-Inactive-Dept
!*************************************************************************!

begin-procedure Get-Inactive-Dept

begin-select
EFF_STATUS
FROM PS_DEPT_TBL 
WHERE SETID = $Business_Unit
AND DEPTID = $DeptID
AND EFFDT = (SELECT MAX(EFFDT) 
FROM PS_DEPT_TBL 
WHERE SETID = $Business_Unit
AND DEPTID = $DeptID
AND EFFDT <= &A.ORG_DATE_JPN)
end-select

end-procedure !Get-Inactive-Dept

begin-heading 10
#include 'stdhdgtr.sqc'

   print $org_plan_lbl (+1,1,22)
   print $org_plan_JPN (0,25,4)
   print $org_date_lbl (0,30,25)
   print $org_date_JPN (0,55,10)
   print $BUnit_lbl (+1,1,15)
   print $Business_Unit (0,16,6)
   
   !print $Lvl_lbl (+1,1,4)
   !print $dept_lbl (0,5,40)
   print $dept_lbl (+1,1,40)
   if $hc_type_JPN = 'S'
    print $supv_lvl_lbl (0,61,30)
   end-if
   if $hc_type_JPN = 'J' 
    print $JOBCODE_lbl (0,61,30)
   end-if
   if $hc_type_JPN = 'G'
    print $GRADE_lbl (0,61,30)
   end-if
   print $Post_Count_lbl (0,91,20)
   print '-' (+1,1,160) FILL
end-heading
!*************************************************************************!
! Print_Line
!*************************************************************************!
begin-procedure Print_Line
  !print #tree_lvl_num (+1,1, 4) edit 99
  !print $DeptID (0,5,10) 
  print $DeptID (+1,1,10) 
  print $DeptName (0,20,40)  
  if $hc_type_JPN = 'S'
   print $SupvlvlName (0,61,30) 
  end-if 
  if $hc_type_JPN = 'J'
   print $JobTitle (0,61,30)   
  end-if  
  if $hc_type_JPN = 'G'
   !print $GRADE (0,61,30)
   print $GradeName (0,61,30)
  end-if  
   print #open-count (0,91,10) edit 9999
  if &EFF_STATUS = 'I'
   print 'Inactive Department' (0,103,20)
  end-if
end-procedure !Print_Line

!*************************************************************************!
! Delete-From-AE-Tables
!*************************************************************************!

begin-procedure Delete-From-AE-Tables

begin-sql
Delete from PS_IDO_NEW_ORG_JPN
Where JOBINSTANCE = #prcs_job_instance
end-sql

end-procedure !Delete-From-AE-Tables

!*************************************************************************!
! SQC Files for called procedures
!*************************************************************************!
#include 'prcsdef.sqc'       !Update Process Request variable declare
#include 'prcsapi.sqc'       !Update Process Request API
#include 'curdttim.sqc'      !Get-Current-DateTime procedure
#include 'datetime.sqc'      !Routines for date and time formatting
#include 'number.sqc'        !Routines to format numbers
#include 'sqrtrans.sqc'      !Translate SQR strings to given language
#include 'datemath.sqc'      !Date conversion procedures
#include 'getsupjp.sqc'     ! Get Supervisor Level Name
#Include 'getempnm.sqc'     ! Get Employee Name
#include 'getdptnm.sqc'     ! Get Department Name from the Department Table
#include 'getjobtl.sqc'     ! Get Job Title
#include 'getgrdjp.sqc'     ! Get Grade Name