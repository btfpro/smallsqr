
!**************************************************
!  PER065JP: Intercompany Transfer Employee list  *
!**************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!                                                                      *
! This module contains confidential and proprietary information        *
! of Oracle; it is not to be copied, reproduced, or transmitted        *
! in any form, by any means, in whole or in part, nor is it to         *
! be used for any purpose other than that for which it is              *
! expressly provided under the applicable license agreement.           *
!                                                                      *
! Copyright (C) 2007 Oracle. All Rights Reserved.                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!          $Date:  2007/08/07:05:54:42                                 !
!       $Release:  HR9                                                 !
!    $Resolution:  689848                                              !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization
#define  COMPANY_TBL
!#define  JOB
#include 'rellang.sqc'  !Get the data from related language record
#include 'rellngjp.sqc'  !Get the data from related language record

!--------------------------------!
!report procedure overview
!--------------------------------!
begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  DO Ending-Report
  do Reset
  do Stdapi-Term
end-report

!--------------------------------!
!heading section
!--------------------------------!
begin-heading 7
do Get_Field_Information ('PER065JP',     'REPORT_TITLE',         $REPORTTITLE,   #DW)
do Get_Field_Information ('PER065JP',     'EMPLID',         $TITLE_EMPLID,   #DW)
do Get_Field_Information ('PER065JP',     'EMPL_RCD',            $TITLE_EMPL_RCD,          #DW)
do Get_Field_Information ('PER065JP',     'FROM_DATE',         $TITLE_FROM_DATE,   #DW)
do Get_Field_Information ('PER065JP',     'THRU_DATE',         $TITLE_THRU_DATE,   #DW)
do Get_Field_Information ('PER065JP',     'INTCP_COMPANY',         $TITLE_COMPANY,   #DW)
do Get_Field_Information ('PER065JP',     'INTCP_XFR_START_DT',         $TITLE_START_DT,   #DW)
do Get_Field_Information ('PER065JP',     'INTCP_XFR_END_DT',         $TITLE_END_DT,   #DW)
do Get_Field_Information ('PER065JP',     'INTCP_COMPANY_DESC',         $TITLE_DESCR,   #DW)
do Get_Field_Information ('PER065JP',     'MESSAGE_TEXT',         $LINE_MESSAGE,   #DW)
do Get_Field_Information ('HR',         'NAME',            $TITLE_NAME,          #DW)

  #Include 'stdhdgtr.sqc'

!--------------------------------------------!!
!print the following fields into page header !
!--------------------------------------------!
PRINT $TITLE_FROM_DATE (-1,1)
PRINT $FROM_DATE        (0,15)
PRINT $TITLE_THRU_DATE (+1,1)
PRINT $THRU_DATE        (0,15)

!-------------------------------!
!print the title of each field  !
!-------------------------------!
PRINT   $TITLE_EMPLID            (+2,1)
PRINT   $TITLE_EMPL_RCD          (0,15)
PRINT   $TITLE_NAME              (0,35)
PRINT   $TITLE_COMPANY           (0,67)
PRINT   $TITLE_DESCR             (0,87)
PRINT   $TITLE_START_DT          (0,118)
PRINT   $TITLE_END_DT            (0,141)
print   '-'                      (+1,1,176)  fill

end-heading

!--------------------------------!
!Init-Report section             !
!--------------------------------!
begin-procedure Init-Report
  #Define Year4 '1'
  move 'PER065JP' to $ReportID
  do Stdapi-Init

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

  LET $PNLGRPNAME = &RUN_CNTL_HR_JPN.PNLGRPNAME


end-procedure

!--------------------------------!
!Get-Values section             !
!--------------------------------!
begin-procedure Get-Values
let $language_cd = $PRCS_LANGUAGE_CD
  do Get-From-Thru-Date
  do Format-DateTime($FromDate, $FROM_DATE, {DEFYMD}, '', '')
  do Format-DateTime($ThruDate, $THRU_DATE, {DEFYMD}, '', '')

end-procedure

!--------------------------------!
!Process-Main section            !
!--------------------------------!
begin-procedure Process-Main
let #DATA_COUNT = 0
let $_TableAlias = 'A'
   do Security-Param-JPN

BEGIN-SELECT DISTINCT
A.EMPLID &A.EMPLID
A.EMPL_RCD &A.EMPL_RCD
A.EFFDT &A.EFFDT
A.EFFSEQ &A.EFFSEQ

  LET $EMPLID = &A.EMPLID
  LET #EMPL_RCD = &A.EMPL_RCD
  LET $EFFDT = &A.EFFDT
  LET #EFFSEQ = &A.EFFSEQ

A.EMPL_STATUS &A.EMPL_STATUS
A.COMPANY &A.COMPANY
C.INTCP_XFR_FLG &C.INTCP_XFR_FLG
C.INTCP_XFR_START_DT &C.INTCP_XFR_START_DT
  do Format-DateTime(&C.INTCP_XFR_START_DT, $INTCP_XFR_START_DT, {DEFYMD}, '', '')
C.INTCP_XFR_END_DT &C.INTCP_XFR_END_DT
  do Format-DateTime(&C.INTCP_XFR_END_DT, $INTCP_XFR_END_DT, {DEFYMD}, '', '')

C.INTCP_COMPANY &C.INTCP_COMPANY
C.INTCP_COMPANY2 &C.INTCP_COMPANY2
B.NAME
 LET $EMPL_NAME = RTRIM(&B.NAME,' ')


!-----------------------------!
!judge the printed field      !
!-----------------------------!
 LET $INTCP_COMPANY = RTRIM(&C.INTCP_COMPANY,' ')
 LET $INTCP_COMPANY2 = RTRIM(&C.INTCP_COMPANY2,' ')
 let $CompanyName = ' '
 let $Company = ' '

 do Active-Employee

 EVALUATE $INTCP_COMPANY
    WHEN = ' '
    WHEN = ''
      if $INTCP_COMPANY2 =  ' ' OR $INTCP_COMPANY2 =  ''
       do get-multiple-job
         IF #DATA_COUNT2 = 0
         let $CompanyName = ' '
         let $Company = ' '
         END-IF
       if $Active_empl = 'Y'
          do print-line
       end-if
      ELSE
!        let $Company = ' '
!        let $CompanyName = $INTCP_COMPANY2
!        let $JOB-INTCP_COMPANY_DESC = $CompanyName
!        do Get_Related_JOB($EMPLID,#EMPL_RCD,$EFFDT,#EFFSEQ)
!        let $CompanyName = $JOB-INTCP_COMPANY_DESC
       let $Company = $INTCP_COMPANY2
       DO Get-IntcpxCpy-Data
       let $CompanyName = $IntcpxCpyName
       if $Active_empl = 'Y'
          do print-line
       end-if
      end-if
    break

    when-other
    let $Company = &C.INTCP_COMPANY
!    let $CompanyName = &C.INTCP_COMPANY2
    DO Get-Company-Data

   if $Active_empl = 'Y'
      do print-line
   end-if
    break
 end-evaluate

 if $Active_empl = 'Y'
   LET #DATA_COUNT = #DATA_COUNT + 1
 end-if


FROM  PS_JOB A, PS_PERSON_NAME B, PS_JOB_JR C

WHERE A.EMPLID = B.EMPLID
  AND A.HR_STATUS = 'A'
  AND A.PER_ORG = 'EMP'
  AND A.EFFDT <=
      (SELECT MAX(H.EFFDT)
       FROM PS_JOB H
       WHERE H.EMPLID = A.EMPLID
         AND H.EMPL_RCD = A.EMPL_RCD
         AND H.EFFDT <= $Asoftoday)
  AND A.EFFSEQ =
      (SELECT MAX(I.EFFSEQ)
       FROM PS_JOB I
       WHERE I.EMPLID = A.EMPLID
         AND I.EMPL_RCD = A.EMPL_RCD
         AND I.EFFDT = A.EFFDT)
  AND C.EMPLID = A.EMPLID
  AND C.EMPL_RCD = A.EMPL_RCD
  AND C.EFFDT = A.EFFDT
  AND C.EFFSEQ = A.EFFSEQ
  AND C.INTCP_XFR_FLG = '2'
  AND C.INTCP_XFR_END_DT >= $FromDate
  AND C.INTCP_XFR_END_DT <= $ThruDate
       [$SecurityClausewithERN]


END-SELECT

END-PROCEDURE

!---------------------------------------------!
!get-multiple-job section
!retrieve the job data having EMPL_RCD over 1 !
!---------------------------------------------!
begin-procedure get-multiple-job
LET #DATA_COUNT2 = 0
let $_TableAlias = 'AA'
 do Security-Param-JPN

BEGIN-SELECT DISTINCT
AA.EMPLID &AA.EMPLID
AA.EMPL_RCD &AA.EMPL_RCD
AA.EFFDT &AA.EFFDT
AA.EFFSEQ &AA.EFFSEQ

 LET $AA_EFFDT = &AA.EFFDT

AA.EMPL_STATUS &AA.EMPL_STATUS
AA.COMPANY &AA.COMPANY
CC.INTCP_XFR_FLG &CC.INTCP_XFR_FLG

 LET $INTCP_COMPANY2 = RTRIM(&AA.COMPANY,' ')
 EVALUATE $INTCP_COMPANY2
    WHEN = ' '
    WHEN = ''
    let $Company = ' '
    let $CompanyName = ' '
    break

    when-other
    let $Company = &AA.COMPANY
    DO Get-Company-Data
    break

 end-evaluate
   LET #DATA_COUNT2 = #DATA_COUNT2 + 1


FROM PS_JOB AA, PS_JOB_JR CC

WHERE AA.EMPLID = &A.EMPLID
  AND AA.EMPL_RCD <> &A.EMPL_RCD
  AND AA.EMPL_RCD >= 1
  AND AA.HR_STATUS = 'A'
  AND AA.EFFDT =
      (SELECT MAX(HH.EFFDT)
       FROM PS_JOB HH
       WHERE HH.EMPLID = AA.EMPLID
         AND HH.EMPL_RCD = AA.EMPL_RCD
         AND HH.EFFDT <= $Asoftoday)
  AND AA.EFFSEQ =
      (SELECT MAX(II.EFFSEQ)
       FROM PS_JOB II
       WHERE II.EMPLID = AA.EMPLID
         AND II.EMPL_RCD = AA.EMPL_RCD
         AND II.EFFDT = AA.EFFDT)
  AND CC.EMPLID = AA.EMPLID
  AND CC.EMPL_RCD = AA.EMPL_RCD
  AND CC.EFFDT = AA.EFFDT
  AND CC.EFFSEQ = AA.EFFSEQ

      [$SecurityClausewithERN]

END-SELECT

END-PROCEDURE

!-----------------------------!
!Active-Employee section      !
!This section validates that  !
!an employee is still active  !
!as of the Thru date          !
!-----------------------------!
begin-procedure Active-Employee

let $Active_empl = 'Y'

begin-select
JB.EMPLID
JB.EFFDT

      let $Active_empl = 'N'

FROM  PS_JOB JB
WHERE JB.EMPLID         = &A.EMPLID
AND   JB.EMPL_RCD       = &A.EMPL_RCD
AND   JB.HR_STATUS = 'I'
AND   JB.EFFDT          = (SELECT MAX(JB1.EFFDT) FROM PS_JOB JB1
                        WHERE JB1.EMPLID = JB.EMPLID
                        AND JB1.EMPL_RCD = JB.EMPL_RCD
                        AND JB1.EFFDT     <= $ThruDate)
AND   JB.EFFSEQ         = (SELECT MAX(JB2.EFFSEQ) FROM PS_JOB JB2
                        WHERE JB2.EMPLID = JB.EMPLID
                        AND JB2.EMPL_RCD = JB.EMPL_RCD
                        AND JB2.EFFDT     = JB.EFFDT)
end-select

END-PROCEDURE

!-----------------------------!
!print-line section          !
!-----------------------------!
begin-procedure print-line

 PRINT &A.EMPLID (+1,1)
 PRINT &A.EMPL_RCD (0,15)
 PRINT $EMPL_NAME (0,35)
! PRINT &C.INTCP_COMPANY (0,67)
 PRINT $Company (0,67)
 PRINT $CompanyName (0,87)
 PRINT $INTCP_XFR_START_DT (0,118)
 PRINT $INTCP_XFR_END_DT (0,141)

END-PROCEDURE

!-----------------------------!
!Ending-Report section        !
!-----------------------------!
begin-procedure Ending-Report

  if #DATA_COUNT  = 0
        do Get_Field_Information ('PER065JP',     'MESSAGE_TEXT',         $LINE_MESSAGE,   #DW)
        PRINT $LINE_MESSAGE (+4,1) CENTER
  else
  END-IF

END-PROCEDURE

!-----------------------------!
!#include files               !
!-----------------------------!
#include 'hrcntljp.sqc'  !Get run control parameter values
#include 'hrgetvjp.sqc'  !Get values mask routines
#include 'hrsectjp.sqc'   !Get SQR Security parameters
#include 'getcodta.sqc'  !Get selected fields from the Company Table
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
#include 'geticpjp.sqc'  !Get selected fields from the Intercompany Transfer Company Table
