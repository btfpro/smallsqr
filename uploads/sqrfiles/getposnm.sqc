!**************************************************************
!  GetPosNm:  Get Position Name from the Position Data Table  *
!**************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/08/17:18:44:06                                 
!       $Release:  HR9                                                 
!      $Revision:  103                                                 
!                                                                      *
!***********************************************************************

begin-procedure Get-Position-Title
  if rtrim($Position_Nbr,' ') = ''
    move '' to $PosName
    move '' to $PosNameAbbrv
    goto GetPosNmExit
  end-if
  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if
begin-SELECT
PD.DESCR,PD.DESCRSHORT
FROM  PS_POSITION_DATA PD
WHERE PD.POSITION_NBR = $Position_Nbr
  AND PD.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_POSITION_DATA
       WHERE  POSITION_NBR = $Position_Nbr
         AND  EFFDT  <= $AsOfDate)
end-SELECT
  let $PosName       = rtrim(&PD.Descr,' ')
  let $PosNameAbbrv  = rtrim(&PD.DescrShort,' ')

  if $Curr_Language_Cd <> $PSOptions_Language_Cd
     do Get-Pos-Title-Lang
  end-if

GetPosNmExit:
end-procedure



begin-procedure Get-Pos-Title-Lang

begin-SELECT
PDL.DESCR
PDL.DESCRSHORT
FROM PS_POSN_DATA_LANG PDL
WHERE PDL.POSITION_NBR = $Position_Nbr
  AND PDL.LANGUAGE_CD = $curr_Language_Cd
  AND PDL.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_POSN_DATA_LANG PDX
       WHERE  PDX.POSITION_NBR = $Position_Nbr
         AND  PDX.LANGUAGE_CD = $curr_Language_Cd
         AND  PDX.EFFDT  <= $AsOfDate)
end-SELECT
  let $PosName       = rtrim(&PDL.Descr,' ')
  let $PosNameAbbrv  = rtrim(&PDL.DescrShort,' ')

end-procedure