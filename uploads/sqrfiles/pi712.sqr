!***********************************************************************
!  PI712:    Contents of Interface Configuration Table                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!               Confidentiality Information:                           *
!                                                                      *
!   This module contains confidential and proprietary information      *
!   of Oracle; it is not to be copied, reproduced, or transmitted      *
!   in any form, by any means, in whole or in part, nor is it to       *
!   be used for any purpose other than that for which it is            *
!   expressly provided under the applicable license agreement.         *
!                                                                      *
!   Copyright (C) 2006 Oracle. All Rights Reserved.                    *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2006/07/20:02:33:05                                 
!       $Release:  HR9                                                 
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#include 'setup31.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


!***************************
begin-procedure Init-Report
!***************************

  move 'PI712' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi712_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
!**********************************
begin-procedure Report-Translation
!**********************************

! Read from PI712 program ids
do Init_Report_Translation($ReportID,$Curr_Language_Cd)

  do Get_Field_Information ('PI712', 'REPORT_TITLE', $Pi712_ReportTitle, #dummy_width)
  do Get_Field_Information ('PI712', 'CONFIGID', $Pi712_ConfigId, #dummy_width)
  do Get_Field_Information ('PI712', 'DESCRIPTION', $Pi712_Description, #dummy_width)
  do Get_Field_Information ('PI712', 'EFFDT', $Pi712_EffDt, #dummy_width)
  do Get_Field_Information ('PI712', 'STATUS', $Pi712_Status, #dummy_width)
  do Get_Field_Information ('PI712', 'SYSID', $Pi712_SysId, #dummy_width)
  do Get_Field_Information ('PI712', 'FILEID', $Pi712_FileId, #dummy_width)
  do Get_Field_Information ('PI712', 'FILEDESCR', $Pi712_FileDescr, #dummy_width)
  do Get_Field_Information ('PI712', 'FILEHANDLE', $Pi712_FileHandle, #dummy_width)

end-procedure


begin-heading 7
  #Include 'stdhdgtr.sqc'

  print $Pi712_ConfigId     (+2,1,12)
  print $Pi712_Description  (,15,30)
  print $Pi712_EffDt        (,47,10)
  print $Pi712_Status       (,59,8)
  print $Pi712_SysId        (,69,8)
  print $Pi712_FileId       (,79,8)
  print $Pi712_FileDescr    (,89,15)
  print $Pi712_FileHandle   (,106,12)

end-heading


!****************************
begin-procedure Process-Main
!****************************

begin-SELECT

A.PI_CONFIG_ID         !(+1,1)  on-break level=1 skiplines=1
B.DESCR                !(,15)   on-break level=1
A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  !print   $out          (,47)   on-break level=2
B.EFF_STATUS           !(,59)   on-break level=1
B.PI_SYSTEM_ID         !(,69)   on-break level=1
A.PI_FILE_ID           !(,79)
C.DESCRSHORT           !(,89)
A.FILE_HANDLE          !(,106)

   let $prnt_pi_config_id = &A.PI_CONFIG_ID
   let $prnt_b_descr      = &B.DESCR
   let $prnt_effdt        = &A.EFFDT
   let $prnt_eff_status   = &B.EFF_STATUS
   let $prnt_pi_system_id = &B.PI_SYSTEM_ID
   let $prnt_pi_file_id   = &A.PI_FILE_ID
   let $prnt_c_descrshort = &C.DESCRSHORT
   let $prnt_file_handle  = &A.FILE_HANDLE
   let $basedate          = &A.EFFDT

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!
  if $Curr_language_cd <> $Psoptions_language_cd
     do Get-Rel-Lang-Pi712
  end-if

  PRINT $PRNT_PI_CONFIG_ID           (+1,1,12)  on-break level=1 skiplines=1
  PRINT $PRNT_B_DESCR                (,15,30)   on-break level=1
  PRINT $OUT                         (,47,12)   on-break level=2
  PRINT $PRNT_EFF_STATUS             (,61,6)   on-break level=1
  PRINT $PRNT_PI_SYSTEM_ID           (,69,8)   on-break level=1
  PRINT $PRNT_PI_FILE_ID             (,79,8)
  PRINT $PRNT_C_DESCRSHORT           (,89,15)
  PRINT $PRNT_FILE_HANDLE            (,106,2)


FROM  PS_PI_CONFIG_FILE  A, PS_PI_CONFIG_TBL B, PS_PI_DEFN_FILE C
WHERE A.PI_CONFIG_ID = B.PI_CONFIG_ID
  AND A.EFFDT        = B.EFFDT
  AND A.PI_FILE_ID   = C.PI_FILE_ID
  AND C.EFFDT        =
    (SELECT MAX(EFFDT)
       FROM PS_PI_DEFN_FILE
           WHERE PI_FILE_ID = A.PI_FILE_ID
             AND EFF_STATUS = 'A'
             AND EFFDT     <= A.EFFDT)
ORDER BY A.PI_CONFIG_ID, A.EFFDT DESC, A.PI_FILE_ID
end-SELECT
end-procedure


!**********************************
begin-procedure Get-Rel-Lang-Pi712
!**********************************

begin-SELECT

BL.DESCR
CL.DESCRSHORT

  if rtrim(&BL.DESCR,' ') <> ''
     let $prnt_b_descr      = &BL.DESCR
  end-if

  if rtrim(&CL.DESCRSHORT,' ') <> ''
     let $prnt_c_descrshort = &CL.DESCRSHORT
  end-if

FROM  PS_PI_CONFIG_FILE A1, PS_PI_CONFIG_LANG BL, PS_PI_DEFN_F_LANG CL
WHERE A1.PI_CONFIG_ID = $prnt_PI_CONFIG_ID
  AND A1.PI_CONFIG_ID = BL.PI_CONFIG_ID
  AND A1.EFFDT        = $prnt_EFFDT
  AND A1.EFFDT        = BL.EFFDT
  AND A1.PI_FILE_ID   = $prnt_PI_FILE_ID
  AND A1.PI_FILE_ID   = CL.PI_FILE_ID
  AND BL.LANGUAGE_CD  = $Curr_language_cd
  AND CL.LANGUAGE_CD  = $Curr_language_cd
  AND CL.EFFDT        =
    (SELECT MAX(EFFDT)
       FROM PS_PI_DEFN_F_LANG
           WHERE PI_FILE_ID  = A1.PI_FILE_ID
             AND LANGUAGE_CD = CL.LANGUAGE_CD
             AND EFFDT      <= $Basedate)

end-SELECT
end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

